#INCLUDE "PROTHEUS.CH"
#INCLUDE "MATA242.CH"
#INCLUDE "FWLIBVERSION.CH"

Static _lWmsVlDesm := NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA242  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 31/10/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Movim. Internas de 1 Produto p/ N Produtos     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array Cabecalho para Rotina Automatica             ³±±
±±³          ³ ExpA2 = Array Itens para Rotina Automatica                 ³±±
±±³          ³ ExpN2 = Opcao do aRotina para Rotina Automatica            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mata242(xAutoCab,xAutoItens,nOpcAuto)
Local aCores	 := A240aCores()
Local aIndexSD3  := {}
Local bFiltraBrw := {|| Nil }
Local cFiltraSD3 := ""

PRIVATE cCadastro := OemToAnsi(STR0001)  //"Desmontagem de Produtos"
PRIVATE l240:=.F.,l250:=.F.,l241:=.F.,l242:=.T.,l261:=.F.,l185:=.F.
PRIVATE cPicProd,cPicLocal,cPicQtOri,cPicQtDes,cPicDoc,cPictPot,cPicLote,cPicLotCtl,cPicDtVld,cPicLocalz,cPicNumSe
PRIVATE lMta242C   := (ExistBlock("MTA242C"))
PRIVATE lMta242Q   := (ExistBlock("MTA242Q"))
PRIVATE lMta242S   := (ExistBlock("MTA242S"))
PRIVATE l242Auto   := ValType(xAutoCab) == "A" .And. ValType(xAutoItens) == "A"
Private nFCICalc   := SuperGetMV("MV_FCICALC",.F.,0)
Private lCpoTransf := SD3->(ColumnPos("D3_TRANSF")) > 0

DEFAULT nOpcAuto     := 3

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa pictures dos gets fixos.                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPicProd	:= PesqPict("SD3","D3_COD")
cPicLocal	:= PesqPict("SD3","D3_LOCAL")
cPicQtOri	:= PesqPictQt("D3_QUANT",TamSX3("D3_QUANT")[1])
cPicQtDes	:= PesqPictQt("D3_QTSEGUM",TamSX3("D3_QTSEGUM")[1])
cPicDoc		:= PesqPict("SD3","D3_DOC")
cPicLote	:= PesqPict("SD3","D3_NUMLOTE")
cPicLotCtl	:= PesqPict("SD3","D3_LOTECTL")
cPicDtVld	:= PesqPict("SD3","D3_DTVALID")
cPictPot    := PesqPict("SD3","D3_POTENCI")
cPicLocalz  := PesqPict("SD3","D3_LOCALIZ")
cPicNumSe   := PesqPict("SD3","D3_NUMSERI")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pega a variavel que identifica se o calculo do custo e' :    ³
//³               O = On-Line                                    ³
//³               M = Mensal                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCusMed := GetMv("MV_CUSMED")
PRIVATE bCols :={|x,y|aCols[x][y]}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o custo medio e' calculado On-Line               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cCusMed == "O"
	PRIVATE nHdlPrv				// Endereco do arquivo de contra prova dos lanctos cont.
	PRIVATE lCriaHeader := .T.	// Para criar o header do arquivo Contra Prova
	PRIVATE cLoteEst            // Numero do lote para lancamentos do estoque
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona numero do Lote para Lancamentos do Estoque     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX5")
	dbSeek(xFilial("SX5")+"09EST")
	cLoteEst:=IIf(Found(),Trim(X5Descri()),"EST ")
	PRIVATE nTotal := 0 	// Total dos lancamentos contabeis
	PRIVATE cArquivo		// Nome do arquivo contra prova
EndIf


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Esta variavel indica se utiliza segunda unidade de medida    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE lUsaSegUM

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 -Pesquisa e Posiciona em um Banco de Dados              ³
//³    2 -Simplesmente Mostra os Campos                          ³
//³    3 -Inclui registros no Bancos de Dados                    ³
//³    4 -Altera o registro corrente                             ³
//³    5 -Estorna registro selecionado gerando uma contra-partida³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se trabalha com segunda unidade de medida           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SD3->(dbSetOrder(1))

If l242Auto
	PRIVATE aAutoCab   := xAutoCab
	PRIVATE aAutoItens := xAutoItens
	DEFAULT nOpcAuto   := 3
	MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SD3",.F.)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ativa tecla F4 para comunicacao com Saldos dos Lotes         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Set Key VK_F4 TO ShowF4()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o custo medio e' calculado On-Line               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cCusMed == "O"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ativa tecla F10 para acionar perguntas                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F12, {|| MTA242PERG()})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MT242FIL")
		cFiltraSD3 := ExecBlock("MT242FIL",.F.,.F.)
		If ValType(cFiltraSD3) == "C"
			bFiltraBrw := {|| FilBrowse("SD3",@aIndexSD3,@cFiltraSD3) }
			Eval(bFiltraBrw)
		EndIf
	EndIf
	mBrowse( 6, 1,22,75,"SD3",,,,,,aCores)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	EndFilBrw("SD3",aIndexSD3)

EndIf

If ! l242Auto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desativa tecla que aciona perguntas                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Set Key VK_F4  To
	Set Key VK_F12 To
EndIf
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242Visual³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 31/10/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para visualizar movimentacoes                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A242Visual(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Visual(cAlias,nReg,nOpc)
Local lContinua  := .T.
Local nCnt       := 0
Local nRecOld    := 0
Local aPosObj    := {}
Local nRecAtu    := 0
Local aButtons   := {}
Local oDlg
Local cCampos    := "D3_COD.D3_LOCAL.D3_QUANT.D3_RATEIO.D3_QTSEGUM.D3_LOCALIZ.D3_NUMLOTE.D3_LOTECTL.D3_DTVALID.D3_NUMSERI.D3_POTENCI.D3_CONTA.D3_ITEMCTA.D3_CLVL.D3_CC"
Local aCampos    := {}
Local aCpos      := {}
Local aCpoUsr    := {}
Local aNoFields  := {}
Local nCntFor    := 0
Local aGet       := {}
Local aSay       := {}
Local aAux       := {}
Local oSize      := NIL
Local lMT242CPO  := ExistBlock("MT242CPO")
Local cSeek      := ""
Local bSeekWhile := ""
Local cQryA242   := ""
Local aCmbTransf := {}

PRIVATE oGet
PRIVATE cProduto,cLocOrig,nQtdOrig,nQtdOrigSe,cDocumento,cNumLote,cLoteDigi,dDtValid,dEmis260,cSeq,nPotencia,dDtValid2
PRIVATE cLocaliza,cNumSerie, cTM, cCF, cTransf

//-- Valida existencia Ponto de Entrada que permite a inclusao de campos na GetDados


If SubStr(D3_CF,3,1) != "7"
	Help(" ",1,"A242NAO")
	lContinua := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nFCICalc == 1
	cCampos+=".D3_PERIMP"
EndIf

If lContinua
	dbSelectArea(cAlias)
	dbSetOrder(4)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pega a seq. do movimento para achar a origem e recupera os   ³
	//³ dados da origem.                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSeek(xFilial(cAlias)+D3_NUMSEQ)
	cDocumento:= D3_DOC
	dEmis260  := D3_EMISSAO
	cSeq      := D3_NUMSEQ

	While !SD3->( Eof() ) .And. SD3->D3_NUMSEQ == cSeq
		If SD3->D3_CF != "RE7" .Or. SD3->D3_DOC != cDocumento .Or. SD3->D3_EMISSAO != dEmis260 .Or. SD3->D3_RATEIO != 100
			dbSkip()
			Loop
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Trecho utilizado para posicionar corretamente no reg. do  ³
			//³ cabecalho quando visualizada uma desmontagem estornada.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ElseIf SD3->D3_ESTORNO == "S"
			nRecAtu := Recno()
		EndIf
		Exit
	End

	cProduto   := D3_COD
	cLocOrig   := D3_LOCAL
	nQtdOrig   := D3_QUANT
	nQtdOrigSe := D3_QTSEGUM
	cDocumento := D3_DOC
	cNumLote   := D3_NUMLOTE
	cLoteDigi  := D3_LOTECTL
	dDtValid   := D3_DTVALID
	nPotencia  := D3_POTENCI
	cLocaliza  := D3_LOCALIZ
	cNumSerie  := D3_NUMSERI
	dEmis260   := D3_EMISSAO
	cSeq       := D3_NUMSEQ
	cTM        := D3_TM
	cCF        := D3_CF

	If lCpoTransf
		cTransf := SD3->D3_TRANSF
		aCmbTransf := strToKarr( getSx3Cache('D3_TRANSF','X3_CBOX'), ';' )
	EndIf

	If nRecAtu > 0
		MsGoto(nRecAtu)
	EndIf
	nRecOld:=Recno()
	dbSkip()

	Do While !Eof() .And. D3_FILIAL+D3_NUMSEQ == xFilial("SD3")+cSeq
		If SD3->D3_CF != "DE7" .Or. SD3->D3_DOC != cDocumento .Or. SD3->D3_EMISSAO != dEmis260 .Or. SD3->D3_COD == cProduto //.Or. (SD3->D3_RATEIO == 100 .And. SD3->D3_ESTORNO == "S")
			dbSkip()
			Loop
		EndIf
		nCnt++
		dbSkip()
	EndDo

	MsGoto(nRecOld)
	dbSkip()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE aHeader[0],nUsado:=0

	If IntDl()
		cCampos +=  ".D3_SERVIC"
	EndIf

	aAux := A242GetCpo( 'SD3', .T., .T.  )
	aCpoUsr := AClone( aAux )

	//-- Ponto de Entrada que permite a inclusao de campos na GetDados
	If lMT242CPO
		aCpos := ExecBlock("MT242CPO",.F.,.F. )
		For nCntFor := 1 To Len( aCpos )
			cCampos += '.' + aCpos[nCntFor]
		Next nCntFor
	EndIf

	AEval( aCpoUsr, { | x | IIf( ( lMT242CPO .And. Len( aCpos ) > 0 ) , IIf( Ascan( aCpos, AllTrim( x ) ) == 0 , Aadd( aNoFields, AllTrim( x ) ) , Nil  ) , Aadd( aNoFields, AllTrim( x )  )  )   }   )

	aAux := StrToKArr( cCampos, '.' )
	AEval( aAux , { | x | IIf( Ascan( aCampos, x ) == 0 .And. !Empty(GETSX3CACHE(x,"X3_CAMPO")), Aadd( aCampos, x ), Nil ) } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada criado para mudar os botoes da enchoicebar                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA242BUT" )
		aButtons := ExecBlock( "MA242BUT", .F., .F., { nOpc, aButtons } )
		If ValType( aButtons ) # "A"
			aButtons := {}
		EndIf
	EndIf
	
	cQryA242 := ""
	cQryA242 += "SELECT * "
	cQryA242 += "FROM " + RetSQLName("SD3") + " SD3 "
	cQryA242 += "WHERE SD3.D3_FILIAL = '" + FWxFilial( "SD3" ) + "' "
	cQryA242 += "AND SD3.D3_DOC = '" + cDocumento + "' "
	cQryA242 += "AND SD3.D3_NUMSEQ = '" + cSeq + "' "
	cQryA242 += "AND SD3.D3_EMISSAO = '" + DTOS( dEmis260 ) + "' "
	cQryA242 += "AND SD3.D3_CF = 'DE7' "
	If Empty( cLoteDigi )
		// trato o produto
		cQryA242 += "AND SD3.D3_COD <> '" + cProduto + "' "
	Else
		// trato pelo lote
		cQryA242 += "AND (SD3.D3_COD <> '" + cProduto + "' OR (SD3.D3_COD = '" + cProduto + "' AND SD3.D3_LOTECTL <> '" + cLoteDigi + "')) "
	EndIf
	cQryA242 += "AND SD3.D_E_L_E_T_ = ' ' "

	cSeek	  := FWxFilial( "SD3" ) + cSeq + 'E9'
	bSeekWhile:= {|| SD3->D3_FILIAL + SD3->D3_NUMSEQ + SD3->D3_CHAVE} //Condicao While para montar o aCols

	FillGetDados(nOpc,cAlias, 4 ,cSeek,bSeekWhile,/*bSeekFor*/, aNoFields , aCampos ,/*lOnlyYes*/, cQryA242 ,/*bMontCols*/,/*lEmpty*/, /*aHeaderAux*/, /*aColsAux*/ , /*bAfterCols */, /*bBeforeCols*/, /* bAfterHeader */,/*cAliasQry */, /*bCriaVar */, .T., /*aYesUsado */   )

	//Verifica se existe "produto pai" no aCols, caso exista, exclui
	a242AfterC(cDocumento, cSeq)

	//Define objeto para tela cheia
	oSize := FwDefSize():New()

	//divide em linhas
	oSize:AddObject( "LINHA1",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA2",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA3",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA4",  100, 7, .T., .T. )
	oSize:AddObject( "ITENS" ,  100, 72, .T., .T. )

	oSize:lProp := .T.				// Proporcional
	oSize:aMargins := { 3, 3, 3, 3 }// Espaco ao lado dos objetos 0, entre eles 3
	oSize:Process()					// Dispara os calculos
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o posicionamento dos Says/Gets    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A242PosGet(@aSay, @aGet, aPosObj, oSize)

	DEFINE MSDIALOG oDlg TITLE cCadastro ;
	FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] ;
	OF oMainWnd PIXEL
	@ aSay[01,1],aSay[01,2] SAY OemToAnsi(STR0006)	SIZE aSay[01,3],aSay[01,4] OF oDlg PIXEL		//"Produto Origem"
	@ aSay[02,1],aSay[02,2] SAY OemToAnsi(STR0007)	SIZE aSay[02,3],aSay[02,4] OF oDlg PIXEL		//"Armazem"
	@ aSay[03,1],aSay[03,2] SAY OemToAnsi(STR0008)	SIZE aSay[03,3],aSay[03,4] OF oDlg PIXEL		//"Endereco"
	@ aSay[04,1],aSay[04,2] SAY OemToAnsi(STR0011)	SIZE aSay[04,3],aSay[04,4] OF oDlg PIXEL		//"Lote"
	@ aSay[05,1],aSay[05,2] SAY OemToAnsi(STR0010)	SIZE aSay[05,3],aSay[05,4] OF oDlg PIXEL		//"Sub-Lote"
	@ aSay[06,1],aSay[06,2] SAY OemToAnsi(STR0012)	SIZE aSay[06,3],aSay[06,4] OF oDlg PIXEL		//"Validade"
	@ aSay[07,1],aSay[07,2] SAY OemToAnsi(STR0018)	SIZE aSay[07,3],aSay[07,4] OF oDlg PIXEL		//"Pot."
	If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
		@ aSay[08,1],aSay[08,2] SAY OemToAnsi(STR0013)	SIZE aSay[08,3],aSay[08,4] OF oDlg PIXEL		//"N£mero de S‚rie"
	EndIf
	@ aSay[09,1],aSay[09,2] SAY OemToAnsi(STR0014)	SIZE aSay[09,3],aSay[09,4] OF oDlg PIXEL		//"Quantidade"
	If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
		@ aSay[10,1],aSay[10,2] SAY OemToAnsi(STR0015)	SIZE aSay[10,3],aSay[10,4] OF oDlg PIXEL		//"Quantidade Secund ria"
	EndIf
	@ aSay[11,1],aSay[11,2] SAY OemToAnsi(STR0009)	SIZE aSay[11,3],aSay[11,4] OF oDlg PIXEL		//"Data"
	@ aSay[12,1],aSay[12,2] SAY OemToAnsi(STR0016)	SIZE aSay[12,3],aSay[12,4] OF oDlg PIXEL		//"Documento"
	If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
		@ aSay[13,1],aSay[13,2] SAY OemToAnsi(STR0037)	SIZE aSay[13,3],aSay[13,4] OF oDlg PIXEL //"Transf. K220"
	EndIf

	@ aGet[01,1],aGet[01,2] MSGET cProduto   		SIZE aGet[01,3],aGet[01,4] WHEN .F. OF oDlg PIXEL
	@ aGet[02,1],aGet[02,2] MSGET cLocOrig   		SIZE aGet[02,3],aGet[02,4] WHEN .F. OF oDlg PIXEL
	@ aGet[03,1],aGet[03,2] MSGET cLocaliza  		SIZE aGet[03,3],aGet[03,4] WHEN .F. OF oDlg PIXEL
	@ aGet[04,1],aGet[04,2] MSGET cLoteDigi   	SIZE aGet[04,3],aGet[04,4] WHEN .F. OF oDlg PIXEL
	@ aGet[05,1],aGet[05,2] MSGET cNumLote  		SIZE aGet[05,3],aGet[05,4] WHEN .F. OF oDlg PIXEL
	@ aGet[06,1],aGet[06,2] MSGET dDtValid   		SIZE aGet[06,3],aGet[06,4] WHEN .F. OF oDlg PIXEL HasButton
	@ aGet[07,1],aGet[07,2] MSGET nPotencia   	SIZE aGet[07,3],aGet[07,4] WHEN .F. OF oDlg PIXEL HasButton
	If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
		@ aGet[08,1],aGet[08,2] MSGET cNumSerie  		SIZE aGet[08,3],aGet[08,4] WHEN .F. OF oDlg PIXEL
	EndIf
	@ aGet[09,1],aGet[09,2] MSGET nQtdOrig  		SIZE aGet[09,3],aGet[09,4] WHEN .F. OF oDlg PIXEL Picture cPicQtOri HasButton
	If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
		@ aGet[10,1],aGet[10,2] MSGET nQtdOrigSe  	SIZE aGet[10,3],aGet[10,4] WHEN .F. OF oDlg PIXEL Picture cPicQtDes HasButton
	EndIf
	@ aGet[11,1],aGet[11,2] MSGET dEmis260 		SIZE aGet[11,3],aGet[11,4] WHEN .F. OF oDlg PIXEL HasButton
	@ aGet[12,1],aGet[12,2] MSGET cDocumento 		SIZE aGet[12,3],aGet[12,4] WHEN .F. OF oDlg PIXEL
	If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
		@ aGet[13,1],aGet[13,2] MSCOMBOBOX cTransf  ITEMS aCmbTransf	SIZE aGet[13,3],aGet[13,4] WHEN .F. OF oDlg PIXEL
	EndIf

	If ExistBlock("MT242SCR")
		ExecBlock("MT242SCR",.F.,.F.,{@oDlg, oSize:aPosObj, nOpc, nReg})
	EndIf
	oGet := MSGetDados():New(	oSize:GetDimension("ITENS","LININI"),;
								oSize:GetDimension("ITENS","COLINI"),;
								oSize:GetDimension("ITENS","LINEND"),;
								oSize:GetDimension("ITENS","COLEND"),;
								nOpc,"AllwaysTrue","AllwaysTrue","",.F.,,,,300)

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()},,aButtons)

	dbSelectArea(cAlias)
	MsGoto(nReg)
EndIf
Return NIL

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242Inclui³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 31/10/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para inclusao de movimentacoes                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A242Inclui(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Inclui(cAlias,nReg,nOpc)
Local nOpca
Local dDataFec   := MVUlmes()
Local nx
Local oDlg
Local oSize
Local oTransf
Local aPosObj    := {}
Local lRet       := lRetPE := .T.
Local lMta242L   := ExistBlock("MTA242L")
Local aButtons   :={{ 'PRODUTO' ,{|| A242Estru(cProduto,nQtdOrig,oGet)},STR0020,STR0027}}
Local cCampos    := "D3_COD.D3_LOCAL.D3_QUANT.D3_RATEIO.D3_QTSEGUM.D3_LOCALIZ.D3_NUMLOTE.D3_LOTECTL.D3_DTVALID.D3_NUMSERI.D3_POTENCI.D3_CONTA.D3_ITEMCTA.D3_CLVL.D3_CC"
Local aCampos    := {}
Local aCpos      := {}
Local nCntFor    := 0
Local aGet       := {}
Local aSay       := {}
Local aAux       := {}
Local aCpoUsr    := {}
Local aNoFields  := {}
Local aCmbTransf := {}
Local cSeek      := ""
Local bSeekWhile := ""
Local lMT242CPO  := ExistBlock("MT242CPO")
Local lMa242But  := ExistBlock( "MA242BUT" )
Local lMt242SCR  := ExistBlock("MT242SCR")
Local cQryA242   := ""
Local lIntWMS    := SuperGetMV('MV_INTWMS',.F.,.F.)

PRIVATE oGet
PRIVATE cProduto,cLocOrig,nQtdOrig,nQtdOrigSe,cDocumento,cNumLote,cLoteDigi,dDtValid,dEmis260,nPotencia, cSeq
PRIVATE cLocaliza,cNumSerie, cTransf
PRIVATE nPosCod,nPosLocal,nPosLocali:=0,nPosNumSer:=0,nPosQuant,nPosRateio:=0,cTM:="499",nPosLotCtl,nPosDValid,nPosLote,nPosPotenc
PRIVATE nPosQTSegUM:=0;nPosCta:=0;nPosItCta:=0;nPosCLVL:=0;nPosCC:=0;nPosServic:=0;nPosPerImp:=0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificar data do ultimo fechamento em SX6.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataFec >= dDataBase
	Help ( " ", 1, "FECHTO" )
	lRet:=.F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nFCICalc == 1
	cCampos+=".D3_PERIMP"
EndIf

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Botao para exportar dados para EXCEL                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If RemoteType() == 1
		aAdd(aButtons ,{PmsBExcel()[1],{|| DlgToExcel({ {"CABECALHO",cCadastro,{OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008),OemToAnsi(STR0009),OemToAnsi(STR0011),OemToAnsi(STR0010),OemToAnsi(STR0012),OemToAnsi(STR0018),OemToAnsi(STR0013),OemToAnsi(STR0014),OemToAnsi(STR0015),OemToAnsi(STR0016)},	{cProduto,cLocOrig,cLocaliza,dEmis260,cLoteDigi,cNumLote,dDtValid,nPotencia,cNumSerie,nQtdOrig,nQtdOrigSe,cDocumento}	       },{"GETDADOS",OemToAnsi(STR0026),aHeader,aCols}})},PmsBExcel()[2],PmsBExcel()[3]})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa variaveis p/ criar gets fixos com tamanho correto ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cProduto   := CriaVar('D3_COD')
	cLocOrig   := CriaVar('D3_LOCAL')
	nQtdOrig   := CriaVar('D3_QUANT')
	nQtdOrigSe := CriaVar('D3_QTSEGUM')
	cDocumento := ESTNXTNUM("SD3","D3_DOC",.T.)
	cNumLote   := CriaVar('D3_NUMLOTE')
	cLoteDigi  := CriaVar('D3_LOTECTL')
	dDtValid   := CriaVar('D3_DTVALID')
	dDtValid2  := CriaVar('D3_DTVALID')
	nPotencia  := CriaVar('D3_POTENCI')
	cLocaliza  := CriaVar('D3_LOCALIZ')
	cNumSerie  := CriaVar('D3_NUMSERI')
	cSeq       := Criavar('D3_NUMSEQ')
	If lCpoTransf
		cTransf := Criavar('D3_TRANSF')
		aCmbTransf := strToKarr( getSx3Cache('D3_TRANSF','X3_CBOX'), ';' )
	EndIf

	dEmis260   := dDataBase

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE aHeader[0], nUsado:=0

	If IntDl()
		cCampos +=  ".D3_SERVIC"
	EndIf

	aAux := A242GetCpo( 'SD3', .T., .T.  )
	aCpoUsr := AClone( aAux )

	//-- Ponto de Entrada que permite a inclusao de campos na GetDados
	If lMT242CPO
		aCpos := ExecBlock("MT242CPO",.F.,.F. )
		For nCntFor := 1 To Len( aCpos )
			cCampos += '.' + aCpos[ nCntFor ]
		Next nCntFor
	EndIf

	AEval( aCpoUsr, { | x | IIf( ( lMT242CPO .And. Len( aCpos ) > 0 ) , IIf( Ascan( aCpos, AllTrim( x ) ) == 0 , Aadd( aNoFields, AllTrim( x ) ) , Nil  ) , Aadd( aNoFields, AllTrim( x )  )  )   }   )

	aAux := StrToKArr( cCampos, '.' )
	AEval( aAux, { | x | IIf( Ascan( aCampos, x ) == 0 .And. !Empty(GETSX3CACHE(x,"X3_CAMPO")), Aadd( aCampos, x ), Nil ) } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o cabecalho da GetDados                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSeek	  := FWxFilial( "SD3" ) + cSeq
	bSeekWhile:= {|| SD3->D3_FILIAL + SD3->D3_NUMSEQ } //Condicao While para montar o aCols

	cQryA242 := ""
	cQryA242 += "SELECT * "
	cQryA242 += "FROM " + RetSQLName("SD3") + " SD3 "
	cQryA242 += "WHERE SD3.D3_FILIAL = '" + FWxFilial( "SD3" ) + "' "
	cQryA242 += "AND SD3.D3_DOC = '" + cDocumento + "' "
	cQryA242 += "AND SD3.D3_NUMSEQ = '" + cSeq + "' "
	cQryA242 += "AND SD3.D3_EMISSAO = '" + DTOS( dEmis260 ) + "' "
	cQryA242 += "AND SD3.D3_CF = 'DE7' "
	cQryA242 += "AND SD3.D_E_L_E_T_ = ' ' "

	FillGetDados (nOpc,cAlias,4,cSeek,bSeekWhile,/*bSeekFor*/, aNoFields, aCampos ,/*lOnlyYes*/,cQryA242,/*bMontCols*/,/*lEmpty*/, /*aHeaderAux*/, /*aColsAux*/ , /*bAfterCols */, /*bBeforeCols*/, /* bAfterHeader */,/*cAliasQry */, /*bCriaVar */, .T., /*aYesUsado */   )

 	For nx:= 1 To Len(aHeader)
		If Trim(aHeader[nx][2]) == "D3_COD"
			nPosCod:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOCAL"
			nPosLocal:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_QUANT"
			nPosQuant:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_QTSEGUM"
			nPosQTSegUM:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_RATEIO"
			nPosRateio:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOCALIZ"
			nPosLocali:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_NUMSERI"
			nPosNumSer:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_NUMLOTE"
			nPosLote:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOTECTL"
			nPosLotCTL:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_DTVALID"
			nPosDValid:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_POTENCI"
			nPosPotenc:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CONTA"
			nPosCta:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_ITEMCTA"
			nPosItCta:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CLVL"
			nPosCLVL:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CC"
			nPosCC:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_SERVIC"
			nPosServic:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_PERIMP"
			nPosPerImp:=nX
		EndIf
	Next nx

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Validacao do uso de alguns campos obrigatorios WMS           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lIntWMS .And. Empty(nPosServic)
		UserException("D3_SERVIC"+OemToAnsi(STR0036))
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada criado para mudar os botoes da enchoicebar                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMa242But
		aButtons := ExecBlock( "MA242BUT", .F., .F., { nOpc, aButtons } )
		If ValType( aButtons ) # "A"
			aButtons := {}
		EndIf
	EndIf

	//Define objeto para tela cheia
	oSize := FwDefSize():New()

	//divide em linhas
	oSize:AddObject( "LINHA1",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA2",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA3",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA4",  100, 7, .T., .T. )
	oSize:AddObject( "ITENS" ,  100, 72, .T., .T. )

	oSize:lProp 		:= .T. 			// Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3
	oSize:Process() 						// Dispara os calculos
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o posicionamento dos Says/Gets    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A242PosGet(@aSay, @aGet, aPosObj, oSize)

	Do While .T.
		nOpca:=3
		If ! l242Auto

			DEFINE MSDIALOG oDlg TITLE cCadastro ;
			FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] ;
			OF oMainWnd PIXEL

			@ aSay[01,1],aSay[01,2] SAY OemToAnsi(STR0006)	SIZE aSay[01,3],aSay[01,4] OF oDlg PIXEL		//"Produto Origem"
			@ aSay[02,1],aSay[02,2] SAY OemToAnsi(STR0007)	SIZE aSay[02,3],aSay[02,4] OF oDlg PIXEL		//"Armazem"
			@ aSay[03,1],aSay[03,2] SAY OemToAnsi(STR0008)	SIZE aSay[03,3],aSay[03,4] OF oDlg PIXEL		//"Endereco"
			@ aSay[04,1],aSay[04,2] SAY OemToAnsi(STR0011)	SIZE aSay[04,3],aSay[04,4] OF oDlg PIXEL		//"Lote"
			@ aSay[05,1],aSay[05,2] SAY OemToAnsi(STR0010)	SIZE aSay[05,3],aSay[05,4] OF oDlg PIXEL		//"Sub-Lote"
			@ aSay[06,1],aSay[06,2] SAY OemToAnsi(STR0012)	SIZE aSay[06,3],aSay[06,4] OF oDlg PIXEL		//"Validade"
			@ aSay[07,1],aSay[07,2] SAY OemToAnsi(STR0018)	SIZE aSay[07,3],aSay[07,4] OF oDlg PIXEL		//"Pot."
			If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
				@ aSay[08,1],aSay[08,2] SAY OemToAnsi(STR0013)	SIZE aSay[08,3],aSay[08,4] OF oDlg PIXEL		//"N£mero de S‚rie"
			EndIf
			@ aSay[09,1],aSay[09,2] SAY OemToAnsi(STR0014)	SIZE aSay[09,3],aSay[09,4] OF oDlg PIXEL		//"Quantidade"
			If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
				@ aSay[10,1],aSay[10,2] SAY OemToAnsi(STR0015)	SIZE aSay[10,3],aSay[10,4] OF oDlg PIXEL		//"Quantidade Secund ria"
			EndIf
			@ aSay[11,1],aSay[11,2] SAY OemToAnsi(STR0009)	SIZE aSay[11,3],aSay[11,4] OF oDlg PIXEL		//"Data"
			@ aSay[12,1],aSay[12,2] SAY OemToAnsi(STR0016)	SIZE aSay[12,3],aSay[12,4] OF oDlg PIXEL		//"Documento"
			If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
				@ aSay[13,1],aSay[13,2] SAY OemToAnsi(STR0037)	SIZE aSay[13,3],aSay[13,4] OF oDlg PIXEL //"Transf. K220"
			EndIf

			@ aGet[01,1],aGet[01,2] MSGET oGetProd Var cProduto	F3 "SB1" HASBUTTON SIZE aGet[01,3],aGet[01,4] PICTURE cPicProd	 VALID  (NaoVazio() .And. ExistCpo("SB1") .And. A242IniCpo() .And. If(lMta242C,ExecBlock("MTA242C",.F.,.F.,{cProduto,nQtdOrig,oGet}),.T.)  ) OF oDlg PIXEL
			@ aGet[02,1],aGet[02,2] MSGET cLocOrig                F3 "NNR" HASBUTTON SIZE aGet[02,3],aGet[02,4] PICTURE cPicLocal VALID  ExistCpo("NNR") .And. A242Local(&(ReadVar())) .And. NaoVazio() OF oDlg PIXEL
			@ aGet[03,1],aGet[03,2] MSGET cLocaliza               F3 "SBE" HASBUTTON SIZE aGet[03,3],aGet[03,4] PICTURE cPicLocalz VALID  If(Localiza(cProduto),Empty(&(ReadVar())) .Or. ExistCpo("SBE",cLocOrig+&(ReadVar())) .And. If(lMta242L,ExecBlock("MTA242L",.F.,.F.,{cProduto,nQtdOrig,oGet,cLocOrig,cLocaliza}),.T.) ,Vazio()) WHEN Localiza(cProduto) OF oDlg PIXEL
			@ aGet[04,1],aGet[04,2] MSGET cLoteDigi                                  SIZE aGet[04,3],aGet[04,4] PICTURE cPicLotCtl VALID  A242Lote(1) WHEN (Rastro(cProduto,"L") .Or. Rastro(cProduto,"S")) OF oDlg PIXEL
			@ aGet[05,1],aGet[05,2] MSGET cNumLote                                   SIZE aGet[05,3],aGet[05,4] PICTURE cPicLote	VALID  A242Lote(2) WHEN Rastro(cProduto,"S") OF oDlg PIXEL
			@ aGet[06,1],aGet[06,2] MSGET dDtVALID                         HASBUTTON SIZE aGet[06,3],aGet[06,4] WHEN .F. OF oDlg PIXEL
			@ aGet[07,1],aGet[07,2] MSGET nPotencia                        HASBUTTON SIZE aGet[07,3],aGet[07,4] WHEN .F. OF oDlg PIXEL
			If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
				@ aGet[08,1],aGet[08,2] MSGET cNumSerie                                  SIZE aGet[08,3],aGet[08,4] PICTURE cPicNumSe VALID A242NumSer() WHEN Localiza(cProduto) OF oDlg PIXEL
			EndIf
			@ aGet[09,1],aGet[09,2] MSGET nQtdOrig                         HASBUTTON SIZE aGet[09,3],aGet[09,4] PICTURE cPicQtOri VALID  (A242Quant() .And. nQtdOrig > 0 .And. IIf(lMta242Q,ExecBlock("MTA242Q",.F.,.F.,{cProduto,nQtdOrig,oGet}),.T.)) OF oDlg PIXEL
			If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
				@ aGet[10,1],aGet[10,2] MSGET nQtdOrigSe                       HASBUTTON SIZE aGet[10,3],aGet[10,4] PICTURE cPicQtDes VALID  (Positivo() .And. A242PriUm() .And. IIf(lMta242S,ExecBlock("MTA242S",.F.,.F.,{cProduto,nQtdOrigSe,oGet}),.T.)) OF oDlg PIXEL
			EndIf
			@ aGet[11,1],aGet[11,2] MSGET dEmis260                         HASBUTTON SIZE aGet[11,3],aGet[11,4] VALID  A242Data() WHEN A242When('D3_EMISSAO') OF oDlg PIXEL
			@ aGet[12,1],aGet[12,2] MSGET cDocumento                                 SIZE aGet[12,3],aGet[12,4] PICTURE cPicDoc	VALID  NaoVazio() .And. A240Doc() .And. VldUser('D3_DOC') OF oDlg PIXEL
			If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
				@ aGet[13,1],aGet[13,2] MSCOMBOBOX oTransf VAR cTransf ITEMS aCmbTransf SIZE aGet[13,3],aGet[13,4] VALID A242VldCmb() WHEN A242When('D3_TRANSF') OF oDlg PIXEL
			EndIf

			If lMt242SCR
				ExecBlock("MT242SCR",.F.,.F.,{@oDlg, oSize:aPosObj, nOpc, nReg})
			EndIf

			//Itens
			oGet := MSGetDados():New(	oSize:GetDimension("ITENS","LININI"),;
										oSize:GetDimension("ITENS","COLINI"),;
										oSize:GetDimension("ITENS","LINEND"),;
										oSize:GetDimension("ITENS","COLEND"),;
										nOpc,"A242LinOk","A242TudoOk","",.T.,,,,300)

			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,Iif(VldCabec().and.oGet:TudoOk(),oDlg:End(),nOpca := 0)},{||oDlg:End()},,aButtons)

		Else
			//Preenche o valid dos campos do cabeçalho quando executado por rotina automática
			A242Auto()
			If MsVldGAuto(aAutoCab)
				If MsGetDAuto(aAutoItens,"A242LinOk",{|| A242TudoOk()},aAutoCab,aRotina[nOpc][4])
					nOpca := 1
				Else
					nOpca := 0
				EndIf
			EndIf
		EndIf
		Exit
	EndDo
	If nOpca == 1
		If ExistBlock('MTA242V')
			lRet := If(ValType(lRetPE := ExecBlock('MTA242V', .F., .F.))=='L', lRetPE, lRet)
		EndIf
		If lRet
			A242Proces()
		EndIf
		If __lSX8
			ConfirmSX8()
		EndIf
	Else
		If __lSX8
			RollBackSX8()
		EndIf
	EndIf

	dbSelectArea(cAlias)
	MsGoto(nReg)
EndIf
Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242Estorn³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa para estornar movimentacoes                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A242Estorn(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Estorn(cAlias,nReg,nOpc)
Local nCnt       := 0
Local dDataFec   := MVUlmes()
Local zi         := 0,lAbandona:=.F.
Local oDlg
Local aPosObj    := {}
Local aButtons   := {}
Local nX         := 0
Local lContinua  := .T.
Local cCampos    := "D3_COD.D3_LOCAL.D3_QUANT.D3_RATEIO.D3_QTSEGUM.D3_LOCALIZ.D3_NUMLOTE.D3_LOTECTL.D3_DTVALID.D3_NUMSERI.D3_POTENCI.D3_CONTA.D3_ITEMCTA.D3_CLVL.D3_CC"
Local aCampos    := {}
Local aCpos      := {}
Local nCntFor    := 0
Local aSay       := {}
Local aGet       := {}
Local aAux       := {}
Local aCpoUsr    := {}
Local aNoFields  := {}
Local oSize      := NIL
Local cSeek      := ""
Local bSeekWhile := ""
Local lMt242Cpo  := ExistBlock("MT242CPO")
Local lMt242Scr  := ExistBlock("MT242SCR")
Local cQryA242   := ""
Local aCmbTransf := {}

PRIVATE oGet
PRIVATE nRecOld
PRIVATE cProduto,cLocOrig,nQtdOrig,nQtdOrigSe,cDocumento,cNumLote,cLoteDigi,dDtValid,dEmis260,cSeqEst,nPotencia
PRIVATE cLocaliza,cNumSerie, cTransf
PRIVATE nPosCod,nPosLocal,nPosLocali:=0,nPosNumSer:=0,nPosQuant,nPosRateio:=0,cTM:="499",nPosLotCtl,nPosDValid,nPosLote,nPosPotenc
PRIVATE nPosQTSegUM:=0;nPosCta:=0;nPosItCta:=0;nPosCLVL:=0;nPosCC:=0;nPosServic:=0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nFCICalc == 1
	cCampos+=".D3_PERIMP"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificar data do ultimo fechamento em SX6.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataFec >= SD3->D3_EMISSAO
	Help ( " ", 1, "FECHTO" )
	lContinua := .F.
EndIf

If lContinua .And. SubStr(D3_CF,3,1) != "7"
	Help(" ",1,"A242NAO")
	lContinua := .F.
EndIf

If lContinua .And. D3_ESTORNO == "S"
	Help(" ",1,"A242ESTORN")
	SetCursor(0)
	lContinua := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica calendário contábil                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua
	lContinua := (CtbValiDt(Nil,SD3->D3_EMISSAO,,Nil ,Nil ,{"EST001"}))
EndIf

If lContinua
	dbSelectArea(cAlias)
	dbSetOrder(4)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pega a seq. do movimento para achar a origem e recupera os   ³
	//³ dados da origem.                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSeek(xFilial(cAlias)+D3_NUMSEQ)
	cDocumento:=D3_DOC
	dEmis260  :=D3_EMISSAO
	cSeqEst   :=D3_NUMSEQ

	While !Eof() .And. SD3->D3_NUMSEQ == cSeqEst
		If SD3->D3_CF != "RE7" .Or. SD3->D3_DOC != cDocumento .Or. SD3->D3_EMISSAO != dEmis260
			dbSkip()
			Loop
		EndIf
		Exit
	End

	cProduto   := D3_COD
	cLocOrig   := D3_LOCAL
	nQtdOrig   := D3_QUANT
	nQtdOrigSe := D3_QTSEGUM
	cDocumento := D3_DOC
	cNumLote   := D3_NUMLOTE
	cLoteDigi  := D3_LOTECTL
	dDtValid   := D3_DTVALID
	nPotencia  := D3_POTENCI
	cLocaliza  := D3_LOCALIZ
	cNumSerie  := D3_NUMSERI
	dEmis260   := D3_EMISSAO
	cSeqEst    := D3_NUMSEQ
	nRecOld    := Recno()
	
	If lCpoTransf
		cTransf := SD3->D3_TRANSF
		aCmbTransf := strToKarr( getSx3Cache('D3_TRANSF','X3_CBOX'), ';' )
	EndIf

	dbSkip()

	Do While !Eof() .And. D3_FILIAL+D3_NUMSEQ == xFilial("SD3")+cSeqEst
		nCnt++
		dbSkip()
	EndDo

	MsGoto(nRecOld)
	dbSkip()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE aHeader[0],nUsado:=0

	If IntDl()
		cCampos +=  ".D3_SERVIC"
	EndIf

	aAux := A242GetCpo( 'SD3', .T., .T.  )
	aCpoUsr := AClone( aAux )

	//-- Ponto de Entrada que permite a inclusao de campos na GetDados
	If lMt242Cpo
		aCpos := ExecBlock("MT242CPO",.F.,.F. )
		For nCntFor := 1 To Len( aCpos )
			cCampos += '.' + aCpos[nCntFor]
		Next nCntFor
	EndIf

	AEval( aCpoUsr, { | x | IIf( ( lMT242CPO .And. Len( aCpos ) > 0 ) , IIf( Ascan( aCpos, AllTrim( x ) ) == 0 , Aadd( aNoFields, AllTrim( x ) ) , Nil  ) , Aadd( aNoFields, AllTrim( x )  )  )   }   )

	aAux := StrToKArr( cCampos, '.' )
	AEval( aAux , { | x | IIf( Ascan( aCampos, x ) == 0 .and. !Empty(GETSX3CACHE(x,"X3_CAMPO")), Aadd( aCampos, x ), Nil ) } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o cabecalho da GetDados                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSeek	  := FWxFilial( "SD3" ) + cSeqEst + 'E9'
	bSeekWhile:= {|| SD3->D3_FILIAL + SD3->D3_NUMSEQ + SD3->D3_CHAVE } //Condicao While para montar o aCols

	cQryA242 := ""
	cQryA242 += "SELECT * "
	cQryA242 += "FROM " + RetSQLName("SD3") + " SD3 "
	cQryA242 += "WHERE SD3.D3_FILIAL = '" + FWxFilial( "SD3" ) + "' "
	cQryA242 += "AND SD3.D3_DOC = '" + cDocumento + "' "
	cQryA242 += "AND SD3.D3_NUMSEQ = '" + cSeqEst + "' "
	cQryA242 += "AND SD3.D3_EMISSAO = '" + DTOS( dEmis260 ) + "' "
	cQryA242 += "AND SD3.D3_CF = 'DE7' "
	cQryA242 += "AND SD3.D_E_L_E_T_ = ' ' "

	FillGetDados(nOpc,cAlias,4,cSeek,bSeekWhile,/*bSeekFor*/, aNoFields , aCampos ,/*lOnlyYes*/,cQryA242,/*bMontCols*/,/*lEmpty*/, /*aHeaderAux*/, /*aColsAux*/ , /*bAfterCols */, /*bBeforeCols*/, /* bAfterHeader */,/*cAliasQry */, /*bCriaVar */, .T., /*aYesUsado */   )

	 For nx:= 1 To Len(aHeader)
		If Trim(aHeader[nx][2]) == "D3_COD"
			nPosCod:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOCAL"
			nPosLocal:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_QUANT"
			nPosQuant:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_RATEIO"
			nPosRateio:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_QTSEGUM"
			nPosQTSegUM:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOCALIZ"
			nPosLocali:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_NUMSERI"
			nPosNumSer:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_NUMLOTE"
			nPosLote:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_LOTECTL"
			nPosLotCTL:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_DTVALID"
			nPosDValid:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_POTENCI"
			nPosPotenc:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CONTA"
			nPosCta:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_ITEMCTA"
			nPosItCta:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CLVL"
			nPosCLVL:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_CC"
			nPosCC:=nX
		ElseIf Trim(aHeader[nx][2]) == "D3_SERVIC"
			nPosServic:=nX
		EndIf
	Next nx
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se algum produto est  sendo inventariado.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For zi:=1 To Len(aCols)
		If BlqInvent(aCols[zi,nPosCod],aCols[zi,nPosLocal])
			Help(" ",1,"BLQINVENT",,aCols[zi,nPosCod]+OemToAnsi(STR0017)+aCols[zi,nPosLocal],1,11)
			lAbandona:=.T.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Analisa se o tipo do armazem permite a movimentacao |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lAbandona .And. AvalBlqLoc(aCols[zi,nPosCod],aCols[zi,nPosLocal],Nil)
			lAbandona:=.T.
		EndIf
	Next i

	If lAbandona
		lContinua := .F.
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o produto origem est  sendo inventariado³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If BlqInvent(cProduto,cLocOrig)
			Help(" ",1,"BLQINVENT",,cProduto+OemToAnsi(STR0017)+cLocOrig,1,11)
			lContinua := .F.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Analisa se o tipo do armazem permite a movimentacao |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lContinua .And. AvalBlqLoc(cProduto,cLocOrig,Nil)
			lContinua :=.F.
		EndIf
	EndIf
EndIf

If lContinua
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada criado para mudar os botoes da enchoicebar                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA242BUT" )
		aButtons := ExecBlock( "MA242BUT", .F., .F., { nOpc, aButtons } )
		If ValType( aButtons ) # "A"
			aButtons := {}
		EndIf
	EndIf

	//Define objeto para tela cheia
	oSize := FwDefSize():New()

	//divide em linhas
	oSize:AddObject( "LINHA1",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA2",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA3",  100, 7, .T., .T. )
	oSize:AddObject( "LINHA4",  100, 7, .T., .T. )
	oSize:AddObject( "ITENS" ,  100, 72, .T., .T. )

	oSize:lProp 		:= .T. 			// Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3
	oSize:Process() 						// Dispara os calculos
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o posicionamento dos Says/Gets    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A242PosGet(@aSay, @aGet, aPosObj, oSize)

	Do While .T.
		nOpca:=2
		If ! l242Auto
			DEFINE MSDIALOG oDlg TITLE cCadastro ;
			FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] ;
			OF oMainWnd PIXEL

			@ aSay[01,1],aSay[01,2] SAY OemToAnsi(STR0006)	SIZE aSay[01,3],aSay[01,4] OF oDlg PIXEL		//"Produto Origem"
			@ aSay[02,1],aSay[02,2] SAY OemToAnsi(STR0007)	SIZE aSay[02,3],aSay[02,4] OF oDlg PIXEL		//"Armazem"
			@ aSay[03,1],aSay[03,2] SAY OemToAnsi(STR0008)	SIZE aSay[03,3],aSay[03,4] OF oDlg PIXEL		//"Endereco"
			@ aSay[04,1],aSay[04,2] SAY OemToAnsi(STR0011)	SIZE aSay[04,3],aSay[04,4] OF oDlg PIXEL		//"Lote"
			@ aSay[05,1],aSay[05,2] SAY OemToAnsi(STR0010)	SIZE aSay[05,3],aSay[05,4] OF oDlg PIXEL		//"Sub-Lote"
			@ aSay[06,1],aSay[06,2] SAY OemToAnsi(STR0012)	SIZE aSay[06,3],aSay[06,4] OF oDlg PIXEL		//"Validade"
			@ aSay[07,1],aSay[07,2] SAY OemToAnsi(STR0018)	SIZE aSay[07,3],aSay[07,4] OF oDlg PIXEL		//"Pot."
			If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
				@ aSay[08,1],aSay[08,2] SAY OemToAnsi(STR0013)	SIZE aSay[08,3],aSay[08,4] OF oDlg PIXEL		//"N£mero de S‚rie"
			EndIf
			@ aSay[09,1],aSay[09,2] SAY OemToAnsi(STR0014)	SIZE aSay[09,3],aSay[09,4] OF oDlg PIXEL		//"Quantidade"
			If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
				@ aSay[10,1],aSay[10,2] SAY OemToAnsi(STR0015)	SIZE aSay[10,3],aSay[10,4] OF oDlg PIXEL		//"Quantidade Secund ria"
			EndIf
			@ aSay[11,1],aSay[11,2] SAY OemToAnsi(STR0009)	SIZE aSay[11,3],aSay[11,4] OF oDlg PIXEL		//"Data"
			@ aSay[12,1],aSay[12,2] SAY OemToAnsi(STR0016)	SIZE aSay[12,3],aSay[12,4] OF oDlg PIXEL		//"Documento"
			If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
				@ aSay[13,1],aSay[13,2] SAY OemToAnsi(STR0037)	SIZE aSay[13,3],aSay[13,4] OF oDlg PIXEL //"Transf. K220"
			EndIf

			@ aGet[01,1],aGet[01,2] MSGET cProduto   		SIZE aGet[01,3],aGet[01,4] WHEN .F. OF oDlg PIXEL
			@ aGet[02,1],aGet[02,2] MSGET cLocOrig   		SIZE aGet[02,3],aGet[02,4] WHEN .F. OF oDlg PIXEL
			@ aGet[03,1],aGet[03,2] MSGET cLocaliza  		SIZE aGet[03,3],aGet[03,4] WHEN .F. OF oDlg PIXEL
			@ aGet[04,1],aGet[04,2] MSGET cLoteDigi   		SIZE aGet[04,3],aGet[04,4] WHEN .F. OF oDlg PIXEL
			@ aGet[05,1],aGet[05,2] MSGET cNumLote  		SIZE aGet[05,3],aGet[05,4] WHEN .F. OF oDlg PIXEL
			@ aGet[06,1],aGet[06,2] MSGET dDtValid   		SIZE aGet[06,3],aGet[06,4] WHEN .F. OF oDlg PIXEL HasButton
			@ aGet[07,1],aGet[07,2] MSGET nPotencia   		SIZE aGet[07,3],aGet[07,4] WHEN .F. OF oDlg PIXEL HasButton
			If X3USO(GetSX3Cache('D3_NUMSERI', "X3_USADO"))
				@ aGet[08,1],aGet[08,2] MSGET cNumSerie  		SIZE aGet[08,3],aGet[08,4] WHEN .F. OF oDlg PIXEL
			EndIf
			@ aGet[09,1],aGet[09,2] MSGET nQtdOrig  		SIZE aGet[09,3],aGet[09,4] WHEN .F. OF oDlg PIXEL Picture cPicQtOri HasButton
			If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
				@ aGet[10,1],aGet[10,2] MSGET nQtdOrigSe  		SIZE aGet[10,3],aGet[10,4] WHEN .F. OF oDlg PIXEL Picture cPicQtDes HasButton
			EndIf
			@ aGet[11,1],aGet[11,2] MSGET dEmis260 			SIZE aGet[11,3],aGet[11,4] WHEN .F. OF oDlg PIXEL HasButton
			@ aGet[12,1],aGet[12,2] MSGET cDocumento 		SIZE aGet[12,3],aGet[12,4] WHEN .F. OF oDlg PIXEL
			If lCpoTransf .And. X3USO(GetSX3Cache('D3_TRANSF', "X3_USADO"))
				@ aGet[13,1],aGet[13,2] MSCOMBOBOX cTransf  ITEMS aCmbTransf	SIZE aGet[13,3],aGet[13,4] WHEN .F. OF oDlg PIXEL
			EndIf

			If lMt242Scr
				ExecBlock("MT242SCR",.F.,.F.,{@oDlg, oSize:aPosObj, nOpc, nReg})
			EndIf

			oGet := MSGetDados():New(	oSize:GetDimension("ITENS","LININI"),;
										oSize:GetDimension("ITENS","COLINI"),;
										oSize:GetDimension("ITENS","LINEND"),;
										oSize:GetDimension("ITENS","COLEND"),;
										nOpc,"AllwaysTrue","AllwaysTrue","",.F.,,,,300)
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End(),nopca:=1},{||oDlg:End(),nopca:=2},,aButtons)
		Else
			If MsVldGAuto(aAutoCab)
				If MsGetDAuto(aAutoItens,"AllwaysTrue",{|| .T.},aAutoCab,aRotina[nOpc][4])
					nOpca := 1
				Else
					nOpca := 0
				EndIf
			EndIf
		EndIf
		Exit
	EndDo
	If nOpca == 1
		If A242ValEst()
			A242PrEsto(cAlias)
		EndIf
	EndIf
	dbSelectArea(cAlias)
	MsGoto(nReg)
EndIf
Return NIL

/*/{Protheus.doc} A242PosGet
Funcao que posiciona os campos na tela de acordo com o tamanho do codigo do produto
@author Emerson Rony Oliveira
@since 18/12/2008
@version 1.0
@return .T., Sempre Verdadeiro
@param aSay, array, Vetor com as coordenadas e tamanho dos objetos SAY
@param aGet, array, Vetor com as coordenadas e tamanho dos objetos Get
@param aPosObj, array, descricao
@param oSize, object, Objeto FWDefSize gerado
@type function
/*/
Static Function A242PosGet(aSay, aGet, aPosObj, oSize)
Local oSizeLin1
Local oSizeLin3
Local oSizeLin4

//define novo objeto para linha 1
oSizeLin1 := FwDefSize():New()

//Define o tamanho do objeto
oSizeLin1:aWorkArea := oSize:GetNextCallArea( "LINHA1" )

//Adiciona a área para os campos, dividindo em colunas
oSizeLin1:AddObject( "CAMPO1"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin1:AddObject( "CAMPO2"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin1:AddObject( "CAMPO3"   ,  33, 100, .T., .T.) // Não dimensionavel

oSizeLin1:lLateral 	:= .T.				//Calculo em Lateral
oSizeLin1:lProp		:= .T.            // Proporcional
oSizeLin1:aMargins	:= { 3, 3, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3
oSizeLin1:Process() 						// Dispara os calculos

//Produto Origem
aAdd( aSay, {oSizeLin1:GetDimension("CAMPO1","LININI")+3, oSizeLin1:GetDimension("CAMPO1","COLINI")   , 195, 010})
aAdd( aGet, {oSizeLin1:GetDimension("CAMPO1","LININI")  , oSizeLin1:GetDimension("CAMPO1","COLINI")+42, 100, 010})

//"Armazem"
aAdd( aSay, {oSizeLin1:GetDimension("CAMPO2","LININI")+3, oSizeLin1:GetDimension("CAMPO2","COLINI")   , 062, 006})
aAdd( aGet, {oSizeLin1:GetDimension("CAMPO2","LININI")  , oSizeLin1:GetDimension("CAMPO2","COLINI")+42, 020, 010})

//Endereço
aAdd( aSay, {oSizeLin1:GetDimension("CAMPO3","LININI")+3, oSizeLin1:GetDimension("CAMPO3","COLINI")   , 062, 006})
aAdd( aGet, {oSizeLin1:GetDimension("CAMPO3","LININI")  , oSizeLin1:GetDimension("CAMPO3","COLINI")+47, 100, 010})

//define novo objeto para linha 2
oSizeLin2 := FwDefSize():New()

oSizeLin2:aWorkArea := oSize:GetNextCallArea( "LINHA2" )

oSizeLin2:AddObject( "CAMPO1"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin2:AddObject( "CAMPO2"   ,  16, 100, .T., .T.) // Não dimensionavel
oSizeLin2:AddObject( "CAMPO3"   ,  16, 100, .T., .T.) // Não dimensionavel
oSizeLin2:AddObject( "CAMPO4"   ,  33, 100, .T., .T.) // Não dimensionavel

oSizeLin2:lLateral := .T.            //Calculo em Lateral
oSizeLin2:lProp := .T.               // Proporcional
oSizeLin2:aMargins := { 3, 3, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3
oSizeLin2:Process() 				 // Dispara os calculos

//"Lote"
aAdd( aSay, {oSizeLin2:GetDimension("CAMPO1","LININI")+3, oSizeLin2:GetDimension("CAMPO1","COLINI")   , 195, 010})
aAdd( aGet, {oSizeLin2:GetDimension("CAMPO1","LININI")  , oSizeLin2:GetDimension("CAMPO1","COLINI")+42, 100, 010})

//"Sub-Lote"
aAdd( aSay, {oSizeLin2:GetDimension("CAMPO2","LININI")+3, oSizeLin2:GetDimension("CAMPO2","COLINI")   , 062, 006})
aAdd( aGet, {oSizeLin2:GetDimension("CAMPO2","LININI")  , oSizeLin2:GetDimension("CAMPO2","COLINI")+42, 020, 010})

//Validade
aAdd( aSay, {oSizeLin2:GetDimension("CAMPO3","LININI")+3, oSizeLin2:GetDimension("CAMPO3","COLINI")   , 062, 006 })
aAdd( aGet, {oSizeLin2:GetDimension("CAMPO3","LININI")  , oSizeLin2:GetDimension("CAMPO3","COLINI")+27, 050, 010 })

//Potência
aAdd( aSay, {oSizeLin2:GetDimension("CAMPO4","LININI")+3, oSizeLin2:GetDimension("CAMPO4","COLINI")   , 062,006 })
aAdd( aGet, {oSizeLin2:GetDimension("CAMPO4","LININI")  , oSizeLin2:GetDimension("CAMPO4","COLINI")+47, 020,010 })

//define novo objeto para linha 3
oSizeLin3 := FwDefSize():New()

oSizeLin3:aWorkArea := oSize:GetNextCallArea( "LINHA3" )

oSizeLin3:AddObject( "CAMPO1"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin3:AddObject( "CAMPO2"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin3:AddObject( "CAMPO3"   ,  33, 100, .T., .T.) // Não dimensionavel

oSizeLin3:lLateral := .T.            //Calculo em Lateral
oSizeLin3:lProp := .T.               // Proporcional
oSizeLin3:aMargins := { 3, 3, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3
oSizeLin3:Process() 						// Dispara os calculos

//"Número de Série"
aAdd( aSay, {oSizeLin3:GetDimension("CAMPO1","LININI")+3, oSizeLin3:GetDimension("CAMPO1","COLINI")   , 195, 010})
aAdd( aGet, {oSizeLin3:GetDimension("CAMPO1","LININI")  , oSizeLin3:GetDimension("CAMPO1","COLINI")+42, 100, 010})

//"Quantidade"
aAdd( aSay, {oSizeLin3:GetDimension("CAMPO2","LININI")+3, oSizeLin3:GetDimension("CAMPO2","COLINI")   , 034, 006})
aAdd( aGet, {oSizeLin3:GetDimension("CAMPO2","LININI")  , oSizeLin3:GetDimension("CAMPO2","COLINI")+42, 100, 010})

//"Quantidade Secundária"
aAdd( aSay, {oSizeLin3:GetDimension("CAMPO3","LININI")+3, oSizeLin3:GetDimension("CAMPO3","COLINI")   , 062, 006})
aAdd( aGet, {oSizeLin3:GetDimension("CAMPO3","LININI")  , oSizeLin3:GetDimension("CAMPO3","COLINI")+47, 080, 010})

//define novo objeto para linha 4
oSizeLin4 := FwDefSize():New()

oSizeLin4:aWorkArea := oSize:GetNextCallArea( "LINHA4" )

oSizeLin4:AddObject( "CAMPO1"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin4:AddObject( "CAMPO2"   ,  33, 100, .T., .T.) // Não dimensionavel
oSizeLin4:AddObject( "CAMPO3"   ,  33, 100, .T., .T.) // Não dimensionavel

oSizeLin4:lLateral := .T.            //Calculo em Lateral
oSizeLin4:lProp := .T.               // Proporcional
oSizeLin4:aMargins := { 3, 3, 0, 0 } // Espaco ao lado dos objetos 0, entre eles 3
oSizeLin4:Process() 						// Dispara os calculos

//Data
aAdd( aSay, {oSizeLin4:GetDimension("CAMPO1","LININI")+3, oSizeLin4:GetDimension("CAMPO1","COLINI")   , 062,010})
aAdd( aGet, {oSizeLin4:GetDimension("CAMPO1","LININI")  , oSizeLin4:GetDimension("CAMPO1","COLINI")+42, 050,010})

//"Documento"
aAdd( aSay, {oSizeLin4:GetDimension("CAMPO2","LININI")+3, oSizeLin4:GetDimension("CAMPO2","COLINI")   , 062,006})
aAdd( aGet, {oSizeLin4:GetDimension("CAMPO2","LININI")  , oSizeLin4:GetDimension("CAMPO2","COLINI")+42, 100,010})

//"Transf. K220"
aAdd( aSay, {oSizeLin4:GetDimension("CAMPO3","LININI")+3, oSizeLin4:GetDimension("CAMPO3","COLINI")   , 080,006})
aAdd( aGet, {oSizeLin4:GetDimension("CAMPO3","LININI")  , oSizeLin4:GetDimension("CAMPO3","COLINI")+47, 080,013})

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242Proces³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 04/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao que faz o processamento da inclusao                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A242Proces()

Local aSoma      := {}
Local aSvCus     := {}
Local cLocCQ     := GetMvNNR('MV_CQ','98')
Local cCampo     := ""
Local cConta     := ''
Local nQtd2UM    := 0
Local nx         := 0
Local ny         := 0
Local lMTA242I   := ExistBlock("MTA242I")
Local lMa242D3   := ExistBlock("MA242D3")
Local lContinua  :=.T.
Local aLockSB2   := {}
Local aRetPE     := {}
Local lPergWMS   := .T.
Local aCtbDia    := {}
Local nTamDec    := TamSx3("D3_CUSTO1")[2]
Local nPosCtrWms := 0
Local lFATHER	 := SD3->(ColumnPos("D3_FATHER")) > 0
Local lWmsNew    := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local lWmsMvItD  := FindFunction("WmsMvItDes")

PRIVATE cNumSeq:=""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o custo medio e' calculado On-Line               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cCusMed == "O"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se necessario cria o cabecalho do arquivo de prova           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCriaHeader
		lCriaHeader := .F.
		nHdlPrv := HeadProva(cLoteEst,"MATA242",Subs(cUsuario,7,6),@cArquivo)
		If nHdlPrv <= 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Restaura a integridade da janela                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SD3")
			lContinua := .F.
		EndIf
	EndIf
EndIf

If	lContinua
	If	IntDL() .And. nPosServic > 0

		nPosCtrWms := aScan(aCols,{|x| !Empty(x[nPosServic]) .And. !x[Len(x)]})

		If	nPosCtrWms > 0
			Private cA240End := CriaVar('DB_LOCALIZ')
			//-- Permite a informacao do Endereco e da Estrutura de Origem via Ponto de Entrada
			If	ExistBlock('A242WMSO')
				//-- Retorno esperado do RDMAKE
				//-- Array[1] = cA240End
				//-- Array[2] = cA240Est
				aRetPE := ExecBlock('A242WMSO', NIL, NIL)
				If	ValType(aRetPE)=='A' .And. Len(aRetPE) > 0
					lPergWMS := .F.
					cA240End := aRetPE[1]
				EndIf
			EndIf
			If lPergWMS .And. (Type('l242Auto') == 'U' .Or. (Type('l242Auto') == 'L' .And. !l242Auto))
				DLPergEnd(@cA240End,.T.,.T.,'1',aCols[nPosCtrWms][nPosLocal])
			EndIf
		EndIf
	EndIf
	Begin Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nX:=1 to Len(aCols)
			If aScan(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])==0
				aAdd(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])
			EndIf
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If MultLock("SB2",aLockSB2,1)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza arquivo de saldos em estoque      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SB1")
			MsSeek(xFilial("SB1")+cProduto)
			dbSelectArea("SB2")
			MsSeek(xFilial("SB2")+cProduto+cLocOrig)
			If Eof()
				CriaSB2(cProduto,cLocOrig)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Pega o proximo numero sequencial de movimento      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cNumSeq := ProxNum()
			RecLock("SD3",.T.)
			Replace D3_FILIAL  With xFilial("SD3")		,D3_COD     With cProduto,;
					D3_QUANT   With nQtdOrig			,D3_CF      With "RE7",;
					D3_CHAVE   With SubStr(D3_CF,2,1)+IIf(D3_CF=="DE7","9","0"),;
					D3_LOCAL   With cLocOrig			,D3_DOC     With cDocumento,;
					D3_EMISSAO With dEmis260			,D3_UM      With SB1->B1_UM,;
					D3_GRUPO   With SB1->B1_GRUPO		,D3_NUMSEQ  With cNumSeq,;
					D3_QTSEGUM With nQtdOrigSe			,D3_SEGUM   With SB1->B1_SEGUM,;
					D3_TM      With "999"        		,D3_TIPO    With SB1->B1_TIPO,;
					D3_CONTA   With SB1->B1_CONTA		,D3_USUARIO With CUSERNAME,;
					D3_ITEMCTA With SB1->B1_ITEMCC  	,;
					D3_CLVL    With SB1->B1_CLVL    	,;
					D3_CC      With SB1->B1_CC      	,;
					D3_RATEIO  With 100                 ,;
					D3_NUMLOTE With IIf(Rastro(cProduto,"S"),cNumLote,CriaVar("D3_NUMLOTE")),;
					D3_LOTECTL With IIf(Rastro(cProduto),cLoteDigi,CriaVar("D3_LOTECTL")),;
					D3_DTVALID With IIf(Rastro(cProduto),dDtValid,CriaVar("D3_DTVALID")),;
					D3_POTENCI With IIf(PotencLote(cProduto),nPotencia,CriaVar("D3_POTENCI")),;
					D3_CODLAN  With A240CAT83()

			Replace D3_NUMSERI With cNumSerie,D3_LOCALIZ With cLocaliza

			If lFATHER
				Replace D3_FATHER With "F"
			EndIf

			If lCpoTransf
				Replace D3_TRANSF With cTransf
			EndIf

			SD3->(MsUnlock())

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ	ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de Entrada criado para possibilitar a alteração do registro de origem na D3 - Cabecalho da desmontagem ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("M242D3IN")
				ExecBlock("M242D3IN", .F., .F.)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Pega os 15 custos medios atuais            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCM := PegaCMAtu(SD3->D3_COD,SD3->D3_LOCAL)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava o custo da movimentacao              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCusto := GravaCusD3(aCM)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o saldo atual (VATU) com os dados do SD3     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !B2AtuComD3(aCusto)
				aSvCus := AClone(aCusto)
				aSoma  := AClone(aCusto)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o custo medio e' calculado On-Line               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cCusMed == "O"
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Gera o lancamento no arquivo de prova           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nTotal+=DetProva(nHdlPrv,"670","MATA242",cLoteEst)
				EndIf
				For nx:=1 to Len(aCols)
					If !aCols[nx,Len(aCols[nx])]
						dbSelectArea("SB1")
						MsSeek(xFilial("SB1")+aCols[nx,nPosCod])
						dbSelectArea("SB2")
						If !(MsSeek(xFilial("SB2")+aCols[nx,nPosCod]+aCols[nx,nPosLocal]))
							CriaSB2(aCols[nx,nPosCod],aCols[nx,nPosLocal])
						EndIf
						cConta  := IIf(nPosCta > 0, aCols[nx,nPosCta], CriaVar("D3_CONTA"))
						nQtd2UM := IIf(nPosQTSegUM > 0, aCols[nx,nPosQTSegUM], 0)
						RecLock("SD3",.T.)
						Replace D3_FILIAL  With xFilial("SD3"),			D3_COD		With aCols[nx,nPosCod],;
								D3_QUANT   With aCols[nx,nPosQuant],	D3_CF		With "DE7",;
								D3_LOCAL   With aCols[nx,nPosLocal],	D3_DOC		With cDocumento,;
								D3_EMISSAO With dEmis260,				D3_UM		With SB1->B1_UM,;
								D3_GRUPO   With SB1->B1_GRUPO,			D3_NUMSEQ	With cNumSeq,;
								D3_SEGUM   With SB1->B1_SEGUM,			D3_TM		With "499",;
								D3_TIPO    With SB1->B1_TIPO,;
								D3_CONTA   With IIf(Empty(cConta),SB1->B1_CONTA,aCols[nx,nPosCta]),;
								D3_ITEMCTA With IIf(Empty(aCols[nx,nPosItCta]),SB1->B1_ITEMCC,aCols[nx,nPosItCta]),;
								D3_CLVL    With IIf(Empty(aCols[nx,nPosCLVL]),SB1->B1_CLVL,aCols[nx,nPosCLVL]),;
								D3_CC      With IIf(Empty(aCols[nx,nPosCC]),SB1->B1_CC,aCols[nx,nPosCC]),;
								D3_USUARIO With CUSERNAME,;
								D3_CHAVE   With SubStr(D3_CF,2,1)+IIf(D3_CF=="DE7","9","0"),;
								D3_QTSEGUM With ConvUm(SB1->B1_COD,aCols[nx,nPosQuant],nQtd2UM,2),;
								D3_LOTECTL With IIf(ValType(nPosLotCtl) == "N", aCols[nx,nPosLotCtl],Criavar("D3_LOTECTL")),;
								D3_DTVALID With IIf(ValType(nPosDValid) == "N", IIf(Empty(aCols[nx,nPosDValid]),dDataBase,aCols[nx,nPosDValid]),dDataBase),;
								D3_POTENCI With IIf(ValType(nPosPotenc) == "N", IIf(PotencLote(aCols[nx,nPosCod]),aCols[nx,nPosPotenc],Criavar("D3_POTENCI")),Criavar("D3_POTENCI")),;
								D3_CODLAN  With A240CAT83(cProduto)

						If nPosPerImp>0
							Replace D3_PERIMP With aCols[nx,nPosPerImp]
						EndIf

						If nPosLocali > 0
							Replace D3_LOCALIZ With aCols[nx,nPosLocali]
						EndIf
						If nPosNumSer > 0
							Replace D3_NUMSERI With aCols[nx,nPosNumSer]
						EndIf
						If nPosServic > 0 .And. IntDL(aCols[nx,nPosCod]) .And. !Empty(aCols[nx,nPosServic])
							Replace	D3_SERVIC With aCols[nx,nPosServic]
						EndIf

						If nPosRateio > 0
							Replace	D3_RATEIO  With aCols[nx,nPosRateio]
						EndIf

						//-- Ponto de Entrada para informar campos do SD3
						If	lMa242D3
							ExecBlock("MA242D3", .F., .F., {nx})
						EndIf
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava os campos customizados               ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						For nY := 1 To Len( aHeader )
							If GetSx3Cache( AllTrim( aHeader[ nY ][ 02 ] ), 'X3_PROPRI' ) == 'U' .And. GetSx3Cache( AllTrim( aHeader[ nY ][ 02 ] ), 'X3_CONTEXT' ) <> 'V'
								SD3->( FieldPut( ColumnPos( AllTrim( aHeader[ nY ][ 02 ] ) ) , aCols[ nX ][ nY ] ) )
							EndIf
						Next nY

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava o custo da movimentacao com rateio   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aCusto := {0,0,0,0,0}
						For ny:=1 To 5
							cCampo := "D3_CUSTO"+StrZero(nY,1,0)
							If nx == Len(aCols)
								aCusto[ny] := aSoma[ny]
							ElseIf nPosRateio > 0
								aCusto[ny] := Round(aSvCus[ny]*aCols[nX,nPosRateio]/100,nTamDec)
								aSoma[ny] -= aCusto[ny]
							EndIf
							Replace &(cCampo) With aCusto[ny]
						Next

						If lCpoTransf
							Replace D3_TRANSF With cTransf
						EndIf

						SD3->(MsUnlock())
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o saldo atual (VATU) com os dados do SD3     ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !B2AtuComD3(aCusto)
							If cLocCQ == aCols[nx,nPosLocal] .And. cLocCQ # cLocOrig
								//-- Inclui o Produto no CQ
								fGeraCQ0('SD3', SD3->D3_COD, 'TR')
							EndIf
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Verifica se o custo medio e' calculado On-Line               ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If cCusMed == "O"
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Gera o lancamento no arquivo de prova           ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								nTotal+=DetProva(nHdlPrv,"672","MATA242",cLoteEst)
							EndIf
						EndIf
						//produto com controle WMS chamar executar funcao pra inserir o saldo no endereço destino wms 
						If lWmsNew .AND. lWmsMvItD .And. IntWms(aCols[nx,nPosCod]) 
						   WmsMvItDes("1",SD3->D3_LOCAL,;
						                  SD3->D3_LOCALIZ,;
										  SD3->D3_COD,;
										  SD3->D3_LOTECTL,;
										  SD3->D3_NUMLOTE,;
										  SD3->D3_NUMSERI,;
										  SD3->D3_QUANT,;
										  SD3->D3_QTSEGUM,;
										  cDocumento,;
										  cNumSeq)
						EndIf 
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Executa ponto de entrada apos gravacao          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lMTA242I
						ExecBlock("MTA242I",.F.,.F.,{nx})
					EndIf
				Next nx
				If ExistBlock("MTA242IN")
					ExecBlock("MTA242IN",.F.,.F.)
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o custo medio e' calculado On-Line               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cCusMed == "O"
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicializa perguntas deste programa                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ mv_par01 - Se mostra e permite digitar lancamentos contabeis   ³
					//³ mv_par02 - Se deve aglutinar os lancamentos contabeis          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Pergunte("MTA260",.F.)
					lDigita   := Iif(mv_par01 == 1,.T.,.F.)
					lAglutina := Iif(mv_par02 == 1,.T.,.F.)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Se ele criou o arquivo de prova ele deve gravar o rodape'    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RodaProva(nHdlPrv,nTotal)
					If ( UsaSeqCor() )
						cCodDiario := CtbaVerdia()
						aCtbDia := {{"SD3",SD3->(RECNO()),cCodDiario,"D3_NODIA","D3_DIACTB"}}
					Else
						aCtbDia := {}
					EndIF
					cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
					lCriaHeader := .T.
				EndIf
			EndIf
		EndIf
	End Transaction
EndIf
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242PrEsto³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 04/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao que faz o processamento do estorno                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A242PrEsto(cAlias)
Local nX        := 0
Local lMTA242E:=ExistBlock("MTA242E")
Local lContinua := .T.
Local aLockSD3 := {}
Local aLockSB2 := {}
Local aCtbDia  := {}
Local ld3kLimp	:= Findfunction('MatLimpD3K')
Local lWmsNew   := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local lWmsMvItD := FindFunction("WmsMvItDes")
Local aCpD3EST  := {}
Local nRecESTOR := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o custo medio e' calculado On-Line               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cCusMed == "O"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se necessario cria o cabecalho do arquivo de prova           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lCriaHeader
		lCriaHeader := .F.
		nHdlPrv := HeadProva(cLoteEst,"MATA242",Subs(cUsuario,7,6),@cArquivo)
		If nHdlPrv <= 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Restaura a integridade da janela                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea(cAlias)
			lContinua := .F.
		EndIf
	EndIf
EndIf

If lContinua
	Begin Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//Produto Origem
		If aScan(aLockSD3,cProduto+cLocOrig+cSeqEst)==0
			aadd(aLockSD3,cProduto+cLocOrig+cSeqEst)
		EndIf
		If aScan(aLockSB2,cProduto+cLocOrig)==0
			aadd(aLockSB2,cProduto+cLocOrig)
		EndIf
		For nX := 1 to Len(aCols)
			If aScan(aLockSD3,aCols[nX][nPosCod]+aCols[nX][nPosLocal]+cSeqEst)==0
				aadd(aLockSD3,aCols[nX][nPosCod]+aCols[nX][nPosLocal]+cSeqEst)
			EndIf
			If aScan(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])==0
				aadd(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])
			EndIf
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If MultLock("SD3",aLockSD3,3) .And. MultLock("SB2",aLockSB2,1)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gera movimento inverso da origem           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava o Flag de estorno                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SD3")
			MsGoTo(nRecOld)
			If D3_ESTORNO == "S"
				Help(" ",1,"A242ESTORN")
			Else
				aCpD3EST:={}
				aAdd (aCpD3EST,{"D3_TM"   ,"499"})
				aAdd (aCpD3EST,{"D3_CF"   ,"DE7"})
				aAdd (aCpD3EST,{"D3_CHAVE","E9" })
				nRecESTOR := SD3Estorno(recno(),aCpD3EST,.T.,.T.)
				dbgoto(nRecESTOR)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza o periodo da geracao do bloco K                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ld3kLimp
					MatLimpD3K(SD3->D3_COD,SD3->D3_NUMSEQ)
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Pega o custo da movimentacao               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aCusto := PegaCusD3()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza o saldo atual (VATU) com os dados do SD3     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !B2AtuComD3(aCusto)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se o custo medio e' calculado On-Line               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If cCusMed == "O"
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Gera o lancamento no arquivo de prova           ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						nTotal+=DetProva(nHdlPrv,"672","MATA242",cLoteEst)
					EndIf

					dbSelectArea("SD3")
					dbSetOrder(4)
					dbSeek(xFilial("SD3")+cSeqEst)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Gera movimento inverso do destino          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					While !Eof() .and. SD3->D3_NUMSEQ == cSeqEst

						If SD3->D3_CF != "DE7" .or. SD3->D3_DOC != cDocumento .or. SD3->D3_EMISSAO != dEmis260 .Or. SD3->D3_ESTORNO == "S"
							dbSkip()
							Loop
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava o Flag de estorno                    ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("SD3")
						aCpD3EST:={}
						aAdd (aCpD3EST,{"D3_TM"   ,"999"})
						aAdd (aCpD3EST,{"D3_CF"   ,"RE7"})
						aAdd (aCpD3EST,{"D3_CHAVE","E0" })
						nRecESTOR := SD3Estorno(recno(),aCpD3EST,.T.,.T.)
						dbgoto(nRecESTOR)

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o periodo da geracao do bloco K                       ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ld3kLimp
							MatLimpD3K(SD3->D3_COD,SD3->D3_NUMSEQ)
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Pega o custo da movimentacao               ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aCusto := PegaCusD3()

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza o saldo atual (VATU) com os dados do SD3     ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !B2AtuComD3(aCusto)

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Verifica se o custo medio e' calculado On-Line               ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If cCusMed == "O"
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Gera o lancamento no arquivo de prova           ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								nTotal+=DetProva(nHdlPrv,"670","MATA242",cLoteEst)
							EndIf
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Executa ponto de entrada apos gravacao          ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If lMTA242E
								ExecBlock("MTA242E",.F.,.F.)
							EndIf
							If IntDL(SD3->D3_COD) .And. !Empty(SD3->D3_SERVIC)
								//-- Estorna a O.S.WMS nao executada
								WmsDelDCF('1','SD3')
							EndIf
						EndIf
						//funcao WMS para gravar os registros de estorno na D13. 
						If lWmsNew .AND. lWmsMvItD .And. IntWms(SD3->D3_COD)
							WmsMvItDes("2",SD3->D3_LOCAL,;
						  	           	SD3->D3_LOCALIZ,;
									   	SD3->D3_COD,;
										SD3->D3_LOTECTL,;
										SD3->D3_NUMLOTE,;
										SD3->D3_NUMSERI,;
										SD3->D3_QUANT,;
										SD3->D3_QTSEGUM,;
										SD3->D3_DOC,;
										SD3->D3_NUMSEQ)	
                    	EndIF 
						dbSelectArea("SD3")
						dbSkip()
					End
					If ExistBlock("MTA242ES")
						ExecBlock("MTA242ES",.F.,.F.)
					EndIf
				EndIf
			EndIf
		EndIf
	End Transaction

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o custo medio e' calculado On-Line               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cCusMed == "O"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa perguntas deste programa                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ mv_par01 - Se mostra e permite digitar lancamentos contabeis   ³
		//³ mv_par02 - Se deve aglutinar os lancamentos contabeis          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Pergunte("MTA260",.F.)
		lDigita   := Iif(mv_par01 == 1,.T.,.F.)
		lAglutina := Iif(mv_par02 == 1,.T.,.F.)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se ele criou o arquivo de prova ele deve gravar o rodape'    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RodaProva(nHdlPrv,nTotal)
		If ( UsaSeqCor() )
			cCodDiario := CtbaVerdia()
			aCtbDia := {{"SD3",SD3->(RECNO()),cCodDiario,"D3_NODIA","D3_DIACTB"}}
		Else
			aCtbDia := {}
		EndIF
		cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
		lCriaHeader := .T.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)
	SetCursor(0)
EndIf
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242LinOK ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida‡„o da linha da GetDados                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242LinOK()
Local lRet       := .T.
Local nx         := 0
Local cAlias     := Alias()
Local nOrder     := IndexOrd()
Local nRecno     := Recno()
Local nCC        := GDFieldPos("D3_CC")
Local nPConta    := GDFieldPos("D3_CONTA")
Local nPItemCTA	 := GDFieldPos("D3_ITEMCTA")
Local nPCLVL     := GDFieldPos("D3_CLVL")
Local lRetPE     := .T.
Local lLoteAtivo := ValType(nPosLotCtl) == "N" .And. ValType(nPosLote) == "N"
Local lWmsNew    := SuperGetMv("MV_WMSNEW",.F.,.F.)
Local oProduto   := Nil
Local cNumSeriG  := ''
Local cNumSeriX  := ''
Local cLnLotCTL  := ''
Local cLnNumLt   := ''
Local cLnLocali  := ''
Local cLxLotCTL  := ''
Local cLxNumLt   := ''
Local cLxLocali  := ''

_lWmsVlDesm := iIf(_lWmsVlDesm == NIL,FindFunction("WmsVlDesm"),_lWmsVlDesm) 

If !aCols[n,Len(aCols[n])]
	dbSelectArea("SB2")
	dbSetOrder(1)
	If Empty(cDocumento) .Or. Empty(aCols[n,nPosCod]) .Or. aCols[n,nPosQuant] = 0 .Or. Empty(aCols[n,nPosLocal]) .Or. IIf(nPosRateio > 0,aCols[n,nPosRateio] == 0,.F.)
		Help(" ",1,"MA242PR",,)
		lRet:=.F.
	EndIf

	If lRet .And. _lWmsVlDesm .AND. lWmsNew .AND. IntWms(aCols[n,nPosCod]) 
		If !WmsVlDesm("1",aCols[n,nPosLocal],IIf(nPosLocali > 0,aCols[n,nPosLocali]," "),aCols[n,nPosCod],aCols[n,nPosLotCTL],aCols[n,nPosLote],aCols[n,nPosQuant],IIf(nPosServic > 0,aCols[n,nPosServic]," "))
			lRet := .F. 
		EndIf 	
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a permissao do armazem. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet
	   lRet:= MaAvalPerm(3,{aCols[n,nPosLocal],aCols[n,nPosCod]})
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o produto est  sendo inventariado.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. BlqInvent(aCols[n,nPosCod],aCols[n,nPosLocal])
		Help(" ",1,"BLQINVENT",,aCols[n,nPosCod]+OemToAnsi(STR0017)+aCols[n,nPosLocal],1,11)
		lRet:=.F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Analisa se o tipo do armazem permite a movimentacao |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. AvalBlqLoc(aCols[n,nPosCod],aCols[n,nPosLocal],Nil)
		lRet:=.F.
	EndIf

	If lRet .And. (aCols[n, nPosQuant] < 0 .Or. aCols[n, nPosQTSegUM] < 0)
		Help(,,'A242NEGAT',,STR0035,1,0) //Não é possível informar a quantidade negativa.
		lRet := .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o produto ‚ igual ao produto origem.    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. aCols[n,nPosCod]+IIf(lLoteAtivo, aCols[n,nPosLotCtl]+aCols[n,nPosLote], CriaVar("B8_LOTECTL")+CriaVar("B8_NUMLOTE")) == cProduto+cLoteDigi+cNumLote
		If ExistBlock("MT242IG")
			lRet := ExecBlock("MT242IG",.F.,.F.)
		Else
			Help(" ",1,"MA260IGUAL")
			lRet:=.F.
		EndIf
	EndIf
	If lRet .And. !(MsSeek(xFilial("SB2")+aCols[n,nPosCod]+aCols[n,nPosLocal])) .And. (GetMV("MV_VLDALMO") == "S")
		Help(" ",1,"A260LOCAL")
		lRet:=.F.
	EndIf
	If lRet .And. Localiza(aCols[n,nPosCod])
		If !(IntDL(GDFieldGet("D3_COD",n)) .And. !Empty(GDFieldGet("D3_SERVIC",n)))
			If Empty(nPosLocali) .And. Empty(nPosNumSer)
				UserException(OemToAnsi(STR0025))
			EndIf
			If nPosNumSer > 0 .And. !MtAvlNSer(aCols[n,nPosCod],aCols[n,nPosNumSer],aCols[n,nPosQuant],aCols[n,nPosQTSegUM])
				lRet:=.F.
			EndIf

			If lRet
				If ( nPosLocali > 0 .And. Empty( aCols[ n, nPosLocali ] ) ) .And. ( nPosNumSer > 0 .And. Empty( aCols[ n, nPosNumSer ] ) )
					Help(" ",1,"LOCALIZOBR")
					lRet:=.F.
				EndIf
			EndIf

		EndIf
	EndIf
	If lRet
		For nx:=1 to Len(aCols)
			cNumSeriG := IIf(nPosNumSer > 0, aCols[n,nPosNumSer] , CriaVar("D3_NUMSERI"))
			cNumSeriX := IIf(nPosNumSer > 0, aCols[nx,nPosNumSer], CriaVar("D3_NUMSERI"))
			cLnLotCTL := IIf(nPosLotCtl > 0, aCols[n,nPosLotCtl] , CriaVar("D3_LOTECTL"))
			cLxLotCTL := IIf(nPosLotCtl > 0, aCols[nx,nPosLotCtl], CriaVar("D3_LOTECTL"))
			cLnNumLt  := IIf(nPosLote   > 0, aCols[n,nPosLote]   , CriaVar("D3_NUMLOTE"))
			cLxNumLt  := IIf(nPosLote   > 0, aCols[nx,nPosLote]  , CriaVar("D3_NUMLOTE"))
			cLnLocali := IIf(nPosLocali > 0, aCols[n,nPosLocali] , CriaVar("D3_LOCALIZ"))
			cLxLocali := IIf(nPosLocali > 0, aCols[nx,nPosLocali], CriaVar("D3_LOCALIZ"))
			If nx # n .And. ;
				aCols[n,nPosCod]+cLnLotCTL+cLnNumLt+cNumSeriG+cLnLocali== ;
				aCols[nx,nPosCod]+cLxLotCTL+cLxNumLt+cNumSeriX+cLxLocali .And. ;
				!aCols[nx,Len(aCols[nx])]
				Help(" ",1,"A242JACAD")
				lRet:=.F.
			EndIf
		Next nx
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se ja nao existe um numero de serie p/ este produto ³
	//³ neste almoxarifado.                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. nPosNumSer > 0 .And. !Empty(aCols[n,nPosNumSer])
		dbSelectArea("SBF")
		dbSetOrder(4)
		cSeek  := xFilial("SBF")+aCols[n,nPosCod]+aCols[n,nPosNumSer]
		nAchou := ASCAN(aCols,{|x| x[nPosNumSer] == aCols[n,nPosNumSer]})
		If dbSeek(cSeek) .And. QtdComp(SBF->BF_QUANT) > QtdComp(0) .And. nAchou > 0
			Help(" ",1,"NUMSERIEEX")
			lRet:=.F.
		EndIf
	EndIf

	If lRet .And. (ExistBlock("MT242LOK"))
		lRet := ExecBlock("MT242LOK",.F.,.F.)
		lRet := If(ValType(lRet)#"L",.T.,lRet)
	EndIf
	If lRet .And. ExistBlock("MT242OK2")
		lRet := If(ValType(lRetPE := ExecBlock("MT242OK2",.F.,.F.,{lRet})) == "L",lRetPE,lRet)
	EndIf
	dbSelectArea(cAlias)
	dbSetOrder(nOrder)
	MsGoto(nRecno)

	If lRet
		MTWmsPai(cProduto,@oProduto)
		If ProdIsWms(aCols[n, nPosCod])
			If Empty(aCols[n,nPosServic])
				//-- Produto não é pai
				If aScan(oProduto:aProduto,{|x| x[1] == (oProduto:GetProduto())}) > 0
					lRet := .F.
					Help(" ",1,"A240SERVIC")
				EndIf
			ElseIf aScan(oProduto:aProduto,{|x| x[1] == (oProduto:GetProduto())}) == 0
				Help(" ",1,"A242PRODENDWMS")
				lRet := .F.
			EndIf
		EndIf
	EndIf
	//Consiste amarração da Conta Contábil X Centro de Custo
	If lRet .And. nPConta <> 0 .And. nCC <> 0 .And. nPItemCTA <> 0 .And. nPCLVL <> 0
		If !CtbAmarra(aCols[n,nPConta],aCols[n,nCC],aCols[n,nPItemCTA],aCols[n,nPCLVL])
			lRet:=.F.
		EndIf
	EndIf
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242TudoOK³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida‡„o da GetDados                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242TudoOK(o)
Local lRet    :=.T.
Local nX      := 0
Local nTot    := 0
Local nDif    := 0
Local AuxaCols:= {}
Local aAux 	  := AClone( aCols )
Local nRegZero:= 0
Local oProduto := Nil

If ExistBlock("MT242TOK")
	lRet := ExecBlock("MT242TOK",.F.,.F.)
	lRet := If(ValType(lRet)#"L",.T.,lRet)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica calendário contábil                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	lRet := (CtbValiDt(Nil,dEmis260,,Nil ,Nil ,{"EST001"}))
EndIf

// Elimina linhas deletadas
For nX := 1 to Len(aCols)
	If aCols[nX,Len(aCols[nX])] == .F. // Item Nao Deletado
		AADD(AuxaCols,aCols[nX])
	EndIf
Next
aCols :=Aclone(AuxaCols)

If Len( aCols ) == 0
	aCols := AClone( aAux )
	lRet := .F.
EndIf

If lRet .And. nPosRateio > 0
	For nx:=1 to Len(aCols)
		If !(aCols[nx,Len(aCols[nx])])
			nTot+=aCols[nx,nPosRateio]
			If aCols[nx,nPosRateio] == 0
				nRegZero++
			EndIf
		EndIf
	Next nx
	If Empty(nTot)
		lRet:=.F.
	ElseIf nTot > 100
		Help(" ",1,"A242RATEIO")
		lRet:=.F.
	ElseIf nTot < 100
		nDif:=100-nTot
		If nRegZero > 1
			For nx:=1 to Len (aCols)
				If aCols[nx,nPosRateio] == 0
					aCols[nx,nPosRateio] := nDif/nRegZero
				EndIf
			Next nx
		Else
			aCols[Len(aCols),nPosRateio]+=nDif
			If !l242Auto
				oGet:ForceRefresh()
			EndIf
		EndIf
	EndIf
EndIf
If lRet .And. !MtAvlNSer(cProduto,cNumSerie,nQtdOrig,nQtdOrigSe)
	lRet:=.F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Custo FIFO On-Line                     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsFifoOnLine()
	If SaldoSBD("SD3",cProduto,cLocOrig,dDataBase,.F.)[1] < nQtdOrig
		Help(" ",1,"DIVFIFO2")
		lRet := .F.
	EndIf
EndIf

If ProdIsWms(cProduto)
	MTWmsPai(cProduto,@oProduto)
	If aScan(oProduto:aProduto,{|x| x[1] == (oProduto:GetProduto())}) = 0
		For nX := 1 To Len(oProduto:aProduto)
			If aScan(aCols,{|x| x[nPosCod] == (oProduto:aProduto[nX, 1])}) == 0
				Help(" ",1,"A242PRODWMS")
				lRet := .F.
				Exit
			EndIf
		Next nX
	EndIf
EndIf

If Empty(cLocOrig)
	Help(" ",1,"A242LOCAL")
	lRet:=.F.
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242Local ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida‡„o do campo cLocOrig                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.				                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Local(cLocOrig)
Local lRet     := .T.
Local cAlias   := Alias()
Local nOrder   := IndexOrd()
Local nRecno   := Recno()

Default cLocOrig := &(ReadVar())
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o produto est  sendo inventariado.      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If BlqInvent(cProduto,cLocOrig)
	Help(" ",1,"BLQINVENT",,cProduto+OemToAnsi(STR0017)+cLocOrig,1,11)
	lRet:=.F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Analisa se o tipo do armazem permite a movimentacao |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. AvalBlqLoc(cProduto,cLocOrig,Nil)
	lRet:=.F.
EndIf

If lRet
   lRet:= MaAvalPerm(3,{cLocOrig,cProduto})
EndIf

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localizacoes - Valida armazem de transito                     |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If IsLocTran(cLocOrig)
		lRet := .F.
	EndIf
	If cLocOrig == GetMvNNR('MV_CQ','98')
		Help(" ",1,"A260LOCCQ")
		lRet:=.F.
	ElseIf cLocOrig == GetMvNNR('MV_LOCPROC','99')
		Help(" ",1,"A260LOCPRO")
		lRet:=.F.
	Else
		dbSelectArea("SB2")
		dbSetOrder(1)
		MsSeek(xFilial("SB2")+cProduto+cLocOrig)
		If !Found()
			Help(" ",1,"A242LOCAL")
			lRet:=.F.
		EndIf
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrder)
MsGoto(nRecno)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242Data  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida‡„o do campo dEmis260                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.				                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Data()
Local lRet:=.T.//,dEmis260
Local dDataFec := MVUlmes()

Default dEmis260:= &(ReadVar())
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificar data do ultimo fechamento em SX6.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataFec >= dEmis260
	Help ( " ", 1, "FECHTO" )
	lRet:=.F.
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242Quant ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida‡„o do campo nQtdOrig                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Quant()
Local lRet := .T.,nQuant:=&(ReadVar())
Local lEstNeg:=IIf(GetMV("MV_ESTNEG") == "S",.T.,.F.)
Local cAlias:=Alias(),nOrder:=IndexOrd(),nRecno:=Recno()
dbSelectArea("SB2")
dbSetOrder(1)
If MsSeek(xFilial("SB2")+cProduto+cLocOrig)
	If Localiza(cProduto)
		If Empty(cNumSerie) .And. Empty(cLocaliza)
			Help(" ",1,"LOCALIZOBR")
			lRet:=.F.
		EndIf
	EndIf
	If lRet .And. nQuant > SaldoMov(Nil,Nil,Nil,Nil,Nil,Nil,Nil,If(Type('dEmis260')=="D",dEmis260,dDataBase)) .And. !lEstNeg
		Help(" ",1,"MA240NEGAT")
		lRet := .F.
	EndIf
	If lRet .And. (!Empty(cLocaliza) .Or. !Empty(cNumSerie)) .And. Localiza(cProduto) .And. SaldoSBF(cLocOrig,cLocaliza,cProduto,cNumSerie,cLoteDigi,cNumLote) < nQuant
		Help(" ",1,"SALDOLOCLZ")
		lRet:=.F.
	EndIf
	//-- Valida Saldo em Estoque ref. a Rastreabilidade
	If lRet .And. Rastro(cProduto)
		If QtdComp(SaldoLote(cProduto,cLocOrig, cLoteDigi,cNumLote,nil,nil,nil,dEmis260)) < QtdComp(nQuant)
			Help(' ', 1, 'A240NEGAT')
			lRet := .F.
		EndIf
	EndIf
	If lRet .And. SB2->(ColumnPos("B2_QTSEGUM")) > 0
		dbSelectArea("SB1")
		dbSetOrder(1)
		MsSeek(xFilial("SB1")+cProduto+cLocOrig)
		nQtdOrigSe:=ConvUm(B1_COD,nQuant,nQtdOrigSe,2)
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrder)
MsGoto(nRecno)
Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242PriUm ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 07/04/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula e inicializa a primeira unidade de medida          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242PriUm()
Local lRet:=.T.
Local cAlias:=Alias(),nOrder:=IndexOrd(),nRecno:=Recno()

If !Empty(nQtdOrigSe)
	dbSelectArea("SB1")
	MsSeek(xFilial("SB1")+cProduto)
	nQtdOrig := ConvUm(B1_COD,nQtdOrig,nQtdOrigSe,1)
	If GetMv("MV_ESTNEG") == "N" .And. !IsProdMod(cProduto) .And.	!Empty(nQtdOrig)
		dbSelectArea("SB2")
		If MsSeek(xFilial("SB2")+cProduto+cLocOrig)
			If (SaldoMov(Nil,Nil,Nil,Nil,Nil,Nil,Nil,If(Type('dEmis260') == "D",dEmis260,dDataBase))-nQtdOrig) < 0
				Help(" ",1,"MA240NEGAT")
				lRet:=.F.
			EndIf
		EndIf
	EndIf
	dbSelectArea(cAlias)
	dbSetOrder(nOrder)
	MsGoto(nRecno)
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTA242PERG³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 02/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao PERGUNTE                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA242PERG()
Pergunte("MTA260",.T.)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ShowF4   ³ Autor ³ Fernando Joly Siquini ³ Data ³ 13/04/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao F4                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ShowF4(a,b,c)
Local nPosLin 	:= 1
Local cNumSerAnt := ""
Local cLocaliAnt := ""
Local cVar	:= Upper(ReadVar())

If !(cVar $ 'CNUMLOTEúCLOTEDIGI*CLOCALIZA*CPRODUTOúNQTDORIG') .And. !((cVar $ 'M->D3_LOCALIZ*M->D3_LOTECTL') .And. Type('oGet')=='O')
	Return Nil
EndIf

// Armazena os dados originais do produto PAI a ser desmontado
cNumSerAnt := cNumSerie
cLocaliAnt := cLocaliza

If cVar $ 'CNUMLOTEúCLOTEDIGI'
	F4Lote(,,,   'A242',cProduto,cLocOrig,NIL,cLocaliza)
ElseIf cVar == 'M->D3_LOTECTL' .And. Type('oGet')=='O'
	F4Lote(,,,   'A242',Eval(bCols,n,nPosCod), Eval(bCols,n,nPosLocal),NIL, Eval(bCols,n,nPosLocali))
ElseIf cVar $ 'CPRODUTOúNQTDORIG'
	MaViewSB2(cProduto)
ElseIf cVar == 'CLOCALIZA'
	F4Localiz(,,,   'A242', cProduto, cLocOrig,, ReadVar())
	If Type("dDtValid2")=="D"
		dDtValid := dDtValid2
	EndIf
ElseIf cVar == 'M->D3_LOCALIZ' .And. Type('oGet')=='O'
	nPosLin := oGet:oBrowse:nAt
	F4Localiz(,,,   'A242C', aCols[nPosLin,nPosCod], aCols[nPosLin,nPosLocal],, ReadVar())
EndIf

If !Empty(cNumSerAnt)
	cNumSerie := cNumSerAnt  // Restaura num.serie do produto PAI
EndIf

If !Empty(cLocaliAnt)
	cLocaliza := cLocaliAnt  // Restaura o endereco do produto PAI
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A242Lote   ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 10/12/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Inicializa‡„o dos campos de Lote Proprio                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 =  1 - controle por lote                              ³±±
±±³          ³          2 - controle por sublote                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A242Lote(nx)
Local cAlias:=Alias(),nOrder:=IndexOrd(),nRecno:=Recno()
Local lRet:=.T.,cSeek:=""
Local lEmpPrev := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

lRet:=NaoVazio(If(nx==1,cLoteDigi,cNumLote))
If lRet
	dbSelectArea("SB8")
	If nx == 2
		dbSetOrder(2)
		cSeek:=xFilial("SB8")+cNumLote+cLoteDigi+cProduto+cLocOrig
	Else
		dbSetOrder(3)
		cSeek:=xFilial("SB8")+cProduto+cLocOrig+cLoteDigi
	EndIf
	lRet:=NaoVazio(If(nx==1,cLoteDigi,cNumLote))
	If dbSeek(cSeek)
		If If(Rastro(cProduto,"S"),SB8Saldo(Nil,Nil,Nil,Nil,Nil,lEmpPrev),SaldoLote(cProduto,cLocOrig,cLoteDigi,nil,nil,nil,nil,dEmis260)) >= nQtdOrig
			cLoteDigi:=SB8->B8_LOTECTL
			dDtValid :=SB8->B8_DTVALID
			nPotencia:=SB8->B8_POTENCI
		Else
			Help(" ",1,"A240LOTENE")
			lRet:=.F.
		EndIf
	Else
		Help(" ",1,"A240LOTERR")
		lRet:=.F.
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrder)
MsGoto(nRecno)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A242ValEst³ Autor ³ Larson Zordan         ³ Data ³ 04/12/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para validar o saldo em estoque para desmontagem    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A242ValEst()
Local aAreaAnt := GetArea()
Local cLocal   := ""
Local cSeek    := ""
Local lRet     := .T.
Local lEstNeg  := GetMV("MV_ESTNEG")=="N"
Local nSaldo   := 0
Local nQtd     := 0
Local nOrdSB8  := 0
Local nRecSB8  := 0
Local nx, i
Local cLocaliz := ""
Local cNumSer  := ""
Local lEmpPrev := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lTypedEmis
Local lWmsNew    := SuperGetMv("MV_WMSNEW",.F.,.F.)

_lWmsVlDesm := iIf(_lWmsVlDesm == NIL,FindFunction("WmsVlDesm"),_lWmsVlDesm)

lTypedEmis:= Type('dEmis260') == "D"

For nx := 1 To Len(aCols)
	//--> Verifica o Saldo do Produto
	If lEstNeg
		dbSelectArea("SB2")
		MsSeek(xFilial("SB2")+aCols[nx,nPosCod]+aCols[nx,nPosLocal])
		If QtdComp(aCols[nx,nPosQuant]) > QtdComp(SaldoMov(Nil,Nil,Nil,Nil,Nil,Nil,Nil,If(lTypedEmis,dEmis260,dDataBase)))
			Help(" ",1,"MA240NEGAT",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],4,0)
			lRet := .F.
		EndIf
	EndIf
	//--> Verifica o saldo no Endereco
	If lRet .And. Localiza(aCols[nx,nPosCod])
		If Empty(nPosLocali) .And. Empty(nPosNumSer)
			UserException(OemToAnsi(STR0025))
		EndIf
		cLocaliz := aCols[nx,nPosLocali]
		cNumSer  := IIf(nPosNumSer > 0, aCols[nx,nPosNumSer], CriaVar('D3_NUMSERI'))
		If (!Empty(cLocaliz) .Or. !Empty(cNumSer)) .And. QtdComp(SaldoSBF(aCols[nx,nPosLocal],cLocaliz,aCols[nx,nPosCod],cNumSer,aCols[nx,nPosLotCTL],aCols[nx,nPosLote],.F.)) < QtdComp(aCols[nx,nPosQuant])
			Help(" ",1,"SALDOLOCLZ",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],5,0)
			lRet:=.F.
		EndIf
		//-- Todas as ordens de servico do WMS nao executadas para esta producao, serao travadas e estornadas na funcao A250DesAtu
		//-- As ordens de servico ja executadas deverao ser estornadas pela rotina de execucao de servicos do WMS
		If	lRet .And. IntDL(GDFieldGet("D3_COD",nx)) .And. !Empty(GDFieldGet("D3_SERVIC",nx))
			If WmsChkDCF('SD3',,,GDFieldGet("D3_SERVIC",nx),'3',,cDocumento,,,,GDFieldGet("D3_LOCAL",nx),GDFieldGet("D3_COD",nx),,,cSeqEst)
				lRet := WmsAvalDCF('2')
			EndIf
		EndIf
	EndIf
	//Validação de saldo de estoque WMS
	If lRet .And. lWmsnew .AND. _lWmsVlDesm .AND. IntWMS(aCols[nx,nPosCod]) 
		If !WmsVlDesm("2",aCols[nx,nPosLocal],IIf(nPosLocali > 0,aCols[nx,nPosLocali]," "),aCols[nx,nPosCod],aCols[nx,nPosLotCTL],aCols[nx,nPosLote],aCols[nx,nPosQuant],IIf(nPosServic > 0,aCols[n,nPosServic]," "))
			lRet := .F. 
		EndIf 		
    EndIf
	//--> Verifica o saldo do Lote
	If lRet .And. Rastro(aCols[nx,nPosCod]) .And. !Empty(aCols[nx,nPosLote]+aCols[nx,nPosLotCTL])
		If !(IntDL(GDFieldGet("D3_COD",nx)) .And. !Empty(GDFieldGet("D3_SERVIC",nx)))
			dbSelectArea("SB8")
			nOrdSB8 := IndexOrd()
			nRecSB8 := RecNo()
			cLocal  := aCols[nx,nPosLocal]
			If lRet .And. Rastro(aCols[nx,nPosCod],"S")
				dbSetOrder(2)
				If dbSeek(xFilial("SB8")+aCols[nx,nPosLote]+aCols[nx,nPosLotCTL]+aCols[nx,nPosCod]+cLocal)
					nQtd := 0
					For i:=1 to Len(aCols)
						If aCols[i,nPosLotCTL]+aCols[i,nPosLote]+aCols[i,nPosCod] == aCols[nx,nPosLotCTL]+aCols[nx,nPosLote]+aCols[nx,nPosCod]
							If aCols[i,nPosLocal] == cLocal
								nQtd += aCols[i,nPosQuant]
							EndIf
						EndIf
					Next i
					If QtdComp(SB8Saldo(nil,nil,nil,nil,nil,lEmpPrev,nil,dEmis260)) < QtdComp(nQtd)
						Help(" ",1,"A240LOTENE",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],4,0)
						lRet := .f.
					EndIf
				Else
					Help(" ",1,"A240LOTERR",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],5,0)
					lRet := .f.
				EndIf
			Else
				dbSetOrder(3)
				cSeek:=xFilial("SB8")+aCols[nx,nPosCod]+cLocal+aCols[nx,nPosLotCTL]
				If dbSeek(cSeek)
					nQtd := 0
					For i:=If(ValType(nx)=="N",nx,1) to Len(aCols)
						If aCols[i,nPosLotCTL]+aCols[i,nPosCod] == aCols[nx,nPosLotCTL]+aCols[nx,nPosCod]
							If aCols[i,nPosLocal] == cLocal
								nQtd += aCols[i,nPosQuant]
							EndIf
						EndIf
					Next i
					nSaldo:=SaldoLote(aCols[nx,nPosCod],cLocal,aCols[nx,nPosLotCTL],nil,nil,nil,nil,dEmis260)
					If QtdComp(nSaldo) < QtdComp(nQtd)
						Help(" ",1,"A240LOTENE",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],4,0)
						lRet := .f.
					EndIf
				Else
					Help(" ",1,"A240LOTERR",,STR0019+aCols[nx,nPosCod]+"  "+STR0017+aCols[nx,nPosLocal],5,0)
					lRet := .f.
				EndIf
			EndIf
		EndIf
	EndIf
Next nX

If lRet .And. ExistBlock("MT242PRE")
	lRet:=ExecBlock("MT242PRE",.F.,.F.)
	If Valtype(lRet) # "L"
		lRet:=.T.
	EndIf
EndIf

RestArea(aAreaAnt)
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A242Estru  ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 11/11/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Explode 1o nivel da estrutura de produtos                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cProduto  Codigo do produto a ter a estrutura explodida      ³±±
±±³          ³ nQtdOrig  Quantidade base para explosao da estrutura         ³±±
±±³          ³ oGet      Objeto da get dados atualizada                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA242                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A242Estru(cProduto,nQtdOrig,oGet)
Local aEstrutura:= {}
Local aMT242CLS := {}
Local i         := 0
Local nx        := 0
Local lContinua := .T.
Local lMT242CLS := ExistBlock("MT242CLS")
Local nPosCod   := GDFieldPos("D3_COD")
Local nLinVal   := aScan(aCols,{|x| x[Len(aHeader)+1] == .F.})

Private nEstru:=0

// Verifica se ja existe informacao digitada no aCols
If lContinua .And. !Empty(nLinVal) .And. !Empty(aCols[nLinVal,nPosCod])
	// Pergunta ao usuario se deve sobrescrever informacoes digitadas
	lContinua := Aviso(STR0021,STR0022,{STR0023,STR0024},2) = 2 //"Atencao"###"Ja existem informacoes digitadas para a desmontagem. Escolha entre manter as informacoes atuais ou substituir."###"Manter"###"Substituir"
EndIf

If lContinua .And. !Empty(cProduto) .And. nQtdOrig > 0

	If FindFunction("aCusbExpld")
		aCusbExpld(cProduto,nQtdOrig,@aEstrutura) // Le estrutura do produto origem da desmontagem
	Else
		Help(,,"OUTDATEDSOURCE",,STR0032, 1, 0) // "O Fonte SIGACUSB.PRX encontra-se desatualizado."
		aEstrutura := {}
	EndIf

	// Se o produto tiver estrutura cria um novo aCols
	If Len(aEstrutura) > 0
		aCols:={}
	EndIf
	// Le somente os itens de primeiro nivel
	For i:=1 to Len(aEstrutura)
		// Adiciona item no acols
		AADD(aCols,Array(Len(aHeader)+1))
		// Preenche conteudo do acols
		For nx:=1 to Len(aHeader)
			cCampo:=Alltrim(aHeader[nx,2])
			If IsHeadRec(cCampo)
				aCols[Len(aCols)][nx] := 0
			ElseIf IsHeadAlias(cCampo)
				aCols[Len(aCols)][nx] := "SD3"
			Else
				aCols[Len(aCols)][nx] := CriaVar(cCampo,.F.)
			EndIf
		Next nx

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tratamento para marcar como deletados os itens com quantidade negativa na ³
		//³estrutura ou positiva-los, de acordo com o tipo de movimentacao RE/DE.    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If QtdComp(aEstrutura[i,4]) <= QtdComp(0)
			lItemNeg := .T. // variavel para exibicao de aviso ao usuario
			// Requisicoes
			If QtdComp(aEstrutura[i,4]) <= QtdComp(0)
				// Quantidade negativa ou zerada: deve ser deletado, pois nao se pode requisitar quantidade negativa ou zerada
				aCols[Len(aCols)][Len(aHeader)+1] := .T.
				// Devolucoes
			ElseIf QtdComp(aEstrutura[i,4]) < QtdComp(0)
				// Quantidade negativa: deve ser positivada, pois trata-se de devolucao
				aCols[Len(aCols)][Len(aHeader)+1] := .F.
				aEstrutura[i,4] := (aEstrutura[i,4] * -1)
			Else
				// Quantidade zerada: deve ser deletado, pois nao se pode devolver quantidade zerada
				aCols[Len(aCols)][Len(aHeader)+1] := .T.
			EndIf
		Else
			// Item com quantidade positiva > 0
			aCols[Len(aCols)][Len(aHeader)+1] := .F.
		EndIf

		// Preenche campos especificos
		SB1->(dbSetOrder(1))
		SB1->(MsSeek(xFilial("SB1")+aEstrutura[i,3]))
		GDFieldPut("D3_COD",aEstrutura[i,3],Len(aCols))
		GDFieldPut("D3_QUANT",aEstrutura[i,4],Len(aCols))
		GDFieldPut("D3_LOCAL",RetFldProd(SB1->B1_COD,"B1_LOCPAD"),Len(aCols))
		If X3USO(GetSX3Cache('D3_QTSEGUM', "X3_USADO"))
			GDFieldPut("D3_QTSEGUM",ConvUm(aEstrutura[i,3],aEstrutura[i,4],0,2),Len(aCols))
		EndIf
	Next i

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de Entrada que retorna os itens da Desmontagem customizados.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMT242CLS
		aMT242CLS := ExecBlock( "MT242CLS", .F., .F.,{aEstrutura})
		If ( ValType(aMT242CLS) == 'A' )
			aCols := AClone(aMT242CLS)
		EndIf
	EndIf

	oGet:ForceRefresh()

EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³04/10/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Private aRotina	:= {	{OemToAnsi(STR0002),"AxPesqui"		, 0 , 1,0,.F.},; //"Pesquisar"
						{OemToAnsi(STR0003),"A242Visual"	, 0 , 2,0,nil},; //"Visualizar"
						{OemToAnsi(STR0004),"A242Inclui"	, 0 , 3,0,nil},; //"Incluir"
						{OemToAnsi(STR0005),"a242Estorn"	, 0 , 5,0,nil},; //"Estornar"
						{OemToAnsi(STR0028),"A240Legenda"	, 0 , 2,0,.F.} } //"Legenda"
If ExistBlock("MTA242MNU")
	ExecBlock("MTA242MNU",.F.,.F.)
Endif
return(aRotina)

//-------------------------------------------------------------------
/*/{Protheus.doc}A242IniCpo
A242IniCpo Help

@author Bruno.Schmidt
@since 30/07/2015
@version P11.80
/*/
//-------------------------------------------------------------------
Function A242IniCpo()
Local lRet		:= .T.
Local lWmsNew   := SuperGetMV("MV_WMSNEW",.F.,.F.)

If lWmsNew .And. IntWMS(cProduto)
	WmsMessage(STR0031) //Este produto possui o controle WMS. Para efetuar essa operação ir para o modulo WMS Atualizações > WMS> Montagem/Desmontagem > Produto
	lRet := .F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}A242CpoUsr
Funcao responsavel por retornar os campos de usuário de um determinado Alias

@author Beraldo
@since Jan/2019
@version P12.1.17
/*/
//-------------------------------------------------------------------
Static Function A242GetCpo( xAlias, lCpoUsrOnly, lCpoVirt  )
Local aRet		:= {}
Local aAux		:= {}

Default xAlias		:= ''
Default lCpoVirt 	:= .T.
Default lCpoUsrOnly	:= .F.

aAux := ESTFwSX3Util():xGetAllFields( xAlias , lCpoVirt )
AEval( aAux , { | x | IIf( lCpoUsrOnly, IIf( GetSx3Cache( x, 'X3_PROPRI' ) == 'U', Aadd( aRet, x ) , Nil ), Aadd( aRet, x ) ) } )

Return aRet

//-------------------------------------------------------------------
/*/{Protheus.doc}A242Auto
Funcao utilizada para preenchimento da validação dos campos do cabeçalho
via rotina automática

@author Squad Entradas
@since Jul/2020
@version P12.1.27
/*/
//-------------------------------------------------------------------
Static Function A242Auto()
Local lSubLote:= .F.
Local lLote   := .F.
Local lLocaliz:= .F.
Local lMta242L:= ExistBlock("MTA242L")
Local nX      := 0
Local nY      := Len(aAutoCab)

For nX := 1 to nY
	Do Case
		Case AllTrim(aAutoCab[nX][1]) == "cProduto"
			aAutoCab[nX][3]:= "(NaoVazio() .And. Existcpo('SB1') .And. A242IniCpo() .And. If(lMta242C,ExecBlock('MTA242C',.F.,.F.,{cProduto,nQtdOrig,oGet}),.T.))"
			IIF(Rastro(aAutoCab[nX][2],"L"),lLote := .T.,)
			IIF(!lLote .And. Rastro(aAutoCab[nX][2],"S"),lSubLote := .T.,)
			IIF(Localiza(aAutoCab[nX][2]),lLocaliz := .T.,)
		Case AllTrim(aAutoCab[nX][1]) == "cLocOrig"
			aAutoCab[nX][3]:= "ExistCpo('NNR') .And. A242Local(&(ReadVar())) .And. NaoVazio()"
		Case AllTrim(aAutoCab[nX][1]) == "nQtdOrig"
			aAutoCab[nX][3]:= "(A242Quant() .And. nQtdOrig > 0 .And. IIf(lMta242Q,ExecBlock('MTA242Q',.F.,.F.,{cProduto,nQtdOrig,oGet}),.T.))"
		Case AllTrim(aAutoCab[nX][1]) == "nQtdOrigSe"
			aAutoCab[nX][3]:= "(Positivo() .And. A242PriUm() .And. IIf(lMta242S,ExecBlock('MTA242S',.F.,.F.,{cProduto,nQtdOrigSe,oGet}),.T.))"
		Case AllTrim(aAutoCab[nX][1]) == "cDocumento"
			aAutoCab[nX][3]:= "NaoVazio() .And. A240Doc() .And. VldUser('D3_DOC')"
		Case (AllTrim(aAutoCab[nX][1]) == "cNumLote" .And. lSubLote) .Or. (AllTrim(aAutoCab[nX][1]) == "cNumLote" .And. !Empty(aAutoCab[nX][2]))
			aAutoCab[nX][3]:= "A242Lote(2)"
		Case (AllTrim(aAutoCab[nX][1]) == "cLoteDigi" .And. (lLote .Or. lSubLote)) .Or. (AllTrim(aAutoCab[nX][1]) == "cLoteDigi" .And. !Empty(aAutoCab[nX][2]))
			aAutoCab[nX][3]:= "A242Lote(1)"
		Case AllTrim(aAutoCab[nX][1]) == "cLocaliza"
			If lLocaliz
				aAutoCab[nX][3]:= "Empty(&(ReadVar())) .Or. ExistCpo('SBE',cLocOrig+&(ReadVar()))"
				If lMta242L
					aAutoCab[nX][3]+= " .And. ExecBlock('MTA242L',.F.,.F.,{cProduto,nQtdOrig,oGet,cLocOrig,cLocaliza})"
				EndIf
			Else
				aAutoCab[nX][2]:= " "
			EndIf
		Case AllTrim(aAutoCab[nX][1]) == "dEmis260"
			aAutoCab[nX][3]:= "A242Data()"
	EndCase
Next

Return


/*/{Protheus.doc} VldCabec
   Valida os campos padrão obrigatorios do cabecalho da dialog
   @type  Function
   @author Reynaldo
   @since 21/12/2020
   @version 1.0
   @param nenhum
   @return lRet, logico, Verdadeiro se os campos estiver ok
   /*/
Static Function VldCabec()
Local lRastroL
Local lRastroS
Local lRet
Local cSolucao

	cSolucao := ""

   If Empty(cProduto)
		cSolucao := I18N(STR0033 ,{STR0006}) // "Verifique o #1[Lote]#" // "Produto Origem"
		lRet := .F.
	Else
		lRet := .T.
   EndIf

   If lRet
		If Empty(cLocOrig)
			cSolucao := I18N(STR0033 ,{STR0007}) // "Verifique o #1[Lote]#" // "Armazem"
			lRet := .F.
		EndIf
   EndIf

   If lRet
      lRastroL := Rastro(cProduto,"L")
      lRastroS := Rastro(cProduto,"S")
      If lRastroL .OR. lRastroS
			If Empty(cLoteDigi)
				cSolucao := I18N(STR0033 ,{STR0011}) // "Verifique o #1[Lote]#" // "Lote"
	         lRet := .F.
			EndIf
      Else
         lRet := .T.
      Endif
   EndIf

   If lRet
      If lRastroS
			If Empty(cNumLote)
				cSolucao := I18N(STR0033 ,{STR0010}) // "Verifique o #1[Lote]#" // "Sub-Lote"
	         lRet := .F.
			EndIf
      EndIF
   EndIf

   If lRet
      If nQtdOrig <=0
			cSolucao := I18N(STR0034 ,{STR0014}) // "Verifique a #1[Quantidade]#" // "Quantidade"
			lRet := .F.
		EndIf
   EndIf

   If lRet
      If nQtdOrigSe < 0
			cSolucao := I18N(STR0034 ,{STR0015}) // "Verifique a #1[Quantidade]#" // "Quantidade Secund ria"
         lRet := .F.
      EndIf
   EndIf

   If lRet
		If Empty(dEmis260)
			cSolucao := I18N(STR0034 ,{STR0009}) // "Verifique a #1[Quantidade]#" // "Data"
         lRet := .F.
      EndIf
   EndIf

   If lRet
      If Empty(cDocumento)
			cSolucao := I18N(STR0033 ,{STR0016}) // "Verifique o #1[Lote]#" // "Documento"
         lRet := .F.
		EndIf
   EndIf

   If !Empty(cSolucao) .and. !lRet
		Help(" ", 1,"MA242PR", NIL, NIL, 1, 0, NIL, NIL, NIL, NIL, NIL, {cSolucao})
   EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}a242AfterC
Funcao responsavel validar e retirar se existe "produto pai/produto 
cabeçalho" no aCols

@author Miqueias L. Coelho
@since 30/03/2023
@version v1.0
/*/
//-------------------------------------------------------------------
Static Function a242AfterC(cDocumento, cSeq)
Local lRet		:= .T.
Local cQryACols := ""
Local nPosHdRec, nPosCsRec, nRecFather := 0

Static __lLibVer  := Nil
Static oQryRecSD3 := Nil

If __lLibVer == Nil
	__lLibVer := FWLibVersion() >= "20211116"
EndIf

If oQryRecSD3 == NIL
	cQryACols := ""
	cQryACols += "SELECT SD3.R_E_C_N_O_  "
	cQryACols += "FROM " + RetSQLName("SD3") + " SD3 "
	cQryACols += "WHERE SD3.D3_FILIAL = ? "
	cQryACols += "AND SD3.D3_NUMSEQ = ? "
	cQryACols += "AND SD3.D3_CHAVE = ? "
	cQryACols += "AND SD3.D_E_L_E_T_ = ? "
	cQryACols += "AND SD3.D3_DOC = ? "
	cQryACols += "AND SD3.D3_FATHER = ? "
	
	cQryACols := ChangeQuery(cQryACols)

	If __lLibVer
	    oQryRecSD3 := FwExecStatement():New(cQryACols)
	Else
	    oQryRecSD3 := FWPreparedStatement():New(cQryACols)
	EndIf
EndIf

oQryRecSD3:SetString(1,xFilial("SD3"))
oQryRecSD3:SetString(2,cSeq)
oQryRecSD3:SetString(3,'E9')
oQryRecSD3:SetString(4,' ')
oQryRecSD3:SetString(5,cDocumento)
oQryRecSD3:SetString(6,'F')

If __lLibVer
    nRecFather := oQryRecSD3:ExecScalar('R_E_C_N_O_')
Else
    cQryACols  := oQryRecSD3:GetFixQuery()
    nRecFather := MpSysExecScalar(cQryACols, 'R_E_C_N_O_')
EndIf

nPosHdRec := GDFieldPos("D3_REC_WT")
nPosCsRec := Ascan(aCols,{|x| x[nPosHdRec] == nRecFather})

If nPosCsRec > 0
	ADEL(aCols, nPosCsRec)
	ASIZE(aCols, Len(aCols)-1)
EndIf

Return lRet

/*/{Protheus.doc} A242When
	Verifica se o campo do cabeçalho pode ser editado
	conforme configurado no dicionário de dados
	@type  Static Function
	@author Gianluca Moreira
	@since 25/05/2023
/*/
Static Function A242When(cCampo)
	Local lEdit   := .T.
	Local cVisual := GetSX3Cache(cCampo, 'X3_VISUAL')
	Local cWhen   := GetSX3Cache(cCampo, 'X3_WHEN')

	lEdit := cVisual != 'V' .And. &cWhen

Return lEdit

/*/{Protheus.doc} A242NumSer
	Validacao de saldo no endereço e numero de serie do cabeçalho
	@type  Function
	@author Squad.Entradas
	@since 04/08/2024
	@return lRet, logical, lRet
	/*/
Static Function A242NumSer()
	Local lRet := .T.

	If lRet .And. nQtdOrig > 0 .And. Localiza(cProduto) .And. SaldoSBF(cLocOrig,cLocaliza,cProduto,cNumSerie,cLoteDigi,cNumLote) < nQtdOrig
		Help(" ",1,"SALDOLOCLZ")
		lRet := .F.
	EndIf

Return lRet

/*/{Protheus.doc} A242VldCmb
	Validação do conteúdo que preenche o campo D3_TRANSF
	@type  Function
	@author Squad.Entradas
	@since 29/01/2025
	@return lRet, logical, lRet
	/*/
Static Function A242VldCmb()

Local lRet := .T.

	if !(cTransf $ 'S|N')
		lRet := .F.
		Help( ,, 'A242VLDCMB',, STR0040, 1, 0,,,,,, {STR0041} ) // "Os valores de seleção definidos no campo 'Transf. K200' (D3_TRANSF) são incompatíveis com a regra estabelecida pela TOTVS." # "Solicite ao administrador do sistema que as opções do campo D3_TRANSF sejam atualizadas, considerando as opções S=Sim e N=Não."
	endIf

Return lRet
