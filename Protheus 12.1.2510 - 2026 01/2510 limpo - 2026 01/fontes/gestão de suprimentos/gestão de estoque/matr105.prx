#INCLUDE "MATR105.CH"

/*/


Ŀ
Programa   Matr105   Autor   Edson Maricate        Data 02.12.1998
Ĵ
Descrio  Relacao das Solicitacoes ao Almoxarifado  			      
Ĵ
Sintaxe    MATR105(ExpL1,ExpA1)                                       
Ĵ
Parametros ExpL1 = (DEFAULT = .T.) Se passado .F. grava conteudo das  
             perguntas do relat.em SX1, conf. prox.parametro, se array
           ExpA1 = array com conteudo das perguntas do grupo do relat.
                [1] = Data da S.A.  (alimenta faixas inicial e final) 
                [2] = Numero da S.A.(alimenta faixas inicial e final) 
Ĵ
Retorno    Nenhum		                                              
Ĵ
Uso        Generico                                                   
ٱ


/*/
Function MATR105(lMTR105,aPerg)

Local oReport

oReport := ReportDef(lMTR105,aPerg)
oReport:PrintDialog()

Return


/*/


Ŀ
Programa  ReportDef  Autor  Ricardo Berti 		 Data 24.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatorio                                  
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
 Uso       MATR105                                                    
ٱ


/*/
Static Function ReportDef(lMTR105,aPerg)

Local oReport 
Local oSection 
Local oCell         
Local cPerg	:= "MTR105"
Local nTamOp	:= (TamSx3("CP_NUM")[1])+2 
Local aRetCQ	:= {}

Private cAliasQRY := "SCP"

If !lMTR105 .And. ValType(aPerg) ==  "A"
	AjustaSX1(cPerg,aPerg)
EndIf

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
If !lMTR105 .And. ValType(aPerg) ==  "A"
	oReport := TReport():New("MATR105",STR0001,"", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003)  //"  Este relatorio lista a posicao das SA's de acordo com os para-"##"metros selecionados." 
Else
	oReport := TReport():New("MATR105",STR0001,cPerg, {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003)  //"  Este relatorio lista a posicao das SA's de acordo com os para-"##"metros selecionados." 
EndIf
If TamSX3("CP_PRODUTO")[1] > 15
	oReport:SetLandScape()
Else	
	oReport:SetPortrait()
EndIf
//Ŀ
// Verifica as Perguntas Seleciondas                                      
// mv_par01  -  Da data      ?                                            
// mv_par02  -  Ate a data   ?                                            
// mv_par03  -  Numero de    ?                                            
// mv_par04  -  Numero Ate   ?                                            
//
Pergunte(cPerg,.F.)

If !lMTR105 .And. ValType(aPerg) ==  "A"
	MV_PAR01:= aPerg[1]
	MV_PAR02:= aPerg[1]
	MV_PAR03:= aPerg[2]
	MV_PAR04:= aPerg[2]	 
EndIf
oSection := TRSection():New(oReport,STR0013,{"SCP"}) //"Solicitaes ao armazem"
oSection:SetHeaderPage()

TRCell():New(oSection,"CP_NUM","SCP",/*Titulo*/,/*Picture*/,nTamOp)
TRCell():New(oSection,"CP_ITEM","SCP",STR0014) // "Item"
TRCell():New(oSection,"CP_PRODUTO","SCP")
TRCell():New(oSection,"CP_DESCRI","SCP")
TRCell():New(oSection,"CP_EMISSAO","SCP")
TRCell():New(oSection,"CP_QUANT","SCP")

// Celula CALCULO1 somente para resolver funcao externa CA100RetCQ()
TRCell():New(oSection,"CALCULO1","",,,,,{|| aRetCQ:=ca100RetCQ((cAliasQRY)->CP_NUM,(cAliasQRY)->CP_ITEM) })

TRCell():New(oSection,"SALDO",""	,STR0009,PesqPict("SCP","CP_QUANT"),TamSx3("CP_QUANT")[1],,{|| IIF(LEN(aRetCQ)=0,aRetCQ:=ca100RetCQ((cAliasQRY)->CP_NUM,(cAliasQRY)->CP_ITEM),nil),aRetCQ[1]})
TRCell():New(oSection,"QENTREGUE","",STR0010,PesqPict("SCP","CP_QUANT"),TamSx3("CP_QUANT")[1],,{|| IIF(LEN(aRetCQ)=0,aRetCQ:=ca100RetCQ((cAliasQRY)->CP_NUM,(cAliasQRY)->CP_ITEM),nil),aRetCQ[7]})
TRCell():New(oSection,"REQUIS",""	,STR0011,PesqPict("SD3","D3_DOC")  ,TamSx3("D3_DOC")[1]  ,,{|| IIF(LEN(aRetCQ)=0,aRetCQ:=ca100RetCQ((cAliasQRY)->CP_NUM,(cAliasQRY)->CP_ITEM),nil),aRetCQ[6]})
TRCell():New(oSection,"CP_CC","SCP")
TRCell():New(oSection,"CP_SOLICIT","SCP",,"@X")

Return(oReport)


/*/


Ŀ
Programa  ReportPrin Autor  Ricardo Berti 		 Data 24.05.2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

Local oSection  := oReport:Section(1)

dbSelectArea("SCP")
dbSetOrder(1)
//Ŀ
//Filtragem do relatrio                                                  
//

//Ŀ
//Transforma parametros Range em expressao SQL                            	
//
MakeSqlExpr(oReport:uParam)
//Ŀ
//Query do relatrio da secao 1                                           
//
oReport:Section(1):BeginQuery()	

cAliasQRY := GetNextAlias()

BeginSql Alias cAliasQRY
SELECT CP_FILIAL,CP_NUM,CP_ITEM,CP_PRODUTO,CP_DESCRI,CP_EMISSAO,CP_QUANT,CP_CC,CP_SOLICIT

FROM %table:SCP% SCP

WHERE CP_FILIAL = %xFilial:SCP% AND 
	CP_NUM   >= %Exp:mv_par03% AND 
	CP_NUM   <= %Exp:mv_par04% AND 
	CP_EMISSAO >= %Exp:Dtos(mv_par01)% AND 
	CP_EMISSAO <= %Exp:Dtos(mv_par02)% AND 
	SCP.%NotDel%
ORDER BY %Order:SCP%
		
EndSql 
//Ŀ
//Metodo EndQuery ( Classe TRSection )                                    
//                                                                        
//Prepara o relatrio para executar o Embedded SQL.                       
//                                                                        
//ExpA1 : Array com os parametros do tipo Range                           
//                                                                        
//
oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)

// Necessario, devido 'a funcao externa ca100RetCQ(), que utiliza outros campos de SCP
TRPosition():New(oSection,"SCP",1,{|| xFilial("SCP")+(cAliasQRY)->CP_NUM+(cAliasQRY)->CP_ITEM })


//Ŀ
//Inicio da impressao do fluxo do relatrio                               
//

oSection:Cell("CALCULO1"):Hide()
oSection:Cell("CALCULO1"):HideHeader()

oSection:Print()

Return NIL

/*           


Ŀ
Funcao     AjustaSX1  Autor  Ricardo Berti       Data  24/05/2006 
Ĵ
Descricao  Preenche conteudo das perguntas no SX1                     
Ĵ
Sintaxe    AjustaSX1(ExpC1,ExpA1)             	                      
Ĵ
Parametros ExpC1 = Grupo do pergunte 		                          
           ExpA1 = Array com conteudo das perguntas                   
Ĵ
Retorno    Nenhum                                                     
Ĵ
Uso        MATR105                                                    
ٱ

*/
Static Function AjustaSX1(cPerg,aPerg)

	Pergunte(cPerg,.F.)

	SetMVValue(cPerg,"MV_PAR01",aPerg[1])
	SetMVValue(cPerg,"MV_PAR02",aPerg[1])
	SetMVValue(cPerg,"MV_PAR03",aPerg[2])
	SetMVValue(cPerg,"MV_PAR04",aPerg[2])

Return Nil
