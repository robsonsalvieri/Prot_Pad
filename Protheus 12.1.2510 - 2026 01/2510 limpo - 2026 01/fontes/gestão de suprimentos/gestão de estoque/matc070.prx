#INCLUDE "MATC070.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATC070  ³ Autor ³ Sergio Silveira       ³ Data ³ 17/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria consulta de produtos por lote/localizacao/bloqueio    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATC070()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cFiltro		:= "SB1->B1_RASTRO $ 'L|S' .And. RetFldProd(SB1->B1_COD,'B1_LOCALIZ') == 'S'"
Local oBrowse		:= NIL
PRIVATE cPictQtd14	:= PesqPict('SB8', 'B8_SALDO' )
PRIVATE aTotal
PRIVATE cCadastro	:= OemToAnsi(STR0003)	//"Lote X Localiza‡„o"
PRIVATE aRotina		:= MenuDef()

Set Key VK_F12 To MTC070PERG()  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oBrowse := FWmBrowse():New()
oBrowse:SetAlias('SB1')
oBrowse:SetDescription(cCadastro)
oBrowse:SetFilterDefault(cFiltro)
oBrowse:Activate()

Set Key VK_F12 To

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MC070Con ³ Autor ³ Sergio Silveira       ³ Data ³ 17/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Envia para funcao que monta o arquivo de trabalho          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MC070Con()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MC070Con()

PRIVATE aTrbP     := {}
PRIVATE aTrbT     := {}
PRIVATE aBloqueio := {}
PRIVATE aTela     := {}
PRIVATE aQuant    := { 0, 0 }
PRIVATE nTotBloq  := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava as movimentacoes no arquivo de trabalho                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SuperGetMV("MV_WMSNEW",.F.,.F.) .And. IntWMS(SB1->B1_COD)
	Processa( {|lEnd| MATC070WMS(@lEnd) }, STR0052,STR0053,.F.) //"Processando Consulta..."###"Gerando Consulta Lote x Endereco..."
Else
	Processa( {|lEnd| MC070Monta(@lEnd) }, STR0052,STR0053,.F.) //"Processando Consulta..."###"Gerando Consulta Lote x Endereco..."
EndIf

If Len(aTrbP) > 0
	MW070Brows()
Else
	Help("",1,"MC030NOREC")
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MC070Monta³ Autor ³ Sergio Silveira       ³ Data ³ 17/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava arquivo de trabalho                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MC070Monta()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function MC070Monta(lEnd)
Local bCompara
Local cChave    := ""
Local cDescri   := ""
Local cChaveB8  := ""
Local cChaveBF  := ""
Local cChaveDD  := ""
Local cChaveDC  := ""
Local cLocal    := ""
Local cLoteCtl  := ""
Local cLocaliz  := ""
Local cNumLote  := ""
Local cNumSeri  := ""
Local dDtValid  := CtoD("  /  /  ")
Local lContinua := .F.
Local lEmpPrev  := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local nSaldo    := 0
Local nEmpenho  := 0
Local nScan     := 0
Local nSaldo2   := 0
Local nEmpenh2  := 0
Local nPosLote  := 0
Local lLotUni   := SuperGetMV('MV_LOTEUNI', .F., .F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega o grupo de perguntas MTC070                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTC070",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Produto utiliza Localizacao Fisica             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Localiza( SB1->B1_COD )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o Produto utiliza Rastro                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Rastro(SB1->B1_COD)

		DbSelectArea("SB8")
		DbSetOrder(3)
		cChave := xFilial("SB8") + SB1->B1_COD
		DbSeek(cChave)
		ProcRegua(LastRec())
		While !SB8->(Eof()) .And. cChave == SB8->B8_FILIAL + SB8->B8_PRODUTO
			IncProc()
	
			If mv_par01 == 2 .And. SB8SALDO(,,,,,lEmpPrev,,,.T.) == 0
				SB8->(dbSkip())
				Loop
			EndIf
	
			cChaveB8 := xFilial("SB8") + SB8->B8_PRODUTO + SB8->B8_LOCAL + SB8->B8_LOTECTL + If(lLotUni,SB8->B8_NUMLOTE,"")
			nSaldo   := nEmpenho := nSaldo2 := nEmpenh2 := 0
			cLocal   := SB8->B8_LOCAL
			cLoteCtl := SB8->B8_LOTECTL
			dDtValid := SB8->B8_DTVALID
			cNumSeri := CriaVar("BF_NUMSERIE")
			cLocaliz := CriaVar("BF_LOCALIZ")
			cNumLote := CriaVar("BF_NUMLOTE" )
	
			While !SB8->(Eof()) .And. cChaveB8 == SB8->B8_FILIAL + ;
				SB8->B8_PRODUTO + SB8->B8_LOCAL + SB8->B8_LOTECTL  + If(lLotUni,SB8->B8_NUMLOTE,"")
				nSaldo   += SB8SALDO(,,,,,lEmpPrev,,,.T.)   
				nEmpenho += SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)
				nSaldo2  += SB8SALDO(,,,.T.,,lEmpPrev,,,.T.)
				nEmpenh2 += SB8SALDO(.T.,,,.T.,,lEmpPrev,,,.T.)
				SB8->(dbSkip())
			EndDo
	
			If mv_par01 == 1 .And. nSaldo == 0
				aAdd(aTrbp, {cLocal,cLoteCtl,cNumLote,dDtValid, ;
							cLocaliz, ;
							cNumSeri, ;
							nSaldo, ;
							nSaldo  - nEmpenho, ;
							nSaldo2, ;
							nSaldo2 - nEmpenh2} )
				Loop			
	        EndIf
			
			DbSelectArea("SBF")
			DbSetOrder(2) // BF_FILIAL+BF_PRODUTO+BF_LOCAL+BF_LOTECTL+BF_NUMLOTE
			cChaveBF := cChaveB8 //xFilial("SBF") + SB8->B8_PRODUTO + SB8->B8_LOCAL + SB8->B8_LOTECTL
			dbSeek(cChaveBF)
			ProcRegua(LastRec())
			While !SBF->(Eof()) .And. xFilial("SB8")+SBF->BF_PRODUTO+SBF->BF_LOCAL+SBF->BF_LOTECTL+If(lLotUni,SBF->BF_NUMLOTE,"") == cChaveB8
				IncProc()
			   
				If mv_par02 == 2 .And. SBFSALDO(,,lEmpPrev,,.T.) == 0
					SBF->(dbSkip())
					Loop
				EndIf
				
				nPosLote := aScan( aTrbp, {|x| x[1]+x[2]+x[3]+DtoS(x[4])+x[5]+x[6] == SBF->BF_LOCAL+SBF->BF_LOTECTL+SBF->BF_NUMLOTE+DtoS(dDtValid)+SBF->BF_LOCALIZ+SBF->BF_NUMSERI})
				
				If nPosLote == 0
					aAdd( aTrbp,{SBF->BF_LOCAL,SBF->BF_LOTECTL,SBF->BF_NUMLOTE,dDtValid,;
								 SBF->BF_LOCALIZ, ;
								 SBF->BF_NUMSERI, ;
								 SBF->BF_QUANT, ;
								 SBF->BF_QUANT   - SBF->BF_EMPENHO, ;
								 SBF->BF_QTSEGUM, ;
								 SBF->BF_QTSEGUM - SBF->BF_EMPEN2} )
					
					aQuant[ 1 ] += SBF->BF_QUANT
					aQuant[ 2 ] += SBF->BF_EMPENHO
					
					cChaveDC := xFilial("SDC") +  SBF->BF_PRODUTO + ;
					SBF->BF_LOCAL  + SBF->BF_LOTECTL + SBF->BF_NUMLOTE + SBF->BF_LOCALIZ + SBF->BF_NUMSERI + "SDD"
				
					SDC->(DBSetOrder(3))
					SDC->(DbSeek(cChaveDC))
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Percorre o arquivo de composicao de empenho ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
					While !SDC->(Eof()) .And. cChaveDC==SDC->DC_FILIAL + SDC->DC_PRODUTO+;
						SDC->DC_LOCAL + SDC->DC_LOTECTL + SDC->DC_NUMLOTE + SDC->DC_LOCALIZ +;
						SDC->DC_NUMSERI+"SDD"
				
						cChaveDD := xFilial("SDD") + PadR(SDC->DC_PEDIDO,Len(SDD->DD_DOC)) + SDC->DC_PRODUTO + ;
						SDC->DC_LOCAL + SDC->DC_LOTECTL + SDC->DC_NUMLOTE
				
						SDD->(DbSetOrder(1))
				
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica a existencia de bloqueios ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
						If	SDD->(DbSeek(cChaveDD))
				
							If SDD->DD_SALDO <= 0
								SDC->(dbSkip())
								Loop
							EndIf
							cDescri := ""
							SX5->(DbSetOrder(1))
							If SX5->( DbSeek( xFilial("SX5") + "E1" + SDD->DD_MOTIVO ) )
								cDescri := X5Descri()
							EndIf
							aAdd(aBloqueio,{SDD->DD_LOTECTL,SDD->DD_NUMLOTE,SDD->DD_DOC,SDD->DD_MOTIVO,cDescri,SDD->DD_OBSERVA,Transform( SDC->DC_QUANT, cPictQtd14 ),SDD->DD_LOCAL,SDC->DC_LOCALIZ , SDC->DC_NUMSERI})
							nScan := aScan( aTrbT, { |x| x[ 1 ] == SDD->DD_MOTIVO } )
							If nScan <> 0
								aTrbT[ nScan, 3 ] += SDC->DC_QUANT
							Else
								AAdd( aTrbT, { SDD->DD_MOTIVO, Alltrim(cDescri), SDC->DC_QUANT } )
							EndIf
							nTotBloq += SDC->DC_QUANT
		
						EndIf
						SDC->(DbSkip())
					EndDo
				EndIf
				SBF->(dbSkip())
			EndDo
		EndDo
    
    Else // Produto utiliza apenas controle de localizacao fisica

		DbSelectArea("SB8")
		DbSetOrder(3)
		DbSelectArea("SBF")
		DbSetOrder(2)
		cChave := xFilial("SBF") + SB1->B1_COD
		DbSeek(cChave)
		ProcRegua(LastRec())
		While !SBF->(Eof()) .And. cChave == SBF->BF_FILIAL + SBF->BF_PRODUTO
			IncProc()
			If !SB8->(dbSeek(xFilial("SB8")+SBF->BF_PRODUTO+SBF->BF_LOCAL+SBF->BF_LOTECTL+If(!Empty(SBF->BF_NUMLOTE),SBF->BF_NUMLOTE,"")))
				dDtValid := CriaVar("B8_DTVALID")
			EndIf
			lContinua := .T.
			While !SB8->(Eof()) .And. xFilial("SB8")+SBF->BF_PRODUTO+SBF->BF_LOCAL+SBF->BF_LOTECTL+If(!Empty(SBF->BF_NUMLOTE),SBF->BF_NUMLOTE,"") ;
					    		   == SB8->B8_FILIAL+SB8->B8_PRODUTO+SB8->B8_LOCAL+SB8->B8_LOTECTL+If(!Empty(SBF->BF_NUMLOTE),SB8->B8_NUMLOTE,"")
				If mv_par01 == 2 .And. SB8SALDO(,,,,,lEmpPrev,,,.T.) == 0
					lContinua := .F.
					SB8->( dbSkip() )
					Loop
				EndIf
				dDtValid  := SB8->B8_DTVALID
				lContinua := .T.
				Exit
			EndDo
	
			If lContinua
		
				aAdd( aTrbp,{SBF->BF_LOCAL,SBF->BF_LOTECTL,SBF->BF_NUMLOTE,dDtValid,;
							 SBF->BF_LOCALIZ, ;
							 SBF->BF_NUMSERI, ;
							 SBF->BF_QUANT, ;
							 SBF->BF_QUANT   - SBF->BF_EMPENHO, ;
							 SBF->BF_QTSEGUM, ;
							 SBF->BF_QTSEGUM - SBF->BF_EMPEN2} )
		
				aQuant[ 1 ] += SBF->BF_QUANT
				aQuant[ 2 ] += SBF->BF_EMPENHO
		
				cChaveDC := xFilial("SDC") +  SBF->BF_PRODUTO + ;
				SBF->BF_LOCAL  + SBF->BF_LOTECTL + SBF->BF_NUMLOTE + SBF->BF_LOCALIZ + SBF->BF_NUMSERI + "SDD"
		
				SDC->(DBSetOrder(3))
				SDC->(DbSeek(cChaveDC))
		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Percorre o arquivo de composicao de empenho ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
				While !SDC->(Eof()) .And. cChaveDC==SDC->DC_FILIAL + SDC->DC_PRODUTO+;
					SDC->DC_LOCAL + SDC->DC_LOTECTL + SDC->DC_NUMLOTE + SDC->DC_LOCALIZ +;
					SDC->DC_NUMSERI+"SDD"
		
					cChaveDD := xFilial("SDD") + SDC->DC_PEDIDO + SDC->DC_PRODUTO + ;
					SDC->DC_LOCAL + SDC->DC_LOTECTL + SDC->DC_NUMLOTE
		
					SDD->(DbSetOrder(1))
		
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica a existencia de bloqueios ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
					If	SDD->(DbSeek(cChaveDD))
		
						If SDD->DD_SALDO <= 0
							SDC->(dbSkip())
							Loop
						EndIf
						cDescri := ""
						SX5->(DbSetOrder(1))
						If SX5->( DbSeek( xFilial("SX5") + "E1" + SDD->DD_MOTIVO ) )
							cDescri := X5Descri()
						EndIf
						aAdd(aBloqueio,{SDD->DD_LOTECTL,SDD->DD_NUMLOTE,SDD->DD_DOC,SDD->DD_MOTIVO,cDescri,SDD->DD_OBSERVA,Transform( SDC->DC_QUANT, cPictQtd14 ),SDD->DD_LOCAL,SDC->DC_LOCALIZ , SDC->DC_NUMSERI})
						nScan := aScan( aTrbT, { |x| x[ 1 ] == SDD->DD_MOTIVO } )
						If nScan <> 0
							aTrbT[ nScan, 3 ] += SDC->DC_QUANT
						Else
							AAdd( aTrbT, { SDD->DD_MOTIVO, Alltrim(cDescri), SDC->DC_QUANT } )
						EndIf
						nTotBloq += SDC->DC_QUANT
		
					EndIf
					SDC->(DbSkip())
				EndDo
	
		    EndIf
	
			SBF->(dbSkip())
		EndDo
		
    EndIf
    
Else

	DbSelectArea("SB8")
	DbSetOrder(3)
	cChave := xFilial("SB8") + SB1->B1_COD
	DbSeek(cChave)
	ProcRegua(LastRec())
	While !SB8->(Eof()) .And. cChave == SB8->B8_FILIAL + SB8->B8_PRODUTO
		IncProc()

		If mv_par01 == 2 .And. SB8SALDO(,,,,,lEmpPrev,,,.T.) == 0
			SB8->(dbSkip())
			Loop
		EndIf

		If Rastro( SB1->B1_COD, "S" )

			aAdd(aTrbp, {SB8->B8_LOCAL,SB8->B8_LOTECTL,SB8->B8_NUMLOTE,SB8->B8_DTVALID, ;
						"", ;
						"", ;
						SB8SALDO(,,,,,lEmpPrev,,,.T.), ;
						SB8SALDO(,,,,,lEmpPrev,,,.T.)	  - SB8SALDO(.T.,,,,,lEmpPrev,,,.T.), ;
						SB8SALDO(,,,.T.,,lEmpPrev,,,.T.), ;
						SB8SALDO(,,,.T.,,lEmpPrev,,,.T.) - SB8SALDO(.T.,,,.T.,,lEmpPrev,,,.T.)} )

			aQuant[ 1 ] += SB8SALDO(,,,,,lEmpPrev,,,.T.)
			aQuant[ 2 ] += SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)

			cChaveDD :=  xFilial("SDD") + SB8->B8_PRODUTO + SB8->B8_LOCAL + ;
			SB8->B8_LOTECTL + SB8->B8_NUMLOTE

			bCompara := { ||  SDD->DD_FILIAL + SDD->DD_PRODUTO + SDD->DD_LOCAL+;
			SDD->DD_LOTECTL + SDD->DD_NUMLOTE }
			
			SB8->(DbSkip())

		Else

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se utilizar rastreabilidade lote ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			cChaveB8 := xFilial("SB8") + SB8->B8_PRODUTO + SB8->B8_LOCAL + SB8->B8_LOTECTL
			nSaldo   := nEmpenho := nSaldo2 := nEmpenh2 := 0
			cLocal   := SB8->B8_LOCAL
			cLoteCtl := SB8->B8_LOTECTL
			dDtValid := SB8->B8_DTVALID
			cNumLote := CriaVar( "DD_NUMLOTE" )
			cNumSeri := CriaVar( "DD_NUMSERI" )

			While !SB8->(Eof()) .And. cChaveB8 == SB8->B8_FILIAL + ;
				SB8->B8_PRODUTO + SB8->B8_LOCAL + SB8->B8_LOTECTL
				nSaldo   += SB8SALDO(,,,,,lEmpPrev,,,.T.)   
				nEmpenho += SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)
				nSaldo2  += SB8SALDO(,,,.T.,,lEmpPrev,,,.T.)
				nEmpenh2 += SB8SALDO(.T.,,,.T.,,lEmpPrev,,,.T.)
				SB8->(dbSkip())
			EndDo

			aAdd(aTrbp, {cLocal,cLoteCtl,cNumLote,dDtValid, ;
						"", ;
						"", ;
						nSaldo, ;
						nSaldo  - nEmpenho, ;
						nSaldo2, ;
						nSaldo2 - nEmpenh2} )
			aQuant[ 1 ] += nSaldo
			aQuant[ 2 ] += nEmpenho

			cChaveDD := xFilial("SDD") + SB1->B1_COD + cLocal + cLoteCtl

			bCompara := { ||  SDD->DD_FILIAL + SDD->DD_PRODUTO + SDD->DD_LOCAL+;
			SDD->DD_LOTECTL }
			
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pesquisa por bloqueios de lote   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		SDD->(DbSetOrder(2))
		SDD->(DbSeek(cChaveDD))

		While !SDD->(Eof()) .And. cChaveDD == Eval(bCompara)

			If SDD->DD_SALDO <= 0
				SDD->(dbSkip())
				Loop
			EndIf
			cDescri := ""
			SX5->(dbSetOrder(1))
			If SX5->(dbSeek( xFilial("SX5") + "E1" + SDD->DD_MOTIVO ))
				cDescri := X5Descri()
			EndIf
			aAdd(aBloqueio,{SDD->DD_LOTECTL,SDD->DD_NUMLOTE,SDD->DD_DOC,SDD->DD_MOTIVO,cDescri,SDD->DD_OBSERVA,Transform( SDD->DD_QUANT, cPictQtd14 ),SDD->DD_LOCAL,SDD->DD_LOCALIZ, SDD->DD_NUMSERI } )
			nScan := aScan( aTrbT, { |x| x[ 1 ] == SDD->DD_MOTIVO } )
			If nScan <> 0
				aTrbT[ nScan, 3 ] += SDD->DD_QUANT
			Else
				AAdd( aTrbT, { SDD->DD_MOTIVO, Alltrim(cDescri), SDD->DD_QUANT } )
			EndIf
			nTotBloq += SDD->DD_QUANT
			SDD->(dbSkip())
		EndDo

	EndDo

EndIf

aTotal := aClone(aQuant)

Return(aQuant)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MW070Brows³ Autor ³ Patricia A. Salomao   ³ Data ³ 27.07.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta o Browse para o Windows                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATC070                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MW070Brows()

LOCAL oDlg, oQual, nOAT
LOCAL oFont     := TFont():New( "Mono AS", 6, 15 )
LOCAL cCadastro := OemtoAnsi( STR0012 )	//"Consulta Lote x Endereco"
LOCAL nReg,nX
LOCAL aObjects  := {},aPosObj  :={}
LOCAL aSize     := MsAdvSize()
LOCAL aInfo     := {aSize[1],aSize[2],aSize[3],aSize[4],3,3}     
LOCAL aNew      := {}
LoCAL nLoop     := 0
LOCAL nTop      := oMainWnd:nTop+23
LOCAL nBottom   := oMainWnd:nBottom-60
Local aCab      := {}
Local lPrdPai   := SuperGetMV("MV_WMSNEW",.F.,.F.) .And. IntWMS(SB1->B1_COD) .And. WmsPrdPai(SB1->B1_COD)
Local nP        := 1

For nX := 1 to len(atrbp)
	aAdd(aTela, {})
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Local
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Lote
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //SubLote
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Validade
	If lPrdPai
		aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Produto Componente
	EndIf
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Localizacao
	aAdd(aTela[Len(aTela)], aTrbP[Len(aTela), nP++])  //Numero de Serie
	aAdd(aTela[Len(aTela)], Transform(aTrbP[Len(aTela), nP++],cPictQtd14))  //Quantidade
	aAdd(aTela[Len(aTela)], Transform(aTrbP[Len(aTela), nP++],cPictQtd14))  //Quantidade Disponivel
	aAdd(aTela[Len(aTela)], Transform(aTrbP[Len(aTela), nP++],cPictQtd14))  //Quantidade 2a.UM
	aAdd(aTela[Len(aTela)], Transform(aTrbP[Len(aTela), nP++],cPictQtd14))  //Quantidade Disponivel 2a.UM
	nP := 1
Next nX

If (ExistBlock( "MC070CPO" ))
	aNew := ExecBlock("MC070CPO", .f., .f.,{aTela})
	If ValType(aNew) == "A"
		aTela := aNew
	Endif
Endif

AADD(aObjects,{100,030,.T.,.F.,.F.})
AADD(aObjects,{100,100,.T.,.T.,.T.})
AADD(aObjects,{100,020,.T.,.F.,.F.})

aPosObj:=MsObjSize(aInfo,aObjects)

DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]

@ .5 , 001   Say OemToAnsi(STR0013)	//"Produto:"
@ .4,  005   Get SB1->B1_COD When .F.

@ 1.5, 001   Say OemToAnsi(STR0014)	//"Descricao:"
@ 1.4, 005   Get SB1->B1_DESC When .F.

@ 2.5, 001   Say OemToAnsi(STR0015)	//"Est. total:"
@ 2.4, 005   Get Transform( aTotal[ 1 ], cPictQtd14 ) Size 55,10 When .F. Right

@ 2.5, 012.5 Say OemToAnsi(STR0016)	//"Empenho:"
@ 2.4, 016   Get Transform( aTotal[ 2 ], cPictQtd14 ) Size 55,10 When .F. Right

@ 2.5, 023.5 Say OemToAnsi(STR0017)	//"Disponivel:"
@ 2.4, 027.3 Get Transform( aTotal[ 1 ] - aTotal[ 2 ], cPictQtd14 ) Size 55,10 When .F. Right

AAdd(aCab,OemToAnsi(STR0030)) //Local
AAdd(aCab,OemToAnsi(STR0031)) //Lote
AAdd(aCab,OemToAnsi(STR0032)) //SubLote
AAdd(aCab,OemToAnsi(STR0033)) //Validade
If lPrdPai
	AAdd(aCab,OemToAnsi("Prd. Comp.")) //Produto Componente
EndIf
AAdd(aCab,OemToAnsi(STR0036)) //Localizacao
AAdd(aCab,OemToAnsi(STR0037)) //Numero de Serie
AAdd(aCab,OemToAnsi(STR0034)) //Quantidade
AAdd(aCab,OemToAnsi(STR0035)) //Quantidade Disponivel
AAdd(aCab,OemToAnsi(STR0050)) //Quantidade 2a.UM
AAdd(aCab,OemToAnsi(STR0051)) //Quantidade Disponivel 2a.UM

cLine := "{aTela[oQual:nAT, 1], aTela[oQual:nAT, 2],aTela[oQual:nAT,3],aTela[oQual:nAT,4],aTela[oQual:nAT,5], aTela[oQual:nAT,6],aTela[oQual:nAT,7],aTela[oQual:nAT,8],aTela[oQual:nAT,9],aTela[oQual:nAT,10]"+Iif(lPrdPai,",aTela[oQual:nAT,11]","")		
														 
If (ExistBlock( "MC070CAB" ))
	aNew := ExecBlock("MC070CAB", .f., .f.)
	For nLoop := 1 To Len( aNew )
		AAdd( aCab, aNew[ nLoop ] )
		cLine += ",aTela[oQual:nAT," + AllTrim( Str( Len(aCab) ) ) + "]"
	Next nLoop
Endif

oQual := TWBrowse():New( 3.5,.8,aSize[3],((nBottom-nTop-230)/2),,aCab,,,,,,,{|nRow,nCol,nFlags|(,oQual:nAT,)},,,,,,,,,,,,,,.T. )         

cLine += " } "
bLine := &( "{ || " + cLine + " }" )

oQual:SetArray(aTela)
oQual:bLine:=bLine

@ aPosObj[3,1]+5,aPosObj[3,2]+5 Button OemToAnsi(STR0029) Action A070Pesq(oQual) Size 44,11 Of oDlg Pixel

Define SButton From aPosObj[3,1]+5,aPosObj[3,4]-150 Type  9 Enable Of oDlg Action (A070Ordem(oQual))
If !lPrdPai
	Define SButton From aPosObj[3,1]+5,aPosObj[3,4]-110 Type 15 Enable Of oDlg Action (A070Deta(aTela[oQual:nAT,2],aTela[oQual:nAT,3],aTela[oQual:nAT,1],aTela[oQual:nAT,5],aTela[oQual:nAT,6]))
Else
	Define SButton From aPosObj[3,1]+5,aPosObj[3,4]-110 Type 15 Enable Of oDlg Action (A070Deta(aTela[oQual:nAT,2],aTela[oQual:nAT,3],aTela[oQual:nAT,1],aTela[oQual:nAT,6],aTela[oQual:nAT,7]))
EndIf
Define SButton From aPosObj[3,1]+5,aPosObj[3,4]- 70 Type  1 Enable Of oDlg Action (oDlg:End())
Define SButton From aPosObj[3,1]+5,aPosObj[3,4]- 30 Type  6 Enable Of oDlg Action (A070Imp())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para criacao de botoes do usuario  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock( "MC070BUT" ))
	ExecBlock("MC070BUT",.F.,.F.,{@oDlg})
EndIf

Activate MsDialog oDlg Centered Valid (nOAT := oQual:nAT,.T.)

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A070Imp  ³ Autor ³ Sergio Silveira       ³ Data ³ 08/12/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Envia para funcao que faz a impressao da consulta.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A070Imp()
LOCAL cTitulo  := STR0018 //"LOTE X ENDERECO"
LOCAL cDesc1   := STR0019 //"Este programa ira imprimir a Consulta do Produto selecionado,"
LOCAL cDesc2   := STR0020 //"informando os saldos por lotes, localizacoes, informacoes"
LOCAL cDesc3   := STR0021 //"de bloqueio de lotes e empenhos."
LOCAL cString  := "SBF"
LOCAL wnrel    := "MATC070"
LOCAL cTamanho := "G"
LOCAL cPerg	   := Nil

PRIVATE aReturn  :={STR0022, 1,STR0023, 1, 2, 1, "", 1 }   //"Zebrado"###"Administracao"
PRIVATE nLastKey :=0

wnRel := SetPrint( cString, wnrel, cPerg, cTitulo, cDesc1, cDesc2, cDesc3, .F., "", .F. , cTamanho, , .F. )

If nLastKey <> 27

	SetDefault( aReturn, cString )

	If nLastKey <> 27

		RptStatus( { |lEnd| C070Imp( @lEnd, ctitulo, wnRel ) }, ctitulo )

	EndIf
EndIf
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ C070Imp  ³ Autor ³ Patricia A. Salomao   ³ Data ³27.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Envia para funcao que faz a impressao da consulta.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function C070Imp( lEnd, ctitulo, wnRel )

LOCAL cCabec1   := STR0012	//"Consulta Lote X Endereco"
LOCAL cCabec2   := ""
LOCAL cTamanho  := "G"
LOCAL cNomeProg := "MATC070"
LOCAL cbtxt     := Space( 10 )
LOCAL cbcont    := 0
Local nI :=1,  nAchou, nCont, cLote, cLoteCtl, cLocal, cLocaliza, cNumSeri, cVar1, cVar2
li              := 80
m_pag           := 1

cabec( cTitulo, cCabec1, cCabec2, cNomeProg, cTamanho )
li++

@ li++, 00 PSAY STR0024 + " "+SB1->B1_COD  //"Produto...:"
@ li++, 00 PSAY STR0025 + " "+SB1->B1_DESC //"Descricao.:"
@ li  , 00 PSAY STR0015 + Transform( aTotal[ 1 ], cPictQtd14 )  //"Est. total:"
@ li  , 32 PSAY STR0026 + Transform( aTotal[ 2 ], cPictQtd14 )	//"Empenho:"
@ li  , 62 PSAY STR0017 + Transform( aTotal[ 1 ] - aTotal[ 2 ], cPictQtd14 ) //"Disponivel:"

li += 2
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Lista Estoque Total p/ Situacao de Bloqueio ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If Len(aTrbT) > 0
	@ li,00 Psay STR0010 // Estoque Total por Situacao de Bloqueio :
	li+=2
	For nI:=1 to len(aTrbT)
		@li,00 Psay aTrbT[nI][1] + " - "   // Motivo Bloqueio
		@li,08 Psay Substr(aTrbT[nI][2],1,20) + " -> "  // Descricao do Motivo
		@li,28 Psay Transform(aTrbT[nI][3], cPictQtd14)   // Quantidade do Bloqueio
		li+=1
	Next nI
	li+=2
	cVar1 := Transform( aQuant[ 2 ], cPictQtd14 )
	@ li,00 Psay OemToAnsi(STR0011)  // Total Bloqueado        ->
	@ li,50 Psay cVar1
	li+=1
	@ li,00 Psay __PrtThinLine()
	li+=1
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Lista a Composicao de Lotes / Localizacoes  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

@ li,00 Psay STR0005  // Composicao de Lotes / Localizacoes / Bloqueios
li+=2
nI :=1
For nI := 1 to len(aTela)
	If li > 55
		@ ++li, 120 Psay STR0027	//"Continua......."
		cabec( cTitulo, cCabec1, cCabec2, cNomeProg,cTamanho )
	EndIf
	li+=1
	@ li, 00 Psay STR0030 //"Armz"
	@ li, 07 Psay STR0031 //"Lote"
	@ li, 22 Psay STR0032 //"Sub-Lote"
	@ li, 37 Psay STR0033 //"Validade"
	@ li, 61 Psay STR0034 //"Quantidade"
	@ li, 78 Psay STR0035 //"Qtde. Disponível"
	@ li,100 Psay STR0050 //"Quantidade 2a.UM"
	@ li,121 Psay STR0051 //"Qtde. Disp. 2a.UM"
	@ li,145 Psay STR0036 //"Endereco"
	@ li,165 Psay STR0037 //"Num. de Serie"
	li+=1
	@ li, 00 Psay aTela[nI][1]  //Local
	@ li, 07 Psay aTela[nI][2]  //Lote
	@ li, 22 Psay aTela[nI][3]  //SubLote
	@ li, 37 Psay aTela[nI][4]  //Validade
	@ li, 53 Psay aTela[nI][7]  //Quantidade
	@ li, 76 Psay aTela[nI][8]  //Quantidade DisponIvel
	@ li, 98 Psay aTela[nI][9]  //Quantidade 2a.UM
	@ li,120 Psay aTela[nI][10] //Quantidade DisponIvel 2a.UM
	@ li,145 Psay aTela[nI][5]  //Localizacao
	@ li,165 Psay aTela[nI][6]  //Numero de Serie
	li+=2

	nAchou := Ascan(aBloqueio, {|x| x[1] == aTela[nI][2] .And. x[2] == aTela[nI][3].And.;
	x[8] == aTela[nI][1] .And. x[9] == aTela[nI][5] .And. x[10] == aTela[nI][6] } )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Lista Bloqueio dos Lotes / Localizacoes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nAchou != 0
		@ li,02  Psay STR0007  // Composicao dos Bloqueios
		li+=2
		@ li,00  Psay STR0008 // "  Documento  Motivo  Descricao                          Quantidade    Observacao"
		li+=1
		cLote    := aTela[nI][2]
		cLoteCtl := aTela[nI][3]
		cLocal   := aTela[nI][1]
		cLocaliza:= aTela[nI][5]
		cNumSeri := aTela[ni][6]
		nCont    := nAchou
		Do While nCont <= len(aBloqueio) .And. aBloqueio[nCont][1] == cLote .And. aBloqueio [nCont][2] == cLoteCtl;
			.And.  aBloqueio[nCont][8] == cLocal .And. aBloqueio[nCont][9] == cLocaliza .And. aBloqueio[nCont][10] == cNumSeri

			If li > 55
				@ ++li, 120 PSAY STR0027	//"Continua......."
				cabec( cTitulo, cCabec1, cCabec2, cNomeProg,cTamanho )
			EndIf

			@ li,02 Psay aBloqueio[nCont][3] //Documento
			@ li,15 Psay aBloqueio[nCont][4] // Motivo
			@ li,21 PSay Substr(aBloqueio[nCont][5],1,30) //Descricao do Motivo
			@ li,52 Psay aBloqueio[nCont][7] // Quantidade
			@ li,70 PSay aBloqueio[nCont][6] // Observacao
			nCont +=1
			li    +=1
		EndDo
	EndIf
	@ li,00 Psay __PrtThinLine()
	li+=1
Next nI

If li != 80
	li++
	roda(cbcont,cbtxt,cTamanho)
EndIf

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	ourspool(wnrel)
EndIf
MS_FLUSH()
Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A070Deta ³ Autor ³ Patricia A. Salomao   ³ Data ³27.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Mostra Bloqueios do Produto por Lote / Sublote             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A070Deta(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5)					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - Numero do Lote                                     ³±±
±±³          ³ ExpC2 - Numero do SubLote                                  ³±±
±±³          ³ ExpC3 - Local                                              ³±±
±±³          ³ ExpC4 - Localizacao                                        ³±±
±±³          ³ ExpC5 - Numero de Serie                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function A070Deta(cLoteCtl, cNumLote, cLocal, cLocaliza, cNumSeri)
Local oDlg
Local oFolder
Local oLbx1
Local oLbx2
Local oLbx3
Local oLbx4
Local aAreaAnt:= GetArea()
Local aLbx1   := {}
Local aLbx2   := {}
Local aLbx3   := {}
Local aLbx4   := {}
Local aFolder
Local nX      := 1
Local nTotEmp := 0
Local lContinua:=.T.

//--> Monta a Consulta dos Lotes Bloqueados
For nX := 1 To Len(aBloqueio)
	If aBloqueio[nX][1] == cLoteCtl .And. aBloqueio[nX][2] == cNumLote  .And. ;
		aBloqueio[nX][8] == cLocal   .And. aBloqueio[nX][9] == cLocaliza .And.  aBloqueio[nX][10] == cNumSeri
		aAdd(aLbx1,{aBloqueio[nX,1] ,;   // Lote
					aBloqueio[nX,2] ,;   // SubLote
					aBloqueio[nX,3] ,;   // Documento
					aBloqueio[nX,4] + " - " + Left(aBloqueio[nX,5],20),;     // Motivo + Descricao
					aBloqueio[nX,7] ,;   // Quantidade
					aBloqueio[nX,6]} )   // Observacao
		nTotEmp += Val(aBloqueio[nX,7])
	EndIf
Next nX

//--> Monta Consulta dos Pedidos de Vendas (SC9)
Processa( {|lEnd| MC070Proc(@lEnd,"SC9",aLbx2,cLoteCtl,cNumLote,cLocal,cLocaliza,cNumSeri) }, STR0052,STR0054,.F.) //"Processando Consulta..."###"Consulta os Pedidos de Vendas..."

//--> Monta Consulta das Reservas Para Faturamento (SC0)
Processa( {|lEnd| MC070Proc(@lEnd,"SC0",aLbx3,cLoteCtl,cNumLote,cLocal,cLocaliza,cNumSeri) }, STR0052,STR0055,.F.) //"Processando Consulta..."###"Consulta as Reservas p/ Fat...."

//--> Monta Consulta dos Empenhos Para Ordens de Producao (SD4)
Processa( {|lEnd| MC070Proc(@lEnd,"SD4",aLbx4,cLoteCtl,cNumLote,cLocal,cLocaliza,cNumSeri) }, STR0052,STR0056,.F.) //"Processando Consulta..."###"Consulta os Empenhos p/ OPs...."

If (Len(aLbx1)+Len(aLbx2)+Len(aLbx3)+Len(aLbx4))== 0
	Help("",1,"MC030NOREC")
	lContinua := .F.
EndIf

If lContinua

	If Len(aLbx1) > 0
		aFolder := {STR0057} //"Lotes Bloqueados"
	EndIf
	If Len(aLbx2) > 0
		If aFolder == Nil
			aFolder := {STR0058} //"Pedidos de Vendas"
		Else
			aFolder := {aFolder[1],STR0058} //"Pedidos de Vendas"
		EndIf
	EndIf
	If Len(aLbx3) > 0
		If aFolder == Nil
			aFolder := {STR0059} //"Reservas p/ Fat."
		Else
			If Len(aFolder) == 1
				aFolder := {aFolder[1],STR0059} //"Reservas p/ Fat."
			Else
				aFolder := {aFolder[1],aFolder[2],STR0059} //"Reservas p/ Fat."
			EndIf
		EndIf
	EndIf
	If Len(aLbx4) > 0
		If aFolder == Nil
			aFolder := {STR0060} //"Empenhos p/ OPs"
		Else
			If Len(aFolder) == 1
				aFolder := {aFolder[1],STR0060} //"Empenhos p/ OPs"
			ElseIf Len(aFolder) == 2
				aFolder := {aFolder[1],aFolder[2],STR0060} //"Empenhos p/ OPs"
			Else
				aFolder := {aFolder[1],aFolder[2],aFolder[3],STR0060} //"Empenhos p/ OPs"
			EndIf
		EndIf
	EndIf
	
	Define MsDialog oDlg Title STR0061 From 0,0 To 28, 77 OF oMainWnd //"Visualizar Lote x Endereco"
	
	oFolder:=TFolder():New(5,5,aFolder,{},oDlg,,,, .T., .F.,295,180,)
	
	nX := 1
	//--> Consulta dos Lotes Bloqueados
	If Len(aLbx1) > 0
		@ 15, 1 Say OemToAnsi(STR0016) Of oFolder:aDialogs[nX]	//"Empenho:"
		@ 15, 5 Get Transform(nTotEmp  , cPictQtd14) When .F.	Size 58,10 Right Of oFolder:aDialogs[nX]
		@  0, 0 ListBox oLbx1 Fields HEADER OemToAnsi(STR0031),OemToAnsi(STR0032),OemToAnsi(STR0039),;
											OemToAnsi(STR0040),OemToAnsi(STR0043),OemToAnsi(STR0042) ;
											Size 293.5,167.5 Of oFolder:aDialogs[nX]
		oLbx1:SetArray(aLbx1)
		oLbx1:bLine := { ||{aLbx1[oLbx1:nAT,1],aLbx1[oLbx1:nAT,2],aLbx1[oLbx1:nAT,3],aLbx1[oLbx1:nAT,4],aLbx1[oLbx1:nAT,5],aLbx1[oLbx1:nAT,6]} }
		nX ++
	EndIf
	
	//--> Consulta dos Pedidos de Vendas (SC6)
	If Len(aLbx2) > 0
		@ 0,0 ListBox oLbx2 Fields HEADER OemToAnsi(STR0031),OemToAnsi(STR0032),OemToAnsi(STR0041),OemToAnsi(STR0043), ;
										  OemToAnsi(STR0036),OemToAnsi(STR0037) ;
										  Size 293.5,167.5 Of oFolder:aDialogs[nX]
		oLbx2:SetArray(aLbx2)
		oLbx2:bLine := { ||{aLbx2[oLbx2:nAT,1],aLbx2[oLbx2:nAT,2],aLbx2[oLbx2:nAT,3],aLbx2[oLbx2:nAT,4],aLbx2[oLbx2:nAT,5],aLbx2[oLbx2:nAT,6]} }
		nX ++
	EndIf
	
	//--> Consulta das Reservas Para Faturamento (SC0)
	If Len(aLbx3) > 0
		@ 0,0 ListBox oLbx3 Fields HEADER OemToAnsi(STR0031),OemToAnsi(STR0032),OemToAnsi(STR0048),;
											  OemToAnsi(STR0045),OemToAnsi(STR0046),OemToAnsi(STR0047),OemToAnsi(STR0043),RetTitle("C0_QTDORIG"),RetTitle("C0_QTDPED") ;
										  	  Size 293.5,167.5 Of oFolder:aDialogs[nX]
		oLbx3:SetArray(aLbx3)
		oLbx3:bLine := { ||{aLbx3[oLbx3:nAT,1],aLbx3[oLbx3:nAT,2],aLbx3[oLbx3:nAT,3],aLbx3[oLbx3:nAT,4],aLbx3[oLbx3:nAT,5],aLbx3[oLbx3:nAT,6],aLbx3[oLbx3:nAT,7],aLbx3[oLbx3:nAT,8],aLbx3[oLbx3:nAT,9]} }
		nX ++
	EndIf
	
	//--> Consulta dos Empenhos Para Ordens de Producao (SD4)
	If Len(aLbx4) > 0
		@ 0,0 ListBox oLbx4 Fields HEADER OemToAnsi(STR0031),OemToAnsi(STR0032),OemToAnsi(STR0049),;
											  OemToAnsi(STR0043),OemToAnsi(STR0036),OemToAnsi(STR0037) ;
										  	  Size 293.5,167.5 Of oFolder:aDialogs[nX]
		oLbx4:SetArray(aLbx4)
		oLbx4:bLine := { ||{aLbx4[oLbx4:nAT,1],aLbx4[oLbx4:nAT,2],aLbx4[oLbx4:nAT,3],aLbx4[oLbx4:nAT,4],aLbx4[oLbx4:nAT,5]} }
		nX ++
	EndIf
	
	Define SButton From 192,260 Type 1 Action (oDlg:End())	Enable Of oDlg
	
	Activate MsDialog oDlg Center

EndIf
RestArea(aAreaAnt)
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A070Pesq  ³ Autor ³ Patricia A. Salomao   ³ Data ³27.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Pesquisa  os Lotes, conforme opcao escolhida no ComboBox    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 - Objeto do ListBox                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. / .F.												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Matc070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A070Pesq(oQual)
Local oDlg
Local nPosicao, oCombo1, nRecno, cCombo1 := ""
Local cCampo := Criavar("BF_NUMSERI")
Local lRet   := .F.
Local aCombo1 := { Alltrim(OemToAnsi(STR0028)) }  // Numero de Serie

Define MsDialog oDlg From 5,5 To 14,50 Title OemToAnsi(STR0001)  //"Pesquisa"
@ 0.6,1.3 MsComboBox oCombo1 Var cCombo1 Items aCombo1 Size 165,44 Of oDlg FONT oDlg:oFont
@ 2.1,1.3 MsGet cCampo Size 165,10
Define SButton From 50,114 Type 1 Action (lRet:=.T.,oDlg:End()) Enable Of oDlg
Define SButton From 50,146 Type 2 Action oDlg:End() 			 Enable Of oDlg
Activate MsDialog oDlg Centered

If lRet
	nPosicao := AScan(oQual:aHeaders,Upper(cCombo1))
	If nPosicao == 0
		Help(" ",1,"PESQ01")
		lRet := .F.
	EndIf
	If lRet
		nRecno  := Ascan(aTela,{|x| x[nPosicao]==cCampo})
		If nRecno == 0
			Help(" ",1,"PESQ01")
			lRet := .F.
		Else
			oQual:nAt := nRecno
			oQual:Refresh()
		EndIf
	EndIf
EndIf
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A070Ordem ³ Autor ³ Patricia A. Salomao   ³ Data ³28.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ordena o ListBox, conforme opcao escolhida no ComboBox      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 - Objeto do ListBox                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Matc070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function A070Ordem(oQual)

Local cCombo1		:= ""
Local oCombo1, oDlg2, nPosicao
LOCAL aCombo1 :=  {}
LOCAL cTexto:="", lRet:= .T.
LOCAL nOpca:=0

DbSelectArea("SIX")
DbSeek("SBF")
While INDICE == "SBF" .And. ORDEM <= "3"
	cTexto := Capital(SixDescricao())
	AADD(aCombo1,cTexto)
	dbSkip()
EndDo

DEFINE MSDIALOG oDlg2 FROM 5, 5 TO 12, 060 TITLE OemToAnsi(STR0006)  //"Ordenar Por :"
@ .6,1.3 MSCOMBOBOX oCombo1 VAR cCombo1 ITEMS aCombo1 SIZE 170,36   OF oDlg2 FONT oDlg2:oFont
DEFINE SBUTTON FROM 09 ,185  TYPE 1 ACTION (nOpca := 1,oDlg2:End()) ENABLE OF oDlg2
DEFINE SBUTTON FROM 23 ,185  TYPE 2 ACTION oDlg2:End() ENABLE OF oDlg2
ACTIVATE MSDIALOG oDlg2 centered
If nOpca == 0
	lRet := .F.
EndIf

If lRet
	nOrdem := aScan(aCombo1,cCombo1)
	If nOrdem == 1  // Ordena por: Local + Localizacao + Num. de Serie + Lote + SubLote
		aTela :=aSort( aTela,,, { | x , y | x[1]+x[5]+x[6]+ x[2] + x[3] < y[1]+y[5]+y[6]+ y[2] + y[3] } )
	ElseIf nOrdem == 2  // Ordena por: Local + Lote + SubLote + Localizacao + Num. de Serie
		aTela :=aSort( aTela,,, { | x , y | x[1]+x[2]+x[3]+ x[5] + x[6] < y[1]+y[2]+y[3]+ y[5] + y[6] } )
	Else  // Ordena por: Numero de Serie
		aTela :=aSort( aTela,,, { | x , y | x[6] <  y[6] } )
	EndIf
	oQual:Refresh()
EndIf
Return (.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MTC070PERG³ Autor ³ Larson Zordan         ³ Data ³ 25/03/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada da funcao PERGUNTE                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTC070PERG()
Pergunte("MTC070",.T.)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MC070PROC ³ Autor ³ Larson Zordan         ³ Data ³ 06/10/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa as consultas                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MC070Proc(ExpL1,ExpC1,ExpA1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 - Variavel p/ controle de interrupcao pelo usuario   ³±±
±±³          ³ ExpC1 - Alias do arquivo                                   ³±±
±±³          ³ ExpA1 - Array com os dados a serem exibidos                ³±±
±±³          ³ ExpC2 - Numero do Lote                                     ³±±
±±³          ³ ExpC3 - Numero do SubLote                                  ³±±
±±³          ³ ExpC4 - Local                                              ³±±
±±³          ³ ExpC5 - Localizacao                                        ³±±
±±³          ³ ExpC6 - Numero de Serie                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATC070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MC070Proc(lEnd,cAlias,aDados,cLoteCtl,cNumLote,cLocal,cLocaliza,cNumSeri)
Local aAreaAnt := GetArea()
Local lBloqueio := SuperGetMV("MV_BLOQUEI",.F.,.F.)

If cAlias == "SC9" 
	dbSelectArea("SDC")
	dbSetOrder(3)
	If dbSeek(xFilial("SDC")+SB1->B1_COD+cLocal)
		ProcRegua(LastRec())
		While !Eof() .And. xFilial("SDC")+SB1->B1_COD+cLocal == DC_FILIAL+DC_PRODUTO+DC_LOCAL
			IncProc()
			If "SC6"+cLoteCtl+cNumLote+cLocaliza+cNumSeri == DC_ORIGEM+DC_LOTECTL+DC_NUMLOTE+DC_LOCALIZ+DC_NUMSERI .And. DC_QUANT > 0 .And. Empty(DC_OP)
				aAdd(aDados,{DC_LOTECTL,DC_NUMLOTE,DC_PEDIDO,Transform(DC_QUANT,cPictQtd14),DC_LOCALIZ,DC_NUMSERI})
			EndIf
			If lBloqueio .and. len(adados) == 0  
				If "SC0"+cLoteCtl+cNumLote+cLocaliza+cNumSeri == DC_ORIGEM+DC_LOTECTL+DC_NUMLOTE+DC_LOCALIZ+DC_NUMSERI .And. DC_QUANT > 0 .And. Empty(DC_OP)
					SC0->(dbSetOrder(1))					
					If SC0->(dbSeek(xFilial("SC0")+SDC->(DC_PEDIDO+DC_PRODUTO+DC_LOCAL)))
						aAdd(aDados,{SDC->DC_LOTECTL,SDC->DC_NUMLOTE,SC0->C0_DOCRES,Transform(SDC->DC_QUANT,cPictQtd14),SDC->DC_LOCALIZ,SDC->DC_NUMSERI})
					EndIf
				EndIf
			EndIf 
			dbSkip()
		EndDo
	EndIf
ElseIf cAlias == "SC0"
	dbSelectArea("SC0")
	dbSetOrder(2)
	If dbSeek(xFilial("SC0")+SB1->B1_COD+cLocal)
		ProcRegua(LastRec())
		While !Eof() .And. xFilial("SC0")+SB1->B1_COD+cLocal == C0_FILIAL+C0_PRODUTO+C0_LOCAL
			IncProc()
			If cLoteCtl+cNumLote == C0_LOTECTL+C0_NUMLOTE  .and. (QtdComp(C0_QUANT) > QtdComp(0) .or. ValSC0Emp())
				aAdd(aDados,{C0_LOTECTL,C0_NUMLOTE,C0_NUM,C0_TIPO,C0_DOCRES,Left(C0_SOLICIT,15),Transform(C0_QUANT,cPictQtd14),Transform(C0_QTDORIG,cPictQtd14),Transform(C0_QTDPED,cPictQtd14)})
			EndIf
			dbSkip()
		EndDo
	EndIf
ElseIf cAlias == "SD4"
	If !Empty(cLocaliza+cNumSeri)
		dbSelectArea("SDC")
		dbSetOrder(3)
		If dbSeek(xFilial("SDC")+SB1->B1_COD+cLocal)
			ProcRegua(LastRec())
			While !Eof() .And. xFilial("SDC")+SB1->B1_COD+cLocal == DC_FILIAL+DC_PRODUTO+DC_LOCAL
				IncProc()
				If "SC2"+cLoteCtl+cNumLote+cLocaliza+cNumSeri == DC_ORIGEM+DC_LOTECTL+DC_NUMLOTE+DC_LOCALIZ+DC_NUMSERI .And. DC_QUANT > 0 .And. !Empty(DC_OP)
					aAdd(aDados,{DC_LOTECTL,DC_NUMLOTE,DC_OP,Transform(DC_QUANT,cPictQtd14),DC_LOCALIZ,DC_NUMSERI})
				EndIf
				dbSkip()
			EndDo
		EndIf
	Else
		dbSelectArea("SD4")
		dbSetOrder(3)
		If dbSeek(xFilial("SD4")+SB1->B1_COD+cLocal)
			ProcRegua(LastRec())
			While !Eof() .And. xFilial("SD4")+SB1->B1_COD+cLocal == D4_FILIAL+D4_COD+D4_LOCAL
				IncProc()
				If cLoteCtl+cNumLote == D4_LOTECTL+D4_NUMLOTE .And. D4_QUANT > 0
					aAdd(aDados,{D4_LOTECTL,D4_NUMLOTE,D4_OP,Transform(D4_QUANT,cPictQtd14),"",""})
				EndIf
				dbSkip()
			EndDo
		EndIf
	EndIf
EndIf
RestArea(aAreaAnt)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³05/10/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/        
Static Function MenuDef()     
Private aRotina	:= {	{STR0001,"AxPesqui", 0 , 1,0,.F.},;		//"Pesquisa
					  		{STR0002,"MC070Con", 0 , 2,0,nil } }		//"Consulta"  
If ExistBlock ("MTC070MNU")					  		  
	ExecBlock ("MTC070MNU",.F.,.F.)
Endif
return (aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ValSC0Emp  ³ Autor ³ Fabio José Batista    ³ Data ³31/01/2023³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Retorno os produtos do pedido em aberto                    ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValSC0Emp()

Local cQry       := ''
Local cAlSC5     := '' 
Local lRet       := .f.

cQry := "select C5_LIBEROK,C5_NOTA,C5_BLQ from "+RetSqlName("SC5")+" SC5 "
cQry += "inner join "+RetSqlName("SC6")+" SC6 "
cQry += "on C5_FILIAL = C6_FILIAL and C5_NUM = C6_NUM and SC6.D_E_L_E_T_ = ' ' "
cQry += "and C6_PRODUTO = '"+SC0->C0_PRODUTO+"' and C6_RESERVA = '"+SC0->C0_NUM+"' and C6_LOTECTL = '"+SC0->C0_LOTECTL+"' "
cQry += "and C6_LOCALIZ = '"+SC0->C0_LOCALIZ+"' and C6_LOCAL = '"+SC0->C0_LOCAL+"' and C6_NUMLOTE = '"+SC0->C0_NUMLOTE+"' "
cQry += "and C6_NUMSERI = '"+SC0->C0_NUMSERI+"' "
cQry += "where SC5.D_E_L_E_T_ = ' ' "

cQry   := ChangeQuery(cQry)
cAlSC5 := MPSysOpenQuery(cQry)

While !(cAlSC5)->(Eof()) 
	If (Empty((cAlSC5)->C5_LIBEROK) .And. Empty((cAlSC5)->C5_NOTA) .And. Empty((cAlSC5)->C5_BLQ));
	   .or. ((cAlSC5)->C5_BLQ == '1') .or. ((cAlSC5)->C5_BLQ == '2')
		lRet := .t.
		Exit 
	EndIf 
	(cAlSC5)->(dbSkip())
EndDo  

(cAlSC5)->(DbCloseArea())

Return lRet
