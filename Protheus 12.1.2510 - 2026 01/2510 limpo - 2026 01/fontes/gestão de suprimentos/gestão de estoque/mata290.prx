#INCLUDE "MATA290.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWSCHEDULE.CH"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MATA290  ³ Autor ³ Eveli Morasco         ³ Data ³ 31/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo do lote economico                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATA290(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³  DATA  ³ BOPS ³Program.³					ALTERACAO				  ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03.08.98³17223A³Eduardo ³ Alterado o Calc.do LE para considerar o PE   ³±±
±±³22.09.98³XXXXXX³Rodrigo ³ Alterado o Calc.do LE para desconsiderar o PE³±±
±±³05.10.98³03771A³Rodrigo ³ Incluida chamada da funcao ListBoxAll        ³±±
±±³24.10.98³XXXXXX³Fabio C.³ Incluida tratamento para a Shell com IFDEF   ³±±
±±³06.11.98³15652A³Rodrigo ³ Acerto no calculo com material aprp. indireta³±±
±±³30.06.98³PROTHE³CesarVal³ Acerto no tamanho dos MSGET                  ³±±
±±³24/05/00³XXXXXX³Patricia³ Alt. A290MENU(), da tabela "03" para "SBM".  ³±±
±±³20/07/06³XXXXXX³Guilherm³ Alt. A290MENU(), inclusao var. Proc. Filiais ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡ao ³ PLANO DE MELHORIA CONTINUA        ³Programa: MATA290.PRX   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data       	|BOPS             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³ Marcos V. Ferreira       ³ 04/05/2006    |00000093380      ³±±
±±³      02  ³ Marcos V. Ferreira       ³ 20/09/2006    |00000104904      ³±±
±±³      03  ³ Ricardo Berti            ³ 05/09/2006    |                 ³±±
±±³      04  ³ Ricardo Berti            ³ 28/08/2006    |00000102229      ³±±
±±³      05  ³ Marcos V. Ferreira       ³ 20/09/2006	|00000104904      ³±±
±±³      06  ³ Ricardo Berti            ³ 02/10/2006    |00000107738      ³±±
±±³      07  ³ Ricardo Berti            ³ 02/10/2006    |00000107738      ³±±
±±³      08  ³ Marcos V. Ferreira       ³ 04/05/2006	|00000093380      ³±±
±±³      09  ³ Ricardo Berti            ³ 05/09/2006    |                 ³±±
±±³      10  ³                          ³           	|                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA290(lBat, aLista, lAutoOld, cCodIndic)

Local cPerg := "MTA290"
Local lContinua := .T.
Local lRetPE := .T.
Local lRet := .T.
Local lSchedule := .F.
Local lA290Proc := .F.
Local aPergunte := {}

Private nTotRegs := 0
Private lGrvABC  :=.T.
Private lEnd     :=.F.
Private aOpcoes

Private lMTA290Fil := ExistBlock("MTA290FIL")
Private cMTA290Fil

Default lBat := .F.
Default lAutoOld := .F. // Valida se está executando a automação no formato antigo
Default cCodIndic := "" // Código do indicador para execução via Schedule ou serviço

TCInternal(5,"*OFF")   // Desliga Refresh no Lock do Top

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        	// Armazem De                            ³
//³ mv_par02        	// Armazem Ate                           ³
//³ mv_par03        	// Produto De 	                         ³
//³ mv_par04        	// Produto Ate   	                     ³
//| mv_par05            // Consumo Med. Neg. Zerar (Sim/Nao)     |
//| mv_par06            // Considerar Mov. Inventario (Sim/Nao)  | 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)

//Indica se irá processar a nova tela de parâmetros
lA290Proc := !lAutoOld .And. A290Proc()

//Verifica se o novo serviço de execução está ativo e se o ambiente está preparado
If lA290Proc
	lSchedule := (FwIsInCallStack("WFLAUNCHER") .Or. FwIsInCallStack("FWBOSCHDEXECUTE") .Or. FWGetRunSchedule()) //Verifica se está sendo executado por Schedule
	If lSchedule
		Pergunte("MTA290B", .F.)
	Else
		Pergunte("MTA290A", .F.)
	EndIf
	If Empty(cCodIndic)
		cCodIndic := MV_PAR01 //Código do indicador informado nos parâmetros do pergunte
	EndIf
	If lBat .Or. lSchedule .Or. IsBlind()
		lRet := !Empty(cCodIndic) //Verifica se foi informado o código do indicador
		
		If lRet //Se informou o código do indicador
			aOpcoes := A290Load(cCodIndic, lBat, lSchedule, @aPergunte) //Carrega parâmetros preenchidos da nova tela
		EndIf
	Else
		cCodIndic := A290NewScr() //Retorna o código selecionado na nova tela de parâmetros
		lRet      := !Empty(cCodIndic) //Verifica se foi informado o código do indicador
		
		If lRet //Se informou o código do indicador
			aOpcoes := A290Load(cCodIndic, lBat, lSchedule, @aPergunte) //Carrega parâmetros preenchidos da nova tela
		EndIf
	EndIf
	Pergunte("MTA290", .F.) //Restaura o pergunte original
Else
	//Mantém a chamada original da tela A290Menu
	If !lBat
	    aOpcoes := A290Menu(lBat)
	Else
 	    aOpcoes := aClone(aLista)
	EndIf
EndIf

If ExistBlock("A290CONT")
	lContinua := If(ValType(lRetPE := ExecBlock("A290CONT",.F.,.F.,{aOpcoes})) == "L",lRetPE,.T.)
Endif

If !aOpcoes == NIL .And. lContinua .And. lRet

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variaveis utilizadas para desenhar cursor                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB1")
	nTotRegs := nTotRegs + RecCount()
	nTotRegs := nTotRegs + RecCount()
	
	dbSelectArea("SB3")
	nTotRegs := nTotRegs + RecCount()
	nTotRegs := nTotRegs + RecCount()
	nTotRegs := nTotRegs + RecCount()
	nTotRegs := nTotRegs + RecCount()
	nTotRegs := nTotRegs + RecCount()
	
	dbSelectArea("SD1")
	nTotRegs := nTotRegs + RecCount()
	
	dbSelectArea("SD2")
	nTotRegs := nTotRegs + RecCount()
	
	dbSelectArea("SD3")
	nTotRegs := nTotRegs + RecCount()
	
	If !lBat
		If IsBlind()
			BatchProcess(OemToAnsi(STR0001),OemToAnsi(STR0002),"MTA290",{ || Processa({|lEnd| MA290Process(aOpcoes,@lEnd,lBat,,cCodIndic,lA290Proc,@aPergunte)},OemToAnsi(STR0001),OemToAnsi(STR0002),.F.)})
	    Else
			Processa({|lEnd| MA290Process(aOpcoes,@lEnd,lBat,,cCodIndic,lA290Proc,@aPergunte)},OemToAnsi(STR0001),OemToAnsi(STR0002),.F.)
	    EndIf
	Else
		MA290Process(aOpcoes,@lEnd,lBat,,cCodIndic,lA290Proc,@aPergunte)  //"C lculo do Lote Econ“mico"###"Efetuando C lculo do Lote Econ“mico..."
	EndIf    
EndIf
Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalCon³ Autor ³ Eveli Morasco         ³ Data ³ 10/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recalcula o consumo medio do mes                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalCon()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290CalCon(oCenterPanel,lA290Proc,aPergunte)
Static l290Cons

Local cAliasSD1	:= "SD1"
Local cAliasSD2	:= "SD2"
Local cAliasSD3	:= "SD3"
Local cAliasSB1	:= "SB1"
Local cAliasSB3	:= "SB3"
Local cCod      := ""
Local cIndex	:= ""
Local cCond		:= ""
Local cCondUsr  := ""
Local cMes		:= "B3_Q"+StrZero(Month(dDataBase),2)

Local nTotCod   := 0
Local nCons		:= 0
Local nX		:= 0
Local nIndex    := 0

Local lA290CSD2 := ExistBlock("A290CSD2")
Local lMT290SD1 := ExistBlock("MT290SD1")
Local lMT290SD3 := ExistBlock("MT290SD3")

Local lM290QSD1 := ExistBlock("M290QSD1")
Local lM290QSD2 := ExistBlock("M290QSD2")
Local lM290QSD3 := ExistBlock("M290QSD3")

Local lRetPE	:= .F.
Local lConsumo  := .F.
Local lQuery	:= .F.
Local dDataIni  := Ctod("01/"+StrZero(Month(dDataBase),2,0)+"/"+StrZero(Year(dDataBase),4,0))
Local dDataFim  := LastDay(dDataBase)

Local lM290QSB1 := ExistBlock("M290QSB1")
Local cOpcoes	:= ""
Local cQuery	:= ""
Local cQueryUsr	:= ""

Local cArmDe    := ""
Local cArmAte   := ""
Local cProdDe   := ""
Local cProdAte  := ""
Local cMovInv   := ""

l290Cons := If(l290Cons==NIL,ExistBlock("A290CONS"),l290Cons)

If lA290Proc
	cArmDe   := aPergunte[1]
	cArmAte  := aPergunte[2]
	cProdDe  := aPergunte[3]
	cProdAte := aPergunte[4]
	cMovInv  := aPergunte[6]
Else
	cArmDe   := mv_par01
	cArmAte  := mv_par02
	cProdDe  := mv_par03
	cProdAte := mv_par04
	cMovInv  := mv_par06
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Zera o arquivo de consumos antes de recalcular               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")

lQuery	  := .T.
cAliasSB3 := GetNextAlias()
cAliasSB1 := cAliasSB3
cQuery := "SELECT B3_FILIAL,B3_COD,SB3.R_E_C_N_O_ RECNOSB3 "
cQuery += "FROM " + RetSqlName("SB3") + " SB3 "
cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = B3_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
cQuery += "WHERE SB3.B3_FILIAL='"+xFilial("SB3")+ "' "
cQuery += "AND SB3.D_E_L_E_T_ = ' ' "
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB3,.T.,.T.)
dbGoTop()

While !EOF() .And. IIf(lQuery,.T.,(cAliasSB3)->B3_FILIAL+(cAliasSB3)->B3_COD <= xFilial("SB3")+cProdAte)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona na tabela SB3 para gravar cMes   |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lQuery
		dbSelectArea("SB3")
		dbGoto((cAliasSB3)->RECNOSB3)
	Else
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1")+(cAliasSB3)->B3_COD)
		If !MA290Filtro()
			dbSelectArea(cAliasSB3)
			dbSkip()
			Loop
		EndIf	
		dbSelectArea("SB3")
	EndIf
	
	RecLock("SB3",.F.)
	Replace &(cMes) With 0
	MsUnlock()

	dbSelectArea(cAliasSB3)
	dbSkip()

	If !lBat
		If oCenterPanel <> NIL
			oCenterPanel:IncRegua1(OemToAnsi(STR0033))
		Else
			IncProc()	
		EndIf
	EndIf
EndDo

If lQuery
	dbSelectArea(cAliasSB3)
	dbCloseArea()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD2 -> Itens das notas fiscais de entrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD2") // itens das notas fiscais de saida
dbSetOrder(1)

cAliasSD2 := GetNextAlias()
cQuery := "SELECT D2_FILIAL,D2_COD,D2_QUANT,D2_EMISSAO,D2_REMITO,D2_TES,D2_TIPO,D2_ORIGLAN,SD2.R_E_C_N_O_ RECNOSD2 "
cQuery += "FROM "+RetSqlName("SD2")+" SD2 "

cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = D2_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "

cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+ "' "
cQuery += "AND D2_LOCAL >= '" + cArmDe + "' AND D2_LOCAL <= '" + cArmAte + "' "
cQuery += "AND D2_EMISSAO >= '" + DTOS(dDataIni)	+ "' "
cQuery += "AND D2_EMISSAO <= '" + DTOS(dDataFim)	+ "' "
cQuery += "AND D2_ORIGLAN <> 'LF' AND D2_TIPO IN ('N','B') "
cQuery += "AND D2_REMITO = '"+CriaVar("D2_REMITO",.F.)+"' "
cQuery += "AND SD2.D_E_L_E_T_=' ' "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSD2 - Ponto de Entrada para adicionar filtro na query do SD2  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSD2
	cQueryUsr := ExecBlock("M290QSD2",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += " AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf

cQuery += " ORDER BY "+SqlOrder(SD2->(IndexKey()))
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2,.T.,.T.)
dbGoTop()

TcSetField(cAliasSD2,"D2_QUANT"   ,"N", TamSx3("D2_QUANT")[1], TamSx3("D2_QUANT")[2] )
TcSetField(cAliasSD2,"D2_EMISSAO" ,"D", TamSx3("D2_EMISSAO")[1], TamSx3("D2_EMISSAO")[2] )

While !EOF()

	cCod    := D2_COD
	If !lQuery
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1")+cCod)
		If !MA290Filtro()
			dbSelectArea(cAliasSD2)
			dbSkip()
			Loop
		EndIf	
		dbSelectArea(cAliasSD2)
	EndIf	
	nTotCod := 0
	lConsumo:= .F.

	While !EOF() .And. (cAliasSD2)->D2_COD == cCod

		lConsumo:= .T.
		dbSelectArea("SF4")
		dbSeek(xFilial("SF4")+(cAliasSD2)->D2_TES)
		dbSelectArea(cAliasSD2)
		If SF4->F4_ESTOQUE == "S"

			//-- Nao permite remessa de poder de terceiros
			If SF4->F4_PODER3 == "D"
				dbSelectArea(cAliasSD2)
				dbSkip()
				Loop
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ A290CSD2 - Ponto de Entrada para filtrar a tabela SD2 (ANTIGO) ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lA290CSD2
				dbSelectArea("SD2") // posiciona o arq. para o filtro logico do usuario
				If lQuery
					dbGoto((cAliasSD2)->RECNOSD2)
				EndIf	
				If ValType(lRetPE := ExecBlock("A290CSD2",.F.,.F.,{"SD2"})) == "L" .And. !lRetPE
					dbSelectArea(cAliasSD2)
					dbSkip()
					Loop
				EndIf
				dbSelectArea(cAliasSD2)
			EndIf

			If (cAliasSD2)->D2_TES <= "500"
				nTotCod := nTotCod - (cAliasSD2)->D2_QUANT
			Else
				nTotCod := nTotCod + (cAliasSD2)->D2_QUANT
			EndIf
		EndIf
		
		dbSkip()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lBat
			If oCenterPanel <> NIL
				oCenterPanel:IncRegua1(OemToAnsi(STR0034))
			Else
				IncProc()
			EndIf
		EndIf
	EndDo
	
	If lConsumo
		dbSelectArea("SB3")
		dbSeek(xFilial("SB3")+cCod)
		If EOF()
			RecLock("SB3",.T.)
			Replace B3_FILIAL With xFilial("SB3"), B3_COD With cCod
		Else
			RecLock("SB3",.F.)
		EndIf
		Replace &(cMes) With &(cMes) + nTotCod
		MsUnlock()
	EndIf
	
	dbSelectArea(cAliasSD2)
EndDo

If !lQuery
	RetIndex("SD2")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD1 -> Itens das notas fiscais de Dev.Vdas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD1") // Itens das notas fiscais de entrada
dbSetOrder(5) //D1_FILIAL+D1_COD+D1_LOCAL

cAliasSD1 := GetNextAlias()
cQuery := "SELECT D1_FILIAL,D1_COD,D1_ORIGLAN,D1_TIPO,D1_REMITO,D1_DTDIGIT,D1_TES,D1_QUANT,SD1.R_E_C_N_O_ RECNOSD1 "
cQuery += "FROM "+RetSqlName("SD1")+" SD1 "

cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = D1_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "

cQuery += "WHERE SD1.D1_FILIAL='"+xFilial("SD1")+ "' "
cQuery += "AND D1_LOCAL >= '" + cArmDe + "' AND D1_LOCAL <= '" + cArmAte + "' "
cQuery += "AND D1_DTDIGIT >= '" + DTOS(dDataIni)	+ "' "
cQuery += "AND D1_DTDIGIT <= '" + DTOS(dDataFim)	+ "' "
cQuery += "AND D1_ORIGLAN <> 'LF' AND D1_TIPO IN ('D','B','N') "
cQuery += "AND D1_REMITO = '"+CriaVar("D1_REMITO",.F.)+"' "
cQuery += "AND SD1.D_E_L_E_T_=' ' "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSD1 - Ponto de Entrada para adicionar filtro na query do SD1  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSD1
	cQueryUsr := ExecBlock("M290QSD1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += " AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery += " ORDER BY "+SqlOrder(SD1->(IndexKey()))
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
dbGoTop()

TcSetField(cAliasSD1,"D1_QUANT"   ,"N", TamSx3("D1_QUANT")[1]  , TamSx3("D1_QUANT")[2] )
TcSetField(cAliasSD1,"D1_DTDIGIT" ,"D", TamSx3("D1_DTDIGIT")[1], TamSx3("D1_DTDIGIT")[2] )

While !EOF()

	cCod    := (cAliasSD1)->D1_COD
	If !lQuery
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1")+cCod)
		If !MA290Filtro()
			dbSelectArea(cAliasSD1)
			dbSkip()
			Loop
		EndIf	
		dbSelectArea(cAliasSD1)
	EndIf	
	nTotCod	:= 0
	lConsumo:= .F.

	While !EOF() .And. (cAliasSD1)->D1_COD == cCod
		
		lConsumo := .T.
		dbSelectArea("SF4")
		dbSeek(xFilial("SF4")+(cAliasSD1)->D1_TES)
		dbSelectArea(cAliasSD1)
		If SF4->F4_ESTOQUE == "S"
			//-- Valida se permite poder de terceiros na entrada
			If (cAliasSD1)->D1_TIPO $ 'B|N'
				If SF4->F4_PODER3 # "D"
					dbSelectArea(cAliasSD1)
					dbSkip()
					Loop
				EndIf
			EndIf	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ MT290SD1 - Ponto de Entrada para adicionar filtro na query do SD1 (ANTIGO) |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMt290SD1
				dbSelectArea("SD1")
				If lQuery
					dbGoto((cAliasSD1)->RECNOSD1)
				EndIf
				If ValType(lRetPE := ExecBlock("MT290SD1",.F.,.F.)) == "L" .And. !lRetPE
					dbSelectArea(cAliasSD1)
					dbSkip()
					Loop
				EndIf
				dbSelectArea(cAliasSD1)
			EndIf

			If (cAliasSD1)->D1_TES <= "500"
				nTotCod := nTotCod - (cAliasSD1)->D1_QUANT
			Else
				nTotCod := nTotCod + (cAliasSD1)->D1_QUANT
			EndIf

		EndIf
		
		dbSkip()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lBat
			If oCenterPanel <> NIL
				oCenterPanel:IncRegua1(OemToAnsi(STR0035))
			Else
				IncProc()
			EndIf
		EndIf

	EndDo

	If lConsumo
		dbSelectArea("SB3")
		dbSeek(xFilial("SB3")+cCod)
		If EOF()
			RecLock("SB3",.T.)
			Replace B3_FILIAL With xFilial("SB3"), B3_COD With cCod
		Else
			RecLock("SB3",.F.)
		EndIf
		Replace &(cMes) With &(cMes) + nTotCod
		MsUnlock()
	EndIf
		
	dbSelectArea(cAliasSD1)
EndDo

If !lQuery
	RetIndex("SD1")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processa o arquivo SD3 -> Movimentacoes Internas             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD3")	// Movimentacoes Internas
dbSetOrder(3)       // D3_FILIAL+D3_COD+D3_LOCAL

cAliasSD3 := GetNextAlias()
cQuery := "SELECT D3_FILIAL,D3_COD,D3_TM,D3_CF,D3_EMISSAO,D3_QUANT,SD3.R_E_C_N_O_ RECNOSD3 "
cQuery += "FROM "+RetSqlName("SD3")+" SD3 "

cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = D3_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "

cQuery += "WHERE SD3.D3_FILIAL='"+xFilial("SD3")+ "' "
cQuery += "AND D3_LOCAL >= '" + cArmDe + "' AND D3_LOCAL <= '" + cArmAte + "' "
cQuery += "AND D3_EMISSAO >= '" + DTOS(dDataIni)	+ "' "
cQuery += "AND D3_EMISSAO <= '" + DTOS(dDataFim)	+ "' "
cQuery += "AND D3_CF <> 'DE8' AND  D3_CF  <>  'RE8' "
cQuery += IIf(cMovInv == 2,"AND D3_DOC <> 'INVENT' ",'')
cQuery += "AND SD3.D_E_L_E_T_=' ' "

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ MT290SD3 - Ponto de Entrada para adicionar filtro na query do SD3  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSD3
	cQueryUsr := ExecBlock("M290QSD3",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += " AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery += " ORDER BY "+SqlOrder(SD3->(IndexKey()))
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD3,.T.,.T.)
dbGoTop()

TcSetField(cAliasSD3,"D3_QUANT"   ,"N", TamSx3("D3_QUANT")[1]  , TamSx3("D3_QUANT")[2] )
TcSetField(cAliasSD3,"D3_EMISSAO" ,"D", TamSx3("D3_EMISSAO")[1], TamSx3("D3_EMISSAO")[2] )

While !EOF() 

	cCod    := (cAliasSD3)->D3_COD
	nTotCod := 0
	lConsumo:= .F.

	While !EOF() .And. (cAliasSD3)->D3_COD == cCod

		If Subs((cAliasSD3)->D3_CF,2,1) == "E" .And. !Substr((cAliasSD3)->D3_CF,3,1) $ "3478"

			If !lQuery
				dbSelectArea("SB1")
				dbSeek(xFilial("SB1")+cCod)
				If !MA290Filtro()
					dbSelectArea(cAliasSD3)
					dbSkip()
					Loop
				EndIf	
				dbSelectArea(cAliasSD3)
			EndIf	

    		lConsumo := .T.              
	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ MT290SD3 - Ponto de Entrada para adicionar filtro no SD3 (ANTIGO)  |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMt290SD3
				dbSelectArea("SD3")
				If lQuery
					dbGoto((cAliasSD3)->RECNOSD3)
				EndIf
				If ValType(lRetPE := ExecBlock("MT290SD3",.F.,.F.)) == "L" .And. !lRetPE
					dbSelectArea(cAliasSD3)
					dbSkip()
					Loop
				EndIf
				dbSelectArea(cAliasSD3)
			EndIf
				
			If (cAliasSD3)->D3_TM <= "500"
				nTotCod := nTotCod - (cAliasSD3)->D3_QUANT
			Else
				nTotCod := nTotCod + (cAliasSD3)->D3_QUANT
			EndIf

		EndIf

		dbSkip()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Movimentacao do Cursor                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lBat
			If oCenterPanel <> NIL
				oCenterPanel:IncRegua1(OemToAnsi(STR0036))
			Else
				IncProc()
			EndIf
		EndIf
	EndDo

	If lConsumo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ A290CONS - Ponto de Entrada utilizado para alterar o consumo calculado ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If l290Cons
			nCons:=ExecBlock("A290CONS",.F.,.F.,{cCod,nTotCod})
			If ValType(nCons) == "N"
				nTotCod:=nCons
			EndIf
		EndIf
	
		dbSelectArea("SB3")
		dbSeek(xFilial("SB3")+cCod)
		If EOF()
			RecLock("SB3",.T.)
			Replace B3_FILIAL With xFilial("SB3"), B3_COD With cCod
		Else
			RecLock("SB3",.F.)
		EndIf
		Replace &(cMes) With &(cMes) + nTotCod
	
		MsUnlock()
	EndIf
		
	dbSelectArea(cAliasSD3)
EndDo
If !lQuery
	RetIndex("SD3")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve ordem principal dos arquivos                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasSD1)->(dbCloseArea())
(cAliasSD2)->(dbCloseArea())
(cAliasSD3)->(dbCloseArea())
RETINDEX("SD1")
dbClearFilter()
RETINDEX("SD2")
dbClearFilter()
RETINDEX("SD3")
dbClearFilter()            

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} A290CalGiro
Função para cálcular e registrar o giro mensal de estoque
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   24/10/2025
@version P12
@param   lA290Proc - Lógico que indica se a função foi chamada pelo novo processo
@param   aPergunte - Array com os parâmetros de filtro:
		 [1] - Produto De
		 [2] - Produto Ate
/*/
//-------------------------------------------------------------------
Static Function A290CalGiro(lA290Proc, aPergunte)
	Local cQueryGiro  As Character
	Local cQueryUsr   As Character
	Local cAliasGiro  As Character
	Local cArmDe      As Character
	Local cArmAte     As Character
	Local lM290QSB1   As Logical
	Local nSaldoIni   As Numeric
	Local nSaldoFim   As Numeric
	Local nSaldoMedio As Numeric
	Local nGiro       As Numeric
	Local nConsumoMes As Numeric
	Local oQueryGiro  As Object
	Local cMes        As Character
	Local cMesGiro    As Character
	Local dDataIni    As Date
	Local dDataFim    As Date
	Local aArea       As Array
	Local aAreaB3     As Array
	Local lCpoGiro    As Logical
	Local cQuerySaldo As Character
	Local cAliasSaldo As Character
	Local oQuerySaldo As Object

	cQueryGiro  := ""
	cQueryUsr   := ""
	cAliasGiro  := GetNextAlias()
	nSaldoIni   := 0
	nSaldoFim   := 0
	nSaldoMedio := 0
	nGiro       := 0
	nConsumoMes := 0
	oQueryGiro  := Nil
	cArmDe      := aPergunte[1]
	cArmAte     := aPergunte[2]
	lM290QSB1   := ExistBlock("M290QSB1")
	cMes        := "B3_Q" + StrZero(Month(dDataBase), 2)
	cMesGiro    := "B3_G" + StrZero(Month(dDataBase), 2)
	dDataIni    := CToD("01/" + StrZero(Month(dDataBase), 2, 0)+"/" + StrZero(Year(dDataBase), 4, 0))
	dDataFim    := LastDay(dDataBase)
	aArea       := FWGetArea()
	aAreaB3     := SB3->(FWGetArea())
	lCpoGiro    := SB3->(ColumnPos(cMesGiro)) > 0 // Verifica se o campo de giro mensal existe
	oQuerySaldo := Nil
	cQuerySaldo := ""
	cAliasSaldo := GetNextAlias()

	cQueryGiro :=    " SELECT SB3.B3_FILIAL, SB3.B3_COD, SB3.R_E_C_N_O_ RECSB3 "
	cQueryGiro +=      " FROM " + RetSqlName("SB3") + " SB3 "
	cQueryGiro +=     " INNER JOIN " + RetSqlName("SB1") + " SB1 "
	cQueryGiro +=        " ON SB1.B1_FILIAL = ? "    //#1 Filial SB1
	cQueryGiro +=       " AND SB1.B1_COD = SB3.B3_COD "
	cQueryGiro +=       " AND SB1.B1_COD >= ? "      //#2 Produto DE
	cQueryGiro +=       " AND SB1.B1_COD <= ? "      //#3 Produto ATE
	cQueryGiro +=       " AND SB1.D_E_L_E_T_ = ? "   //#4 Deleção SB1
	cQueryGiro +=     " WHERE SB3.B3_FILIAL = ? "    //#5 Filial SB3
	cQueryGiro +=       " AND SB3.D_E_L_E_T_ = ? "   //#6 Deleção SB3

	If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
		cOpcoes := Trim(aOpcoes[8][1])
		cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
		cQueryGiro += "AND SB1.B1_TIPO IN (?) " //#7 Lista de grupos
	EndIf

	If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
		cOpcoes := Trim(aOpcoes[9][1])
		cOpcoes := Substr(cOpcoes, 1, Len(cOpcoes) -1)
		cQueryGiro += "AND ?" //#8 MontaGrp caso exista
	EndIf

	//Aplica o P.E. de Filtro Adicional (M290QSB1)
	If lM290QSB1
		cQueryUsr := ExecBlock("M290QSB1", .F., .F.)
		If ValType(cQueryUsr) == "C"
			cQueryGiro += "?" //#9 Filtro adicional
		EndIf
	EndIf

	oQueryGiro := FWExecStatement():New(cQueryGiro)
	nParam := 1

	//Binding dos parâmetros na ordem
	oQueryGiro:SetString(nParam++, xFilial("SB1"))                           //#1 Filial SB1
	oQueryGiro:SetString(nParam++, aPergunte[3])                             //#2 Produto DE
	oQueryGiro:SetString(nParam++, aPergunte[4])                             //#3 Produto ATE
	oQueryGiro:SetString(nParam++, ' ')                                      //#4 Deleção SB1
	oQueryGiro:SetString(nParam++, xFilial("SB3"))                           //#5 Filial SB3
	oQueryGiro:SetString(nParam++, ' ')                                      //#6 Deleção SB3
	If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
		oQueryGiro:SetIn(nParam++, StrToKarr(cOpcoes, "|"))                  //#7 Lista de grupos
	EndIf
	If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
		oQueryGiro:SetUnsafe(nParam++, MontaGrp("SB1", "B1_GRUPO", cOpcoes)) //#8 MontaGrp caso exista
	EndIf
	If !Empty(cQueryUsr) .And. ValType(cQueryUsr) == "C"
		oQueryGiro:SetUnsafe(nParam++, cQueryUsr) //#9 Filtro adicional
	EndIf

	oQueryGiro:OpenAlias(cAliasGiro)

	DbSelectArea("SB3")
	SB3->(DbSetOrder(1)) //B3_FILIAL + B3_COD

	While (cAliasGiro)->(!Eof())
		//Zera o campo de Giro Mensal antes do cálculo
		SB3->(DbGoTo((cAliasGiro)->RECSB3))
		RecLock("SB3",.F.)
			&(cMesGiro) := 0
		SB3->(MsUnlock())
		
		//Obtém o consumo do mês (B3_QXX)
		nConsumoMes := SB3->&(cMes)

		//Zera os valores para cálculo
		nSaldoIni   := 0
		nSaldoFim   := 0
		nSaldoMedio := 0
		nGiro       := 0

		//Query na SB2 (Saldos Físico e Financeiro)
		cQuerySaldo :=    " SELECT SB2.B2_FILIAL, SB2.B2_COD, SB2.B2_LOCAL "
		cQuerySaldo +=      " FROM " + RetSqlName("SB2") + " SB2 "
		cQuerySaldo +=     " WHERE SB2.B2_FILIAL = ? "    //#1 Filial SB2
		cQuerySaldo +=       " AND SB2.D_E_L_E_T_ = ? "   //#2 Deleção SB2
		cQuerySaldo +=       " AND SB2.B2_LOCAL >= ? "    //#3 Local De
		cQuerySaldo +=       " AND SB2.B2_LOCAL <= ? "    //#4 Local Ate
		cQuerySaldo +=       " AND SB2.B2_COD = ? "       //#5 Produto SB2

		oQuerySaldo := FWExecStatement():New(cQuerySaldo)
		nParam := 1

		//Binding dos parâmetros na ordem
		oQuerySaldo:SetString(nParam++, (cAliasGiro)->B3_FILIAL) //#1 Filial SB2
		oQuerySaldo:SetString(nParam++, ' ')                     //#2 Deleção SB2
		oQuerySaldo:SetString(nParam++, cArmDe)                  //#3 Local De
		oQuerySaldo:SetString(nParam++, cArmAte)                 //#4 Local Ate
		oQuerySaldo:SetString(nParam++, (cAliasGiro)->B3_COD)    //#5 Produto SB2

		oQuerySaldo:OpenAlias(cAliasSaldo)

		While (cAliasSaldo)->(!Eof())
			//Cálculo do Saldo Inicial
			aEstoque := CalcEst((cAliasSaldo)->B2_COD, (cAliasSaldo)->B2_LOCAL, dDataIni, (cAliasGiro)->B3_FILIAL)
			nSaldoIni += aEstoque[1]
					
			//Cálculo do Saldo Final
			aEstoque := CalcEst((cAliasSaldo)->B2_COD, (cAliasSaldo)->B2_LOCAL, dDataFim, (cAliasGiro)->B3_FILIAL)
			nSaldoFim += aEstoque[1]
				
			(cAliasSaldo)->(DbSkip())
			EndDo

		(cAliasSaldo)->(DbCloseArea())
		
		//Cálculo do Saldo Médio
		nSaldoMedio := (nSaldoFim + nSaldoIni) / 2

		//Se for negativo ou zero, o saldo médio recebe 1
		If nSaldoMedio <= 0
			nSaldoMedio := 1
		EndIf

		//Cálculo do Giro Mensal
		nGiro := nConsumoMes / nSaldoMedio

		If aPergunte[5] == 1 //Consumo Med. Neg. Zerar (1=Sim/2=Não)
			If nGiro <= 0
				nGiro := 0 //Zera o Giro Mensal (B3_GXX) se for negativo
			EndIf
		EndIf

		If lCpoGiro //Verifica se o campo existe
			//Grava o Giro Mensal no campo B3_GXX
			SB3->(DbGoTo((cAliasGiro)->RECSB3))
			RecLock("SB3", .F.)
				&(cMesGiro) := nGiro
			SB3->(MsUnlock())
		EndIf
		
		(cAliasGiro)->(DbSkip())
	EndDo

	(cAliasGiro)->(DbCloseArea())

	FWRestArea(aAreaB3)
	FWRestArea(aArea)
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalNor³ Autor ³ Eveli Morasco         ³ Data ³ 10/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo normal da media de consumos (utiliza os pesos)     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A290CalNor()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290CalNor(nPorInc,oCenterPanel,cCodIndic,lA290Proc,aPergunte)
Local nX,aPesos[12],cPesos,nTPesos:=0,nMeses,aPesoAux,nI,nConsid,aPesoUsr,aPesosGiro[12],aPesoAuxGiro[12],nTPesosGiro:=0
Local nTamanho	:=TamSX3("B3_CTOTAL")[1]
Local nDecimais	:=TamSX3("B3_TOTAL")[2]
Local lA290CalP	:=ExistBlock("A290CALP")
Local lQuery	:= .F.
Local cAliasSB3	:= "SB3"
Local cAliasSB1	:= "SB1"
Local lM290QSB1 :=ExistBlock("M290QSB1")
Local cOpcoes	:= ""
Local cQuery	:= ""
Local cQueryUsr	:= ""
Local lCpoGiroM := SB3->(ColumnPos("B3_GMEDIA")) > 0 //Verifica se o campo de giro médio existe
Local aArea     := {}
Local aAreaD4R  := {}
Local cProdDe   := ""
Local cProdAte  := ""
Local nConMdNeg := 0 //Consumo Med. Neg. Zerar (1=Sim/2=Não)

If lA290Proc
	cProdDe   := aPergunte[3]
	cProdAte  := aPergunte[4]
	nConMdNeg := aPergunte[5]
Else
	cProdDe   := mv_par03
	cProdAte  := mv_par04
	nConMdNeg := mv_par05
EndIf

AFILL(aPesos,0)
AFILL(aPesosGiro,0)

//Se o processo for pela nova tela de parâmetros
If lA290Proc
	aArea    := FWGetArea()
	aAreaD4R := D4R->(FWGetArea())
	
	//Carregamento dos Pesos da tabela Indicadores de Giro ou Consumo(D4U) - Se cálculo for por Peso
	DbSelectArea('D4R')
	D4R->(DbSetOrder(1)) //D4R_FILIAL + D4R_COD

	If D4R->(DbSeek(FwxFilial('D4R') + cCodIndic))
		If D4R->D4R_TPCALC == '1' //Por Peso
			DbSelectArea('D4U')
			D4U->(DbSetOrder(1)) //D4U_FILIAL + D4U_CINDIC + D4U_TPIND
			
			//Busca os pesos do D4U (Tipo: D4U_TPIND '2' = Consumo)
			If D4U->(DbSeek(FwxFilial('D4U') + cCodIndic + '2'))
				For nX := 1 To 12
					aPesos[nX] := D4U->&('D4U_MES' + StrZero(nX, 2))
				Next nX
			EndIf

			//Busca os pesos do D4U (Tipo: D4U_TPIND '1' = Giro)
			If D4U->(DbSeek(FwxFilial('D4U') + cCodIndic + '1'))
				For nX := 1 To 12
					aPesosGiro[nX] := D4U->&('D4U_MES' + StrZero(nX, 2))
				Next nX
			EndIf
		EndIf
	EndIf

	FWRestArea(aAreaD4R)
	FWRestArea(aArea)

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pega a configuracao de pesos do cliente                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cPesos := GetMv("MV_PESOS")
	cPesos := SubStr(cPesos,1,12)
	cPesos := cPesos+Space(12-Len(cPesos))

	For nX := 1 To 12
	    aPesos[nX] := Val(SubStr(cPesos,nX,1))
	Next nX
EndIf

aPesoAux := aClone(aPesos)
aPesoAuxGiro := aClone(aPesosGiro)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Varre o arquivo de demandas e verifica filtro no SB1         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")

lQuery	  := .T.
cAliasSB3 := GetNextAlias()
cAliasSB1 := cAliasSB3
cQuery := "SELECT SB3.B3_FILIAL,SB3.B3_COD,SB3.R_E_C_N_O_ RECNOSB3,"
cQuery += "SB1.B1_FILIAL,SB1.B1_COD,SB1.B1_CONINI,SB1.B1_CUSTD,SB1.R_E_C_N_O_ RECNOSB1  "
cQuery += "FROM " + RetSqlName("SB3") + " SB3 "
cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = B3_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
cQuery += "WHERE SB3.B3_FILIAL='"+xFilial("SB3")+ "' "
cQuery += "AND SB3.D_E_L_E_T_=' ' "
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB3,.T.,.T.)
dbGoTop()

TcSetField(cAliasSB3,"B1_CUSTD" ,"N", TamSx3("B1_CUSTD")[1], TamSx3("B1_CUSTD")[2] )
TcSetField(cAliasSB3,"B1_CONINI","D", 8, 0)

While !EOF() .And. IIf(lQuery,.T.,(cAliasSB3)->B3_FILIAL+(cAliasSB3)->B3_COD <= xFilial("SB3")+cProdAte)
	If !lQuery
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1")+(cAliasSB3)->B3_COD)
	EndIf		

	If MA290Filtro(lQuery)

		nMeses := CalcMeses(RetFldProd((cAliasSB1)->B1_COD,"B1_CONINI",cAliasSB1))
		aPesos := aClone(aPesoAux)
		aPesosGiro := aClone(aPesoAuxGiro)
		nI := nMeses
	
		If nMeses < 12
			nX := (Month(dDataBase)+1) - nMeses
			If nX <= 0
				nX += 12
			EndIf
			If nX = 13
				nX := 1
			EndIf
			aPesos := {0,0,0,0,0,0,0,0,0,0,0,0}
			aPesosGiro := {0,0,0,0,0,0,0,0,0,0,0,0}
			If nMeses > 0
				nConsid := Month(dDataBase)+1
				nConsid := IIF(nConsid >12,1,nConsid)
				While nX != nConsid
					aPesos[nX] := aPesoAux[nX]
					aPesosGiro[nX] := aPesoAuxGiro[nX]
					nX++
					nX := IIF(nX>12,1,nX)
				End
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada p/ manipulacao de meses para calculo	  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lA290CalP
			aPesoUsr := ExecBlock("A290CALP",.F.,.F.,{nMeses,aPesos,aPesoAux,cAliasSB1,cAliasSB3})
			If ValType(aPesoUsr) == "A"
				aPesos := aClone(aPesoUsr)
			EndIf

			aPesoUsr := ExecBlock("A290CALP",.F.,.F.,{nMeses,aPesosGiro,aPesoAuxGiro,cAliasSB1,cAliasSB3})
			If ValType(aPesoUsr) == "A"
				aPesosGiro := aClone(aPesoUsr)
			EndIf
		EndIf	
		
		nTpesos := 0
		nTPesosGiro := 0
		For nX := 1 To 12
			nTpesos    += aPesos[nX]
			nTPesosGiro += aPesosGiro[nX]
		Next nX
	
		dbSelectArea("SB3")
		If lQuery
			dbGoto((cAliasSB3)->RECNOSB3)
		EndIf
		RecLock("SB3",.F.)
		Replace  B3_MEDIA With ((	B3_Q01*aPesos[01]+B3_Q02*aPesos[02]+B3_Q03*aPesos[03]+;
									B3_Q04*aPesos[04]+B3_Q05*aPesos[05]+B3_Q06*aPesos[06]+;
									B3_Q07*aPesos[07]+B3_Q08*aPesos[08]+B3_Q09*aPesos[09]+;
									B3_Q10*aPesos[10]+B3_Q11*aPesos[11]+B3_Q12*aPesos[12])/;
									nTpesos)*(1+nPorInc/100)
									
		//Se campo existir, grava o giro médio (B3_GMEDIA)
		If lCpoGiroM
			Replace  B3_GMEDIA With ((	B3_G01*aPesosGiro[01]+B3_G02*aPesosGiro[02]+B3_G03*aPesosGiro[03]+;
										B3_G04*aPesosGiro[04]+B3_G05*aPesosGiro[05]+B3_G06*aPesosGiro[06]+;
										B3_G07*aPesosGiro[07]+B3_G08*aPesosGiro[08]+B3_G09*aPesosGiro[09]+;
										B3_G10*aPesosGiro[10]+B3_G11*aPesosGiro[11]+B3_G12*aPesosGiro[12])/;
										nTPesosGiro)*(1+nPorInc/100)
		EndIf

		//Se o consumo medio for negativo zerar o consumo medio
		If nConMdNeg == 1
			If B3_MEDIA < 0 
				Replace B3_MEDIA With 0
			EndIf
			//Se campo existir, zera a media do giro caso negativo
			If lCpoGiroM
				If B3_GMEDIA <= 0
					Replace B3_GMEDIA With 0
				EndIf
			EndIf
		EndIf
		Replace	B3_TOTAL  With B3_MEDIA*RetFldProd((cAliasSB1)->B1_COD,"B1_CUSTD",cAliasSB1)
		Replace	B3_CTOTAL With StrZero(B3_TOTAL,nTamanho,nDecimais)
		Replace B3_MES    With dDatabase
		MsUnlock()
	EndIf
	dbSelectArea(cAliasSB3)
	dbSkip()
	If !lBat
		If oCenterPanel <> NIL
			oCenterPanel:IncRegua1(OemToAnsi(STR0037))
		Else
			IncProc()
		EndIf
	EndIf
EndDo
If lQuery
	(cAliasSB3)->(dbCloseArea())
EndIf
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalMin³ Autor ³ Eveli Morasco         ³ Data ³ 11/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo da media de consumos que utiliza o conceito dos    ³±±
±±³          ³ minimos quadrados.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalMin()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290CalMin(nQtdMes,oCenterPanel,lA290Proc,aPergunte)
Local nX,cMesAux2,cMeses:="010203040506070809101112"
Local nTotx:=0,nMpx:=0,nToty:=0,nMpy:=0,nTotXaYa:=0,nTotXaXa:=0,nTotYaYa:=0
Local aX[nQtdMes],aY[nQtdMes],aXa[nQtdMes],aYa[nQtdMes]
Local aXaYa[nQtdMes],aXaXa[nQtdMes],aYaYa[nQtdMes]
Local nMesProj,nSX,nSY,nRXY,nK1,nK2,nYP,nMeses,nMesAux
Local nTamanho  :=TamSX3("B3_CTOTAL")[1]
Local nDecimais :=TamSX3("B3_TOTAL")[2]
Local lQuery	:= .F.
Local cAliasSB3	:= "SB3"
Local cAliasSB1	:= "SB1"
Local lM290QSB1 := ExistBlock("M290QSB1")
Local aStru     := {}
Local cOpcoes   := ""
Local cQuery    := ""
Local cQueryUsr	:= ""
Local lCpoGiroM := SB3->(ColumnPos("B3_GMEDIA")) > 0 // Verifica se o campo de giro médio existe
Local cProdDe   := ""
Local cProdAte  := ""
Local nConMdNeg := 0 //Consumo Med. Neg. Zerar (1=Sim/2=Não)

If lA290Proc
	cProdDe   := aPergunte[3]
	cProdAte  := aPergunte[4]
	nConMdNeg := aPergunte[5]
Else
	cProdDe   := mv_par03
	cProdAte  := mv_par04
	nConMdNeg := mv_par05
EndIf

nMesAux := nQtdMes

For nX := 1 To nQtdMes
	aX[nX]    := nX
	aY[nX]    := 0
	aXa[nX]   := 0
	aYa[nX]   := 0
	aXaYa[nX] := 0
	aXaXa[nX] := 0
	aYaYa[nX] := 0
Next nX

Store nQtdMes+1 To nMesProj

// DEFINICOES DO X  --> MESES

For nX := 1 To nQtdMes
	nTotx := nTotx + aX[nX]
Next
nMpx := nTotx/nQtdMes               // MEDIA PONDERADA DE X

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Varre o arquivo de demandas e verifica filtro no SB1         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB3")

aStru     := SB3->(dbStruct())
lQuery	  := .T.
cAliasSB3 := GetNextAlias()
cAliasSB1 := cAliasSB3
cQuery := "SELECT SB3.*,SB3.R_E_C_N_O_ RECNOSB3,"
cQuery += "SB1.B1_FILIAL,SB1.B1_COD,SB1.B1_CONINI,SB1.B1_CUSTD,SB1.R_E_C_N_O_ RECNOSB1 "
cQuery += "FROM " + RetSqlName("SB3") + " SB3 "
cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = B3_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
cQuery += "WHERE SB3.B3_FILIAL='" + xFilial("SB3") + "' "
cQuery += "AND SB3.D_E_L_E_T_=' ' "
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB3,.T.,.T.)
dbGoTop()

For nX := 1 To Len(aStru)
	If ( aStru[nX][2] <> "C")
		TcSetField(cAliasSB3,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
	EndIf
Next
TcSetField(cAliasSB3,"B1_CUSTD" ,"N", TamSx3("B1_CUSTD")[1], TamSx3("B1_CUSTD")[2] )
TcSetField(cAliasSB3,"B1_CONINI","D", 8, 0)

While !EOF() .And. IIf(lQuery,.T.,(cAliasSB3)->B3_FILIAL+(cAliasSB3)->B3_COD <= xFilial("SB3")+cProdAte)

	nToty    := 0
	nTotXaYa := 0
	nTotXaXa := 0
	nTotYaYa := 0

	If !lQuery
		dbSelectArea("SB1")
		dbSeek(xFilial("SB1")+(cAliasSB3)->B3_COD)
	EndIf		

	If MA290Filtro(lQuery)

		nQtdMes := CalcMeses(RetFldProd((cAliasSB1)->B1_COD,"B1_CONINI",cAliasSB1),nMesAux)

		dbSelectArea(cAliasSB3)
		
		// DEFINICOES DO Y  --> QTDES
		For nX := 1 To nQtdMes
			STORE "B3_Q"+SUBS(cMeses+cMeses,(24+Month(dDataBase)*2)-1-(nQtdMes-nX)*2,2) TO cMesAux2
			aY[nX] := &(cMesAux2)
			nToty  := nToty + aY[nX]
		Next nX
		nMpy := nToty/nQtdMes               // MEDIA PONDERADA DE Y
		
		For nX := 1 To nQtdMes
			
			// CALCULO DO XA
			aXa[nX] := aX[nX] - nMpx
			
			// CALCULO DO YA
			aYa[nX] := aY[nX] - nMpy
			
			// CALCULO DO XA*YA
			aXaYa[nX] := aXa[nX]  * aYa[nX]
			nTotXaYa  := nTotXaYa + aXaYa[nX]
			
			// CALCULO DO XA*XA
			aXaXa[nX] := aXa[nX]  * aXa[nX]
			nTotXaXa  := nTotXaXa + aXaXa[nX]
			
			// CALCULO DO YA*YA
			aYaYa[nX] := aYa[nX]  * aYa[nX]
			nTotYaYa  := nTotYaYa + aYaYa[nX]
		Next nX
		
		nSX  := Abs( Sqrt( nTotXaXa / nQtdMes ) )
		nSY  := Abs( Sqrt( nTotYaYa / nQtdMes ) )
		nRXY := nTotXaYa / ( nQtdMes * nSX * nSY )
		nK1  := nRXY * ( nSX / nSY )
		nK2  := nRXY * ( nSY / nSX )
		nYP  := ( nK2 * nMesProj ) + ( nMpy - ( nK2 * nMpx ) )
		
		dbSelectArea("SB3")
		If lQuery
			dbGoto((cAliasSB3)->RECNOSB3)
		EndIf
		RecLock("SB3",.F.)
		Replace B3_MEDIA WITH nYP,B3_TOTAL WITH B3_MEDIA*RetFldProd((cAliasSB1)->B1_COD,"B1_CUSTD",cAliasSB1)

		//Se campo existir, grava a media do giro
		If lCpoGiroM
			Replace B3_GMEDIA WITH nYP,B3_TOTAL WITH B3_GMEDIA*RetFldProd((cAliasSB1)->B1_COD,"B1_CUSTD",cAliasSB1)
		EndIf
		//Se o consumo medio for negativo zerar o consumo medio
		If nConMdNeg == 1
			If B3_MEDIA < 0 
				Replace B3_MEDIA With 0
			EndIf
			//Se campo existir, zera a media do giro caso negativo
			If lCpoGiroM
				If B3_GMEDIA < 0 
					Replace B3_GMEDIA With 0
				EndIf
			EndIf
		EndIf
		Replace	B3_CTOTAL With StrZero(B3_TOTAL,nTamanho, nDecimais)
		Replace B3_MES    With dDatabase
		MsUnlock()
	EndIf
	dbSelectArea(cAliasSB3)
	dbSkip()
	If !lBat
		If oCenterPanel <> NIL
			oCenterPanel:IncRegua1(OemToAnsi(STR0037))
		Else
			IncProc()
		EndIf
	EndIf
EndDo       
If lQuery
	(cAliasSB3)->(dbCloseArea())
EndIf
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290CalLot³ Autor ³ Eveli Morasco         ³ Data ³ 12/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calcula o lote economico                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A290CalLot()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290CalLot(cCalPon,nPerA,nPerB,nPerC,nPorA,nPorB,nPorC,lCtrLote,oCenterPanel,lA290Proc,aPergunte)
Local nTotCon:=0,nTotA:=0,nTotB:=0,nTotC:=0,nLote,cClasse,nPrazo
Local lMta290Abc:= Existblock("A290ABC")
Local lA290CALL := .F.                                         
Local lA290CAPP := .F.                                         
Local nA290CALL := 0
Local nA290CAPP := 0
Local nQE		:= 0
Local bWhileSB3 := {}
Local cAliasSB1	:= "SB1"
Local cAliasSB3	:= "SB3"
Local lQuery	:= .F.
Local cIndSB3	:= CriaTrab(NIL,.F.), nIndSB3
Local cCond		:= ""
Local lM290QSB1 := ExistBlock("M290QSB1")
Local cOpcoes	:= ""
Local cQuery	:= ""
Local cQueryUsr	:= ""
Local cProdDe   := ""
Local cProdAte  := ""

If lA290Proc
	cProdDe  := aPergunte[3]
	cProdAte := aPergunte[4]
Else
	cProdDe  := mv_par03
	cProdAte := mv_par04
EndIf

Default lCtrLote := .T.

dbSelectArea("SB3")

bWhileSB3 := { || (cAliasSB3)->( !Eof()) }
lQuery	  := .T.
cAliasSB3 := GetNextAlias()
cAliasSB1 := cAliasSB3
cQuery := "SELECT B3_FILIAL,B3_COD,B3_MEDIA,B3_TOTAL,SB3.R_E_C_N_O_ RECNOSB3,"
cQuery += "B1_FILIAL,B1_COD,B1_QE,B1_LE,SB1.R_E_C_N_O_ RECNOSB1  "
cQuery += "FROM " + RetSqlName("SB3") + " SB3 "
cQuery += "JOIN " + RetSqlName("SB1") + " SB1 "
cQuery += "ON  B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD = B3_COD "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
cQuery += "WHERE SB3.B3_FILIAL='" + xFilial("SB3") + "' "
cQuery += "AND SB3.D_E_L_E_T_=' ' "
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery += " ORDER BY B3_FILIAL,B3_CTOTAL DESC "
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB3,.T.,.T.)
dbGoTop()

TcSetField(cAliasSB3,"B1_LE"      ,"N", TamSx3("B1_LE")[1]    , TamSx3("B1_LE")[2] )
TcSetField(cAliasSB3,"B1_QE"      ,"N", TamSx3("B1_QE")[1]    , TamSx3("B1_QE")[2] )
TcSetField(cAliasSB3,"B3_MEDIA"   ,"N", TamSx3("B3_MEDIA")[1] , TamSx3("B3_MEDIA")[2] )
TcSetField(cAliasSB3,"B3_TOTAL"   ,"N", TamSx3("B3_TOTAL")[1] , TamSx3("B3_TOTAL")[2] )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Soma o valor total das medias de consumo                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While Eval( bWhileSB3 )
	nTotCon += B3_TOTAL
	dbSkip()
EndDo
dbGoTop()

nTotA   :=  nPorA * nTotCon / 100
nTotB   := (nPorB * nTotCon / 100) + nTotA
nTotC   := (nPorC * nTotCon / 100) + nTotB
nTotCon := 0

While Eval( bWhileSB3 )
	If lQuery .Or. SB1->(dbSeek(xFilial("SB1")+(cAliasSB3)->B3_COD))
		If MA290Filtro(lQuery)
			Do Case
				Case nTotCon < nTotA
					nLote   := (cAliasSB3)->B3_MEDIA * nPerA
					cClasse := 'A'
				Case nTotCon < nTotB
					nLote   := (cAliasSB3)->B3_MEDIA * nPerB
					cClasse := 'B'
				OtherWise
					nLote   := (cAliasSB3)->B3_MEDIA * nPerC
					cClasse := 'C'
			EndCase
			nQE	  := RetFldProd((cAliasSB1)->B1_COD,"B1_QE",cAliasSB1)
			nLote := If(nLote < nQE,nQE,Round(nLote / If( nQE==0,1,nQE ), 0) * If( nQE==0,1,nQE ) )
			nPrazo := CalcPrazo((cAliasSB1)->B1_COD,RetFldProd((cAliasSB1)->B1_COD,"B1_LE",cAliasSB1))

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ A290CALL - Ponto de Entrada para manipulacao do Valor a ser gravado no Lote Economico |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nA290CALL := 0
			lA290CALL := .F.
			If lCtrLote .And. ExistBlock('A290CALL')
				nA290CALL := ExecBlock('A290CALL', .F., .F., {(cAliasSB1)->B1_COD, nLote})
				If Valtype(nA290CALL) == 'N'
					lA290CALL  := .T.
				EndIf
			EndIf

			If lQuery
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Posiciona SB3 devido funcao CalcEstSeg executar formula    |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cCalPon == "x" 
					dbSelectArea("SB3")
					dbGoto((cAliasSB3)->RECNOSB3)
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ A290CAPP - Ponto de Entrada para manipulacao do Valor a ser gravado no Ponto de Pedido  |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nA290CAPP := 0
			lA290CAPP := .F.
			If cCalPon == "x" .And. ExistBlock('A290CAPP')
				If RetArqProd((cAliasSB1)->B1_COD)
					nA290CAPP := ((cAliasSB3)->B3_MEDIA * (nPrazo / 30 )) + CalcEstSeg( SB1->B1_ESTFOR )
				Else
					nA290CAPP := ((cAliasSB3)->B3_MEDIA * (nPrazo / 30 )) + CalcEstSeg( SBZ->BZ_ESTFOR )
				EndIf
				nA290CAPP := ExecBlock('A290CAPP', .F., .F., {(cAliasSB1)->B1_COD, nA290CAPP})
				If Valtype(nA290CAPP) == 'N'
					lA290CAPP  := .T.
				EndIf
			EndIf
			
			If RetArqProd((cAliasSB1)->B1_COD)
				If lQuery
					dbSelectArea("SB1")
					dbGoto((cAliasSB1)->RECNOSB1)
				EndIf
				RecLock("SB1",.F.)
				If lCtrLote
					Replace B1_LE With If(lA290CALL,nA290CALL,nLote)
				EndIf
				If cCalPon == "x"
					Replace B1_EMIN WITH If(lA290CAPP,nA290CAPP,((cAliasSB3)->B3_MEDIA * (nPrazo / 30 )) + CalcEstSeg( SB1->B1_ESTFOR ))
				EndIf
			Else
				RecLock("SBZ",.F.)
				If lCtrLote
					Replace BZ_LE With If(lA290CALL,nA290CALL,nLote)
				EndIf
				If cCalPon == "x"
					Replace BZ_EMIN WITH If(lA290CAPP,nA290CAPP,((cAliasSB3)->B3_MEDIA * (nPrazo / 30 )) + CalcEstSeg( SBZ->BZ_ESTFOR ))
				EndIf
			EndIf
			MsUnlock()
			If lGrvABC
				dbSelectArea("SB3")
				If lQuery .And. cCalPon <> "x" 
					dbGoto((cAliasSB3)->RECNOSB3)
				EndIf				
				RecLock("SB3",.F.)
				Replace B3_CLASSE With cClasse
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ A290ABC - Ponto de Entrada para manipulacao da Classe ABC |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lMta290Abc
					Execblock("A290ABC",.F.,.F.)
				EndIf
				MsUnlock()
			EndIf
			dbSelectArea(cAliasSB3)
			nTotCon := nTotCon + (cAliasSB3)->B3_TOTAL
		EndIf
	EndIf
	If !lQuery
		dbSkip(-1)
	Else 
		dbSkip()
	EndIf
	If !lBat
		If oCenterPanel <> NIL
			oCenterPanel:IncRegua1(OemToAnsi(STR0038))
		Else
			IncProc()
		EndIf
	EndIf
EndDo

If lQuery
	dbSelectArea(cAliasSB3)
	dbCloseArea()	
Else
	RetIndex("SB3")
	Ferase(cIndSB3+OrdBagExt())
	dbSetOrder(1)
EndIf
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290AjuLot³ Autor ³ Eveli Morasco         ³ Data ³ 13/02/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ajusta o lote economico pelo valor disponivel para compras ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A290AjuLot()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290AjuLot(nValDisp,oCenterPanel,lA290Proc,aPergunte)
Local nQatu:=0,nPedido:=0,nTotVal:=0,nFalta:=0,nDif:=0
Local nA290AJUL := 0
Local lA290AJUL := .F.
Local lQuery    := .F.
Local cAliasSB1	:= "SB1"
Local cAliasSB3	:= "SB3"
Local lM290QSB1 := ExistBlock("M290QSB1")
Local cOpcoes	:= ""
Local cQuery	:= ""
Local cQueryUsr	:= ""
Local cArmDe    := ""
Local cArmAte   := ""
Local cProdDe   := ""
Local cProdAte  := ""

If lA290Proc
	cArmDe   := aPergunte[1]
	cArmAte  := aPergunte[2]
	cProdDe  := aPergunte[3]
	cProdAte := aPergunte[4]
Else
	cArmDe   := mv_par01
	cArmAte  := mv_par02
	cProdDe  := mv_par03
	cProdAte := mv_par04
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Seleciona ordem por produto para o SC1 e o SC7               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC1")
dbSetOrder(2)

dbSelectArea("SC7")
dbSetOrder(4)

dbSelectArea("SB1")
dbSetOrder(1)

lQuery	  := .T.
cAliasSB1 := GetNextAlias()
cAliasSB3 := cAliasSB1
cQuery := "SELECT B1_FILIAL,B1_COD,B1_EMIN,B1_LE,B1_UPRC,B1_CUSTD,SB1.R_E_C_N_O_ RECNOSB1,B3_MEDIA "
cQuery += "FROM " + RetSqlName("SB1") + " SB1 "
cQuery += "LEFT JOIN " + RetSqlName("SB3") + " SB3 "
cQuery += "ON  B3_FILIAL = '" + xFilial("SB3") + "' "
cQuery += "AND B3_COD = B1_COD "
cQuery += "AND SB3.D_E_L_E_T_ = ' ' "
cQuery += "WHERE B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
	cOpcoes := Trim(aOpcoes[8][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
	cOpcoes := Trim(aOpcoes[9][1])
	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
	cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM290QSB1
	cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
	If ValType(cQueryUsr) == "C"
		cQuery += cQueryUsr
	EndIf
EndIf
cQuery += " ORDER BY "+SqlOrder(SB1->(IndexKey()))
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB1,.T.,.T.)
dbGoTop()

TcSetField(cAliasSB1,"B1_EMIN"  ,"N", TamSx3("B1_EMIN")[1],  TamSx3("B1_EMIN")[2] )
TcSetField(cAliasSB1,"B1_LE" 	,"N", TamSx3("B1_LE")[1],	 TamSx3("B1_LE")[2] )
TcSetField(cAliasSB1,"B1_UPRC"  ,"N", TamSx3("B1_UPRC")[1],  TamSx3("B1_UPRC")[2] )
TcSetField(cAliasSB1,"B1_CUSTD" ,"N", TamSx3("B1_CUSTD")[1], TamSx3("B1_CUSTD")[2] )
TcSetField(cAliasSB1,"B3_MEDIA" ,"N", TamSx3("B3_MEDIA")[1], TamSx3("B3_MEDIA")[2] )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Soma o valor total das compras do mes                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !EOF() .And. IIf(lQuery,.T.,(cAliasSB1)->B1_FILIAL+(cAliasSB1)->B1_COD <= xFilial("SB1")+cProdAte)
	If MA290Filtro(lQuery)
		If lQuery
			nQatu	:= A290SB2Aju((cAliasSB1)->B1_COD, lA290Proc, aPergunte)
			nPedido := A290SC1Aju((cAliasSB1)->B1_COD, lA290Proc, aPergunte)
			nPedido += A290SC7Aju((cAliasSB1)->B1_COD, lA290Proc, aPergunte)
		Else
			nQatu   := 0
			nPedido := 0
			dbSelectArea("SB2")
			dbSeek(xFilial("SB2")+SB1->B1_COD)
			While !EOF() .And. B2_FILIAL+B2_COD == xFilial("SB2")+SB1->B1_COD
				If B2_LOCAL >= mv_par01 .And. B2_LOCAL <= mv_par02
					nQatu := nQatu + B2_QATU
				EndIf
				dbSkip()
			EndDo
			dbSelectArea("SC1")
			dbSeek(xFilial("SC1")+SB1->B1_COD)
			While !EOF() .And. C1_FILIAL+C1_PRODUTO == xFilial("SC1")+SB1->B1_COD
				If C1_QUANT > C1_QUJE .And. Month(C1_DATPRF) == Month(dDataBase);
				   .And. Year(C1_DATPRF) == Year(dDataBase);
				   .And. C1_LOCAL >= mv_par01 .And. C1_LOCAL <= mv_par02
					nPedido := nPedido + (C1_QUANT - C1_QUJE)
				EndIf
				dbSkip()
			EndDo
			dbSelectArea("SC7")
			dbSeek(xFilial("SC7")+SB1->B1_COD)
			While !EOF() .And. C7_FILIAL+C7_PRODUTO == xFilial("SC7")+SB1->B1_COD
				If C7_QUANT > C7_QUJE .And. Month(C7_DATPRF) == Month(dDataBase) .And. Empty(C7_RESIDUO);
				   .And. Year(C7_DATPRF) == Year(dDataBase);
				   .And. C7_LOCAL >= mv_par01 .And. C7_LOCAL <= mv_par02
					nPedido := nPedido + (C7_QUANT-C7_QUJE)
				EndIf
				dbSkip()
			EndDo
			dbSelectArea("SB3")
			dbSeek(xFilial("SB3")+SB1->B1_COD)
			dbSelectArea("SB1")
		EndIf

		nFalta := (RetFldProd((cAliasSB1)->B1_COD,"B1_EMIN",cAliasSB1)+(cAliasSB3)->B3_MEDIA) - (nQatu+nPedido)

		If nFalta > 0
			If nFalta < RetFldProd((cAliasSB1)->B1_COD,"B1_LE",cAliasSB1)
				nFalta := RetFldProd((cAliasSB1)->B1_COD,"B1_LE",cAliasSB1)
			EndIf
			nTotVal := nTotVal + (nFalta * IIF(RetFldProd((cAliasSB1)->B1_COD,"B1_UPRC",cAliasSB1) > 0,RetFldProd((cAliasSB1)->B1_COD,"B1_UPRC",cAliasSB1),RetFldProd((cAliasSB1)->B1_COD,"B1_CUSTD",cAliasSB1)))
		EndIf
		
	EndIf
	dbSkip()
	If !lBat
		If oCenterPanel <> NIL
			oCenterPanel:IncRegua1(OemToAnsi(STR0039))
		Else
			IncProc()
		EndIf
	EndIf
EndDo
If nTotVal <= 0
	nTotVal := 1
EndIf
nDif := (nValDisp/nTotVal)
If lQuery
	dbSelectArea(cAliasSB1)
	dbCloseArea()
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Somente ajusta se o valor informado nao conseguir comprar    ³
//³ pelo menos 85% da necessidade calculada.                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nDif < 0.85
	dbSelectArea("SB1")
	cAliasSB1 := GetNextAlias()
	cQuery := "SELECT B1_FILIAL,B1_COD,B1_LE,SB1.R_E_C_N_O_ RECNOSB1 "
	cQuery += "FROM "+RetSqlName("SB1")+" SB1 "
	cQuery += "WHERE B1_FILIAL = '" + xFilial("SB1") + "' "
	cQuery += "AND B1_COD >= '" + cProdDe + "' AND B1_COD <= '" + cProdAte + "' "
	cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
	If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
		cOpcoes := Trim(aOpcoes[8][1])
		cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
		cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
	EndIf
	If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
		cOpcoes := Trim(aOpcoes[9][1])
		cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
		cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ M290QSB1 - Ponto de Entrada utilizado para adicionar filtro na query (ref. SB1)   |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lM290QSB1
		cQueryUsr := ExecBlock("M290QSB1",.F.,.F.)
		If ValType(cQueryUsr) == "C"
			cQuery += cQueryUsr
		EndIf
	EndIf

	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB1,.T.,.T.)
	dbGoTop()

	TcSetField(cAliasSB1,"B1_LE" 	,"N", TamSx3("B1_LE")[1],	 TamSx3("B1_LE")[2] )
			
	While !EOF() .And. IIf(lQuery,.T.,(cAliasSB1)->B1_FILIAL+(cAliasSB1)->B1_COD <= xFilial("SB1")+cProdAte)
		If MA290Filtro(lQuery)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ A290AJUL - Ponto de Entrada para manipulacao do Valor a ser gravado no Lote Economico |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nA290AJUL := 0
			lA290AJUL := .F.
			If ExistBlock('A290AJUL')
				nA290AJUL := ExecBlock('A290AJUL', .F., .F., {(cAliasSB1)->B1_COD, (RetFldProd((cAliasSB1)->B1_COD,"B1_LE",cAliasSB1) * nDif)})
				If Valtype(nA290AJUL) == 'N'
					lA290AJUL := .T.
				EndIf
			EndIf
			If RetArqProd((cAliasSB1)->B1_COD)
				If lQuery
					dbSelectArea("SB1") // posiciona SB1 somente qdo necessario
					dbGoto((cAliasSB1)->RECNOSB1)
				EndIf
				RecLock("SB1",.F.)
				Replace B1_LE With If(lA290AJUL,nA290AJUL,B1_LE * nDif)
				MsUnlock()
			Else
				RecLock("SBZ",.F.)
				Replace BZ_LE With If(lA290AJUL,nA290AJUL,BZ_LE * nDif)
				MsUnlock()
			EndIf
			dbSelectArea(cAliasSB1)
		EndIf
		dbSkip()
		If !lBat
			If oCenterPanel <> NIL
				oCenterPanel:IncRegua1(OemToAnsi(STR0039))
			Else
				IncProc()
			EndIf
		EndIf
	EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a ordem principal dos arquivos                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery .And. Select(cAliasSB1) > 0
	dbSelectArea(cAliasSB1)
	dbCloseArea()
EndIf
dbSelectArea("SC1")
dbSetOrder(1)

dbSelectArea("SC7")
dbSetOrder(1)
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A290Menu ³ Autor ³ Jorge Queiroz         ³ Data ³ 06/01/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa a string das opcoes de processamento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ExpA1 := A290Menu()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Altera‡„o ³Guilherme Santos - 20/07/06                                 ³±±
±±³          ³Incluido tratamento para selecao de Filiais que deverao ser ³±±
±±³          ³processadas.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 := Array devolvido pela escolha                      ³±±
±±³          ³ [1][1]-> Atualiza Consumo do Mes (x ou Branco)             ³±±
±±³          ³ [2][1]-> Calcula por Peso        (x ou Branco)             ³±±
±±³          ³    [2]-> Incremento Percentual   (Percentual N 3)          ³±±
±±³          ³ [3][1]-> Calculo pela Tendencia  (x ou Branco)             ³±±
±±³          ³    [2]-> Numero de Meses         (Meses N 2)               ³±±
±±³          ³ [4][1]-> Calculo Lote Economico  (x ou Branco)             ³±±
±±³          ³    [2]-> Calculo Ponto Pedido    (x ou Branco)             ³±±
±±³          ³ [5][1]-> Ajusta Lote Economico   (x ou Branco)             ³±±
±±³          ³    [2]-> Disponibilidade         (Valor N 11)              ³±±
±±³          ³ [6][1]-> Periodo Aquisicao A     (Valor N  4,1)            ³±±
±±³          ³ [6][2]-> Periodo Aquisicao B     (Valor N  4,1)            ³±±
±±³          ³ [6][3]-> Periodo Aquisicao C     (Valor N  4,1)            ³±±
±±³          ³ [7][1]-> %Distribuicao     A     (Percentual N 5,2)        ³±±
±±³          ³ [7][2]-> %Distribuicao     B     (Percentual N 5,2)        ³±±
±±³          ³ [7][3]-> %Distribuicao     C     (Percentual N 5,2)        ³±±
±±³          ³ [8][1]-> Tipo de Material  (** Todos)                      ³±±
±±³          ³ [9][1]-> Grupos de Material  (** Todos)                    ³±±
±±³          ³[10][1]-> Selecao de Filiais (.T. ou .F.)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290MENU(lBat)
Local oDlg,oQual,oQual2,oUsado,oConsumo,nCalculo:=1,oChk1,oChk2,oChk3,oChk4,oChk7,oChk8,lCalc,lLote,lEstSeg:=.F.,;
lPedido,lDispoF,nIncre:=nMeses:=nDispoF:=0,;
nA1:=nB1:=nC1:=1,nA2:=nB2:=30,nC2:=40,oGet1,oGet2,oGet3,nOpca:=2,aOpt[10][3],;
oChkQual,oChkQual2,lQual:=.T.,lQual2:=.T.,oChk5,oChk6,lFilial,oChkGrpVz,lGrpVazio:=.T.
Local cVarQ := cVarQ2 := "  "
Local cCapital
Local i := 0
Local bBlNewProcess := {|oCenterPanel|aOpcoes:=a290PrepPrc(nIncre,nMeses,lPedido,lDispoF,nDispoF,nA1,nA2,nB1,nB2,nC1,nC2,lFilial,lCalc,nCalculo,lLote,lGrpVazio),;
						MA290Process(aOpcoes,@lEnd,lBat,oCenterPanel)}
Local aTreeProc 	:= {}
Local lUsaNewPrc	:= UsaNewPrc()
Local nMesesES	:= 0
Local nConsumo	:= 1
Local lConsumo	:= .T.
Local aRetSX5
Local nI

SetKey( VK_F12 ,{|| Pergunte("MTA290",.T.)})

Private cCadastro := OemToAnsi(STR0004)   //"Lote Econ“mico"
Private aTipo := {}
Private aGrupo := {}

Private oOk   := LoadBitmap( GetResources(), "LBOK")
Private oNo   := LoadBitmap( GetResources(), "LBNO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a Tabela de Tipos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRetSX5 := FWGetSX5("02")
For nI := 1 To Len(aRetSX5)
	cCapital := OemToAnsi(Capital(aRetSX5[nI,4]))
	AADD(aTipo,{.T.,SubStr(aRetSX5[nI,3],1,3)+" - "+cCapital})
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a Tabela de Grupos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SBM")
dbSeek(xFilial("SBM"))
While !EOF() .And. BM_FILIAL == xFilial("SBM")
	cCapital := OemToAnsi(Capital(BM_DESC))
	AADD(aGrupo,{.T.,SubStr(BM_GRUPO,1,5)+" - "+cCapital})
	dbSkip()
EndDo

If !lUsaNewPrc
	DEFINE MSDIALOG oDlg TITLE cCadastro From 145,0 To 625,628 OF oMainWnd PIXEL
	@ 95,15 TO 230,120 LABEL "" OF oDlg  PIXEL
	@ 140,20 MSGET oGet3 VAR nMesesES Picture "999" SIZE 15,10 VALID (nMesesES < 13 .And. nMesesES > 0) OF oDlg PIXEL ;oGet3:Disable()
	@ 155,20 RADIO oConsumo VAR nConsumo 3D SIZE 70,10 PROMPT OemToAnsi(STR0049),OemToAnsi(STR0050) OF oDlg PIXEL  ON CLICK If(lEstSeg .And. nConsumo==1,lConsumo:=.T.,lConsumo:=.F.) ;oConsumo:Disable()      //Por Consumo Medio ou Por Previsao de Venda
	@ 123,20 CHECKBOX oChk8 VAR lEstSeg PROMPT OemToAnsi(STR0043) SIZE 100, 10 OF oDlg PIXEL ON CLICK If(lEstSeg,(oGet3:Enable(),oConsumo:Enable()),(oGet3:Disable(),oConsumo:Disable())) ;oChk8:oFont := oDlg:oFont //"Estoque de Seguranca" 
	@ 133,20 Say OemToAnsi(STR0044) SIZE 100,10 OF oDlg PIXEL //considerando consumo dos ultimos.
    @ 138,40 Say OemToAnsi(STR0045) SIZE 20,10 OF oDlg PIXEL //meses.	                   
	@ 185,20 CHECKBOX oChk4 VAR lDispoF PROMPT OemToAnsi(STR0013) SIZE 90, 10 OF oDlg PIXEL ON CLICK If(lDispoF,oGet4:Enable(),oGet4:Disable()) ;oChk4:oFont := oDlg:oFont  //"Ajusta Lote Econ“mico pela dispo-"
	@ 195,20 Say OemToAnsi(STR0014) SIZE 80,10 OF oDlg PIXEL    																		//"nibilidade financeira"
	@ 202,70 MSGET oGet4 VAR nDispoF Picture "@E 99,999,999,999" VALID nDispoF > 0 SIZE 45,10 OF oDlg PIXEL ;oGet4:Disable() 
	DEFINE SBUTTON FROM 220,240 TYPE 1 ACTION IIF(A290VldPer(nA2,nB2,nC2,oChk8,oGet3,lEstSeg,nMesesES),(nOpca:=1,oDlg:End()),"") ENABLE OF oDlg
	DEFINE SBUTTON FROM 220,270 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	@ 02,15 TO 30,120 LABEL "" OF oDlg  PIXEL
	@ 08,20 CHECKBOX oChk1 VAR lCalc PROMPT OemToAnsi(STR0005) SIZE 100, 10 OF oDlg PIXEL ;oChk1:oFont := oDlg:oFont   			  //"Atualiza‡„o do Consumo do Mˆs"		
	@ 18,20 CHECKBOX oChk6 VAR lFilial PROMPT OemToAnsi(STR0027) SIZE 90, 10 OF oDlg PIXEL ;oChk6:oFont := oDlg:oFont             //"Seleciona Filial"
	@ 32,15 TO 92,120 LABEL OemToAnsi(STR0006) OF oDlg  PIXEL   															      //"C lculos"
	@ 38,20 RADIO oUsado VAR nCalculo 3D SIZE 70,10 PROMPT OemToAnsi(STR0007),OemToAnsi(STR0008) OF oDlg PIXEL      			 //"Por Peso"###"Pela Tendˆncia"
	oUsado:bChange := { || IIF(nCalculo=1,(oGet1:Enable(),oGet2:Disable()),(oGet1:Disable(),oGet2:Enable())) }
	@ 62,20 Say OemToAnsi(STR0009) SIZE 40,10 OF oDlg PIXEL    																		 //"Incremento:"
	@ 62,67 MSGET oGet1 VAR nIncre When IIF(nCalculo=1,.T.,.F.) Picture "999" SIZE 15,10 OF oDlg PIXEL
	@ 78,20 Say OemToAnsi(STR0010) SIZE 60,10 OF oDlg PIXEL     																	 //"N£mero de Meses:"
	@ 78,67 MSGET oGet2 VAR nMeses When IIF(nCalculo=2,.T.,.F.) Picture "999" SIZE 15,10 VALID (nMeses < 13 .And. nMeses > 0) OF oDlg PIXEL
	@ 103,20 CHECKBOX oChk2 VAR lLote PROMPT OemToAnsi(STR0011) SIZE 80, 10 OF oDlg PIXEL ;oChk2:oFont := oDlg:oFont    			//"C lculo do Lote Econ“mico"
	@ 113,20 CHECKBOX oChk3 VAR lPedido PROMPT OemToAnsi(STR0012) SIZE 80, 10 OF oDlg PIXEL ;oChk3:oFont := oDlg:oFont       	//"C lculo do Ponto de Pedido"
	@ 02,130 TO 73,300 LABEL OemToAnsi(STR0015) OF oDlg PIXEL    																	//"Classifica‡„o ABC"
	@ 12,182 TO 58,219 LABEL "A" OF oDlg  PIXEL
	@ 12,220 TO 58,257 LABEL "B" OF oDlg  PIXEL
	@ 12,258 TO 58,295 LABEL "C" OF oDlg  PIXEL
	@ 18,135 Say OemToAnsi(STR0016) SIZE 50,10 OF oDlg PIXEL   //"Per¡odo de"
	@ 25,135 Say OemToAnsi(STR0017) SIZE 50,10 OF oDlg PIXEL   //"Aquisi‡„o(meses)"
	@ 38,135 Say OemToAnsi(STR0018) SIZE 45,10 OF oDlg PIXEL   //"Distribui‡„o"
	@ 45,135 Say OemToAnsi(STR0019) SIZE 45,10 OF oDlg PIXEL   //"Percentual (%)"
	@ 20,185 MSGET nA1 Picture "99.9" VALID nA1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 20,223 MSGET nB1 Picture "99.9" VALID nB1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 20,261 MSGET nC1 Picture "99.9" VALID nC1 >0 SIZE 17,10 OF oDlg PIXEL
	@ 40,185 MSGET nA2 Picture "99.9" VALID nA2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 40,223 MSGET nB2 Picture "99.9" VALID nB2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 40,261 MSGET nC2 Picture "99.9" VALID nC2 >0 SIZE 17,10 OF oDlg PIXEL
	@ 60,135 CHECKBOX oChk5 VAR lGrvABC PROMPT OemToAnsi(STR0025) SIZE 95,10 OF oDlg PIXEL ;oChk5:oFont := oDlg:oFont   										//"Grava Classificacao ABC"
	If Len(aTipo) > 0
		@ 76,130 LISTBOX oQual VAR cVarQ Fields HEADER "",OemToAnsi(STR0020) SIZE 75,100 ON DBLCLICK (aTipo:=ca290troca(oQual:nAt,aTipo),oQual:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual,oOk,oNo,@aTipo) NOSCROLL OF oDlg PIXEL  //"Tipos de Material"
		oQual:SetArray(aTipo)
		oQual:bLine := { || {if(aTipo[oQual:nAt,1],oOk,oNo),aTipo[oQual:nAt,2]}}
		@ 185,130 CHECKBOX oChkQual  VAR lQual  PROMPT OemToAnsi(STR0024) SIZE 90, 10 OF oDlg PIXEL ON CLICK (AEval(aTipo , {|z| z[1] := If(z[1]==.T.,.F.,.T.)}), oQual:Refresh(.F.)) //"Inverter Selecao"
	EndIf
	If Len(aGrupo) > 0
		@ 76,225 LISTBOX oQual2 VAR cVarQ2 Fields HEADER "",OemToAnsi(STR0021) SIZE 75, 100 ON DBLCLICK (aGrupo:=ca290troca(oQual2:nAt,aGrupo),oQual2:Refresh()) ON RIGHT CLICK ListBoxAll(nRow,nCol,@oQual2,oOk,oNo,@aGrupo) NOSCROLL OF oDlg  PIXEL   //"Grupos de Material"
		oQual2:SetArray(aGrupo)
		oQual2:bLine := { || {if(aGrupo[oQual2:nAt,1],oOk,oNo),aGrupo[oQual2:nAt,2]}}
		@ 185,225 CHECKBOX oChkQual2 VAR lQual2 PROMPT OemToAnsi(STR0024) SIZE 90, 10 OF oDlg PIXEL ON CLICK (AEval(aGrupo, {|z| z[1] := If(z[1]==.T.,.F.,.T.)}),oQual2:Refresh(.F.)) //"Inverter Selecao"
		@ 193,225 CHECKBOX oChkGrpVz VAR lGrpVazio PROMPT STR0042 SIZE 90, 10 OF oDlg PIXEL
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada MTA290FIL - p/ adicionar filtro no SB1  |
	//³ (permite a criacao de um botao para customizacao)		 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lMta290Fil )
		DEFINE SBUTTON FROM 220,210 TYPE 5 ACTION ( cMTA290Fil:=ExecBlock("MTA290FIL",.F.,.F.) ) ENABLE OF oDlg
	EndIf

	oChk2:BlClicked :=	{|| oChk2:Refresh()}
	ACTIVATE MSDIALOG oDlg CENTERED
Else
	aAdd(aTreeProc,{OemToAnsi(STR0029),{|oCenterPanel|a290MontCfg(oCenterPanel,@lCalc,@lFilial,@nCalculo,@nIncre,@nMeses,@lLote,@lPedido,@lDispoF,@nDispof)},"ENGRENAGEM"})
	aAdd(aTreeProc,{OemToAnsi(STR0015),{|oCenterPanel|a290MontABC(oCenterPanel,@nA1,@nB1,@nC1,@nA2,@nB2,@nC2,@lGrvABC)},"PMSRRFSH"})
	aAdd(aTreeProc,{OemToAnsi(STR0031),{|oCenterPanel|a290MontFil(oCenterPanel,@lGrpVazio)},"filtro1"})	
	tNewProcess():New("MATA290",cCadastro,bBlNewProc,OemToAnsi(STR0028),"",aTreeProc,,,,,.T.)
EndIf

If nOpca == 1
	aOpt := a290PrepPrc(nIncre,nMeses,lPedido,lDispoF,nDispoF,nA1,nA2,nB1,nB2,nC1,nC2,lFilial,lCalc,nCalculo,lLote,lGrpVazio,lEstSeg,nMesesES,lConsumo)
Else
	aOpt:=NIL
EndIf

DeleteObject(oOk)
DeleteObject(oNo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desativa a tecla F12							             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey( VK_F12, Nil )

Return aOpt

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ CalcMeses³ Autor ³ Wilson Junior         ³ Data ³ 28/03/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o Numero de Meses para calculo                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CalcMeses(dInicio,nMaximo)
Local nAnoIni := Year(dInicio),nMesIni:=Month(dInicio)
Local nAnoFim := Year(dDataBase),nMesFim:=Month(dDataBase)
Local nMeses
nMaximo := IIF(nMaximo == Nil,12,nMaximo)
nMeses := ((nAnoFim-nAnoIni)*12) + (nMesFim-nMesIni) + 1
nMeses := IIF(nMeses>nMaximo,nMaximo,nMeses)
nMeses := IIF(nMeses<0,0,nMeses)
Return(nMeses)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MA290Filt³ Autor ³ Marcos Bregantim      ³ Data ³ 04/07/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna .t. ou .f. para validacao do filtro                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = indica se esta' utilizando query ou nao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MA290Filtro(lQuery)
Local lRet := .T.

DEFAULT lQuery	  := .F.

If !lQuery
	If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1]) .And. !(SB1->B1_TIPO$Trim(aOpcoes[8][1]))
		lRet := .F.
	EndIf
	If lRet .And. aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1]) .And. !(SB1->B1_GRUPO$Trim(aOpcoes[9][1]))
		lRet := .F.
	EndIf
	If lRet .And. (SB1->B1_COD < mv_par03 .Or. SB1->B1_COD > mv_par04)
		lRet := .F.
	EndIf			
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ cMTA290Fil = Retorno do Ponto de Entrada MTA290FIL - filtro para customizacao		     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. lMTA290Fil .And. Valtype(cMTA290Fil) == "C"
	If !(&(cMTA290Fil))
		lRet := .F.
	EndIf
EndIf

Return (lRet)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA290Process  ³ Autor ³Rodrigo de A Sartorio³ Data ³ 08/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Funcao que chama a regua e executa as funcoes de processamento³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Altera‡„o ³Guilherme Santos - 20/07/06                                   ³±±
±±³          ³Incluido tratamento para selecao de Filiais que deverao ser   ³±±
±±³          ³processadas.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA290                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MA290Process(aOpcoes,lEnd,lBatch,oCenterPanel,cCodIndic,lA290Proc,aPergunte)
// Variaveis utilizadas para processamento de calculo por empresa
Local aFilsCalc :={}
Local nForFilial:=0
Local cFilBack  :=cFilAnt 	//Guarda Filial Atual

Private lBat := lBatch

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿	
//³ Inicializa o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogIni( {},"MATA290" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogAtu("INICIO")

//Tratamento para selecao de Filiais para Processamento
If !lBat .And. !IsBlind()
	If !(oCenterPanel <> NIL)
		ProcRegua(nTotRegs)
	Else
		oCenterPanel:SetRegua1(nTotRegs)
	EndIf
	aFilsCalc := MatFilCalc(aOpcoes[10][1])
Else
	aFilsCalc := MatFilCalc(.F.,,.F.)
EndIf

For nForFilial := 1 to Len(aFilsCalc)
	If aFilsCalc[nForFilial,1]

		cFilAnt := aFilsCalc[nForFilial,2]	//Altera Filial Corrente
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("MENSAGEM",STR0051+cFilAnt,STR0051+cFilAnt) // "Inicio Filial: "
		
		If (oCenterPanel <> NIL)
			oCenterPanel:SaveLog(OemToAnsi(STR0040))
		EndIf
		
		If aOpcoes[1][1] == "x"         // Atualiza Consumo do Mes
			A290CalCon(oCenterPanel,lA290Proc,aPergunte)
			If lA290Proc // Indica se irá processar a nova tela de parâmetros
				A290CalGiro(lA290Proc, aPergunte) // Atualiza Giro de Estoque
			EndIf
		EndIf
		
		If aOpcoes[2][1] == "x"         // Calcula por Peso
			A290CalNor(aOpcoes[2][2],oCenterPanel,cCodIndic,lA290Proc,aPergunte)
		Else
			A290CalMin(aOpcoes[3][2],oCenterPanel,lA290Proc,aPergunte)    // Calculo pela Tendencia
		EndIf
		If aOpcoes[11][1] == "x"         // Processa o calculo do estoque de seguranca
			A290CalcES(aOpcoes[11][2],oCenterPanel,aOpcoes[11,3],lA290Proc,aPergunte)
		EndIf		
		If aOpcoes[4][1] == "x"         // Calculo Lote Economico
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Os parametros passados na funcao A290CalLot sao :            ³
			//³                                                              ³
			//³ aOpcoes[4][2] = Se deve calcular o ponto de pedido           ³
			//³ aOpcoes[6][1]-> Periodo Aquisicao A                          ³
			//³ aOpcoes[6][2]-> Periodo Aquisicao B                          ³
			//³ aOpcoes[6][3]-> Periodo Aquisicao C                          ³
			//³ aOpcoes[7][1]-> %Distribuicao     A                          ³
			//³ aOpcoes[7][2]-> %Distribuicao     B                          ³
			//³ aOpcoes[7][3]-> %Distribuicao     C                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A290CalLot(aOpcoes[4][2],aOpcoes[6][1],aOpcoes[6][2],aOpcoes[6][3],;
			aOpcoes[7][1],aOpcoes[7][2],aOpcoes[7][3],.T.,oCenterPanel,lA290Proc,aPergunte)
		Else  
			A290CalLot(aOpcoes[4][2],aOpcoes[6][1],aOpcoes[6][2],aOpcoes[6][3],;
			aOpcoes[7][1],aOpcoes[7][2],aOpcoes[7][3],.F.,oCenterPanel,lA290Proc,aPergunte)
		EndIf
		If aOpcoes[5][1] == "x"          // Ajusta Lote Economico
			A290AjuLot(aOpcoes[5][2],oCenterPanel,lA290Proc,aPergunte)    // Disponibilidade
		EndIf		
		If (oCenterPanel <> NIL)
			oCenterPanel:SaveLog(OemToAnsi(STR0041))
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o log de processamento             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ProcLogAtu("MENSAGEM",STR0052+cFilAnt,STR0052+cFilAnt) //"Final Filial: "
		
	EndIf
Next nForFilial

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogAtu("FIM")

cFilAnt := cFilBack	 //Retorna Filial Inicial

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA290VldPer  ³ Autor ³Rodrigo de A Sartorio³ Data ³ 08/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Funcao que valida os percentuais do Calculo ABC              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA290                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290VldPer(nA2,nB2,nC2,oChk8,oGet3,lEstSeg,nMesesES)
lRet:=.T.
If nA2+nB2+nC2 != 100
	Help(" ",1,"A290PERC")
	lRet:=.F.
EndIf

If lRet .And. (lEstSeg .And. nMesesES == 0)
	Aviso(STR0046,STR0047,{STR0048}) //"Atenção"#"Preencha a quantidade de meses para o cálculo do estoque de segurança."#"OK"
	lRet := .F.
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o    ³ CA290TROCA                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Autor     ³ Rodrigo de Almeida Sartorio              ³ Data ³ 09.01.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o ³ Troca marcador entre x e branco                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  Uso      ³ SigaEST - Advanced                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function CA290TROCA(nIt,aArray)
aArray[nIt,1] := !aArray[nIt,1]
Return aArray


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290SB2Aju³ Autor ³ Ricardo Berti         ³ Data ³ 31/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa query p/ arq.Saldos (SB2) para um produto         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExN1 := A290SB2Aju(ExpC1)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExC1 = Codigo do produto                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExN1 = Saldo Total do produto 			                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290SB2AJU(cProd, lA290Proc, aPergunte)

Local cQuery
Local cAliasQry := GetNextAlias()
Local cArea		:= Alias()
Local nQtde		:= 0
Local cArmDe    := ""
Local cArmAte   := ""

If lA290Proc
	cArmDe   := aPergunte[1]
	cArmAte  := aPergunte[2]
Else
	cArmDe   := mv_par01
	cArmAte  := mv_par02
EndIf

cQuery := "SELECT SUM(B2_QATU) B2TOTAL "
cQuery += "FROM "+RetSqlName("SB2")+" SB2 "
cQuery += "WHERE B2_FILIAL = '" + xFilial("SB2") + "' "
cQuery += "AND B2_COD  = '"+cProd+"' "
If !Empty(cArmDe) .Or. cArmAte <> "ZZ"
	cQuery += "AND B2_LOCAL >= '" + cArmDe + "' AND B2_LOCAL <= '" + cArmAte + "' "
EndIf			
cQuery += "AND SB2.D_E_L_E_T_ = ' ' "
cQuery := ChangeQuery(cQuery)
	
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)
dbGoTop()

TcSetField(cAliasQry,"B2TOTAL",  "N", TamSx3("B2_QATU")[1],  TamSx3("B2_QATU")[2] )

nQtde := B2TOTAL
dbCloseArea()

dbSelectArea(cArea)
Return (nQtde)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290SC1Aju³ Autor ³ Ricardo Berti         ³ Data ³ 31/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa query p/ solic.de compras(SC1) para um produto    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExN1 := A290SC1Aju(ExpC1)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExC1 = Codigo do produto                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExN1 = Total da Qtde. - Qtde.ja'entregue                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290SC1AJU(cProd, lA290Proc, aPergunte)

Local cQuery
Local cAliasQry := GetNextAlias()
Local dDataIni  := Ctod("01/"+StrZero(Month(dDataBase),2,0)+"/"+StrZero(Year(dDataBase),4,0))
Local dDataFim  := LastDay(dDataBase)
Local cArea		:= Alias()
Local nQtde		:= 0
Local cArmDe    := ""
Local cArmAte   := ""

If lA290Proc
	cArmDe   := aPergunte[1]
	cArmAte  := aPergunte[2]
Else
	cArmDe   := mv_par01
	cArmAte  := mv_par02
EndIf

cQuery := "SELECT SUM(C1_QUANT - C1_QUJE) C1TOTAL "
cQuery += "FROM "+RetSqlName("SC1")+" SC1 "
cQuery += "WHERE C1_FILIAL = '" + xFilial("SC1") + "' "
cQuery += "AND C1_PRODUTO   = '"+cProd+"' "
If !Empty(cArmDe) .Or. cArmAte <> "ZZ"
	cQuery += "AND C1_LOCAL >= '" + cArmDe + "' AND C1_LOCAL <= '" + cArmAte + "' "
EndIf			
cQuery += "AND C1_QUANT     > C1_QUJE "
cQuery += "AND C1_DATPRF   >= '" + DTOS(dDataIni)	+ "' "
cQuery += "AND C1_DATPRF   <= '" + DTOS(dDataFim)	+ "' "
cQuery += "AND SC1.D_E_L_E_T_ = ' ' "
cQuery := ChangeQuery(cQuery)
	
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)
dbGoTop()

TcSetField(cAliasQry,"C1TOTAL",  "N", TamSx3("C1_QUANT")[1], TamSx3("C1_QUANT")[2] )

nQtde := C1TOTAL
dbCloseArea()

dbSelectArea(cArea)
Return (nQtde)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A290SC7Aju³ Autor ³ Ricardo Berti         ³ Data ³ 31/08/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa query p/ pedidos de compras(SC7) para um produto  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExN1 := A290SC7Aju(ExpC1)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExC1 = Codigo do produto                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExN1 = Total da Qtde. - Qtde.ja'entregue                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A290SC7AJU(cProd, lA290Proc, aPergunte)

Local cQuery
Local cAliasQry := GetNextAlias()
Local dDataIni  := Ctod("01/"+StrZero(Month(dDataBase),2,0)+"/"+StrZero(Year(dDataBase),4,0))
Local dDataFim  := LastDay(dDataBase)
Local cArea		:= Alias()
Local nQtde		:= 0
Local cArmDe    := ""
Local cArmAte   := ""

If lA290Proc
	cArmDe   := aPergunte[1]
	cArmAte  := aPergunte[2]
Else
	cArmDe   := mv_par01
	cArmAte  := mv_par02
EndIf

cQuery := "SELECT SUM(C7_QUANT - C7_QUJE) C7TOTAL "
cQuery += "FROM "+RetSqlName("SC7")+" SC7 "
cQuery += "WHERE C7_FILIAL = '" + xFilial("SC7") + "' "
cQuery += "AND C7_PRODUTO   = '"+cProd+"' "
If !Empty(cArmDe) .Or. cArmAte <> "ZZ"
	cQuery += "AND C7_LOCAL >= '" + cArmDe + "' AND C7_LOCAL <= '" + cArmAte + "' "
EndIf			
cQuery += "AND C7_QUANT     > C7_QUJE "
cQuery += "AND C7_DATPRF   >= '" + DTOS(dDataIni)	+ "' "
cQuery += "AND C7_DATPRF   <= '" + DTOS(dDataFim)	+ "' "
cQuery += "AND C7_RESIDUO   = ' ' "
cQuery += "AND SC7.D_E_L_E_T_ = ' ' "
cQuery := ChangeQuery(cQuery)
	
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)
dbGoTop()

TcSetField(cAliasQry,"C7TOTAL",  "N", TamSx3("C7_QUANT")[1], TamSx3("C7_QUANT")[2] )

nQtde := C7TOTAL
dbCloseArea()

dbSelectArea(cArea)
Return (nQtde)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MT290Perg ³ Autor ³Ricardo Berti         ³ Data ³04/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega o 'pergunte' para o grupo do programa              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MT290Perg()  	                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290               	 	                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MT290Perg()

Pergunte("MTA290",.T.)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a290MontCfg ³ Autor ³ Andre Anjos	          ³ Data ³ 17/12/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta painel de configuracoes para a NewProcess                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a290MontCfg(oCenterPanel,lCalc,lFilial,nCalculo,nIncre,nMeses,lLote,lPedido,lDispoF,nDispof)
Local oChk1,oChk2,oChk3,oChk4,oChk5,oChk6,oUsado,oGet1,oGet2,oGet3,oGetDisp

@ 05,05 TO 80,140 LABEL OemToAnsi(STR0032) OF oCenterPanel  PIXEL //Opcoes de Processamento
@ 15,10 CHECKBOX oChk1 VAR lCalc PROMPT OemToAnsi(STR0005) SIZE 90, 10 OF oCenterPanel PIXEL//"Atualiza‡„o do Consumo do Mˆs"
@ 25,10 CHECKBOX oChk2 VAR lFilial PROMPT OemToAnsi(STR0027) SIZE 90, 10 OF oCenterPanel PIXEL//"Seleciona Filial"
@ 35,10 CHECKBOX oChk3 VAR lLote PROMPT OemToAnsi(STR0011) SIZE 80, 10 OF oCenterPanel PIXEL//"C lculo do Lote Econ“mico"
@ 45,10 CHECKBOX oChk4 VAR lPedido PROMPT OemToAnsi(STR0012) SIZE 80, 10 OF oCenterPanel PIXEL//"C lculo do Ponto de Pedido"
@ 55,10 CHECKBOX oChk6 VAR lPedido PROMPT OemToAnsi(STR0043) SIZE 100, 10 OF oCenterPanel PIXEL//"Calculo do Estoque de Seguranca"
@ 65,10 CHECKBOX oChk5 VAR lDispoF PROMPT OemToAnsi(STR0013) SIZE 90,10 ON CLICK (If(lDispoF,oGetDisp:Enable(),oGetDisp:Disable())) OF oCenterPanel PIXEL//"Ajusta Lote Econ“mico pela dispo-
//@ 65,10 Say OemToAnsi(STR0044) SIZE 80,10 OF oCenterPanel PIXEL//"Considerando consumo dos últimos"
//@ 65,90 MSGET oGet3 Var nDispoF Picture "@E 999" VALID nDispoF > 0 SIZE 25,10 OF oCenterPanel PIXEL; oGetDisp:Disable()

//@ 75,10 CHECKBOX oChk5 VAR lDispoF PROMPT OemToAnsi(STR0013) SIZE 90, 10 ON CLICK (If(lDispoF,oGetDisp:Enable(),oGetDisp:Disable())) OF oCenterPanel PIXEL//"Ajusta Lote Econ“mico pela dispo-
@ 72,20 Say OemToAnsi(STR0014) SIZE 80,10 OF oCenterPanel PIXEL//"nibilidade financeira"
@ 65,90 MSGET oGetDisp Var nDispoF Picture "@E 99,999,999,999" VALID nDispoF > 0 SIZE 45,10 OF oCenterPanel PIXEL; oGetDisp:Disable()

@ 05,170 TO 80,310 LABEL OemToAnsi(STR0006) OF oCenterPanel  PIXEL    //"C lculos"
@ 20,175 RADIO oUsado VAR nCalculo 3D SIZE 70,10 PROMPT OemToAnsi(STR0007),OemToAnsi(STR0008) OF oCenterPanel PIXEL      											//"Por Peso"###"Pela Tendˆncia"
oUsado:bChange := { || IIF(nCalculo=1,(oGet1:Enable(),oGet2:Disable()),(oGet1:Disable(),oGet2:Enable())) }
@ 45,177 Say OemToAnsi(STR0009) SIZE 40,10 OF oCenterPanel PIXEL    																													//"Incremento:"
@ 43,240 MSGET oGet1 VAR nIncre When IIF(nCalculo=1,.T.,.F.) Picture "999" SIZE 15,10 OF oCenterPanel PIXEL
@ 60,177 Say OemToAnsi(STR0010) SIZE 60,10 OF oCenterPanel PIXEL     																												//"N£mero de Meses:"
@ 58,240 MSGET oGet2 VAR nMeses When IIF(nCalculo=2,.T.,.F.) Picture "999" SIZE 15,10 VALID (nMeses < 13 .And. nMeses > 0) OF oCenterPanel PIXEL
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a290MontABC ³ Autor ³ Andre Anjos	          ³ Data ³ 17/12/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta painel de curva ABC para a NewProcess                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a290MontABC(oCenterPanel,nA1,nB1,nC1,nA2,nB2,nC2,lGrvABC)
Local oChk1

@ 05,05 TO 73,180 LABEL OemToAnsi(STR0015) OF oCenterPanel PIXEL//"Classifica‡„o ABC"
@ 10,57 TO 58,94 LABEL "A" OF oCenterPanel  PIXEL
@ 10,95 TO 58,132 LABEL "B" OF oCenterPanel  PIXEL
@ 10,133 TO 58,170 LABEL "C" OF oCenterPanel  PIXEL
@ 18,10 Say OemToAnsi(STR0016) SIZE 45,10 OF oCenterPanel PIXEL   //"Per¡odo de"
@ 25,10 Say OemToAnsi(STR0017) SIZE 45,10 OF oCenterPanel PIXEL   //"Aquisi‡„o(meses)"
@ 38,10 Say OemToAnsi(STR0018) SIZE 45,10 OF oCenterPanel PIXEL   //"Distribui‡„o"
@ 45,10 Say OemToAnsi(STR0019) SIZE 45,10 OF oCenterPanel PIXEL   //"Percentual (%)"
@ 20,63 MSGET nA1 Picture "99.9" VALID nA1 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 20,100 MSGET nB1 Picture "99.9" VALID nB1 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 20,139 MSGET nC1 Picture "99.9" VALID nC1 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 40,63 MSGET nA2 Picture "99.9" VALID nA2 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 40,100 MSGET nB2 Picture "99.9" VALID nB2 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 40,139 MSGET nC2 Picture "99.9" VALID nC2 >0 SIZE 17,10 OF oCenterPanel PIXEL
@ 60,10 CHECKBOX oChk1 VAR lGrvABC PROMPT OemToAnsi(STR0025) SIZE 74,10 OF oCenterPanel PIXEL

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a290MontFil ³ Autor ³ Andre Anjos	          ³ Data ³ 17/12/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta painel de filtros para a NewProcess                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a290MontFil(oCenterPanel,lGrpVazio)
Local oQual,oQual2,oChkQual,oChkQual2,oChkGrpVz
Local lQual, lQual2

@ 05,05 TO 110,290 LABEL OemToAnsi(STR0031) OF oCenterPanel PIXEL//"Filtros
If Len(aTipo) > 0
	@ 15,15 CHECKBOX oChkQual  VAR lQual  PROMPT OemToAnsi(STR0024) SIZE 50, 10 OF oCenterPanel PIXEL ON CLICK (AEval(aTipo , {|z| z[1] := If(z[1]==.T.,.F.,.T.)}), oQual:Refresh(.F.)) //"Inverter Selecao"
	oQual := TCBrowse():New(25,15,100,81,,,,oCenterPanel,,,,,{||aTipo:=ca290troca(oQual:nAt,aTipo),oQual:Refresh()},,,,,,,.T.,,.T.,,.F.,,)	
	oQual:SetArray(aTipo)

	oQual:AddColumn(TCColumn():New("",{|| If(aTipo[oQual:nAt,1],oOk,oNo)},,,,"LEFT",,.T.,.F.,,,,.T.,))
	oQual:AddColumn(TCColumn():New(OemToAnsi(STR0020),{|| aTipo[oQual:nAt,2]},,,,"LEFT",,.F.,.F.,,,,.F.,))
	oQual:Refresh()
EndIf
If Len(aGrupo) > 0
	@ 15,130 CHECKBOX oChkQual2 VAR lQual2 PROMPT OemToAnsi(STR0024) SIZE 50, 10 OF oCenterPanel PIXEL ON CLICK (AEval(aGrupo, {|z| z[1] := If(z[1]==.T.,.F.,.T.)}),oQual2:Refresh(.F.)) //"Inverter Selecao"
	@ 15,200 CHECKBOX oChkGrpVz VAR lGrpVazio PROMPT STR0042 SIZE 90, 10 OF oCenterPanel PIXEL
	oQual2 := TCBrowse():New(25,130,150,81,,,,oCenterPanel,,,,,{||aGrupo:=ca290troca(oQual2:nAt,aGrupo),oQual2:Refresh()},,,,,,,.T.,,.T.,,.F.,,)	
	oQual2:SetArray(aGrupo)
	
	oQual2:AddColumn(TCColumn():New("",{|| If(aGrupo[oQual2:nAt,1],oOk,oNo)},,,,"LEFT",,.T.,.F.,,,,.T.,))
	oQual2:AddColumn(TCColumn():New(OemToAnsi(STR0021),{|| aGrupo[oQual2:nAt,2]},,,,"LEFT",,.F.,.F.,,,,.F.,))
	oQual2:Refresh()
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a290PrepPrc ³ Autor ³ Andre Anjos	          ³ Data ³ 17/12/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Prepara variaveis para processamento		                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a290PrepPrc(nIncre,nMeses,lPedido,lDispoF,nDispoF,nA1,nA2,nB1,nB2,nC1,nC2,lFilial,lCalc,nCalculo,lLote,lGrpVazio,lEstSeg,nMesesES,lConsumo)

Local i, nArr
Local aOpt[11][3]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava Atualizacao do Consumo do Mes no array³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCalc
	aOpt[1,1]:= "x"
Else
	aOpt[1,1]:=" "
EndIf
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava Tipo de Calculo e valor p/ formula de calculo no array³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nCalculo=1
	aOpt[2,1]:="x"
	aOpt[2,2]:=nIncre
	aOpt[3,1]:=" "
	aOpt[3,2]:=0
Else
	aOpt[2,1]:=" "
	aOpt[2,2]:=0
	aOpt[3,1]:="x"
	aOpt[3,2]:=nMeses
EndIf
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava Calculo do Lote Economico no array³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLote
	aOpt[4,1]:="x"
	If lPedido
		aOpt[4,2]:="x"
	EndIf
Else
	If lPedido
		aOpt[4,2]:="x"
	Else
		aOpt[4,2]:=" "
	EndIf
	aOpt[4,1]:=" "
EndIf

	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava disponibilidade financeira no array³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lDispoF
	aOpt[5,1]:="x"
	aOpt[5,2]:=nDispoF
Else
	aOpt[5,1]:=" "
	aOpt[5,2]:=0
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava Periodos de Aquisicao no array³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOpt[6,1]:=nA1
aOpt[6,2]:=nB1
aOpt[6,3]:=nC1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava percentuais no array      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOpt[7,1]:=nA2
aOpt[7,2]:=nB2
aOpt[7,3]:=nC2

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Move aTipo p/aOpt               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOpt[8][1]:=""
nArr:=0
For i:=1 To Len(aTipo)
	If aTipo[i][1]
		nArr++
		aOpt[8][1] := aOpt[8][1]+SubStr(aTipo[i,2],1,2)+"|"
	EndIf
Next i
If nArr == Len(aTipo)
	aOpt[8][1]:="**"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Move aGrupo p/aOpt              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOpt[9][1]:=""
nArr:=0
For i:=1 To Len(aGrupo)
	If aGrupo[i][1]
		nArr++
		aOpt[9][1] := aOpt[9][1]+SubStr(aGrupo[i,2],1,4)+"|"
	EndIf
Next i
If nArr == Len(aGrupo) .And. !Empty(aOpt[9][1])
	aOpt[9][1] := IIf(lGrpVazio, "**", aOpt[9][1])
Else
	aOpt[9][1] := IIf(lGrpVazio .And. Len(aGrupo)>0, aOpt[9][1]+Space(Len(SB1->B1_GRUPO))+"|", aOpt[9][1])
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa ou nao, selecao de Filiais no Array    ³
//³ Se nao ativar, processa somente filial atual ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aOpt[10,1] := lFilial

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa ou nao, estoque de segurança por media ³
//³ de consumo ou pela previsão de vendas        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lEstSeg
	aOpt[11,1] := "x"
	aOpt[11,2] := nMesesES
	aOpt[11,3] := lConsumo
EndIf

Return aOpt

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A290CalcES³ Autor ³ Rodrigo Toledo        ³ Data ³ 16/12/2011 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cálculo e atualizacao do estoque de seguranca				    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 = numero de meses                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA290                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/                      
Static Function A290CalcES(nQtMeses,oCenterPanel,lConsumo,lA290Proc,aPergunte)
Local lQuery   	:= .F.
Local cAliasSB1	:= "SB1" 
Local cFormula  := ""
Local cTabela   := SuperGetMV("MV_ARQPROD",.F.,"SB1")
Local nX      	:= 0
Local cData	 	:= DToC(dDataBase)
Local nTot		:= 0
Local nESPE		:= 0
Local lA290PEES := 	ExistBlock("A290PEES")
Static nQtMeses2:= 0

Local cOpcoes := ""
Local cQuery := ""
    
Local cProdDe   := ""
Local cProdAte  := ""

If lA290Proc
	cProdDe  := aPergunte[3]
	cProdAte := aPergunte[4]
Else
	cProdDe  := mv_par03
	cProdAte := mv_par04
EndIf
    
Default lConsumo := .F.

dbSelectArea("SB1")
dbSetOrder(1)
lQuery   := .T.
cAliasSB1 := GetNextAlias()
cQuery := "SELECT SB1.B1_FILIAL,SB1.B1_COD,SB1.B1_ESTSEG,SB1.R_E_C_N_O_ RECNOSB1, "
If lConsumo
	cQuery += "SB3.B3_Q01,SB3.B3_Q02,SB3.B3_Q03,SB3.B3_Q04,SB3.B3_Q05,SB3.B3_Q06,SB3.B3_Q07, "
	cQuery += "SB3.B3_Q08,SB3.B3_Q09,SB3.B3_Q10,SB3.B3_Q11,SB3.B3_Q12 "
Else
	cQuery += " SUM(C4_QUANT) AS TOTSC4 "
EndIf
cQuery += "FROM " + RetSqlName("SB1") + " SB1 "
If lConsumo
	cQuery += "LEFT JOIN " + RetSqlName("SB3") + " SB3 ON  SB3.B3_FILIAL = '" + xFilial("SB3") + "' "
	cQuery += "AND SB3.B3_COD = SB1.B1_COD AND SB3.D_E_L_E_T_ = ' ' "         
Else
cQuery += "LEFT JOIN " + RetSqlName("SC4") + " SC4 ON  SC4.C4_FILIAL = '" + xFilial("SC4") + "' "
	cQuery += "AND SC4.C4_PRODUTO = SB1.B1_COD AND SC4.D_E_L_E_T_ = ' ' "         
Endif
cQuery += "WHERE SB1.B1_FILIAL = '" + xFilial("SB1") + "' "
cQuery += "AND SB1.B1_COD >= '" + cProdDe + "' AND SB1.B1_COD <= '" + cProdAte + "' "
If !lConsumo
	cQuery += " AND SC4.C4_DATA BETWEEN '" +DToS(dDataBase) +"' AND '" +DToS(dDataBase + (nQtMeses * 30)) +"'"
EndIf
If aOpcoes[8][1] != "**" .And. !Empty(aOpcoes[8][1])
      		cOpcoes := Trim(aOpcoes[8][1])
         	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
      		cQuery += "AND SB1.B1_TIPO IN ('"+StrTran(cOpcoes,"|","','")+"') "
EndIf
If aOpcoes[9][1] != "**" .And. !Empty(aOpcoes[9][1])
      		cOpcoes := Trim(aOpcoes[9][1])
         	cOpcoes := Substr(cOpcoes,1,Len(cOpcoes)-1)
			cQuery += "AND " + MontaGrp("SB1","B1_GRUPO",cOpcoes)
EndIf
cQuery += "AND SB1.D_E_L_E_T_ = ' ' "
If !lConsumo
      		cQuery += "GROUP BY SB1.B1_FILIAL,SB1.B1_COD,SB1.B1_ESTSEG,SB1.R_E_C_N_O_"
EndIf      
cQuery := ChangeQuery(cQuery)     
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB1,.T.,.T.)
dbGoTop()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ monta a formula de consumos medios  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFormula := ""
If lConsumo
	nQtMeses2:=nQtMeses
	For nX := 1 To nQtMeses2
   		cMes := StrZero((Month(CToD(cData)) - nX),2)
    	If cMes == "00"
			nX := 0
    		nQtMeses2 := nQtMeses2 - Month(CToD(cData))
    		cMes := "12"
    		cData := "01/" + cMes + "/" + StrZero((Year(dDataBase) - 1),4)
		EndIf   
    	If nX > nQtMeses2
    		Exit
    	EndIf
		cFormula += "B3_Q"+cMes+"+"
	Next
   cFormula := "(" + SubStr(cFormula,1,Len(cFormula)-1) + ")"
EndIf

SB3->(dbSetOrder(1))
SB5->(dbSetOrder(1))
SC4->(dbSetOrder(1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Realiza o calculo do estoque de seguranca   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While (cAliasSB1)->(!EOF()) .And. (lQuery .Or. (cAliasSB1)->(B1_FILIAL+B1_COD) <= xFilial("SB1")+cProdAte)
	SB5->(dbSeek(xFilial("SB5")+(cAliasSB1)->B1_COD))
	
	If lQuery
		If lConsumo
      		nMC := (cAliasSB1)->(&cFormula / (nQtMeses * 30))
		Else
      		nMC := (cAliasSB1)->(TOTSC4 / (nQtMeses * 30))
		EndIf
	Else 
		If lConsumo
         	If SB3->(dbSeek(xFilial("SB3")+(cAliasSB1)->B1_COD))
         		nMC := SB3->(&cFormula / (nQtMeses * 30))
    		EndIf                                                 
		Else
			SC4->(dbSeek(xFilial("SC4")+(cAliasSB1)->B1_COD+DToS(dDataBase),.T.))
	   		While !SC4->(EOF()) .And. SC4->(C4_FILIAL+C4_PRODUTO) == xFilial("SB1")+(cAliasSB1)->B1_COD .And.;
	   									SC4->C4_DATA <= (dDataBase + (nQtMeses * 30))
   				nTot += SC4->C4_QUANT
   				SC4->(dbSkip())
   			EndDo
   			nMC := nTot / (nQtMeses * 30)
		EndIf
	EndIf

	nCalcES := Round(nMC * SB5->B5_DIASES,0)

	If lA290PEES   
    	nESPE:= Execblock("A290PEES",.F.,.F.,{(cAliasSB1)->B1_FILIAL,(cAliasSB1)->B1_COD,(cAliasSB1)->B1_ESTSEG,nCalcES,nMC})
		If valtype(nESPE) = 'N' .and. nESPE <> NIL
			nCalcES := nESPE
		Endif
	EndIF
	
	If cTabela == "SBZ"
    	If SBZ->(dbSeek(xFilial("SBZ")+(cAliasSB1)->B1_COD))
        	RecLock("SBZ",.F.)
            Replace BZ_ESTSEG With nCalcES
			MsUnlock()
		EndIf
	Else
    	If SB1->(dbSeek(xFilial("SB1")+(cAliasSB1)->B1_COD))
        	RecLock("SB1",.F.)
            Replace B1_ESTSEG With nCalcES
            MsUnlock()
		EndIf
	EndIf
	(cAliasSB1)->(dbSkip())

	If !lBat
   		If oCenterPanel <> NIL
      		oCenterPanel:IncRegua1(OemToAnsi(STR0039))
		Else
      		IncProc()
		EndIf
	EndIf
EndDo

If lQuery
   (cAliasSB1)->(dbCloseArea())
EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} MontaGrp
Função para tratar montagem da condição IN da Query
para não ultrapassar o limite de 1000

@author Leonardo Kichitaro
@since 06/10/2021
/*/
//-------------------------------------------------------------------
Static Function MontaGrp(cAlias,cCampo,cGrupos)

Local cRetWhere := cAlias+"."+cCampo+" IN ("
Local cStrGRP	:= cGrupos
Local cFimCond	:= " "
Local nPos		:= 0
Local nQtdGroup	:= 0

nPos := At("|",cStrGRP+'|')
While nPos > 0
	If nQtdGroup > 0
		cRetWhere += ","
	EndIf
	cRetWhere += "'"+SubStr(cStrGRP,1,nPos-1)+"'"
	cStrGRP := SubStr(cStrGRP,nPos+1)
	nQtdGroup++
	If nQtdGroup == 1000 //Limite da condição IN no Oracle
		cRetWhere := "(" + cRetWhere
		cRetWhere += ") OR "
		cRetWhere += cAlias+"."+cCampo+" IN ("
		cFimCond += ") "
		nQtdGroup := 0
	EndIf
	nPos := At("|",cStrGRP)
	If nPos == 0 .And. Len(cStrGRP) > 0
		If nQtdGroup > 0
			cRetWhere += ","
		EndIf
		cRetWhere += "'"+SubStr(cStrGRP,1)+"'"
	EndIf
EndDo

cRetWhere += ")" + cFimCond

Return cRetWhere

//-------------------------------------------------------------------
/*/{Protheus.doc} A290NewScr
Tela nova de Cálculo de Consumo e Indicadores
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   15/10/2025
@version P12
@return cCodIndic - Código do indicador para cálculo
/*/
//-------------------------------------------------------------------
Static Function A290NewScr()
	Local aArea         As Array
	Local nCorFundo     As Numeric
	Local nJanAltura    As Numeric
	Local nJanLargur    As Numeric
	Local cJanTitulo    As Character
	Local lAutPixels    As Logical
	Local lCentraliz    As Logical
	Local nObjLinha     As Numeric
	Local nObjColun     As Numeric
	Local nObjLargu     As Numeric
	Local nObjAltur     As Numeric
	Local cFontNome     As Character
	Local cCodIndic	    As Character
	Local oFont         As Object
	Local oDialog       As Object
	Local oBtnParam     As Object
	Local cBtnParam     As Character
	Local bBtnParam     As CodeBlock
	Local oBtnFiltro    As Object
	Local cBtnFiltro    As Character
	Local bBtnFiltro    As CodeBlock
	Local oBtnOk        As Object
	Local cBtnOk        As Character
	Local bBtnOk        As CodeBlock
	Local oBtnCancelar  As Object
	Local cBtnCancelar  As Character
	Local bBtnCancelar  As CodeBlock
	Local lLowered      As Logical
	Local lRaised       As Logical
	Local lCentered	    As Logical
	Local cTexto        As Character
	Local oSX1          As Object

	oSX1 := FWSX1Util():New()
	oSX1:AddGroup("MTA290")
	oSX1:SearchGroup()
	aPerg := oSX1:GetGroup("MTA290")
	lCustomSX1 := Len(aPerg) > 0 .And. Len(aPerg[2]) > 6

	If lCustomSX1 //Se existir pesquisa customizada no pergunte MTA290, atribui função ao F12
		cMensagem := STR0061 //"As seis perguntas padronizadas foram incorporadas no cadastro de parâmetros de consumo e indicadores. Apenas as perguntas customizadas serão consideradas no processamento."
		cTitulo   := STR0062 //"Pergunta customizada"
		SetKey( VK_F12 ,{|| MsgInfo(cMensagem, cTitulo), Pergunte("MTA290",.T.)})
	EndIf

	aArea         := FWGetArea()
	nCorFundo     := RGB(255, 255, 255)
	nJanAltura    := 355
	nJanLargur    := 606
	cJanTitulo    := STR0053 //"Cálculo de Consumo e Indicadores"
	lAutPixels    := .T.
	lCentraliz    := .T.
	nObjLinha     := 0
	nObjColun     := 0
	nObjLargu     := 0
	nObjAltur     := 0
	cFontNome     := 'Tahoma'
	cCodIndic     := ''
	oFont         := TFont():New(cFontNome, , -12)
	oDialog       := Nil
	oBtnParam     := Nil
	cBtnParam     := STR0054 //"Param."
	bBtnParam     := {|| Pergunte('MTA290A', .T.)}
	oBtnFiltro    := Nil
	cBtnFiltro    := STR0055 //"Filtro"
	bBtnFiltro    := {|| cMTA290Fil := ExecBlock("MTA290FIL",.F.,.F.)}
	oBtnOk        := Nil
	cBtnOk        := STR0048 //"Ok"
	bBtnOk        := {|| Pergunte('MTA290A', .F.), MTA290BTNOK(oDialog, @cCodIndic)}
	oBtnCancelar  := Nil
	cBtnCancelar  := STR0056 //"Cancelar"
	bBtnCancelar  := {|| oDialog:End()}
	lLowered      := .T.
	lRaised       := .T.
	lCentered     := .T.
	cTexto        := STR0057 //"Este programa tem como objetivo gerar automaticamente os cálculos de consumo e indicadores."

	//Cria a dialog
	oDialog := TDialog():New(0, 0, nJanAltura, nJanLargur, cJanTitulo, , , , , , nCorFundo, , , lAutPixels)
		//Painel
		nObjLinha := 010
		nObjColun := 020
		nObjLargu := (nJanLargur / 2.2) - 002
		nObjAltur := (nJanAltura / 2.2) - 002 - 025
		oPanCampo := TPanel():New(nObjLinha, nObjColun, cTexto, oDialog, oFont, lCentered, /*uParam7*/, /*nClrText*/, nCorFundo, nObjLargu, nObjAltur, lLowered, lRaised)

		//Botão Parâmetro
		nObjLinha := 158
		nObjColun := if(lMta290Fil, 78, 134)
		nObjLargu := 50
		nObjAltur := 15
		oBtnParam  := TButton():New(nObjLinha, nObjColun, cBtnParam, oDialog, bBtnParam, nObjLargu, nObjAltur, , oFont, , lAutPixels)

		//Botão Filtro (Caso exista o P.E MTA290FIL)
		If lMta290Fil
			nObjLinha := 158
			nObjColun := 134
			nObjLargu := 50
			nObjAltur := 15
			oBtnFiltro  := TButton():New(nObjLinha, nObjColun,  cBtnFiltro, oDialog, bBtnFiltro, nObjLargu, nObjAltur, , oFont, , lAutPixels)
		EndIf

		//Botão Ok
		nObjLinha := 158
		nObjColun := 190
		nObjLargu := 50
		nObjAltur := 15
		oBtnOk  := TButton():New(nObjLinha, nObjColun, cBtnOk, oDialog, bBtnOk, nObjLargu, nObjAltur, , oFont, , lAutPixels)

		//Botão Cancelar
		nObjLinha := 158
		nObjColun := 245
		nObjLargu := 50
		nObjAltur := 15
		oBtnCancelar  := TButton():New(nObjLinha, nObjColun, cBtnCancelar, oDialog, bBtnCancelar, nObjLargu, nObjAltur, , oFont, , lAutPixels)

	//Ativa e exibe a janela
	oDialog:Activate(, , , lCentraliz, , , ,)

	FWRestArea(aArea)
Return cCodIndic

//-------------------------------------------------------------------
/*/{Protheus.doc} MTA290BTNOK
Função do botão OK da tela de parâmetros
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   20/10/2025
@version P12
@param oDialog - Objeto diálogo
@param cCodIndic - Código do indicador para cálculo
/*/
//-------------------------------------------------------------------
Static Function MTA290BTNOK(oDialog, cCodIndic)
	Local aArea As Array

	If Empty(MV_PAR01)
		FWAlertError(STR0060, STR0046) //"Selecione um código válido de parâmetro para cálculo." / "Atenção"

	ElseIf ExistCpo("D4R", MV_PAR01, 1)
		aArea := FWGetArea()

		cCodIndic := Alltrim(MV_PAR01)

		FWRestArea(aArea)

		oDialog:End()
	EndIf
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} A290Proc
Verifica se existe registro de indicadores cadastrado na D4R
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   20/10/2025
@version P12
@return  lRet - .T. se houver registros, .F. caso contrário
/*/
//-------------------------------------------------------------------
Static Function A290Proc()
	Local aArea      As Array
	Local oQuery     As Object
	Local lRet       As Logical
	Local cQuery     As Character
	Local cAliasQry  As Character
	Local nParam     As Numeric

	aArea     := FWGetArea()
	cAliasQry := GetNextAlias()
	lRet      := .F.
	
	//Verifica se a tabela D4R existe e tem registro
	If AliasInDic('D4R')

		cQuery := " SELECT D4R.D4R_COD"
		cQuery +=   " FROM " + RetSqlName("D4R") + " D4R"
		cQuery +=  " WHERE D4R.D4R_FILIAL = ?" //#1
		cQuery +=    " AND D4R.D_E_L_E_T_ = ?" //#2

		oQuery := FWExecStatement():New(cQuery)
	
		//Binding dos parâmetros na ordem
		nParam := 1
		oQuery:SetString(nParam++, FWxFilial("D4R")) //#1
		oQuery:SetString(nParam++, ' ') //#2

		oQuery:OpenAlias(cAliasQry)

		If !(cAliasQry)->(Eof())
			lRet := .T.
		EndIf

		(cAliasQry)->(DbCloseArea())
	EndIf

	FWRestArea(aArea)
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A290Load
Carrega os parâmetros da tabela D4R (Indicadores Lote Eonômico)
D4S (Indicadores Tipo de Materiais) e D4T (Indicadores Grupo de Materiais)
para as variáveis de memória e array de opções utilizadas pela MATA290
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   20/10/2025
@version P12
@param cCodIndic - Código do indicador para cálculo (D4R_COD)
@param lBat - Indica se está em processamento batch (.T. ou .F.)
@param lSchedule - Indica se está em processamento via schedule (.T. ou .F.)
@param aPergunte - Array com os parâmetros de pergunta para substituir o Pergunte (MTA290)
@return aParams - Array com os parâmetros carregados
/*/
//-------------------------------------------------------------------
Static Function A290Load(cCodIndic, lBat, lSchedule, aPergunte)
	Local aArea      As Array
	Local aParams    As Array

	aArea          := FWGetArea()
	aAreaD4R       := D4R->(FWGetArea())
	aParams        := Array(11, 3)
	aParams[1][1]  := Space(1)
	aParams[2][1]  := Space(1)
	aParams[2][2]  := 0
	aParams[3][1]  := 'x' //Cálculo pela Tendência
	aParams[3][2]  := D4R->D4R_NUMMES //Número de Meses
	aParams[4][1]  := Space(1)
	aParams[4][2]  := Space(1)
	aParams[5][1]  := Space(1)
	aParams[11][1] := Space(1)
	aParams[11][3] := .F.

	DbSelectArea('D4R')
	D4R->(DbSetOrder(1)) //D4R_FILIAL + D4R_COD

	If D4R->(DbSeek(FwxFilial('D4R') + cCodIndic))
		//Carrega o array aPergunte dos campos da D4R para substituir o Pergunte (MTA290)
		aAdd(aPergunte, D4R->D4R_LOCDE)       //Armazém de
		aAdd(aPergunte, D4R->D4R_LOCATE)      //Armazém até
		aAdd(aPergunte, D4R->D4R_PRDDE)       //Produto de
		aAdd(aPergunte, D4R->D4R_PRDATE)      //Produto até 
		aAdd(aPergunte, Val(D4R->D4R_ZRMDNG)) //Zera Md. Neg
		aAdd(aPergunte, Val(D4R->D4R_MOVINV)) //Cons Mov Inv
		aAdd(aPergunte, Val(D4R->D4R_PRDSGP)) //Analisa Produtos sem Grupo

		//--- 1. Mapeamento (D4R) para aOpcoes ---
		//[1][1]-> Atualiza Consumo do Mes (D4R_ATUCON é lógico, converte para "x" / " ")
		If D4R->D4R_ATUCON
			aParams[1][1] := 'x'
		EndIf

		//[10][1]-> Selecao de Filiais (MV_PAR02 1=Sim;2=Não)
		aParams[10][1] := lBat .Or. lSchedule .Or. IsBlind() .Or. MV_PAR02 == 1
		
		//lGrvABC (variável global/private) (D4R_GRVABC é lógico)
		lGrvABC := D4R->D4R_GRVABC
		
		//[2] / [3] -> Tipo de Cálculo (D4R_TPCALC: C: "1"=Peso, "2"=Tendência)
		If D4R->D4R_TPCALC == '1' //Por Peso (A290CalNor)
			aParams[2][1] := 'x'
			aParams[2][2] := D4R->D4R_TPINCR
			aParams[3][1] := ' '
			aParams[3][2] := 0
		EndIf

		//[4] -> Lote Econômico / Ponto de Pedido (D4R_CALLEC/D4R_CALCPP são lógicos)
		If D4R->D4R_CALLEC
			aParams[4][1] := 'x'
		EndIf

		If D4R->D4R_CALCPP
			aParams[4][2] := 'x'
		EndIf

		//[5] -> Ajuste Financeiro (D4R_AJUFIN é lógico)
		If D4R->D4R_AJUFIN
			aParams[5][1] := 'x'
		EndIf

		aParams[5][2] := D4R->D4R_VALFIN
		
		//[6] -> Períodos de Aquisição ABC
		aParams[6][1] := D4R->D4R_PERIOA
		aParams[6][2] := D4R->D4R_PERIOB
		aParams[6][3] := D4R->D4R_PERIOC
		
		//[7] -> Distribuição Percentual ABC
		aParams[7][1] := D4R->D4R_PERCTA
		aParams[7][2] := D4R->D4R_PERCTB
		aParams[7][3] := D4R->D4R_PERCTC

		//--- 2. Mapeamento (D4S/D4T) para Filtros (aOpcoes[8]/[9]) ---
		//[8][1] -> Tipos de Materiais (D4S_CTIPO)
		//D4S -> Indicadores Tipo de Materiais
		aParams[8][1] := A290GetDet(cCodIndic, 'D4S', 'D4S_CTIPO', lBat, lSchedule, aPergunte)
		
		//[9][1] -> Grupos de Materiais (D4T_CGRUPO)
		//D4T -> Indicadores Grupo de Materiais
		aParams[9][1] := A290GetDet(cCodIndic, 'D4T', 'D4T_CGRUPO', lBat, lSchedule, aPergunte)
		
		//--- 3. Estoque de Segurança (aOpcoes[11]) ---
		If D4R->D4R_CALCES
			aParams[11][1] := 'x'
		EndIf

		aParams[11][2] := D4R->D4R_MESSEG

		//Tipo: D4R_TPSEGU: "1"=Média Consumo (.T.), "2"=Previsão Venda (.F.)
		If D4R->D4R_TPSEGU == '1'
			aParams[11][3] := .T.
		EndIf
	EndIf

	FWRestArea(aAreaD4R)
	FWRestArea(aArea)
Return aParams

//-------------------------------------------------------------------
/*/{Protheus.doc} A290GetDet
Lê a tabela de detalhes (D4S ou D4T) e constrói a string de filtro 
com pipe ('CODIGO1|CODIGO2|')
@type    Function
@author  Paulo Dias Oliveira (oliveira.paulo)
@since   20/10/2025
@version P12
@param cCodIndic - Código do indicador (D4R_COD)
@param cAliasTab - Alias da tabela de detalhes (D4S ou D4T)
@param cCampoCod - Nome do campo de código no detalhe (D4S_CGRUPO ou D4T_CTIPO)
@param lBat      - Indica se está em processamento batch (.T. ou .F.)
@param lSchedule - Indica se está em processamento via schedule (.T. ou .F.)
@return cFilter - String do filtro ('CODIGO1|CODIGO2|')
/*/
//-------------------------------------------------------------------
Static Function A290GetDet(cCodIndic, cAliasTab, cCampoCod, lBat, lSchedule, aPergunte)
	Local cFilter   As Character
	Local aArea     As Array
	Local aAreaTab  As Array
	Local cFil      As Character
	Local nTotal    As Numeric
	Local cChave    As Character
	Local cQuery    As Character
	Local cAliasGrp As Character
	Local aRetSX5   As Array
	Local nAtivos   As Numeric
	Local nParam    As Numeric
	Local oGrupo    As Object
	Local lSemGrp   As Logical //Indica se consulta produtos sem grupo

	cFilter   := ''
	aArea     := FWGetArea()
	aAreaTab  := (cAliasTab)->(FwGetArea())
	cFil      := FwxFilial(cAliasTab)
	nTotal    := 0
	nAtivos   := 0
	nParam    := 1
	cChave    := cAliasTab + '_CINDIC'
	cAliasGrp := GetNextAlias()

	DbSelectArea(cAliasTab)
	&(cAliasTab)->(DbSetOrder(1))

	If &(cAliasTab)->(DbSeek(cFil + cCodIndic))
		While &(cAliasTab)->(!Eof()) .And. &(&(cAliasTab)->(cAliasTab + '_FILIAL')) + &(&(cAliasTab)->(cChave)) == cFil + cCodIndic
			nTotal++
			cFilter += AllTrim(&(&(cAliasTab)->(cCampoCod))) + '|'
			&(cAliasTab)->(DbSkip())
		EndDo
	EndIf

	//Tipo de materiais
	If cAliasTab == "D4S"
		//Carrega materiais
		aRetSX5 := FWGetSX5("02")
		nAtivos := Len(aRetSX5)

		//Se todos os itens possíveis no detalhe estiverem ativos, retorna '**' que todos foram selecionados
		If nTotal > 0 .And. nAtivos == nTotal .And. cAliasTab == "D4S"
			cFilter := '**'
		EndIf
	EndIf

	//Grupo de materiais
	If cAliasTab == 'D4T'
		//Carrega grupos
		cQuery := "SELECT COUNT(1) QTD"
		cQuery +=  " FROM " + RetSqlName("SBM") + " SBM"
		cQuery += " WHERE SBM.BM_FILIAL = ?"  //#1
		cQuery +=   " AND SBM.D_E_L_E_T_ = ?" //#2

		oGrupo := FWExecStatement():New(cQuery)

		oGrupo:SetString(nParam++, FWxFilial("SBM")) //#1
		oGrupo:SetString(nParam++, Space(1))         //#2

		oGrupo:OpenAlias(cAliasGrp)

		If !(cAliasGrp)->(EOF())
			nAtivos := (cAliasGrp)->(QTD)
		EndIf

		(cAliasGrp)->(DbCloseArea())

		lSemGrp := lBat .Or. lSchedule .Or. IsBlind() .Or. aPergunte[7] == 1 //Analisa Produtos Sem Grupo

		//Analisa Produtos Sem Grupo ? 1=Sim;2=Não
		If nAtivos == nTotal .And. !Empty(cFilter)
			If lSemGrp
				cFilter := '**'
			EndIf
		Else
			//O espaço em branco (Space(Len(SB1->B1_GRUPO))) é o código para 'sem grupo'
			If lSemGrp .And. nTotal > 0
				cFilter := cFilter + Space(Len(SB1->B1_GRUPO)) + '|'
			EndIf
		EndIf
	EndIf

	FWRestArea(aAreaTab)
	FWRestArea(aArea)
Return cFilter

//-------------------------------------------------------------------
/*/{Protheus.doc} SchedDef
Função para definição de parametros na tela de schedule

@type    Function
@author  Daniel Tonon
@since   28/10/2025
@version P12
/*/
//-------------------------------------------------------------------
Static Function SchedDef()
	Local aOrd   As Array
	Local aParam As Array

	aOrd   := {}

	aParam := { "P"      ,; // Tipo R para relatorio P para processo
	            "MTA290B",; // Nome do pergunte do relatorio, caso nao use passar ParamDef
	            ""       ,; // Alias
	            aOrd     ,; // Array de ordens
	            ""       ,; // Título (para Relatório)
	            ""       ,; // Nome do relatório
	            .F.      ,; // Lógico, indica se permite que o agendamento possa ser cadastrado como sempre ativo
	            .T.      ,; // Lógico, indica que o agendamento pode ser realizado por filiais
	}

Return aParam
