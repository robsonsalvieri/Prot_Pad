#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.stock.trackcosts.d4b.ch"

namespace totvs.protheus.backoffice.stock.trackcosts.d4b.controller

using namespace totvs.protheus.backoffice.stock.trackcosts.d4b.service
using namespace ac.BasicValidation.Controller

/*/{Protheus.doc} D4BController()
    Classe responsavel por receber as requisicoes dos parametros de filtro do Produto
    @type Class
    @author Adriano Vieira
    @since 12/01/2023
    @version 1.0
/*/
Class D4BController from acBasicValidationController

    public method New()
    public method version()
    public data cId         as character
    public data jPath       as object

    @Post("api/stock/trackcosts/kardex/v1/D4B")
    public method postInsertD4B()

    @Get("api/stock/trackcosts/kardex/v1/D4B/:id")
    public method getQueryD4B()

    @Put("api/stock/trackcosts/kardex/v1/D4B/:id")
    public method putUpdateD4B()

    @Delete("api/stock/trackcosts/kardex/v1/D4B/:id")
    public method deleteQueryD4B()

    @Get("api/stock/trackcosts/kardex/ESTR900/v1/filter")
    public method getListD4B()

    @Post("api/stock/trackcosts/kardex/ESTR900/v1/filter/deactivate")
    public method postDeactivateFilter()

EndClass

/*/{Protheus.doc} New()
    Metodo responsavel por instanciar a classe e iniciar variaveis
    @type Method
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
/*/
Method New() class D4BController
     _Super:New()
    ::cId         := ''
Return Self

/*/{Protheus.doc} postInsertD4B()
    Recebe os dados da requisicao de POST para efetuar o INSERT na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
    //Exemplo POSTMAN
    {
        "parameters": [
            {"parameter": "mv_par01","value": ["PROD005","PROD006","PROD007","PROD008"]},
            {"parameter": "mv_par02","value": ["PA","PI","MP","MO"]},
            {"parameter": "mv_par03","value": "20140701"},
            {"parameter": "mv_par04","value": "20220731"},
            {"parameter": "mv_par05","value": 1},
            {"parameter": "mv_par06","value": ["01","02"]},
            {"parameter": "mv_par07","value": 1},
            {"parameter": "mv_par08","value": 2},
            {"parameter": "mv_par09","value": 1},
            {"parameter": "mv_par10","value": ["0001","0002","0003","0004"]},
            {"parameter": "mv_par11","value": 1}
        ],
        "branches": [
            {
                "Code": "D MG 01 ",
                "Cgc": "53485215000106",
                "Description": "Filial BELO HOR                          "
            }
        ]
    }
/*/
Method postInsertD4B() class D4BController
    Local jBodyRequest  := JsonObject():New() as json
    Local jBodyResponse := JsonObject():New() as json
    Local jError        := JsonObject():New() as json
    Local aParameters                         as array
    Local aBranches                           as array
    Local cResponse                           as character
    Local nOperation := 3                     as numeric
    Local oD4BService                         as object

    oRest:setKeyHeaderResponse('Content-type','application/json')
    jBodyRequest:fromJson(oRest:getBodyRequest())

    aParameters := jBodyRequest:GetJsonObject("parameters")
    aBranches   := jBodyRequest:GetJsonObject("branches")
    
    oD4BService := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()
    
    oD4BService:postInsertD4B(aParameters,aBranches,nOperation)

    if Empty(oD4BService:cError)
        jBodyResponse["id"] := oD4BService:cId
        cResponse := jBodyResponse:toJson()
        oRest:setResponse(cResponse) 
    Else 
        oRest:setStatusCode(400)
        jError["error"] := oD4BService:cError
        cResponse := jError:toJson()
        oRest:setResponse(cResponse) 
    Endif

    FreeObj(jBodyRequest)
    FreeObj(jBodyResponse)

Return

/*/{Protheus.doc} putUpdateD4B()
    Recebe os dados da requisicao de PUT/PATCH para Efetua o UPDATE na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 26/01/2023
    @version 1.0
    //Exemplo POSTMAN
    {
        "parameters": [
            {"parameter": "mv_par06","value": ["01","02","03"]}
        ],
        "branches": [
            {
                "Code": "D MG 01 ",
                "Cgc": "53485215000106",
                "Description": "Filial BELO HOR                          "
            }
        ]
    }
/*/
Method putUpdateD4B() Class D4BController
    Local jBodyRequest  := JsonObject():New() as json
    Local jBodyResponse := JsonObject():New() as json
    Local jError        := JsonObject():New() as json
    Local aParameters                         as array
    Local aBranches                           as array
    Local cResponse                           as character
    Local nOperation := 4                     as numeric
    Local oD4BService                         as object

    oRest:setKeyHeaderResponse('Content-type','application/json')
    ::jPath := oRest:getPathParamsRequest()
    
    oD4BService := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()

    If ( ::jPath <> Nil )
        ::cId := ::jPath[ 'id' ]
        jBodyRequest:fromJson(oRest:getBodyRequest())

        aParameters := jBodyRequest:GetJsonObject("parameters")
        aBranches   := jBodyRequest:GetJsonObject("branches")
        

        oD4BService:putUpdateD4B(::cId,aParameters,aBranches,nOperation)

    Endif

    if Empty(oD4BService:cError)
        jBodyResponse["id"] := oD4BService:cId
        cResponse := jBodyResponse:toJson()
        oRest:setResponse(cResponse) 
    Else 
        oRest:setStatusCode(400)
        jError["error"] := oD4BService:cError
        cResponse := jError:toJson()
        oRest:setResponse(cResponse) 
    Endif

    FreeObj(jBodyRequest)
    FreeObj(jBodyResponse)

Return

/*/{Protheus.doc} getQueryD4B()
    Recebe os dados da requisicao de GET para Efetua a consulta na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 26/01/2023
    @version 1.0
    //Exemplo POSTMAN
    GET: http://localhost:1243/rest/api/stock/trackcosts/kardex/v1/D4B/1226f7b4eec31004023E
/*/
Method getQueryD4B() Class D4BController

    Local jBodyResponse     := JsonObject():New() as json
    Local jError            := JsonObject():New() as json
    Local jBodyGetResponse  := JsonObject():New() as json
    Local cRetornoD4B                             as character
    Local oD4BService                             as object

    oRest:setKeyHeaderResponse("Content-Type", "application/json")
    ::jPath := oRest:getPathParamsRequest()
    ::cId := ::jPath[ 'id' ]

    oD4BService := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()
    
    jBodyGetResponse := oD4BService:getQueryD4B(::cId)

    if Empty(oD4BService:cError)
        jBodyGetResponse := oD4BService:montaGet(jBodyGetResponse)
        jBodyResponse["parameters"] := jBodyGetResponse
        oRest:setResponse( jBodyResponse["parameters"] )
    Else 
        oRest:setStatusCode(400)
        jError["error"] := oD4BService:cError
        cRetornoD4B := jError:toJson()
        oRest:setResponse(cRetornoD4B) 
    Endif

Return

/*/{Protheus.doc} deleteQueryD4B()
    Recebe os dados da requisicao de PUT/PATCH para Efetua o UPDATE na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 26/01/2023
    @version 1.0
/*/
Method deleteQueryD4B() Class D4BController
    Local jBodyRequest  := JsonObject():New() as json
    Local jBodyResponse := JsonObject():New() as json
    Local jError        := JsonObject():New() as json
    Local cResponse                           as character
    Local oD4BService                         as object

    oRest:setKeyHeaderResponse('Content-type','application/json')
    ::jPath := oRest:getPathParamsRequest()

    oD4BService := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()

    If ( ::jPath <> Nil )
        ::cId := ::jPath[ 'id' ]
        oD4BService:deleteQueryD4B(::cId)
    Endif

    if Empty(oD4BService:cError)
        jBodyResponse["id"] := oD4BService:cId
        cResponse := jBodyResponse:toJson()
        oRest:setResponse(cResponse) 
    Else 
        oRest:setStatusCode(400)
        jError["error"] := oD4BService:cError
        cResponse := jError:toJson()
        oRest:setResponse(cResponse) 
    Endif

    FreeObj(jBodyRequest)
    FreeObj(jBodyResponse)

Return

/*/{Protheus.doc} getQueryD4B()

/*/
Method getListD4B() Class D4BController

    Local oDictionaryValidator  as Object
    Local jParams               as Object
    Local cData                 as character
    Local oStatus               as object
    Local oD4BService           as object

    oStatus := JsonObject():New()

    oDictionaryValidator := _Super:GetDictionaryValid(.T.)
    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    jParams       := oRest:getQueryRequest()


    If !oDictionaryValidator['bIsValid']
        oRest:setStatusCode(oDictionaryValidator['nErrorNumber'])
        oRest:setResponse(oDictionaryValidator['cErrorMsg'])
    Else
        oD4BService := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()
        cData := oD4BService:getListD4B(jParams)
        oRest:setResponse( cData )
    Endif

Return

/*/{Protheus.doc} postInsertD4B()
    Recebe os dados da requisicao de POST para efetuar o INSERT na tabela D4B
    @type  Method
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
    //Exemplo POSTMAN
    {
        "parameters": [
            {"parameter": "mv_par01","value": ["PROD005","PROD006","PROD007","PROD008"]},
            {"parameter": "mv_par02","value": ["PA","PI","MP","MO"]},
            {"parameter": "mv_par03","value": "20140701"},
            {"parameter": "mv_par04","value": "20220731"},
            {"parameter": "mv_par05","value": 1},
            {"parameter": "mv_par06","value": ["01","02"]},
            {"parameter": "mv_par07","value": 1},
            {"parameter": "mv_par08","value": 2},
            {"parameter": "mv_par09","value": 1},
            {"parameter": "mv_par10","value": ["0001","0002","0003","0004"]},
            {"parameter": "mv_par11","value": 1}
        ],
        "branches": [
            {
                "Code": "D MG 01 ",
                "Cgc": "53485215000106",
                "Description": "Filial BELO HOR                          "
            }
        ]
    }
/*/
Method postDeactivateFilter() class D4BController
    Local jBodyRequest  := JsonObject():New() as json
    Local jBodyResponse := JsonObject():New() as json
    Local jError        := JsonObject():New() as json
    Local oService      := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()
    Local cResponse     := ""

    oRest:setKeyHeaderResponse('Content-type','application/json')
    jBodyRequest       := oRest:getQueryRequest()

    If jBodyRequest:HasProperty("configuration") .and. !Empty(jBodyRequest:getJsonText("configuration"))
        oService:deactivateFilter(jBodyRequest)

        if Empty(oService:cError)
            jBodyResponse["configuration"]  := jBodyRequest:getJsonText("configuration")
            jBodyResponse["status"]         := .F.
            cResponse := jBodyResponse:toJson()
            oRest:setResponse(cResponse) 
        Else 
            oRest:setStatusCode(400)
            jError["error"] := oService:cError
            cResponse := jError:toJson()
            oRest:setResponse(cResponse) 
        Endif
    Else
        jError["error"] := STR0001
        cResponse := jError:toJson()
        oRest:setStatusCode(400)
        oRest:setResponse(cResponse) 
    Endif

    FreeObj(jBodyRequest)
    FreeObj(jBodyResponse)

Return

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class D4BController
    Local nVersion := 200
Return nVersion
