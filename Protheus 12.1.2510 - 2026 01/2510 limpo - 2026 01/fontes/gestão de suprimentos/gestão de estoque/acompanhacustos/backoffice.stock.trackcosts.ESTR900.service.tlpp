#include "tlpp-core.th"
#include "backoffice.stock.trackcosts.estr900.ch"

namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.service

using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.ESTR900A.service
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4A
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4B
using namespace totvs.protheus.backoffice.stock.trackcosts.d4b.service
using namespace ac.TableTempory.Repository

class ESTR900Service

    public data cError as character
    public data cEmpty as character
    
    public data aRules      as Array
    public data aBranches   as Array

    public data oParameters  as Object
    public data cTableName01 as character
    public data cTableName02 as character
    public data cTableName03 as character
    public data cTableName04 as character

    public data cIdJobAverageCost as character
    public data cIdConfiguration  as character
    public data cIdJobTotalizer   as character

    public data oThreads    as character
    public data lError      as logical

    public data cProducts   as character
    public data cMovements  as character

    public data oQuery  as object
    public data aFils   as array
    
    public method new()
    
    // api/stock/trackcosts/kardex/v1/id
    public method getMovements()
    private method getListMovementsColumns()
    private method getListQueryMovements()
    private method getListWhereMovements()

    // api/stock/trackcosts/kardex/v1/id
    public method getProducts()
    private method getListProductsColumns()
    private method getListQueryProducts()
    private method getListWhereProducts()
    
    // api/stock/trackcosts/kardex/v1/id
    public method getIdByConfig()
    private method getQuery()

    public method calculateESTR900()
    public method getStatus()

    // api/stock/trackcosts/kardex/v1/thread
    public method getThread()
    private method getThreadQuery()

    // Métodos acionados de forma assíncrona (Via Jobs)
    public method setESTR900()
    public method getSTRFromCF()

    public method IsBodyValidFromStatus()
    public method setBranches()
    public method setParameters()
    public method setRules()
    public method setQuery()
    public method getMovimentQuery()
    public method getProductQuery()
    public method getItemsDoneQueryPerThread()
    public method getItemByCode()
    public method getWarehouseByCode()
    public method addMovementType()
    public method rebootThread()
    public method containsWarehouseLocProc()
    public method version()
endclass

/*/{Protheus.doc} ESTR900Service:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method new() class ESTR900Service
     
    ::cError := ""
    ::cEmpty := ""
    ::aRules := {}
    ::aFils  := {}
    ::aBranches := {}
    ::oParameters := JsonObject():New()

    ::cTableName01 := ""
    ::cTableName02 := ""
    ::cTableName03 := ""
    ::cTableName04 := ""

    ::cIdJobAverageCost := ""
    ::cIdJobTotalizer := ""
    ::cIdConfiguration := ""
    ::oThreads := JsonObject():New()
    ::cProducts := ""
    ::cMovements := ""

return Self

/*/{Protheus.doc} ESTR900Service:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method calculateESTR900(jBody) class ESTR900Service
    
    Local lCusEmp       := .F.
    Local dDataIni      := Nil
    Local dDataFim      := Nil
    Local oRepository   := Nil
    Local jParam        := JsonObject():New()
    Local oFilters      := JsonObject():New()
    Local oD4BService   := totvs.protheus.backoffice.stock.trackcosts.d4b.service.D4BService():new()
    
    lCusEmp    := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A")) == "E"

    ::cError            := ""
    ::cEmpty            := ""
    ::cIdJobAverageCost := ""
    ::cIdConfiguration  := ""

    
    jParam["filter"] := "user eq '" + RETCODUSR() + "' and isactive eq 'T'"
    jParam["order"] := "D4B_DTEXEC DESC, D4B_HREXEC DESC"

    oFilters:FromJson(oD4BService:getListD4B(jParam))

    If Valtype(oFilters["items"]) == "A" .and. Len(oFilters["items"]) > 0
        ::cError := STR0005
    Else 
        oD4BService:postInsertD4B(jBody["parameters"], jBody["branches"], 3)

        ::cError := oD4BService:cError

        //Validação do corpo da requisição 
        If Empty(::cError)
            
            ::cIdJobAverageCost := Upper(SUBSTR(FWUUIDV1(), 0, 16))
            ::cIdConfiguration := oD4BService:cId
            ::cTableName01 := "A" + Upper(::cIdConfiguration) + "_01"
            ::cTableName02 := "A" + Upper(::cIdConfiguration) + "_02"

            oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()

            //Configuração das filiais para o padrão utilizada na MatFilCalc

            ::setBranches(jBody["branches"], lCusEmp)
            ::setParameters(jBody["parameters"])

            dDataIni      := ::oParameters:GetJsonObject("mv_par03")
            dDataFim      := ::oParameters:GetJsonObject("mv_par04")
    
            oRepository:startAsyncESTR900(::cIdJobAverageCost, ::cIdConfiguration, dDataIni, dDataFim, ::oParameters, ::aBranches, jBody:ToJson())
                
        Endif
    Endif 

return Self

/*/{Protheus.doc} getStatus
    Monitora as threads referentes ao id de processamento e processo informados e retorna o status de cada uma
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method getStatus(jQuery) class ESTR900Service
    
    Local oRepository   := Nil
    Local oD4A          := Nil
    Local oStatus       := Nil
    Local oAux          := Nil
    Local aAux          := {}
    Local aGetDate      := {}
    Local nX            := 0
    Local cQueryDone    := ""
    Local cStatus       := ""
    Local lThreadOn     := .F.
    Local nTotDone      := 0
    Local nTot          := 0
    Local nTotRema      := 0
    Local nTotalThreads := 0
    Local oTableTemp    := ac.TableTempory.Repository.acTableTemporyRepository():new()
    Local lHasTable     := .F.
    Local cCusFil    := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A"))
    Local lSameDate     := .F. 
    Local nTimeDif      := 0
    Local cWarehouse    := ""
    Local oBody         := JsonObject():new()
    Local cFilBack      := ""

    ::cError := ""
    ::cEmpty := ""
    ::oThreads := JsonObject():New()


    oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
    oD4A := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
    oAux := JsonObject():New()
    oStatus := JsonObject():New()
    
    //Validação do corpo da requisição 
    If ::IsBodyValidFromStatus(jQuery)
        
        oAux := oD4A:GET(Alltrim(jQuery["id"]), jQuery["process"])
       
        ::cTableName01 := "A" + Upper(Alltrim(jQuery["configuration"])) + "_01"
        ::cTableName02 := "A" + Upper(Alltrim(jQuery["configuration"])) + "_02"
        ::cTableName03 := "A" + Upper(Alltrim(jQuery["configuration"])) + "_03"
        ::cTableName04 := "A" + Upper(Alltrim(jQuery["configuration"])) + "_04"
        
        If Len(oAux["transactions"]) > 0
            cFilBack := cFilAnt
            oStatus["process"]      := Alltrim(jQuery["process"])
            oStatus["id"]           := Alltrim(jQuery["id"])
            oStatus["startedBy"]    := Alltrim(oAux["transactions"][1]["user"])
            For nX := 1 to Len(oAux["transactions"])
                
                cFilAnt := PADR(oAux["transactions"][nX]["branch"], Len(cFilAnt), " ")

                Aadd(aAux, JsonObject():New())

                aAux[nX]["threadNumber"]    := oAux["transactions"][nX]["threadnumber"]
                
                cStatus := UPPER(Alltrim(oAux["transactions"][nX]["status"]))

                If cStatus == "PROCESSING" .OR. cStatus == "SETUP"
                    oD4A:SEEK(jQuery["id"], oAux["transactions"][nX]["threadnumber"], jQuery["process"])
                    lThreadOn := !oD4A:LOCK()


                    If !lThreadOn
                        oD4A:UNLOCK()
        
                        If cStatus == "SETUP"
                            ::rebootThread(oD4A)
                        Else 
                            aGetDate := StrTokArr(oAux["transactions"][1]["dateexecution"],'-')

                            aGetDate[2] := StrZero(Val(aGetDate[2]),2)
                            aGetDate[3] := StrZero(Val(aGetDate[3]),2)

                            lSameDate := STod(aGetDate[1]+aGetDate[2]+aGetDate[3]) == MsDate()

                            If lSameDate
                                nTimeDif := SubHoras(SubStr(Time(),1,TamSx3("D4A_HREXEC")[1]), oAux["transactions"][1]["hourexecution"]) 
                            Endif 

                            If !lSameDate .OR. nTimeDif > 0.01
                                oD4A:PATCH('D4A_STATUS', 'ERROR')
                                cStatus := "ERROR"
                            Endif
                        Endif
                    Endif
                Endif 

                aAux[nX]["status"]          := cStatus
                aAux[nX]["total"]           := oAux["transactions"][nX]["total"]
                aAux[nX]["branch"]          := oAux["transactions"][nX]["branch"]

                nTotalThreads += oAux["transactions"][nX]["total"]

                If Alltrim(jQuery["process"]) == "ESTR900" .AND. cStatus == 'FINISHED'

                    aAux[nX]["done"]       := 1
                    aAux[nX]["remaining"]  := 0
                    aAux[nX]["percentage"]  := 100
                Else 

                    If Alltrim(jQuery["process"]) == "ESTR900A" .OR. Alltrim(jQuery["process"]) == "ESTR900"
                        lHasTable := oTableTemp:doesTableIdExist(::cTableName02)
                    Elseif Alltrim(jQuery["process"]) == "ESTR900B"
                        lHasTable := oTableTemp:doesTableIdExist(::cTableName01)
                        oBody:FromJson(oAux["transactions"][nX]["body"])
                        cWarehouse := oBody:GetJsonText("warehouse")
                    Elseif Alltrim(jQuery["process"]) == "ESTR900C"
                        lHasTable := oTableTemp:doesTableIdExist(::cTableName03)
                    Endif

                    If lHasTable 
                        cQueryDone := ::getItemsDoneQueryPerThread(jQuery, cCusFil)
                        aAux[nX]["done"] := oRepository:getDoneMovimentByProduct(cQueryDone, jQuery["id"],; 
                                            oAux["transactions"][nX]["start"],; 
                                            oAux["transactions"][nX]["end"],; 
                                            oAux["transactions"][nX]["branch"], cWarehouse, cCusFil, jQuery["process"])
                        
                        aAux[nX]["remaining"]   := oAux["transactions"][nX]["total"] - aAux[nX]["done"]
                        
                        aAux[nX]["percentage"]  := Round((aAux[nX]["done"] * 100) / oAux["transactions"][nX]["total"], 2)

                    Else 
                        ::cEmpty := STR0006
                        Exit
                    Endif

                Endif
                
                nTot +=  aAux[nX]["total"]
                nTotDone +=  aAux[nX]["done"]
                nTotRema +=  aAux[nX]["remaining"]
                
                If cStatus == 'ERROR' 
                    If Empty(Alltrim(oAux["transactions"][nX]["error"]))
                        aAux[nX]["error"] := STR0007
                        aAux[nX]["callstack"] := STR0007
                    Else 
                        aAux[nX]["error"] := Alltrim(oAux["transactions"][nX]["error"]) 
                        aAux[nX]["callStack"] := oAux["transactions"][nX]["callstack"]
                    Endif
                    
                Endif

            Next nX 
            
            oStatus["totalPercentage"]      := Round(((nTotDone * 100) / nTot), 2)
            oStatus["totalRemaining"]       := nTotRema
            oStatus["totalRecords"]         := nTotalThreads
            oStatus["totalDone"]            := nTotalThreads - nTotRema
            oStatus["threads"]              := AClone(aAux)

            aAux := {}
            ::oThreads := oStatus
            cFilAnt := cFilBack
        Else 
            ::cEmpty := STR0008
        Endif

    Endif

return Self

/*/{Protheus.doc} ESTR900Service:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method setBranches(jBranches, lCusEmp) class ESTR900Service

Local nX as numeric
Local aEmpBranches :=  MatFilCalc(.F.)

nX := 0

If lCusEmp
    For nX := 1 to Len(aEmpBranches)
        Aadd(::aBranches,{.T.,aEmpBranches[nX][2], aEmpBranches[nX][3] ,aEmpBranches[nX][4], .F.})
    Next nX
Else 
    For nX := 1 to Len(jBranches)
        Aadd(::aBranches,{.T.,jBranches[nX]['Code'], jBranches[nX]['Description'] ,jBranches[nX]['Cgc'], .F.})
    Next nX
Endif

return

/*/{Protheus.doc} ESTR900Service:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method setParameters(jParameters) class ESTR900Service

Local nX as numeric
Local cKeyAux as character
Local xValueAux := Nil

nX := 0
cKeyAux := ""

For nX := 1 to Len(jParameters)
    cKeyAux := jParameters[nX]["parameter"]
    xValueAux := jParameters[nX]["value"]
    If cKeyAux $ "mv_par03|mv_par04"
        ::oParameters[cKeyAux] := STOD(xValueAux)
    Else 
        ::oParameters[cKeyAux] := xValueAux
    Endif

Next nX

return


/*/{Protheus.doc} setRules
    Monta a query da consulta do Kardex
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusEmp, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method setRules(lCusEmp) class ESTR900Service

    Local nFils      := 0

    Local lCusFil    := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A")) == "F"
    Local lVEIC      := AllTrim(SuperGetMv("MV_VEICULO", .F., "N")) == "S" // Concessionaria de Veiculos
    Local lCusRep    := SuperGetMv("MV_CUSREP", .F., .F.) .And. MA330AvRep()
    Local lD3Estorn  := AllTrim(SuperGetMv("MV_D3ESTOR", .F., "N")) == "N"
    Local lWmsNew	 := SuperGetMv("MV_WMSNEW", .F., .F.)
    Local lD3Servi   := IIf(lWmsNew, .F., AllTrim(SuperGetMv("MV_D3SERVI" ,.F. ,"N")) == "N")
    //Local lLocProc   :=  AScan(::oParameters:GetJsonObject("mv_par06"), AllTrim(GetMvNNR("MV_LOCPROC", "99"))) > 0 // Indica se esta listando relatorio do almox. de processo

    Local cSelectD1     := ""
    Local cSelectVe     := ""
    Local cWhereD1C     := ""
    Local cTamEMPR      := ""
    Local cWhereB1A     := ""
    Local cWhereB1B     := ""
    Local cWhereB1C     := ""
    Local cWhereB1D     := ""
    Local cSelectD2     := ""
    Local cWhereD2C     := ""
    Local cSelectD3     := ""
    Local cWhereD3C     := ""
    Local cWhereD3      := ""
    Local cQueryD1      := ""
    Local cQueryD2      := ""
    Local cQueryD3      := ""
    Local cQuery2       := ""
    Local cUnion        := ""
    Local cOrder        := ""
    Local nForFilial    := 0
    Local nOrdem        := 1
    Local cSelectD3RE3  := ""
    Local cWhereD3CRE3  := ""
    Local cWhereD3RE3   := ""

    ::aRules := {}
    
    //VERIFICAR TEXTO DE HELP
    If ::oParameters:GetJsonObject("mv_par11") == 2 .And. !lCusRep
        Help(" ", 1, "A910CUSRP")
        ::oParameters["mv_par11"] := 1
    EndIf

    // aFils - Utilizado para filtar na query as filiais a considerar
    If lCusEmp .And. !Empty(::aBranches)
        For nForFilial := 1 To Len(::aBranches)
            aAdd(::aFils, ::aBranches[nForFilial, 2])
        Next nForFilial
    EndIf 

    // Complemento do SELECT da tabela SD1
    If lCusRep .And. ::oParameters:GetJsonObject("mv_par11") == 2 // Qual Custo Imprimir ? (1=Medio; 2=Reposicao)
        cSelectD1 += " D1_CUSRP?" // Coloca a Moeda do Custo
    Else
        cSelectD1 += " D1_CUSTO"
        If ::oParameters:GetJsonObject("mv_par07") > 1
            cSelectD1 += "?" // Coloca a Moeda do Custo
        EndIf
    EndIf
    cSelectD1 += " CUSTO,"
    aAdd(::aRules, cSelectD1)

    // Complemento do SELECT da tabela SB1 para MV_VEICULO
    cSelectVe += ","
    If lVEIC
        cSelectVe += "SB1.B1_CODITE B1_CODITE,"
    EndIf
    aAdd(::aRules, cSelectVe)

    // Complemento do Where da tabela SD1 (Tratamento Filial)
    If lCusEmp
        If FWModeAccess("SD1") == "E" .And. FWModeAccess("SF4") == "E"
            cWhereD1C += " D1_FILIAL = F4_FILIAL AND "
        EndIf
        If FWModeAccess("SD1") == "E" .And. FWModeAccess("SB1") == "E"
            cWhereD1C += "SD1.D1_FILIAL = SB1.B1_FILIAL AND "
        EndIf
        If "E" $ SM0->M0_LEIAUTE .And. FWModeAccess("SB1") == "C" .And. FWModeAccess("SB1", 1) <> "C"
            cTamEMPR := cValToChar(Rat("E", SM0->M0_LEIAUTE))
            cWhereD1C += " "+MatiSubStr()+"(SB1.B1_FILIAL,1," + cTamEMPR + ") = "
            cWhereD1C += " "+MatiSubStr()+"(SD1.D1_FILIAL,1," + cTamEMPR + ") AND " 
        EndIf
        For nFils := 1 To Len(::aFils)
            If nFils == 1
                cWhereD1C += " D1_FILIAL IN (? "
            Else
                cWhereD1C += ", ? "
            EndIf
            If nFils = Len(::aFils)
                cWhereD1C += ") AND "
            EndIf
        Next nFils
    Else
        cWhereD1C += " D1_FILIAL = ? AND "
        cWhereD1C += " SF4.F4_FILIAL = ? AND"
    EndIf
    aAdd(::aRules, cWhereD1C)

    // Complemento do WHERE da tabela SB1 para todos os selects
    If !lVEIC
        cWhereB1A += " AND " + ::getItemByCode("SB1", "B1_COD", ::oParameters:GetJsonObject("mv_par01"))
        cWhereB1B += " AND SB1.B1_COD = SB1EXS.B1_COD"
        If lCusEmp
            cWhereB1C += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_COD", ::oParameters:GetJsonObject("mv_par01")) + " AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
        Else
            cWhereB1C += " SB1.B1_FILIAL = ? AND"
            cWhereB1C += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
            cWhereB1D += " SB1EXS.B1_FILIAL = ? AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_COD", ::oParameters:GetJsonObject("mv_par01")) + " AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
        EndIf
    Else
        cWhereB1A += " AND " + ::getItemByCode("SB1", "B1_CODITE", ::oParameters:GetJsonObject("mv_par01"))
        cWhereB1B += " AND SB1.B1_COD = SB1EXS.B1_COD"
        If lCusEmp
            cWhereB1C += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_CODITE", ::oParameters:GetJsonObject("mv_par01")) + " AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND "
        Else
            cWhereB1C += " SB1.B1_FILIAL = ? AND"
            cWhereB1C += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
            cWhereB1D += " SB1EXS.B1_FILIAL = ? AND"
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_CODITE", ::oParameters:GetJsonObject("mv_par01")) + " AND" 
            cWhereB1D += ::getItemByCode("SB1EXS", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND "
        EndIf
    EndIf

    cWhereB1C += ::getItemByCode("SB1", "B1_GRUPO", ::oParameters:GetJsonObject("mv_par10")) + " AND "
    cWhereB1C += " SB1.B1_COD <> ? AND "
    cWhereB1C += " SB1.D_E_L_E_T_ = ?"

    cWhereB1D += ::getItemByCode("SB1EXS", "B1_GRUPO", ::oParameters:GetJsonObject("mv_par10")) + " AND "
    cWhereB1D += " SB1EXS.B1_COD <> ? AND "
    cWhereB1D += " SB1EXS.D_E_L_E_T_ = ? "

    aAdd(::aRules, cWhereB1A)
    aAdd(::aRules, cWhereB1C)

    // Complemento do SELECT da tabela SD2
    If lCusRep .And. ::oParameters:GetJsonObject("mv_par11") == 2
        cSelectD2 += " D2_CUSRP?" // Coloca a Moeda do Custo
    Else
        cSelectD2 += " D2_CUSTO?" // Coloca a Moeda do Custo
    EndIf
    cSelectD2 += " CUSTO,"
    aAdd(::aRules, cSelectD2)

    // Complemento do Where da tabela SD2 (Tratamento Filial)
    If lCusEmp
        If FWModeAccess("SD2") == "E" .And. FWModeAccess("SF4") == "E"
            cWhereD2C += " D2_FILIAL = F4_FILIAL AND "
        EndIf
        If FWModeAccess("SD2") == "E" .And. FWModeAccess("SB1") == "E"
            cWhereD2C += "SD2.D2_FILIAL = SB1.B1_FILIAL AND "
        EndIf
        If "E" $ SM0->M0_LEIAUTE .And. FWModeAccess("SB1") == "C" .And. FWModeAccess("SB1", 1) <> "C"
            cTamEMPR := cValToChar(Rat("E", SM0->M0_LEIAUTE))
            cWhereD2C += " "+MatiSubStr()+"(SB1.B1_FILIAL,1," + cTamEMPR + ") = "
            cWhereD2C += " "+MatiSubStr()+"(SD2.D2_FILIAL,1," + cTamEMPR + ") AND "
        EndIf
        For nFils := 1 To Len(::aFils)
            If nFils == 1
                cWhereD2C += " D2_FILIAL IN (?"
            Else
                cWhereD2C += ",?"
            EndIf
            If nFils = Len(::aFils)
                cWhereD2C += ") AND "
            EndIf
        Next nFils
    Else
        cWhereD2C += " D2_FILIAL = ? AND "
        cWhereD2C += " SF4.F4_FILIAL = ? AND"
    EndIf
    aAdd(::aRules, cWhereD2C)

    // Complemento do SELECT da tabela SD3
    If lCusRep .And. ::oParameters:GetJsonObject("mv_par11") == 2
        cSelectD3 := " D3_CUSRP?" // Coloca a Moeda do Custo
    Else
        cSelectD3 := " D3_CUSTO?" // Coloca a Moeda do Custo
    EndIf
    cSelectD3 += " CUSTO,"
    aAdd(::aRules, cSelectD3)

    // Complemento do Where da tabela SD3 (Tratamento Filial)
    If lCusEmp
        For nFils := 1 To Len(::aFils)
            If nFils == 1
                cWhereD3C += " D3_FILIAL IN (?"
            Else
                cWhereD3C += ", ? "
            EndIf
            If nFils == Len(::aFils)
                cWhereD3C += ") AND "
            EndIf
        Next nFils
    Else
        cWhereD3C += " D3_FILIAL = ? AND "
    EndIf
    aAdd(::aRules, cWhereD3C)

    // Complemento do WHERE da tabela SD3
    If lD3Estorn
        cWhereD3 += " D3_ESTORNO <> ? AND"
    EndIf
    If lD3Servi .And. IntDL()
        cWhereD3 += " ( (D3_SERVIC = ?) OR (D3_SERVIC <> ? AND D3_TM <= ?)  "
        cWhereD3 += " OR  (D3_SERVIC <> ? AND D3_TM > ? AND D3_LOCAL = ?) ) AND"
    EndIf
    If !(lCusFil .Or. lCusEmp) //.And. !::containsWarehouseLocProc()
        cWhereD3 += ::getWarehouseByCode("D3_LOCAL", ::oParameters:GetJsonObject("mv_par06"), lCusFil .Or. lCusEmp) + " AND"
    EndIf
    If !lVEIC
        cWhereD3 += ::getItemByCode("SB1", "B1_COD", ::oParameters:GetJsonObject("mv_par01")) + " AND"
    Else
        cWhereD3 += ::getItemByCode("SB1", "B1_CODITE", ::oParameters:GetJsonObject("mv_par01")) + " AND"
    EndIf
    If lCusEmp
        cWhereD3 += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02"))+ " AND"
        If FWModeAccess("SD3") == "E" .And. FWModeAccess("SB1") == "E"
            cWhereD3 += " SD3.D3_FILIAL = SB1.B1_FILIAL AND "
        EndIf
        If "E" $ SM0->M0_LEIAUTE .And. FWModeAccess("SB1") == "C" .And. FWModeAccess("SB1",1) <> "C"
            cTamEMPR := cValToChar(Rat("E", SM0->M0_LEIAUTE)) 
            cWhereD3 += " "+MatiSubStr()+"(SB1.B1_FILIAL,1," + cTamEMPR + ") = "
            cWhereD3 += " "+MatiSubStr()+"(SD3.D3_FILIAL,1," + cTamEMPR + ") AND " 
        EndIf
    Else
        cWhereD3 += " SB1.B1_FILIAL = ? AND "
        cWhereD3 += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
    EndIf
    cWhereD3 += ::getItemByCode("SB1", "B1_GRUPO", ::oParameters:GetJsonObject("mv_par10")) + " AND"
    cWhereD3 += " SB1.B1_COD <> ? AND "
    cWhereD3 += " SB1.D_E_L_E_T_= ? AND"
    aAdd(::aRules, cWhereD3)

    // Complemento do SELECT da tabela SD3 - MOVIMENTOS DE APROPRIACAO INDIRETA - RE3*
    If lCusRep .And. ::oParameters:GetJsonObject("mv_par11") == 2
        cSelectD3RE3 := " D3_CUSRP?" // Coloca a Moeda do Custo
    Else
        cSelectD3RE3 := " D3_CUSTO?" // Coloca a Moeda do Custo
    EndIf
    cSelectD3RE3 += " CUSTO,"
    aAdd(::aRules, cSelectD3RE3)

    // Complemento do Where da tabela SD3 (Tratamento Filial)
    If lCusEmp
        For nFils := 1 To Len(::aFils)
            If nFils == 1
                cWhereD3CRE3 += " D3_FILIAL IN (?"
            Else
                cWhereD3CRE3 += ",?"
            EndIf
            If nFils == Len(::aFils)
                cWhereD3CRE3 += ") AND "
            EndIf
        Next nFils
    Else
        cWhereD3CRE3 += " D3_FILIAL = ? AND "
    EndIf
    aAdd(::aRules, cWhereD3CRE3)

    // Complemento do WHERE da tabela SD3
    If lD3Estorn
        cWhereD3RE3 += " D3_ESTORNO <> ? AND"
    EndIf
    If lD3Servi .And. IntDL()
        cWhereD3RE3 += " ( (D3_SERVIC = ?) OR (D3_SERVIC <> ? AND D3_TM <= ?)  "
        cWhereD3RE3 += " OR  (D3_SERVIC <> ? AND D3_TM > ? AND D3_LOCAL = ?) ) AND"
    EndIf
    If !(lCusFil .Or. lCusEmp) .And. !::containsWarehouseLocProc()
        cWhereD3RE3 += ::getWarehouseByCode("D3_LOCAL", ::oParameters:GetJsonObject("mv_par06"), lCusFil .Or. lCusEmp) + " AND"
    EndIf
    If !lVEIC
        cWhereD3RE3 += ::getItemByCode("SB1", "B1_COD", ::oParameters:GetJsonObject("mv_par01")) + " AND"
    Else
        cWhereD3RE3 += ::getItemByCode("SB1", "B1_CODITE", ::oParameters:GetJsonObject("mv_par01")) + " AND"
    EndIf
    If lCusEmp
        cWhereD3RE3 += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02"))+ " AND"
        If FWModeAccess("SD3") == "E" .And. FWModeAccess("SB1") == "E"
            cWhereD3RE3 += " SD3.D3_FILIAL = SB1.B1_FILIAL AND "
        EndIf
        If "E" $ SM0->M0_LEIAUTE .And. FWModeAccess("SB1") == "C" .And. FWModeAccess("SB1",1) <> "C"
            cTamEMPR := cValToChar(Rat("E", SM0->M0_LEIAUTE)) 
            cWhereD3RE3 += " "+MatiSubStr()+"(SB1.B1_FILIAL,1," + cTamEMPR + ") = "
            cWhereD3RE3 += " "+MatiSubStr()+"(SD3.D3_FILIAL,1," + cTamEMPR + ") AND " 
        EndIf
    Else
        cWhereD3RE3 += " SB1.B1_FILIAL = ? AND "
        cWhereD3RE3 += ::getItemByCode("SB1", "B1_TIPO", ::oParameters:GetJsonObject("mv_par02")) + " AND"
    EndIf
    cWhereD3RE3 += ::getItemByCode("SB1", "B1_GRUPO", ::oParameters:GetJsonObject("mv_par10")) + " AND"
    cWhereD3RE3 += " SB1.B1_COD <> ? AND "
    cWhereD3RE3 += " SB1.D_E_L_E_T_= ? AND"
    aAdd(::aRules, cWhereD3RE3)

    // Lista produtos sem movimentacao? (1=Sim; 2=Nao)
    If ::oParameters:GetJsonObject("mv_par05") == 1

        // So inclui as condicoes a seguir qdo lista produtos sem movimento
        cQueryD1 := " FROM "
        cQueryD1 += RetSqlName("SB1") + " SB1"
        cQueryD1 += (", " + RetSqlName("SD1") + " SD1 ")
        cQueryD1 += (", " + RetSqlName("SF4") + " SF4 ")
        cQueryD1 += " WHERE SB1.B1_COD = D1_COD"
        If lCusEmp
            If FWModeAccess("SD1") == "E" .And. FWModeAccess("SF4") == "E"
                cQueryD1 += " AND F4_FILIAL = D1_FILIAL "
            EndIf
        Else
            cQueryD1 += (" AND D1_FILIAL = ? " )
            cQueryD1 += (" AND F4_FILIAL = ? " )
        EndIf
        cQueryD1 += (" AND SD1.D1_TES = F4_CODIGO AND F4_ESTOQUE = ? ")
        cQueryD1 += (" AND D1_DTDIGIT >= ? ")
        cQueryD1 += (" AND D1_DTDIGIT <= ? ")
        cQueryD1 +=  " AND D1_ORIGLAN <> ? "
        If !(lCusFil .Or. lCusEmp)
            cQueryD1 += " AND " + ::getWarehouseByCode("D1_LOCAL", ::oParameters:GetJsonObject("mv_par06"), lCusFil .Or. lCusEmp)
        EndIf
        cQueryD1 += " AND SD1.D_E_L_E_T_= ? AND SF4.D_E_L_E_T_= ? "

        cQueryD2 := " FROM "
        cQueryD2 += RetSqlName("SB1") + " SB1 , " + RetSqlName("SD2") + " SD2 , " + RetSqlName("SF4") + " SF4 "
        cQueryD2 += " WHERE SB1.B1_COD = D2_COD "
        If lCusEmp
            If FWModeAccess("SD2") == "E" .And. FWModeAccess("SF4") == "E"
                cQueryD2 += " AND F4_FILIAL = D2_FILIAL "
            EndIf
        Else
            cQueryD2 += " AND D2_FILIAL = ? "
            cQueryD2 += " AND F4_FILIAL = ? "
        EndIf
        cQueryD2 += " AND SD2.D2_TES = F4_CODIGO AND F4_ESTOQUE = ? "
        cQueryD2 += " AND D2_EMISSAO >= ? "
        cQueryD2 += " AND D2_EMISSAO <= ? "
        cQueryD2 += " AND D2_ORIGLAN <> ? "
        If !(lCusFil .Or. lCusEmp)
            cQueryD2 += " AND " + ::getWarehouseByCode("D2_LOCAL", ::oParameters:GetJsonObject("mv_par06"), lCusFil .Or. lCusEmp)
        EndIf
        cQueryD2 += " AND SD2.D_E_L_E_T_= ? AND SF4.D_E_L_E_T_= ? "

        cQueryD3 := " FROM "
        cQueryD3 += RetSqlName("SB1") + " SB1 , " + RetSqlName("SD3") + " SD3 "
        cQueryD3 += " WHERE SB1.B1_COD = D3_COD "
        If !lCusEmp
            cQueryD3 += " AND D3_FILIAL = ? "
        EndIf
        cQueryD3 += " AND D3_EMISSAO >= ? "
        cQueryD3 += " AND D3_EMISSAO <= ? "
        If lD3Estorn
            cQueryD3 += " AND D3_ESTORNO <> ? "
        EndIf
        If lD3Servi .And. IntDL()
            cQueryD3 += " AND ( (D3_SERVIC = ?) OR (D3_SERVIC <> ? AND D3_TM <= ?)  "
            cQueryD3 += " OR  (D3_SERVIC <> ? AND D3_TM > ? AND D3_LOCAL = ?) )"
        EndIf
        If !(lCusFil .Or. lCusEmp) //.And. !::containsWarehouseLocProc()
            cQueryD3 += " AND " + ::getWarehouseByCode("D3_LOCAL", ::oParameters:GetJsonObject("mv_par06"), lCusFil .Or. lCusEmp)
        EndIf
        cQueryD3 += " AND SD3.D_E_L_E_T_= ?"

        cQuery2 := " AND NOT EXISTS (SELECT 1 " 
        cQuery2 += cQueryD1
        cQuery2 += cWhereB1B
        cQuery2 += " AND "
        cQuery2 += Substr(cWhereB1C, 2, (Len(cWhereB1C)))
        cQuery2 += ") AND NOT EXISTS (SELECT 1 "
        cQuery2 += cQueryD2
        cQuery2 += cWhereB1B
        cQuery2 += " AND "
        cQuery2 += Substr(cWhereB1C, 2, (Len(cWhereB1C)))
        cQuery2 += ") AND NOT EXISTS (SELECT 1 "
        cQuery2 += cQueryD3
        cQuery2 += cWhereB1B
        cQuery2 += " AND "
        cQuery2 += Substr(cWhereB1C, 2, (Len(cWhereB1C))) + ")"

        cUnion := " UNION SELECT 'SB1'"		// 01
        cUnion += ", '?'"			        // 02
        cUnion += ", ' '"			        // 03
        cUnion += ", SB1EXS.B1_COD"			// 04
        cUnion += ", SB1EXS.B1_TIPO"		// 05
        cUnion += ", SB1EXS.B1_UM"			// 06
        cUnion += ", SB1EXS.B1_GRUPO"		// 07
        cUnion += ", SB1EXS.B1_DESC"		// 08
        cUnion += ", SB1EXS.B1_POSIPI"		// 09
        cUnion += ", ' '"					// 10
        cUnion += ", ' '"					// 11
        cUnion += ", ' '"					// 12
        cUnion += ", ' '"					// 13
        cUnion += ", ' '"					// 14
        cUnion += ", ' '"					// 15
        cUnion += ", ' '"					// 16
        cUnion += ", ' '"					// 17
        cUnion += ", ' '"					// 18
        cUnion += ", ' '"					// 19
        cUnion += ", 0"						// 20
        cUnion += ", 0"						// 21
        cUnion += ", ' '"		            // 22
        cUnion += ", ' '"            		// 23
        cUnion += ", ' '"					// 24
        cUnion += ", ' '"					// 25
        cUnion += ", ' '"					// 26
        cUnion += ", ' '"					// 27
        cUnion += ", ' '"					// 28
        cUnion += ", ' '"					// 29
        cUnion += ", 0"						// 30
        cUnion += ", ' '"					// 31
        cUnion += ", ' '"					// 32
        If lVEIC
            cUnion += ", SB1EXS.B1_CODITE CODITE"	// 28
        EndIf
        cUnion += ", 0"						// 29
        cUnion += ", ' '"					// 30 local
        cUnion += " FROM " + RetSqlName("SB1") + " SB1EXS"
        cUnion += " WHERE"
        cUnion += cWhereB1D
        cUnion += cQuery2
    EndIf
    aAdd(::aRules, cUnion)

    If !lVEIC
        If nOrdem == 1 //" Codigo Produto "###" Tipo do Produto"
            cOrder += " 3, "
        EndIf
    Else
        If nOrdem == 1 //" Codigo Produto "###" Tipo do Produto"
            cOrder += " 29, 3, 6," 	// B1_CODITE, B1_COD, B1_GRUPO
        EndIf
    EndIf

    If ::oParameters:GetJsonObject("mv_par08") == 1
        cOrder += "13" + IIf(lVEIC, ", 30", ", 29")
    Else
        If lCusFil .Or. lCusEmp
            cOrder += "9, 13" + IIf(lVEIC, ", 30", ", 29")
        Else
            cOrder += "9" + IIf(lVEIC, ", 30", ", 29")
        EndIf
    EndIf
    aAdd(::aRules, cOrder)

Return


/*/{Protheus.doc} PocQuery
    POC query de movimentacoes do MATR900
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method setQuery() class ESTR900Service

    Local cLocProc          := AllTrim(GetMvNNR("MV_LOCPROC", "99")) // Indica se esta listando relatorio do almox. de processo
    Local lCusRep           := SuperGetMv("MV_CUSREP", .F., .F.) .And. MA330AvRep()
    Local cProdImp          := AllTrim(SuperGetMv("MV_PRODIMP", .F., "PROD. IMPORTADO")) // Codigo do produto importado - NAO DEVE SER LISTADO
    Local lD3Estorn         := AllTrim(SuperGetMv("MV_D3ESTOR", .F., "N")) == "N"
    Local lWmsNew           := SuperGetMv("MV_WMSNEW", .F., .F.)
    Local cMvCQ             := AllTrim(GetMvNNR("MV_CQ", "98"))
    Local lCusFil           := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A")) == "F"
    Local lCusEmp           := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A")) == "E"
    Local lD3Servi          := .F.
    Local cAliasTop         as character
    Local cQuery            as character
    Local cTabela  := "13"  as character
    Local cSelectD3RE3      as character
    Local cWhereD3CRE3      as character
    Local cWhereD3RE3       as character
    Local nX := 1           as numeric
    Local nY                as numeric
    Local aMvPar01          := ::oParameters:GetJsonObject("mv_par01")
    Local aMvPar02          := ::oParameters:GetJsonObject("mv_par02")
    Local cMVPAR03          := ::oParameters:GetJsonObject("mv_par03")
    Local cMVPAR04          := ::oParameters:GetJsonObject("mv_par04")
    Local aMvPar10          := ::oParameters:GetJsonObject("mv_par10")
    Local aMvPar06          := ::oParameters:GetJsonObject("mv_par06")

    cSelectD1 := ::aRules[1]  // Complemento do SELECT da tabela SD1
    cSelectVe := ::aRules[2]  // Complemento do SELECT da tabela SB1 para MV_VEICULO
    cWhereD1C := ::aRules[3]  // Complemento do Where da tabela SD1 (Tratamento Filial)
    cWhereB1A := ::aRules[4]  // Complemento do WHERE da tabela SB1 - produto de/ate
    cWhereB1C := ::aRules[5]  // Complemento do WHERE da tabela SB1 - tipo de/ate e grupo de/ate
    cSelectD2 := ::aRules[6]  // Complemento do SELECT da tabela SD2
    cWhereD2C := ::aRules[7]  // Complemento do Where da tabela SD2 (Tratamento Filial)
    cSelectD3 := ::aRules[8]  // Complemento do SELECT da tabela SD3
    cWhereD3C := ::aRules[9]  // Complemento do Where da tabela SD3 (Tratamento Filial)
    cWhereD3  := ::aRules[10] // Complemento do WHERE da tabela SD3

    cSelectD3RE3 := ::aRules[11] // Complemento do SELECT da tabela SD3 - RE3*
    cWhereD3CRE3 := ::aRules[12] // Complemento do Where da tabela SD3 (Tratamento Filial) - RE3*
    cWhereD3RE3  := ::aRules[13] // Complemento do WHERE da tabela SD3 - RE3* 

    cUnion    := ::aRules[14] // Union produtos sem movimentacao
    cOrder    := ::aRules[15] // Ordenacao da query

    If !lWmsNew
        lD3Servi := AllTrim(SuperGetMv("MV_D3SERVI" ,.F. ,"N")) == "N"
    EndIf

    cQuery := " SELECT 'SD1' ARQ,"          //-- 01 ARQ
    cQuery += " '?'  FILIAL,"               //-- 02 FILANT
    cQuery += " D1_FILIAL FILMOV,"          //-- 03 FILIAL DA MOVIMENTACAO
    cQuery += " SB1.B1_COD PRODUTO," 	    //-- 04 PRODUTO
    cQuery += " SB1.B1_TIPO TIPO," 		    //-- 05 TIPO
    cQuery += " SB1.B1_UM UM,"   	        //-- 06 UM
    cQuery += " SB1.B1_GRUPO GRUPO,"        //-- 07 GRUPO
    cQuery += " SB1.B1_DESC DESCRI,"        //-- 08 DESCRICAO
    cQuery += " SB1.B1_POSIPI POSIPI,"      //-- 09
    cQuery += " D1_SEQCALC SEQCALC,"        //-- 10
    cQuery += " D1_DTDIGIT DTDIGIT,"		//-- 11 DTDIGIT
    cQuery += " D1_TES TES,"			    //-- 12 TES
    cQuery += " D1_CF CF,"				    //-- 13 CF
    cQuery += " COALESCE(X5_DESCRI, ' ') X5_DESCRI,"        //-- 13 CF 
    cQuery += " COALESCE(X5_DESCSPA,' ') X5_DESCSPA,"		//-- 13 CF
    cQuery += " COALESCE(X5_DESCENG,' ') X5_DESCENG,"		//-- 13 CF
    cQuery += " D1_NUMSEQ SEQUENCIA,"	    //-- 14 SEQUENCIA
    cQuery += " D1_DOC DOCUMENTO,"		    //-- 15 DOCUMENTO
    cQuery += " D1_SERIE SERIE,"		    //-- 16 SERIE
    cQuery += " D1_QUANT QUANTIDADE,"	    //-- 17 QUANTIDADE
    cQuery += " D1_QTSEGUM QUANT2UM,"	    //-- 18 QUANT2UM
    cQuery += " D1_LOCAL ARMAZEM,"		    //-- 19 ARMAZEM
    cQuery += " ' ' PROJETO,"			    //-- 20 PROJETO
    cQuery += " ' ' OP,"				    //-- 21 OP
    cQuery += " ' ' CC,"			        //-- 22 OP
    cQuery += " D1_FORNECE FORNECEDOR,"	    //-- 23 FORNECEDOR
    cQuery += " D1_LOJA LOJA,"			    //-- 24 LOJA
    cQuery += " ' ' PEDIDO,"                //-- 25 PEDIDO
    cQuery += " D1_TIPO TIPONF,"            //-- 26 TIPO NF
    cQuery += cSelectD1		                //-- 27 CUSTO
    cQuery += " ' ' TRT," 				    //-- 27 TRT
    cQuery += " D1_LOTECTL LOTE" 	   	    //-- 28 LOTE
    cQuery += cSelectVe                	    //-- 29 CODITE
    cQuery += " SD1.R_E_C_N_O_ NRECNO,"     //-- 30 RECNO
    cQuery += " SD1.D1_LOCAL ARMLOC "       //-- 31 ARMLOC

    cQuery += " FROM " + RetSqlName("SB1") + " SB1 ," + RetSqlName("SF4") + " SF4 ," + RetSqlName("SD1") + " SD1 "

    cQuery += " LEFT JOIN " + RetSqlName("SX5") + " SX5 ON "
    cQuery += " SX5.X5_CHAVE = SD1.D1_CF "
    cQuery += " AND SX5.X5_TABELA = ? "
    cQuery += " AND SX5.D_E_L_E_T_ = ? "

    cQuery += " WHERE "
    cQuery += " SB1.B1_COD = SD1.D1_COD AND " 
    cQuery += cWhereD1C
    cQuery += " SD1.D1_TES = SF4.F4_CODIGO AND "
    cQuery += " SF4.F4_ESTOQUE = ? AND "
    cQuery += " SD1.D1_DTDIGIT >= ? AND "
    cQuery += " SD1.D1_DTDIGIT <= ? AND "
    cQuery += " SD1.D1_ORIGLAN <> ? AND "
    If !(lCusFil .Or. lCusEmp)
        cQuery += ::getWarehouseByCode("D1_LOCAL", aMvPar06, lCusFil .Or. lCusEmp) + " AND "
    EndIf
    cQuery += " SD1.D_E_L_E_T_ = ? AND "
    cQuery += " SF4.D_E_L_E_T_ = ? "
    cQuery += cWhereB1A + " AND "
    cQuery += cWhereB1C

    cQuery += " UNION "

    cQuery += " SELECT 'SD2',"
    cQuery += " '?' FILIAL,"
    cQuery += " D2_FILIAL FILMOV,"
    cQuery += " SB1.B1_COD,"
    cQuery += " SB1.B1_TIPO,"
    cQuery += " SB1.B1_UM,"
    cQuery += " SB1.B1_GRUPO,"
    cQuery += " SB1.B1_DESC,"
    cQuery += " SB1.B1_POSIPI,"
    cQuery += " D2_SEQCALC,"
    cQuery += " D2_EMISSAO,"
    cQuery += " D2_TES,"
    cQuery += " D2_CF,"
    cQuery += " COALESCE(X5_DESCRI, ' ') X5_DESCRI," //-- 13 CF 
    cQuery += " COALESCE(X5_DESCSPA,' ') X5_DESCSPA,"		//-- 13 CF
    cQuery += " COALESCE(X5_DESCENG,' ') X5_DESCENG,"
    cQuery += " D2_NUMSEQ,"
    cQuery += " D2_DOC,"
    cQuery += " D2_SERIE,"
    cQuery += " D2_QUANT,"
    cQuery += " D2_QTSEGUM,"
    cQuery += " D2_LOCAL,"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " D2_CLIENTE,"
    cQuery += " D2_LOJA,"
    cQuery += " D2_PEDIDO,"
    cQuery += " D2_TIPO,"
    cQuery += cSelectD2
    cQuery += " ' ', "
    cQuery += " D2_LOTECTL"
    cQuery += cSelectVe
    cQuery += " SD2.R_E_C_N_O_ SD2RECNO," //-- 29 RECNO
    cQuery += " SD2.D2_LOCAL "

    cQuery += " FROM " + RetSqlName("SB1") + " SB1 ," + RetSqlName("SF4") + " SF4 ," + RetSqlName("SD2") + " SD2 "

    cQuery += " LEFT JOIN " + RetSqlName("SX5") + " SX5 ON "
    cQuery += " SX5.X5_CHAVE = SD2.D2_CF "
    cQuery += " AND SX5.X5_TABELA = ? "
    cQuery += " AND SX5.D_E_L_E_T_ = ? "

    cQuery += " WHERE SB1.B1_COD = SD2.D2_COD AND "
    cQuery += cWhereD2C
    cQuery += " SD2.D2_TES = SF4.F4_CODIGO AND "
    cQuery += " SF4.F4_ESTOQUE = ? AND "
    cQuery += " SD2.D2_EMISSAO >= ? AND "
    cQuery += " SD2.D2_EMISSAO <= ? AND "
    cQuery += " SD2.D2_ORIGLAN <> ? AND "
    If !(lCusFil .Or. lCusEmp)
        cQuery += ::getWarehouseByCode("D2_LOCAL", aMvPar06, lCusFil .Or. lCusEmp) + " AND "
    EndIf
    cQuery += " SD2.D_E_L_E_T_ = ? AND "
    cQuery += " SF4.D_E_L_E_T_ = ? "
    cQuery += cWhereB1A + " AND "
    cQuery += cWhereB1C

    cQuery += " UNION "

    cQuery += " SELECT 'SD3', "
    cQuery += " '?' FILIAL,"
    cQuery += " D3_FILIAL FILMOV,"
    cQuery += " SB1.B1_COD,"
    cQuery += " SB1.B1_TIPO,"
    cQuery += " SB1.B1_UM,"
    cQuery += " SB1.B1_GRUPO,"
    cQuery += " SB1.B1_DESC,"
    cQuery += " SB1.B1_POSIPI,"
    cQuery += " D3_SEQCALC,"
    cQuery += " D3_EMISSAO,"
    cQuery += " D3_TM,"
    cQuery += " D3_CF,"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " D3_NUMSEQ,"
    cQuery += " D3_DOC,"
    cQuery += " ' ',"
    cQuery += " D3_QUANT,"
    cQuery += " D3_QTSEGUM,"
    cQuery += " D3_LOCAL,"
    cQuery += " D3_PROJPMS,"
    cQuery += " D3_OP,"
    cQuery += " D3_CC,"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += cSelectD3
    cQuery += " D3_TRT,"
    cQuery += " D3_LOTECTL"
    cQuery += " ? "
    cQuery += " SD3.R_E_C_N_O_ SD3RECNO," //-- 29 RECNO
    cQuery += " SD3.D3_LOCAL "

    cQuery += " FROM " + RetSqlName("SB1") + " SB1 ," + RetSqlName("SD3") + " SD3 "
    
    cQuery += " WHERE SB1.B1_COD = SD3.D3_COD AND "
    cQuery += cWhereD3CRE3
    cQuery += " SD3.D3_EMISSAO >= ? AND "
    cQuery += " SD3.D3_EMISSAO <= ? AND "
    cQuery += cWhereD3
    cQuery += " SD3.D_E_L_E_T_ = ? "

    cQuery += " UNION "
        
    cQuery += " SELECT 'SD3', "
    cQuery += " '?' FILIAL,"
    cQuery += " D3_FILIAL FILMOV,"
    cQuery += " SB1.B1_COD,"
    cQuery += " SB1.B1_TIPO,"
    cQuery += " SB1.B1_UM,"
    cQuery += " SB1.B1_GRUPO,"
    cQuery += " SB1.B1_DESC,"
    cQuery += " SB1.B1_POSIPI,"
    cQuery += " D3_SEQCALC,"
    cQuery += " D3_EMISSAO,"
    cQuery += " D3_TM,"
    cQuery += " D3_CF,"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " D3_NUMSEQ,"
    cQuery += " D3_DOC,"
    cQuery += " ' ',"
    cQuery += " D3_QUANT,"
    cQuery += " D3_QTSEGUM,"
    cQuery += " ? D3_LOCAL,"
    cQuery += " D3_PROJPMS,"
    cQuery += " D3_OP,"
    cQuery += " D3_CC,"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += " ' ',"
    cQuery += cSelectD3RE3
    cQuery += " D3_TRT,"
    cQuery += " D3_LOTECTL"
    cQuery += " ?"
    cQuery += " SD3.R_E_C_N_O_ SD3RECNO," //-- 29 RECNO
    cQuery += " SD3.D3_LOCAL "

    cQuery += " FROM " + RetSqlName("SB1") + " SB1 ," + RetSqlName("SD3") + " SD3 "

    cQuery += " WHERE "
    cQuery += " SB1.B1_COD = SD3.D3_COD AND "
    cQuery += cWhereD3CRE3
    cQuery += " SD3.D3_EMISSAO >= ? AND "
    cQuery += " SD3.D3_EMISSAO <= ? AND "
    cQuery += " (SD3.D3_CF = ? OR SD3.D3_CF = ?) AND "
    cQuery += cWhereD3RE3
    cQuery += " SD3.D_E_L_E_T_ = ? "

    cQuery += cUnion + "ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    ::oQuery := FwExecStatement():New(cQuery)

    //SD1 - Notas Fiscais de Entrada
    ::oQuery:SetUnsafe(nX++,cFilAnt)
    If (lCusRep .And. ::oParameters:GetJsonObject("mv_par11") == 2) .Or. ::oParameters:GetJsonObject("mv_par07") > 1 
        ::oQuery:SetUnsafe(nX++,Str(::oParameters:GetJsonObject("mv_par07"), 1, 0))
    EndIf
    ::oQuery:SetString(nX++,cTabela)
    ::oQuery:SetString(nX++,' ')
    If lCusEmp
        For nY := 1 to Len(::aFils)
            ::oQuery:SetString(nX++,::aFils[nY])
        Next nY
    Else
        ::oQuery:SetString(nX++,FWxFilial("SD1"))
        ::oQuery:SetString(nX++,FWxFilial("SF4"))
    Endif
    ::oQuery:SetString(nX++,'S')
    ::oQuery:SetString(nX++,dtos(cMVPAR03))
    ::oQuery:SetString(nX++,dtos(cMVPAR04))
    ::oQuery:SetString(nX++,'LF')
    If !(lCusFil .Or. lCusEmp)
        For nY := 1 to Len(aMvPar06)
            ::oQuery:SetString(nX++,aMvPar06[nY])
        Next nY
    EndIf
    ::oQuery:SetString(nX++,' ')
    ::oQuery:SetString(nX++,' ')
    For nY := 1 to Len(aMvPar01)
        ::oQuery:SetString(nX++,aMvPar01[nY])
    Next nY
    If !lCusEmp
        ::oQuery:SetString(nX++,FWxFilial("SB1"))
    EndIf
    For nY := 1 to Len(aMvPar02)
        ::oQuery:SetString(nX++,aMvPar02[nY])
    Next nY
    For nY := 1 to Len(aMvPar10)
        ::oQuery:SetString(nX++,aMvPar10[nY])
    Next nY
    ::oQuery:SetString(nX++,cProdImp)
    ::oQuery:SetString(nX++,' ')

    //SD2 - Notas Fiscais de Saída
    ::oQuery:SetUnsafe(nX++,cFilAnt)
    ::oQuery:SetUnsafe(nX++,Str(::oParameters:GetJsonObject("mv_par07"), 1, 0))
    ::oQuery:SetString(nX++,cTabela)
    ::oQuery:SetString(nX++,' ')
    If lCusEmp
        For nY := 1 to Len(::aFils)
            ::oQuery:SetString(nX++,::aFils[nY])
        Next nY
    Else
        ::oQuery:SetString(nX++,FWxFilial("SD2"))
        ::oQuery:SetString(nX++,FWxFilial("SF4"))
    Endif
    ::oQuery:SetString(nX++,'S')
    ::oQuery:SetString(nX++,dtos(cMVPAR03))
    ::oQuery:SetString(nX++,dtos(cMVPAR04))
    ::oQuery:SetString(nX++,'LF')
    If !(lCusFil .Or. lCusEmp)
        For nY := 1 to Len(aMvPar06)
            ::oQuery:SetString(nX++,aMvPar06[nY])
        Next nY
    EndIf
    ::oQuery:SetString(nX++,' ')
    ::oQuery:SetString(nX++,' ')
    For nY := 1 to Len(aMvPar01)
        ::oQuery:SetString(nX++,aMvPar01[nY])
    Next nY
    For nY := 1 to Len(aMvPar02)
        ::oQuery:SetString(nX++,aMvPar02[nY])
    Next nY
    For nY := 1 to Len(aMvPar10)
        ::oQuery:SetString(nX++,aMvPar10[nY])
    Next nY
    If !lCusEmp
        ::oQuery:SetString(nX++,FWxFilial("SB1"))
    EndIf
    ::oQuery:SetString(nX++,cProdImp)
    ::oQuery:SetString(nX++,' ')

    //SD3 - Movimentos Internos
    ::oQuery:SetUnsafe(nX++,cFilAnt)
    ::oQuery:SetUnsafe(nX++,Str(::oParameters:GetJsonObject("mv_par07"), 1, 0))
    ::oQuery:SetUnsafe(nX++,cSelectVe)
    If lCusEmp
        For nY := 1 to Len(::aFils)
            ::oQuery:SetString(nX++,::aFils[nY])
        Next nY
    Else
        ::oQuery:SetString(nX++,FWxFilial("SD3"))
    Endif
    ::oQuery:SetString(nX++,dtos(cMVPAR03))
    ::oQuery:SetString(nX++,dtos(cMVPAR04))
    If lD3Estorn
        ::oQuery:SetString(nX++,'S')
    EndIf
    If lD3Servi .And. IntDL()
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'500')
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'500')
        ::oQuery:SetString(nX++,cMvCQ)
    EndIf
    If !(lCusFil .Or. lCusEmp) //.And. !::containsWarehouseLocProc()
        For nY := 1 to Len(aMvPar06)
            ::oQuery:SetString(nX++,aMvPar06[nY])
        Next nY
    EndIf
    For nY := 1 to Len(aMvPar01)
        ::oQuery:SetString(nX++,aMvPar01[nY])
    Next nY
    If !lCusEmp
        ::oQuery:SetString(nX++,FWxFilial("SB1"))
    EndIf
    For nY := 1 to Len(aMvPar02)
        ::oQuery:SetString(nX++,aMvPar02[nY])
    Next nY
    For nY := 1 to Len(aMvPar10)
        ::oQuery:SetString(nX++,aMvPar10[nY])
    Next nY
    ::oQuery:SetString(nX++,cProdImp)
    ::oQuery:SetString(nX++,' ')
    ::oQuery:SetString(nX++,' ')

    //SD3 - Movimentos Internos - Union
    ::oQuery:SetUnsafe(nX++,cFilAnt)
    ::oQuery:SetString(nX++,cLocProc)
    ::oQuery:SetUnsafe(nX++,Str(::oParameters:GetJsonObject("mv_par07"), 1, 0))
    ::oQuery:SetUnsafe(nX++,cSelectVe)
    If lCusEmp
        For nY := 1 to Len(::aFils)
            ::oQuery:SetString(nX++,::aFils[nY])
        Next nY
    Else
        ::oQuery:SetString(nX++,FWxFilial("SD3"))
    EndIf
    ::oQuery:SetString(nX++,dtos(cMVPAR03))
    ::oQuery:SetString(nX++,dtos(cMVPAR04))
    ::oQuery:SetString(nX++,'RE3')
    ::oQuery:SetString(nX++,'DE3')
    If lD3Estorn
        ::oQuery:SetString(nX++,'S')
    EndIf
    If !(lCusFil .Or. lCusEmp) .And. !::containsWarehouseLocProc()
        For nY := 1 to Len(aMvPar06)
            ::oQuery:SetString(nX++,aMvPar06[nY])
        Next nY
    EndIf
    For nY := 1 to Len(aMvPar01)
        ::oQuery:SetString(nX++,aMvPar01[nY])
    Next nY
    If !lCusEmp
        ::oQuery:SetString(nX++,FWxFilial("SB1"))
    EndIf
    For nY := 1 to Len(aMvPar02)
        ::oQuery:SetString(nX++,aMvPar02[nY])
    Next nY
    For nY := 1 to Len(aMvPar10)
        ::oQuery:SetString(nX++,aMvPar10[nY])
    Next nY
    If lD3Servi .And. IntDL()
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'500')
        ::oQuery:SetString(nX++,'   ')
        ::oQuery:SetString(nX++,'500')
        ::oQuery:SetString(nX++,cMvCQ)
    EndIf
    ::oQuery:SetString(nX++,cProdImp)
    ::oQuery:SetString(nX++,' ')
    ::oQuery:SetString(nX++,' ')

    //Where para produtos sem movimentação
    If !Empty(cUnion)
        ::oQuery:SetUnsafe(nX++,cFilAnt)
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SB1"))
        EndIf
        For nY := 1 to Len(aMvPar01)
            ::oQuery:SetString(nX++,aMvPar01[nY])
        Next nY
        For nY := 1 to Len(aMvPar02)
            ::oQuery:SetString(nX++,aMvPar02[nY])
        Next nY
        For nY := 1 to Len(aMvPar10)
            ::oQuery:SetString(nX++,aMvPar10[nY])
        Next nY
        ::oQuery:SetString(nX++,cProdImp)
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SD1"))
            ::oQuery:SetString(nX++,FWxFilial("SF4"))
        EndIf
        ::oQuery:SetString(nX++,'S')
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par03")))
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par04")))
        ::oQuery:SetString(nX++,'LF')
        If !(lCusFil .Or. lCusEmp)
            For nY := 1 to Len(aMvPar06)
                ::oQuery:SetString(nX++,aMvPar06[nY])
            Next nY
        EndIf
        ::oQuery:SetString(nX++,' ')
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SB1"))
        EndIf
        For nY := 1 to Len(aMvPar02)
            ::oQuery:SetString(nX++,aMvPar02[nY])
        Next nY
        For nY := 1 to Len(aMvPar10)
            ::oQuery:SetString(nX++,aMvPar10[nY])
        Next nY
        ::oQuery:SetString(nX++,cProdImp)
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SD2"))
            ::oQuery:SetString(nX++,FWxFilial("SF4"))
        EndIf
        ::oQuery:SetString(nX++,'S')
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par03")))
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par04")))
        ::oQuery:SetString(nX++,'LF')
        If !(lCusFil .Or. lCusEmp)
            For nY := 1 to Len(aMvPar06)
                ::oQuery:SetString(nX++,aMvPar06[nY])
            Next nY
        EndIf
        ::oQuery:SetString(nX++,' ')
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SB1"))
        EndIf
        For nY := 1 to Len(aMvPar02)
            ::oQuery:SetString(nX++,aMvPar02[nY])
        Next nY
        For nY := 1 to Len(aMvPar10)
            ::oQuery:SetString(nX++,aMvPar10[nY])
        Next nY
        ::oQuery:SetString(nX++,cProdImp)
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SD3"))
        EndIf
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par03")))
        ::oQuery:SetString(nX++,DTOS(::oParameters:GetJsonObject("mv_par04")))
        If lD3Estorn
            ::oQuery:SetString(nX++,'S')
        EndIf
        If lD3Servi .And. IntDL()
            ::oQuery:SetString(nX++,'   ')
            ::oQuery:SetString(nX++,'   ')
            ::oQuery:SetString(nX++,'500')
            ::oQuery:SetString(nX++,'   ')
            ::oQuery:SetString(nX++,'500')
            ::oQuery:SetString(nX++,cMvCQ)
        EndIf
        If !(lCusFil .Or. lCusEmp)
            For nY := 1 to Len(aMvPar06)
                ::oQuery:SetString(nX++,aMvPar06[nY])
            Next nY
        EndIf
        ::oQuery:SetString(nX++,' ')
        If !lCusEmp
            ::oQuery:SetString(nX++,FWxFilial("SB1"))
        EndIf
        For nY := 1 to Len(aMvPar02)
            ::oQuery:SetString(nX++,aMvPar02[nY])
        Next nY
        For nY := 1 to Len(aMvPar10)
            ::oQuery:SetString(nX++,aMvPar10[nY])
        Next nY
        ::oQuery:SetString(nX++,cProdImp)
        ::oQuery:SetString(nX++,' ')
    EndIf
    ::oQuery:SetUnsafe(nX++,cOrder)

    cAliasTop := ::oQuery:OpenAlias()

Return cAliasTop

/*/{Protheus.doc} PocMedio
    POC query de movimentacoes do MATR900 com custo medio do movimento
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method getMovimentQuery(cQuery, cIdConfig, nSeq, cCondition) class ESTR900Service

    Local cNewQuery as character
    Local nPosOrderBy as numeric
    Local cSeq  as character

    Default cCondition := ""

    If nSeq == 1 
        cSeq := "SEQUENCIA"
    Elseif nSeq == 2
        cSeq := "SEQCALC"
    Endif

    cNewQuery := "SELECT  ROW_NUMBER() OVER (ORDER BY PRODUTO," + cSeq  + " DESC, NRECNO ) ORINV, "  //--1
    cNewQuery += " ROW_NUMBER() OVER (ORDER BY PRODUTO, " + cSeq  + " ASC, NRECNO ) ORDEM, "              //--2

    // TESTE
    // cNewQuery := "SELECT  ROW_NUMBER() OVER (ORDER BY SEQCALC DESC ) ORINV, " 
    // cNewQuery += " ROW_NUMBER() OVER (ORDER BY SEQCALC ) ORDEM, "

    cNewQuery += " ' ' AS ID_900B,"                                                                   //--3
    cNewQuery += " ' ' AS ID_900C,"                                                                   //--3
    cNewQuery += "'" + cIdConfig + "' AS CONFIG,"                                               
    cNewQuery += "  FILIAL, "                                                                   //--4
    cNewQuery += "  FILMOV, "                                                                   //--4
    cNewQuery += "  ARMAZEM, "
    cNewQuery += "  PRODUTO, "
    cNewQuery += "  DESCRI, "
    cNewQuery += "  ARQ, "
    cNewQuery += "  TIPO, "
    cNewQuery += "  UM, "
    cNewQuery += "  GRUPO, "
    cNewQuery += "  POSIPI, "
    cNewQuery += "  SEQCALC, "
    cNewQuery += "  DTDIGIT, "
    cNewQuery += "  TES, "
    cNewQuery += "  CF, "
    cNewQuery += "  X5_DESCRI, "
    cNewQuery += "  X5_DESCSPA, "
    cNewQuery += "  X5_DESCENG, "
    cNewQuery += "  SEQUENCIA, "
    cNewQuery += "  DOCUMENTO, "
    cNewQuery += "  SERIE, "
    cNewQuery += "  QUANTIDADE, "
    cNewQuery += "  QUANT2UM, "
    If cPaisLoc == "BRA"
        cNewQuery += "  PROJETO, "
    EndIf
    cNewQuery += "  OP, "
    cNewQuery += "  CC, "
    cNewQuery += "  FORNECEDOR, "
    cNewQuery += "  LOJA, "
    cNewQuery += "  PEDIDO, "
    cNewQuery += "  TIPONF, "
    cNewQuery += "  CUSTO, "
    cNewQuery += "  TRT, "
    cNewQuery += "  LOTE, "
    cNewQuery += "  NRECNO, "
    cNewQuery += "  ARMLOC, "
    cNewQuery += "  CASE WHEN QUANTIDADE = 0 THEN 0 ELSE ROUND((CUSTO / QUANTIDADE), " + cValToChar(TamSX3("B2_CM1")[2]) + ") END CUSMED, "
    cNewQuery += "  0 TOTQUANT, "
    cNewQuery += "  0 TOTCUSTO "
    cNewQuery += "FROM ( "

    nPosOrderBy := At("ORDER BY", cQuery)

    cNewQuery += Substr(cQuery, 1, (nPosOrderBy - 1))
    cNewQuery += ") ORIGINAL "
    // cNewQuery += "ORDER BY 3, 4, 11"

    If !Empty(cCondition) .And. cCondition == "movements"
        cNewQuery += cCondition
    EndIf

Return cNewQuery

/*/{Protheus.doc} getProductQuery
    Agrupa os produtos com base nos movimentos
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method getProductQuery(cIdConfig, cCusFil, cCondition) class ESTR900Service

    Local cNewQuery as character

    Default cCondition := ""

    cNewQuery := " SELECT  ROW_NUMBER() OVER (ORDER BY PRODUTO DESC ) ORINV , "
    cNewQuery += " ROW_NUMBER() OVER (ORDER BY PRODUTO) ORDEM, " 
    cNewQuery += " COUNT(CASE WHEN ARQ = 'SB1' THEN 1 END) AS NOTHASMOV, " //Caso lista produtos sem movimentos, vem com valor > 0 - mv_par07
    cNewQuery += " '"+ cIdConfig + "' AS CONFIG, "
    cNewQuery += " ' ' AS ID_900A, ' ' AS ID_900B, "
    
    If cCusFil == "E"
        cNewQuery += " ' ' AS FILIAL, ' ' AS ARMAZEM, PRODUTO, ' ' AS DESCRI,"
    Elseif cCusFil == "F"
        cNewQuery += " FILIAL, ' ' AS ARMAZEM, PRODUTO, DESCRI,"
    else
        cNewQuery += "FILIAL, ARMAZEM, PRODUTO, DESCRI,"
    Endif  

    cNewQuery += " 0 AS SALDOINI, 0 AS SALDOFIN, 0 AS QUANTINI, "
    cNewQuery += " 0 AS CMINI, 0 AS CMFIN, "

    cNewQuery += "0 AS QUANTIFIN, "
    cNewQuery += "0 AS VARIACAO,  "
    
    cNewQuery += "0 AS TOTQENT,  "
    cNewQuery += "0 AS TOTCENT,  "
    cNewQuery += "0 AS TOTQSAI,  "
    cNewQuery += "0 AS TOTCSAI,  "
    cNewQuery += "0 AS VARIAQUANT  "

    cNewQuery += " FROM " + ::cTableName01

    If !(cCusFil == "E")
       cNewQuery += " WHERE FILIAL = '" + cFilAnt + "'"
    Endif
    
    cNewQuery += " GROUP BY "

    If cCusFil == "E"
        cNewQuery += "PRODUTO"
    Elseif cCusFil == "F"
        cNewQuery += "FILIAL, PRODUTO, DESCRI"
    Else 
        cNewQuery += "FILIAL, ARMAZEM, PRODUTO, DESCRI"
    Endif

    If !Empty(cCondition) .And. cCondition == "products"
        cNewQuery += " " + cCondition
    EndIf

Return cNewQuery

/*/{Protheus.doc} PocMedio
    POC query de movimentacoes do MATR900 com custo medio do movimento
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method getItemsDoneQueryPerThread(jQuery, cCusFil) class ESTR900Service

    Local cNewQuery     as character
    Local cId           as character
    Local cProcess      as character

    cProcess := Upper(Alltrim((jQuery["process"])))

    If cProcess == "ESTR900C"
        cNewQuery := " SELECT  COUNT(B1_COD) DONE"
        cNewQuery += " FROM "
    Else 
        cNewQuery := " SELECT  COUNT(PRODUTO) DONE"
        cNewQuery += " FROM "
    Endif

    If cProcess == 'ESTR900B'
        cNewQuery += ::cTableName01
        cId := "ID_900B"
    Elseif cProcess == "ESTR900A" .OR. cProcess == "ESTR900"
        cNewQuery += ::cTableName02
        cId := "ID_900A"
    Elseif cProcess == "ESTR900C"
        cNewQuery += ::cTableName03
        cId := "ID"
    Endif
    //cNewQuery += " WHERE ID = '" + Alltrim(cId) + "' AND ORDEM >= " + cValToChar(nStart) + " AND ORDEM <= " +  cValToChar(nEnd)
    cNewQuery += " WHERE " + cId + " = ? AND ORDEM >= ? AND ORDEM <= ? "
    
    If !(cCusFil == 'E')
        cNewQuery += " AND FILIAL = ? "
    Endif

    If cCusFil == 'A' .and. cProcess == 'ESTR900B'
        cNewQuery += " AND ARMAZEM = ? "
    Endif

Return cNewQuery

/*/{Protheus.doc} PocMedio
    POC query de movimentacoes do MATR900 com custo medio do movimento
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
    /*/
Method getItemByCode(cAlias , cField, aItems, lBindQuery) class ESTR900Service

    Local cQuery    as character
    Local cFrom     as character
    Local cTo       as character
    Local nX        as Numeric
    Local aItems    as Array

    Default lBindQuery := .T.
    
    cFrom   := Repl(" ",TamSX3(cField)[1])
    cTo     := Repl("z",TamSX3(cField)[1])

    cQuery := ""    
    If Len(aItems) > 0
        For nX := 1 to Len(aItems)
            If nX != 1 
                cQuery += " OR "
            Else 
                cQuery += " ("
            Endif
            If lBindQuery
                cQuery += cAlias + "." + cField + " = ? " 
            Else
                cQuery += cAlias + "." + cField + " = '" + aItems[nX] + "'"
            EndIf
        Next nX

        cQuery += ")"
    Else 
        cQuery += " 'A' = 'A' " //Sempre verdadeiro
    Endif

Return cQuery

/*/{Protheus.doc} PocMedio
    POC query de movimentacoes do MATR900 com custo medio do movimento
    @type  Function
    @author Squad.Entradas
    @since 26/09/2022
    @version 1.0
    @param lCusFil, logical, Tipo do custo
    @return character, Alias da query
/*/

Method getWarehouseByCode(cField, aItems, lCusFil,lBindQuery) class ESTR900Service

    Local cQuery as character
    Local cFrom as character
    Local cTo as character
    Local nX as Numeric
    Local aItems as Array
    Local lIsLocProc as logical
    Local nIndexLocProc as numeric

    Default lBindQuery := .T.

    nIndexLocProc := AScan(aItems, AllTrim(GetMvNNR("MV_LOCPROC", "99")))
    lIsLocProc := nIndexLocProc > 0
    
    If lIsLocProc .and. lCusFil
        aItems := aDel(aItems, nIndexLocProc)
        ASize(aItems, Len(aItems)-1)
    Endif
    
    cFrom := Repl(" ",TamSX3(cField)[1])
    cTo := Repl("z",TamSX3(cField)[1])

    cQuery := ""    

    For nX := 1 to Len(aItems)        
        If nX != 1 
            cQuery += " OR "
        Else 
            cQuery += " ("
        Endif

        If aItems[nX] != NIL
            If lBindQuery
                cQuery += cField + " = ? "
            Else
                cQuery += cField + " = '" + aItems[nX] + "'"
            EndIf
        Endif

    Next nX

    cQuery += ")"

Return cQuery

/*/{Protheus.doc} ESTR900Service:New()
    Metodo responsavel por instanciar e iniciar as variaveis da class acUser
    @type  Metodo
    @author Pedro Missaglia
    @since  Outubro 02, 2020
    @version 12.1.27
/*/
method IsBodyValidFromStatus(jQuery) class ESTR900Service

Local lBodyValid    as Logical
Local aKeys         as Array
Local nx            as Numeric
Local cAux          as Character 

nX := 0
cAux := ""
lBodyValid := .T.
aKeys := { {"id", "C", "caracter"}, {"process", "C", "caracter"}, {"configuration", "C", "caracter"}}

If jQuery == NIL
    ::cError := STR0009
    lBodyValid := .F.
Else 
    For nX := 1 to Len(aKeys)
        cAux := aKeys[nX][1]
        If jQuery:GetJsonObject(cAux) != NIL
            If !(Valtype(jQuery:GetJsonObject(cAux)) == aKeys[nX][2])
                ::cError := STR0010 + cAux + STR0011 + aKeys[nX][3] + STR0012
                lBodyValid := .F.
                Exit
            Endif
        Else 
            ::cError := STR0010 + aKeys[nX][1] + STR0013
            lBodyValid := .F.
            Exit
        Endif
    Next nX
Endif 

return lBodyValid

/*/{Protheus.doc} Mtstart
    Funcao chamada para subir as threads da MANUALJOB
    @type  Function
    @author Squad Entradas
    @since 20/06/2022
    @version 1.0
    @param cParam, character, empresa e filial
/*/
Method setESTR900(cId, cIdConfig, dDataIni, dDataFim, oParameters, aBranches, cUserName, cPayload)  class ESTR900Service

Local nForFilial    := 0
Local oRep900       := Nil
Local oServ900A     := Nil
Local oStructTable  := JsonObject():New()
Local oObj          := JsonObject():New()
Local oPayLoad      := JsonObject():New()
Local oD4A          := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
Local lMayIStart    := .F.
Local cFilBack      := cFilAnt
Local cCondition    := ""
Local cExtraField   := ""
Local cAliasTop     := ""
Local nMoeda        := 0

::oParameters   := oParameters
::oParameters['mv_par03'] :=  dDataIni
::oParameters['mv_par04'] :=  dDataFim

nMoeda := ::oParameters['mv_par07'] 

::aBranches     := aBranches

::cTableName01 := 'A' + Upper(cIdConfig) + '_01'
::cTableName02 := 'A' + Upper(cIdConfig) + '_02'

oPayLoad:fromJson(cPayload)
If oPayLoad:HasProperty("condition") .And. !Empty(oPayLoad["condition"])
    cCondition := oPayLoad["condition"]
EndIf
If oPayLoad:HasProperty("extraField") .And. !Empty(oPayLoad["extraField"])
    cExtraField := oPayLoad["extraField"]
EndIf

oServ900A   := totvs.protheus.backoffice.stock.trackcosts.ESTR900A.service.ESTR900AService():New()
oRep900     := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()

oRep900:createMovimentTable(::cTableName01)
oRep900:createProductTable(::cTableName02)

oStructTable["table"] := JsonObject():New() 

oStructTable["table"]["movement"] := JsonObject():New() 
oStructTable["table"]["product"] := JsonObject():New()

oStructTable["table"]["movement"]["fields"] := oRep900:getMovementFields(cExtraField)
oStructTable["table"]["movement"]["realName"] := ::cTableName01

oStructTable["table"]["product"]["fields"] := oRep900:getProductFields()
oStructTable["table"]["product"]["realName"] := ::cTableName02

oStructTable["branch"] := JsonObject():New()

For nForFilial := 1 To Len(aBranches)

    cFilAnt := ::aBranches[nForFilial, 2]

    cCusFil    := Alltrim(SuperGetMv("MV_CUSFIL", .F., "A"))
    
    // Chamada das regras de negocio para query
    ::setRules(cCusFil == 'E')

    // Chamada da query com as movimentacoes SD1 / SD2 / SD3
    cAliasTop := ::setQuery()

    cQuery := ::oQuery:GetFixQuery()

    oStructTable["branch"][cFilAnt] := JsonObject():New()

    oStructTable["branch"][cFilAnt]['rawQuery'] := cQuery
    
    oStructTable["branch"][cFilAnt]['queryMovements']   := ::getMovimentQuery(oStructTable["branch"][cFilAnt]['rawQuery'], cIdConfig, ::oParameters:GetJsonObject("mv_par08"), cCondition)
    oStructTable["branch"][cFilAnt]['queryProducts']    := ::getProductQuery(cIdConfig, cCusFil, cCondition)

    If oRep900:insertItems(oStructTable["table"]["movement"]["realName"], oStructTable["table"]["movement"]["fields"], oStructTable["branch"][cFilAnt]['queryMovements'])   
        If oRep900:insertItems(oStructTable["table"]["product"]["realName"], oStructTable["table"]["product"]["fields"], oStructTable["branch"][cFilAnt]['queryProducts'])   
            lMayIStart := .T.
        Else 

            cFilAnt := cFilBack

            oObj["D4A_STATUS"]  := 'ERROR'
            oObj["D4A_ERROR"]   := STR0014
            oObj["D4A_STACK"]   := oRep900:getSQLError()
            oD4A:UPDATE(oObj)
        Endif
    Else
        cFilAnt := cFilBack
    
        oObj["D4A_STATUS"]  := 'ERROR'
        oObj["D4A_ERROR"]   := STR0015
        oObj["D4A_STACK"]   := oRep900:getSQLError()
        oD4A:UPDATE(oObj)

    Endif

    If Select(cAliasTop) > 0
        (cAliasTop)->(DbCloseArea())
    EndIf

    If cCusFil == 'E'
        Exit
    EndIf

Next nForFilial

cFilAnt := cFilBack

If lMayIStart 
    oServ900A:calculateESTR900A(oParameters:GetJsonObject("mv_par03"), oParameters:GetJsonObject("mv_par04"), cId, cIdConfig, oStructTable, cUserName, ::aBranches, nMoeda, cPayload)
Endif

oD4A:SEEK(cId, 1, "ESTR900")
If lMayIStart
    oD4A:PATCH('D4A_STATUS', 'FINISHED')
Endif

FreeObj(oPayLoad)
oPayLoad := Nil

Return

/*/{Protheus.doc} getIdByConfig()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getIdByConfig(jParams) class ESTR900Service  

    Local oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
    Local cIdConfig := ""
    Local cProc     := ""
    Local cId       := ""

    ::cError := ""
    ::cEmpty := ""

    If jParams:HasProperty("configuration")
        cIdConfig := Alltrim(jParams:getJsonText("configuration"))
    EndIf

    If jParams:HasProperty("process")
        cProc := Alltrim(jParams:getJsonText("process"))
    EndIf

    If !Empty(cIdConfig)

        If !Empty(cProc)

            cId := oRepository:getIdByConfig(::getQuery(cIdConfig, cProc))

            If Empty(cId)
                ::cEmpty := STR0031 // "Inconsistência ao gerar resultado de movimentos. Crie um novo filtro para refazer a consulta."
            Endif
        Else 
            ::cError := STR0017 // "Não foi informado o parâmetro de query obrigatório - process."
        Endif
    Else 
        ::cError := STR0003 // "Não foi informado o parâmetro de query obrigatório - configuration."

    Endif

return cId

/*/{Protheus.doc} getIdByConfig
    Retorna o id de processamento referente a configuracao e processo informados
    @author Squad.Entradas
    @since 21/02/2024
    @version 1.0
    @param jParams, json objec, parametros recebidos pela api
    @return cId, character, id de processamento
/*/

Method getThread(jParams) class ESTR900Service  

    Local oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
    Local cIdConfig := ''
    Local cProc     := ''
    Local cId       := ''
    Local oThread   := JsonObject():New()
    
    ::cError := ""
    ::cEmpty := ""

    If jParams:HasProperty("configuration")
        cIdConfig := Alltrim(jParams:getJsonText("configuration")) 
    EndIf
    If jParams:HasProperty("process")
        cProc     := Alltrim(jParams:getJsonText("process")) 
    EndIf
    If jParams:HasProperty("id")
        cId       := Alltrim(jParams:getJsonText("id")) 
    EndIf

    If jParams:HasProperty("configuration") .and. !Empty(cIdConfig)
            
        If jParams:HasProperty("process") .and. !Empty(cProc)
            
            oThread := oRepository:getThread(::getThreadQuery(cId, cIdConfig, cProc))

            If Empty(oThread["id"])
                ::cEmpty := STR0016
            Endif
        Else 
            ::cError := STR0017
        Endif
    Else 
        ::cError := STR0003

    Endif

return oThread

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getQuery(cIdConfig, cProc) class ESTR900Service  

Local cQuery := ""

If Alltrim(Upper(TcGetDb())) == "ORACLE"
   cQuery := "SELECT D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, TO_CHAR(D4A_BODY) AS D4A_BODY FROM "  
Else
    cQuery := "SELECT D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, D4A_BODY FROM " 
Endif

cQuery += RetSqlName("D4A") + " WHERE D4A_CONFIG = '" + cIdConfig + "' AND "
cQuery += "D4A_PROC = '" + cProc + "' AND D_E_L_E_T_ = ' ' "

If Alltrim(Upper(TcGetDb())) == "ORACLE"
    cQuery += "GROUP BY D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, TO_CHAR(D4A_BODY)"
Else 
    cQuery += "GROUP BY D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, D4A_BODY"
Endif

return cQuery

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getThreadQuery(cId, cIdConfig, cProc) class ESTR900Service  

Local cQuery := ""

If Alltrim(Upper(TcGetDb())) == "ORACLE"
    cQuery := "SELECT D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, TO_CHAR(D4A_BODY) FROM "
Else
	cQuery := "SELECT D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, D4A_BODY FROM " 
EndIf
cQuery += RetSqlName("D4A") + " WHERE D4A_CONFIG = '" + cIdConfig + "' AND "
cQuery += "D4A_PROC = '" + cProc + "' AND D4A_ID = '" + cId + "' AND D_E_L_E_T_ = ' ' "
If Alltrim(Upper(TcGetDb())) == "ORACLE"
    cQuery += "GROUP BY D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, TO_CHAR(D4A_BODY)"
Else
	cQuery += "GROUP BY D4A_FILIAL, D4A_ID, D4A_CONFIG, D4A_PROC, D4A_BODY"
EndIf

return cQuery


/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getMovements(jParams) class ESTR900Service  

Local oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
Local oTempTable    := ac.TableTempory.Repository.acTableTemporyRepository():new()
Local aFilter       := {}
Local nPage         := 1
Local nPageSize     := 10
Local cIdConfig     := Alltrim(jParams:getJsonText("configuration"))
Local cId           := Alltrim(jParams:getJsonText("id"))

::cError        := ""
::cMovements    := ""
::cTableName01  := "A" + cIdConfig + "_01"

If oTempTable:doesTableIdExist(::cTableName01)
    ::cIdConfiguration := cIdConfig
    
    If jParams:HasProperty("page") .and. !Empty(jParams:getJsonText("page"))
        nPage := VAL( jParams:getJsonText("page"))
    Endif

    If jParams:HasProperty("pageSize") .and. !Empty(jParams:getJsonText("pageSize"))
        nPageSize := VAL( jParams:getJsonText("pageSize"))
    Endif

    If jParams:HasProperty("filter")
        aFilter := {{"FILTER", jParams:getJsonText("filter")}}
    Else 
        aFilter := {{"FILTER", ''}}
    Endif

    ::cMovements := oRepository:getMovements(;
    ::getListMovementsColumns(),;
    ::getListQueryMovements(Alltrim(cIdConfig)),;
    ::getListWhereMovements(Alltrim(cIdConfig), Alltrim(cId)), nPage, nPageSize, aFilter)

    ::addMovementType()

Else 
    ::cError := STR0001
Endif

return

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListMovementsColumns() CLASS ESTR900Service

Local aColumns      := {}
Local aKeys         := {}
Local aRawFields    := {}
Local nPosAux       := 0
Local oRep          := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
Local nX            := 0

aRawFields := oRep:getMovementFields()

aAdd(aKeys, {"inverseOrder"          ,"ORINV"        })
aAdd(aKeys, {"order"                 ,"ORDEM"        })
aAdd(aKeys, {"id900b"                ,"ID_900B"      })
aAdd(aKeys, {"id900c"                ,"ID_900C"      })
aAdd(aKeys, {"configuration"         ,"CONFIG"       })
aAdd(aKeys, {"branch"                ,"FILIAL"       })
aAdd(aKeys, {"branchMovement"        ,"FILMOV"       })
aAdd(aKeys, {"warehouse"             ,"ARMAZEM"      })
aAdd(aKeys, {"code"                  ,"PRODUTO"      })
aAdd(aKeys, {"description"           ,"DESCRI"       })
aAdd(aKeys, {"archive"               ,"ARQ"          })
aAdd(aKeys, {"type"                  ,"TIPO"         })
aAdd(aKeys, {"unityMesure"           ,"UM"           })
aAdd(aKeys, {"group"                 ,"GRUPO"        })
aAdd(aKeys, {"posIpi"                ,"POSIPI"       })
aAdd(aKeys, {"calculationSequence"   ,"SEQCALC"      })
aAdd(aKeys, {"typingDate"            ,"DTDIGIT"      })
aAdd(aKeys, {"tes"                   ,"TES"          })
aAdd(aKeys, {"cf"                    ,"CF"           })
aAdd(aKeys, {"x5_desc"               ,"X5_DESCRI"    })
aAdd(aKeys, {"x5_descSpa"            ,"X5_DESCSPA"   })
aAdd(aKeys, {"x5_descEng"            ,"X5_DESCENG"   })
aAdd(aKeys, {"sequence"              ,"SEQUENCIA"    })
aAdd(aKeys, {"document"              ,"DOCUMENTO"    })
aAdd(aKeys, {"series"                ,"SERIE"        })
aAdd(aKeys, {"quantity"              ,"QUANTIDADE"   })
aAdd(aKeys, {"secondaryQuantity"     ,"QUANT2UM"     })
If cPaisLoc == "BRA"
    aAdd(aKeys, {"project"               ,"PROJETO"      })
EndIf
aAdd(aKeys, {"op"                    ,"OP"           })
aAdd(aKeys, {"cc"                    ,"CC"           })
aAdd(aKeys, {"supplier"              ,"FORNECEDOR"   })
aAdd(aKeys, {"store"                 ,"LOJA"         })
aAdd(aKeys, {"order"                 ,"PEDIDO"       })
aAdd(aKeys, {"nfType"                ,"TIPONF"       })
aAdd(aKeys, {"cost"                  ,"CUSTO"        })
aAdd(aKeys, {"trt"                   ,"TRT"          })
aAdd(aKeys, {"batch"                 ,"LOTE"         })
aAdd(aKeys, {"numberRecno"           ,"NRECNO"       })
aAdd(aKeys, {"locWarehouse"          ,"ARMLOC"       })
aAdd(aKeys, {"averageCost"           ,"CUSMED"       })
aAdd(aKeys, {"totalQuantity"         ,"TOTQUANT"     })
aAdd(aKeys, {"totalCost"             ,"TOTCUSTO"     })

For nX := 1 to Len(aKeys)
    
    nPosAux := aScan(aRawFields, {|x| x[1] == aKeys[nX][2]})

    aAdd(aColumns, { aKeys[nX][1] , aRawFields[nPosAux][1]   , aRawFields[nPosAux][2] , aRawFields[nPosAux][3] , aRawFields[nPosAux][4]})

Next nX

Return aColumns

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListProductsColumns() CLASS ESTR900Service

Local aColumns      := {}
Local aKeys         := {}
Local aRawFields    := {}
Local nPosAux       := 0
Local oRep          := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
Local nX            := 0
Local cMV_CUSFIL    := SuperGetMV('MV_CUSFIL',.F.,"A") 

aRawFields := oRep:getProductFields()

aAdd(aKeys, {"inverseOrder"      , "ORINV"   })
aAdd(aKeys, {"order"             , "ORDEM"   })
aAdd(aKeys, {"configuration"     , "CONFIG"  })
aAdd(aKeys, {"id900A"            , "ID_900A" })
aAdd(aKeys, {"id900B"            , "ID_900B" })

If !(cMV_CUSFIL == 'E')
    aAdd(aKeys, {"branch"        , "FILIAL" })
Endif

If !(cMV_CUSFIL == 'F')
    aAdd(aKeys, {"warehouse"     , "ARMAZEM"})
Endif

aAdd(aKeys, {"code"                 , "PRODUTO"  })
aAdd(aKeys, {"description"          , "DESCRI"   })
aAdd(aKeys, {"notHasMov"            , "NOTHASMOV"}) //Caso lista produtos sem movimentos, vem com valor > 0 - mv_par07
aAdd(aKeys, {"initialBalance"       , "SALDOINI" })
aAdd(aKeys, {"finalBalance"         , "SALDOFIN" })
aAdd(aKeys, {"initialQuantity"      , "QUANTINI" })
aAdd(aKeys, {"finalQuantity"        , "QUANTFIN" })
aAdd(aKeys, {"variation"            , "VARIACAO" })
aAdd(aKeys, {"initialAverageCost"   , "CMINI"    })
aAdd(aKeys, {"finalAverageCost"     , "CMIFIN"   })

aAdd(aKeys, {"inflowTotalQuantity"  , "TOTQENT"  })
aAdd(aKeys, {"inflowTotalBalance"   , "TOTCENT"  })

aAdd(aKeys, {"outflowTotalQuantity" , "TOTQSAI"  })
aAdd(aKeys, {"outflowTotalBalance"  , "TOTCSAI"  })

aAdd(aKeys, {"quantityVariation"  , "VARIAQUANT" })

For nX := 1 to Len(aKeys)
    
    nPosAux := aScan(aRawFields, {|x| x[1] == aKeys[nX][2]})

    aAdd(aColumns, { aKeys[nX][1] , aRawFields[nPosAux][1]   , aRawFields[nPosAux][2] , aRawFields[nPosAux][3] , aRawFields[nPosAux][4]})

Next nX

Return aColumns

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListQueryMovements(cId) CLASS ESTR900Service

Local cQuery := ""

cQuery += " SELECT #QueryFields# "
// cQuery += " FROM " + ::cTableName02
cQuery += " FROM A" + cId + "_01"
cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListWhereMovements(cIdConfig, cId) CLASS ESTR900Service

Local cWhere := ""

// cWhere += "CONFIG =  '" + ::cIdConfiguration + "' AND "

cWhere += "CONFIG =  '" + cIdConfig + "' AND "
cWhere += "ID_900B =  '" + cId + "' AND "
cWhere += "D_E_L_E_T_ =  ' '"

Return cWhere

/*/{Protheus.doc} acLogService:New()
    @type  Metodo
    @since  Novembro 10, 2020
    @version 12.1.27
/*/

Method getProducts(jParams) class ESTR900Service  

Local oRepository := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
Local oTempTable    := ac.TableTempory.Repository.acTableTemporyRepository():new()
Local aFilter       := {}
Local nPage         := 1
Local nPageSize     := 10

::cError        := ""
::cProducts     := JsonObject():New()
::cTableName02  := "A" + jParams:getJsonText("configuration") + "_02"

If oTempTable:doesTableIdExist(::cTableName02)
    ::cIdConfiguration := jParams:getJsonText("configuration")

    If jParams:HasProperty("page") .and. !Empty(jParams:getJsonText("page"))
        nPage := VAL( jParams:getJsonText("page"))
    Endif

    If jParams:HasProperty("pageSize") .and. !Empty(jParams:getJsonText("pageSize"))
        nPageSize := VAL( jParams:getJsonText("pageSize"))
    Endif

    If jParams:HasProperty("filter")
        aFilter := {{"FILTER", jParams:getJsonText("filter")}}
    Else 
        aFilter := {{"FILTER", ''}}
    Endif

    ::cProducts  := oRepository:getProducts(::getListProductsColumns(), ::getListQueryProducts(), ::getListWhereProducts(), nPage, nPageSize, aFilter)
Else 
    ::cError := STR0001
Endif

return

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListQueryProducts() CLASS ESTR900Service

Local cQuery := ""

cQuery += " SELECT #QueryFields# "
cQuery += " FROM " + ::cTableName02
cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method getListWhereProducts() CLASS ESTR900Service

Local cWhere := ""

cWhere += "CONFIG =  '" + ::cIdConfiguration + "' AND "
cWhere += "D_E_L_E_T_ =  ' '"

Return cWhere

/*/{Protheus.doc} acResultsAnalysisRepository:getFullFields()
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method addMovementType() CLASS ESTR900Service

Local nX := 0
Local cTypeAux := ""
Local oMovements := JsonObject():New()

oMovements:FromJson(::cMovements)

If Valtype(oMovements["items"]) == 'A'

    For nX := 1 to Len(oMovements["items"])
        
        cTypeAux := oMovements["items"][nX]:GetJsonText("archive")
        
        If cTypeAux == "SD1"

            oMovements["items"][nX]["archiveLabel"] := "entryDocument"

        Elseif cTypeAux == "SD2"
            oMovements["items"][nX]["archiveLabel"] := "exitDocument"
        Else 

            If cTypeAux == "SD3" .and. !Empty(oMovements["items"][nX]:GetJsonText("op"))
                oMovements["items"][nX]["archiveLabel"] := "productionOrder"
            Else 
                oMovements["items"][nX]["archiveLabel"] := "internalMovements"
            Endif

            oMovements["items"][nX]["x5_desc"] := ::getSTRFromCF(oMovements["items"][nX]["cf"])

            
        Endif
    Next nX
Endif

::cMovements := oMovements:ToJson()

Return 

/*/{Protheus.doc} containsWarehouseLocProc
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author pedro.missaglia
    @since  29/11/2023
    @version 12.1.22
/*/
Method getSTRFromCF(cCf) CLASS ESTR900Service

Local cText := ''

If SUBSTR(cCf, 2, 2) == 'E0'
    cText := Upper(STR0019)
Elseif SUBSTR(cCf, 2, 2) == 'E1'
    cText := Upper(STR0020)
Elseif SUBSTR(cCf, 2, 2) == 'E2'
    cText := Upper(STR0021)
Elseif SUBSTR(cCf, 2, 2) == 'E3'
    cText := Upper(STR0022)
Elseif SUBSTR(cCf, 2, 2) == 'E4'
    cText := Upper(STR0023)
Elseif SUBSTR(cCf, 2, 2) == 'E5'
    cText := Upper(STR0024)
Elseif SUBSTR(cCf, 2, 2) == 'E6'
    cText := Upper(STR0025)
Elseif SUBSTR(cCf, 2, 2) == 'E7'
    cText := Upper(STR0026)
Elseif SUBSTR(cCf, 2, 2) == 'E8'
    cText := Upper(STR0027)
Elseif SUBSTR(cCf, 2, 2) == 'E9'
    cText := Upper(STR0028)
Elseif SUBSTR(cCf, 2, 2) == 'EA'
    cText := Upper(STR0029)
Elseif SUBSTR(cCf, 1, 2) == 'PR'
    cText := Upper(STR0030)
Endif

Return cText



/*/{Protheus.doc} rebootThread
    Reinicia as threads que foram interrompidas em processamento anterior
    @type  Metodo
    @author andre.maximo
    @since  Jan 29, 2021
    @version 12.1.27
/*/
Method rebootThread() CLASS ESTR900Service

Local oESTR900      := totvs.protheus.backoffice.stock.trackcosts.ESTR900.repository.ESTR900Repository():New()
Local oESTR900A     := totvs.protheus.backoffice.stock.trackcosts.ESTR900A.repository.ESTR900ARepository():New()
Local oESTR900B     := totvs.protheus.backoffice.stock.trackcosts.ESTR900B.repository.ESTR900BRepository():New()
Local oESTR900C     := totvs.protheus.backoffice.stock.trackcosts.ESTR900C.repository.ESTR900CRepository():New()
Local oD4B          := totvs.protheus.backoffice.stock.trackcosts.model.D4B.D4B():new()
Local oD4A          := totvs.protheus.backoffice.stock.trackcosts.model.D4A.D4A():New()
Local nTimeDif      := 0
Local lSameDate     := .F.
Local cGlbValue     := ''
Local cGlbName      := cFilAnt + Alltrim(D4A->D4A_ID) + Alltrim(D4A->D4A_THRNUM)
Local oBody         := JsonObject():New()
Local lCusEmp       := AllTrim(SuperGetMv("MV_CUSFIL", .F., "A")) == "E"
Local oReproc       := JsonObject():New()
Local oObj          := JsonObject():New()

lSameDate := D4A->D4A_DTEXEC == MsDate()

If lSameDate
    nTimeDif := SubHoras(SubStr(Time(),1,TamSx3("D4A_HREXEC")[1]), D4A->D4A_HREXEC) 
Endif 

If !lSameDate .OR. nTimeDif > 0.01

    If LockByName(cFilAnt + '_' + Alltrim(D4A->D4A_PROC) + '_' +  Alltrim(D4A->D4A_ID) + '_' + Alltrim(D4A->D4A_THRNUM), .F. , .F.)
        UnlockByName(cFilAnt + '_' + Alltrim(D4A->D4A_PROC) + '_' +  Alltrim(D4A->D4A_ID) + '_' + Alltrim(D4A->D4A_THRNUM), .F. , .F.)    

        cGlbValue := GetGlbValue(cGlbName)

        If Trim(cGlbValue) == ""

            cGlbValue := 'PROCESSING' + Alltrim(D4A->D4A_PROC)
            PutGlbValue( cGlbName, cGlbValue )

            If Alltrim(D4A->D4A_PROC) == "ESTR900"

                oBody:FromJson(D4A->D4A_BODY)

                ::setBranches(oBody["branches"], lCusEmp)
                ::setParameters(oBody["parameters"])

                oESTR900:startAsyncESTR900(Alltrim(D4A->D4A_ID), Alltrim(D4A->D4A_CONFIG), StoD(oBody["parameters"][3]["value"]), StoD(oBody["parameters"][4]["value"]), ::oParameters, ::aBranches, oBody:toJson(), .T.)

            Elseif Alltrim(D4A->D4A_PROC) == "ESTR900A"
                
                oBody:FromJson(D4A->D4A_BODY)

                oReproc["thread"]   := Alltrim(D4A->D4A_THRNUM)
                oReproc["start"]    := D4A->D4A_START
                oReproc["end"]      := D4A->D4A_END
                oReproc["total"]    := D4A->D4A_TOTAL

                
                oESTR900A:startAsyncESTR900A(StoD(oBody["parameters"][3]["value"]), StoD(oBody["parameters"][4]["value"]),;
                    Alltrim(D4A->D4A_ID), Alltrim(D4A->D4A_CONFIG), cUserName, D4A->D4A_TOTAL,;
                    oD4B:getById(Alltrim(D4A->D4A_CONFIG))["D4B_MOEDA"],;
                    oBody:ToJson(), .T., oReproc;
                )

            Elseif Alltrim(D4A->D4A_PROC) == "ESTR900B"
                
                oBody:FromJson(D4A->D4A_BODY)

                oReproc["thread"]   := Alltrim(D4A->D4A_THRNUM)
                oReproc["start"]    := D4A->D4A_START
                oReproc["end"]      := D4A->D4A_END
                oReproc["total"]    := D4A->D4A_TOTAL
                oReproc["id"]       := Alltrim(D4A->D4A_ID)

                oESTR900B:startAsyncESTR900B(oBody, Nil, Nil, "MONO",.T., oReproc)

            Elseif  Alltrim(D4A->D4A_PROC) == "ESTR900C"
                
                oBody:FromJson(D4A->D4A_BODY)

                ::setParameters(oBody:GetJsonObject("parameters"))

                oESTR900C:startAsyncESTR900C(Alltrim(D4A->D4A_ID), Alltrim(D4A->D4A_CONFIG), ::oParameters, oBody:toJson(), .T.)
            Endif
        Else 
            ClearGlbValue( cGlbName )
            oObj["D4A_STATUS"]  := "ERROR"
            oObj["D4A_ERROR"]   := STR0018
            oObj["D4A_STACK"]   := STR0018
            oObj["D4A_DTEXEC"]  := MsDate()
            oObj["D4A_HREXEC"]  := SubStr(Time(),1,TamSx3("D4A_HREXEC")[1])
            
            oD4A:UPDATE(oObj)

        Endif
    Endif
Endif

Return 

/*/{Protheus.doc} containsWarehouseLocProc
    Metodo responsavel por verificar se existe uma tabela com o Id informado
    @type  Metodo
    @author pedro.missaglia
    @since  29/11/2023
    @version 12.1.22
/*/
Method containsWarehouseLocProc() CLASS ESTR900Service

Local cLocProc  := GetMvNNR('MV_LOCPROC','99')
Local nX        := 0
Local lLocProc  := .F.   

For nX := 1 to Len(self:oParameters:GetJsonObject("mv_par06"))
    If self:oParameters:GetJsonObject("mv_par06")[nX] == cLocProc
        lLocProc := .T.
        Exit
    Endif
Next nX

Return lLocProc

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() CLASS ESTR900Service
    Local nVersion := 200
Return nVersion
