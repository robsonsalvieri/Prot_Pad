#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.stock.trackcosts.d4b.repository
using namespace totvs.protheus.backoffice.stock.trackcosts.model.D4B

/*/{Protheus.doc} D4BRepository()
    Classe responsavel pelas regras de negocio e montagem de estruturas das requisicoes
    POST/PUT/PATCH/GET na tabela D4B
    @type Class
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
/*/
Class D4BRepository FROM FWAdapterBaseV2

    public method New()
    public method setModelD4B()
    public method getQueryD4B()
    public method deleteQueryD4B()
    public method getListD4B()
    public method version()

EndClass

/*/{Protheus.doc} New()
    Metodo responsavel por instanciar a classe e iniciar variaveis
    @type Method
    @author Adriano Vieira
    @since 20/01/2023
    @version 1.0
/*/
Method new(cVerbo as Character, lList as Logical) Class D4BRepository

    Default cVerbo  := 'GET'
    Default lList   := .T.
    _Super:New(cVerbo, lList)

Return Self

/*/{Protheus.doc} setModelD4B()
    Metodo responsavel montar a estrutura a ser gravada nas requisicoes de POST/PUT/PATCH
    @type  Metodo
    @author Adriano Vieira
    @since  20/01/2023
    @version 1.0
/*/
Method setModelD4B(aParameters as array,cUser as Character,dDate as Date,cHora as Character,cId as Character,aBranches as array, nOperation as numeric) class D4BRepository

Local oObj      := JsonObject():New()   as Object
Local oD4BModel := JsonObject():New()   as Object
Local oProd     := JsonObject():New()   as Object
Local oType     := JsonObject():New()   as Object
Local oLocal    := JsonObject():New()   as Object
Local oGroup    := JsonObject():New()   as Object
Local oBranch   := JsonObject():New()   as Object
Local nX                                as Numeric
Local xAux      := NIL

Default cId := ""

oD4BModel := totvs.protheus.backoffice.stock.trackcosts.model.D4B.D4B():new()

If aBranches <> NIL
    oBranch["branches"]  := aBranches
EndIf

For nX := 1 to Len(aParameters)
    
    aParameters[nX]:GetJsonValue("parameter",@xAux)

    Do Case
        Case xAux == "mv_par01"
            If !Empty(aParameters[nX]["value"])
                oProd["products"]   := aParameters[nX]["value"]
                oObj["D4B_PRODUT"]  := oProd:ToJson()
            EndIf
        Case xAux == "mv_par02"
            If !Empty(aParameters[nX]["value"])
                oType["types"]      := aParameters[nX]["value"]
                oObj["D4B_TIPO"]    := oType:ToJson()
            EndIf
        Case xAux == "mv_par03"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_DATAIN"]  := sToD(aParameters[nX]["value"])
            EndIf
        Case xAux == "mv_par04"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_DATAFN"]  := sToD(aParameters[nX]["value"])
            EndIf
        Case xAux == "mv_par05"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_PRODMO"]  := aParameters[nX]["value"]
            EndIf
        Case xAux == "mv_par06"
            If !Empty(aParameters[nX]["value"])
                oLocal["warehouses"]:= aParameters[nX]["value"]
                oObj["D4B_LOCAL"]   := oLocal:ToJson()
            EndIf
        Case xAux == "mv_par07"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_MOEDA"]   := aParameters[nX]["value"]
            EndIf
        Case xAux == "mv_par08"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_SEQDIG"]  := aParameters[nX]["value"]
            EndIf
        Case xAux == "mv_par09"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_TRANSL"]  := aParameters[nX]["value"]
            EndIf
        Case xAux == "mv_par10"
            If !Empty(aParameters[nX]["value"])
                oGroup["groups"]    := aParameters[nX]["value"]
                oObj["D4B_GRUPO"]   := oGroup:ToJson()
            EndIf
        Case xAux == "mv_par11"
            If !Empty(aParameters[nX]["value"])
                oObj["D4B_TCUSTO"]  := aParameters[nX]["value"]
            EndIf
    EndCase
Next

oObj["D4B_DTEXEC"]  := dDate
oObj["D4B_HREXEC"]  := cHora


If nOperation == 3
    oObj["D4B_FILIAL"]  := xFilial("D4B")
    oObj["D4B_USER"]    := cUser
    oObj["D4B_ID"]      := cId
    oObj["D4B_ACTIVE"]  := .T.
    oObj["D4B_BRANCH"]  := oBranch:ToJson()

    oD4BModel:INSERT(oObj)
ElseIf nOperation == 4
    If aBranches <> NIL
        oObj["D4B_BRANCH"]  := oBranch:ToJson()
    EndIf

    oD4BModel:UPDATE(oObj)
EndIf

Return

/*/{Protheus.doc} getQueryD4B()
    Metodo responsavel por acionar o GetById na classe ModelD4B
    e tambem montar a estrutura a ser apresentada no Response da requisicao
    @type  Metodo
    @author Adriano Vieira
    @since  20/01/2023
    @version 1.0
/*/
Method getQueryD4B(cId as Character) class D4BRepository

    Local oAux := JsonObject():New()    as Object

    oD4BModel := totvs.protheus.backoffice.stock.trackcosts.model.D4B.D4B():new()
    oAux :=  oD4BModel:getById(cId)

Return oAux

/*/{Protheus.doc} deleteQueryD4B()
    Metodo responsavel por acionar o GetById na classe ModelD4B
    e tambem montar a estrutura a ser apresentada no Response da requisicao
    @type  Metodo
    @author Adriano Vieira
    @since  20/01/2023
    @version 1.0
/*/
Method deleteQueryD4B(cId as Character) class D4BRepository

    Local lRet    as Logical

    oD4BModel := totvs.protheus.backoffice.stock.trackcosts.model.D4B.D4B():new()
    lRet :=  oD4BModel:DELETE(cId)

Return lRet

/*/{Protheus.doc} getListD4B()
    Metodo responsavel por acionar o GetById na classe ModelD4B
    e tambem montar a estrutura a ser apresentada no Response da requisicao
    @type  Metodo
    @author Adriano Vieira
    @since  20/01/2023
    @version 1.0
/*/
Method getListD4B(aColumns, cQuery, cWhere, nPage, nPagesize, aFilter, cOrder) Class D4BRepository

Local jsonResponse  as object
Local nX            as numeric


For nX := 1 to Len(aColumns)
    ::AddMapFields(aColumns[nX][1], aColumns[nX][2] , .T.,.T.,{ aColumns[nX][2] , aColumns[nX][3], aColumns[nX][4], aColumns[nX][5]})
Next nX

::setPage(nPage)
::setPageSize(nPagesize)
::setQuery(cQuery)
::SetWhere( cWhere )
::SetOrder( cOrder )
::SetUrlFilter(aFilter)

If ::Execute()
    ::FillGetResponse()
    jsonResponse := ::GetJsonResponse()
EndIf

Return jsonResponse

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type Method
    @author Adriano Vieira
    @since  15/02/2024
    @version 12.1.23.10
/*/
Method version() class D4BRepository
    Local nVersion := 200
Return nVersion
