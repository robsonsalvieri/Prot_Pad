#INCLUDE "MATA241.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#DEFINE LINHAS 999

Static a241ISD3  := NIL
Static __oSQLSN1 As Object
Static __oFilSD3 as Object
Static _o241ATFD1
Static _o241ATFB2
Static _o241ATFN1
Static _o241ATFF5
Static __aEmpSupOP := {}	//Array para controle de produtos com quantidade maior que a empenhada
Static __lTCIWritten := NIL
Static __lCustoRegra

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Mata241  ³ Autor ³ Gilson do Nascimento  ³ Data ³ 02/04/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Movimentacoes Internas (Requisicoes/Devolucoes)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void mata241(void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mata241(xAutoCab,xAutoItens,nCallOpcx)
Local nX           := 0
Local nPos         := 0
Local lEstorno     := nCallOpcx <> Nil .And. nCallOpcx == 6
Local lAutEstIt    := Nil
Local aCores       := A240aCores()
Local bBlock
Local cFiltro
Local cMsg         As Character
Local cSol         As Character

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aAC 	     := {OemToAnsi(STR0002),OemToAnsi(STR0001)}
PRIVATE aCRA	     := {OemToAnsi(STR0001),OemToAnsi(STR0003),OemToAnsi(STR0002)}
PRIVATE lLogMov    := GetMV("MV_IMPMOV")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pega a variavel que identifica se o calculo do custo e' :    ³
//³ O = On-Line ou M = Mensal                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCusMed    := GetMv("MV_CUSMED")
PRIVATE bCols      := {|x,y|aCols[x][y]}
PRIVATE nPosCod    := 0
PRIVATE nPosLocal  := 0
PRIVATE nPosLote   := 0
PRIVATE nPosLotCTL := 0
PRIVATE nPosDValid := 0
PRIVATE nPosQuant  := 0
PRIVATE nPosTES    := 0
PRIVATE nPosCusto1 := 0
PRIVATE nPosCusto2 := 0
PRIVATE nPosCusto3 := 0
PRIVATE nPosCusto4 := 0
PRIVATE nPosCusto5 := 0
PRIVATE nPosUm     := 0
PRIVATE nPosServic := 0
PRIVATE nPosPotenc := 0
PRIVATE nPosConta  := 0
PRIVATE nPosCC     := 0
PRIVATE nPosGrupo  := 0
PRIVATE nPosTipo   := 0
PRIVATE nPosSegUm  := 0
PRIVATE nPosQtSegUm:= 0
PRIVATE nPosDesc   := 0
PRIVATE nPosOp     := 0
PRIVATE nPosTRT    := 0
PRIVATE nPosLocali := 0
PRIVATE nPosNumSer := 0
PRIVATE nPos241Loc := 0
PRIVATE nPos241Qtd := 0
PRIVATE nPosCodVei := 0
PRIVATE nPosViagem := 0
PRIVATE nPosProj   := 0
PRIVATE nPosTarefa := 0
PRIVATE aCtbDia    := {}
PRIVATE l241Auto   := ( xAutoCab <> NIL  .and. IIf(lEstorno,.T.,xAutoItens <> NIL) )
PRIVATE aAutoCab   := {}
PRIVATE aAutoItens := {}
PRIVATE cXFunc     := Nil
PRIVATE lMT241GRV  := ExistBlock("MT241GRV")
PRIVATE aButtons   := {}
PRIVATE aMemos     := {}
Private cAlsSTL241 := GetNextAlias()
Private oTmpMrk

If l241Auto
	If !lEstorno
		aAutoCab := SD3->(MSArrayXDB(xAutoCab))
		For nX := 1 To Len(xAutoItens)
			aAdd(aAutoItens,SD3->(MSArrayXDB(xAutoItens[nX])))
		Next
	Else
		If (nX := ASCan(xAutoCab, {|x| AllTrim(x[1]) == 'AUTOESTORN'})) > 0
			If xAutoCab[nX, 2] == 'DOC' 
				lAutEstIt := .F.
			ElseIf xAutoCab[nX, 2] == 'ITEM'
				lAutEstIt := .T.
			Else
				cMsg := STR0057 //O campo AUTOESTORN aceita somente os valores DOC ou ITEM
				cSol := STR0058 //Informe o conteúdo correto na chamada da rotina automática.
				Help(,, "AUTOESTORN",, cMsg, 1, 0, , , , , , {cSol})
    			Return Nil
			EndIf
		EndIf
	EndIf
EndIf

//Para estorno oriundo de WMS, deverá estornar um SD3 por vez, com base no Recno enviado pelo WMS.
If IntWms() .And. l241Auto .And. lEstorno .And. IsInCallStack('EstMovSD3') .And. FindFunction('WmsRecNoD3')
	Private nWmsRecnoSD3 := WmsRecNoD3(xAutoItens[1])
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o custo medio e' calculado On-Line               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cCusMed == "O"
	PRIVATE nHdlPrv				      // Endereco do arquivo de contra prova dos lanctos cont.
	PRIVATE lCriaHeader := .T.	// Para criar o header do arquivo Contra Prova
	PRIVATE cLoteEst			      // Numero do lote para lancamentos do estoque
	PRIVATE lCtbOnLine  := .T.	// Contabilizacao On Line
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona numero do Lote para Lancamentos do Estoque         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX5")
	dbSeek(xFilial()+"09EST")
	cLoteEst:=IIf(Found(),Trim(X5Descri()),"EST ")
	PRIVATE nTotal := 0 		// Total dos lancamentos contabeis
	PRIVATE cArquivo			  // Nome do arquivo contra prova
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Esta variavel indica se utiliza segunda unidade de medida    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE lUsaSegUm

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Estas variaveis indicam para as funcoes de validacao qual    ³
//³ programa as esta' chamando, no caso igual ao do MATA240      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE l240 :=.F.,l250:=.F.,l241 :=.T.,l242:=.F.,l261 := .F.,l185:=.F.
PRIVATE cCadastro:=STR0004	//"Movimentos Internos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Aadd(aButtons	, {'PRODUTO',{||M241SeleEs()},OemToAnsi(STR0023),OemToAnsi(STR0029)}) //"Explode 1o nivel estrutura"
If IntTMS()		// Integracao TMS
	Aadd(aButtons	, {'CARGA',{||A103RATVEI()}, STR0025, STR0027}) //"Rateio por Veiculo/Viagem - <F11>"
	Aadd(aButtons	, {'CARGASEQ',{||A103FROTA()},STR0026, STR0028})//"Rateio por Frota"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para adicao de campos memo do usuario       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MT241MEM" )
	aMemUser := ExecBlock( "MT241MEM", .F., .F. )
	If ValType( aMemUser ) == "A"
		AEval( aMemUser, { |x| AAdd( aMemos, x ) } )
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Botao para exportar dados para EXCEL                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If RemoteType() == 1
	aAdd(aButtons, {PmsBExcel()[1],{|| DlgToExcel({{"CABECALHO",cCadastro,{STR0009,STR0010,STR0011,STR0012},;
	{cDocumento,cTm,cCC,dA241Data}},{"GETDADOS",OemToAnsi(STR0033),aHeader,aCols}})},PmsBExcel()[2],PmsBExcel()[3]})
EndIf

PRIVATE lCodBarra := .F.
PRIVATE aRotina   := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega perguntas do SX1                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTA240",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se trabalha com segunda unidade de medida           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lUsaSegUm := X3Uso(GetSx3Cache("B1_SEGUM","X3_USADO")) .And. X3Uso(GetSx3Cache("D3_QTSEGUM","X3_USADO"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ativa tecla F12 para acionar perguntas se nao for rotina autom.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !l241Auto
	SetKey(VK_F12,{|| MTA241PERG() })
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para pre-validar os dados a serem exibidos. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("M240BROW")
	ExecBlock("M240BROW",.F.,.F.)
EndIf

If !l241Auto
	If nCallOpcx <> Nil
		dbSelectArea("SD3")
		nPos := Ascan(aRotina,{|x| x[4]== nCallOpcx})
		If ( nPos # 0 )
			bBlock := &( "{ |x,y,z,k| " + aRotina[ nPos,2 ] + "(x,y,z,k) }" )
			Eval( bBlock,Alias(),SD3->(Recno()),nPos)
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada para verificacao de filtros na Mbrowse      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock("M241FILB")
			cFiltro := ExecBlock("M241FILB",.F.,.F.)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Endereca a funcao de BROWSE                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		mBrowse( 6, 1,22,75,"SD3",,,,,,aCores,,,,,,,, IF(Empty(cFiltro), NIL, cFiltro))
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desativa tecla que aciona perguntas                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Set Key VK_F12 To
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chamada da Rotina Automatica                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lEstorno
		dbSelectArea("SD3")
		a241Estorn("SD3",Recno(),4,lAutEstIt)
	Else
		dbSelectArea("SD3")
		a241Inclui("SD3",Recno(),3)
	EndIf
EndIf

/*---------------------------------------------------------------+
| Fecha o alias e exclui a tabela temporária do processo SIGAMNT |
+---------------------------------------------------------------*/
If Sele( cAlsSTL241 ) > 0

	oTmpMrk:Delete()

	FWFreeObj( oTmpMrk )

EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241Visual³ Autor ³ Gilson Nascimento     ³ Data ³ 02/05/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de visualizacao de uma Movimentacao Interna       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void a241Visual(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a241Visual(cAlias,nReg,nOpc)
Local nSavRec
Local oDlg
Local oSize
Local nX       	    := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Walk-Thru                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNoFields	   	:= {"D3_DOC","D3_TM","D3_CC","D3_PERDA","D3_EMISSAO","D3_PARCTOT","D3_NODIA","D3_DIACTB"}
Local bSeekFor		:= {|| dA241Data == SD3->D3_EMISSAO .And. (D3_TM == cTm .Or. (D3_TM <> cTm .And. !Empty(D3_ESTORNO))) .And. If(Empty(D3_DOC), D3_NUMSEQ == cD3NumSeq, .T.)}
Local bSeekWhile
Local cSeek			:= ""
Local aBtnBack    	:= {}

PRIVATE aRatVei   	:= {}
PRIVATE aRatFro   	:= {}
PRIVATE aArraySDG 	:= {}

If Type("aButtons") == "U"
	aButtons  := {}
EndIf

If !GETNEWPAR("MV_DCLNEW",".F.")
	AADD(aNoFields,"D3_OBS")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Portaria CAT83  - Se o parâmetro não estiver ativo, não inclui o campo no acols ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !V240CAT83()
	aAdd(aNoFields,"D3_CODLAN")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aNoFields,"D3_PERIMP")
AADD(aNoFields,"D3_VLRVI")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe algum dado no arquivo                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( cAlias )
dbSetOrder(1)
If RecCount() == 0
	Return .T.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se esta' na filial correta                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If xFilial() != D3_FILIAL
	Help(" ",1,"A000FI")
	Return .T.
EndIf

If SubStr(D3_CF,1,2) == "PR" .Or. Subs(D3_CF,3,1)$"2457"
	Help(" ",1,"A240NAO")
	Return
EndIf

PRIVATE cDocumento := D3_DOC
PRIVATE dA241Data  := D3_EMISSAO
PRIVATE cTM        := D3_TM
PRIVATE cCC        := D3_CC
PRIVATE cD3NumSeq  := D3_NUMSEQ

nSavRec := RecNo()
SB1->(dbSetOrder(1))
dbSelectArea(cAlias)
dbSetOrder(2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cDocumento)
	cSeek      := xFilial("SD3")+cDocumento
	bSeekWhile := {|| D3_FILIAL+D3_DOC }		// Condicao While para montar o aCols
Else
	cSeek      := xFilial("SD3") + cDocumento + D3_COD
	bSeekWhile := {|| D3_FILIAL+D3_DOC+D3_COD }	// Condicao While para montar o aCols
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader e aCols utilizando a funcao FillGetDados       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aHeader[0]
PRIVATE aCols := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry )   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
FillGetDados(nOpc,cAlias,2,cSeek,bSeekWhile,bSeekFor,aNoFields,,,,,,,,{|aColsX| a241AfterC(nOpc,aColsX,cAlias)})
For nx = 1 To Len(aHeader)
	Do Case
		Case Trim(aHeader[nx][2]) == "D3_COD"
			nPosCod:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOCAL"
			nPosLocal:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMLOTE"
			nPosLote:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOTECTL"
			nPosLotCTL:=nX
		Case Trim(aHeader[nx][2]) == "D3_DTVALID"
			nPosDValid:=nX
		Case Trim(aHeader[nx][2]) == "D3_POTENCI"
			nPosPotenc:=nX
		Case Trim(aHeader[nx][2]) == "D3_QUANT"
			nPosQuant:=nX
		Case Trim(aHeader[nx][2]) == "D3_SEGUM"
			nPosSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_QTSEGUM"
			nPosQtSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_TEATF"
			nPosTES:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO1"
			nPosCusto1:=nX
		Case Trim(aHeader[nx][2]) == "D3_UM"
			nPosUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_CONTA"
			nPosConta:=nX
		Case Trim(aHeader[nx][2]) == "D3_GRUPO"
			nPosGrupo:=nX
		Case Trim(aHeader[nx][2]) == "D3_TIPO"
			nPosTipo:=nX
		Case Trim(aHeader[nx][2]) == "D3_OP"
			nPosOp:=nX
		Case Trim(aHeader[nx][2]) == "D3_TRT"
			nPosTrt:=nX
		Case Trim(aHeader[nx][2]) == "D3_DESCRI"
			nPosDesc:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOCALIZ"
			nPosLocali:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMSERI"
			nPosNumSer:=nX
		Case Trim(aHeader[nx][2]) == "D3_SERVIC"
			nPosServic:=nX
	EndCase
Next nx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do uso de alguns campos obrigatorios               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(nPosCod)
	UserException("D3_COD"  +OemToAnsi(STR0021)) //" deve estar em uso!!!"
ElseIf Empty(nPosLocal)
	UserException("D3_LOCAL"+OemToAnsi(STR0021))
ElseIf Empty(nPosQuant)
	UserException("D3_QUANT"+OemToAnsi(STR0021))
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Ponto de Entrada p/ adicao de campos na getdados             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA241CPO")
	ExecBlock("MTA241CPO",.F.,.F.,{nOpc})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso nao ache nenhum item , abandona rotina.         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCols) == 0
	dbSelectArea( cAlias )
	MsGoTo( nSavRec )
	dbSetOrder(1)
	Return .T.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada criado para configurar botoes da enchoicebar        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "A241BUT" )
	aBtnBack := aClone(aButtons)
	aButtons := ExecBlock( "A241BUT", .F., .F., { nOpc, aButtons } )
	If ValType( aButtons ) # "A"
		aButtons := aClone(aBtnBack)
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definição array aObjects                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSize := FwDefSize():New()

oSize:AddObject( "CABECALHO",  100, 8, .T., .T. ) // Totalmente dimensionavel
oSize:AddObject( "GETDADOS" ,  100, 100, .T., .T. ) // Totalmente dimensionavel
oSize:lProp 	:= .T. // Proporcional
oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

oSize:Process() 	   // Dispara os calculos

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definição Dialog                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0004) OF oMainWnd PIXEL FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4]  //"Movimentos Internos"

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Definição Panel que sera usado no posicionamento da tela             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPanel1:= TSay():New(;
			oSize:GetDimension("CABECALHO","LININI"),oSize:GetDimension("CABECALHO","COLINI"),;
			{||''},oDlg,,,,,,.T.,,,;
		oSize:GetDimension("CABECALHO","XSIZE"),oSize:GetDimension("CABECALHO","YSIZE"))
		oPanel2:= tPanel():New(;
			oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
			,oDlg,,,,,,;
			oSize:GetDimension("GETDADOS","XSIZE"),oSize:GetDimension("GETDADOS","YSIZE"),,)


		@ 0.4,00.7 SAY OemToAnsi(STR0009) OF oPanel1 //"N£mero Documento"
		@ 0.3,08.0 MSGET cDocumento When .F. SIZE 65,08 OF oPanel1
		@ 0.4,17.3 SAY OemToAnsi(STR0010) OF oPanel1 //"TM"
		@ 0.3,18.7 MSGET cTm When .F. OF oPanel1
		@ 0.4,22.7 SAY OemToAnsi(STR0011) OF oPanel1 //"Centro de Custo"
		@ 0.3,27.9 MSGET cCC When .F. OF oPanel1
		If len(cCC) <= 9
			@ 0.4,36.5 SAY OemToAnsi(STR0012) OF oPanel1 //"Emissao"
			@ 0.3,39.5 MSGET dA241Data When .F. SIZE 40,08 OF oPanel1
		Else
		 	@ 0.4,43.5 SAY OemToAnsi(STR0012) OF oPanel1 //"Emissao"
			@ 0.3,46.5 MSGET dA241Data When .F.  SIZE 40,08 OF oPanel1
		EndIf



//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de Entrada que disponibiliza o Objeto da Dialog - oDlg  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock( "MT241CAB" ))
	ExecBlock("MT241CAB",.F.,.F.,{@oPanel1,nOpc})
EndIf
	oGet := MSGetDados():New(;
		oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
		oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),;
		nOpc,'AllwaysTrue','A241TudoOk','',;
		.F.,{"D3_QUANT","D3_QTSEGUM"},NIL,NIL,;
		LINHAS,,,,,,,,,oPanel2)

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()},,aButtons)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cAlias)
MsGoTo(nSavRec)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ProcH     ³ Autor ³ Microsiga S.A.        ³ Data ³ 25/07/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de pesquisa para consulta do campo que esta dentro  ³±±
±±³          ³ do array aAutoCab.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void ProcH(ExpC1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Nome do Campo                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ProcH(cCampo)
Return aScan(aAutoCab,{|x|Trim(x[1])== cCampo })

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241Inclui³ Autor ³ Gilson do Nascimento  ³ Data ³ 02/05/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de inclusao de movimentacao interna               ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void a241Inclui(ExpC1,ExpN1,ExpN2,ExpC2,ExpL1)             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±³          ³ ExpC2 = Numero do Documento                                ³±±
±±³          ³ ExpL1 = Indicara' se o No.do Documento foi alterado        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a241Inclui(cAlias,nReg,nOpc,cNewDoc,lChangeDoc)
Local nOpcao     := 3
Local oDlg
Local oPanel1
Local oPanel2

Local dDataFec   := MVUlmes()
Local nX         := 0
Local aCamposVld := {}
Local bBlkVld    := {|x| x[3] := &(x[2])}
Local lRet			 := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva a integridade dos dados                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nSavReg, lVldPCO

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a Existencia do ponto de entrada e seta valor       ³
//³ da variavel que define se edita o documento ou nao           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lDocto       := .T.
Local lPimsInt	   := SuperGetMV("MV_PIMSINT",.F.,.F.)
Local oSize
Local cF3          := If(CtbInUse(), 'CTT', If(SuperGetMV('MV_SIGAGSP', .F., '0')=='1', 'NI3', 'SI3')) //-- MV_SIGAGSP = "0"-Nao integra/ "1"-Integra
Local aADDButtons  := {}
Local aTelaButtons := {}
Local aCpoCab:={}
Local aCpx:={}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Walk-Thru						                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNoFields	 := {}
Local aBtnBack   := {}
Local cDocAux	 := ""
Local nTamDoc	 := FWTamSX3("D3_DOC")[1]

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  := FWHasEAI("MATA241")
Local lWmsNew    := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local lIntWMS    := SuperGetMV('MV_INTWMS',.F.,.F.)
Local lWmsSD3    := Iif(Type('lExecWms')=='L', lExecWms, .F.)
Local lDocSD3    := Iif(Type('lDocWms')=='L', lDocWms, .F.)
Local lMT241CAB  := ExistBlock("MT241CAB")
Local lMT241SD3  := ExistBlock("MT241SD3")
Local lMT241CAN  := ExistBlock("MT241CAN")
Local cPicD3Doc  := PesqPict("SD3","D3_DOC")
Local cPicD3TM   := PesqPict("SD3","D3_TM")
Local cPicD3CC   := PesqPict("SD3","D3_CC")
Local cvalidcc   :=	getvalid("D3_CC")

Local aD3NumSeq	:= {}
Local aRetInt   := {}
Local aArea     := {}
Local aAreaSD3  := {}
Local lMTA241DOC as Logical
Local cDocto	:= ""
Local lcCC      := .F.
Local cNrAbate  := ""

PRIVATE nPosCod    := 0
PRIVATE nPosLocal  := 0
PRIVATE nPosLote   := 0
PRIVATE nPosLotCTL := 0
PRIVATE nPosDValid := 0
PRIVATE nPosQuant  := 0
PRIVATE nPosTES    := 0
PRIVATE nPosCusto1 := 0
PRIVATE nPosUm     := 0
PRIVATE nPosServic := 0
PRIVATE nPosPotenc := 0
PRIVATE nPosConta  := 0
PRIVATE nPosCC     := 0
PRIVATE nPosGrupo  := 0
PRIVATE nPosTipo   := 0
PRIVATE nPosSegUm  := 0
PRIVATE nPosQtSegUm:= 0
PRIVATE nPosDesc   := 0
PRIVATE nPosOp     := 0
PRIVATE nPosTRT    := 0
PRIVATE nPosLocali := 0
PRIVATE nPosNumSer := 0
PRIVATE nPos241Loc := 0
PRIVATE nPos241Qtd := 0
PRIVATE nPosCodVei := 0
PRIVATE nPosViagem := 0
PRIVATE nPosProj   := 0
PRIVATE nPosTarefa := 0
PRIVATE nPosClVl   := 0
PRIVATE nPosItemCT := 0
PRIVATE nPosStatus := 0
PRIVATE nPosNumSa  := 0
PRIVATE nPosItemSa := 0
PRIVATE nPosDoc    := 0

//Variavel de controle do itens os quais terao pergunta ao usuario, para que o mesmo seja perguntado somente uma vez
PRIVATE aResposta  := {}

//-- Variavel utilizada pela integracao com SIGAMNT
Private aMntGarant := {}

If Type("INCLUI") == "L" .And. !INCLUI
	INCLUI := .T.
EndIf

lMTA241DOC := ExistBlock("MTA241DOC")

// -- Protecao quando rotina for executada em modo automatico

If (lPimsInt .And. l241Auto) .Or. FunName() == "MATA185" .Or. IsInCallStack("MATI241") .Or. (l241Auto .And. FunName() == "EICDI154")
	aNoFields := {"D3_DOC","D3_TM","D3_PERDA","D3_EMISSAO","D3_PARCTOT","D3_ESTORNO","D3_NODIA","D3_DIACTB"}
Else
	aNoFields := {"D3_DOC","D3_TM","D3_CC","D3_PERDA","D3_EMISSAO","D3_PARCTOT","D3_ESTORNO","D3_NODIA","D3_DIACTB"}
EndIf


If !GETNEWPAR("MV_DCLNEW",".F.")
	AADD(aNoFields,"D3_OBS")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aNoFields,"D3_PERIMP")
AADD(aNoFields,"D3_VLRVI")

If Type("aCtbDia") == "U"
   Private aCtbDia := {}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona informacoes para validacao de Get Fixo no array     ³
//³ aCamposVld                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aCamposVld,{"D3_CC","cCC",""})
AADD(aCamposVld,{"D3_EMISSAO","dA241Data",""})

If ExistBlock("MTA241DOC")
	lDocto:=ExecBlock("MTA241DOC",.F.,.F.)
	If Valtype(lDocto) # "L"
		lDocto:=.T.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a Existencia da varivael l241Auto                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Type("l241Auto") == "U" )
	Private l241Auto := .F.
EndIf

PRIVATE aLogSld    := {}
PRIVATE aValidGet  := {}

PRIVATE cDocumento := CriaVar("D3_DOC")
PRIVATE dA241Data  := CriaVar("D3_EMISSAO")
PRIVATE cCC        := CriaVar("D3_CC")
PRIVATE cTM        := CriaVar("D3_TM")
PRIVATE nOpca      := 0
PRIVATE nFatConv   := 0
PRIVATE nCOpcao    := 3

PRIVATE aRatVei    := {}
PRIVATE aRatFro    := {}
PRIVATE aArraySDG  := {}
PRIVATE l241Inc    := .T.

Default cNewDoc    := ''
Default lChangeDoc := .F.

If __oFilSD3 == NIL
	__oFilSD3 := JsonObject():New()
EndIf

If __oFilSD3[cFilAnt] == NIL
	__oFilSD3[cFilAnt] := FWxFilial('SD3')
EndIf

If Type("aButtons") == "U"
	aButtons := {}
Endif

aTelaButtons := ACLONE(aButtons)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Portaria CAT83  - Se o parâmetro não estiver ativo, não inclui o campo no acols ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !V240CAT83()
	aAdd(aNoFields,"D3_CODLAN")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o numero com o ultimo + 1                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD3")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificar data do ultimo fechamento em SX6.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataFec >= dDataBase
	Help ( " ", 1, "FECHTO" )
	Return 0
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtra tabela de tipos de movimentacao para aparecerem apenas req/dev ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF5")
If Upper(AllTrim(cXFunc)) == "A185ATU2SD3"
	cCond := 'F5_TIPO == "R"'
Else
	cCond := 'F5_TIPO == "R" .OR. F5_TIPO == "D"'
EndIf
MsFilter(cCond)
dbGoTop()

dbSelectArea(cAlias)
nSavReg     := RecNo()

If l241Auto .And. lDocSD3
	If (nPosDoc := Ascan(aAutoCab,{|x| Alltrim(x[1]) == 'D3_DOC'})) > 0
		cDocumento := Iif(!Empty(aAutoCab[nPosDoc,2]),aAutoCab[nPosDoc,2],cDocumento)
	EndIf
EndIf

// Se não fornecido pela rotina automática, obtém o próximo número sequencial
if empty(cDocumento)
	cDocumento := A240NumDoc("SD3",cDocumento,lWmsNew,lWmsSD3,lChangeDoc)
endif

// Executa ponto de entrada para alterar documento
If ExistBlock("MT241DOCT")
	cDocto := ExecBlock("MT241DOCT",.F.,.F.,{cDocumento})
	If ValType(cDocto) == "C"
		cDocumento := cDocto
	EndIf
EndIf

RestArea(aAreaSD3)
RestArea(aArea)

dbSetOrder(1)
MsGoTo(nSavReg)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader e aCols utilizando a funcao FillGetDados       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aHeader[0]
PRIVATE aCols[0]
PRIVATE Continua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry )   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Upper(AllTrim(cXFunc)) == "A185ATU2SD3"
	FillGetDados(nOpc,cAlias,1,,,,aNoFields,,,,{|aColsX| A185Atu2SD3(aColsX) }/*bMontCols*/,,,,,, {|aHeaderX| a241AfterH(aHeaderX,nOpcao)}/*bAfterHeader*/  )
Else
	FillGetDados(nOpc,cAlias,1,,,,aNoFields,,,,,.T./*lEmpty*/,,, {|aColsX| a241AfterC(nOpcao,aColsX,cAlias)}/*bAfterCols*/,, {|aHeaderX| a241AfterH(aHeaderX,nOpcao)}/*bAfterHeader*/  )
EndIf

For nx := 1 To Len(aHeader)
	Do Case
		Case Trim(aHeader[nx][2]) == "D3_COD"
			nPosCod:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOCAL"
			nPosLocal:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMLOTE"
			nPosLote:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOTECTL"
			nPosLotCTL:=nX
		Case Trim(aHeader[nx][2]) == "D3_DTVALID"
			nPosDValid:=nX
		Case Trim(aHeader[nx][2]) == "D3_POTENCI"
			nPosPotenc:=nX
		Case Trim(aHeader[nx][2]) == "D3_QUANT"
			nPosQuant:=nX
		Case Trim(aHeader[nx][2]) == "D3_SEGUM"
			nPosSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_QTSEGUM"
			nPosQtSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_TEATF"
			nPosTES:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO1"
			nPosCusto1:=nX
		Case Trim(aHeader[nx][2]) == "D3_UM"
			nPosUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_CONTA"
			nPosConta:=nX
		Case Trim(aHeader[nx][2]) == "D3_GRUPO"
			nPosGrupo:=nX
		Case Trim(aHeader[nx][2]) == "D3_TIPO"
			nPosTipo:=nX
		Case Trim(aHeader[nx][2]) == "D3_OP"
			nPosOp:=nX
		Case Trim(aHeader[nx][2]) == "D3_TRT"
			nPosTrt:=nX
		Case Trim(aHeader[nx][2]) == "D3_DESCRI"
			nPosDesc:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOCALIZ"
			nPosLocali:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMSERI"
			nPosNumSer:=nX
		Case Trim(aHeader[nx][2]) == "D3_CODVEI"
			nPosCodVei:=nX
		Case Trim(aHeader[nx][2]) == "D3_VIAGEM"
			nPosViagem:=nX
		Case Trim(aHeader[nx][2]) == "D3_SERVIC"
			nPosServic:=nX
		Case Trim(aHeader[nx][2]) == "D3_PROJPMS"
			nPosProj:=nX
		Case Trim(aHeader[nx][2]) == "D3_TASKPMS"
			nPosTarefa:=nX
		Case Trim(aHeader[nx][2]) == "D3_CLVL"
			nPosClVl:=nX
		Case Trim(aHeader[nx][2]) == "D3_ITEMCTA"
			nPosItemCT:=nX
		Case Trim(aHeader[nx][2]) == "D3_STATUS"
			nPosStatus:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMSA"
			nPosNumSa:=nX
		Case Trim(aHeader[nx][2]) == "D3_ITEMSA"
			nPosItemSa:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO2"
			nPosCusto2:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO3"
			nPosCusto3:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO4"
			nPosCusto4:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO5"
			nPosCusto5:=nX
		Case Trim(aHeader[nx][2]) == "D3_CC"
			nPosCC    :=nX
	EndCase
Next nx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do uso de alguns campos obrigatorios               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(nPosCod)
	UserException("D3_COD"  +OemToAnsi(STR0021))
ElseIf Empty(nPosLocal)
	UserException("D3_LOCAL"+OemToAnsi(STR0021))
ElseIf Empty(nPosQuant)
	UserException("D3_QUANT"+OemToAnsi(STR0021))
ElseIf lIntWMS .And. Empty(nPosServic)
	UserException("D3_SERVIC"+OemToAnsi(STR0060))
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Ponto de Entrada p/ adicao de campos na getdados             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA241CPO")
	ExecBlock("MTA241CPO",.F.,.F.,{nOpc})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Ponto de Entrada p/ validacao do SIGAPCO                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If l185 .And. ExistBlock("MTA241PCO")
	lVldPCO := ExecBlock("MTA241PCO",.F.,.F.,{nOpc})
	If ValType(lVldPCO) == "L"
	 	If !lVldPCO
	 		Return nOpca
	 	EndIf
	EndIf
EndIf

If (l241Auto)
	MsAuto2aCols()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa ponto de entrada para montar array com botoes a      ³
//³ serem apresentados na tela de INCLUSAO                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock( "M241BUT" ) )
	aADDButtons:=ExecBlock("M241BUT",.F.,.F.)
	If ValType(aADDButtons) == "A"
		For nx:=1 to Len(aADDButtons)
			AADD(aTelaButtons,aADDButtons[nx])
		Next nx
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada criado para configurar botoes da enchoicebar        ³
//³ Este PE foi criado porque o PE acima (M241BUT) nao permite manipular ³
//³ botoes da enchoicebar.                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "A241BUT" )
	aBtnBack := aClone(aTelaButtons)
	aTelaButtons := ExecBlock( "A241BUT", .F., .F., { nOpc, aTelaButtons } )
	If ValType( aTelaButtons ) # "A"
		aTelaButtons := aClone(aBtnBack)
	EndIf
EndIf

While .T.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa a gravacao dos lancamentos do SIGAPCO          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	__aEmpSupOP := {}	//Sempre inicializar essa variável static antes da abertura da tela de inclusão
	PcoIniLan("000151")
	If (! l241Auto)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Definição array aObjects                                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize := FwDefSize():New()

		oSize:AddObject( "CABECALHO",  100, 8, .T., .T. ) // Totalmente dimensionavel
		oSize:AddObject( "GETDADOS" ,  100, 92, .T., .T. ) // Totalmente dimensionavel
		oSize:lProp 	:= .T. // Proporcional
		oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

		oSize:Process() 	   // Dispara os calculos

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ativa tecla F4 para Saldos de Lotes e Localizacao            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Set Key VK_F4 TO ShowF4()

		Continua := .F.
		DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0004) OF oMainWnd PIXEL FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4]
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Definição Panel que sera usado no posicionamento da tela             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPanel1:= TSay():New(oSize:GetDimension("CABECALHO","LININI"),oSize:GetDimension("CABECALHO","COLINI"),{||''},oDlg,,,,,,.T.,,,oSize:GetDimension("CABECALHO","XSIZE"),oSize:GetDimension("CABECALHO","YSIZE"))
		oPanel2:= tPanel():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),,oDlg,,,,,,oSize:GetDimension("GETDADOS","XSIZE"),oSize:GetDimension("GETDADOS","YSIZE"),,)

		If l185
			for nX := 1 to len(acols)
				If nX = 1
				 	lcCC := .T.
					cCC := acols[nX,nPoscc]
				EndIf
				If cCC <> acols[nX,nPoscc] 
					lcCC := .F.
				EndIf
			Next nx
		EndIf

		@ 0.4,00.7  SAY OemToAnsi(STR0009) OF oPanel1 	//"N£mero Documento"
		@ 0.3,08.0  MSGET cDocumento	Picture cPicD3Doc Valid CheckSx3("D3_DOC") .And. VldUser('D3_DOC') WHEN iIf(lMTA241DOC ,lDocto,If(GetSx3Cache("D3_DOC","X3_VISUAL") == "V",.F.,&(GetSx3Cache("D3_DOC","X3_WHEN")))) SIZE 65,08 OF oPanel1
		@ 0.4,17.3	SAY OemToAnsi(STR0010) OF oPanel1	//"TM"
		@ 0.3,18.7	MSGET oTm Var cTm F3 "SF5" Picture cPicD3TM Valid A241ChkSX3("D3_TM") .And. VldUser('D3_TM') WHEN If(GetSx3Cache("D3_TM","X3_VISUAL") == "V",.F.,&(GetSx3Cache("D3_TM","X3_WHEN"))) OF oPanel1
		oTm:bLostFocus := { || IIf(!Vazio(cTm) ,{cNrAbate:=A240NGUIA(cTm,.F.)},cNrAbate:="aaa")}
		If !l185 .or. (l185 .and. lcCC)      
			@ 0.4,22.7  SAY OemToAnsi(STR0011) OF oPanel1 	//"Centro de Custo"
			@ 0.3,27.9  MSGET cCC F3 cF3 Picture cPicD3CC Valid &(cvalidcc) .And. VldUser('D3_CC') WHEN If(GetSx3Cache("D3_CC","X3_VISUAL") == "V",.F.,&(GetSx3Cache("D3_CC","X3_WHEN"))) OF oPanel1
		EndIf
		If len(cCC) <= 9
			@ 0.4,36.5 SAY OemToAnsi(STR0012) OF oPanel1 	//"Emissao"
			@ 0.3,39.5 MSGET dA241Data Valid A241Data() .And. VldUser('D3_EMISSAO') WHEN If(GetSx3Cache("D3_EMISSAO","X3_VISUAL") == "V",.F.,&(GetSx3Cache("D3_EMISSAO","X3_WHEN"))) SIZE 40,08 OF oPanel1
		Else
		 	@ 0.4,43.5 SAY OemToAnsi(STR0012) OF oPanel1 //"Emissao"
			@ 0.3,46.5 MSGET dA241Data Valid A241Data() .And. VldUser('D3_EMISSAO') WHEN If(GetSx3Cache("D3_EMISSAO","X3_VISUAL") == "V",.F.,&(GetSx3Cache("D3_EMISSAO","X3_WHEN"))) SIZE 40,08 OF oPanel1
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Ponto de Entrada que disponibiliza o Objeto da Dialog - oDlg  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lMT241CAB
			aCpx:=ExecBlock("MT241CAB",.F.,.F.,{@oPanel1,nOpc})
		EndIf
		oGet := MSGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
		 oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),;
					nOpc,'A241LinOk','A241TudoOk','',;
					If(l185,.F.,.T.),If(l185,aAlter,Nil),NIL,NIL,;
					LINHAS,,,,,,,,,oPanel2)

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| (AEval(aCamposVld,bBlkVld),IIf(oGet:TudoOK() .And. ChkGetFix(aCamposVld,.F.,.T.) .And. ChkOpSusp(),(oDlg:End(),nOpca:=1),nOpca := 0))},{||oDlg:End()},,aTelaButtons)
	Else
		// Criacao do Array aValidGet
		aValidGet := {}
		aAdd(aValidGet,{"cDocumento"   ,aAutoCab[ProcH("D3_DOC"    ),2],"CheckSx3('D3_DOC').And. VldUser('D3_DOC')",.T.})
		aAdd(aValidGet,{"cTM"          ,aAutoCab[ProcH("D3_TM"     ),2],"CheckSx3('D3_TM') .And. VldUser('D3_TM')" ,.F.})
		aAdd(aValidGet,{"cCC"          ,aAutoCab[ProcH("D3_CC"     ),2],"CheckSx3('D3_CC') .And. VldUser('D3_CC')" ,.F.})
		aAdd(aValidGet,{"dA241Data"    ,aAutoCab[ProcH("D3_EMISSAO"),2],"A241Data()"                               ,.T.})
		If ! SD3->(MsVldGAuto(aValidGet)) // consiste os gets
			Return .F.
		EndIf
		If ! SD3->(MsVldAcAuto(aValidGet,"A241LinOk(o)","A241TudoOk(o)"))   // consiste o campos do Acols
			Return .F.
		EndIf
		nOpcA := 1
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Apaga lancamentos de validacao nao utilizados gerados no SIGAPCO ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpcA <> 1
		PcoFreeBlq("000151",.T.)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Recupera o Conteudo do Ponto de Entrada MT241CAB para utilizacao |
	//³ posterior no Ponto de Entrada MT241GRV           			     |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCpoCab:={}
	If lMT241CAB
		If VALTYPE(aCpx)='A'
 		    aCpoCab:=Aclone(aCpx)
		EndIf
	EndIf

	If nOpcA == 1

			If lChangeDoc .AND. !SD3->(DbSeek(xFilial("SD3") + cDocumento ) )
				lChangeDoc := .F.
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se durante a digitacao não foi incluido um documento³
			//³ com o mesmo numero por outro usuario.                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			if l241Auto
				
				if len(FWGETSX5('00','D3DOCP')) > 0
					cDocAux := Padr(alltrim(cDocumento),nTamDoc,' ')
					FWPUTSX5('','00','D3DOCP',cDocAux,cDocAux,cDocAux,cDocAux)
				else
					if cDocumento == Replicate("Z", nTamDoc)
						FWPUTSX5('','00','D3DOCP',Replicate("0", nTamDoc-1)+'1')
					endif
				endif
			
			else
				
				dbSelectArea(cAlias)
				dbSetOrder(2)
				cDocumento 	:= A240NumDoc(cAlias,cDocumento,lWmsNew,lWmsSD3,lChangeDoc)
				cDocumento 	:= A261RetINV(cDocumento)

			endif
			
			Begin Transaction
			
			aD3NumSeq := a241Grava(cAlias,nOpcao,@lChangeDoc,aCpoCab)
			
			If !( aD3NumSeq[ 1 ] )
				lRet := .F.
				nOpca:= 0
				disarmTransaction()
			Else
				// Processa Gatilhos
				EvalTrigger()
				If __lSX8
					ConfirmSX8()
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se a funcao estiver sendo executada via MATA185, esta funcao         ³
				//³atualizara as demais tabelas dentro da mesma transacao (SCQ/SCP/SB2) ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If l185
					If !A185AtuSCQ(SD3->D3_DOC, aD3NumSeq[ 2 ] )
						nOpca:= 0
						lRet := .F.
						disarmTransaction()
					EndIf
				EndIf

				If lIntegDef
					SD3->(DbSetOrder(2))
					If	SD3->(DbSeek(xFilial("SD3")+cDocumento))
						aRetInt := FwIntegDef( "MATA241",,,,"MATA241" )
						If Valtype(aRetInt) == "A"
							If Len(aRetInt) == 2
								If !aRetInt[ 01 ]
									disarmTransaction()
									Help( Nil, Nil, STR0055, Nil, aRetInt[ 02 ], 1, 0, Nil, Nil, Nil, Nil, Nil, { STR0056 } )
									nOpcA:= 0
								Endif
							Endif
						EndIf
					EndIf
				EndIf
			EndIf
			End Transaction

			//Tratamento Realizado para Estornar Mov. Interno Gerado via A241Inclui com Adapter Config como Sincrono
			If lIntegDef .And. ( Valtype(aRetInt) == "A" .And. Len(aRetInt) == 2 .And. !aRetInt[ 01 ] )
				If SD3->(DbSeek(xFilial("SD3") + cDocumento ) )
					l241Auto := .T.
					A241Estorn( 'SD3', SD3->( RecNo() ), 6 )
					l241Auto := .F.
				EndIf
			EndIf

			If lRet
				If lChangeDoc  //-- No. do Documento foi alterado
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Se a funcao estiver sendo executada via MATA185, esta mensagem ³
					//³sera exibida no MATA185 (fora da Transacao)                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !l185 .And. !l241Auto
						Help("",1,"A240DOC",,cDocumento,4,30)
					EndIf
					cNewDoc := cDocumento
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Finaliza a gravacao dos lancamentos do SIGAPCO            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				PcoFinLan("000151")

				If lMT241SD3
					ExecBlock("MT241SD3",.F.,.F.)
				End

				//Somente executa as ordens de serviço do WMS, após a transação
				//Pois a execução dos serviços no WMS tem transação própria
				If IntWMS() .And. !lWmsNew .Or. (lWmsNew .And. !lWmsSD3)
					WmsExeServ()
				EndIf

				If lLogMov .And. Len(aLogSld) > 0
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Imprimir Relatorio de Movimentos nao realizados por falta de saldo ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RelLogMov(aLogSld)
				EndIf
		  EndIf
	ElseIf __lSX8
		RollBackSX8()
	EndIf

	If nOpca == 0 .And. lMT241CAN
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada para tratamento especifico do usuario ³
		//³ no momento do Cancelamento                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		ExecBlock('MT241CAN', .F., .F.)
	EndIf

	Exit
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desativa tecla F4                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Set Key VK_F4 to

//Limpando array static para evitar existir conteúdo em execuções via execauto.
aSize(__aEmpSupOP,0)
__aEmpSupOP := {}

dbSelectArea("SF5")
dbClearFilter()
dbGoTop()
SetCursor(0)
dbSelectArea(cAlias)
FwFreeObj(oDlg)
oDlg:=Nil
FwFreeObj(oPanel1)
oPanel1:=Nil
FwFreeObj(oPanel2)
oPanel2:=Nil
FwFreeObj(oSize)
oSize:=Nil
__oFilSD3	:= Nil

Return nOpca

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241Estorn³ Autor ³ Gilson do Nascimento  ³ Data ³ 02/05/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de estorno de movimentacao interna                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void a241Estorn(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a241Estorn(cAlias,nReg,nOpc,lAutEstIt)
Local nSavRec
Local nCnt        := 0
Local dDataFec    := MVUlmes()
Local oDlg
Local aAlter      := {}
Local lAbandona   := .F.
Local nPosEstr    := 0
Local zi          := 0
Local cNumSeq     := ""
Local aAreaSD3    := {}
Local lMt241Est   := ExistBlock("MT241EST")
Local oSize
Local lRet        := .T.
Local lInventario := .F.
Local nX          := 0
Local nY          := 0
Local aLockSB2    := {}
Local aLockSB3    := {}
Local aLockSD3    := {}
Local aArea2      := {}
Local cD3_LOCALIZ := ""
Local lWmsSD3      := Iif(Type( 'lExecWms' )== 'L' , lExecWms, .F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Walk-Thru                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aNoFields	  := {"D3_DOC","D3_TM","D3_CC","D3_PERDA","D3_EMISSAO","D3_PARCTOT","D3_NODIA","D3_DIACTB"}
Local bSeekFor	  := NIL
Local bSeekWhile
Local cSeek	   	  := ""
Local cMvPar      := "mv_par04"
Local xMvParBkp
Local nPosRec     := 0
Local aBtnBack    := {}
Local lIntegDef   :=  FWHasEAI("MATA241")
Local aRetInt	  := {}
Local lComp       := .F.
Local lTransit	  := .F.
local cAliasQry   := ""
Local cQuery      := ""
local oSD3Brow    := NIL
Local aCposSD3    := {}	
Local aCposMemo	  := {}

PRIVATE aRatVei   := {}
PRIVATE aRatFro   := {}
PRIVATE aArraySDG := {}
PRIVATE lEstornaIt:= .F.

// -- Protecao quando rotina for executada em modo automatico
If Type("aCtbDia") == "U"
   Private aCtbDia := {}
EndIf

If Type( "l241Inc" ) <> "L"
	l241Inc := .F.
EndIf

If !GETNEWPAR("MV_DCLNEW",".F.")
	AADD(aNoFields,"D3_OBS")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percentual FCI   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aNoFields,"D3_PERIMP")
AADD(aNoFields,"D3_VLRVI")

aCposSD3 := FWSX3Util():GetAllFields('SD3',.T.)
For NX:= 1 to Len(aCposSD3)
	aCposMemo := FWSX3Util():GetFieldStruct( aCposSD3[NX] )
	If aCposMemo[2] == 'M'
		AADD(aNoFields,aCposSD3[NX])
	EndIf
Next NX

If Type("aButtons") == "U"
	aButtons := {}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Campos que podem ser alterados na GetDados                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAlter := {"D3_ESTORNO"}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe algum dado no arquivo                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( cAlias )
dbSetOrder(1)
If RecCount() == 0
	Return lRet
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se esta' na filial correta                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If xFilial() != SD3->D3_FILIAL
	Help(" ",1,"A000FI")
	Return lRet
EndIf

If Subs(SD3->D3_CF,1,2) == "PR" .Or. Subs(SD3->D3_CF,3,1)$"12457"  
	Help(" ",1,"A240NAO")
	lRet := .F.
	Return lRet
EndIf

If Subs(SD3->D3_CF,3,1)$"6" 
	SD7->(Dbsetorder(3))
	SD7->(dbSeek(xFilial("SD7")+SD3->(D3_COD+D3_NUMSEQ+D3_DOC)))
	If SD7->(!EOF()) .and. SD7->(D7_FILIAL+D7_PRODUTO+D7_NUMSEQ+D7_NUMERO) == AllTrim(xFilial("SD7")+SD3->(D3_COD+D3_NUMSEQ+D3_DOC))
		Help(" ",1,"A240NAO")
		lRet := .F.
		Return lRet	
	EndIf
EndIf

If SD3->D3_ESTORNO == "S"
	Help(" ",1,"A240ESTORN")
	lRet := .F.
	Return lRet
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Movimento foi gerado pelo SIGAEIC                          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !l241Auto .And. cPaisLoc == "BRA"
	If !Empty(SD3->D3_HAWB)
		Help(" ",1,"DI161HAWB")
		lRet := .F.
		Return lRet
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Movimento foi gerado pelo MATA185                          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. !l185 .And. !lWMSSD3 .And. AvalMovSA(SD3->D3_COD,SD3->D3_NUMSEQ)
	Help(" ",1,"A185ESTOR")
	lRet := .F.
	Return lRet
EndIf

// Verifica se o Movimento foi gerado pelo MATA685
If lRet .And. FindFunction("AvalMov685") .And. AvalMov685(SD3->D3_OP,SD3->D3_NUMSEQ)
	Help(NIL, NIL, "A685ESTORPERD", NIL, STR0061, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0062}) //"Não é permitido realizar estorno de apontamentos de perda realizados pela rotina de apontamento de perda (MATA685)."  //"Acesse a rotina de Apontamento de perda ( MATA685) para realizar o estorno deste movimento."
	lRet := .F.
	Return lRet
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Movimento foi gerado pelo MATA907                          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If AvalMovSelo(SD3->D3_COD,SD3->D3_TM)
	Help(" ",1,"SELOFISCA")
	lRet := .F.
	Return lRet
EndIf

// Pergunta cadastrada a partir do release 12.1.37 (Quanto ao Estorno: 1=Por Documento; 2=Por Item)
If cPaisLoc == "ANG" .Or. cPaisLoc == "EQU" .Or. cPaisLoc == "HAI"
	cMvPar := "mv_par05"
EndIf
xMvParBkp := &cMvPar
&cMvPar := ""
Pergunte("MTA240",.F.)
If !Empty(&cMvPar)
	lEstornaIt := (&cMvPar == 2)
Else
	&cMvPar := xMvParBkp
EndIf

If l241Auto
	If lAutEstIt != Nil //Informou AUTOESTORN
		lEstornaIt := lAutEstIt
	EndIf
EndIf

If IsInCallStack('MATA140') // Se o estorno vem da classificação da nota assume que o estorno será por item
	lEstornaIt := .T.
	If IsInCallStack('A103TRFSLD')
		lTransit := .T.
	EndIf
EndIf

dbSelectArea( cAlias )
dbSetOrder(1)

//Se o estorno for oriundo do WMS, estorna um item por vez.
If Type('nWmsRecnoSD3') <> 'U' .And. nWmsRecnoSD3 > 0
	dbGoTo(nWmsRecnoSD3)
	lEstornaIt := .T.
EndIf

If !Empty(SD3->D3_DOC)
	cNumSeq:=SD3->D3_NUMSEQ
	aAreaSD3:=GetArea()
	dbSetOrder(4)
	dbSeek(xFilial('SD3')+cNumSeq)
	While !Eof() .And. xFilial('SD3')+cNumSeq == SD3->(D3_FILIAL+D3_NUMSEQ)
		If !lEstornaIt .Or. (lEstornaIt .And. Recno() == nReg)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ AvalMovDiv - Funcao utilizada para avaliar possiveis divergencias de     |
			//|              saldo no estorno do movimento selecionado.                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AvalMovDiv(D3_COD,D3_LOCAL,D3_LOTECTL,D3_NUMLOTE,D3_NUMSEQ,,SD3->D3_TM)
				RestArea(aAreaSD3)
				lRet := .F.
				Return lRet
			EndIf
		EndIf
		dbSkip()
	End
	RestArea(aAreaSD3)
Else
	If AvalMovDiv(D3_COD,D3_LOCAL,D3_LOTECTL,D3_NUMLOTE,D3_NUMSEQ,,SD3->D3_TM)
		lRet := .F.
		Return lRet
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a OP ja' foi encerrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC2")
dbSeek(xFilial("SC2")+SD3->D3_OP)
If !Empty(SC2->C2_DATRF)
	Help(" ",1,"MA240OPENC")
	dbSelectArea("SD3")
	lRet := .F.
	Return lRet
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificar data do ultimo fechamento em SX6.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If dDataFec >= SD3->D3_EMISSAO
	Help ( " ", 1, "FECHTO" )
	lRet := .F.
	Return lRet
EndIf

//------------------------------------------------------
// Caso haja EPI entregue a algum funcionario, não
// poderá realizar o estorno (SIGAMDT)
// MV_NGMDTES deverá estar habilitado
//------------------------------------------------------
If lRet .And. FindFunction('MDTValEst') .And. !MDTValEst( SD3->D3_NUMSEQ , SD3->D3_FILIAL , 1 )
	lRet := .F.
	Return lRet
Endif

//--------------------------------------------------------------
// Valida estorno de movimentação - SIGAMNT
//--------------------------------------------------------------
If lRet .And. FindFunction( 'MNTVLDEST' ) .And. !MNTVLDEST('MATA241')
	Return .F.
EndIf

dbSelectArea(cAlias)

PRIVATE cDocumento := SD3->D3_DOC
PRIVATE dA241Data  := SD3->D3_EMISSAO
PRIVATE cCC        := SD3->D3_CC
PRIVATE cTM        := SD3->D3_TM
PRIVATE cProd	   := SD3->D3_COD
PRIVATE	nOpcA

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o usuario tem permissao de delecao. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cDocumento)
	aArea2 := GetArea()
	SD3->(DbSetorder(2))
	If !lEstornaIt
		If SDW->(dbseek(xFilial("SDW"))) // se não tem regra na SDW não varre as permissões
			SD3->(dbSeek(xFilial("SD3")+cDocumento))
			While !SD3->(Eof()) .And. !lAbandona .And. SD3->D3_FILIAL+SD3->D3_DOC == Xfilial("SD3")+cDocumento
				If SD3->D3_EMISSAO == dA241Data
					lAbandona := ! MaAvalPerm(1,{SD3->D3_COD,"MTA240",5})
				EndIf
				SD3->(dbSkip())
			End
		EndIf
	Else
		If SD3->(Recno()) == nReg
			lAbandona := ! MaAvalPerm(1,{SD3->D3_COD,"MTA240",5})
		EndIf
	EndIf
	RestArea(aArea2)
Else
	lAbandona := !MaAvalPerm(1,{SD3->D3_COD,"MTA240",5})
EndIf
If lAbandona
	Help(,,1,'SEMPERM')
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica calendário contábil                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	lRet := (CtbValiDt(Nil,SD3->D3_EMISSAO,.T.,Nil ,Nil ,{"EST001"}))
EndIf

If !lRet
	Return lRet
EndIf

nSavRec := RecNo()
dbSetOrder(2)
If !Empty(cDocumento)
	If !lEstornaIt
		dbSeek( xFilial()+cDocumento )
	Else
		dbSeek( xFilial()+cDocumento+cProd )
	EndIf
EndIf

SB1->(dbSetOrder(1))
dbSelectArea(cAlias)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lTransit 
	bSeekFor := {|| D3_ESTORNO <> "S" .And. dA241Data == SD3->D3_EMISSAO .And. (D3_TM == cTm .Or. (D3_TM <> cTm .And. !Empty(D3_ESTORNO))) .And. If(lTransit,.T.,If(lEstornaIt .Or. Empty(D3_DOC), Recno() == nReg, .T.))}
	cQuery := ""

	cSeek      := xFilial("SD3")+cDocumento+cProd
	bSeekWhile := {|| D3_FILIAL+D3_DOC+D3_COD }		// Condicao While para montar o aCols

Else
	bSeekFor := NIL

	if (lEstornaIt .OR. empty(cDocumento))

		cQuery := "SELECT * FROM " +RetSQLName("SD3")+ " SD3TMP "
		cQuery += "WHERE "
		cQuery += " R_E_C_N_O_ = ? "

		cSeek      := xFilial("SD3")+cDocumento+cProd
		bSeekWhile := {|| D3_FILIAL+D3_DOC+D3_COD }		// Condicao While para montar o aCols
	Else
		cQuery := "SELECT * FROM " +RetSQLName("SD3")+ " SD3TMP "
		cQuery += "WHERE D3_FILIAL = ? AND "
		cQuery += " D3_DOC = ? AND "
		cQuery += " (D3_TM = ? OR ( D3_TM <> ? AND D3_ESTORNO = ? )) AND " 
		cQuery += " D3_EMISSAO = ?  AND "
		cQuery += " D3_ESTORNO  = ? AND"
		cQuery += " D_E_L_E_T_ = ? "
		cSeek      := xFilial("SD3")+cDocumento
		bSeekWhile := {|| D3_FILIAL+D3_DOC }		// Condicao While para montar o aCols

	EndIf

	cQuery := changeQuery(cQuery)
	oSD3Brow := FwExecStatement():New(cQuery)
	If !lTransit .AND. (lEstornaIt .OR. empty(cDocumento))
		oSD3Brow:SetNumeric(1, nReg)
	else
		nx :=1
		oSD3Brow:SetString(nX++, xFilial("SD3"))
		oSD3Brow:SetString(nX++, cDocumento)
		oSD3Brow:SetString(nX++, cTm)
		oSD3Brow:SetString(nX++, cTm)
		oSD3Brow:SetString(nX++, ' ')
		oSD3Brow:SetString(nX++, dtos(dA241Data) )
		oSD3Brow:SetString(nX++, ' ')
		oSD3Brow:SetString(nX++, ' ')
	EndIf

	cQuery := oSD3Brow:GetFixQuery()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader e aCols utilizando a funcao FillGetDados       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aHeader[0]
PRIVATE aCols[0]
PRIVATE Continua

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry )   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
FillGetDados(nOpc,cAlias,2,cSeek,bSeekWhile,bSeekFor,aNoFields,,,cQuery,,,,,{|aColsX| a241AfterC(nOpc,aColsX,cAlias)},,cAlias)

For nx = 1 To Len(aHeader)
	Do Case
		Case Trim(aHeader[nx][2]) == "D3_COD"
			nPosCod:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOCAL"
			nPosLocal:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMLOTE"
			nPosLote:=nX
		Case Trim(aHeader[nx][2]) == "D3_LOTECTL"
			nPosLotCTL:=nX
		Case Trim(aHeader[nx][2]) == "D3_DTVALID"
			nPosDValid:=nX
		Case Trim(aHeader[nx][2]) == "D3_POTENCI"
			nPosPotenc:=nX
		Case Trim(aHeader[nx][2]) == "D3_QUANT"
			nPosQuant:=nX
		Case Trim(aHeader[nx][2]) == "D3_SEGUM"
			nPosSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_QTSEGUM"
			nPosQtSegUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_TEATF"
			nPosTES:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO1"
			nPosCusto1:=nX
		Case Trim(aHeader[nx][2]) == "D3_UM"
			nPosUm:=nX
		Case Trim(aHeader[nx][2]) == "D3_CONTA"
			nPosConta:=nX
		Case Trim(aHeader[nx][2]) == "D3_GRUPO"
			nPosGrupo:=nX
		Case Trim(aHeader[nx][2]) == "D3_TIPO"
			nPosTipo:=nX
		Case Trim(aHeader[nx][2]) == "D3_OP"
			nPosOp:=nX
		Case Trim(aHeader[nx][2]) == "D3_TRT"
			nPosTrt:=nX
		Case Trim(aHeader[nx][2]) == "D3_DESCRI"
			nPosDesc:=nX
		Case Trim(aHeader[nx][2]) == "D3_ESTORNO"
			nPosEstr:=NX
		Case Trim(aHeader[nx][2]) == "D3_LOCALIZ"
			nPosLocali:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMSERI"
			nPosNumSer:=nX
		Case Trim(aHeader[nx][2]) == "D3_CODVEI"
			nPosCodVei:=nX
		Case Trim(aHeader[nx][2]) == "D3_VIAGEM"
			nPosViagem:=nX
		Case Trim(aHeader[nx][2]) == "D3_SERVIC"
			nPosServic:=nX
		Case Trim(aHeader[nx][2]) == "D3_PROJPMS"
			nPosProj:=nX
		Case Trim(aHeader[nx][2]) == "D3_TASKPMS"
			nPosTarefa:=nX
		Case Trim(aHeader[nx][2]) == "D3_CLVL"
			nPosClVl:=nX
		Case Trim(aHeader[nx][2]) == "D3_ITEMCTA"
			nPosItemCT:=nX
		Case Trim(aHeader[nx][2]) == "D3_STATUS"
			nPosStatus:=nX
		Case Trim(aHeader[nx][2]) == "D3_NUMSA"
			nPosNumSa:=nX
		Case Trim(aHeader[nx][2]) == "D3_ITEMSA"
			nPosItemSa:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO2"
			nPosCusto2:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO3"
			nPosCusto3:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO4"
			nPosCusto4:=nX
		Case Trim(aHeader[nx][2]) == "D3_CUSTO5"
			nPosCusto5:=nX
	EndCase
Next nx

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do uso de alguns campos obrigatorios               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(nPosCod)
	UserException("D3_COD"  +OemToAnsi(STR0021))
ElseIf Empty(nPosLocal)
	UserException("D3_LOCAL"+OemToAnsi(STR0021))
ElseIf Empty(nPosQuant)
	UserException("D3_QUANT"+OemToAnsi(STR0021))
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa Ponto de Entrada p/ adicao de campos na getdados             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA241CPO")
	ExecBlock("MTA241CPO",.F.,.F.,{nOpc})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso nao ache nenhum item , abandona rotina.         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCols) == 0
	MsGoTo( nSavRec )
	dbSetOrder(1)
	lRet := .F.
	Return lRet
EndIf

cD3_LOCALIZ := CriaVar("D3_LOCALIZ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se algum produto est  sendo inventariado.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For zi:=1 to Len(aCols)
	If nPosLocali >0
		cD3_LOCALIZ := aCols[zi,nPosLocali]
	EndIf
	If BlqInvent(aCols[zi,nPosCod],aCols[zi,nPosLocal],,cD3_LOCALIZ)
		Help(" ",1,"BLQINVENT",,aCols[zi,nPosCod]+OemToAnsi(STR0017)+aCols[zi,nPosLocal],1,11)
		lAbandona:=.T.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Analisa se o tipo do armazem permite a movimentacao |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAbandona .And. AvalBlqLoc(aCols[zi,nPosCod],aCols[zi,nPosLocal],Nil,,,,,,,aCols[zi,nPosOp])
		lAbandona:=.T.
	EndIf
Next i

If lAbandona
	lRet := .F.
	Return lRet
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integracao com o Modulo de Transporte (TMS)                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IntTms()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se nao ha movimento de custo de transporte baixado. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !A240TmsVld()
		lRet := .F.
		Return lRet
	EndIf
EndIf

nOpca:=0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada criado para configurar botoes da enchoicebar        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "A241BUT" )
	aBtnBack := aClone(aButtons)
	aButtons := ExecBlock( "A241BUT", .F., .F., { nOpc, aButtons } )
	If ValType( aButtons ) # "A"
		aButtons := aClone(aBtnBack)
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa a gravacao dos lancamentos do SIGAPCO ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PcoIniLan("000151")
If !l241Auto

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Definição array aObjects                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSize := FwDefSize():New()

	oSize:AddObject( "CABECALHO",  100, 8, .T., .T. ) // Totalmente dimensionavel
	oSize:AddObject( "GETDADOS" ,  100, 92, .T., .T. ) // Totalmente dimensionavel
	oSize:lProp 	:= .T. // Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

	oSize:Process() 	   // Dispara os calculos

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Definição Dialog                                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0004) OF oMainWnd PIXEL FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4]

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Definição Panel que sera usado no posicionamento da tela             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanel1:= TSay():New(oSize:GetDimension("CABECALHO","LININI"),oSize:GetDimension("CABECALHO","COLINI"),{||''},oDlg,,,,,,.T.,,,oSize:GetDimension("CABECALHO","XSIZE"),oSize:GetDimension("CABECALHO","YSIZE"))
	oPanel2:= tPanel():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),,oDlg,,,,,,oSize:GetDimension("GETDADOS","XSIZE"),oSize:GetDimension("GETDADOS","YSIZE"),,)

	@ 0.6,00.7 SAY OemToAnsi(STR0009) of oPanel1 //"N£mero Documento"
	@ 0.5,08.0 MSGET cDocumento When .F. SIZE 65,08 OF oPanel1
	@ 0.6,17.3 SAY OemToAnsi(STR0010) of oPanel1 //"TM"
	@ 0.5,18.7 MSGET cTm When .F. of oPanel1
	@ 0.6,22.7 SAY OemToAnsi(STR0011) of oPanel1 //"Centro de Custo"
	@ 0.5,27.9 MSGET cCC When .F. of oPanel1
	If len(cCC) <= 9
		@ 0.6,36.4 SAY OemToAnsi(STR0012) of oPanel1 //"Emissao"
		@ 0.5,39.4 MSGET dA241Data When .F. SIZE 40,08 of oPanel1
    Else
     	@ 0.6,43.4 SAY OemToAnsi(STR0012) OF oPanel1 //"Emissao"
   		@ 0.5,46.4 MSGET dA241Data When .F. SIZE 40,08 OF oPanel1
    EndiF

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de Entrada que disponibiliza o Objeto da Dialog - oDlg  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (ExistBlock( "MT241CAB" ))
		ExecBlock("MT241CAB",.F.,.F.,{@oPanel1,nOpc})
	EndIf

	oGet := MSGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
	oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),;
	4,"AllwaysTrue","AllwaysTrue","",;
	.F.,aAlter,NIL,NIL,;
	LINHAS,,,,,,,,,oPanel2)

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=2,If(MTA241ESTOk(lIntegDef),oDlg:End(),nOpca:=0)},{||oDlg:End(),nOpca:=0},,aButtons)
Else
	If MTA241EstOk(lIntegDef)
		nOpcA := 2
		lRet  := .T.
	EndIf
EndIf

// Posicao da coluna No.de registro
nPosRec := GDFieldPos("D3_REC_WT")

If nOpcA == 2
	Begin Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If MultLock("SD3",aLockSD3,3) .And. MultLock("SB2",aLockSB2,1) .And. MultLock("SB3",aLockSB3,1)
			nCnt := 0
			dbSelectArea(cAlias)
			For nX = 1 to Len(aCols)
				If nPosEstr > 0
					If aCols[nx,nPosEstr] == "S"
						MsGoTo(aCols[nX,nPosRec])
						If A240EstrOk(SD3->D3_COD,SD3->D3_LOCAL,SD3->D3_LOTECTL,SD3->D3_NUMLOTE,SD3->D3_NUMSEQ,SD3->D3_DOC,SD3->D3_QUANT,NIL,@lInventario,l241Auto)
							//Estorna Ativo
							If SF5->(MsSeek(xFilial("SF5")+SD3->D3_TM))
								If SF5->(FieldPos("F5_GERAATF"))> 0 .And. SF4->(MsSeek(xFilial("SF4")+SF5->F5_TEATF))
									If SF5->F5_GERAATF == "1"
										lComp := IIf(SF4->(FieldPos('F4_COMPONE')) > 0, SF4->F4_COMPONE == "1", .F.)
										lRet  := A241AvalATF(SF4->F4_BENSATF == "1",lComp,5)
									EndIf
								EndIf
							EndIf
							If lRet
								RecLock(cAlias,.F.)
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Envia p/func. de atualizacoes (SD3,SB2,SB3,SC2,..) ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								a240DesAtu(lInventario)
								dbSelectArea(cAlias)
							Else
								nOpcA := 0
								If InTransact()
									DisarmTransaction()
									Break
								EndIf
								Exit
							EndIf
						EndIf
					EndIf
				Else
					Help("",1,"D3_ESTORNO")
					disarmTransaction()
					lRet := .F.
					nOpcA:= 0
					Exit
				EndIf
			Next nx

			//Chamada da integração
			If	lIntegDef .And. !( l241Inc )
				SD3->(DbSetOrder(2))
				If	SD3->(MsSeek(xFilial("SD3")+SD3->D3_DOC))
					aRetInt := FwIntegDef( "MATA241",,,,"MATA241" )
					If Valtype(aRetInt) == "A"
						If Len(aRetInt) == 2
							If !aRetInt[ 01 ]
								Help( Nil, Nil, STR0055, Nil, aRetInt[ 02 ], 1, 0, Nil, Nil, Nil, Nil, Nil, { STR0056 } )
								disarmTransaction()
								lRet := .F.
								nOpcA:= 0
							Endif
						Endif
					EndIf
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada ap¢s a grava‡„o do estorno        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMt241Est
				Execblock("MT241EST",.F.,.F.)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o custo medio e' calculado On-Line               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cCusMed == "O" .And. nTotal > 0
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Inicializa perguntas deste programa                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ mv_par01 - Se mostra e permite digitar lancamentos contabeis   ³
				//³ mv_par02 - Se deve aglutinar os lancamentos contabeis          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				Pergunte("MTA240",.F.)
				lDigita   := IIf(mv_par01 == 1,.T.,.F.)
				lAglutina := IIf(mv_par02 == 1,.T.,.F.)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se ele criou o arquivo de prova ele deve gravar o rodape'    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RodaProva(nHdlPrv,nTotal)

				If !Empty(aCtbDia)
					cCodDiario := CtbaVerdia()
					For nX := 1 to Len(aCtbDia)
						aCtbDia[nX][3] := cCodDiario
					Next nX
				EndIf
				If cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,ACtbDia)
					For nY := 1 to Len(aCols)
						SD3->(MsGoTo(aCols[nY,nPosRec]))
						RecLock("SD3",.F.)
						Replace SD3->D3_DTLANC With dDataBase
						MsUnLock()
					Next nY
				EndIf
				lCriaHeader := .T.
				nTotal      := 0 // Total dos lancamentos contabeis
			EndIf
		Else
			ConOut("WARNING: DEADLOCK CONTROL IS ON")
			disarmTransaction()
			lRet := .F.
			nOpcA:= 0
		EndIf



	End Transaction
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza a gravacao dos lancamentos do SIGAPCO            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoFinLan("000151")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da janela                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cAlias)
MsGoto(nReg)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³                                                                       ³±±
±±³                                                                       ³±±
±±³                   ROTINAS DE CRITICA DE CAMPOS                        ³±±
±±³                                                                       ³±±
±±³                                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241TudOk ³ Autor ³ Eveli Morasco         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a nota toda esta' Ok                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a241TudoOk(o)
Static lExistMT241OK := NIL
Local  lRetorno      := .T.
Local  zi            := 0
Local  nLinha        := n

If Empty(cTM)
	Help(" ",1,"D3_TM")
	lRetorno := .F.
Else
	__aEmpSupOP := {}

	If ! A241Check()
		lRetorno := .F.
	Else
		For zi:=1 to Len(aCols)
			If !A241LinOk(nil,zi,.T.)
				lRetorno:=.F.
				Exit
			EndIf
		Next zi
		n := nlinha
		If lRetorno
			If lExistMT241OK == NIL
				lExistMT241OK:=ExistBlock("MT241TOK")
			EndIf
			If lExistMT241OK
				lRetorno := ExecBlock("MT241TOK",.F.,.F.)
				If ValType(lRetorno) # "L"
					lRetorno:=.T.
				EndIf
			EndIf

		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica calendário contábil                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	lRetorno := (CtbValiDt(Nil,DA241DATA,.T.,Nil ,Nil ,{"EST001"}))
EndIf

Return lRetorno

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241Grava ³ Autor ³ Gilson Nascimento     ³ Data ³04/05/95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gravar os dados no arquivo                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero da opcao selecionada                        ³±±
±±³          ³ ExpL1 = Valida se o no. do documento sera alterado         ³±±
±±³          ³ ExpA1 = Campos do Cabecalho implementados pelo PE:MT241CAB ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a241Grava(cAlias,nOpcao,lChangeDoc,aCpoUsu)
Local lRet       := .T.
Local nY         := 0
Local nMaxArray
Local nX
Local cItem
Local aRegSD3    := {}
Local aLockSB2   := {}
Local aLockSB3   := {}
Local lIntGH     := SuperGetMV("MV_INTGH",.F.,.F.)
Local aCMNF      := {0, 0, 0, 0, 0}
Local lCAT83     := FindFunction("V240CAT83") .And. V240CAT83()
Local cNumseq    := ""
Local nPosIdent  := 0
Local aEndereco  := {}
Local lHSGrvGaj  := FindFunction("HS_GRVGAJ")
Local aItenSD3   := {}
Local cCF        := ""
Local cAproPri   := "0"
Local nPosIdDCF  := 0
Local nPosNumSeq := 0
Local lWmsNew    := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local lWmsSD3    := Iif(Type( 'lExecWms' )== 'L' , lExecWms, .F.)
Local aAuxRet    := {}
Local nPosSeq    := 0
Local cPosNumSa  := ""
Local cPosItemSa := ""
Local lComp      := .F.

Local lCpoUser   := ExistBlock( 'CPOSDH1' )
Local aCpoUser   := {}
Local aCpoAuxUsr := {}
Local nPosAux    := 0
Local nQtdItem   := 0

Private cNewItSDG  := "" // Variavel utilizada pelo programa Mata103

// -- Protecao quando rotina for executada em modo automatico
If Type("cA240End") == "U"
	Private cA240End   := CriaVar('DB_LOCALIZ')
EndIf

If Type("aCtbDia") == "U"
   Private aCtbDia := {}
EndIf

Default lChangeDoc := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o ultimo elemento do array esta em branco        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nMaxArray := Len(aCols)
If Empty(aCols[nMaxArray,nPosCod]) .And. Empty(aCols[nMaxArray,nPosQuant])
	nMaxArray--
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento para Dead-Lock                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX:=1 to Len(aCols)
	If aScan(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])==0
		aAdd(aLockSB2,aCols[nX][nPosCod]+aCols[nX][nPosLocal])
	EndIf
	If aScan(aLockSB3,aCols[nX][nPosCod])==0
		aAdd(aLockSB3,aCols[nX][nPosCod])
	EndIf
Next nX
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento para Dead-Lock                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MultLock("SB2",aLockSB2,1) .And. MultLock("SB3",aLockSB3,1)
	nQtdItem++
	For nx := 1 to nMaxArray
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ verifica se nao esta deletado (DEL)                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !aCols[nx][Len(aCols[nx])]
			If A241LinOk(nil,nx)

				// Conta o numero de itens a serem gravados por movmento interno
				nQtdItem++

				If lWmsSD3
					nPosNumSeq := aScan(aAutoItens[nx], {|x| AllTrim(x[1]) == "D3_NUMSEQ"})
					cNumseq    := aAutoItens[nx][nPosNumSeq][2]
				Else
					cNumseq := ProxNum() //Pega o numero sequencial do movimento
				EndIf

				// Integração WMS - Busca endereço
				If !lWmsSD3 .And. IntWMS(aCols[nx,nPosCod]) .And. !Empty(aCols[nx,nPosServic]) .And. !(Type('cA240End')=='U')
					a240Ender(aCols[nx,nPosLocal],aCols[nx,nPosCod],aCols[nx,nPosQuant],cNumseq,cTm,aEndereco)
				EndIf
				// Verifica se produto controla WMS
				If lWMSNew .And. !lWmsSD3 .And. (cTm > '500') .And. IntWMS(aCols[nx,nPosCod]) .And. !Empty(aCols[nx,nPosServic])

					cCF:= A240GeraCF(cAproPri,aCols[nx,nPosCod],cTM,aCols[nx,nPosOp]) //-- Retorna D3_CF

					aAdd(aItenSD3,{})
					aAdd(aItenSD3[Len(aItenSD3)],xFilial("DH1"))
					aAdd(aItenSD3[Len(aItenSD3)],cTM)
					aAdd(aItenSD3[Len(aItenSD3)],dA241Data)
					aAdd(aItenSD3[Len(aItenSD3)],cNumseq)
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosCod])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosLotCTL])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosLocal])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosLocali])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosQuant])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosQtSegUm])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosTrt])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosProj])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosTarefa])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosClVl])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosServic])
					aAdd(aItenSD3[Len(aItenSD3)],cCC)
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosConta])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosItemCT])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosStatus])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosOp])
					If nPosNumSa > 0 //-- FieldPos
						aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosNumSa])
					Else
						aAdd(aItenSD3[Len(aItenSD3)],"")
					EndIf
					If nPosItemSa > 0 //-- FieldPos
						aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosItemSa])
					Else
						aAdd(aItenSD3[Len(aItenSD3)],"")
					EndIf
					aAdd(aItenSD3[Len(aItenSD3)],cDocumento)
					aAdd(aItenSD3[Len(aItenSD3)],cCF)
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosLote])
					aAdd(aItenSD3[Len(aItenSD3)],aCols[nx,nPosNumSer])
					If nPosCusto1 > 0
						AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosCusto1]) //D3_CUSTO1
					Else
				   	AADD(aItenSD3[Len(aItenSD3)], 0)	//D3_CUSTO1
					EndIf
					If nPosCusto2 > 0
						AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosCusto2])	//D3_CUSTO2
					Else
						AADD(aItenSD3[Len(aItenSD3)], 0)	//D3_CUSTO2
					Endif
					If nPosCusto3 > 0
						AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosCusto3])	//D3_CUSTO3
					Else
						AADD(aItenSD3[Len(aItenSD3)], 0)	//D3_CUSTO3
					Endif
					If nPosCusto4 > 0
						AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosCusto4])	//D3_CUSTO4
					Else
						AADD(aItenSD3[Len(aItenSD3)], 0)	//D3_CUSTO4
					Endif
					If nPosCusto5 > 0
						AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosCusto5])	//D3_CUSTO5
					Else
						AADD(aItenSD3[Len(aItenSD3)], 0)	//D3_CUSTO5
					Endif
					AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosDValid])//D3_DTVALID
					AADD(aItenSD3[Len(aItenSD3)], aCols[nx,nPosPotenc])//D3_POTENCI

					If lCpoUser
						aCpoUser := ExecBlock('CPOSDH1',.F.,.F.,{"MATA241",nX})
						If ValType(aCpoUser) == 'A'
							aADD(aCpoAuxUsr,{})
							nPosAux := Len(aCpoAuxUsr)
							For nY := 1 to Len(aCpoUser)
								aADD(aCpoAuxUsr[nPosAux],{aCpoUser[nY,1],aCpoUser[nY,2]})
							Next nY
						EndIf
					EndIf

					//Atualiza B2_QEMPSA - Pre-Requisicao
					A185BxEmp(nX)
				Else
					RecLock(cAlias,.T.)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza dados padroes da movimentacao interna           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SD3->D3_DOC:= cDocumento
					SD3->D3_EMISSAO := dA241Data
					SD3->D3_FILIAL :=xFilial("SD3")
					SD3->D3_TM:= cTM
					SD3->D3_CC:= cCC
					aAdd(aRegSD3,SD3->(Recno()))
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza dados do corpo da movimentacao interna          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					For nY := 1 to Len(aHeader)
						If aHeader[nY][10] # "V"
							xVar := Trim(aHeader[nY][2])
							If AllTrim(xVar) != "D3_CC" .Or. (!Empty(aCols[nx,nY]))
								Replace &xVar With aCols[nx][nY]
							EndIf
						EndIf
					Next nY
					If l241Auto
						nPosIdent := aScan(aAutoItens[nx], {|x| AllTrim(x[1]) == "D3_IDENT"})
						If nPosIdent > 0
							SD3->D3_IDENT:= aAutoItens[nx][nPosIdent][2]
						EndIf
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza B2_QEMPSA - Pre-Requisicao                ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					A185BxEmp(nX)

					//Gera Ativo
					aCMNF := {0,0,0,0,0}
					If SF5->(MsSeek(xFilial("SF5")+cTm))
						If SF5->(FieldPos("F5_GERAATF"))> 0 .And. SF4->(MsSeek(xFilial("SF4")+SF5->F5_TEATF))
							If SF5->F5_GERAATF == "1"
								lComp := IIf(SF4->(FieldPos('F4_COMPONE')) > 0, SF4->F4_COMPONE == "1", .F.)
								lRet := A241AvalATF(SF4->F4_BENSATF == "1",lComp,,cNumseq,@aCMNF,SD3->D3_LOTECTL)
								If !lRet
									FreeUsedCode()
									Return { lRet , aAuxRet }
								EndIf
							EndIf
						EndIf
					EndIf
					If l241Auto .And. lWmsSD3
						If nPosLotCTL > 0
							SD3->D3_LOTECTL := aCols[nx][nPosLotCTL]
						EndIf
						If nPosLote > 0
							SD3->D3_NUMLOTE := aCols[nx][nPosLote]
						EndIf
						If nPosLocali > 0
							SD3->D3_LOCALIZ := aCols[nx][nPosLocali]
						EndIf
						nPosIdDCF := aScan(aAutoItens[nx], {|x| AllTrim(x[1]) == "D3_IDDCF"})
						If nPosIdDCF > 0 .And. SD3->(FieldPos("D3_IDDCF")) > 0
							SD3->D3_IDDCF   := aAutoItens[nx][nPosIdDCF][2]
						EndIf
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Envia p/func. de atualizacoes (SD3,SB2,SB3,SC2,..) ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					a240Atu(NIL,nx,,cNumseq,aCMNF)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Gravacao da CAT83                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lCAT83 .And. Empty(SD3->D3_CODLAN)
						SD3->D3_CODLAN := A240CAT83()
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Integracao TMS                                                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If IntTMS() .And. (Len(aRatVei)>0  .Or. Len(aRatFro)>0)
						If Type("nHdlPrv") <> "N"	.And. Type("lCtbOnLine") <> "L"
							lCtbOnLine := .F.
							nHdlPrv    := 0
							nTotal     := 0
							cLoteEst   := ''
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Verifica se o Item da NF foi rateado por Veiculo/Viagem ou por Frota    ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cItem  := StrZero(nx,Len(SDG->DG_ITEM))
						nItRat := aScan(aRatVei,{|x| x[1] == cItem})
						If nItRat > 0
							cTpRateio := "V"
							A103GrvSDG('SD3',aRatVei,cTpRaTeio,cItem,lCtbOnLine,nHdlPrv,@nTotal,cLoteEst,"MATA241")
						Else
							nItRat := aScan(aRatFro,{|x| x[1] == cItem})
							If nItRat > 0
								cTpRateio := "F"
								A103GrvSDG('SD3',aRatFro,cTpRateio,cItem,lCtbOnLine,nHdlPrv,@nTotal,cLoteEst,"MATA241")
							EndIf
						EndIf
					EndIf

				    If lIntGH .And. lHSGrvGaj
						If If(Type("__lMovest") <> "U", __lMovest, .F.)
							HS_GRVGAJ(,nx, SD3->D3_NUMSEQ, SD3->D3_DOC)
						Endif
					EndIf
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria array com os movimentos dos Produtos sem saldos         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lLogMov
					LogSaldo(aCols[nx,1],aCols[nx,15],aCols[nx,2],aCols[nx,6],aCols[nx,3],;
					aCols[nx,17],aCols[nx,18],aCols[nx,19],aCols[nx,20],aCols[nx,21],;
					@aLogSld,cDocumento,dA241Data)
				EndIf
				lRet := .F.
				Exit
			EndIf
		EndIf

		If lRet
			//Tratamento para Inclusao da Chave Doc + NumSeq para ser Utilizado na funcao A185AtuSCQ
			If IntWMS(aCols[nx,nPosCod])
				//No caso do WMS deve-se utilizar as informações das variáveis para preencher o array, visto que não foi gerada a SD3 neste momento
				cPosNumSa := Iif(nPosNumSa > 0,aCols[nx,nPosNumSa],"")
				cPosItemSa := Iif(nPosItemSa > 0,aCols[nx,nPosItemSa],"")
				nPosSeq := Ascan( aAuxRet, { | x | AllTrim( x[ 1 ] ) == AllTrim( cDocumento ) .And. AllTrim( x[ 2 ] ) == AllTrim( cNumseq ) .And. AllTrim( x[ 3 ] ) == AllTrim( aCols[nx,nPosCod] ) .And. AllTrim( x[ 4 ] ) == AllTrim( aCols[nx,nPosLocal] ) .And. AllTrim( x[ 5 ] ) == AllTrim( cPosNumSa ) .And. AllTrim( x[ 6 ] ) == AllTrim( cPosItemSa ) } )
				If nPosSeq == 0
					Aadd( aAuxRet,{ cDocumento, cNumseq, aCols[nx,nPosCod], aCols[nx,nPosLocal], cPosNumSa, cPosItemSa} )
				EndIf
			Else
				nPosSeq := Ascan( aAuxRet, { | x | AllTrim( x[ 1 ] ) == AllTrim( SD3->D3_DOC ) .And. AllTrim( x[ 2 ] ) == AllTrim( SD3->D3_NUMSEQ ) .And. AllTrim( x[ 3 ] ) == AllTrim( SD3->D3_COD ) .And. AllTrim( x[ 4 ] ) == AllTrim( SD3->D3_LOCAL ) .And. AllTrim( x[ 5 ] ) == AllTrim( SD3->D3_NUMSA ) .And. AllTrim( x[ 6 ] ) == AllTrim( SD3->D3_ITEMSA ) } )
				If nPosSeq == 0
					Aadd( aAuxRet,{ SD3->D3_DOC, SD3->D3_NUMSEQ, SD3->D3_COD, SD3->D3_LOCAL, SD3->D3_NUMSA, SD3->D3_ITEMSA } )
				EndIf
			EndIf
		EndIf

	Next nx


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
	// Telemetria - Uso da classe FwCustomMetrics                   //
	// Metrica - setAverageMetric                                   //
	// Grava a media de itens de um movimento multiplo incluido     //
	//?ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ?
	If FWLibVersion() >= "20210628"
		FWCustomMetrics():setAverageMetric(	"MATA241"/*cSubRoutine*/,;
											"estoque-protheus_movimentacao_multipla_media_mensa_qtd_itens_average" /*cIdMetric*/,;
											nQtdItem /*nValue*/,;
											/*dDateSend*/,;
											/*nLapTime*/,;
											"MATA241"/*cRotina*/)
	EndIf


	If Len(aItenSD3) > 0
		lRet := EspDH1Wms(aItenSD3,"MATA241",cA240End,,,aCpoAuxUsr)
		ADEL(aItenSD3[1],1)
		ASIZE(aItenSD3[1],1)
		aItenSD3[1] := {}
	EndIf

	FreeUsedCode()

	If lMT241GRV
		ExecBlock("MT241GRV",.F.,.F.,{cDocumento,aCpoUsu})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o custo medio e' calculado On-Line               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cCusMed == "O"
		If !lCriaHeader .And. nTotal > 0
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Inicializa perguntas deste programa                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ mv_par01 - Se mostra e permite digitar lancamentos contabeis   ³
			//³ mv_par02 - Se deve aglutinar os lancamentos contabeis          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Pergunte("MTA240",.F.)
			lDigita   := Iif(mv_par01 == 1,.T.,.F.)
			lAglutina := Iif(mv_par02 == 1,.T.,.F.)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se ele criou o arquivo de prova ele deve gravar o rodape'    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RodaProva(nHdlPrv,nTotal)

			If !Empty(aCtbDia)
				cCodDiario := CtbaVerdia()
				For nX := 1 to Len(aCtbDia)
					aCtbDia[nX][3] := cCodDiario
				Next nX
			EndIf

			If cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina,,,,,,aCtbDia)
				lCriaHeader := .T.
				nTotal      := 0 // Total dos lancamentos contabeis

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava a data de Contabilizacao do campo D3_DTLANC         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				For nX := 1 To Len(aRegSD3)
					SD3->(MsGoTo(aRegSD3[nX]))
					RecLock("SD3",.F.)
					Replace D3_DTLANC With dDataBase
					MsUnLock()
				Next nX
			EndIf

		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ 	Integracao com PIMS				³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (SuperGetMV("MV_PIMSINT",.F.,.F.))
			PIMSCtOnline(aRegSD3)
		EndIf

	EndIf

Else
	ConOut("WARNING: DEADLOCK CONTROL IS ON")
	lRet := .F.
EndIf

Return { lRet , aAuxRet }

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241LinOk ³ Autor ³ Rosane L. Chene       ³ Data ³ 05/10/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa que faz consistencias apos a digitacao da tela    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA240                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 = Variavel Objeto	                                  ³±±
±±³          ³ ExpN1 = Numero da variavel n referente ao aCols            ³±±
±±³          ³ ExpL1 = Se .T. analisa se ha qtde disponivel em estoque    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241LinOk(o,nx,lVldQtd)
Local lRet        := .T.
Local lSai        := .F.
Local cAlias      := "SD3"
Local nOrdSB8
Local nRecSB8
Local cLocal
Local i
Local nQtd        := 0
Local nQtdEmp     := 0
Local nSaldo      := 0
Local lBaixaEmp   := .F.
Local lBxEmpB8    := .F.
Local lQtdZero    := .F.
Local cTrt        := ""
Local lMov        := If(nx==Nil,.T.,.F.)
Local lEmpPrj     := .F.
Local nQtdPrj     := 0
Local nXZ         := 0
Local cProcName   := Upper( AllTrim( ProcName(1) ) )
Local lEmpPrev    := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lVldAlmo    := GetMV("MV_VLDALMO") == "S"
Local cSeek       := ""
Local lGrade      := MaGrade()
Local lReferencia := .F.
Local cPROJPMS    := ""
Local cTASKPMS    := ""
Local nPosCod     := aScan(aHeader,{|x| AllTrim(x[2])=="D3_COD"})
Local nPosLocal   := aScan(aHeader,{|x| AllTrim(x[2])=="D3_LOCAL"})
Local nRevisao    := aScan(aHeader,{|x| AllTrim(x[2])=="D3_REVISAO"})
Local nPConta     := GDFieldPos("D3_CONTA")
Local nPItemCta   := GDFieldPos("D3_ITEMCTA")
Local nPCLVL      := GDFieldPos("D3_CLVL")
Local lRevProd    := SuperGetMv("MV_REVPROD",.F.,.F.)
Local cRvSB5      := ""
Local cBlqSG5     := ""
Local cStatus     := ""
Local lMata340    := IsInCallStack("MATA340")
Local lWmsNew     := SuperGetMv("MV_WMSNEW",.F.,.F.)
Local lWmsSD3     := Iif(Type( 'lExecWms' )== 'L' , lExecWms, .F.)
Local lPrdIntWms  := .F.
Local cLocProc    := GetMvNNR( 'MV_LOCPROC' , '99' )
Local lDAmarCt    := SuperGetMV("MV_DAMARCT",.F.,.F.)
Local cD3_LOCALIZ := ""
Local cNumSeri    := ""
Local oProdEmp as Object
Local cChave      := ''
Local lBaixaSd4   := .F.
Local lA241Data   as logical
Local nSdlD14     := 0
Local nSldDisp	  := 0
Local lWmsBxOp    := SuperGetMV("MV_WMSBXOP",.F.,.F.)

Default lVldQtd  := .F.

lA241Data := Type('dA241Data') == "D"

// Impede a inclusao de movimento com TM inexistente
If !lMata340
	If SF5->F5_FILIAL+SF5->F5_CODIGO # xFilial("SF5")+cTm
		If !SF5->(MsSeek(xFilial("SF5")+cTm))
			Help(" ",1,"REGNOIS")
			Return .F.
		EndIf
	EndIf
EndIf

// Seta variavel n para valor de nx
If Valtype(nx) == "N"
	n:=nx
EndIf

lPrdIntWms := IntWMS(aCols[n,nPosCod])

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o produto est  em revisao vigente e envia para armazem de CQ para ser validado pela engenharia    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRevProd .And. !GDdeleted(n)

	cRvSB5 := Posicione("SB5",1,xFilial("SB5")+aCols[n,nPosCod],"B5_REVPROD")
	cBlqSG5:= Posicione("SG5",1,xFilial("SG5")+aCols[n,nPosCod]+aCols[n,nRevisao],"G5_MSBLQL")
	cStatus:= Posicione("SG5",1,xFilial("SG5")+aCols[n,nPosCod]+aCols[n,nRevisao],"G5_STATUS")
    If cRvSB5=="1"
	    If Empty(cRvSB5)
			Aviso(STR0014,STR0048,{STR0051})//"Não foi encontrado registro do produto selecionado na rotina de Complemento de Produto."
			lRet:= .F.
		ElseIf Empty(cBlqSG5)
			Aviso(STR0014,STR0049,{STR0051})//"O produto selecionado não possui revisão em uso. Verifique o cadastro de Revisões."
			lRet:= .F.
		ElseIf cBlqSG5=="1"
			Help(" ",1,"REGBLOQ")
			lRet:= .F.
		ElseIf cStatus=="2" .AND. cTM<"500"
			Aviso(STR0014,STR0050,{STR0051})//"Esta revisão não pode ser alimentada pois está inativa."
			lRet:= .F.
		EndIf
	EndIf
EndIf

IF nPosProj>0 .and. nPosTarefa>0
	If lRet .And. IntePMS() .And. FindFunction('VldMovPMS')
		lRet := VldMovPMS(aCols[n,nPosProj], aCols[n,nPosTarefa], cTm, aCols[n,nPosCod], aCols[n,nPosQuant], "MATA241")
	EndIf
EndIf
//Validacao de permissao do armazem
If !lMata340 .And. lRet .And. !GDdeleted(n)
	lRet := MaAvalPerm(3,{aCols[n][nPosLocal],aCols[n][nPosCod]})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o armazem está bloqueado.   ³
//ÀÄÄÄÄÄÄÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .and. !GDdeleted(n) .And. !ExistCpo("NNR",aCols[n][nPosLocal])
	lRet := .F.
EndIf

If lRet .And. !Empty(aCols[n,nPosOp]) .And. !("OS001" $ aCols[n,nPosOp])
	SC2->(DbSetOrder(1)) // FILIAL + NUM + ITEM + SEQUEN + ITEMGRD
	If SC2->(dbSeek(xFilial("SC2") + aCols[n,nPosOp])) .And. !Empty(SC2->C2_DATRF)
		Help(" ",1,"OPENCERR")
		lRet := .F.
	EndIf
EndIf

//-- Posiciona SB1 para validacoes
SB1->(MsSeek(xFilial("SB1")+aCols[n,nPosCod]))
//Bloqueia produto fantasma
IIf(lRet,lRet := A241VLDFan(aCols[n,nPosCod]),lRet)

If lRet .And. !GDdeleted(n) .And. (lRet:=MaCheckCols(aHeader,aCols,n))
	For nxZ = 1 To Len(aHeader)
		If Empty(aCols[n][nxZ])
			If Trim(aHeader[nxZ][2]) == "D3_COD" .And. n == Len(aCols)
				Help(" ",1,"A11002")
				lRet := .F.
				lSai:=.T.
			EndIf
			If (Trim(aHeader[nxZ][2]) == "D3_COD" .Or.;
				(Trim(aHeader[nxZ][2]) == "D3_QUANT" .And. SF5->F5_QTDZERO # "1")) .And. !lSai
				Help(" ",1,"A241BRANC",,'"'+' '+aHeader[nxZ][1]+' '+'"',4,3)
				lRet := .F.
			EndIf
		EndIf
		If !lRet
			Exit
		EndIf
	Next

	If lRet .And. Inclui .And. lPrdIntWms .And. !lWmsSD3
		lRet := WmsAvalSD3("6")
	EndIf
	If lRet .And. Inclui
		If !lSai .And. !lMata340
			SB2->(DbSetOrder(1))
			If aCols[n][Len(aCols[n])]
				Return(lRet)
			EndIf
			If lGrade
				cVar:=aCols[n,nPosCod]
				lReferencia := MatGrdPrrf(@cVar)
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Utiliza o parametro MV_VLDALMO para validar o local |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lReferencia
				If lRet .And. lVldAlmo .And. !(SB2->(MsSeek(xFilial("SB2")+aCols[n,nPosCod]+aCols[n,nPosLocal])))
					Help("",1,"A241LOCAL",,aCols[n,nPosCod],2,23)
					lRet := .F.
				EndIf
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o produto est  sendo inventariado.      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lReferencia
				cD3_LOCALIZ := iIf(nPosLocali >0,aCols[n,nPosLocali],CriaVar("D3_LOCALIZ"))
				If BlqInvent(aCols[n,nPosCod],aCols[n,nPosLocal],,cD3_LOCALIZ)
					Help(" ",1,"BLQINVENT",,aCols[n,nPosCod]+OemToAnsi(STR0017)+aCols[n,nPosLocal],1,11) //" Almox: "
					lRet:=.F.
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Analisa se o tipo do armazem permite a movimentacao |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lRet .And. AvalBlqLoc(aCols[n,nPosCod],aCols[n,nPosLocal],Nil,,,,,,,aCols[n,nPosOp])
					lRet:=.F.
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se a Qtde estiver em branco ele deve dar uma mensagem  ³
			//³ de que este registro nao sera' gravado.                ³
			//³ Este campo nao pode ficar obrigatorio porque ele e'    ³
			//³ utilizado na Producao(Informacao da Perda de Producao),³
			//³ onde nao deve ser obrigatorio.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. Empty(aCols[n][nPosQuant]) .And. !lMata340
				If SF5->(MsSeek(xFilial("SF5")+cTm))
					If SF5->F5_QTDZERO # "1"
						Help(" ",1,"MA240NAOGR")
						lRet := .F.
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Validacao para nao permitir que usuario informe os campos       |
						//|LOTE/SUB-LOTE quando quando a qtde. informada e' igual a zero.  |
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					ElseIf ( !Empty(nPosLotCTL).And.!Empty(aCols[n,nPosLotCTL]) .Or. ;
						!Empty(nPosLote).And.!Empty(aCols[n,nPosLote]) )
						Help(" ",1,"A240QLZERO")
						lRet := .F.
					EndIf
				Else
					Help(" ",1,"MA240NAOGR")
					lRet := .F.
				EndIf
			EndIf

			dbSelectArea("SF5")
			If !lMata340 .And. MsSeek(xFilial("SF5")+cTM)
				lQtdZero:=Empty(aCols[n][nPosQuant]) .And. SF5->F5_QTDZERO == "1"

				cPROJPMS := iIf(Empty(nPosProj), CriaVar("D3_PROJPMS"), aCols[n,nPosProj])
				cTASKPMS := iIf(Empty(nPosTarefa), CriaVar("D3_TASKPMS"), aCols[n,nPosTarefa])

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Caso esteja baixando empenho verifica o saldo empenhado³
				//³ a fim de evitar divergencia entre o movimento no SD3 e ³
				//³ os movimentos no SD5 e SDB.                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			  If !lReferencia
					lEmpPrj  := ( !Empty(cPROJPMS) .And. ;
								  !Empty(cTASKPMS) )
					lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[n,nPosOP])) .OR. lEmpPrj)
					cTrt     :=If(ValType(nPosTRT)=="N" .And. nPosTRT > 0,aCols[n,nPosTrt],CriaVar("D3_TRT"))
					lRet:=lRet .And. A240AvalEm(@lBaixaEmp,aCols[n,nPosCod],aCols[n,nPosLocal],aCols[n,nPosQuant],If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[n,nPosLotCTL]),If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[n,nPosLote]),If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[n,nPosLocali]),If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[n,nPosNumSer]),If(Empty(nPosOP),CriaVar("D3_OP"),aCols[n,nPosOp]),cTRT,.T.,cTM<="500",@lBxEmpB8,If(nPosPotenc>0,aCols[n,nPosPotenc],), cPROJPMS, cTASKPMS)
					If ValType(nPosTRT)=="N" .And. nPosTRT > 0 .and. lRet .and. lEmpPrj
						aCols[n,nPosTrt] := cTrt
					EndIf
				Endif
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Validacao para nao permitir que um produto de apropriacao indireta     |
			//| tenha o numero da ordem de producao preenchido.                        |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. !lReferencia
				If (SB1->B1_APROPRI == 'I') .And. (SF5->F5_APROPR != "S") .And. If(Empty(nPosOP),.F.,!Empty(aCols[n,nPosOP]))
					Help("",1,"MA241IND")
					lRet := .F.
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Validacao para nao permitir que um produto de apropriacao indireta     |
				//| tenha movimentacao valorizada (armazem de processo).				   |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lRet .And. (SB1->B1_APROPRI == 'I') .And. (SF5->F5_APROPR != "S") .And. (SF5->F5_VAL == "S")
					Help("",1,"A240VALIN")
					lRet := .F.
				EndIf
			Endif

			nQtdPrj := If(lBaixaEmp .And. lEmpPrj, aCols[n,nPosQuant], 0)

			If lRet.and.!lReferencia
				//--> Verifica se o saldo do armazem esta liberado
				lRet := SldBlqSB2(aCols[n,nPosCod],aCols[n,nPosLocal])
			EndIf
			If lRet .And. (GETMV("MV_ESTNEG") != "S" .Or. Localiza(aCols[n,nPosCod],.T.) .Or. Rastro(aCols[n,nPosCod])) .And. !IsProdMod(aCols[n][nPosCod]) .And. !lReferencia
				If cTM > "500"
					dbSelectArea("SB2")
					MsSeek(xFilial("SB2")+aCols[n][nPosCod]+aCols[n][nPosLocal])
					If !lQtdZero .And. QtdComp(aCols[n][nPosQuant]) > QtdComp(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,If(lBaixaEmp,aCols[n,nPosQuant],Nil),nQtdPrj,Nil, If(Type('dA241Data')=="D",dA241Data,dDataBase),!l185)) + If(l185 .And. GetMv("MV_TPSALDO")=="S",QtdComp(aCols[n][nPosQuant]),0)
						cHelp:=OemToAnsi(STR0016)+AllTrim(B2_COD)+OemToAnsi(STR0017)+B2_LOCAL+OemToAnsi(STR0018)+ALLTRIM(Transform(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,If(lBaixaEmp,aCols[n,nPosQuant],Nil),nQtdPrj,Nil,If(Type('dA241Data')=="D",dA241Data,dDataBase)),PesqPictQt("B2_QATU",14)))	// Produto Local Saldo Disp.
						Help(" ",1,"MA240NEGAT",,cHelp,4,1)
						lRet := .F.
					EndIf
					If Localiza(aCols[n,nPosCod],.T.)
						// Verifica campos necessarios p/ Localizacao
						If Empty(nPosLocali)
							UserException("D3_LOCALIZ"+OemToAnsi(STR0021))
						EndIf
						cNumSeri := IIf(nPosNumSer > 0, aCols[n,nPosNumSer], CriaVar("D3_NUMSERI"))
						If !(lPrdIntWms .And. !Empty(aCols[n,nPosServic]))
							If Empty(aCols[n,nPosLocali]) .And. Empty(cNumSeri) .And. !(SF5->F5_QTDZERO=="1" .And. QtdComp(aCols[n,nPosQuant]) == QtdComp(0))
								Help(" ",1,"LOCALIZOBR")
								lRet:=.F.
							EndIf
						EndIf
						If lRet .And. (!Empty(aCols[n,nPosLocali]) .Or. !Empty(cNumSeri))
						 	If !(lWmsNew .And. lPrdIntWms)
						 		/*
									Alterada chamada da função SaldoSBF para passar o número da OP.
									Necessário para o projeto de integração TOTVS MES.
								*/
								If QtdComp(SaldoSBF(aCols[n,nPosLocal],aCols[n,nPosLocali],aCols[n,nPosCod],cNumSeri,aCols[n,nPosLotCTL],aCols[n,nPosLote],lBxEmpB8,,,aCols[n,nPosoP])) < QtdComp(aCols[n,nPosQuant])
									Help(" ",1,"SALDOLOCLZ")
									lRet:=.F.
								EndIf
							Else
								//Executa SEMPRE que for WMS novo com produto WMS
								If QtdComp(nSdlD14 := WmsSldD14(aCols[n,nPosLocal],aCols[n,nPosLocali],aCols[n,nPosCod],aCols[n,nPosNumSer],aCols[n,nPosLotCTL],aCols[n,nPosLote],lBxEmpB8)) < QtdComp(aCols[n,nPosQuant])
									If !Empty(aCols[n,nPosServic]) .Or. !lWmsBxOp
										Help(" ",1,"SALDOLOCLZ")
										lRet:=.F.
									ElseIf lWmsBxOp .And. FindFunction("WMS241SlOp") .And. !WMS241SlOp(cTm,aCols[n,nPosCod],aCols[n,nPosoP],aCols[n,nPosServic],aCols[n,nPosLocal],aCols[n,nPosLocali],aCols[n,nPosLotCTL],aCols[n,nPosLote],aCols[n,nPosQuant],aCols[n,nPosTrt],aCols[n,nPosLocali],nSdlD14)
										lRet := .F.
									EndIf
								EndIf
							EndIf
						EndIf
					EndIf
				Else
					dbSelectArea("SB1")
					MsSeek(xFilial("SB1")+aCols[n,nPosCod])
					If (B1_APROPRI == "I" .And. Empty(If(Empty(nPosOP),CriaVar("D3_OP"),aCols[n,nPosOp]))) .And. SF5->F5_APROPR != "S"
						cApropri := "3"
					Else
						cApropri := "0"
					EndIf
					If cApropri == "3"
						dbSelectArea("SB2")
						If MsSeek(xFilial("SB2")+aCols[n,nPosCod] + cLocProc)
							If cApropri == "3" .And. (QtdComp(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,Nil,Nil,Nil,If(lA241Data,dA241Data,dDataBase))) < QtdComp(aCols[n,nPosQuant]) )
								Help(" ",1,"MA240PRNEG")
								lRet:=.F.
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se usar Rastreabilidade e o numero do Lote nao for     ³
			//³ preenchido o registro nao sera gravado tambem.         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. Rastro(aCols[n][nPosCod]) .And. !lQtdZero .And. !lReferencia
				// Verifica campos necessarios p/ Rastreabilidade
				If Empty(nPosLotCTL)
					UserException("D3_LOTECTL"+OemToAnsi(STR0021))
				EndIf
				If !IsProdMod(aCols[n][nPosCod])
					cLocal := aCols[n][nPosLocal]
					If  cTM <= "500" .And. SF5->F5_APROPR != "S" .And. SB1->B1_APROPRI == "I"
						cLocal := cLocProc
					EndIf
					If cTM > "500" .Or. (SF5->F5_APROPR != "S" .And. SB1->B1_APROPRI == "I")
						If Empty(If(Rastro(aCols[n][nPosCod],"S"),aCols[n][nPosLote],aCols[n][nPosLotCTL]))
							Help(" ",1,"A240NUMLOT")
							lRet := .F.
						EndIf
						If lRet
							dbSelectArea("SB8")
							nOrdSB8 := IndexOrd()
							nRecSB8 := RecNo()
							If Rastro(aCols[n][nPosCod],"S")
								dbSetOrder(2)
								If dbSeek(xFilial("SB8")+aCols[n][nPosLote]+aCols[n][nPosLotCTL]+aCols[n][nPosCod]+cLocal)
									oProdEmp := JsonObject():New()
									nQtd:=0
									nQtdEmp := 0
									For i:=If(ValType(nx)=="N",nx,1) to Len(aCols)
										If aCols[i][nPosLotCTL]+aCols[i][nPosLote]+aCols[i][nPosCod] == aCols[n][nPosLotCTL]+aCols[n][nPosLote]+aCols[n][nPosCod] .And. !aCols[i][Len(aCols[i])]
											If aCols[i, nPosLocal] == cLocal
												lBaixaSd4 := .F.
												lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOP])) .OR. lEmpPrj)

												cChave := aCols[i,nPosCod];
															+aCols[i,nPosLocal];
															+cValToChar(aCols[i,nPosQuant]);
															+If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]);
															+If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]);
															+If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]);
															+If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]);
															+If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]);
															+If(nPosPotenc>0,cValToChar(aCols[i,nPosPotenc]),"");

												If oProdEmp[cChave] == NIL
													A240AvalEm(lBaixaEmp,aCols[i,nPosCod],aCols[i,nPosLocal],aCols[i,nPosQuant],If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]),If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]),If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]),If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]),If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]),cTRT,.T.,cTM<="500",@lBaixaSd4,If(nPosPotenc>0,aCols[i,nPosPotenc],), cPROJPMS, cTASKPMS)
													oProdEmp[cChave] := lBaixaSd4
												Else
													lBaixaSd4 := oProdEmp[cChave]
												EndIf

												If !lBaixaSd4
													nQtd+=aCols[i][nPosQuant]
												Else
													nQtdEmp += aCols[i][nPosQuant]
												EndIf
											EndIf
										EndIf
									Next i
									If nQtd > 0
										If QtdComp(SB8Saldo(.F.,nil,nil,nil,nil,lEmpPrev,nil,dA241Data)) < QtdComp(nQtd)
											cHelp:=OemToAnsi(STR0016)+AllTrim(B8_PRODUTO)+OemToAnsi(STR0017)+B8_LOCAL+OemToAnsi(STR0018)+ALLTRIM(Transform(SB8Saldo(nil,nil,nil,nil,nil,lEmpPrev,nil,dA241Data),PesqPictQt("B8_SALDO", 14)))+OemToAnsi(STR0019)+aCols[n][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
											Help(" ",1,"A240LOTENE",,cHelp,4,1)
											lRet := .F.
										EndIf
									EndIf
									If lRet
										nSaldo := 0
										If nQtdEmp > 0
											nSaldo:=SB8Saldo(.T.,nil,nil,nil,nil,.F.,nil,dA241Data)
										EndIf
										If QtdComp(nSaldo) < QtdComp(nQtdEmp)
											cHelp:=OemToAnsi(STR0016)+AllTrim(B8_PRODUTO)+OemToAnsi(STR0017)+B8_LOCAL+OemToAnsi(STR0018)+ALLTRIM(Transform(SB8Saldo(nil,nil,nil,nil,nil,lEmpPrev,nil,dA241Data),PesqPictQt("B8_SALDO", 14)))+OemToAnsi(STR0019)+aCols[n][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
											Help(" ",1,"A240LOTENE",,cHelp,4,1)
											lRet := .F.
										EndIf
									EndIf
								Else
									Help(" ",1,"A240LOTERR")
									lRet := .F.
								EndIf
							Else
								dbSetOrder(3)
								cSeek:=xFilial("SB8")+aCols[n][nPosCod]+cLocal+aCols[n][nPosLotCTL]
								If dbSeek(cSeek)
									oProdEmp := JsonObject():New()

									nQtd:=0
									nQtdEmp := 0
									For i:=If(ValType(nx)=="N",nx,1) to Len(aCols)
										If aCols[i][nPosLotCTL]+aCols[i][nPosCod] == aCols[n][nPosLotCTL]+aCols[n][nPosCod] .And. !aCols[i][Len(aCols[i])]
											If aCols[i, nPosLocal] == cLocal .Or. (SF5->F5_APROPR != "S" .And. SB1->B1_APROPRI == "I")
												lBaixaSd4 := .F.
												lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOP])) .OR. lEmpPrj)

												cChave := aCols[i,nPosCod];
															+aCols[i,nPosLocal];
															+cValToChar(aCols[i,nPosQuant]);
															+If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]);
															+If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]);
															+If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]);
															+If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]);
															+If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]);
															+If(nPosPotenc>0,cValToChar(aCols[i,nPosPotenc]),"");

												If oProdEmp[cChave] == NIL
													A240AvalEm(lBaixaEmp,aCols[i,nPosCod],aCols[i,nPosLocal],aCols[i,nPosQuant],If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]),If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]),If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]),If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]),If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]),If(Empty(nPosTrt),CriaVar("D3_TRT"),aCols[i,nPosTrt]),.T.,cTM<="500",@lBaixaSd4,If(nPosPotenc>0,aCols[i,nPosPotenc],), cPROJPMS, cTASKPMS)
													oProdEmp[cChave] := lBaixaSd4
												Else
													lBaixaSd4 := oProdEmp[cChave]
												EndIf

												If !lBaixaSd4
													nQtd+=aCols[i][nPosQuant]
												Else
													nQtdEmp += aCols[i][nPosQuant]
												EndIf
											EndIf
										EndIf
									Next i
									If nQtd > 0
										nSaldo:=SaldoLote(aCols[n][nPosCod],cLocal,aCols[n][nPosLotCTL],NIL,.F.,.T.,nil,dA241Data,aCols[n][nPosOp])
									EndIf
									If QtdComp(nSaldo) < QtdComp(nQtd)
										cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[n][nPosCod])+OemToAnsi(STR0017)+cLocal+OemToAnsi(STR0018)+ALLTRIM(Transform(nSaldo,PesqPictQt("B8_SALDO", 14)))+OemToAnsi(STR0019)+aCols[n][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
										Help(" ",1,"A240LOTENE",,cHelp,4,1)
										lRet := .F.
									EndIf
									If lRet
										nSaldo := 0
										If nQtdEmp > 0
											nSldDisp	:= SaldoLote(aCols[n][nPosCod],cLocal,aCols[n][nPosLotCTL],NIL,.F.,.T.,nil,dA241Data,aCols[n][nPosOp])
											nSaldo		:= SaldoLote(aCols[n][nPosCod],cLocal,aCols[n][nPosLotCTL],NIL,.T.,.T.,nil,dA241Data,aCols[n][nPosOp],,.F.)
											
											If nQtdEmp > nSaldo
												nSaldo		:= (nSldDisp - nSaldo)
											EndIf
										EndIf
										If QtdComp(nSaldo) < QtdComp(nQtdEmp)
											cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[n][nPosCod])+OemToAnsi(STR0017)+cLocal+OemToAnsi(STR0018)+ALLTRIM(Transform(nSaldo,PesqPictQt("B8_SALDO", 14)))+OemToAnsi(STR0019)+aCols[n][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
											Help(" ",1,"A240LOTENE",,cHelp,4,1)
											lRet := .F.
										EndIf
									EndIf
								Else
									Help(" ",1,"A240LOTERR")
									lRet := .F.
								EndIf
							EndIf
							dbSetOrder(nOrdSB8)
							MsGoto(nRecSB8)
						EndIf
					Else
						dbSelectArea("SB8")
						nOrdSB8 := IndexOrd()
						nRecSB8 := RecNo()
						If !Empty(If(Rastro(aCols[n][nPosCod],"S"),aCols[n][nPosLote],aCols[n][nPosLotCTL]))
							If Rastro(aCols[n][nPosCod],"S")
								dbSetOrder(2)
								cSeek:=xFilial("SB8")+aCols[n][nPosLote]+aCols[n][nPosLotCTL]+aCols[n][nPosCod]+cLocal
							Else
								dbSetOrder(3)
								cSeek:=xFilial("SB8")+aCols[n][nPosCod]+cLocal+aCols[n][nPosLotCTL]
							EndIf
							If dbSeek(cSeek)
								If Rastro(aCols[n][nPosCod],"S")
									nQtd:=0
									For i:=If(ValType(nx)=="N",nx,1) to Len(aCols)
										If aCols[i][nPosLotCTL]+aCols[i][nPosLote]+aCols[i][nPosCod] == aCols[n][nPosLotCTL]+aCols[n][nPosLote]+aCols[n][nPosCod] .And. !aCols[i][Len(aCols[i])]
											If aCols[i, nPosLocal] == cLocal
												nQtd+=aCols[i][nPosQuant]
											EndIf
										EndIf
									Next i
									If QtdComp(SB8SALDO(,,,,,lEmpPrev,,,.T.) + nQtd) > QtdComp(SB8->B8_QTDORI)
										Help(" ",1,"A240LOTQTD")
										lRet := .F.
									EndIf
								EndIf
							EndIf
							dbSetOrder(nOrdSB8)
							MsGoto(nRecSB8)
						Else
							SB8->( dbSetOrder( 3 ) )
							SB8->( dbSeek( FWxFilial( "SB8" ) + aCols[n][nPosCod] + cLocal + aCols[n][nPosLotCTL] ) )
							dbSetOrder(nOrdSB8)
						EndIf
					EndIf
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se a movimentacao for valorizado e o custo nao for     ³
			//³ preenchido o registro nao sera gravado tambem.         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. SF5->F5_VAL=="S"
				IF   nPosCusto1 == 0 .Or.  aCols[n][nPosCusto1] = 0
					Help(" ",1,"A240VALSD3")
					lRet := .F.
				Endif
			EndIf
		Else
			lRet := .T.
		EndIf

	EndIf
	If !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[n,nPosOp])) .And. lRet .And. lMov .And. !lReferencia
		If ( l241Auto )
			lRet:= .T.
		Else
			lRet:=A240Alert(aCols[n,nPosCod],If(Empty(nPosOP),CriaVar("D3_OP"),aCols[n,nPosOP]))
		EndIf
	EndIf
	If lRet
		lRet:=A240Data( dA241Data, aCols[n,nPosOP] )
	EndIf
	If lUsaSegUm .And. lRet
		lRet := A240SegUm()
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Validacao para nao permitir que o usuario restaure uma linha    |
	//³ deletada sem antes verificar o saldo disponivel para o produto. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet
		If lVldQtd .And. SuperGetMV("MV_ESTNEG") == "N"
			M->D3_QUANT	:= aCols[n][nPosQuant]
			lRet:= A240Quant()
		EndIf
	EndIf

	If lRet
		If (AvalMovSelo(aCols[n][nPosCod],cTM))
			Help(" ",1,"SELOFSINC")
			lRet:=.F.
		EndIf
	EndIf

	dbSelectarea(cAlias)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Custo FIFO On-Line                     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. IsFifoOnLine() .And. cTM > "500"
	If SaldoSBD("SD3",aCols[n,nPosCod],aCols[n,nPosLocal],dDataBase,.F.)[1] < aCols[n][nPosQuant]
		Help(" ",1,"DIVFIFO2")
		lRet := .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada MT241TOK                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. ExistBlock("MT241LOK")
	lRet := ExecBlock("MT241LOK",.F.,.F.,{n})
	If ValType(lRet) # "L"
		lRet:=.T.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa lancamento de validacao, quando a integracao com PCO estah ligada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. !GDdeleted(n)
	If cProcName # "A241GRAVA" .And. cProcName # "A241TUDOOK"
		If cTM <= "500"		// Entrada
			lRet := PCOVldLan("000151","01","MATA240",.T.)
		Else				// Saida
			lRet := PCOVldLan("000151","02","MATA240",.T.)
		EndIf
	EndIf
EndIf

//Consiste amarração da Conta Contábil X Centro de Custo
If lRet .And. nPConta <> 0 .And. nPItemCta <> 0 .And. nPClVl <> 0 .And. !( lDAmarCt )
	If !CtbAmarra(aCols[n,nPConta],cCC,aCols[n,nPItemCTA],aCols[n,nPCLVL])
		lRet:=.F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Conta Contábil está bloqueada. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. nPConta >0 .AND. !( Empty( aCols[ n, nPConta ] ) )
	lRet := Ctb105Cta( aCols[ n, nPConta ] )
EndIf

/*--------------------------+
| Validação SIGAMNT/SIGAGFR |
+--------------------------*/
If lRet .And. !GDdeleted( n ) .And. FindFunction( 'MntSD3Vld' )

	lRet := MntSD3Vld( cTM, aCols, n )

EndIf

cProcName:=Nil
oProdEmp:=Nil

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A241Data ³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 31/10/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a data em relacao a data do Ultimo fechamento       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241Data()
Local dDataFec := MVUlmes()
Local lRet:=.T.,lBackRet:=.T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a existencia do ponto de Entrada MTA241I                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lMta241I := (ExistBlock("MTA241I"))
If dDataFec >= &(ReadVar())
	Help ( " ", 1, "FECHTO" )
	lRet:=.F.
EndIf
If lRet .And. lMta241I
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa Ponto de Entrada                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lBackRet:=lRet
	lRet:=ExecBlock("MTA241I",.F.,.F.)
	If ValType(lRet) != "L"
		lRet:=lBackRet
	EndIf
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MTA241PERG³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 29/11/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao PERGUNTE                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA241PERG()
Pergunte("MTA240",.T.)
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ ShowF4   ³ Autor ³ Fernando Joly Siquini ³ Data ³ 13/04/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada da funcao F4                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function ShowF4(a,b,c)
If AllTrim(Upper(ReadVar())) $ 'M->D3_CODúM->D3_QUANT'
	MaViewSB2(Eval(bCols,n,nPosCod))
ElseIf AllTrim(Upper(ReadVar())) $ 'M->D3_NUMLOTEúM->D3_LOTECTL'
	nPos241Qtd := nPosQuant
	if nPosLocali > 0
		F4Lote(,,,   'A241',Eval(bCols,n,nPosCod), Eval(bCols,n,nPosLocal),NIL, Eval(bCols,n,nPosLocali),,Eval(bCols,n,nPosOp))
	Else
		F4Lote(,,,   'A241',Eval(bCols,n,nPosCod), Eval(bCols,n,nPosLocal),NIL, NIL)
	EndIf
ElseIf AllTrim(Upper(ReadVar())) == 'M->D3_LOCALIZ' .Or. AllTrim(Upper(ReadVar())) == 'M->D3_NUMSERI'
	// Verifica campos necessarios p/ Localizacao
	If Empty(nPosLocali)
		UserException("D3_LOCALIZ"+OemToAnsi(STR0021))
	EndIf
	nPos241Loc := nPosLocali
	F4Localiz(,,,   'A241', Eval(bCols,n,nPosCod), Eval(bCols,n,nPosLocal), Eval(bCols,n,nPosQuant), ReadVar(),,Eval(bCols,n,nPosOp))
EndIf
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MTA241OK  ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 04/12/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Confirmacao antes de executar o estorno.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MTA241OK                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA241OK()
Return (MsgYesNo(OemToAnsi(STR0013),OemToAnsi(STR0014))) //"Confirma o estorno da movimentação ?"###"Atenção"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o   ³MT241ESTOK³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 04/12/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o³ Confirmacao antes de executar o estorno.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MTA241OK                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MTA241ESTOK(lIntegDef)
Local lRet := .T.
Local cValInt
Local cValExt

DEFAULT lIntegDef := FWHasEAI("MATA241")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Este ponto de entrada permite validar dados especificos³
//³ no usuario no momento do Estorno.                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. ExistBlock('MT241EXT')
	lRet := ExecBlock('MT241EXT', .F., .F.)
	If ValType(lRet) # "L"
		lRet := .T.
	EndIf
EndIf

// Se o movimento foi originado pela integracao com o PIMS
If lRet .And. lIntegDef .And. !l241Auto .AND. !Empty(SD3->D3_NRBPIMS)
	// Tratamento para garantir que o campo foi preenchido via produto PIMS
	// Pois antes disso o campo D3_NRBPIMS indiferente do produto era atribuido valor
	cValInt := cEmpAnt + '|' + RTrim(xFilial('SD3')) + '|' + RTrim(SD3->D3_DOC) + '|' + RTrim(SD3->D3_NUMSEQ)
	cValExt := CFGA070Ext("PIMS", "SD3", "D3_NUMSEQ", cValInt)

	If !Empty(cValExt)
		Alert(STR0045) //"A integração PROTHEUS x PIMS está habilitada, o estorno desse movimento deverá ser feito pelo PIMS."
		lRet:= .F.
	EndIf
EndIf

If lRet .And. !l241Auto
	lRet := MTA241OK()
EndIf

If !MaAvalPerm(2,{SD3->D3_TM})
	lRet := .F.
Endif

/*--------------------------+
| Validação SIGAMNT/SIGAGFR |
+--------------------------*/
If lRet .And. FindFunction( 'MntSD3Vld' )

	lRet := MntSD3Vld( SD3->D3_TM, aCols, , .T. )

EndIf

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241ValidCB ³Autor³ Alex-Sandro Val rio   ³ Data ³ 07/04/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria o Browser para codigo de barras                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A241ValidCB                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Codigo de Barras                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241ValidCB()
Local nOrdem
Local nCodBar   := 1
Local nOp       := 2
Local nCod      := 3
Local nDesc     := 4
Local nQtde     := 5
Local nAt
Local lOk       := .F.
Local cBuffer
nAt     := N
nColuna := oget1:oBrowse:colpos
cBuffer := &(ReadVar())
If nColuna == nCodBar
	nOrdem:=SB1->(indexOrd())
	If ! Empty(cBuffer)
		SB1->(DbSetOrder(5))
		If SB1->(Dbseek(xFilial("SB1")+cBuffer))
			aCols[nAt,nCodBar ] := SB1->B1_CODBAR
			aCols[nAt,nCod    ] := SB1->B1_COD
			aCols[nAt,nDesc   ] := SB1->B1_DESC
			aCols[nAt,nQtde   ] := 1
			lok := .T.
		Else
			Help(" ",1,"A140HB")
			lOk := .F.
		EndIf
		SB1->(DbSetOrder(nOrdem))
	EndIf
Elseif  nColuna == nOP
	If ExistCpo("SC2")
		aCols[nAt,nOP ] := cBuffer
		lOk := .T.
	Else
		lOk := .F.
	EndIf
ElseIf  nColuna == nCod
	If Empty(cBuffer)
		ConPad1(,,,"SB1")
		&(ReadVar()) := SB1->B1_CODBAR
	EndIf
	nOrdem:=SB1->(indexOrd())
	SB1->(DbSetOrder(1))
	If SB1->(MsSeek(xFilial("SB1")+cBuffer))
		aCols[nAt,nCodBar ] := SB1->B1_CODBAR
		aCols[nAt,nCod    ] := SB1->B1_COD
		aCols[nAt,nDesc   ] := SB1->B1_DESC
		aCols[nAt,nQtde   ] := 1
		lOk := .T.
	Else
		Help(" ",1,"A140HB")
		lOk := .F.
	EndIf
	SB1->(DbSetOrder(nOrdem))
ElseIf nColuna == nDesc
	&(ReadVar()):=	aCols[nAt,nDesc]
	lOK := .T.
EndIf

If ! Empty(_aCtlHeader[nColuna,2]) .and. lOk
		_nProxCol := val(_aCtlHeader[nColuna,2])
		oget1:oBrowse:bEditCol :={ || oget1:oBrowse:nColPos := _nProxCol}
EndIf
Return lOK

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241BLinOk³ Autor ³ Claudinei M. Benzi    ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a linha digitada esta' Ok                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241BLinOk(o)
Local lRet := .T., lDeleted := .F.
// Verifica se linha do acols foi preenchida
If ValType(aCols[n,Len(aCols[n])]) == "L"  /// Verifico se posso Deletar
	lDeleted := aCols[n,Len(aCols[n])]      /// Se esta Deletado
EndIf
Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241BTudOk³ Autor ³ Claudinei M. Benzi    ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a nota toda esta' Ok                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241BTudOk(o)           // codigo de barras

Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241Check ³ Autor ³ Marcelo Iuspa         ³ Data ³ 25/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a digitacao continua ok apos confirmacao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MatA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A241Check()
Local lRet        := .T.
Local lRetOk      := .T.
Local nOrdSB8
Local nRecSB8
Local cLocal
Local i,nQtd      := 0
Local nLin
Local nSaldo      := 0
Local lBaixaEmp   := .F.
Local lBxEmpB8    := .F.
Local lQtdZero    := .F.
Local cTrt        := ""
Local lEmpPrj     := .F.
Local nQtdPrj     := 0
Local lEmpPrev    := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local cSeek	   	  := ""
Local lReferencia := .F.
Local lGrade      := MaGrade()
Local cPROJPMS    := ""
Local cTASKPMS    := ""
Local cPicB2QATU  := PesqPictQt("B2_QATU",14)
Local cPicB8Sld   := PesqPictQt("B8_SALDO", 14)
Local lWmsNew     := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local lPrdIntWms  := .F.
Local cLocProc	  := GetMvNNR('MV_LOCPROC','99')
Local cNumSeri    := ''
Local nQtd2UM     := 0
Local nQtdEmp     := 0
Local oProdEmp as Object
Local cChave      := ''
Local lBaixaSd4   := .F.
Local cMV_ESTNEG  as character
Local cMV_TPSALDO as character
Local lA241Data   as logical
Local nSdlD14     := 0
Local nSldDisp	  := 0
Local nQtdLnReg	  := 0
Local lWmsBxOp    := SuperGetMV("MV_WMSBXOP",.F.,.F.)

cMV_ESTNEG := GETMV("MV_ESTNEG")
cMV_TPSALDO := GetMv("MV_TPSALDO")
lA241Data := Type('dA241Data')=="D"

For nLin := 1 to Len(aCols)
	If aCols[nLin, Len(aCols[nLin])]
		Loop
	EndIf
	If lGrade
		lReferencia := MatGrdPrrf(aCols[nlin,nPosCod])
	EndIf
	lRet := .T.
	lPrdIntWms := IntWMS(aCols[nLin][nPosCod])
	If lRet .And. Inclui .And. !lReferencia
		dbSelectArea("SF5")
		If MsSeek(xFilial("SF5")+cTM)
			lQtdZero:=Empty(aCols[nLin,nPosQuant]) .And. SF5->F5_QTDZERO == "1"

			cPROJPMS := iIf(Empty(nPosProj), CriaVar("D3_PROJPMS"), aCols[nLin,nPosProj])
			cTASKPMS := iIf(Empty(nPosTarefa), CriaVar("D3_TASKPMS"), aCols[nLin,nPosTarefa])

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Caso esteja baixando empenho verifica o saldo empenhado³
			//³ a fim de evitar divergencia entre o movimento no SD3 e ³
			//³ os movimentos no SD5 e SDB.                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lEmpPrj  := ( !Empty(cPROJPMS) .And. ;
						  !Empty(cTASKPMS) )
			lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[nLin,nPosOP])) .OR. lEmpPrj)
			cTrt:=If(ValType(nPosTRT)=="N" .And. nPosTRT > 0,aCols[nLin,nPosTrt],CriaVar("D3_TRT"))
			lRet:=lRet .And. A240AvalEm(@lBaixaEmp,aCols[nLin,nPosCod],aCols[nLin,nPosLocal],aCols[nLin,nPosQuant],If(nPosLotCTL>0,aCols[nLin,nPosLotCTL],CriaVar("D3_LOTECTL")),If(nPosLote>0,aCols[nLin,nPosLote],CriaVar("D3_NUMLOTE")), If(nPosLocali>0,aCols[nLin,nPosLocali],CriaVar("D3_LOCALIZ")), If(nPosNumSer>0,aCols[nLin,nPosNumSer],CriaVar("D3_NUMSERI")), If(Empty(nPosOp),CriaVar("D3_OP"),aCols[nLin,nPosOp]),cTRT,.F.,cTM<="500",@lBxEmpB8,If(nPosPotenc>0,aCols[nLin,nPosPotenc],), cPROJPMS, cTASKPMS)
			If ValType(nPosTRT)=="N" .And. nPosTRT > 0 .and. lRet .and. lEmpPrj
				aCols[nLin,nPosTrt] := cTrt
			EndIf
		EndIf

		nQtdPrj := If(lBaixaEmp .And. lEmpPrj, aCols[nLin,nPosQuant], 0)

		If lRet
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Alerta ao usuario se a devolucao for maior que as      ³
			//³ requisicoes efetuadas para essa OP                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cTm <= "500" .And. !Empty(If(Empty(nPosOP),CriaVar("D3_OP"),aCols[nLin,nPosOP])) .And. !IsProdCoPR(aCols[nLin,nPosCod],aCols[nLin,nPosOP],aCols[nLin,nPosTRT]) //-- Desconsidera produtos coproduzidos (item negativo)
				nQtd2UM := IIf(nPosQtSegum > 0, aCols[nLin,nPosQtSegum], 0)
				lRet:=A240VlDeOP(aCols[nLin,nPosCod],aCols[nLin,nPosLocal],If(Empty(nPosOP),CriaVar("D3_OP"),aCols[nLin,nPosOP]),aCols[nLin,nPosQuant],nQtd2UM,aCols[nLin,nPosLotCTL],aCols[nLin,nPosLote])
			EndIf
		EndIf
		If lRet .and. (cMV_ESTNEG != "S" .Or. Localiza(aCols[nLin,nPosCod],.T.) .Or. Rastro(aCols[nLin,nPosCod])) .And. !IsProdMod(aCols[nLin][nPosCod])
			If cTM > "500"
				dbSelectArea("SB2")
				MsSeek(xFilial("SB2")+aCols[nLin][nPosCod]+aCols[nLin][nPosLocal])
				If !lQtdZero .And. QtdComp(aCols[nLin][nPosQuant]) > QtdComp(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,If(lBaixaEmp,aCols[nLin,nPosQuant],Nil),nQtdPrj,Nil,If(lA241Data,dA241Data,dDataBase),!l185)) + If(l185 .And. cMV_ESTNEG=="S",QtdComp(aCols[n][nPosQuant]),0)
					cHelp:=OemToAnsi(STR0016)+AllTrim(B2_COD)+OemToAnsi(STR0017)+B2_LOCAL+OemToAnsi(STR0018)+ALLTRIM(Transform(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,If(lBaixaEmp,aCols[nLin,nPosQuant],Nil),nQtdPrj,Nil,If(lA241Data,dA241Data,dDataBase)),cPicB2QATU))	// Produto Local Saldo Disp.
					Help(" ",1,"MA240NEGAT",,cHelp,4,1)
					lRet := .F.
				EndIf
				If Localiza(aCols[nLin,nPosCod],.T.)
					// Verifica campos necessarios p/ Localizacao
					If Empty(nPosLocali)
						UserException("D3_LOCALIZ"+OemToAnsi(STR0021))
					EndIf
					cNumSeri := IIf(nPosNumSer > 0, aCols[nLin,nPosNumSer], CriaVar('D3_NUMSERI'))
					If !(lPrdIntWms .And. !Empty(aCols[nLin,nPosServic]))
						If Empty(aCols[nLin,nPosLocali]) .And. Empty(cNumSeri) .And. !(SF5->F5_QTDZERO=="1" .And. QtdComp(aCols[nLin][nPosQuant]) == QtdComp(0))
							Help(" ",1,"LOCALIZOBR")
							lRet:=.F.
						EndIf
					EndIf
					If lRet .And. (!Empty(aCols[nLin,nPosLocali]) .Or. !Empty(cNumSeri))
						If !(lWmsNew .And. lPrdIntWms)
							/*
								Alterada chamada da função SaldoSBF para passar o número da OP.
								Necessário para o projeto de integração TOTVS MES.
							*/
							If QtdComp(SaldoSBF(aCols[nLin,nPosLocal],aCols[nLin,nPosLocali],aCols[nLin,nPosCod],cNumSeri,aCols[nLin,nPosLotCTL],aCols[nLin,nPosLote],lBxEmpB8,,,aCols[nLin,nPosOp])) < QtdComp(aCols[nLin,nPosQuant])
								cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+AllTrim(aCols[nLin,nPosCod])+OemToAnsi(STR0019)+aCols[nLin,nPosLocali]
								Help(" ",1,"SALDOLOCLZ",, cHelp,5,1)
								lRet:=.F.
							EndIf
						Else
							//Executa SEMPRE que for WMS novo com produto WMS
							If QtdComp(nSdlD14 := WmsSldD14(aCols[nLin,nPosLocal],aCols[nLin,nPosLocali],aCols[nLin,nPosCod],aCols[nLin,nPosNumSer],aCols[nLin,nPosLotCTL],aCols[nLin,nPosLote],lBxEmpB8)) < QtdComp(aCols[nLin,nPosQuant])
								If !Empty(aCols[nLin,nPosServic]) .Or. !lWmsBxOp
									cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+AllTrim(aCols[nLin,nPosCod])+OemToAnsi(STR0019)+aCols[nLin,nPosLocali]
									Help(" ",1,"SALDOLOCLZ",, cHelp,5,1)
									lRet:=.F.
								ElseIf lWmsBxOp .And. FindFunction("WMS241SlOp") .And. !WMS241SlOp(cTM,aCols[nLin,nPosCod],aCols[nLin,nPosOp],aCols[nLin,nPosServic],aCols[nLin,nPosLocal],aCols[nLin,nPosLocali],aCols[nLin,nPosLotCTL],aCols[nLin,nPosLote],aCols[nLin,nPosQuant],aCols[nLin,nPosTrt],aCols[nLin,nPosLocali],nSdlD14)
									lRet := .F.
								EndIf
							EndIf
						EndIf
					EndIf
				EndIf
			Else
				dbSelectArea("SF5")
				MsSeek(xFilial("SF5")+cTM)
				dbSelectArea("SB1")
				MsSeek(xFilial("SB1")+aCols[nLin,nPosCod])
				If (B1_APROPRI == "I" .And. Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[nLin,nPosOp]))) .And. SF5->F5_APROPR != "S"
					cApropri := "3"
				Else
					cApropri := "0"
				EndIf
				If cApropri == "3"
					dbSelectArea("SB2")
					If MsSeek(xFilial("SB2")+aCols[nLin,nPosCod] + cLocProc)
						If cApropri == "3" .And. (QtdComp(SaldoMov(Nil,!lBaixaEmp,Nil,mv_par03==1,Nil,Nil,Nil,If(lA241Data,dA241Data,dDataBase))) < QtdComp(aCols[nLin,nPosQuant]))
							Help(" ",1,"MA240PRNEG",,AllTrim(B2_COD) + "-"+B2_LOCAL,4,1)
							lRet:=.F.
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf

		If lRet .And. Rastro(aCols[nLin][nPosCod]) .And. !lQtdZero
			// Verifica campos necessarios p/ Rastreabilidade
			If Empty(nPosLotCTL)
				UserException("D3_LOTECTL"+OemToAnsi(STR0021))
			EndIf
			If !IsProdMod(aCols[nLin][nPosCod])
				cLocal := aCols[nLin][nPosLocal]
				If  cTM <= "500" .and. SF5->F5_APROPR != "S" .And. SB1->B1_APROPRI == "I"
					cLocal := cLocProc
				EndIf
				If  cTM > "500" .And. !(lPrdIntWms .And. !Empty(aCols[nLin,nPosServic]))
					dbSelectArea("SB8")
					nOrdSB8 := IndexOrd()
					nRecSB8 := RecNo()
					If Rastro(aCols[nLin][nPosCod],"S")
						dbSetOrder(2)
						If dbSeek(xFilial("SB8")+aCols[nLin][nPosLote]+aCols[nLin][nPosLotCTL]+aCols[nLin][nPosCod]+cLocal)
							nQtd:=0
							nQtdEmp := 0
							oProdEmp := JsonObject():New()
							For i:=1 to Len(aCols)
								If aCols[i][nPosLotCTL]+aCols[i][nPosLote]+aCols[i][nPosCod] == aCols[nLin][nPosLotCTL]+aCols[nLin][nPosLote]+aCols[nLin][nPosCod] .And. !aCols[i][Len(aCols[i])]
									If aCols[i, nPosLocal] == cLocal
										lBaixaSd4 := .F.
										lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOP])) .OR. lEmpPrj)

										cChave := aCols[i,nPosCod];
													+aCols[i,nPosLocal];
													+cValToChar(aCols[i,nPosQuant]);
													+If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]);
													+If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]);
													+If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]);
													+If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]);
													+If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]);
													+If(nPosPotenc>0,cValToChar(aCols[i,nPosPotenc]),"");

										If oProdEmp[cChave] == NIL
											A240AvalEm(lBaixaEmp,aCols[i,nPosCod],aCols[i,nPosLocal],aCols[i,nPosQuant],If(nPosLotCTL>0,aCols[i,nPosLotCTL],CriaVar("D3_LOTECTL")),If(nPosLote>0,aCols[i,nPosLote],CriaVar("D3_NUMLOTE")), If(nPosLocali>0,aCols[i,nPosLocali],CriaVar("D3_LOCALIZ")), If(nPosNumSer>0,aCols[i,nPosNumSer],CriaVar("D3_NUMSERI")), If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOp]),cTRT,.F.,cTM<="500",@lBaixaSd4,If(nPosPotenc>0,aCols[i,nPosPotenc],), cPROJPMS, cTASKPMS)
											oProdEmp[cChave] := lBaixaSd4
										Else
											lBaixaSd4 := oProdEmp[cChave]
										EndIf

										If !lBaixaSd4
											nQtd+=aCols[i][nPosQuant]
										Else
											nQtdEmp += aCols[i][nPosQuant]
										EndIf
									EndIf
								EndIf
							Next i
							If lRet
								nSaldo := 0
								If nQtdEmp > 0
									nSaldo:=SB8Saldo(.T.,nil,nil,nil,nil,.F.,nil,dA241Data)
								EndIf
								If QtdComp(nSaldo) < QtdComp(nQtdEmp)
									cHelp:=OemToAnsi(STR0016)+AllTrim(B8_PRODUTO)+OemToAnsi(STR0017)+B8_LOCAL+OemToAnsi(STR0018)+ALLTRIM(Transform(SB8Saldo(nil,nil,nil,nil,nil,lEmpPrev,nil,dA241Data),cPicB8Sld))+OemToAnsi(STR0019)+aCols[nLin][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
									Help(" ",1,"A240LOTENE",,cHelp,4,1)
									lRet := .F.
								EndIf
							EndIf
						Else
							cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+aCols[nLin][nPosLocal]+OemToAnsi(STR0019)+aCols[nLin][nPosLotCTL] // Produto#Local#Lote
							Help(" ",1,"A240LOTERR",, cHelp, 5, 1)
							lRet := .F.
						EndIf
					Else
						dbSetOrder(3)
						cSeek:=xFilial("SB8")+aCols[nLin][nPosCod]+cLocal+aCols[nLin][nPosLotCTL]
						If dbSeek(cSeek)
							nQtd:=0
							nQtdEmp := 0
							nQtdLnReg := 0
							oProdEmp := JsonObject():New()
							For i:=1 to Len(aCols)
								If aCols[i][nPosLotCTL]+aCols[i][nPosCod] == aCols[nLin][nPosLotCTL]+aCols[nLin][nPosCod] .And. !aCols[i][Len(aCols[i])]
									If aCols[i, nPosLocal] == cLocal
										lBaixaSd4 := .F.
										lBaixaEmp:=SF5->F5_ATUEMP == "S" .And. ( !Empty(If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOP])) .OR. lEmpPrj)

										cChave := aCols[i,nPosCod];
													+aCols[i,nPosLocal];
													+cValToChar(aCols[i,nPosQuant]);
													+If(Empty(nPosLotCTL),CriaVar("D3_LOTECTL"),aCols[i,nPosLotCTL]);
													+If(Empty(nPosLote),CriaVar("D3_NUMLOTE"),aCols[i,nPosLote]);
													+If(Empty(nPosLocali),CriaVar("D3_LOCALIZ"),aCols[i,nPosLocali]);
													+If(Empty(nPosNumSer),CriaVar("D3_NUMSERI"),aCols[i,nPosNumSer]);
													+If(Empty(nPosOP),CriaVar("D3_OP"),aCols[i,nPosOp]);
													+If(nPosPotenc>0,cValToChar(aCols[i,nPosPotenc]),"");

										If oProdEmp[cChave] == NIL
											A240AvalEm(lBaixaEmp,aCols[i,nPosCod],aCols[i,nPosLocal],aCols[i,nPosQuant],If(nPosLotCTL>0,aCols[i,nPosLotCTL],CriaVar("D3_LOTECTL")),If(nPosLote>0,aCols[i,nPosLote],CriaVar("D3_NUMLOTE")), If(nPosLocali>0,aCols[i,nPosLocali],CriaVar("D3_LOCALIZ")), If(nPosNumSer>0,aCols[i,nPosNumSer],CriaVar("D3_NUMSERI")), If(Empty(nPosOp),CriaVar("D3_OP"),aCols[i,nPosOp]),If(Empty(nPosTrt),CriaVar("D3_TRT"),aCols[i,nPosTrt]),.F.,cTM<="500",@lBaixaSd4,If(nPosPotenc>0,aCols[i,nPosPotenc],), cPROJPMS, cTASKPMS)
											oProdEmp[cChave] := lBaixaSd4
										Else
											lBaixaSd4 := oProdEmp[cChave]
										EndIf

										If !lBaixaSd4
											nQtd+=aCols[i][nPosQuant]
										Else
											nQtdEmp += aCols[i][nPosQuant]
											nQtdLnReg++
										EndIf
									EndIf
								EndIf
							Next i
							If nQtd > 0
								nSaldo:=SaldoLote(aCols[nLin][nPosCod],cLocal,aCols[nLin][nPosLotCTL],NIL,.F.,.T.,nil,dA241Data,aCols[nLin][nPosOp])
							EndIf
							If QtdComp(nSaldo) < QtdComp(nQtd)
								cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+aCols[nLin][nPosLocal]+OemToAnsi(STR0018)+ALLTRIM(Transform(nSaldo,cPicB8Sld))+OemToAnsi(STR0019)+aCols[nLin][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
								Help(" ",1,"A240LOTENE",,cHelp,4,1)
								lRet := .F.
							EndIf
							If lRet
								nSaldo := 0
								If nQtdEmp > 0
									nSldDisp	:= SaldoLote(aCols[nLin][nPosCod],cLocal,aCols[nLin][nPosLotCTL],NIL,.F.,.T.,nil,dA241Data,aCols[nLin][nPosOp])
									nSaldo		:= SaldoLote(aCols[nLin][nPosCod],cLocal,aCols[nLin][nPosLotCTL],NIL,.T.,.T.,nil,dA241Data,aCols[nLin][nPosOp],,.F.)

									If nQtdEmp > nSaldo .And. nQtdLnReg > 1
										nSaldo := (nSldDisp - nSaldo)

										If QtdComp(nSaldo) >= QtdComp(nQtdEmp)
											If Empty(AScan( __aEmpSupOP, aCols[nLin,nPosCod]+aCols[nLin,nPosLocal]+If(Empty(nPosOp),CriaVar("D3_OP"),aCols[nLin,nPosOp])+If(Empty(nPosTrt),CriaVar("D3_TRT"),aCols[nLin,nPosTrt])+If(nPosLotCTL>0,aCols[nLin,nPosLotCTL],CriaVar("D3_LOTECTL"))+If(nPosLote>0,aCols[nLin,nPosLote],CriaVar("D3_NUMLOTE"))+If(nPosLocali>0,aCols[nLin,nPosLocali],CriaVar("D3_LOCALIZ"))+If(nPosNumSer>0,aCols[nLin,nPosNumSer],CriaVar("D3_NUMSERI")) ) )
												aAdd(__aEmpSupOP, aCols[nLin,nPosCod]+aCols[nLin,nPosLocal]+If(Empty(nPosOp),CriaVar("D3_OP"),aCols[nLin,nPosOp])+If(Empty(nPosTrt),CriaVar("D3_TRT"),aCols[nLin,nPosTrt])+If(nPosLotCTL>0,aCols[nLin,nPosLotCTL],CriaVar("D3_LOTECTL"))+If(nPosLote>0,aCols[nLin,nPosLote],CriaVar("D3_NUMLOTE"))+If(nPosLocali>0,aCols[nLin,nPosLocali],CriaVar("D3_LOCALIZ"))+If(nPosNumSer>0,aCols[nLin,nPosNumSer],CriaVar("D3_NUMSERI")))
											EndIf
										EndIf
									EndIf
								EndIf
								If QtdComp(nSaldo) < QtdComp(nQtdEmp)
									cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+aCols[nLin][nPosLocal]+OemToAnsi(STR0018)+ALLTRIM(Transform(nSaldo,cPicB8Sld))+OemToAnsi(STR0019)+aCols[nLin][nPosLotCTL] // Produto#Local#Saldo Disp.#Lote
									Help(" ",1,"A240LOTENE",,cHelp,4,1)
									lRet := .F.
								EndIf
							EndIf
						Else
							cHelp:=OemToAnsi(STR0016)+AllTrim(aCols[nLin][nPosCod])+OemToAnsi(STR0017)+aCols[nLin][nPosLocal]+OemToAnsi(STR0019)+aCols[nLin][nPosLotCTL] // Produto#Local#Lote
							Help(" ",1,"A240LOTERR",,cHelp,5,1)
							lRet := .F.
						EndIf
					EndIf
					dbSetOrder(nOrdSB8)
					MsGoto(nRecSB8)
				EndIf
			EndIf
		EndIf
	EndIf
	lRetOk := If(! lRet, .F., lRetOk)
Next

Return(lRetOk)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241VerSDG³ Autor ³Patricia A. Salomao     ³ Data ³20.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se foi informado Rateio de Veiculo/Viagem ou Rateio ³±±
±±³          ³de Frota para o Item                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Numero de Sequencia	(D3_NUMSEQ)                    ³±±
±±³          ³ ExpN1 = Tamanho do aCols                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function a241VerSDG(cNumSeq,nTamACols)

Local cItem    := ""
Local aAreaSDG := SDG->(GetArea())
Local nPos     := 0
Local lIdent   := nModulo<>43
DEFAULT nTamACols := Len(aCols)

If IntTMS()
	SDG->(dbSetOrder(7))
	If SDG->(MsSeek(xFilial("SDG")+"SD3"+cNumSeq))
		cItem	:= StrZero(nTamACols,Len(SDG->DG_ITEM))
		If Empty(SDG->DG_CODVEI) .And. Empty(SDG->DG_VIAGEM) //Verifica se o Rateio foi por Veiculo/Viagem ou por Frota
			aadd(aRatFro,{cItem,{},SDG->DG_CODDES})
		Else
			aadd(aRatVei,{cItem,{},SDG->DG_CODDES})
		EndIf
		While SDG->(!Eof()) .And. SDG->DG_FILIAL + SDG->DG_ORIGEM + SDG->DG_SEQMOV == xFilial("SD3") + "SD3" + cNumSeq
			If Empty(SDG->DG_CODVEI) .And. Iif( lIdent, Empty(SDG->DG_IDENT), Empty(SDG->DG_VIAGEM)) //Verifica se o Rateio foi por Veiculo/Viagem ou por Frota
				aadd(aRatFro[Len(aRatFro)][2],{SDG->DG_ITEM, SDG->DG_PERC,.F.})
			Else
				If ( nPos := Ascan(aRatVei[Len(aRatVei)][2], { |x| x[2] == SDG->DG_CODVEI } ) ) == 0
					aadd(aRatVei[Len(aRatVei)][2],{SDG->DG_ITEM,SDG->DG_CODVEI,SDG->DG_FILORI,SDG->DG_VIAGEM, SDG->DG_TOTAL,"",0,0,.F.})
				EndIf
			EndIf
			SDG->(dbSkip())
		EndDo
	EndIf
	RestArea( aAreaSDG )
EndIf

Return .T.

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³M241SeleEs³ Autor ³Rodrigo de A Sartorio   ³ Data ³21.10.2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Seleciona estrutura como base para preenchimento automatico  ³±±
±±³          ³de produtos a serem requisitados.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function M241SeleEs()
Local aEstrutura   := {}
Local i            := 0
Local nx           := 0
Local oDlg
Local lOk          := .F.
Local nPercOP      := 0
Local lSugSemSld   := .F.
Local aColsPE      := {}
Local cLocaliz     := Criavar("D3_LOCALIZ",.F.)
Local cNumSeri     := Criavar("D3_NUMSERI",.F.)
Local lItemNeg     := .F.

Private nQtdOrigEs := 1
Private cProdEst   := Criavar("D3_COD",.F.)
Private cOpEst     := Criavar("D3_OP",.F.)
Private nEstru     := 0

DEFINE MSDIALOG oDlg FROM  140,000 TO 300,400 TITLE OemToAnsi(STR0024) PIXEL //"Informe produto com estrutura"
@ 10,15 TO 63,185 LABEL Alltrim(RetTitle("D3_OP"))+" - "+Alltrim(RetTitle("D3_COD"))+" - "+Alltrim(RetTitle("D3_QUANT")) OF oDlg PIXEL
@ 20,20 MSGET cOpEst F3 "SC2" Picture PesqPict("SD3","D3_OP") When(oSugerSld:lVisible := .T.,SysRefresh(),.T.) Valid ((Vazio() .Or. ExistCpo("SC2",cOpEst)) .And. A241IniOP()) SIZE 70,9 OF oDlg PIXEL
@ 35,20 MSGET cProdEst F3 "SB1" Picture PesqPict("SD3","D3_COD") When( lSugSemSld := If(Empty(cOpEst),.F.,lSugSemSld),oSugerSld:lVisible:=!Empty(cOpEst),SysRefresh(),.T.) Valid (NaoVazio() .And. ExistCpo("SB1",cProdEst)) SIZE 70,9 OF oDlg PIXEL
@ 35,95 MSGET nQtdOrigEs Picture PesqPict("SD3","D3_QUANT") Valid (Positivo() .And. NaoVazio()) SIZE 60,9 OF oDlg PIXEL
@ 50,20 CHECKBOX oSugerSld Var lSugSemSld PROMPT OemtoAnsi( STR0030 )	SIZE 150,10 OF oDlg PIXEL
DEFINE SBUTTON FROM 67,131 TYPE 1 ACTION (oDlg:End(),lOk:=.T.) ENABLE OF oDlg
DEFINE SBUTTON FROM 67,158 TYPE 2 ACTION (oDlg:End(),lOk:=.F.) ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTERED ON INIT(oSugerSld:lVisible := .T.)

If lOk .And. !Empty(cProdEst) .And. nQtdOrigEs > 0
	// Le os empenhos caso a op esteja preenchida
	If !Empty(cOPEst)
		dbSelectArea("SC2")
		dbSetOrder(1)
		dbSeek(xFilial("SC2")+cOpEst)
		// Calcula qual o percentual da baixa de insumos
		nPercOp:=nQtdOrigEs/SC2->C2_QUANT

		If cTm > "500"
			A241GetEmp(@aEstrutura,lSugSemSld)
		Else
			SD4->(dbSetOrder(2))
			SD4->(dbSeek(xFilial("SD4")+cOpEst))
			While !SD4->(Eof()) .And. SD4->(D4_FILIAL+D4_OP)==xFilial("SD4")+cOpEst
				If !lSugSemSld .AND. QtdComp(SD4->D4_QUANT)==QtdComp(0)
					SD4->(dbSkip())
					Loop
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//| Produtos de apropriacao indireta, quando ja possuem saldo no armazem de processo |
				//| a baixa e automatica pela producao, quando ainda nao possuem saldo no armazem de |
				//| de processo devera ser feita requisicao manual do armazem padrao.                |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If A260ApropI(SD4->D4_COD,cTm)
					SD4->(dbSkip())
					Loop
				EndIf
				AADD(aEstrutura,{NIL,NIL,SD4->D4_COD,Min(SD4->D4_QTDEORI*nPercOp,SD4->D4_QTDEORI-SD4->D4_QUANT),SD4->D4_TRT,SD4->D4_LOTECTL,SD4->D4_NUMLOTE,cLocaliz,cNumSeri,SD4->D4_LOCAL,SD4->D4_DTVALID})
				SD4->(dbSkip())
			End
		EndIf
	// Se nao preencheu OP le somente a estrutura
	Else
		// Explode o 1 nivel da estrutura
		If FindFunction("aCusbExpld")
			aCusbExpld(cProdEst,nQtdOrigEs,@aEstrutura)
		Else
			Help(,,"OUTDATEDSOURCE",,STR0054, 1, 0) // "O Fonte SIGACUSB.PRX encontra-se desatualizado."
			aEstrutura := {}
		EndIf
	EndIf
	// Le somente os itens de primeiro nivel
	If Len(aEstrutura) > 0
		For i:=1 to Len(aEstrutura)
			// Se a primeira linha esta em branco remove a mesma do acols
			If Empty(aCols[1,nPosCod])
				ADEL(aCols,1)
				ASIZE(aCols,Len(aCols)-1)
			EndIf
			// Adiciona item no acols
			AADD(aCols,Array(Len(aHeader)+1))
			// Preenche conteudo do acols
			For nx:=1 to Len(aHeader)
				cCampo:=Alltrim(aHeader[nx,2])
				If IsHeadRec(cCampo)
					aCols[Len(aCols)][nx] := 0
				ElseIf IsHeadAlias(cCampo)
					aCols[Len(aCols)][nx] := "SD3"
				Else
					aCols[Len(aCols)][nx] := CriaVar(cCampo,.F.)
				EndIf
			Next nx

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Tratamento para marcar como deletados os itens com quantidade negativa na ³
			//³estrutura ou positiva-los, de acordo com o tipo de movimentacao RE/DE.    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If QtdComp(aEstrutura[i,4]) <= QtdComp(0)
				lItemNeg := .T. // variavel para exibicao de aviso ao usuario
				If cTm > "500"
					// Requisicoes
					If QtdComp(aEstrutura[i,4]) <= QtdComp(0)
						// Quantidade negativa ou zerada: deve ser deletado, pois nao se pode requisitar quantidade negativa ou zerada
						aCOLS[Len(aCols)][Len(aHeader)+1] := .T.
					EndIf
				Else
					// Devolucoes
					If QtdComp(aEstrutura[i,4]) < QtdComp(0)
						// Quantidade negativa: deve ser positivada, pois trata-se de devolucao
						aCOLS[Len(aCols)][Len(aHeader)+1] := .F.
						aEstrutura[i,4] := (aEstrutura[i,4] * -1)
					Else
						// Quantidade zerada: deve ser deletado, pois nao se pode devolver quantidade zerada
						aCOLS[Len(aCols)][Len(aHeader)+1] := .T.
					EndIF
				EndIf
			Else
				// Item com quantidade positiva > 0
				aCOLS[Len(aCols)][Len(aHeader)+1] := .F.
			EndIf

			// Preenche campos especificos
			SB1->(dbSetOrder(1))
			SB1->(MsSeek(xFilial("SB1")+aEstrutura[i,3]))
			GDFieldPut("D3_COD",aEstrutura[i,3],Len(aCols))
			GDFieldPut("D3_DESCRI",SB1->B1_DESC,Len(aCols))
			GDFieldPut("D3_UM",SB1->B1_UM,Len(aCols))
			GDFieldPut("D3_SEGUM",SB1->B1_SEGUM,Len(aCols))
			GDFieldPut("D3_QUANT",aEstrutura[i,4],Len(aCols))
			GDFieldPut("D3_LOCAL",RetFldProd(SB1->B1_COD,"B1_LOCPAD"),Len(aCols))
			GDFieldPut("D3_CONTA",SB1->B1_CONTA,Len(aCols))
			GDFieldPut("D3_ITEMCTA",SB1->B1_ITEMCC,Len(aCols))
			// Preenche o numero da OP
			If !Empty(cOpEst)
				If aEstrutura[i,10]# NIL .AND. !Empty(aEstrutura[i,10])
					GDFieldPut("D3_LOCAL",aEstrutura[i,10],Len(aCols))
				EndIf
				GDFieldPut("D3_OP",cOpEst,Len(aCols))
				GDFieldPut("D3_TRT",aEstrutura[i,5],Len(aCols))
				GDFieldPut("D3_LOTECTL",aEstrutura[i,6],Len(aCols))
				GDFieldPut("D3_NUMLOTE",aEstrutura[i,7],Len(aCols))
				GDFieldPut("D3_DTVALID",aEstrutura[i,11],Len(aCols))
				If ! Empty(cTM) .and. SF5->F5_TIPO=="D"
					GDFieldPut("D3_LOCALIZ",Space(15),Len(aCols))
					GDFieldPut("D3_NUMSERI",Space(20),Len(aCols))
				Else
					GDFieldPut("D3_LOCALIZ",aEstrutura[i,8],Len(aCols))
					GDFieldPut("D3_NUMSERI",aEstrutura[i,9],Len(aCols))
				EndIf
			EndIf
			// Executa gatilhos e validacoes
			If ExistTrigger("D3_COD")
				RunTrigger(2,Len(aCols),,,"D3_COD")
			EndIf
			If ExistTrigger("D3_QUANT")
				RunTrigger(2,Len(aCols),,,"D3_QUANT")
			EndIf
			If ExistTrigger("D3_LOCAL")
				RunTrigger(2,Len(aCols),,,"D3_LOCAL")
			EndIf
			If ExistTrigger("D3_OP")
				RunTrigger(2,Len(aCols),,,"D3_OP")
			EndIf
		Next i
	EndIf
EndIf

If lItemNeg
	If cTM > '500'
		Aviso(STR0014, STR0043, {"OK"})
	Else
		Aviso(STR0014, STR0044, {"OK"})
	EndIf
EndIf

If ExistBlock('MT241SE')
	aColsPE := Execblock('MT241SE', .F., .F., aCols)
	If Valtype(aColsPE) == 'A'
		aCols := aClone(aColsPE)
	EndIf
EndIf

//-- Forca atualizacao da GetDados
oGet:Refresh()

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241IniOP ³ Autor ³Rodrigo de A Sartorio   ³ Data ³21.10.2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializa informacoes da Ordem de Producao apos digitar OP  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A241IniOP()
Local lRet := .T.
// Inicializa produto e quantidade
If !Empty(cOPEst) .And. SC2->(MsSeek(xFilial("SC2")+cOpEst))
	If !Empty(SC2->C2_DATRF)
		Help(" ",1,"A250ENCERR")
		lRet := .F.
	ElseIf SC2->C2_TPOP == "P"
		Help(" ",1,"NOPPREVIST")
		lRet := .F.
	Else
		If SC2->C2_QUANT-SC2->C2_QUJE < 0
			nQtdOrigEs := 0
		Else
			nQtdOrigEs := SC2->C2_QUANT-SC2->C2_QUJE
		EndIf

		If ExistBlock("MTA241QTD")
			nQtdOrigEs := ExecBlock("MTA241QTD",.F.,.F.,{nQtdOrigEs})
		EndIf

		cProdEst:=SC2->C2_PRODUTO
	EndIf
EndIf
Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A241ChkSX3³ Autor ³Marcos V. Ferreira      ³ Data ³25.08.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida o campo de acordo com o X3_VALID                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Campo a ser validado de acordo com o X3_VALID       ³±±
±±³          ³ ExpC2 = Se .T. utilizar inicializador padrao                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function A241ChkSX3(cCampo,cIniVar)
Local lRet	:= .T.
Local cVar	:= Nil
Local cValid:= Nil
Local aArea	:= GetArea()
Default cIniVar	:= ''

cVar	:= "M->" + cCampo
cValid 	:= IIf( !Empty( GetSx3Cache( cCampo, "X3_VALID" ) ), AllTrim( GetSx3Cache( cCampo, "X3_VALID" ) ), "" ) + IIf( !Empty( GetSx3Cache( cCampo, "X3_VLDUSER" ) ), " .And. " + AllTrim( GetSx3Cache( cCampo, "X3_VLDUSER" ) ), "" )

If Empty( cIniVar )
	&cVar  := &(ReadVar())
Else
	&cVar	 := cIniVar
EndIf

If !Empty( cValid )
	lRet := &( cValid )
EndIf

RestArea( aArea )
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241AfterH³ Autor ³ Ricardo Berti         ³ Data ³ 29/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao executada APOS gerar aHeader, antes da FillGetDados ³±±
±±³          ³ criar os campos do Walk-Thru, para adicao de novos campos  ³±±
±±³          ³ neste.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ a241AfterH(ExpN1)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array aHeader passado por ref.                     ³±±
±±³          ³ ExpN1 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a241AfterH(aHeaderX,nOpc)
Local nInd		:= 0
Local aAuxHead	:= {}
Local lAdD3Ident:= ( Type( "lSeloFiscal" ) == "L" .And. lSeloFiscal )
Local aVldCpo	:= { { "D3_LOTECTL", .T. }, { "D3_DESCRI", .T. }, { "D3_IDENT", lAdD3Ident } }
Local aCpos 	:= { "", "X3_CAMPO", "X3_PICTURE", "X3_TAMANHO", "X3_DECIMAL", "X3_VALID", "X3_USADO", "X3_TIPO", "X3_ARQUIVO", "X3_CONTEXT" }
Local bGetSx3	:= { | x,y | GetSx3Cache( x, y ) }

If nOpc == 3 .Or. l185	// Inclusao
	If l241Auto
		For nInd := 1 To Len( aVldCpo )
			If aVldCpo[ nInd ][ 2 ]
				aAuxHead := {}
				If Ascan(aHeaderX,{|x| Alltrim(x[2] )== aVldCpo[ nInd ][ 1 ] } ) == 0 .And. !Empty( Eval( bGetSx3, aVldCpo[ nInd ][ 1 ], "X3_TITULO" ) )
					AEval( aCpos, { | x | Aadd( aAuxHead, IIf( Empty( x ), AllTrim( X3Titulo() ), Eval( bGetSx3, aVldCpo[ nInd ][ 1 ], x ) ) ) } )
					Aadd( aHeaderX, AClone( aAuxHead ) )
				EndIf
			EndIf
		Next nInd
	EndIf

	If l185
		If Ascan(aHeaderX,{|x| Alltrim(x[2])=="D3_NUMSA"}) == 0
			AADD(aHeaderX,{ STR0031	, "D3_NUMSA"	, "@!",	10, 0, Nil, Nil, "C", "SD3", Nil }) //"Numero SA"
		EndIf
		If Ascan(aHeaderX,{|x| Alltrim(x[2])=="D3_ITEMSA"}) == 0
			AADD(aHeaderX,{ STR0032	, "D3_ITEMSA"	, "@!",	10, 0, Nil, Nil, "C", "SD3", Nil }) //"Item SA"
		EndIf
	EndIf
EndIf

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a241AfterC³ Autor ³ Ricardo Berti         ³ Data ³ 22/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Tratamento de excecoes na montagem automatica do aCols pela³±±
±±³          ³ FillGetDados, executada APOS gerar cada elemento do aCols. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpL1:=a241AfterC(ExpN1,ExpA1,ExpC1,ExpA2,ExpA3,ExpA4,ExpL1)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Numero da opcao selecionada                        ³±±
±±³          ³ ExpA1 = aCols (passado por ref.)                           ³±±
±±³          ³ ExpC1 = Alias do arq. principal                            ³±±
±±³          ³ ExpA2 = Array c/ dados de SD3 p/ tratamento de Dead-Lock   ³±±
±±³          ³ ExpA3 = Array c/ dados de SB2 p/ tratamento de Dead-Lock   ³±±
±±³          ³ ExpA4 = Array c/ dados de SB3 p/ tratamento de Dead-Lock   ³±±
±±³          ³ ExpL1 = Variavel que indica se o movimento esta sem docum. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 = .F. aborta a FillGetDados (montagem do aCols)  	  ³±±
±±³          ³         .T. continua a montagem do aCols pela FillGetDados ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA241                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function a241AfterC(nOpc,aColsX,cAlias,aLockSB2,aLockSB3,aLockSD3)

Local nPosDesc :=0
Local nPosCod	 :=0
Local nPos		 :=0

DEFAULT aLockSD3 := {}
DEFAULT aLockSB2 := {}
DEFAULT aLockSB3 := {}
If nOpc == 3	// Inclusao
		nPos := Ascan(aHeader,{|x| Alltrim(x[2])=="D3_DTVALID"})
		If nPos > 0
			aColsX[1][nPos] := dDataBase
		EndIf
Else 	// Visualizacao / Estorno

	If nOpc <> 2	// Estorno
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para Dead-Lock                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aScan(aLockSD3,(cAlias)->D3_COD+(cAlias)->D3_LOCAL+(cAlias)->D3_NUMSEQ)==0
			aAdd(aLockSD3,(cAlias)->D3_COD+(cAlias)->D3_LOCAL+(cAlias)->D3_NUMSEQ)
		EndIf
		If aScan(aLockSB2,(cAlias)->D3_COD+(cAlias)->D3_LOCAL)==0
			aAdd(aLockSB2,(cAlias)->D3_COD+(cAlias)->D3_LOCAL)
		EndIf
		If aScan(aLockSB3,(cAlias)->D3_COD)==0
			aAdd(aLockSB3,(cAlias)->D3_COD)
		EndIf

		nPos := Ascan(aHeader,{|x| Alltrim(x[2])=="D3_ESTORNO"})
		If aHeader[nPos,10] # "V"
			aColsX[Len(aColsX)][nPos] := "S"
		EndIf
	EndIf

	nPosDesc := Ascan(aHeader,{|x| Alltrim(x[2])=="D3_DESCRI"})
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Preenche descricao do produto no campo virtual       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nPosDesc > 0
		nPosCod := Ascan(aHeader,{|x| Alltrim(x[2])=="D3_COD"})
		If SB1->(dbSeek(xFilial("SB1")+aColsX[Len(aColsX),nPosCod]))
			aColsX[Len(aColsX),nPosDesc]:= SB1->B1_DESC
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Integracao com o Modulo de Transporte (TMS)                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If IntTMS()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se foi informado Rateio de Viagem/Veiculo ou Rateio ³
		//³ de Frota para o Item                                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		a241VerSDG((cAlias)->D3_NUMSEQ,Len(aColsX))
		dbSelectArea( cAlias )
	EndIf
EndIf
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ MenuDef  ³ Autor ³ Fabio Alves Silva     ³ Data ³04/10/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de Menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Array com opcoes da rotina                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³ 	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Private aRotina	:=  {	{OemToAnsi(STR0005),"AxPesqui"  , 0 , 1,0,.F.},;		//"Pesquisar"
						{OemToAnsi(STR0006),"A241Visual", 0 , 2,0,nil},;		//"Visualizar"
						{OemToAnsi(STR0007),"A241Inclui", 0 , 3,0,nil},;		//"Incluir"
						{OemToAnsi(STR0008),"A241Estorn", 0 , 6,0,nil},;		//"Estornar"
						{OemToAnsi(STR0052),"CTBC662"   , 0 , 7,0,Nil},;		//"Tracker Contábil"
						{OemToAnsi(STR0034),"A240Legenda", 0 , 2,0,.F.} }		//"Legenda"
If ExistBlock ("MTA241MNU")
	Execblock ("MTA241MNU",.F.,.F.)
Endif
Return (aRotina)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F4Grade   ºAutor  ³Microsiga           º Data ³  02/03/07   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Estoque do produto. Funcao chamada pela digitação da grade  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F4Grade()

MaViewSB2(PADR(ograde:GetNameProd(ograde:cprodref,ograde:ogetdados:obrowse:nat,ograde:ogetdados:obrowse:ncolpos),15))

Return


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A241PrdGrd³Autor  ³Emerson Rony Oliveira  ³ Data ³25/07/2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Interface de Grade de Produtos - Movimentos Internos(Mod2)  ³±±
±±³          ³ Substitui a antiga funcao A241Produto(cProduto)             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. se Valido ou .F. se Invalido                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Getdados do MATA241.PRX disparada pelo X3_VALID do D3_COD    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A241PrdGrd()
Local aArea        := GetArea()
Local cNewItem     := ""
Local cPrdOrig     := ""
Local cCpoName     := StrTran(ReadVar(),"M->","")
Local cSaveReadVar := __READVAR
Local lGrade       := MaGrade()
Local lReferencia  := .F.
Local lAadd        := .F.
Local lQtdZero     := .F.
Local lRet         := .T.
Local nSaveN
Local nNewItem
Local nPosProd
Local nPLocal
Local nPosQuant
Local nPosCusto1
Local nPosQtSegum
Local nLinX        := 0
Local nColY        := 0
Local nY           := 0
Local nOpca        := 0
Local oDlg

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o usuario tem permissao de inclusao. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("MATA240") .Or. IsInCallStack("MATA241")
	lRet := MaAvalPerm(1,{M->D3_COD,"MTA240",3})
ElseIf IsInCallStack("MATA250")
	lRet := MaAvalPerm(1,{M->D3_COD,"MTA250",3})
EndIf
If !lRet
	Help(,,1,'SEMPERM')
EndIf

If IntWMS(M->D3_COD)
	cServReq := Space(Len(SB5->B5_SERVREQ))
	cServDev := Space(Len(SB5->B5_SERVDEV))

	dbSelectArea("SB5")
	dbSetOrder(1)

	If dbSeek(xFilial("SB5")+M->D3_COD)
		cServReq := SB5->B5_SERVREQ
		cServDev := SB5->B5_SERVDEV
	EndIf

	dbSelectArea("SF5")
	If FwIsInCallStack("MATA250")

		MsSeek(xFilial("SF5")+M->D3_TM)
		If Iif(!Empty(SF5->F5_VAL),SF5->F5_VAL=="N",.T.) .And. IIf(!Empty(SF5->F5_QTDZERO),SF5->F5_QTDZERO=="2",.T.)

			If M->D3_TM > '500'
				M->D3_SERVIC  := cServReq
			Else
				M->D3_SERVIC  := cServDev
			EndIf
		Else
			M->D3_SERVIC := Space(TamSx3("D3_SERVIC")[1])
		EndIf
	ElseIf FwIsInCallStack("MATA241") .And. Type('cTm') == "C"
		nPosServ := aScan(aHeader,{|x| AllTrim(x[2])=="D3_SERVIC"})

		MsSeek(xFilial("SF5")+cTm)
		If Iif(!Empty(SF5->F5_VAL),SF5->F5_VAL=="N",.T.) .And. IIf(!Empty(SF5->F5_QTDZERO),SF5->F5_QTDZERO=="2",.T.)
			If cTm > '500'
				aCols[n][nPosServ] := cServReq
			Else
				aCols[n][nPosServ] := cServDev
			EndIf
		Else
			aCols[n][nPosServ] := Space(TamSx3("D3_SERVIC")[1])
		EndIf
	EndIf
EndIf

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se a grade esta ativa e se o produto digitado e uma referencia e Monta o AcolsGrade e o AheadGrade para este item ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cProdRef    := &(ReadVar())
	lReferencia := MatGrdPrrf(@cProdRef)
	If lReferencia .And. l241 .And. lGrade .And. !Empty(&(ReadVar()))
		nSaveN       	:= N
		nNewItem     	:= Len(aCols)
		nPosProd     	:= aScan(aHeader,{|x| AllTrim(x[2])=="D3_COD"})
		nPLocal			:= aScan(aHeader,{|x| AllTrim(x[2])=="D3_LOCAL"})
		nPosQuant    	:= aScan(aHeader,{|x| AllTrim(x[2])=="D3_QUANT"})
		nPosCusto1		:= aScan(aHeader,{|x| AllTrim(x[2])=="D3_CUSTO1"})
		nPosQtSegum  	:= aScan(aHeader,{|x| AllTrim(x[2])=="D3_QTSEGUM"})
	  PRIVATE oGrade  := MsMatGrade():New('oGrade', , 'D3_QUANT', , 'A241VldGrd()', {{VK_F4, { || ShowF4() } }},;
		{{"D3_QUANT"    ,.T., {{"D3_QTSEGUM", {|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),aCols[nLinha][nColuna],0,2) }}} },;
	   {"D3_QTSEGUM"  ,NIL, {{"D3_QUANT"  , {|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),0,aCols[nLinha][nColuna],1) }}} },;
	   {"D3_LOCAL"    ,NIL, NIL},;
	   {"D3_CUSTO1"   ,NIL, NIL};
	  })
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ So aceita a entrada de dados via interface de grade se o usr ³
		//³ estiver posicionado na ultima linha da MsGetdados (NewLine). ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If N >= Len(aCols) .And. Empty(aCols[Len(aCols)][nPosProd])

			oGrade:MontaGrade(1,cProdRef,.T.,,lReferencia,.T.)
			oGrade:nPosLinO     := 1
			oGrade:cProdRef	    := cProdRef
			oGrade:lShowMsgDiff := .F. // Desliga apresentacao do "A410QTDDIF"

			nNewItem := Len(aCols)
			lAadd    := .F.

			DEFINE MSDIALOG oDlg TITLE STR0039 OF oMainWnd PIXEL FROM 000,000 TO 220,520  //"Interface para Grade de Produtos"

			@ 035,010 BUTTON STR0040 SIZE 70,15 FONT oDlg:oFont ACTION ;
			{|| __READVAR:='M->D3_QUANT'  ,M->D3_QUANT   := 0,cCpoName := StrTran(ReadVar(),"M->",""),oGrade:Show(cCpoName) } OF oDlg PIXEL //"Quantidade"
			@ 055,010 BUTTON STR0041 SIZE 70,15 FONT oDlg:oFont ACTION ;
			{|| __READVAR:='M->D3_CUSTO1' ,M->D3_CUSTO1  := 0,cCpoName := StrTran(ReadVar(),"M->",""),oGrade:Show(cCpoName) } OF oDlg PIXEL //"Custo"
			If nPosQtSegum > 0
				@ 075,010 BUTTON STR0042 SIZE 70,15 FONT oDlg:oFont ACTION ;
				{|| __READVAR:='M->D3_QTSEGUM',M->D3_QTSEGUM := 0,cCpoName := StrTran(ReadVar(),"M->",""),oGrade:Show(cCpoName) } OF oDlg PIXEL //"Segunda Und Medida"
			EndIf

			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End(), nOpca:=1},{||oDlg:End(), nOpca:=0}) CENTERED

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Somente realiza a carga do item para o aCols se pelo menos uma celula do D3_QUANT contiver valor.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nOpca == 1
				// Verifica se permite quantidade zerada na grade de produtos
				If Type('cTm') == "C" .And. SF5->(MsSeek(xFilial("SF5")+cTm))
					lQtdZero := ( SF5->F5_VAL == "S" .And. SF5->F5_QTDZERO == "1" )
				EndIf
				// Carrega grade de produtos
				If If(lQtdZero,oGrade:SomaGrade("D3_CUSTO1",oGrade:nPosLinO,oGrade:nQtdInformada) <> 0,oGrade:SomaGrade("D3_QUANT",oGrade:nPosLinO,oGrade:nQtdInformada) > 0)
					For nLinX := 1 To Len(oGrade:aColsGrade[1])
						For nColY := 2 To Len(oGrade:aHeadGrade[1])
							If If(lQtdZero,oGrade:aColsFieldByName("D3_CUSTO1",1,nLinX,nColY) <> 0,oGrade:aColsFieldByName("D3_QUANT",1,nLinX,nColY) <> 0)
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Faz a montagem de uma nova linha em branco no aCols para     ³
								//³ adicionar novos itens vindos das celulas da Grade.           ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If lAadd
									aadd(aCols,Array(Len(aHeader)+1))
									nNewItem := Len(aCols)
									cNewItem := StrZero(nNewItem,Len(SD1->D1_ITEM))
									For nY := 1 to Len(aHeader)
										If Trim(aHeader[nY][2]) == "D3_COD"
											aCols[nNewItem][nY] := cNewItem
										ElseIf IsHeadRec(aHeader[nY][2])
											aCols[nNewItem][nY] := 0
										ElseIf IsHeadAlias(aHeader[nY][2])
											aCols[nNewItem][nY] := "SD3"
										Else
											aCols[nNewItem][nY] := CriaVar(aHeader[nY][2])
										EndIf
										aCols[nNewItem][Len(aHeader)+1] := .F.
									Next nY
								EndIf

								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³Efetua a carga dos itens digitados no grid para o aCols                  ³
								//³Executa as validacoes necessarias para se carregar corretamente os dados ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								N := nNewItem
								aCols[nNewItem][nPosProd]:= PadR(oGrade:GetNameProd(cProdRef,nLinX,nColY),Len(SD1->D1_COD))
								M->D3_COD := aCols[nNewItem][nPosProd]
								A240IniCpo() //-- Validacao que estava no X3_VALID da coluna D3_COD e que deve ser executada internamente

								aCols[nNewItem][nPosQuant]:= oGrade:aColsFieldByName("D3_QUANT",1,nLinX,nColY)
								M->D3_QUANT   := oGrade:aColsFieldByName("D3_QUANT",1,nLinX,nColY)
								A240Quant()  //-- Validacao consta no X3_VALID para a coluna D3_QUANT

								aCols[nNewItem][nPosCusto1]:= oGrade:aColsFieldByName("D3_CUSTO1",1,nLinX,nColY)
								M->D3_CUSTO1  := oGrade:aColsFieldByName("D3_CUSTO1",1,nLinX,nColY)

								If nPosQtSegum > 0
									aCols[nNewItem][nPosQtSegum]:= oGrade:aColsFieldByName("D3_QTSEGUM",1,nLinX,nColY)
									M->D3_QTSEGUM := oGrade:aColsFieldByName("D3_QTSEGUM",1,nLinX,nColY)
								EndIf

								AQtdGrade()  //-- Deve ser executada somente neste momento pois o objeto oGrade esta ativo
								A240PriUm()  //-- Validacao consta no X3_VALID para o a coluna D3_QTSEGUM


								If !lAadd
									cPrdOrig := aCols[nNewItem][nPosProd]
									lAadd 	 := .T.
								Endif
							EndIf
						Next nColY
					Next nLinX
				Else
					lRet := .F.
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Restaura os valores originais do N da GetDados, e da Public      ³
			//³__READVAR que fora manipulada pela interface de grade.           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			N         := nSaveN
			__READVAR := cSaveReadVar
			M->D3_COD := cPrdOrig
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Para incluir um produto com referencia de grade e necessario estar³
			//³em uma nova linha do movimento interno.                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Help(" ",1,"A241PRDGRD")
			lRet := .F.
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se o Produto nao for um produto de grade executa a validacao no SB1 ³
		//³ e inicializa os campos na getdados.                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB1")
		dbSetOrder(1)
		lRet := ExistCpo("SB1")
		If lRet .And. (l240 .Or. l241 .Or. l242 .Or. l250)
			A240IniCpo()
		Endif
	EndIf
	RestArea(aArea)
EndIf

Return(lRet)


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³A241VldGrd³Autor  ³Emerson Rony Oliveira  ³ Data ³25/07/2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao dos itens do Grid na grade de produtos            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T. se Valido e .F. se Invalido                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Objeto de Grade do MATA241                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A241VldGrd()
Local lValido := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Houver necessidade de novas validacoes na entrada de dados nas   ³
//³ celulas do Grid elas deverao ser inseridas nessa funcao.            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Positivo()
	lValido := .T.
EndIf

Return lValido

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ChkOPSusp  ³ Autor ³ Sergio S. Fuzinaka    ³ Data ³ 28.10.09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Checa se a OP esta Suspensa.                                 ³±±
±±³			 ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Generico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ChkOPSusp()

Local lRet 		:= .T.
Local nX		:= 0
Local aArea		:= GetArea()
Local aAreaSC2	:= SC2->( GetArea() )
Local lOpSusp	:= GetNewPar("MV_OPSUSP",.T.)
Local cProdMNT	:= SuperGetMv("MV_PRODMNT",.F.,"MANUTENCAO")

SC2->( dbSetOrder( 1 ) )
If lOpSusp
	For nX := 1 To Len( aCols )
		If !aCols[nX][Len(aCols[nX])] .And. !Empty(aCols[nX][nPosOp])
			If SC2->(dbSeek(xFilial("SC2")+Alltrim(aCols[nX][nPosOp])))
				If SC2->C2_STATUS == 'U' .And. ( Alltrim( Upper(SC2->C2_PRODUTO) ) <> Alltrim( Upper(cProdMNT) ) )
					Help("",1,"A250OPSUSP",,"OP: "+aCols[nX][nPosOp],5,0)
					lRet := .F.
				Endif
			EndIf
		Endif
	Next
EndIf

RestArea( aAreaSC2 )
RestArea( aArea )

Return( lRet )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Function  ³ IntegDef º Autor ³ Alex Egydio          º Data ³  08/02/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Descricao ³ Funcao de tratamento para a mensagem unica da movimentacao   º±±
±±º           ³ de estoque (Stockturnover)                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso       ³ MATA241                                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransac, lEAIObj )
Default xEnt := ""
Default nTypeTrans := ""
Default cTypeMessage := ""
Default cVersion := ""
Default cTransac := ""
Default lEAIObj := .F.

Return MATI241(xEnt,nTypeTrans,cTypeMessage, cVersion, cTransac, lEAIObj, a241ISD3 )

// Funcao responsavel por armazenar o Recno SD3 passado no estorno da baixa via MATA185
// para que a mensagem unica MATI241 possa realizar o estorno corretamente
Function MTA241SD3(aReg,lZera)

Default lZera := .F.

If !lZera
	a241ISD3 := aReg
Else
	a241ISD3 := {}
EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} A241VldTM()
Função valida TM
@author Leonardo Quintania
@since 14/11/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function A241VldTM()
Local lRet 	:= .T.
Local lMata340:= IsInCallStack("MATA340")

If !lMata340
	lRet:= ExistCpo("SF5") .And. MaAvalPerm(2,{M->D3_TM}) .And. Iif(l250,A250TM(),A240TM())
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A241AvalATF()
Efetua Avaliação de parametros
@author Leonardo Quintania
@since 27/10/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function A241AvalATF(lDesItem,lComponent,nOpc,cNumseq,aCMNF,cloteD3)
Local aCab			:= {}
Local aItens		:= {}
Local aSD1			:= {}
Local aRecno		:= {}

Local cFilialSB2	:= xFilial("SB2")
Local cFilialSD1	:= xFilial("SD1")
Local cFilialSF4	:= xFilial("SF4")
Local cFilialSN1	:= xFilial("SN1")
Local cFilialCF5	:= xFilial("CF5")

Local cPath		:= ""
Local cFileLog	:= ""
Local cBase		:= ""
Local cItemAtf	:= ""
Local cAux 		:= Replicate("0", TAMSX3("N1_ITEM")[1])
Local cProduto	:= SD3->D3_COD
Local cSimpNac	:= ""
Local cCiap		:= ""

Local nI			:= 0
Local nJ			:= 0
Local nQtdSD3		:= SD3->D3_QUANT
Local nQtdReq		:= SD3->D3_QUANT
Local nTamSN1		:= TamSX3("N1_ITEM")[1]
Local nQtdSB2		:= 0
Local cQuery        := ""
Local nQtdAtf       := 0
Local nQtdSldNf     := 0
Local cAliasSD1     := "TMPSD1"
Local cAliasSN1     := "TMPSN1"
Local cAliasCF5     := "TMPCF5"

Local lF4BensATF	:= IIf(lDesItem .And. nQtdSD3 >= 1, .T. ,.F. )
Local lATFDCBA	:= SuperGetMv("MV_ATFDCBA",.F.,"0") == "1" // "0"- Desmembra itens / "1" - Desmembra codigo base
Local lRet			:= .T.

Local jTributosX	:= Nil
Local cJsonTributos	:= ""
Local cIDTributo	:= ""
Local aIDTributos	:= {}
Local nTributos		:= 0
Local cCredIcm		:= ""
Local cComplIcm		:= ""

Default nOpc		:= 3
Default cNumseq	:= ""
Default cloteD3 := ""

If Type("lMsErroAuto") == "U"
	lMsErroAuto := .F.
	lMsHelpAuto := .T.
EndIf

If nOpc == 3 //Inclusão
	If _o241ATFB2 == NIL
		//Pesquisa todos os armazens do produto
		cQuery := " SELECT SUM(SB2.B2_QATU) B2_QATU "
		cQuery += " FROM " + RetSQLName("SB2") + " SB2 "
		cQuery += " WHERE SB2.B2_FILIAL = ? AND "
		cQuery += " SB2.B2_COD = ? AND "
		cQuery += " SB2.D_E_L_E_T_ = ? "

		cQuery := ChangeQuery(cQuery)

		_o241ATFB2 := FwExecStatement():New(cQuery)
	EndIf

	_o241ATFB2:SetString(1,cFilialSB2)
	_o241ATFB2:SetString(2,cProduto)
	_o241ATFB2:SetString(3,' ')

	nQtdSB2 := _o241ATFB2:ExecScalar('B2_QATU')

	If _o241ATFD1 == NIL
		cQuery := " SELECT D1_FILIAL, D1_COD, D1_QUANT, D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, "
		cQuery += " D1_ITEM, D1_EMISSAO, D1_TES, D1_TIPO, SD1.R_E_C_N_O_ RECNO, F1_ESPECIE, F4_ESTOQUE "
		cQuery += " FROM " + RetSQLName("SD1") + " SD1 "
		cQuery += " INNER JOIN " + RetSQLName("SF1") + " SF1 ON "
		cQuery += " SF1.F1_FILIAL = SD1.D1_FILIAL AND "
		cQuery += " SF1.F1_DOC = SD1.D1_DOC AND "
		cQuery += " SF1.F1_SERIE = SD1.D1_SERIE AND "
		cQuery += " SF1.F1_FORNECE = SD1.D1_FORNECE AND "
		cQuery += " SF1.F1_LOJA = SD1.D1_LOJA AND "
		cQuery += " SF1.D_E_L_E_T_ = ? "
		cQuery += " INNER JOIN " + RetSQLName("SF4") + " SF4 ON "
		cQuery += " SF4.F4_FILIAL = ? AND "
		cQuery += " SF4.F4_CODIGO = SD1.D1_TES AND "
		cQuery += " SF4.F4_ESTOQUE = ? AND "
		cQuery += " SF4.D_E_L_E_T_ = ? "
		cQuery += " WHERE D1_FILIAL = ? AND "
		cQuery += " SD1.D1_COD = ? AND "
		cQuery += " SD1.D1_TIPO = ? AND "
		cQuery += " SD1.D1_LOTECTL = ? AND "
		cQuery += " SD1.D_E_L_E_T_ = ? "
		cQuery += " ORDER BY D1_EMISSAO DESC, SD1.R_E_C_N_O_ DESC "

		cQuery := ChangeQuery(cQuery)

		_o241ATFD1 := FwExecStatement():New(cQuery)
	EndIf

	_o241ATFD1:SetString(1,' ')
	_o241ATFD1:SetString(2,cFilialSF4)
	_o241ATFD1:SetString(3,'S')
	_o241ATFD1:SetString(4,' ')
	_o241ATFD1:SetString(5,cFilialSD1)
	_o241ATFD1:SetString(6,cProduto)
	_o241ATFD1:SetString(7,'N')
	_o241ATFD1:SetString(8,cloteD3)
	_o241ATFD1:SetString(9,' ')

	_o241ATFD1:OpenAlias(cAliasSD1)

	If __oSQLSN1 == Nil
		cQuery := " Select Sum (N1_QUANTD) N1_QUANTD "
		cQuery += "   From "+RetSqlName('SN1')+" "
		cQuery += "  Where N1_FILIAL  = ? "
		cQuery += "    And N1_FORNEC  = ? "
		cQuery += "    And N1_LOJA    = ? "
		cQuery += "    And N1_NFESPEC = ? "
		cQuery += "    And N1_NFISCAL = ? "
		cQuery += "    And N1_NSERIE  = ? "
		cQuery += "    And N1_NFITEM  = ? "
		cQuery += "    And D_E_L_E_T_ = ? "
		cQuery := ChangeQuery(cQuery)

		__oSQLSN1 := FwExecStatement():New(cQuery)

	EndIf

	//Realiza composição do saldo encontrado no estoque da nota mais nova para mais antiga(FIFO)
	While TMPSD1->(!EOF()) .And. nQtdSB2 > 0
		//Pega quantidade já vinculada deste item de nota fiscal na SN1
		__oSQLSN1:SetString(01, cFilialSN1)
		__oSQLSN1:SetString(02, TMPSD1->D1_FORNECE)
		__oSQLSN1:SetString(03, TMPSD1->D1_LOJA)
		__oSQLSN1:SetString(04, TMPSD1->F1_ESPECIE)
		__oSQLSN1:SetString(05, TMPSD1->D1_DOC)
		__oSQLSN1:SetString(06, TMPSD1->D1_SERIE)
		__oSQLSN1:SetString(07, TMPSD1->D1_ITEM)
		__oSQLSN1:SetString(08, ' ')

		nQtdAtf := __oSQLSN1:ExecScalar('N1_QUANTD')

		nQtdSldNf := TMPSD1->D1_QUANT - nQtdAtf

		//Se todo o saldo do item da nota já foi requisitado para o ativo fixo, analisa o próximo
		If nQtdSldNf <= 0
			TMPSD1->(dbSkip())
			Loop
		EndIf

		If (nQtdSB2 - TMPSD1->D1_QUANT - nQtdAtf) > 0
			aAdd(aSD1, { TMPSD1->Recno, TMPSD1->D1_QUANT - nQtdAtf} )
		Else
			aAdd(aSD1, { TMPSD1->Recno, nQtdSB2} ) //Para considerar apenas parte da nota para compor saldo
		EndIf
		nQtdSB2 -= TMPSD1->D1_QUANT - nQtdAtf
		TMPSD1->(dbSkip())
	EndDo

	TMPSD1->(dbCloseArea())

	For nI := Len(aSD1) To 1 STEP -1
		If (nQtdReq - aSD1[nI,2]) > 0
			aAdd(aRecno, {.T.,aSD1[nI,1],aSD1[nI,2]}) //Guarda recno do documento que foi levado em consideração para geração da tabela DH0
			nQtdReq 	-= aSD1[nI,2]
		Else
			aAdd(aRecno, {.T.,aSD1[nI,1],nQtdReq}) //Guarda recno do documento que foi levado em consideração para geração da tabela DH0
			nQtdReq:= 0
			Exit
		EndIf
	Next nI

	If !Empty(nQtdReq) //Caso não tenha encontrado notas suficientes para compor o saldo, é gerado um registro com o restante
		aAdd(aRecno, {.F.,0,nQtdReq})
	EndIf

	For nI := 1 TO Len(aRecno)
		If (lDesItem .And. lF4BensATF )
			For nJ := 1 TO aRecno[nI, 3]
				If !lATFDCBA .OR. ( lATFDCBA .AND. nI == 1 )
					cAux		:= Soma1( cAux,,, .F. )
					cItemAtf	:= PadL( cAux, nTamSN1 , "0" )
				EndIf
				If aRecno[nI,1] //Se considerou nota fiscal
					SD1->(dbGoTo(aRecno[nI,2]))
					SF1->(MsSeek(xFilial("SF1")+ SD1->(D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_TIPO)))
					SA2->(MsSeek(xFilial("SA2")+ SD1->D1_FORNECE+SD1->D1_LOJA))
					SF4->(MsSeek(xFilial("SF4")+ SD3->D3_TEATF ))

					cCredIcm	:= SF4->F4_CREDICM
					cComplIcm	:= SF4->F4_COMPL

					//Realiza atribuição para os impostos encontrados no config. de tributos 
					cJsonTributos := a241fgTrib(SD1->D1_IDTRIB, {"000021","000038"})
					if !Empty(cJsonTributos)
						jTributosX := JsonObject():New()
						jTributosX:FromJson(cJsonTributos)

						aIDTributos := jTributosX:GetNames()
						for nTributos := 1 to len(aIDTributos)
							cIDTributo := aIDTributos[nTributos]

							IF cIDTributo == "000021" // ICMS
								if jTributosX[cIDTributo]["operador_custo"]== "1"
									cCredIcm := "S"								   
								EndIf
							ElseIF cIDTributo == "000038" // ICMS COMPLEMENTAR
								cComplIcm := "S"
							EndIf
						next nTributos

						FreeObj(jTributosX)
						fwFreeArray(aIDTributos)
					EndIf

					cSimpNac := Iif(cPaisLoc == "BRA" ,SA2->A2_SIMPNAC,"")
					If (SF4->F4_CIAP=="S" .And. ((SD1->D1_VALICM  > 0 .And. cCredIcm == "S") .Or. (cPaisLoc == "BRA" .And. SD1->D1_ICMNDES > 0) .Or. ;
						(cComplIcm == "S" .AND. cSimpNac == "1")))
						cCiap:= A103CIAP(1,"SD1",aRecno[nI,3],"MATA240")
					EndIf
				EndIf
				lRet := A241GerATF(lDesItem,lComponent,nOpc,aRecno[nI],@cBase,cItemAtf,cNumseq,@aCMNF,cCiap)
			Next nJ
		Else
			If aRecno[nI,1] //Se considerou nota fiscal
				SD1->(dbGoTo(aRecno[nI,2]))
				SF1->(MsSeek(xFilial("SF1")+ SD1->(D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_TIPO)))
				SA2->(MsSeek(xFilial("SA2")+ SD1->D1_FORNECE+SD1->D1_LOJA))
				SF4->(MsSeek(xFilial("SF4")+ SD3->D3_TEATF ))

				cCredIcm	:= SF4->F4_CREDICM
				cComplIcm	:= SF4->F4_COMPL

				//Realiza atribuição para os impostos encontrados no config. de tributos 
				cJsonTributos := a241fgTrib(SD1->D1_IDTRIB, {"000021","000038"})
				if !Empty(cJsonTributos)
					jTributosX := JsonObject():New()
					jTributosX:FromJson(cJsonTributos)
					
					aIDTributos := jTributosX:GetNames()
					for nTributos := 1 to len(aIDTributos)
						cIDTributo := aIDTributos[nTributos]

						IF cIDTributo == "000021" // ICMS
							if jTributosX[cIDTributo]["operador_custo"]== "1"
							   cCredIcm := "S"
							EndIf
						ElseIF cIDTributo == "000038" // ICMS COMPLEMENTAR
							cComplIcm := "S"
						EndIf
					next nTributos

					FreeObj(jTributosX)
					fwFreeArray(aIDTributos)
				EndIf

				cSimpNac := Iif(cPaisLoc == "BRA" ,SA2->A2_SIMPNAC,"")
				If (SF4->F4_CIAP=="S" .And. ((SD1->D1_VALICM  > 0 .And. cCredIcm == "S") .Or. (cPaisLoc == "BRA" .And. SD1->D1_ICMNDES > 0) .Or. ;
					(cComplIcm == "S" .AND. cSimpNac == "1")))
					cCiap:= A103CIAP(1,"SD1",aRecno[nI,3],"MATA240")
				EndIf
			EndIf
			cItemAtf := StrZero(1,Len(SN1->N1_ITEM))
			lRet := A241GerATF(lDesItem,lComponent,nOpc,aRecno[nI],cBase,cItemAtf,cNumseq,@aCMNF,cCiap)
		EndIf
	Next nI
Else
	cNumSeq:= SD3->D3_NUMSEQ

	If _o241ATFN1 == NIL
		cQuery := " SELECT SN1.N1_CBASE,SN1.N1_ITEM,SN1.N1_CODCIAP "
		cQuery += " FROM " + RetSQLName("SN1") + " SN1 "
		cQuery += " WHERE SN1.N1_FILIAL = ? AND "
		cQuery += " SN1.N1_NUMSEQ = ? AND "
		cQuery += " SN1.D_E_L_E_T_ = ? "

		cQuery := ChangeQuery(cQuery)

		_o241ATFN1 := FwExecStatement():New(cQuery)
	Endif

	_o241ATFN1:SetString(1,cFilialSN1)
	_o241ATFN1:SetString(2,cNumSeq)
	_o241ATFN1:SetString(3,' ')

	_o241ATFN1:OpenAlias(cAliasSN1)

	While !TMPSN1->(Eof())

		AAdd(aCab,{"N1_CBASE"  	, TMPSN1->N1_CBASE		,NIL})
		AAdd(aCab,{"N1_ITEM"   	, TMPSN1->N1_ITEM			,NIL})

		MSExecAuto({|x,y,z,w| Atfa012(x,y,z,w)},aCab,aItens,5)

		If lMsErroAuto
			cFileLog := NomeAutoLog()
			lRet:= .F.
			Exit
		Else
			//--Exclui Tabela de CIAP
			SF9->(dbSetOrder(1))
			If SF9->(dbSeek(xFilial("SF9")+TMPSN1->N1_CODCIAP))
				While SF9->(!Eof()) .And. SF9->(F9_FILIAL+F9_CODIGO) == xFilial("SF9")+TMPSN1->N1_CODCIAP
					SFA->(dbSetOrder(1))
					If SFA->(dbSeek(xFilial("SFA")+SF9->F9_CODIGO))
						While SFA->(!Eof()) .And. SFA->(FA_FILIAL+FA_CODIGO) == xFilial("SFA")+SF9->F9_CODIGO
                    		RecLock("SFA", .F.)
                    		SFA->(dbdelete())
                   	 	SFA->(dbSkip())
						EndDo
					EndIf
					RecLock("SF9", .F.)
					SF9->(dbDelete())
					SF9->(dbSkip())
				EndDo
			EndIf
		EndIf
		aCab:= {}
		TMPSN1->(dbSkip())
	EndDo
	TMPSN1->(dbCloseArea())
	If !Empty(cFileLog) 
		If !IsBlind()
			MostraErro(cPath,cFileLog)
		EndIf
	Else
		DH0->(dbSetOrder(2))
		DH0->(dbSeek(xFilial("DH0")+cNumSeq))
		While DH0->(!Eof())  .And. DH0->(DH0_FILIAL+DH0_NUMSEQ) == xFilial("DH0")+cNumSeq
			SD1->(MsSeek(xFilial("SD1")+ DH0->(DH0_FILIAL+DH0_DOC+DH0_SERIE+DH0_FORNEC+DH0_LOJA)))
			RecLock("DH0",.F.,.T.)
			DH0->(dbDelete())
			MsUnlock()
			DH0->(dbSkip())
		EndDo
	EndIf
	If cPaisLoc == "BRA" .And. lRet
		If _o241ATFF5 == NIL
			cQuery := " SELECT CF5_FILIAL,CF5_CODIGO "
			cQuery += " FROM " + RetSQLName("CF5") + " CF5 "
			cQuery += " WHERE CF5.CF5_FILIAL = ? AND "
			cQuery += " CF5.CF5_NUMDOC = ? AND "
			cQuery += " CF5.D_E_L_E_T_ = ? "

			cQuery := ChangeQuery(cQuery)

			_o241ATFF5 := FwExecStatement():New(cQuery)
		EndIf

		_o241ATFF5:SetString(1,cFilialCF5)
		_o241ATFF5:SetString(2,cNumSeq)
		_o241ATFF5:SetString(3,' ')

		_o241ATFF5:OpenAlias(cAliasCF5)

		While !TMPCF5->(Eof()) //-- Tabela refente a apuração de PIS/COFINS para estorno de credito
			CF5->(MsSeek(TMPCF5->(CF5_FILIAL+CF5_CODIGO)))
			RecLock("CF5", .F.)
			CF5->(dbDelete())
			MsUnlock()
			TMPCF5->(dbSkip())
		EndDo
		TMPCF5->(dbCloseArea())
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A241GerATF()
Função realiza a geração de ativos
@author Leonardo Quintania
@since 27/10/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function A241GerATF(lDesItem,lComponent,nOpc,aRecno,cBase,cItem,cNumseq,aCMNF,cCiap)
Local aCab			:= {}
Local aItens		:= {}
Local aAux			:= {}
Local aParam		:= {}
Local aTamVOrig		:= {}
Local aRelImp    	:= MaFisRelImp("MT100",{ "SD1" })

Local cCpBsPisEn 	:= ""
Local cCpBsCofEn 	:= ""
Local cPath			:= ""
Local cFileLog		:= ""
Local cFilialSB2    := xFilial("SB2")
Local cFilialSF1    := xFilial("SF1")
Local cFilialSD1    := xFilial("SD1")
Local cFilialSF4    := xFilial("SF4")
Local cFilialSF8    := xFilial("SF8")
Local cProduto		:= SD3->D3_COD
Local cArmazem		:= SD3->D3_LOCAL

Local nScanPis 	 	:= 0
Local nScanCof 	 	:= 0
Local nQtdEnt 		:= CtbQtdEntd()
Local nI			:= 0
Local nQtdSD3		:= aRecno[3]
Local nAtfQtdIt		:= IIf(lDesItem .And. nQtdSD3 >=1, nQtdSD3 ,1)
Local nCusto		:= 0
Local nCustoAux		:= 0
Local nMoeda		:= IIf(cPaisLoc == "BRA",1,SF1->F1_MOEDA)
Local nICMS			:= 0
Local nPis			:= 0
Local nCofins		:= 0
Local nDescrN1 		:= TamSX3("N1_DESCRIC")[1]

Local lF4BensATF	:= IIf(lDesItem .And. aRecno[3] >= 1, .T. ,.F. )
Local lATFDCBA		:= SuperGetMv("MV_ATFDCBA",.F.,"0") == "1" // "0"- Desmembra itens / "1" - Desmembra codigo base
Local lCusAtivo     := .F.
Local lRet			:= .T.
Local aAreaSD1      := {}
Local cDocORI		:= ""
Local cSerORI 		:= ""
Local cChaveFRE     := 0
Local nCustoFRE	 	:= 0
Local nQtdItCTE     := 0

Local jTributosX	:= Nil
Local cJsonTributos	:= ""
Local cIDTributo	:= ""
Local aIDTributos	:= {}
Local nTributos		:= 0
Local cCstPis		:= ""
Local cCstCof		:= ""

Private N 			:= 1 //Inclusão da variavel para execução via MSExecAuto "Robô", será utilizada na função AF012AutRot

Default cCiap		:= ""

aAdd( aParam, {"MV_PAR01", 2} )
aAdd( aParam, {"MV_PAR02", 1} )
aAdd( aParam, {"MV_PAR05", 2} )

aAdd(aTamVOrig,TamSx3("N3_VORIG1"))
aAdd(aTamVOrig,TamSx3("N3_VORIG2"))
AADD(aTamVOrig,TamSx3("N3_VORIG3"))
aAdd(aTamVOrig,TamSx3("N3_VORIG4"))
aAdd(aTamVOrig,TamSx3("N3_VORIG5"))

If (Empty(cBase)) .Or. (lF4BensATF .And. lATFDCBA)
	cBase := &(SuperGetMV("MV_CBASEAF",.F.))
	SN1->(DbSetOrder(1))
	While SN1->(MsSeek(xFilial("SN1")+cBase))
		cBase := Soma1(cBase,Len(SN1->N1_CBASE))
	EndDo
	PutMV("MV_CBASEAF",'"'+Soma1(cBase,Len(SN1->N1_CBASE))+'"')
EndIf

dbSelectArea("SF5")
If MsSeek(xFilial("SF5")+SD3->D3_TM)
	lCusAtivo := SF5->(ColumnPos("F5_CUSTATF")) > 0 .And. SF5->F5_CUSTATF == "1"
EndIf
If !Empty(cBase)

	//Posiciono o campo correto com a aliquota do PIS da tabela SD1
	If !Empty( nScanPis := aScan(aRelImp,{|x| x[1]=="SD1" .And. x[3]=="IT_ALIQPS2"} ) )
		cCpBsPisEn := aRelImp[nScanPis,2]
	EndIf

	//Posiciono o campo correto com a aliquota da COFINS da tabela SD1
	If !Empty( nScanCof := aScan(aRelImp,{|x| x[1]=="SD1" .And. x[3]=="IT_ALIQCF2"} ) )
		cCpBsCofEn := aRelImp[nScanCof,2]
	EndIf

	If aRecno[1] //Se estiver considerando nota fiscal?
		SD1->(dbGoTo(aRecno[2]))
		SF1->(MsSeek(cFilialSF1+SD1->(D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_TIPO)))
		SF4->(MsSeek(cFilialSF4+SD1->D1_TES))
		SB2->(dbSeek(cFilialSB2+cProduto+cArmazem))
		If !lCusAtivo
			cDocORI := SD1->D1_DOC
			cSerORI := SD1->D1_SERIE
			aAreaSD1 := SD1->(GetArea())

			If Alltrim(SD1->D1_ORIGLAN) == 'F'
				SF8->(DbSetOrder(2)) // F8_FILIAL+F8_NFORIG+F8_SERORIG+F8_FORNECE+F8_LOJA
				If SF8->(MsSeek(cFilialSF8+cDocORI+cSerORI))
					cChaveFRE := SF8->(F8_NFDIFRE+F8_SEDIFRE)
					SF8->(DbSetOrder(1)) // F8_FILIAL+F8_NFDIFRE+F8_SEDIFRE+F8_FORNECE+F8_LOJA
					SF8->(MsSeek(cFilialSF8+cChaveFRE))
					
					While !SF8->(EOF())	.AND. SF8->(F8_FILIAL+F8_NFDIFRE+F8_SEDIFRE) == cFilialSF8+cChaveFRE
						If SD1->(MsSeek(cFilialSD1+SF8->(F8_NFDIFRE+F8_SEDIFRE+F8_FORNECE)))
							nQtdItCTE += 1
						EndIf
						SF8->(DbSkip())
					EndDo
					
					If nQtdItCTE > 0 // Efetua rateio do frete de acordo com quantidade de notas
						nCustoFRE += SD1->D1_CUSTO / nQtdItCTE
					EndIf
				EndIf
			EndIf

			// Tratamento para nota que tem complemento de frete
			SD1->(DbSetOrder(19)) // D1_FILIAL+D1_NFORI+D1_SERIORI+D1_FORNECE+D1_LOJA
			If SD1->(MsSeek(cFilialSF1+cDocORI+cSerORI))
				nCustoFRE += SD1->D1_TOTAL
			EndIf

			SD1->(RestArea (aAreaSD1))
			nCusto	:= (((SD1->D1_CUSTO + nCustoFRE) / SD1->D1_QUANT) * nQtdSD3) / nAtfQtdIt //Pega custo medio unitario da nota fiscal
			nCustoAux:= NoRound(nCusto ,2)

			aCMNF[1]+= Round(xMoeda(nCustoAux,nMoeda,1),aTamVOrig[1][2])

			aCMNF[2]+= Round(xMoeda(nCustoAux,nMoeda,2),aTamVOrig[2][2])

			aCMNF[3]+= Round(xMoeda(nCustoAux,nMoeda,3),aTamVOrig[3][2])

			aCMNF[4]+= Round(xMoeda(nCustoAux,nMoeda,4),aTamVOrig[4][2])

			aCMNF[5]+= Round(xMoeda(nCustoAux,nMoeda,5),aTamVOrig[5][2])
		Else
			aCMNF := PegaCMAtu(SD3->D3_COD,SD3->D3_LOCAL)
			nCustoAux:= NoRound((aCMNF[1]*nQtdSD3) ,2)

			aCMNF[1]:= Round(xMoeda(nCustoAux,nMoeda,1),aTamVOrig[1][2])

			aCMNF[2]:= Round(xMoeda(nCustoAux,nMoeda,2),aTamVOrig[2][2])

			aCMNF[3]:= Round(xMoeda(nCustoAux,nMoeda,3),aTamVOrig[3][2])

			aCMNF[4]:= Round(xMoeda(nCustoAux,nMoeda,4),aTamVOrig[4][2])

			aCMNF[5]:= Round(xMoeda(nCustoAux,nMoeda,5),aTamVOrig[5][2])
		EndIf

		nICMS	:= ((SD1->D1_VALICM + SD1->D1_ICMSCOM) / SD1->D1_QUANT) * aRecno[3]
		If cPaisLoc == "BRA"
			nPis	:= ((SD1->D1_VALPIS/SD1->D1_QUANT)*aRecno[3])/ nAtfQtdIt
			nCofins:= ((SD1->D1_VALCOF/SD1->D1_QUANT)*aRecno[3])/ nAtfQtdIt
		EndIf
			
		If !Empty(cCiap)
			//Realiza gravação da tabela DH0 relacionada ao Ativo Fixo X Nota fiscal
			RecLock("DH0",.T.)
			DH0->DH0_FILIAL  := xFilial("DH0")
			DH0->DH0_DOC     := SD1->D1_DOC
			DH0->DH0_SERIE   := SD1->D1_SERIE
			DH0->DH0_ITEM    := SD1->D1_ITEM
			DH0->DH0_ITATF   := cItem
			DH0->DH0_FORNECE := SD1->D1_FORNECE
			DH0->DH0_LOJA    := SD1->D1_LOJA
			DH0->DH0_NUMSEQ  := cNumseq
			DH0->DH0_DATA    := SD3->D3_EMISSAO
			DH0->DH0_CUSTO   := Iif (lCusAtivo,aCMNF[1],nCusto)
			DH0->DH0_QUANT   := aRecno[3] / nAtfQtdIt
			DH0->DH0_VALICM  := NoRound((nICMS / nAtfQtdIt),2)
			DH0->DH0_VALIPI  := ((SD1->D1_VALIPI/SD1->D1_QUANT)*aRecno[3])/ nAtfQtdIt	//Pega Proporcional em relação aos impostos
			DH0->DH0_VALPIS  := nPis													//Pega Proporcional em relação aos impostos
			DH0->DH0_VALCOF  := nCofins													//Pega Proporcional em relação aos impostos
			MsUnlock()
		EndIf
		
		If !Empty(nPis)
			A241GerImp(nPis,cNumseq,1)
		EndIf
		If !Empty(nCofins)
			A241GerImp(nCofins,cNumseq,2)
		EndIf
	Else
		aCMNF := PegaCMAtu(SD3->D3_COD,SD3->D3_LOCAL)

		nCustoAux:= If (lF4BensATF,(aCMNF[1]*nQtdSD3)  / nAtfQtdIt,(aCMNF[1]*nQtdSD3))
		nCustoAux:= NoRound(nCustoAux,2)

		aCMNF[1]:= Round(xMoeda(nCustoAux,nMoeda,1),aTamVOrig[1][2])

		aCMNF[2]:= Round(xMoeda(nCustoAux,nMoeda,2),aTamVOrig[2][2])

		aCMNF[3]:= Round(xMoeda(nCustoAux,nMoeda,3),aTamVOrig[3][2])

		aCMNF[4]:= Round(xMoeda(nCustoAux,nMoeda,4),aTamVOrig[4][2])

		aCMNF[5]:= Round(xMoeda(nCustoAux,nMoeda,5),aTamVOrig[5][2])
	EndIf

	SB1->(DbSetOrder(1))
	SB1->(MsSeek(xFilial("SB1")+cProduto))

	//Preenchimento das Variaveis referentes ao SN1
	aAdd(aCab,{"N1_PATRIM"	, "N" 									,NIL}) //Ativo Imobilizado
	aAdd(aCab,{"N1_CBASE"	, cBase 								,NIL})
	aAdd(aCab,{"N1_ITEM"    , cItem 								,NIL})
	aAdd(aCab,{"N1_AQUISIC"	, SD3->D3_EMISSAO						,NIL})
	aAdd(aCab,{"N1_DESCRIC"	, SUBSTR(SB1->B1_DESC,1,nDescrN1)  ,NIL})
	aAdd(aCab,{"N1_QUANTD"	, nQtdSD3 / nAtfQtdIt				,NIL})
	aAdd(aCab,{"N1_STATUS"	, "0"									,NIL})
	aAdd(aCab,{"N1_PRODUTO"	, SD3->D3_COD							,NIL})
	aAdd(aCab,{"N1_NUMSEQ"	, cNumseq								,NIL})

	If aRecno[1] //Preenche apenas se for de origem de nota fiscal
		aAdd(aCab,{"N1_FORNEC" 	, SD1->D1_FORNECE					,NIL})
		aAdd(aCab,{"N1_LOJA" 	, SD1->D1_LOJA					,NIL})
		aAdd(aCab,{"N1_NSERIE" 	, SD1->D1_SERIE					,NIL})
		aAdd(aCab,{"N1_NFISCAL" 	, SD1->D1_DOC						,NIL})
		aAdd(aCab,{"N1_CHASSIS" 	, SD1->D1_CHASSI					,NIL})
		aAdd(aCab,{"N1_PLACA" 	, SD1->D1_PLACA					,NIL})
		aAdd(aCab,{"N1_NFESPEC" 	, SF1->F1_ESPECIE 				,NIL})
		aAdd(aCab,{"N1_NFITEM" 	, SD1->D1_ITEM 					,NIL})

		If cPaisLoc == "BRA" //Impostos Brasil
			cCstPis	:= SF4->F4_CSTPIS
			cCstCof	:= SF4->F4_CSTCOF

			//Realiza atribuição para os impostos encontrados no config. de tributos 
			cJsonTributos := a241fgTrib(SD1->D1_IDTRIB, {"000015","000016"})
			if !Empty(cJsonTributos)
				jTributosX := JsonObject():New()
				jTributosX:FromJson(cJsonTributos)

				aIDTributos := jTributosX:GetNames()
				for nTributos := 1 to len(aIDTributos)
					cIDTributo := aIDTributos[nTributos]

					IF cIDTributo == "000015" // pis
						cCstPis := jTributosX[cIDTributo]["cst"]
					ElseIF cIDTributo == "000016" // confis
						cCstCof := jTributosX[cIDTributo]["cst"]
					EndIf
				next nTributos

				FreeObj(jTributosX)
				fwFreeArray(aIDTributos)
			EndIf

			aAdd(aCab,{"N1_ICMSAPR"	, NoRound((nICMS / nAtfQtdIt),2)		,NIL})
			aAdd(aCab,{"N1_CSTPIS"  , cCstPis	 							,NIL})
			aAdd(aCab,{"N1_CSTCOFI" , cCstCof	 							,NIL})
			aAdd(aCab,{"N1_ALIQPIS" , SD1->&(cCpBsPisEn) 					,NIL})
			aAdd(aCab,{"N1_ALIQCOF" , SD1->&(cCpBsCofEn) 					,NIL})
			aAdd(aCab,{"N1_CODCIAP" , cCiap									,NIL})
			aAdd(aCab,{"N1_ORIGCRD" , If(Left(SD1->D1_CF,1)=="3","1","0")	,NIL})
		EndIf
	EndIf

	//Preenchimento das Variaveis referentes ao SN3
	If cPaisLoc == "BRA"
		aAdd(aAux,{"N3_TIPO",IIf( lComponent , "03","01"),NIL})
	Else
		aAdd(aAux,{"N3_TIPO","01",NIL} )
	EndIf
	aAdd(aAux,{"N3_CCONTAB"	, ""									, NIL}) 	// Nao grava este campo em hipotese alguma pois o controle de classificacao do Ativo
	aAdd(aAux,{"N3_CUSTBEM"	, SD3->D3_CC							, NIL})
	aAdd(aAux,{"N3_SUBCCON"	, SD3->D3_ITEMCTA						, NIL})
	aAdd(aAux,{"N3_CLVLCON"	, SD3->D3_CLVL						, NIL})

	nCusto:= Round(xMoeda(nCustoAux,nMoeda,1),aTamVOrig[1][2])
	aAdd(aAux,{"N3_VORIG1"	, nCusto	, NIL})

	nCusto:= Round(xMoeda(nCustoAux,nMoeda,2),aTamVOrig[2][2])
	aAdd(aAux,{"N3_VORIG2"	, nCusto	, NIL})

	nCusto:= Round(xMoeda(nCustoAux,nMoeda,3),aTamVOrig[3][2])
	aAdd(aAux,{"N3_VORIG3"	, nCusto	, NIL})

	nCusto:= Round(xMoeda(nCustoAux,nMoeda,4),aTamVOrig[4][2])
	aAdd(aAux,{"N3_VORIG4"	, nCusto	, NIL})

	nCusto:= Round(xMoeda(nCustoAux,nMoeda,5),aTamVOrig[5][2])
	aAdd(aAux,{"N3_VORIG5"	, nCusto	, NIL})

	If aRecno[1] //Preenche apenas se for de origem de nota fiscal
		// Tratamento para levar as entidades contabeis para classificacao de compras no ATF
		For nI := 5 to nQtdEnt
			cLoopEnt  := PADL(cValToChar(nI),2,"0")
			cEntConDB := "EC"+cLoopEnt+"DB"
			cEntConCR := "EC"+cLoopEnt+"CR"
			//Manter o filedpos pois as entidades contábeis são criadas pelo usuário.
			If SN3->(FieldPos("N3_"+cEntConDB)) > 0 .And. SN3->(FieldPos("N3_"+cEntConCR)) > 0
				aAdd(aAux,{"N3_"+cEntConDB, SD1->&("D1_"+cEntConDB), NIL  })
				aAdd(aAux,{"N3_"+cEntConCR, SD1->&("D1_"+cEntConCR), NIL  })
			EndIf
		Next nI
	EndIf

	aAdd(aItens,aAux)

	DbSelectArea("SN1")
	MSExecAuto({|x,y,z,w| Atfa012(x,y,z,w)},aCab,aItens,3,aParam)

	If lMsErroAuto
		lRet := .F.
		cFileLog := NomeAutoLog()
		If !Empty(cFileLog)
			If !IsBlind()
				MostraErro(cPath,cFileLog)
			EndIf
		Endif
	Else // SN1 é gerada com o valor de frete, mas a SD3 sempre gera com o custo medio
		nQtdSD3:= SD3->D3_QUANT
		aCMNF := PegaCMAtu(SD3->D3_COD,SD3->D3_LOCAL)
		nCustoAux:= NoRound((aCMNF[1]*nQtdSD3) ,2)

		aCMNF[1]:= Round(xMoeda(nCustoAux,nMoeda,1),aTamVOrig[1][2])
		aCMNF[2]:= Round(xMoeda(nCustoAux,nMoeda,2),aTamVOrig[2][2])
		aCMNF[3]:= Round(xMoeda(nCustoAux,nMoeda,3),aTamVOrig[3][2])
		aCMNF[4]:= Round(xMoeda(nCustoAux,nMoeda,4),aTamVOrig[4][2])
		aCMNF[5]:= Round(xMoeda(nCustoAux,nMoeda,5),aTamVOrig[5][2])
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A241GerImp()
Função que alimenta tabela de imposto para estorno de credito PIS/COFINS
@author Leonardo Quintania
@since 27/04/2015
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function A241GerImp(nValor,nDocumento,nImposto)
Local oModel  := FWLoadModel("FISA042")
Local nOpc 	:= 3
Local lRet		:= .T.

oModel:SetOperation(nOpc)

If (lRet := oModel:Activate()) //-- Ativa o modelo de dados

	oModel:SetValue("FISA042MOD","CF5_FILIAL",xFilial("CF5"))
	oModel:SetValue("FISA042MOD","CF5_INDAJU","0")
	oModel:SetValue("FISA042MOD","CF5_PISCOF",IIF(nImposto==1,"0","1"))
	oModel:SetValue("FISA042MOD","CF5_VALAJU",nValor)
	oModel:SetValue("FISA042MOD","CF5_CODAJU","06")
	oModel:SetValue("FISA042MOD","CF5_NUMDOC",nDocumento)
	oModel:SetValue("FISA042MOD","CF5_DESAJU","Ajuste de Credito devido a geração de ativo de um produto em estoque")
	oModel:SetValue("FISA042MOD","CF5_DTREF" ,dDataBase)

	If (lRet := oModel:VldData()) //-- Valida os dados e integridade conforme dicionario do Model
		lRet := oModel:CommitData() //-- Efetiva gravacao dos dados na tabela
	EndIf

	oModel:DeActivate() //-- Desativa o Model

EndIf

Return NIL

Function getvalid( cCampo )
Local aArea	 := GetArea()
Local cValid := IIf( !Empty( GetSx3Cache( cCampo, "X3_VALID" ) ), AllTrim( GetSx3Cache( cCampo, "X3_VALID" ) ), "" ) + IIf( !Empty( GetSx3Cache( cCampo, "X3_VLDUSER" ) ), " .And. " + AllTrim( GetSx3Cache( cCampo, "X3_VLDUSER" ) ), "" )

RestArea( aArea )
Return cValid

//-------------------------------------------------------------------
/*/{Protheus.doc} A241VLDFan()
Valida se produto é fantasma
@author Materiais
@since 07/12/2016
@version 1.0
/*/
//-------------------------------------------------------------------

Function A241VLDFan(cProdFan)

Local aArea	:= GetArea()
Local aAreaSB1:= SB1->(GetArea())
Local lRet := .T.
Default cProdFan  := " "

If !Empty(cProdFan)
	dbSelectArea("SB1")
	dbSetOrder(1)
	MsSeek(xFilial("SB1")+cProdFan)
	If RetFldProd(cProdFan,"B1_FANTASM") == "S"
		Help(" ",1,"A241PROFAN")
		lRet := .F.
	EndIf
EndIf

RestArea( aAreaSB1 )
RestArea( aArea )
Return(lRet)

/*/{Protheus.doc} A241GetEmp()
Obtem empenhos para sugestao de baixa aplicando regra
FEFO quando empenhos por lote (igual ao MATA250)
@author andre.oliveira
@since 26/03/2021
@version 1.0
/*/
Static Function A241GetEmp(aEstrutura,lSugSemSld)
	Local cWhere	:= "%%"
	Local cHaving	:= "%%"
	Local nPercOp	:= nQtdOrigEs/SC2->C2_QUANT
	Local nQtBaixa	:= 0
	Local nQuant	:= 0
	Local nTamEnder	:= Len(SDC->DC_LOCALIZ)
	Local nTamNSeri	:= Len(SDC->DC_NUMSERI)

	If Posicione("SF5",1,xFilial("SF5")+cTM,"F5_APROPR") == "N"
		cWhere := "%AND B1_APROPRI <> 'I'%"
	EndIf
	If !lSugSemSld
		cHaving := "%HAVING SUM(D4_QUANT) > 0%"
	EndIf

	//-- Query para somar as quantidades empenhadas desconsiderando as quebras da SD4 por lote
	BeginSQL Alias "SD4PAI"
		SELECT D4_COD, D4_TRT, SUM(D4_QTDEORI) D4_QTDEORI, SUM(D4_QUANT) D4_QUANT
		FROM %Table:SD4% SD4
		JOIN %Table:SB1% SB1 ON SB1.%NotDel% AND B1_FILIAL = %xFilial:SB1% AND B1_COD = D4_COD
		WHERE SD4.%NotDel% AND D4_FILIAL = %xFilial:SD4% AND D4_OP = %Exp:cOpEst% AND 1 = 1 %Exp:cWhere%
		GROUP BY D4_COD, D4_TRT
		%Exp:cHaving%
		ORDER BY D4_COD, D4_TRT
	EndSQL

	If !lSugSemSld
		cHaving := "%HAVING COALESCE(SUM(DC_QUANT),D4_QUANT) > 0%"
	EndIf

	While !SD4PAI->(EOF())
		//-- Estabele a quantida a baixar do item empenhado
		nQtBaixa := Min(SD4PAI->D4_QTDEORI * nPercOp,SD4PAI->D4_QUANT)

		//-- Query para obter a sequencia de baixa priorizando os lotes
		//-- com vencimento mais próximo: mesma lógica do MATA250
		BeginSQL Alias "SD4FILHO"
			SELECT B8_DTVALID, D4_LOCAL, D4_LOTECTL, D4_NUMLOTE, COALESCE(DC_LOCALIZ,'') LOCALIZ, COALESCE(DC_NUMSERI,'') NUMSERI,
				COALESCE(SUM(DC_QTDORIG),D4_QTDEORI) QTDEORI, COALESCE(SUM(DC_QUANT),D4_QUANT) QUANT
			FROM %Table:SD4% SD4
			LEFT JOIN %Table:SDC% SDC ON SDC.%NotDel% AND DC_FILIAL = %xFilial:SDC% AND DC_PRODUTO = D4_COD AND
				DC_LOCAL = D4_LOCAL AND DC_OP = D4_OP AND DC_TRT = D4_TRT AND DC_LOTECTL = D4_LOTECTL AND DC_NUMLOTE = D4_NUMLOTE
			LEFT JOIN %Table:SB8% SB8 ON SB8.%NotDel% AND B8_FILIAL = %xFilial:SB8% AND B8_PRODUTO = D4_COD AND
				B8_LOCAL = D4_LOCAL AND B8_LOTECTL = D4_LOTECTL AND B8_NUMLOTE = D4_NUMLOTE
			WHERE SD4.%NotDel% AND D4_FILIAL = %xFilial:SD4% AND D4_OP = %Exp:cOpEst% AND
				D4_COD = %Exp:SD4PAI->D4_COD% AND D4_TRT = %Exp:SD4PAI->D4_TRT%
			GROUP BY B8_DTVALID, D4_LOCAL, D4_LOTECTL, D4_NUMLOTE, DC_LOCALIZ, DC_NUMSERI, D4_QTDEORI, D4_QUANT
			%Exp:cHaving%
			ORDER BY B8_DTVALID, D4_LOCAL, D4_LOTECTL, D4_NUMLOTE, LOCALIZ, NUMSERI
		EndSQL

		TcSetField("SD4FILHO","B8_DTVALID","D",8,0)

		While !SD4FILHO->(EOF()) .And. nQtBaixa > 0
			nQuant := Min(nQtBaixa, Iif(lSugSemSld,SD4FILHO->QTDEORI,SD4FILHO->QUANT))
			nQtBaixa -= nQuant

			aAdd(aEstrutura,{NIL,NIL,SD4PAI->D4_COD,nQuant,SD4PAI->D4_TRT,SD4FILHO->D4_LOTECTL,SD4FILHO->D4_NUMLOTE,PadR(SD4FILHO->LOCALIZ,nTamEnder),PadR(SD4FILHO->NUMSERI,nTamNSeri),SD4FILHO->D4_LOCAL,SD4FILHO->B8_DTVALID})

			SD4FILHO->(dbSkip())
		End
		SD4FILHO->(dbCloseArea())

		SD4PAI->(dbSkip())
	End
	SD4PAI->(dbCloseArea())
Return

//---------------------------------------------------------------------
/*/{Protheus.doc} a241cDoc
Caso o número de doc informado seja o último registro possível e exista 
na SD3, realiza verificação do próximo número disponível diminuindo a
quantidade de registros processados para busca.

@author Miqueias Lima Coelho
@since 13/11/2023
@return caracter
/*/
//---------------------------------------------------------------------
Static __cQry261Doc
Function a241cDoc(cDocumento)

Local aRet 		:= Array(2)	as array
Local aSD3Area 	:= SD3->(GetArea())	as array
Local cAliasTop	:= "" 	as character
Local nTamSD3   := StrZero(0,TamSX3("D3_DOC")[1]) 	as numeric
Local nX		:= 1	as numeric
Local oBind		:= Nil	as object

If __oFilSD3 == NIL
	__oFilSD3 := JsonObject():New()
EndIf

If __oFilSD3[cFilAnt] == NIL
	__oFilSD3[cFilAnt] := FWxFilial('SD3')
EndIf

	If __cQry261Doc == Nil
		__cQry261Doc := "SELECT " + MatIsnull() + "(SD3.DOCMAX, '" + ntamSD3 + "') AS DOCMAX "
		__cQry261Doc += "FROM ( "
		__cQry261Doc += "  SELECT D3_DOC AS DOCMAX, "
		__cQry261Doc += "    ROW_NUMBER() OVER (ORDER BY D3_DOC DESC) AS LINHA "
		__cQry261Doc += " FROM " + RetSqlName("SD3")
		__cQry261Doc += " WHERE "
		__cQry261Doc += "		D3_FILIAL	= ? "
		__cQry261Doc += "	AND D3_DOC		>= ? "
		__cQry261Doc += "	AND D_E_L_E_T_	= ? "
		__cQry261Doc += ") SD3 "
		__cQry261Doc += "WHERE SD3.LINHA = 2 "
		__cQry261Doc := ChangeQuery(__cQry261Doc)
	EndIf

	oBind := FWExecStatement():New( changeQuery(__cQry261Doc) )
	oBind:setString(nX++, __oFilSD3[cFilAnt])
	oBind:setString(nX++, cDocumento)
	oBind:setString(nX++, ' ')

	cAliasTop := oBind:OpenAlias()

	oBind:destroy()

	aRet[1] := cDocumento									//aRet[1] == cDocumento
	aRet[2] := "SD3"+Alltrim(__oFilSD3[cFilAnt])+cDocumento //aRet[2] == cMay

	While (cAliasTop)->(DOCMAX) == aRet[1]
		aRet[1] := Soma1(aRet[1])
		aRet[2] := "SD3"+Alltrim(__oFilSD3[cFilAnt])+aRet[1]
		(cAliasTop)->(dbSkip())
	EndDo

	(cAliasTop)->(dbCloseArea())
	
	RestArea(aSD3Area)

Return aRet

//---------------------------------------------------------------------
/*/{Protheus.doc} A241EmpSup
Retorna a verificação logica da variável statica em casos de requisição
com quantidade maior que a quantidade empenhada na OP e que tenha saldo
disponivel em estoque para atender a requisição excedente sem empenho.

Parametro ExpC1 = cProduto+cLocal+cOP+cTrt+cLoteCtl+cNumLote+cLocaliz+cNumSerie

@author Leonardo Kichitaro
@since 30/07/2025
@return Lógico
/*/
//---------------------------------------------------------------------
Function A241EmpSup(cChaveEmp)

	Local lRet := .F.

	If !Empty(AScan(__aEmpSupOP, cChaveEmp))
		lRet := .T.
	EndIf

Return lRet

//---------------------------------------------------------------------
/*/{Protheus.doc} a241fgTrib
Verifica utilização do configurador de tributos.

@author Leonardo Kichitaro
@since 19/08/2025
@return cRetJson
/*/
//---------------------------------------------------------------------
static Function a241fgTrib(cIdTribCT As Character, aTributos as array) as character

	Local oDados         as object
	Local cJsonDoc       as character
	Local oJson          as object
	Local cRegra_trib    as character
	Local cIDTributo     as character
	Local cCST			 as character
	Local nI             as numeric
	Local oJsonRetorno   as object
	Local cRetJson       as character
	Local aDados		 as array
	Local aAreaF2B as array
	Local aAreaD4H as array
	Local cOperador as character

	Default cIdTribCT	:= ""
	Default aTributos	:= {}

	if __lTCIWritten == nil
		__lTCIWritten := FindClass("totvs.protheus.backoffice.fiscal.tciclass.TCIWritten")
	EndIf

	if __lCustoRegra == NIL
		__lCustoRegra := .F.
		if fwAliasInDic("D4H") // Tabela de cadastro de Regras do Custo
			dbSelectArea("F2B")
			if F2B->(FieldPos("F2B_RCUSTO"))>0 // campo referencia da regra de custo a ser utilizada no tributo
				__lCustoRegra := .T.
			endif
		endif
	endif

	// Verifica se a classe do Configurador de Tributos existe no repositório.
	If __lTCIWritten .And. !Empty(cIdTribCT)
		oJsonRetorno := JsonObject():New()

		oDados := totvs.protheus.backoffice.fiscal.tciclass.TCIWritten():New()
		oDados:SetId({cIdTribCT})

		cJsonDoc := oDados:GetDataId()

		oJson := JsonObject():New()
		oJson:FromJson(cJsonDoc)

		If oJson:HasProperty("dados_Id")
			If oJson["dados_Id"]:HasProperty(cIdTribCT)
				aDados:= oJson["dados_Id"][cIdTribCT]:GetNames()
				For nI:=1 To Len(aDados)
					cCST		:= ""
					cRegra_trib := aDados[nI]
					If oJson["dados_Id"][cIdTribCT]:HasProperty(cRegra_trib)
						If oJson["dados_Id"][cIdTribCT][cRegra_trib]:HasProperty("codigo_tributo_relacionado")
							cIDTributo := oJson["dados_Id"][cIdTribCT][cRegra_trib]["codigo_tributo_relacionado"]
						EndIF
						cOperador := ""
						if __lCustoRegra // Tabela de cadastro de Regras do Custo
							//
							// busca pela regra de calculo de documentos fiscais
							//
							// busca pela regra de calculo de documentos fiscais ativa
							if oJson["dados_Id"][cIdTribCT][cRegra_trib]:HasProperty("id_cadastro_regra_tributo")
								dbSelectArea("F2B")
								aAreaF2B := F2B->(GetArea())
								F2B->(dbSetOrder(3)) // F2B_FILIAL+F28_ID
								if F2B->(MSSeek(xFilial("F2B")+PADR(oJson["dados_Id"][cIdTribCT][cRegra_trib]["id_cadastro_regra_tributo"],TamSX3("F28_ID")[1])+"2"))
									if !Empty(F2B->F2B_RCUSTO)
										// busca pela regra de custo
										dbSelectArea("D4H")
										aAreaD4H := D4H->(GetArea())
										D4H->(dbSetOrder(1)) // D4H_FILIAL+D4H_REGRA
										if D4H->(MSSeek(xFilial("D4H")+F2B->F2B_RCUSTO))
											cOperador := D4H->D4H_OPERA
										EndIf
										RestArea(aAreaD4H)
										aSize(aAreaD4H,0)
										aAreaD4H := NIL
									EndIf
									RestArea(aAreaF2B)
									aSize(aAreaF2B,0)
									aAreaF2B := NIL
								EndIf
							EndIf
						EndIF

						if oJson["dados_Id"][cIdTribCT][cRegra_trib]:HasProperty("dados_escriturados")
							cCST := oJson["dados_Id"][cIdTribCT][cRegra_trib]["dados_escriturados"]["cst"]
						EndIf
						if aScan(aTributos,{|x|x == cIDTributo })>0
							oJsonRetorno[cIDTributo] := JsonObject():New()
							oJsonRetorno[cIDTributo]["operador_custo"] := cOperador
							oJsonRetorno[cIDTributo]["cst"] := cCST
						EndIf
					EndIf
				Next nI
			EndIf
		EndIf

		fwFreeArray(aDados)

		cRetJson := oJsonRetorno:ToJson()

		oDados:Destroy()
		FreeObj(oJsonRetorno)
		FreeObj(oJson)
	EndIf

Return cRetJson
