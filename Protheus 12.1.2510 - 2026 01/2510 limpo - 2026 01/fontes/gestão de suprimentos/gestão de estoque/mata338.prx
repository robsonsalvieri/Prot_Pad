#INCLUDE "PROTHEUS.CH"
#INCLUDE "MATA338.CH"
/*


Ŀ
Funao     MATA338   Autor  Totvs S/A              Data  22/01/10 
Ĵ
Descrio  Rotina de Reavaliacao de Custo de Estoque                  
Ĵ
Parametros Nenhum                                                     
Ĵ
 Uso       MATA338                                                    
ٱ

*/
Function MATA338(xRotAuto,xOpcAuto)


//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 -Pesquisa e Posiciona em um Banco de Dados              
//    2 -Simplesmente Mostra os Campos                          
//    3 -Inclui registros no Bancos de Dados                    
//    4 -Altera o registro corrente                             
//    5 -Estorna registro selecionado gerando uma contra-partida
//
Private aRotina   := MenuDef()
Private l338Auto  := xRotAuto <> NIL
Private aRotAuto  := xRotAuto
Private nOpcAuto  := xOpcAuto
Private cCadastro := STR0001 //"Reavalio de Custo de Estoque"

If l338Auto
	MsRotAuto(If(nOpcAuto!=Nil,nOpcAuto,3),aRotAuto,"SDQ")
Else
	mBrowse( 6, 1,22,75,"SDQ")
EndIf	

Return

/*

Ŀ
Funao    A338Inclui Autor  Totvs S/A              Data  22/01/10 
Ĵ
Descrio  Funcao utilizada para inclusao de registros                
Ĵ
Sintaxe    A338Inclui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       MATA338                                                    
ٱ


*/
Function A338Inclui(cAlias,nReg,nOpc)
Local nOpca       := 0
Local nX          := 0
Local cNumSeq     := 0
Local aAcho       := {}
Local cDocumento  := ''
Local cMoeda338C  := SuperGetMv('MV_MOEDACM',.F.,"2345")

// Verifica se utiliza todas moedas
aAdd(aACho,"DQ_COD"		)
aAdd(aACho,"DQ_LOCAL"	)
aAdd(aACho,"DQ_DATA"	)
aAdd(aACho,"DQ_CM1"		)
If '2' $ cMoeda338C
	aAdd(aAcho,"DQ_CM2"	)
EndIf	
If '3' $ cMoeda338C
	aAdd(aAcho,"DQ_CM3"	)
EndIf
If '4' $ cMoeda338C
	aAdd(aAcho,"DQ_CM4"	)
EndIf
If '5' $ cMoeda338C
	aAdd(aAcho,"DQ_CM5"	)
EndIf

If cPaisLoc == "BOL"
	aAdd(aAcho,"DQ_INDICE"	)
EndIf	

dbSelectArea("SDQ")
If ( l338Auto )
	nOpca := AxIncluiAuto(cAlias,"A338TudoOk()",Nil,nOpc)
Else
	nOpca := AxInclui(cAlias,nReg,nOpc,aAcho,,,"A338TudoOk()")
EndIf

// Posiciona no Produto 
dbSelectArea("SB1")
SB1->(dbSetOrder(1))
MsSeek(xFilial("SB1")+SDQ->DQ_COD)

// Inclui os movimentos na SD3	
If nOpca == 1
	
	cDocumento := NextNumero("SD3",2,"D3_DOC",.T.)
	cDocumento := A261RetINV(cDocumento)
	Begin Transaction 
		cNumSeq := ProxNum()
		RecLock("SDQ",.F.)
		Replace DQ_NUMSEQ With cNumSeq
		MsUnLock()
		For nX := 1 to 6
			// Verifica se moeda devera ser considerada 	
			If nx # 1 .And. !(Str(nx,1,0) $ cMoeda338C)
    	      	Loop
			EndIf
			If	 cPaisLoc == "BOL" .And. FieldPos("DQ_INDICE") > 0 .And. SDQ->DQ_INDICE > 0
				RecLock("SD3",.T.)
				SD3->D3_FILIAL	:= xFilial("SD3")
				SD3->D3_COD		:= SDQ->DQ_COD
				SD3->D3_LOCAL		:= SDQ->DQ_LOCAL
				SD3->D3_DOC		:= cDocumento
				SD3->D3_TM			:= "999"
				SD3->D3_CF			:= "REA"
				SD3->D3_EMISSAO	:= SDQ->DQ_DATA
				SD3->D3_NUMSEQ	:= cNumSeq
				SD3->D3_MOEDA   	:= Str(nX,1)
				SD3->D3_INDICE  	:= SDQ->DQ_INDICE 
				SD3->D3_USUARIO	:= CUSERNAME
				SD3->D3_TIPO		:= SB1->B1_TIPO
				SD3->D3_GRUPO		:= SB1->B1_GRUPO
				SD3->D3_UM			:= SB1->B1_UM
				MsUnlock()		
			Else			
				RecLock("SD3",.T.)
				SD3->D3_FILIAL	:= xFilial("SD3")
				SD3->D3_COD		:= SDQ->DQ_COD
				SD3->D3_LOCAL		:= SDQ->DQ_LOCAL
				SD3->D3_DOC		:= cDocumento
				SD3->D3_TM			:= "999"
				SD3->D3_CF			:= "REA"
				SD3->D3_EMISSAO	:= SDQ->DQ_DATA
				SD3->D3_NUMSEQ	:= cNumSeq
				SD3->D3_MOEDA		:= Str(nX,1)
				SD3->D3_CMFIXO	:= &("SDQ->DQ_CM"+Str(nX,1))
				SD3->D3_USUARIO	:= CUSERNAME
				SD3->D3_TIPO		:= SB1->B1_TIPO
				SD3->D3_GRUPO		:= SB1->B1_GRUPO
				SD3->D3_UM			:= SB1->B1_UM
				MsUnlock()		
			EndIf
		Next nX
	End Transaction	
EndIf
dbclosearea("SB1")
dbSelectArea( cAlias )
Return

/*

Ŀ
Funao    A338Altera Autor  Totvs S/A              Data  22/01/10 
Ĵ
Descrio  Funcao utilizada para alteracao de registros               
Ĵ
Sintaxe    A338Altera(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       MATA338                                                    
ٱ


*/
Function A338Altera(cAlias,nReg,nOpc)
Local cSeek      := ''
Local aAcho      := {}
Local lRet       := .T.
Local aAreaAnt   := GetArea()
Local aAreaSD3   := SD3->(GetArea())
Local dUlmes     := GetMv("MV_ULMES")
Local cMoeda338C := SuperGetMv('MV_MOEDACM',.F.,"2345")

// Verifica se utiliza todas moedas
aAdd(aACho,"DQ_COD"		)
aAdd(aACho,"DQ_LOCAL"	)
aAdd(aACho,"DQ_DATA"	)
aAdd(aACho,"DQ_CM1"		)
If '2' $ cMoeda338C
	aAdd(aAcho,"DQ_CM2"	)
EndIf	
If '3' $ cMoeda338C
	aAdd(aAcho,"DQ_CM3"	)
EndIf
If '4' $ cMoeda338C
	aAdd(aAcho,"DQ_CM4"	)
EndIf
If '5' $ cMoeda338C
	aAdd(aAcho,"DQ_CM5"	)
EndIf	

If cPaisLoc == "BOL"
	aAdd(aAcho,"DQ_INDICE"	)
EndIf	

//-- Verifica a data de fechamento de estoque
If SDQ->DQ_DATA <= dUlMes
	Help(' ',1,'A338FCA')
	lRet := .F.
EndIf

If lRet
	If ( l338Auto )
		nOpca := AxIncluiAuto(cAlias,Nil,Nil,nOpc,nReg)
	Else	                                                         
		nOpca := AxAltera(cAlias,nReg,nOpc,aAcho)
	EndIf	
	If nOpca == 1
		//Ŀ
		// Alterar o registro de reavaliacao de custo 
		//
		Begin Transaction
			dbSelectArea("SD3")
			dbSetOrder(3) //D3_FILIAL+D3_COD+D3_LOCAL+D3_NUMSEQ
			dbSeek(cSeek := xFilial("SD3")+SDQ->DQ_COD+SDQ->DQ_LOCAL+SDQ->DQ_NUMSEQ)
			Do While !Eof() .And. cSeek == SD3->D3_FILIAL+SD3->D3_COD+SD3->D3_LOCAL+SD3->D3_NUMSEQ
				If SD3->D3_CF $ "REA|DEA"
					RecLock("SD3",.F.)
					Replace D3_CMFIXO With &("SDQ->DQ_CM"+SD3->D3_MOEDA)
					MsUnLock()
				EndIf
				dbSkip()
			EndDo	
		End Transaction	
	EndIf
EndIf
RestArea(aAreaSD3)
RestArea(aAreaAnt)
Return

/*

Ŀ
Funao    A338Exclui| Autor  Marcos V. Ferreira     Data  22/01/10 
Ĵ
Descrio  Funcao utilizada para exclusao de registros                
Ĵ
Sintaxe    A338Exclui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       MATA338                                                    
ٱ


*/
Function A338Exclui(cAlias,nReg,nOpc)
Local cCod       := ''
Local cLocal     := ''
Local cProduto   := ''
Local cSeek      := ''
Local nOpcA      := 0
Local lRet       := .T.
Local aAreaAnt   := GetArea()
local aAreaSD3   := SD3->(GetArea())
Local aAC        := {STR0007,STR0008} // "Abandona"###"Confirma"
Local cTexto     := STR0009  // "Quanto a excluso?"
Local oDlg  
Local aObjects    := {}
Local aSize       := {} 
Local aInfo       := {}
Local aPosObj     := {}

Private aTELA[0][0]
Private aGETS[0]

//-- Verifica a data de fechamento de estoque
If SDQ->DQ_DATA <= GetMv("MV_ULMES")
	Help(' ',1,'A338FCE')
	lRet := .F.
EndIf      

//Ŀ
// Ajusta a largura para o tamanho padrao Protheus 
//
aSize := MsAdvSize()       

aObjects := {}
AAdd( aObjects, { 100, 100, .T., .T. } )
aInfo    := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 } 
	
aPosObj := MsObjSize( aInfo, aObjects, .T.) 

If lRet 
	If !l338Auto
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL 
		nOpcA := EnChoice(cAlias, nReg, nOpc, aAC,'AC',cTexto,,aPosObj[1])
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
	Else
		//Ŀ
		// Exclui os registro passados pela rotina automatica  
		// sem apresentar tela para confirmacao da operacao    
		//
		nOpcA := 2
	EndIf

	If nOpcA == 2
		//Ŀ
		// Excluir o registro de reavaliacao de custo 
		//
		Begin Transaction
			dbSelectArea("SD3")
			dbSetOrder(3) //D3_FILIAL+D3_COD+D3_LOCAL+D3_NUMSEQ
			dbSeek(cSeek := xFilial("SD3")+SDQ->DQ_COD+SDQ->DQ_LOCAL+SDQ->DQ_NUMSEQ)
			Do While !Eof() .And. cSeek == SD3->D3_FILIAL+SD3->D3_COD+SD3->D3_LOCAL+SD3->D3_NUMSEQ
				If SD3->D3_CF $ "REA|DEA"
					RecLock("SD3",.F.)
					dbDelete()
					MsUnLock()
				EndIf
				dbSkip()
			EndDo
			dbSelectArea('SDQ')
			RecLock('SDQ', .F.)
			dbDelete()
			MsUnlock()
		End Transaction	
	EndIf
EndIf

RestArea(aAreaSD3)
RestArea(aAreaAnt)
Return lRet

/*

Ŀ
Funao    A338TudoOk| Autor  Totvs S/A              Data  22/01/10 
Ĵ
Descrio  Funcao utilizada para validacao da inclusao de registros   
Ĵ
Sintaxe    A338TudoOk()                                               
Ĵ
 Uso       MATA338                                                    
ٱ


*/
Function A338TudoOk()
Local lRet      := .T.
Local aAreaAnt  := GetArea()

//-- Nao incluir Movimentacoes geradas pela Virada de Saldos
If M->DQ_DATA <= GetMv("MV_ULMES")
	Help(' ',1,"A338FCI")
	lRet := .F.
EndIf

// Verifica se ja existe o registro cadastrado
SDQ->(dbSetOrder(1))
If SDQ->(dbSeek(xFilial("SDQ")+M->DQ_COD+M->DQ_LOCAL+DTOS(M->DQ_DATA)))	
	Help(' ',1,'A338CADA')
   	lRet := .F.
EndIf

SB2->(dbSetOrder(1))
If !SB2->(dbSeek(xFilial("SB2")+M->DQ_COD+M->DQ_LOCAL))
	Help(' ',1,"A338NSB2")
	lRet := .F.
EndIf
RestArea(aAreaAnt)
Return lRet

/*/


Ŀ
Programa  MenuDef    Autor  Microsiga S/A          Data 22/01/2010
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados    	      
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/                 
Static Function MenuDef()
Private aRotina	:= { 	{STR0002,"AxPesqui"  , 0 , 1, 0,.F.},; 	//"Pesquisar"
	     				{STR0003,"AxVisual", 0 , 2, 0,nil},;     	//"Visualizar"
	     				{STR0004,"A338Inclui", 0 , 3, 0,nil},;     //"Incluir"
	     				{STR0005,"A338Altera", 0 , 4, 0,nil},;	 	//"Alterar"
	     				{STR0006,"A338Exclui", 0 , 5, 3,nil} }		//"Excluir"
If ExistBlock ("MTA338MNU")		      
	ExecBlock ("MTA338MNU",.F.,.F.)
Endif 

Return (aRotina)

/*

Ŀ
Funao    A338Indice Autor  Totvs S/A              Data  07/01/14 
Ĵ
Descrio  Funcao utilizada para retornar o indice                    
Ĵ
Sintaxe    A338Inclui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros                                                            
Ĵ
 Uso       MATA338                                                    
ٱ


*/
Function A338Indice()

Local cMes		:= Alltrim(StrZERO(month(M->DQ_DATA),2))
Local nIndice		:= 0
local cAno  		:= Alltrim(Str(year(M->DQ_DATA)))
Local cMesAnt		:= Alltrim(StrZERO(month((M->DQ_DATA))-1,2))
local cAnoAnt		:= cAno
Local nIndiceAnt	:= 0
Local lachou		:= .T.
Local nIndAtu		:= 0

If cMes=="01"//Alltrim(StrZERO(month((M->DQ_DATA))-1,2)) =="12"
	cAnoAnt:= Alltrim(Str(year(M->DQ_DATA)-1))
	cMesAnt :="12"	
EndIf

dbSelectArea("SIE")
dbSetOrder(1) 
If dbSeek(xFilial("SIE")+cAno+cMes)
	nIndice:= SIE->IE_INDICE
Else
	lachou		:=.F.
	MsgAlert(" Nao achou a taxa de conversao do mes atual","Alerta")
EndIf


dbSelectArea("SIE")
dbSetOrder(1) 
If dbSeek(xFilial("SIE")+cAnoAnt+cMesAnt)
	nIndiceAnt:= SIE->IE_INDICE
Else
	lachou		:=.F.
	MsgAlert(" Nao achou a taxa de conversao do mes anterior","Alerta")
EndIf


If lachou	
	nIndAtu:= nIndice	/nIndiceAnt
EndIf

M->DQ_INDICE  := nIndAtu

Return()

