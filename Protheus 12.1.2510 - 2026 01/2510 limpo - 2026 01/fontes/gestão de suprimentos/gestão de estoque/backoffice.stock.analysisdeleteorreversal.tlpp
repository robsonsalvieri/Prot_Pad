#include "totvs.ch"
#include "protheus.ch"
#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.stock.analysisdeleteorreversal.ch"

namespace totvs.protheus.backoffice.stock.analysisdeleteorreversal

/*/{Protheus.doc} className
    Classe para avaliar e pemitir o usuário de selecionar endereços
	ao excluir ou estornar a classificação de um Documento de Entrada quando um ou mais itens
	passaram a ter controle de localização ativo após a entrada da Nota Fiscal.
@author Everton Fregonezi Diniz
@since 19/08/2025
/*/
class DocumentDeleteOrReversal

	private data aArea		as array
	private data cAlsOrig	as character
    private data dUlMes		as date
    private data jDocument	as json
	private data lLocaliz	as logical
	
	public data cMark		as character
	
	protected Method new()	CONSTRUCTOR
    protected Method getDocumentInfo(cNumSeq as character, cDocumen as character, cSerie as character, cTipo as character, cCliFor as character, cLjCliFor as character)
    protected Method itemLocalizationValid()	as logical
	
	protected Method ApplyFilterInBrw2(lIsApply as logical, oTblBrw1 as object, oTblBrw2 as object, oBrwDwn as object) as logical
	protected Method CanApplyMarkBrwLocaliz(oTblBrw1 as object, oTblBrw2 as object, oBrwDwn as object) as logical
	protected Method UpdateSaldoBrw1(oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object)
	protected Method CanCloseDlg(cAcao as character, oTblBrw1 as object) as logical
	protected Method localizationMovementsAdjust(oTblBrw1 as object, oTblBrw2 as object)
	
	private Method selectLocalization()		as logical
    private Method restoreOpenArea()
	private Method getOpenArea()
    private Method getAliasBrowseUp(aField as array)	as object
    private Method getAliasBrowseDown(aField as array)	as object
    private Method getFormBrowseUp(aField as array, oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object, oWINTOP as object)	as object
    private Method getFormBrowseDown(aField as array, oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object, oWINDWN as object)	as object

endclass


/*/{Protheus.doc} New
	Método para instanciamento da classe
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method New() Class DocumentDeleteOrReversal
	
	self:aArea := {}
	self:cMark := ""
	self:dUlMes	  := SuperGetMV("MV_ULMES",.F.,STOD("19800101"))

	if isIncallStack('MATA103') .Or. isIncallStack('MATA140')
		self:cAlsOrig := "SD1"
	endif

	self:jDocument := jsonObject():New()
	self:lLocaliz := SuperGetMV("MV_LOCALIZ",.F.,"N") == "S"

	self:getOpenArea()

Return self


/*/{Protheus.doc} getDocumentInfo
	Método para receber as informações do documento de entrada
	e gravar em formato jSon para utilização nos métodos da classe.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getDocumentInfo(cNumSeq as character, cDocumen as character, cSerie as character, cTipo as character, cCliFor as character, cLjCliFor as character) Class DocumentDeleteOrReversal
	
default	cDocumen	:= ""
default	cNumSeq		:= ""
default	cSerie		:= ""
default	cTipo		:= ""
default	cCliFor		:= ""
default	cLjCliFor	:= ""

	if !empty(cNumSeq)
		self:jDocument['DOCNUMSEQ']	:= cNumSeq
	endif

	if !empty(cDocumen)
		self:jDocument['DOCUMENT']	:= cDocumen
	endif

	if !empty(cSerie)
		self:jDocument['DOCSERIE']	:= cSerie
	endif

	if !empty(cTipo)
		self:jDocument['DOCTIPO']	:= cTipo
	endif

	if !empty(cCliFor)
		self:jDocument['DOCFORCLI']	:= cCliFor
	endif

	if !empty(cLjCliFor)
		self:jDocument['DOCLOJA']	:= cLjCliFor
	endif

Return


/*/{Protheus.doc} getOpenArea
	Método para gravar os posicionamentos das tabelas abertas antes da classe
	ser instanciada.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getOpenArea() Class DocumentDeleteOrReversal
	
	if self:cAlsOrig == "SD1"
		self:aArea := {SD1->(getArea()), SD3->(getArea()), SDA->(getArea()), SDB->(getArea()), getArea()}
	endif

Return


/*/{Protheus.doc} restoreOpenArea
	Método para restaurar os posicionamentos das tabelas abertas antes da classe
	ser instanciada e devolver ao processamento inicial.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method restoreOpenArea() Class DocumentDeleteOrReversal
	aEval(self:aArea, {|x| restArea(x)} )
Return


/*/{Protheus.doc} ItemLocalizationValid
	Método para validar a existências dos produtos que possuem controle de localização
	mas que não houve movimentação através das tabelas de endereçamento vinculados ao Documento.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method ItemLocalizationValid() as logical Class DocumentDeleteOrReversal

local cAlsQry	:= ""	as character
local cQuery	:= ""	as character
local lEstorna	:= .F.	as logical
local lRet		:= .T.	as logical
local nCount	:= 1	as numeric
local oBind		:= Nil	as object

	if self:lLocaliz

		dbSelectArea("SDA")
		SDA->(dbSetOrder(1))	// DA_FILIAL+DA_PRODUTO+DA_LOCAL+DA_NUMSEQ+DA_DOC+DA_SERIE+DA_CLIFOR+DA_LOJA
		
		dbSelectArea("SDB")
		SDB->(dbSetOrder(1))	// DB_FILIAL+DB_PRODUTO+DB_LOCAL+DB_NUMSEQ+DB_DOC+DB_SERIE+DB_CLIFOR+DB_LOJA+DB_ITEM
		
		if self:cAlsOrig == "SD1"
			
			cQuery := " SELECT	D1_COD "
			cQuery += " 	,	COUNT(SDA.R_E_C_N_O_) SDACOUNT "
			cQuery += " 	,	COUNT(SDB.R_E_C_N_O_) SDBCOUNT "
			cQuery += " 	,	COALESCE(SUM(BF_QUANT - BF_EMPENHO),0) SBFSUM "
			cQuery += " FROM   " + RetSQLName("SD1") + " SD1 "

			cQuery += " INNER JOIN " + RetSQLName("SB1") + " SB1 "
			cQuery += " 	ON	B1_FILIAL		= ? " 
			cQuery += " 	AND	B1_COD			= D1_COD "
			cQuery += "		AND B1_LOCALIZ		= ? "
			cQuery += " 	AND SB1.D_E_L_E_T_	= ? "

			cQuery += " INNER JOIN " + RetSQLName("SF4") + " SF4 "
			cQuery += " 	ON	F4_FILIAL		= ? " 
			cQuery += " 	AND	F4_CODIGO		= D1_TES "
			cQuery += " 	AND	F4_ESTOQUE		= ? "
			cQuery += " 	AND SF4.D_E_L_E_T_	= ? "

			cQuery += " LEFT JOIN " + RetSQLName("SDA") + " SDA "
			cQuery += " 	ON	DA_FILIAL		= ? "
			cQuery += "		AND	DA_PRODUTO 		= D1_COD "
			cQuery += "		AND	DA_LOCAL		= D1_LOCAL "
			cQuery += "		AND	DA_NUMSEQ		= D1_NUMSEQ "
			cQuery += "		AND	DA_DOC			= D1_DOC "
			cQuery += "		AND	DA_SERIE		= D1_SERIE "
			cQuery += "		AND	DA_CLIFOR		= D1_FORNECE "
			cQuery += "		AND	DA_LOJA			= D1_LOJA "
			cQuery += "		AND	DA_LOTECTL    	= D1_LOTECTL "
			cQuery += "		AND DA_NUMLOTE		= D1_NUMLOTE "
			cQuery += "		AND	SDA.D_E_L_E_T_	= ? "

			cQuery += "	LEFT JOIN " + RetSQLName("SDB") + " SDB "
			cQuery += "		ON	DB_FILIAL   	= ? "
			cQuery += "		AND	DB_PRODUTO  	= D1_COD "
			cQuery += "		AND	DB_LOCAL    	= D1_LOCAL "
			cQuery += "		AND	DB_NUMSEQ   	= D1_NUMSEQ "
			cQuery += "		AND	DB_DOC      	= D1_DOC "
			cQuery += "		AND	DB_SERIE    	= D1_SERIE "
			cQuery += "		AND	DB_CLIFOR   	= D1_FORNECE "
			cQuery += "		AND	DB_LOJA     	= D1_LOJA "
			cQuery += "		AND	DB_ESTORNO  	= ? "
			cQuery += "		AND	DB_LOTECTL    	= D1_LOTECTL "
			cQuery += "		AND DB_NUMLOTE		= D1_NUMLOTE "
			cQuery += "		AND	SDB.D_E_L_E_T_	= ? "

			cQuery += "	LEFT JOIN " + RetSQLName("SBF") + " SBF "
			cQuery += "		ON	BF_FILIAL   	= ? "
			cQuery += "		AND	BF_PRODUTO  	= D1_COD "
			cQuery += "		AND	BF_LOCAL    	= D1_LOCAL "
			cQuery += "		AND	BF_LOTECTL    	= D1_LOTECTL "
			cQuery += "		AND	BF_NUMLOTE    	= D1_NUMLOTE "
			cQuery += "		AND	BF_QUANT    	> ? "
			cQuery += "		AND	SBF.D_E_L_E_T_	= ? "

			cQuery += "	WHERE	D1_FILIAL		= ? "
			cQuery += "		AND D1_DOC			= ? "
			cQuery += "		AND D1_SERIE		= ? "
			cQuery += "		AND D1_FORNECE		= ? "
			cQuery += "		AND D1_LOJA			= ? "
			cQuery += " 	AND	D1_TIPO			= ? "
			cQuery += "		AND SD1.D_E_L_E_T_	= ? "

			cQuery += "	GROUP BY D1_COD "

			oBind := FWExecStatement():New( changeQuery(cQuery) )
			oBind:setString(nCount++, FWxFilial("SB1"))
			oBind:setString(nCount++, 'S')
			oBind:setString(nCount++, ' ')
			oBind:setString(nCount++, FWxFilial("SF4"))
			oBind:setString(nCount++, 'S')
			oBind:setString(nCount++, ' ')
			oBind:setString(nCount++, FWxFilial("SDA"))
			oBind:setString(nCount++, ' ')
			oBind:setString(nCount++, FWxFilial("SDB"))
			oBind:setString(nCount++, 'S')
			oBind:setString(nCount++, ' ')
			oBind:setString(nCount++, FWxFilial("SBF"))
			oBind:SetNumeric(nCount++, 0)
			oBind:setString(nCount++, ' ')
			oBind:setString(nCount++, FWxFilial("SD1"))
			oBind:setString(nCount++, self:jDocument['DOCUMENT'])
			oBind:setString(nCount++, self:jDocument['DOCSERIE'])
			oBind:setString(nCount++, self:jDocument['DOCFORCLI'])
			oBind:setString(nCount++, self:jDocument['DOCLOJA'])
			oBind:setString(nCount++, self:jDocument['DOCTIPO'])
			oBind:setString(nCount++, ' ')
			cAlsQry := oBind:OpenAlias()
			oBind:destroy()
		
			while (cAlsQry)->(!EOF())
				if (cAlsQry)->SDACOUNT == 0 .and. (cAlsQry)->SDBCOUNT == 0 .and. (cAlsQry)->SBFSUM > 0
					lEstorna := .T.
					exit
				endif
				(cAlsQry)->(dbSkip())
			enddo

			(cAlsQry)->(DbCloseArea())
			
			if lEstorna
				if lRet := FwAlertYesNo(STR0002,STR0001)	// "Identificamos um ou mais itens que passaram a controlar localização após a entrada deste Documento de Entrada. Para prosseguir com o estorno/exclusão do documento, É OBRIATÓRIO, selecionar endereços válidos e com saldos a serem estornados." # "ATENÇÃO"
					lRet := self:selectLocalization()
				endif
			endif
		endif
		
	endif

	self:restoreOpenArea()

Return lRet


/*/{Protheus.doc} selectLocalization
	Método para montagem das tabelas temporárias, carregamento das browsers para
	interagir com o usuário e permitir que os endereços com saldos sejam selecionados
	para movimentação de estorno.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method selectLocalization()	as logical Class DocumentDeleteOrReversal

local aSize			as array
local aDlgWnd		as array
local aFldBrw1		as array
local aFldBrw2		as array
local lRet	:= .T.	as logical

local oBrwUp		as object
local oBrwDwn		as object
local oDlgEnd		as object
local oDlgWnd		as object
local oLayer		as object
local oTblBrw1		as object
local oTblBrw2		as object
local oWINTOP		as object
local oWINDWN		as object

	self:cMark := GetMark()

	if self:cAlsOrig == "SD1"

		// Campos para o Browse 1
		aFldBrw1 := {"D1_ITEM","D1_COD","B1_DESC","D1_UM","D1_LOCAL","D1_LOTECTL","D1_NUMLOTE","D1_QUANT","D1SALDO"}
		
		// Campos para o Browse 2
		aFldBrw2 := {"MARK","BF_PRODUTO","BF_LOCAL","BF_LOCALIZ","BF_PRIOR","BF_LOTECTL","BF_NUMLOTE","BF_NUMSERI","BF_QUANT","BF_EMPENHO","BFSALDO"}
	
	endif

	oTblBrw1 := self:getAliasBrowseUp(aFldBrw1)
	oTblBrw2 := self:getAliasBrowseDown(aFldBrw2)
	
	//-----------------------------------
	// Montagem do dialog				|
	//-----------------------------------
	aSize	:= FWGetDialogSize( oMainWnd )
	aDlgWnd := Array(2)
	oDlgEnd := MSDialog():New(aSize[1],aSize[2],aSize[3],aSize[4],"",,,,,,,,oMainWnd,.T.,,,,)
	
	oDlgWnd		:= FWFormContainer():New(oDlgEnd)
	aDlgWnd[1]	:= oDlgWnd:CreateHorizontalBox(40)
	aDlgWnd[2]	:= oDlgWnd:CreateHorizontalBox(50)
	oDlgWnd:Activate(oDlgEnd,.F.)
	
	oWINTOP	:= oDlgWnd:GetPanel( aDlgWnd[1] )
	oWINDWN	:= oDlgWnd:GetPanel( aDlgWnd[2] )
	
	//-----------------------------------
	// Montagem do Browse 1				|
	//-----------------------------------
	self:getFormBrowseUp(aFldBrw1, oTblBrw1, oTblBrw2, @oBrwUp, @oBrwDwn, oWINTOP)
	
	//-----------------------------------
	// Montagem do Browse 2				|
	//-----------------------------------
	self:getFormBrowseDown(aFldBrw2, oTblBrw1, oTblBrw2, @oBrwUp, @oBrwDwn, oWINDWN)
    
	oBrwDwn:Activate()
	oBrwUp:Activate()

	oDlgEnd:Activate(,,,.T.,,,{|| EnchoiceBar(oDlgEnd,	{|| if( self:CanCloseDlg("OK",oTblBrw1),(self:ApplyFilterInBrw2(.F.,oTblBrw1,oTblBrw2,oBrwDwn), Processa({|| self:localizationMovementsAdjust(oTblBrw1, oTblBrw2)},STR0014,STR0005), oDlgEnd:End()), Nil ) },;	// "Aguarde" # "Iniciando Estornos.."
														{|| if( self:CanCloseDlg("CANCEL",oTblBrw1),(lRet := .F., oDlgEnd:End()), Nil )};
											 )})
	oTblBrw1:Delete()
	oTblBrw2:Delete()

	oBrwUp:DeActivate()
    oBrwUp:Destroy()

	oBrwDwn:DeActivate()

	fwFreeArray(aDlgWnd)
	fwFreeArray(aFldBrw1)
	fwFreeArray(aFldBrw2)

	freeObj(oBrwUp)
	freeObj(oBrwDwn)
	freeObj(oDlgEnd)
	freeObj(oLayer)

Return lRet


/*/{Protheus.doc} getAliasBrowseUp
	Método para montagem da tabela temporária utilizada pela browse superior
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getAliasBrowseUp(aField as array) as object	Class DocumentDeleteOrReversal

local aColumn	:= {}	as array
local cQuery	:= ""	as character
local cAlsQry	:= ""	as character
local nCount	:= 1	as numeric
local oBind		:= Nil	as object
local oTable	:= Nil	as object

	if self:cAlsOrig == "SD1"
		
		aEval(aField, {|x| AADD(aColumn,{x ,; 
										getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_TIPO') ,;
										getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_TAMANHO') ,;
										getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_DECIMAL') }) })
				
		cQuery := " SELECT "
		cQuery += " 		SD1.D1_ITEM "
		cQuery += " 	,	SD1.D1_COD "
		cQuery += " 	,	SB1.B1_DESC "
		cQuery += " 	,	SD1.D1_UM "
		cQuery += " 	,	SD1.D1_LOCAL "
		cQuery += " 	,	SD1.D1_LOTECTL "
		cQuery += " 	,	SD1.D1_NUMLOTE "
		cQuery += " 	,	SD1.D1_QUANT "
		cQuery += " 	,	0	D1SALDO "
		cQuery += " FROM " + RetSQLName("SD1") + " SD1 "
		
		cQuery += "	INNER JOIN " + RetSQLName("SB1") + " SB1 "
		cQuery += "		ON	B1_FILIAL			= ? "
		cQuery += "		AND	B1_COD				= SD1.D1_COD "
		cQuery += "		AND	B1_LOCALIZ			= ? "
		cQuery += "		AND	SB1.D_E_L_E_T_		= ? "

		cQuery += " INNER JOIN " + RetSQLName("SF4") + " SF4 "
		cQuery += " 	ON	F4_FILIAL		= ? " 
		cQuery += " 	AND	F4_CODIGO		= D1_TES "
		cQuery += " 	AND	F4_ESTOQUE		= ? "
		cQuery += " 	AND SF4.D_E_L_E_T_	= ? "

		cQuery += "	INNER JOIN " + RetSQLName("SBF") + " SBF "
		cQuery += "		ON	SBF.BF_FILIAL		= ? "
		cQuery += "		AND	SBF.BF_PRODUTO		= SD1.D1_COD "
		cQuery += "		AND	SBF.BF_LOCAL		= SD1.D1_LOCAL "
		cQuery += "		AND	SBF.BF_LOTECTL		= SD1.D1_LOTECTL "
		cQuery += "		AND	SBF.BF_NUMLOTE		= SD1.D1_NUMLOTE "
		cQuery += "		AND	(SBF.BF_QUANT - SBF.BF_EMPENHO)	> ? "
		cQuery += "		AND	SBF.D_E_L_E_T_		= ? "

		cQuery += " WHERE "
		cQuery += " 		D1_FILIAL		= ? "
		cQuery += " 	AND	D1_DOC			= ? "
		cQuery += " 	AND	D1_SERIE		= ? "
		cQuery += " 	AND	D1_FORNECE		= ? "
		cQuery += " 	AND	D1_LOJA			= ? "
		cQuery += " 	AND	D1_TIPO			= ? "
		cQuery += "	AND	SD1.D_E_L_E_T_	= ? "
		cQuery += "	AND NOT EXISTS ( SELECT SDB.DB_PRODUTO FROM "+RetSqlName("SDB")+" SDB "
		cQuery += " 		WHERE	SDB.DB_FILIAL		= ? "
		cQuery += " 				AND SDB.DB_PRODUTO 	= SD1.D1_COD "
		cQuery += " 				AND SDB.DB_LOCAL	= SD1.D1_LOCAL "
		cQuery += " 				AND SDB.DB_NUMSEQ	= SD1.D1_NUMSEQ "
		cQuery += " 				AND SDB.DB_DOC		= SD1.D1_DOC "
		cQuery += " 				AND SDB.DB_SERIE	= SD1.D1_SERIE "
		cQuery += " 				AND SDB.DB_CLIFOR	= SD1.D1_FORNECE "
		cQuery += " 				AND SDB.DB_LOJA		= SD1.D1_LOJA "
		cQuery += " 				AND SDB.DB_LOTECTL	= SD1.D1_LOTECTL "
		cQuery += " 				AND SDB.DB_NUMLOTE	= SD1.D1_NUMLOTE "
		cQuery += " 				AND SDB.D_E_L_E_T_	= ?	) "
		cQuery += " GROUP BY SD1.D1_ITEM, SD1.D1_COD, SB1.B1_DESC, SD1.D1_UM, SD1.D1_LOCAL, SD1.D1_LOTECTL, SD1.D1_NUMLOTE, SD1.D1_QUANT "
		cQuery += " ORDER BY SD1.D1_ITEM "

		oBind := FWExecStatement():New( changeQuery(cQuery) )
		oBind:setString(nCount++, FWxFilial("SB1"))
		oBind:setString(nCount++, 'S')
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SF4"))
		oBind:setString(nCount++, 'S')
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SBF"))
		oBind:SetNumeric(nCount++, 0)
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SD1"))
		oBind:setString(nCount++, self:jDocument['DOCUMENT'])
		oBind:setString(nCount++, self:jDocument['DOCSERIE'])
		oBind:setString(nCount++, self:jDocument['DOCFORCLI'])
		oBind:setString(nCount++, self:jDocument['DOCLOJA'])
		oBind:setString(nCount++, self:jDocument['DOCTIPO'])
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SDB"))
		oBind:setString(nCount++, ' ')
		cAlsQry := oBind:OpenAlias()
		oBind:destroy()

		oTable := FWTemporaryTable():New()
		oTable:SetFields(aColumn)
		oTable:Create()

		(cAlsQry)->(DbEval(	{ ||(oTable:GetAlias())->(DbAppend()),; 
								aEval(aField, {|x| (oTable:GetAlias())->&(x) := (cAlsQry)->&(x) }),;
								(oTable:GetAlias())->(DbCommit()) },;
								,;
							{ || (cAlsQry)->(!Eof()) }))
		
		(cAlsQry)->(dbCloseArea())

	endif

	FwFreeArray(aColumn)

Return oTable


/*/{Protheus.doc} getAliasBrowseDown
	Método para montagem da tabela temporária utilizada pela browse inferior
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getAliasBrowseDown(aField as array)	as object	Class DocumentDeleteOrReversal 

local aColumn	:= {}	as array
local cQuery	:= ""	as character
local cAlsQry	:= ""	as character
local nCount	:= 1	as numeric
local oBind		:= Nil	as object
local oTable	:= Nil	as object

	if self:cAlsOrig == "SD1"
	
		aEval(aField, {|x| AADD(aColumn,{x ,; 
										if( x == 'MARK' , "C", getSX3cache(if(x == 'BFSALDO', 'BF_QUANT', x),'X3_TIPO')) ,;
										if( x == 'MARK' , 2, getSX3cache(if(x == 'BFSALDO', 'BF_QUANT', x),'X3_TAMANHO')) ,;
										if( x == 'MARK' , 0, getSX3cache(if(x == 'BFSALDO', 'BF_QUANT', x),'X3_DECIMAL')) }) })

		cQuery := " SELECT "
		cQuery += " 		'  ' MARK "
		cQuery += " 	,	SBF.BF_PRODUTO "
		cQuery += " 	,	SBF.BF_LOCAL "
		cQuery += " 	,	SBF.BF_LOCALIZ "
		cQuery += " 	,	SBF.BF_PRIOR "
		cQuery += " 	,	SBF.BF_LOTECTL "
		cQuery += " 	,	SBF.BF_NUMLOTE "
		cQuery += " 	,	SBF.BF_NUMSERI "
		cQuery += " 	,	SBF.BF_QUANT "
		cQuery += " 	,	SBF.BF_EMPENHO "
		cQuery += " 	,	(BF_QUANT - BF_EMPENHO) BFSALDO"
		cQuery += " FROM " + RetSQLName("SD1") + " SD1 "
		
		cQuery += "	INNER JOIN " + RetSQLName("SB1") + " SB1 "
		cQuery += "		ON	B1_FILIAL			= ? "
		cQuery += "		AND	B1_COD				= SD1.D1_COD "
		cQuery += "		AND	B1_LOCALIZ			= ? "
		cQuery += "		AND	SB1.D_E_L_E_T_		= ? "

		cQuery += " INNER JOIN " + RetSQLName("SF4") + " SF4 "
		cQuery += " 	ON	F4_FILIAL		= ? " 
		cQuery += " 	AND	F4_CODIGO		= D1_TES "
		cQuery += " 	AND	F4_ESTOQUE		= ? "
		cQuery += " 	AND SF4.D_E_L_E_T_	= ? "
		
		cQuery += "	INNER JOIN " + RetSQLName("SBF") + " SBF "
		cQuery += "		ON	SBF.BF_FILIAL		= ? "
		cQuery += "		AND	SBF.BF_PRODUTO		= SD1.D1_COD "
		cQuery += "		AND	SBF.BF_LOCAL		= SD1.D1_LOCAL "
		cQuery += "		AND	SBF.BF_LOTECTL		= SD1.D1_LOTECTL "
		cQuery += "		AND	SBF.BF_NUMLOTE		= SD1.D1_NUMLOTE "
		cQuery += "		AND	(SBF.BF_QUANT - SBF.BF_EMPENHO)	> ? "
		cQuery += "		AND	SBF.D_E_L_E_T_		= ? "

		cQuery += " WHERE "
		cQuery += " 		D1_FILIAL		= ? "
		cQuery += " 	AND	D1_DOC			= ? "
		cQuery += " 	AND	D1_SERIE		= ? "
		cQuery += " 	AND	D1_FORNECE		= ? "
		cQuery += " 	AND	D1_LOJA			= ? "
		cQuery += " 	AND	D1_TIPO			= ? "
		cQuery += "	AND	SD1.D_E_L_E_T_	= ? "
		cQuery += "	AND NOT EXISTS ( SELECT SDB.DB_PRODUTO FROM "+RetSqlName("SDB")+" SDB "
		cQuery += " 		WHERE	SDB.DB_FILIAL		= ? "
		cQuery += " 				AND SDB.DB_PRODUTO 	= SD1.D1_COD "
		cQuery += " 				AND SDB.DB_LOCAL	= SD1.D1_LOCAL "
		cQuery += " 				AND SDB.DB_NUMSEQ	= SD1.D1_NUMSEQ "
		cQuery += " 				AND SDB.DB_DOC		= SD1.D1_DOC "
		cQuery += " 				AND SDB.DB_SERIE	= SD1.D1_SERIE "
		cQuery += " 				AND SDB.DB_CLIFOR	= SD1.D1_FORNECE "
		cQuery += " 				AND SDB.DB_LOJA		= SD1.D1_LOJA "
		cQuery += " 				AND SDB.DB_LOTECTL	= SD1.D1_LOTECTL "
		cQuery += " 				AND SDB.DB_NUMLOTE	= SD1.D1_NUMLOTE "
		cQuery += " 				AND SDB.D_E_L_E_T_	= ?	) "
		cQuery += " ORDER BY D1_ITEM, BF_PRIOR, BF_LOCALIZ, BF_LOTECTL, BF_NUMSERI "

		oBind := FWExecStatement():New( changeQuery(cQuery) )
		oBind:setString(nCount++, FWxFilial("SB1"))
		oBind:setString(nCount++, 'S')
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SF4"))
		oBind:setString(nCount++, 'S')
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SBF"))
		oBind:SetNumeric(nCount++, 0)
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SD1"))
		oBind:setString(nCount++, self:jDocument['DOCUMENT'])
		oBind:setString(nCount++, self:jDocument['DOCSERIE'])
		oBind:setString(nCount++, self:jDocument['DOCFORCLI'])
		oBind:setString(nCount++, self:jDocument['DOCLOJA'])
		oBind:setString(nCount++, self:jDocument['DOCTIPO'])
		oBind:setString(nCount++, ' ')
		oBind:setString(nCount++, FWxFilial("SDB"))
		oBind:setString(nCount++, ' ')
		cAlsQry := oBind:OpenAlias()
		oBind:destroy()

		oTable := FWTemporaryTable():New()
		oTable:SetFields(aColumn)
		oTable:Create()

		(cAlsQry)->(DbEval(	{ ||(oTable:GetAlias())->(DbAppend()),; 
								aEval(aField, {|x| (oTable:GetAlias())->&(x) := (cAlsQry)->&(x) }),;
								(oTable:GetAlias())->(DbCommit()) },;
								,;
							{ || (cAlsQry)->(!Eof()) }))
		
		(cAlsQry)->(dbCloseArea())

	endif

	FwFreeArray(aColumn)

Return oTable


/*/{Protheus.doc} getFormBrowseUp
	Método para montagem da browse superior
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getFormBrowseUp(aField as array, oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object, oWINTOP as object) as object Class DocumentDeleteOrReversal

local aColumn	:= {}	as array

	if self:cAlsOrig == "SD1"
	
		oBrwUp := FWFormBrowse():New()
		oBrwUp:SetOwner(oWINTOP)
		oBrwUp:SetDescription(STR0003)	// "Itens do Documento de Entrada"
		oBrwUp:SetDataTable()
		oBrwUp:SetAlias(oTblBrw1:GetAlias())
		oBrwUp:AddLegend("D1SALDO == 0","RED",STR0004)	// "Item não atendido"
		oBrwUp:AddLegend("D1SALDO > 0 .AND. D1SALDO < D1_QUANT","YELLOW",STR0005) // "Item parcialmente atendido"
		oBrwUp:AddLegend("D1SALDO == D1_QUANT","GREEN",STR0006) // "Item atendido"
		oBrwUp:SetChange({||self:ApplyFilterInBrw2(.T.,oTblBrw1,oTblBrw2,oBrwDwn)})

		aEval(aField, {|x| aAdd( aColumn, FWBrwColumn():New() ) ,;
							aColumn[Len(aColumn)]:SetData( &("{ || " + x + " }")) ,;
							aColumn[Len(aColumn)]:SetTitle( if( x == 'D1SALDO', STR0007, RetTitle(x) ) )														,;	// "Saldo a Estornar"
							aColumn[len(aColumn)]:SetType( getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_TIPO'))											,;
							aColumn[Len(aColumn)]:SetSize( getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_TAMANHO'))										,;
							aColumn[Len(aColumn)]:SetDecimal( getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_DECIMAL'))										,;
							aColumn[Len(aColumn)]:SetPicture( getSX3cache(if( x == 'D1SALDO', 'D1_QUANT', x),'X3_PICTURE'))										,;
							aColumn[Len(aColumn)]:SetAlign( if( x == 'D1SALDO' .or. getSX3cache(x,'X3_TIPO') == "N", CONTROL_ALIGN_RIGHT, CONTROL_ALIGN_LEFT) )	,;
							aColumn[Len(aColumn)]:SetID( x ) })
		
		oBrwUp:SetColumns(aColumn)
		oBrwUp:DisableConfig()
		oBrwUp:DisableDetails()
	
	endif

	FwFreeArray(aColumn)

Return


/*/{Protheus.doc} getFormBrowseDown
	Método para montagem da browse inferior
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method getFormBrowseDown(aField as array, oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object, oWINDWN as object) as object Class DocumentDeleteOrReversal

local aColumn	:= {}	as array

	if self:cAlsOrig == "SD1"
	
		oBrwDwn := FWMarkBrowse():New()
		oBrwDwn:SetOwner(oWINDWN)
		oBrwDwn:SetDescription(STR0008) // "Endereços Disponíveis"
		oBrwDwn:SetDataTable()
		oBrwDwn:SetAlias(oTblBrw2:GetAlias())
		oBrwDwn:AddMarkColumns(	{||if((oTblBrw2:GetAlias())->MARK == self:cMark,"LBOK","LBNO")}	,;
								{||if(self:CanApplyMarkBrwLocaliz(oTblBrw1,oTblBrw2,oBrwDwn),(self:UpdateSaldoBrw1(oTblBrw1,oTblBrw2,oBrwUp,oBrwDwn),oBrwUp:Refresh(),oBrwDwn:Refresh()),Nil) })
		
		aEval(aField, {|x| if( x == 'MARK', Nil, ( aAdd( aColumn, FWBrwColumn():New() ) ,;
							aColumn[Len(aColumn)]:SetData( &("{ || " + x + " }")) ,;
							aColumn[Len(aColumn)]:SetTitle( if(x == 'BFSALDO', STR0009, RetTitle(x)))															,; // "Saldo P/ Estorno"
							aColumn[len(aColumn)]:SetType( getSX3cache(if( x == 'BFSALDO', 'BF_QUANT', x),'X3_TIPO'))											,;
							aColumn[Len(aColumn)]:SetSize( getSX3cache(if( x == 'BFSALDO', 'BF_QUANT', x),'X3_TAMANHO'))										,;
							aColumn[Len(aColumn)]:SetDecimal( getSX3cache(if( x == 'BFSALDO', 'BF_QUANT', x),'X3_DECIMAL'))										,;
							aColumn[Len(aColumn)]:SetPicture( getSX3cache(if( x == 'BFSALDO', 'BF_QUANT', x),'X3_PICTURE'))										,;
							aColumn[Len(aColumn)]:SetAlign( if( x == 'BFSALDO' .or. getSX3cache(x,'X3_TIPO') == "N", CONTROL_ALIGN_RIGHT, CONTROL_ALIGN_LEFT) )	,;
							aColumn[Len(aColumn)]:SetID( x ) )) })
		
		oBrwDwn:SetColumns(aColumn)
		oBrwDwn:SetMenuDef("")
		oBrwDwn:SetIgnoreARotina(.T.)
		oBrwDwn:DisableFilter()
		oBrwDwn:DisableConfig()
		oBrwDwn:DisableDetails()
	
	endif

	FwFreeArray(aColumn)

Return


/*/{Protheus.doc} localizationMovementsAdjust
	Método para processar os endereços selecionados pelo usuário e 
	gerar movimentações de endereços com estorno do saldo.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method localizationMovementsAdjust(oTblBrw1 as object, oTblBrw2 as object) Class DocumentDeleteOrReversal

local cItem		as character
local lEstorno	as logical
local nCount	as numeric
local nQuant	as numeric
local nQtd2UM	as numeric

	cItem		:= ""
	lEstorno	:= .T.
	nCount		:= 0

	if self:cAlsOrig == "SD1"
		
		dbSelectArea("SD1")
		SD1->(dbSetOrder(1))	// D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_COD+D1_ITEM

	endif

	(oTblBrw1:GetAlias())->(DbGoTop())
	(oTblBrw1:GetAlias())->(DbEval({|| nCount++ },,{||(oTblBrw1:GetAlias())->(!EOF()) }))
	(oTblBrw1:GetAlias())->(DbGoTop())

	ProcRegua(nCount)

	while (oTblBrw1:GetAlias())->(!EOF())
		
		if self:cAlsOrig == "SD1"

			cFilter := (oTblBrw2:GetAlias()) + "->MARK = '"			+ self:cMark						+ "' .AND. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_PRODUTO = '"	+ (oTblBrw1:GetAlias())->D1_COD		+ "' .AND. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_LOCAL = '"		+ (oTblBrw1:GetAlias())->D1_LOCAL	+ "' .AND. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_LOTECTL = '"	+ (oTblBrw1:GetAlias())->D1_LOTECTL	+ "' .AND. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_NUMLOTE = '"	+ (oTblBrw1:GetAlias())->D1_NUMLOTE	+ "' "
			
			(oTblBrw2:GetAlias())->(DbClearFilter())
			(oTblBrw2:GetAlias())->(DbSetFilter(&('{||'+cFilter+'}') , ""))
			(oTblBrw2:GetAlias())->(DbGoTop())

			if SD1->(MsSeek( FWxFilial("SD1") + self:jDocument['DOCUMENT'] + self:jDocument['DOCSERIE'] + self:jDocument['DOCFORCLI'] + self:jDocument['DOCLOJA'] + (oTblBrw1:GetAlias())->( D1_COD + D1_ITEM ) ))
				
				IncProc(STR0017 + (oTblBrw1:GetAlias())->D1_ITEM)	// "Estornando item: "

				CriaSDA(self:cAlsOrig, (oTblBrw1:GetAlias())->D1SALDO)
				
				Reclock("SDA",.F.)
				SDA->DA_SALDO := 0
				SDA->(MsUnlock())
				
				cItem := A265UltIt('C')
				
				while (oTblBrw2:GetAlias())->(!EOF())

					if (oTblBrw2:GetAlias())->BFSALDO == 0
						nQuant := (oTblBrw2:GetAlias())->(BF_QUANT - BF_EMPENHO)
					else
						nQuant := (oTblBrw2:GetAlias())->(BF_QUANT - BF_EMPENHO - BFSALDO)
					endif

					nQtd2UM := ConvUm(SD1->D1_COD,nQuant,nQtd2UM,2)

					CriaSDB(SD1->D1_COD												,;
							SD1->D1_LOCAL											,;
							nQuant													,;
							(oTblBrw2:GetAlias())->BF_LOCALIZ						,;
							(oTblBrw2:GetAlias())->BF_NUMSERI						,;
							SDA->DA_DOC												,;
							SDA->DA_SERIE											,;
							SDA->DA_CLIFOR											,;
							SDA->DA_LOJA											,;
							SDA->DA_TIPONF											,;
							SDA->DA_ORIGEM											,;
							if(SDA->DA_DATA > self:dUlMes, SDA->DA_DATA, dDataBase)	,;
							SDA->DA_LOTECTL											,;
							SDA->DA_NUMLOTE											,;
							SDA->DA_NUMSEQ											,;
							"499"													,;
							"M"														,;
							StrZero(Val(cItem)+1, Len(cItem))						,;
							lEstorno												,;
							if( QtdComp(SDA->DA_EMPENHO) > QtdComp(0), nQuant, 0 )	,;
							nQtd2UM)
					
					CriaSDB(SD1->D1_COD												,;
							SD1->D1_LOCAL											,;
							nQuant													,;
							(oTblBrw2:GetAlias())->BF_LOCALIZ						,;
							(oTblBrw2:GetAlias())->BF_NUMSERI						,;
							SDA->DA_DOC												,;
							SDA->DA_SERIE											,;
							SDA->DA_CLIFOR											,;
							SDA->DA_LOJA											,;
							SDA->DA_TIPONF											,;
							SDA->DA_ORIGEM											,;
							if(SDA->DA_DATA > self:dUlMes, SDA->DA_DATA, dDataBase)	,;
							SDA->DA_LOTECTL											,;
							SDA->DA_NUMLOTE											,;
							SDA->DA_NUMSEQ											,;
							"999"													,;
							"M"														,;
							StrZero(Val(cItem)+1, Len(cItem))						,;
							lEstorno												,;
							if( QtdComp(SDA->DA_EMPENHO) > QtdComp(0), nQuant, 0 )	,;
							nQtd2UM)

					GravaSBF("SDB",,,SDB->(RECNO()))

					(oTblBrw2:GetAlias())->(dbSkip())
				enddo
			endif
		
		endif
	
		(oTblBrw1:GetAlias())->(dbSkip())
	
	enddo
	
Return


/*/{Protheus.doc} ApplyFilterInBrw2
	Método para aplicar o filtro do posicionado pela browse superior.
	Usado no método SetChange() da primeira browse.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method ApplyFilterInBrw2(lIsApply as logical, oTblBrw1 as object, oTblBrw2 as object, oBrwDwn as object) as logical Class DocumentDeleteOrReversal 

local cFilter	:= ""	as character

	if self:cAlsOrig == "SD1"
		
		if lIsApply
			cFilter := (oTblBrw2:GetAlias()) + "->BF_PRODUTO = '"	+ (oTblBrw1:GetAlias())->D1_COD		+ "' .and. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_LOCAL = '"		+ (oTblBrw1:GetAlias())->D1_LOCAL	+ "' .and. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_LOTECTL = '"	+ (oTblBrw1:GetAlias())->D1_LOTECTL	+ "' .and. "
			cFilter += (oTblBrw2:GetAlias()) + "->BF_NUMLOTE = '"	+ (oTblBrw1:GetAlias())->D1_NUMLOTE	+ "' "
		endif
	endif
		
	oBrwDwn:SetFilterDefault(cFilter)
	
	if lIsApply
		oBrwDwn:Refresh(.T.)
	endif

return


/*/{Protheus.doc} CanApplyMarkBrwLocaliz
	Método para marcar/desmarcar a seleção do endereço na browse inferior.
	Utilizado no método AddMarkColumns() da browse inferior.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method CanApplyMarkBrwLocaliz(oTblBrw1 as object, oTblBrw2 as object, oBrwDwn as object) as logical Class DocumentDeleteOrReversal 

local lMark			as logical
local lOk	:= .T.	as logical

	lMark := empty((oTblBrw2:GetAlias())->MARK)

	if self:cAlsOrig == "SD1"
	
		if lMark .and. (oTblBrw1:GetAlias())->(D1_QUANT == D1SALDO)
			lOk := .F.
			FwAlertWarning(STR0010,STR0001)	// "Produto com saldo para estorno já atendido." # "ATENÇÃO"
		endif
	
	endif

	if lOk		
		RecLock(oTblBrw2:GetAlias(),.F.)
		(oTblBrw2:GetAlias())->MARK := if(lMark, self:cMark, "  ")
		(oTblBrw2:GetAlias())->(MsUnLock())
	endif

return lOk


/*/{Protheus.doc} UpdateSaldoBrw1
	Método para controle dos saldos a serem estornados após a marcação/desmarcação do endereço.
	Utilizado no método AddMarkColumns() da browse inferior.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method UpdateSaldoBrw1(oTblBrw1 as object, oTblBrw2 as object, oBrwUp as object, oBrwDwn as object) Class DocumentDeleteOrReversal 

local nSaldo	as numeric

	if self:cAlsOrig == "SD1"
		
		if !empty((oTblBrw2:GetAlias())->MARK)
			if (oTblBrw1:GetAlias())->(D1_QUANT - D1SALDO) >= (oTblBrw2:GetAlias())->BFSALDO
				nSaldo := (oTblBrw2:GetAlias())->BFSALDO
			else
				nSaldo := (oTblBrw1:GetAlias())->(D1_QUANT - D1SALDO)
			endif
			(oTblBrw1:GetAlias())->D1SALDO += nSaldo
			(oTblBrw2:GetAlias())->BFSALDO -= nSaldo
		else
			if (oTblBrw1:GetAlias())->D1SALDO <= (oTblBrw2:GetAlias())->(BF_QUANT - BF_EMPENHO - BFSALDO)
				nSaldo := (oTblBrw1:GetAlias())->D1SALDO
			else
				nSaldo := (oTblBrw2:GetAlias())->(BF_QUANT - BF_EMPENHO - BFSALDO)
			endif

			(oTblBrw1:GetAlias())->D1SALDO -= nSaldo
			(oTblBrw2:GetAlias())->BFSALDO += nSaldo
		endif
	endif

return


/*/{Protheus.doc} CanCloseDlg
	Método para validar se o usuário pode ou não seguir com o processo de estorno/exclusão do documento.
@author Everton Fregonezi Diniz
@since 21/08/2025
@version version
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method CanCloseDlg(cAcao as character, oTblBrw1 as object) as logical Class DocumentDeleteOrReversal 

local lRet	:= .T.	as logical

	if cAcao == "OK"
		if self:cAlsOrig == "SD1"
			(oTblBrw1:GetAlias())->(dbGoTop())
			while (oTblBrw1:GetAlias())->(!EOF())
				if !((oTblBrw1:GetAlias())->D1SALDO == (oTblBrw1:GetAlias())->D1_QUANT)
					lRet := .F.
					FwAlertWarning(STR0011 + (oTblBrw1:GetAlias())->D1_ITEM + STR0012,STR0001) // "O item <item> " do documento não está atendido totalmente. Para prosseguir, o item deve estar com a legenda na cor verde."	# 'ATENÇÃO'
					exit
				endif
				(oTblBrw1:GetAlias())->(dbSkip())
			enddo
		endif

		if lRet .And. !FwAlertYesNo(STR0016,STR0001)	// "Iniciar estornos dos endereços selecionados?" # "ATENÇÃO"
			lRet := .F.
		endif

	elseif cAcao == "CANCEL"
		if self:cAlsOrig == "SD1"
			if self:cAlsOrig == "SD1" .and. !FwAlertYesNo(STR0013,STR0001) // "Deseja interromper o processo e cancelar o estorno/exclusão do documento de entrada?" # "ATENÇÃO"
				lRet := .F.
			endif
		endif
	endif

Return lRet
