#INCLUDE "MATA390.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWADAPTEREAI.CH"

Static __lPergPtc := NIL

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё MATA390  Ё Autor Ё Gilson do Nascimento  Ё Data Ё 20/09/94 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Manutencoes nas Movimentacoes de Lote (SD5)    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function MATA390(xRotAuto,nOpc)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define Variaveis                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local cNumLote	:= ""
Local cProduto	:= ""
Local cLocal		:= ""
Local cLoteCtl	:= ""
Local cNumSeq		:= ""
Local nNumLote	:= 0
Local nProduto	:= 0
Local nLocal		:= 0
Local nLoteCtl	:= 0
Local nNumSeq		:= 0

Private aRotAuto	:= {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica utilizacao de rotina automatica                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
nOpc := If(xRotAuto == Nil, Nil, If(nOpc == Nil, 3, nOpc))

PRIVATE l390Auto := ( xRotAuto <> NIL )
PRIVATE aRotina := MenuDef()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Array contendo os campos aceitos no MATXATU                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE aAcho[11]

aAcho[01] := "D5_PRODUTO"
aAcho[02] := "D5_LOCAL"
aAcho[03] := "D5_DOC"
aAcho[04] := "D5_SERIE"
aAcho[05] := "D5_DATA"
aAcho[06] := "D5_QUANT"
aAcho[07] := "D5_LOTECTL"
aAcho[08] := "D5_DTVALID"
aAcho[09] := "D5_QTSEGUM"
aAcho[10] := "D5_LOTEFOR"
aAcho[11] := "D5_POTENCI"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Define o cabecalho da tela de atualizacoes                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
PRIVATE cCadastro := OemToAnsi(STR0005)		//"Manuten┤└o de Lotes"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Deixa o SD5 em ordem de Numero de Lote                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SD5")
dbSetOrder(2)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Endereca a funcao de BROWSE                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If l390Auto
	aRotAuto := xRotAuto
	If nOpc <> 3 //Posicionar registro
		nNumLote	:= aScan(aRotAuto,{|x| x[1] == "D5_NUMLOTE"})
		If nNumLote > 0
			cNumLote := PadR(aRotAuto[nNumLote,2],TamSx3("D5_NUMLOTE")[1])
		Endif

		nProduto	:= aScan(aRotAuto,{|x| x[1] == "D5_PRODUTO"})
		If nProduto > 0
			cProduto := PadR(aRotAuto[nProduto,2],TamSx3("D5_PRODUTO")[1])
		Endif

		nLocal		:= aScan(aRotAuto,{|x| x[1] == "D5_LOCAL"})
		If nLocal > 0
			cLocal := PadR(aRotAuto[nLocal,2],TamSx3("D5_LOCAL")[1])
		Endif

		nLoteCtl	:= aScan(aRotAuto,{|x| x[1] == "D5_LOTECTL"})
		If nLoteCtl > 0
			cLoteCtl := PadR(aRotAuto[nLoteCtl,2],TamSx3("D5_LOTECTL")[1])
		Endif

		nNumSeq	:= aScan(aRotAuto,{|x| x[1] == "D5_NUMSEQ"})
		If nNumSeq > 0
			cNumSeq := PadR(aRotAuto[nNumSeq,2],TamSx3("D5_NUMSEQ")[1])
		Endif

		If SD5->(DbSeek(xFilial("SD5") + cProduto + cLocal + cLoteCtl + cNumLote + cNumSeq))
			MBrowseAuto(nOpc, aRotAuto,"SD5", .F., .F.  )
		Else
			HELP(" ",1,"RECNO",)
		Endif
	Else
		MsRotAuto(nOpc,aRotAuto,"SD5")
	Endif
Else
	SetKey(VK_F12, {||MTA390PERG()})
	mBrowse(6,1,22,75,"SD5")
	SetKey(VK_F12, {||})
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Recupera a Integridade dos dados                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SD5")
dbSetOrder(1)
Return

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390VisualЁ Autor Ё Gilson do Nascimento  Ё Data Ё 30/09/94 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa para Visualizacao da Moviment. de Lotes           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Visual(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390Visual(cAlias,nReg,nOpc)
Local aObjects	:= {}
Local aPosObj	:={}
Local aSize		:= MsAdvSize()
Local aInfo		:= {aSize[1],aSize[2],aSize[3],aSize[4],3,3}
Local aVisual   := {}

Private aHeader := {}
Private aCols	:= {}

dbSelectArea("SB8")
dbSetOrder(2) //B8_FILIAL+B8_NUMLOTE+B8_LOTECTL+B8_PRODUTO+B8_LOCAL
If MsSeek(xFilial("SB8")+SD5->D5_NUMLOTE+SD5->D5_LOTECTL+SD5->D5_PRODUTO+SD5->D5_LOCAL,.F.)
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Configura variaveis da Enchoice                                           Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	RegtoMemory("SB8", .F., .T.,)
	cSeek 	   := xFilial( "SB8" ) + SD5->(D5_NUMLOTE+D5_LOTECTL+D5_PRODUTO+D5_LOCAL)
	bSeekWhile := { || B8_FILIAL+B8_NUMLOTE+B8_LOTECTL+B8_PRODUTO+B8_LOCAL }

	FillGetDados(nOpc, "SB8", 2, cSeek, bSeekWhile,,,,,,,,,,)

	AAdd( aObjects, { 100, 100, .T., .T. } )
	AAdd( aObjects, { 100, 100, .T., .T.} )
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 5, 5 }
	aPosObj := MsObjSize( aInfo, aObjects,.T.)

	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0005) OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta a Enchoice                                                          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oEnchoice:= MsMGet():New("SB8", 1, nOpc,,,,aVisual,aPosObj[1],,nOpc,,,,,,.T. )
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta a GetDados                                                          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oGet := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AllwaysTrue","AllwaysTrue","",.F.,,,)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})
Else
	Help(' ',1,'A390NOSB8')
EndIf

Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390IncluiЁ Autor Ё Gilson do Nascimento  Ё Data Ё 30/09/94 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa para inclusao de Movimentacoes de Lote            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Inclui(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390Inclui(cAlias,nReg,nOpc)
Local nOpca
Local lUsaLote    := IIF(GetMV("MV_RASTRO")== "S",.T.,.F.)
Local dDataFec    := MVUlmes()
Local nQuant      := 0
Local nQuantLote  := 0
Local nSaldoSBF   := 0
Local lEmpPrev    := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lTemMov     := .F.
Local cChaveProd  := SB2->B2_COD
Local cChaveLocal := SB2->B2_LOCAL
Local cFilialMov  := xFilial("SD1") // Mesma filial para SD1, SD2, SD3

// Verifica SD1, SD2 e SD3 para ver se tem movimentacao
SD1->(DbSetOrder(5)) //D1_FILIAL+D1_COD+D1_LOCAL+D1_NUMSEQ
SD2->(DbSetOrder(1)) //D2_FILIAL+D2_COD+D2_LOCAL+D2_NUMSEQ
SD3->(DbSetOrder(3)) //D3_FILIAL+D3_COD+D3_LOCAL+D3_NUMSEQ+D3_CF
If SD1->(DbSeek(cFilialMov + cChaveProd + cChaveLocal)) .Or. SD2->(DbSeek(cFilialMov + cChaveProd + cChaveLocal));
.Or. SD3->(DbSeek(cFilialMov + cChaveProd + cChaveLocal))
	lTemMov := .T.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verificar data do ultimo fechamento em SX6.                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If dDataFec >= dDataBase .and. lTemMov
	Help ( " ", 1, "FECHTO" )
	Return
EndIf

If !lUsaLote
	Help(" ",1,"A390NAORAS")
	Return
EndIf

dbSelectArea(cAlias)

If ( l390Auto )
	nOpca := AxIncluiAuto(cAlias,"A390TudoOk()")
Else
	nOpca := AxInclui(cAlias,nReg,nOpc,aAcho,,,"A390TudoOK()",,"A390ATU()")
EndIf

If nOpca == 1
	Begin Transaction
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё A390AjusEnd - Realiza a conversao dos produto que utilizam 		|
		//| enderecamento e possuem saldo no SBF para comecar a utilizar	|
		//| rastreabilidade.												|
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If QtdComp(nSaldoSBF:=SaldoSBF(SD5->D5_LOCAL,"",SD5->D5_PRODUTO,"")) > QtdComp(0)
			If SB2->(dbSeek(xFilial("SB2")+SD5->D5_PRODUTO+SD5->D5_LOCAL))
				nQuant:=SaldoSB2()
			EndIf
			If nQuant > 0
				SB8->(dbSetOrder(1))
				If	SB8->(dbSeek(xFilial("SB8")+SD5->D5_PRODUTO+SD5->D5_LOCAL))
					While SB8->(!Eof()) .And. SB8->(B8_FILIAL+B8_PRODUTO+B8_LOCAL) == xFilial("SB8")+SD5->D5_PRODUTO+SD5->D5_LOCAL
						nQuantLote+=(SB8SALDO(,,,,,lEmpPrev,,,.T.)-(SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+SB8->B8_QACLASS+AvalQtdPre("SB8",1)))
						SB8->(dbSkip())
					EndDo
				Endif
			Endif
			If nSaldoSBF == nQuantLote .And. nSaldoSBF < nQuant .And. nSaldoSBF > 0
			  	CriaLote("SD5",SD5->D5_PRODUTO,SD5->D5_LOCAL,SD5->D5_LOTECTL, ;
					SD5->D5_NUMLOTE,SD5->D5_LOTEFOR,SD5->D5_CLIFOR,SD5->D5_LOJA, ;
					Nil,"MN",CriaVar("B8_PRODUTO")+CriaVar("B8_DOC")+SerieNfId("SB8",5,"B8_SERIE")+CriaVar("B8_CLIFOR")+CriaVar("B8_LOJA"),SD5->D5_NUMSEQ,SD5->D5_DOC, ;
					SD5->D5_SERIE,SD5->D5_OP,SD5->D5_QUANT,SD5->D5_QTSEGUM, ;
					SD5->D5_DATA,SD5->D5_DTVALID,NIL,SD5->D5_POTENCI)
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Grava flag de identificacao para inclusao manual.                 Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					Reclock("SD5",.F.)
					Replace D5_ORIGLAN With "MAN"
					Replace D5_NUMSEQ  With ProxNum()
					If Empty(D5_DTVALID)
						Replace D5_DTVALID With SB8->B8_DTVALID
					EndIf

					If Localiza(SD5->D5_PRODUTO)
						//Gerar Saldo a Distribuir
						CriaSDA("SD5")
					EndIf
			Endif
			If !A390AjusEnd(cAlias,SD5->D5_PRODUTO,SD5->D5_LOCAL,SD5->D5_LOTECTL,SD5->D5_NUMLOTE,SD5->D5_QUANT)
				Reclock("SD5",.F.)
				dbDelete()
				MsUnLock()
			EndIf
		Else
		  	CriaLote("SD5",SD5->D5_PRODUTO,SD5->D5_LOCAL,SD5->D5_LOTECTL, ;
				SD5->D5_NUMLOTE,SD5->D5_LOTEFOR,SD5->D5_CLIFOR,SD5->D5_LOJA, ;
				Nil,"MN",CriaVar("B8_PRODUTO")+CriaVar("B8_DOC")+CriaVar("B8_SERIE")+SerieNfId("SB8",5,"B8_SERIE")+CriaVar("B8_LOJA"),SD5->D5_NUMSEQ,SD5->D5_DOC, ;
				SD5->D5_SERIE,SD5->D5_OP,SD5->D5_QUANT,SD5->D5_QTSEGUM, ;
				SD5->D5_DATA,SD5->D5_DTVALID,NIL,SD5->D5_POTENCI)
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Grava flag de identificacao para inclusao manual.                 Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				Reclock("SD5",.F.)
				Replace D5_ORIGLAN With "MAN"
				Replace D5_NUMSEQ  With ProxNum()
				If Empty(D5_DTVALID)
					Replace D5_DTVALID With SB8->B8_DTVALID
				EndIf

				If Localiza(SD5->D5_PRODUTO)
					//Gerar Saldo a Distribuir
					CriaSDA("SD5")
				EndIf
		EndIf

		//IntegraГЦo com novo MRP
		If Findfunction("A390PCPInt")
			A390PCPInt(SD5->D5_FILIAL, SD5->D5_PRODUTO, SD5->D5_LOCAL)
		EndIf

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Execblock MT390INC localizado ap╒s a Inclusao                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock('MT390INC')
			ExecBlock('MT390INC', .F., .F.)
		EndIf

		// IntegraГЦo com TOTVS MES - UPSERT
		// Considerar a quantidade informada em tela - D5_QUANT na tag QuantityUpdated
		If Findfunction("PCP390PPI")
		   PCP390PPI(SD5->D5_PRODUTO, SD5->D5_LOCAL, SD5->D5_SERIE, SD5->D5_LOTECTL,SD5->D5_NUMLOTE,;
			         	  SD5->D5_DTVALID,SD5->D5_QUANT,'3',SD5->D5_NUMSEQ, SD5->D5_DOC)
		EndIf
	End Transaction
EndIf
Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390ExcluiЁ Autor Ё Gilson do Nascimento  Ё Data Ё 30/09/94 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de exclusao de Requisicoes empenhadas             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Exclui(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function A390Exclui(cAlias,nReg,nOpc)
Local nOpcA      := 0
Local aAC        := {STR0006,STR0007}	// 'Abandona'###'Confirma'
Local lUsaLote   := If(GetMV('MV_RASTRO')=='S',.T.,.F.)
Local lApaga     := .T.,lDelSDA:=.T.
Local nOrdSB8    := SB8->(IndexOrd())
Local lRet       := .T.
Local lRetPE     := .T.
Local oDlg
Local dDataFec   := MVUlmes()
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lSublote   := Rastro(SD5->D5_PRODUTO,'S')
Local cQuery     := ""
Local cAliasTmp  := ""
Local aAreaSDA,aAreaSDB,aAreaSBF
Local lWmsNew    := SuperGetMV("MV_WMSNEW",.F.,.F.) .And. IntWms(SD5->D5_PRODUTO)
Local nX         := 0
Local cSeek      := ''
Local dUlMes     := GetMV('MV_ULMES')
Local aAtuSBK    := {}
Local aPPIDelete := {}

If lWmsNew
	WmsHelp(STR0039,STR0040) //"Produto controlado pelo mСdulo WMS."//"Utilize a rotina de ManutenГЦo de Lotes WMS (WMSA530).")
	Return
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verificar data do ultimo fechamento em SX6.                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If dDataFec >= dDataBase
	Help ( " ", 1, "FECHTO" )
	Return
EndIf

If !lUsaLote
	Help(' ',1,'A390NAORAS')
	Return .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se ┌ lancamento pela manutencao  Ё
//юддддддддддддддддддддддддддддддддддддддддддды
If SD5->D5_ORIGLAN # 'MAN'
	Help(' ',1,'MA390NOMV')
	dbSelectArea('SD5')
	Return .F.
EndIf

//здддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se ja possui movimentacoes  Ё
//юдддддддддддддддддддддддддддддддддддддды
cQuery := "SELECT COUNT(SD5.D5_NUMSEQ) RECS "
cQuery += "  FROM "+RetSqlName("SD5")+" SD5 "
cQuery += " WHERE SD5.D5_FILIAL   = '"+xFilial("SD5")+ "' "
cQuery += "   AND SD5.D5_PRODUTO  = '"+SD5->D5_PRODUTO+"' "
cQuery += "   AND SD5.D5_LOCAL    = '"+SD5->D5_LOCAL+"' "
cQuery += "   AND SD5.D5_LOTECTL  = '"+SD5->D5_LOTECTL+"' "
cQuery += "   AND SD5.D5_NUMLOTE  = '"+IIf(lSublote, SD5->D5_NUMLOTE, "")+"' "
cQuery += "   AND SD5.D5_ORIGLAN <> 'MAN' "
cQuery += "   AND SD5.D5_ESTORNO <> 'S' "
cQuery += "   AND SD5.D_E_L_E_T_  = ' ' "

cQuery    := ChangeQuery(cQuery)
cAliasTmp := GetNextAlias()
dbUseArea( .T., 'TOPCONN', TcGenQry(,,cQuery), cAliasTmp, .T., .F. )

If !(cAliasTmp)->(Eof()) .And. (cAliasTmp)->RECS > 0
	(cAliasTmp)->( DbCloseArea() )
	Help(' ', 1, 'MA390TEMMV')
	Return .F.
EndIf
(cAliasTmp)->( DbCloseArea() )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se foi gerado pela inclusao de saldo inicial    Ё
//Ё Obs.: se a rotina de exclusao for executada pelo MATA220 Ё
//Ё nao deve realizar esta consistencia.                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !IsInCallStack("A220Deleta")
	If !Empty(SD5->D5_SLDINI) .And. Upper(SD5->D5_SLDINI) == 'B9'
		Help(" ", 1, "A390SLDINI")
		Return .F.
	EndIf
EndIf

PRIVATE aTELA[0][0]
PRIVATE aGETS[0]

SoftLock(cAlias)

If !l390Auto
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
	nOpcA := EnChoice( cAlias, nReg, nOpc, aAC,"AC",STR0008,aAcho) // 'Quanto a exclus└o?'
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
Else
	nOpcA := 2
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Execblock MT390VLE para Validar a Exclusao (O Ret.pode ser L╒gico)Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If nOpcA == 2 .And. ExistBlock('MT390VLE')
	lRet  := If(ValType(lRetPE:=ExecBlock('MT390VLE',.F.,.F.))=='L',lRetPE,.T.)
	nOpcA := If(lRet,2,1)
EndIf

IF nOpcA == 2
	Begin Transaction
		//здддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica SALDO a DISTRIBUIR                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SDA")
		dbSetOrder(1)
		If dbSeek(xFilial("SDA")+SD5->D5_PRODUTO+SD5->D5_LOCAL+SD5->D5_NUMSEQ+SD5->D5_DOC+SD5->D5_SERIE)
			If !(SDA->DA_QTDORI == SDA->DA_SALDO)
				Help(" ",1,"MA265QTEST")
				lDelSDA:=.F.
			EndIf
		EndIf
		SB8->(dbSetOrder(2))
		If lDelSDA .And. SB8->(dbSeek(xFilial('SB8') + SD5->D5_NUMLOTE + SD5->D5_LOTECTL + SD5->D5_PRODUTO + SD5->D5_LOCAL, .F.))
			//-- Caso o lote nao tenha sido alterado, ativa o flag para apagar
			If !(SB8SALDO(,,,,,lEmpPrev,,,.T.) == SB8->B8_QTDORI)
				lApaga:=.F.
			EndIf
			RecLock('SB8', .F.)
			dbSelectArea(cAlias)
			Replace SB8->B8_SALDO With (SB8->B8_SALDO-SD5->D5_QUANT)
			If lApaga .And. QtdComp(SB8SALDO(,,,,,lEmpPrev,,,.T.)) == QtdComp(0)
				RecLock('SB8',.F.,.T.)
				dbSelectArea(cAlias)
				SB8->(dbDelete())
				SB8->(WriteSX2('SB8', 1))
			ElseIf !lApaga .And. QtdComp(SB8SALDO(,,,,,lEmpPrev,,,.T.)) < QtdComp(0)
				Alert(STR0016+SD5->D5_PRODUTO+STR0017) //'Atencao - > O Produto '###' ficou com Saldo por Lote Negativo'
			EndIf
		EndIf
		//Limpa lote do Movimento de DistribuiГЦo (SDB)
		aAreaSDB	:= SDB->(GetArea())
		SDB->(DbSetOrder(2)) //DB_FILIAL+DB_PRODUTO+DB_LOCAL+DB_LOTECTL+DB_NUMLOTE+DB_NUMSERI+DB_LOCALIZ+DB_NUMSEQ
		While SDB->(DbSeek(xFilial("SDB",SD5->D5_FILIAL) + SD5->(D5_PRODUTO + D5_LOCAL + D5_LOTECTL + D5_NUMLOTE) ))
			A390GrvLote("SDB","","",.T.)
		Enddo
		RestArea(aAreaSDB)

		SB8->(dbSetOrder(nOrdSB8))
		If lDelSDA
			// IntegraГЦo PPI
			aPPIDelete := {SB8->(Recno())}
			If Localiza(SD5->D5_PRODUTO)
				CriaSDA("SD5",,.T.)
				//Limpa lote do Saldo a Distribuir (SDA)
				aAreaSDA	:= SDA->(GetArea())
				SDA->(DbSetOrder(2)) //DA_FILIAL+DA_PRODUTO+DA_LOCAL+DA_LOTECTL
				While SDA->(DbSeek(xFilial("SDA",SD5->D5_FILIAL) + SD5->(D5_PRODUTO + D5_LOCAL + D5_LOTECTL) ))
					A390GrvLote("SDA","","",.T.)
				Enddo
				RestArea(aAreaSDA)

				//Limpa lote do Saldo por EndereГo (SBF)
				aAreaSBF	:= SBF->(GetArea())
				SBF->(DbSetOrder(2)) //BF_FILIAL+BF_PRODUTO+BF_LOCAL+BF_LOTECTL+BF_NUMLOTE+BF_PRIOR+BF_LOCALIZ+BF_NUMSERI
				While SBF->(DbSeek(xFilial("SBF",SD5->D5_FILIAL) + SD5->(D5_PRODUTO + D5_LOCAL + D5_LOTECTL + D5_NUMLOTE) ))
					A390GrvLote("SBF","","",.T.)
				Enddo
				RestArea(aAreaSBF)
			EndIf

			//Analisa Saldo Inicial por Endereco
			SBK->(dbSetorder(1))
			If SBK->(MsSeek(cSeek:=xFilial('SBK')+SD5->(D5_PRODUTO + D5_LOCAL + D5_LOTECTL + D5_NUMLOTE),.F.))
				While SBK->(!Eof()) .And. cSeek == SBK->(BK_FILIAL+BK_COD+BK_LOCAL+BK_LOTECTL+BK_NUMLOTE)
					nRecnoSBK := 0
					dData     := CtoD('  /  /  ')
					cSeek1 := cSeek+SBK->(BK_LOCALIZ+BK_NUMSERI)
					While SBK->(!Eof()) .And. cSeek1 == SBK->(BK_FILIAL+BK_COD+BK_LOCAL+BK_LOTECTL+BK_NUMLOTE+BK_LOCALIZ+BK_NUMSERI)
						dData     := SBK->BK_DATA
						nRecnoSBK := SBK->(Recno())
						SBK->(dbSkip())
					EndDo
					If nRecnoSBK > 0 .And. !Empty(dData) .And. dData == dUlMes
						SBK->(dbGoto(nRecnoSBK))
						aAdd(aAtuSBK,nRecnoSBK)
						SBK->(dbSkip())
					EndIf
				EndDo
				//-- Atualiza Lote/SubLote na tabela SBK
				For nX := 1 to Len(aAtuSBK)
					SBK->(dbGoto(aAtuSBK[nX]))
					A390GrvLote("SBK",'','',.T.)
				Next
			EndIf

			RecLock('SD5',.F.,.T.)
			dbSelectArea(cAlias)
			SD5->(dbDelete())
			SD5->(WriteSX2('SD5', 1))
		EndIf

		//IntegraГЦo com novo MRP
		If Findfunction("A390PCPInt")
			A390PCPInt(SD5->D5_FILIAL, SD5->D5_PRODUTO, SD5->D5_LOCAL)
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Execblock MT390EXC localizado ap╒s a Exclusфo                     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock('MT390EXC')
			ExecBlock('MT390EXC', .F., .F.)
		EndIf

		// IntegraГЦo com TOTVS MES - DELETE
		If Findfunction("PCP390PPI")
		   PCP390PPI(SD5->D5_PRODUTO, SD5->D5_LOCAL, SD5->D5_SERIE, SD5->D5_LOTECTL,SD5->D5_NUMLOTE,;
				           SD5->D5_DTVALID ,SD5->D5_QUANT,'3',SD5->D5_NUMSEQ, SD5->D5_DOC,'delete',aPPIDelete)
		EndIf

	End Transaction
Else
	MsUnLock()
EndIf

dbSelectArea(cAlias)
Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390TudoOkЁ Autor ЁRodrigo de A. Sartorio Ё Data Ё 22/05/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de validacao da inclusao.                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390TudoOK()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390TudoOk()
Local cAlias:=Alias(),nOrder:=IndexOrd(),nRecno:=Recno(),lRet:=.T.
Local cProd,cLocal,cDoc,cSerie,dDataMov
Local nQuantSD5  :=0,nQuant:=0,nQuantLote:=0,nPotencia:=0,nQuantSBF:=0
Local cLoteCtl   := ''
Local cNumLote   := ''
Local aTamSX3    :=TamSX3("D5_QUANT")
Local lRetPE     := .T.
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lQtdTotal  := SuperGetMv("MV_A390QTD",.F.,.T.)
Local lA390Zero  := ExistBlock('A390ZERO') .And. ExecBlock('A390ZERO', .F., .F.)
Local lWmsNew    := SuperGetMV("MV_WMSNEW",.F.,.F.)
Local cSldIni    := ""

cProd	 :=M->D5_PRODUTO
cLocal   :=M->D5_LOCAL
cDoc	 :=M->D5_DOC
cSerie	 :=M->D5_SERIE
dDataMov :=M->D5_DATA
nQuantSD5:=M->D5_QUANT
nPotencia:=M->D5_POTENCI
cLoteCtl :=M->D5_LOTECTL
cNumLote :=M->D5_NUMLOTE
cSldIni	:= M->D5_SLDINI

If lWmsNew .And. IntWMS(cProd)
	WmsHelp(STR0039,STR0040) //"Produto controlado pelo mСdulo WMS."//"Utilize a rotina de ManutenГЦo de Lotes WMS (WMSA530).")
	Return
EndIf

//здддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se tem permissao de armazem  |
//юдддддддддддддддддддддддддддддддддддддды
lRet := MaAvalPerm(3,{cLocal,cProd})

If lRet .And. Empty(nQuantSD5) .And. !lA390Zero
	Help(" ",1,"NVAZIO",, RetTitle("D5_QUANT"), 3, 0)
	lRet:=.F.
EndIf
If lRet .And. QtdComp(nQuantSD5) < QtdComp(0)
	Help(" ",1,"POSIT")
	lRet:=.F.
EndIf
If lRet
	dbSelectArea("SB2")
	If	dbSeek(xFilial("SB2")+cProd+cLocal)
		nQuant:=SaldoSB2()
	EndIf
	If nQuant > 0 .AND. FunName() # "MATA220"
		// Tratamento para utilizacao do ponto de entrada A390ZERO
		If lA390Zero .And. QtdComp(nQuantSD5)==QtdComp(0)
			dbSelectArea("SB8")
			dbSetOrder(3)
			If dbSeek(xFilial("SB8")+cProd+cLocal+cLoteCtl+cNumLote)
				Help(" ",1,"A390JAEXIS")
				lRet:=.F.
			EndIf
		Else
			dbSelectArea("SB8")
			dbSetOrder(1)
			If	dbSeek(xFilial("SB8")+cProd+cLocal)
				Do While !Eof() .And. B8_FILIAL+B8_PRODUTO+B8_LOCAL == xFilial("SB8")+cProd+cLocal
					nQuantLote+=(SB8SALDO(,,,,,lEmpPrev,,,.T.)-(SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+B8_QACLASS+AvalQtdPre("SB8",1)))
					dbSkip()
				EndDo
				If nQuantLote == nQuant
					Help(" ",1,"A390NSALDO")
					lRet:=.F.
				EndIf

				If NoRound(nQuantLote+nQuantSD5,aTamSX3[2]) > NoRound(nQuant,aTamSX3[2]) .And. lRet
					Help(" ",1,"A390QUANT",,AllTrim(Str(nQuant - nQuantLote)),3,6)
					lRet:=.F.
				EndIf

				If lRet .And. lQtdTotal .And. NoRound(nQuantSD5,aTamSX3[2]) # NoRound(nQuant - nQuantLote,aTamSX3[2])
					Aviso('MATA390',+STR0034+" "+AllTrim(Str(nQuant - nQuantLote)), {'Ok'})
					lRet:=.F.
				EndIf

			Else
				// ManutenГЦo de lote que nao for proveniente de saldo inicial,
				// deve considerar a quantidade do lote com a quantidade disponivel do produto
				If lRet .AND. cSldIni != "B9"
					If lRet .And. NoRound(nQuantSD5,aTamSX3[2]) > NoRound(nQuant,aTamSX3[2])
						Help(" ",1,"A390QUANT",,AllTrim(Str(nQuant - nQuantLote)),3,6)
						lRet:=.F.
					EndIf
				EndIf

				If lRet .And. lQtdTotal .And. NoRound(nQuantSD5,aTamSX3[2]) # NoRound(nQuant,aTamSX3[2])
					Aviso('MATA390',+STR0034+" "+AllTrim(Str(nQuant)), {'Ok'})
					lRet:=.F.
				EndIf
			EndIf
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica Saldos por Endereco sem Lote informado.		Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lRet
			nQuantSBF:=SaldoSBF(cLocal,"",cProd,"")
			If QtdComp(nQuantSBF) > QtdComp(0)
				If !A390VldSBF(cProd,cLocal,!l390Auto,nQuantSD5,.T.)
					lRet := .F.
				EndIf
			EndIf
		EndIf
	Else
		If !(lA390Zero .And. QtdComp(nQuantSD5)==QtdComp(0)) .AND. FunName() # "MATA220"
			Help(" ",1,"A390NOSB2")
			lRet:=.F.
		EndIf
	EndIf
EndIf
If lRet
	If !(lA390Zero .And. QtdComp(nQuantSD5)==QtdComp(0))
		dbSelectArea("SD5")
		dbSetOrder(2)
		If dbSeek(xFilial("SD5")+cProd+cLocal+cLoteCtl+cNumLote)
			Do While D5_FILIAL+D5_PRODUTO+D5_LOCAL+D5_LOTECTL+D5_NUMLOTE  == xFilial("SD5")+cProd+cLocal+cLoteCtl+cNumLote
				If cDoc == D5_DOC .And. cSerie == D5_SERIE .And. dDataMov == D5_DATA
					lRet:=.F.
					Help(" ",1,"A390JAEXIS")
					Exit
				EndIf
				dbSkip()
			EndDo
		EndIf
	EndIf
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida se o produto utiliza controle de potencia                  Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. (nPotencia > 0 .And. !PotencLote(cProd))
	Help(" ",1,"NAOCPOTENC")
	lRet:=.F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida se o produto utiliza o controle de rastreabilidade         Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. !Rastro(M->D5_PRODUTO)
	Help(" ",1,"A390NRASTR")
	lRet := .F.
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Execblock MT390VLI para Validar a Inclusao (O Ret.pode ser L╒gico)Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. ExistBlock('MT390VLI')
	lRet := If(ValType(lRetPE:=ExecBlock('MT390VLI',.F.,.F.))=='L',lRetPE,.T.)
EndIf

dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoto(nRecno)
Return lRet

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390LotCtlЁ Autor ЁRodrigo de A. Sartorio Ё Data Ё 11/12/96 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de validacao do lote de controle.                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390LotCtl()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390LotCtl()
Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390Quant Ё Autor Ё Marcelo Pimentel      Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina que calcula a 2a unidade medida quanto exitir       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390Quant()
Local nQuant := M->D5_QUANT

If !Empty(nQuant)
	M->D5_QTSEGUM := ConvUm(M->D5_PRODUTO,nQuant,M->D5_QTSEGUM,2)
	nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "D5_QTSEGUM" })
	If nEndereco > 0
		aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := Transform(M->D5_QTSEGUM,PesqPictQT("D5_QTSEGUM"))
	EndIf
Endif
Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390QtSeg Ё Autor Ё Marcelo Pimentel      Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina que calcula a primeira unidade de medida.           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA650                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390QtSeg()
Local nQtSegUm := M->D5_QTSEGUM

If !Empty(nQtSegUm)
	M->D5_QUANT := ConvUm(M->D5_PRODUTO,M->D5_QUANT,nQtSegUm,1)
	nEndereco := Ascan(aGets,{ |x| Subs(x,9,8) == "D5_QUANT" } )
	If nEndereco > 0
		aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := Transform(M->D5_QUANT,PesqPictQT("D5_QUANT"))
	EndIf
Endif
Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390Valid Ё Autor Ё Marcos Patricio       Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de alteracao de data de validade de lotes           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Valid(ExpC1,ExpN1,ExpN2)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function A390Valid(cAlias,nReg,nOpc,dNewDtVld,lBxCQ)

Local nOrder     := IndexOrd()
Local dDataValid := dDatabase
Local dDataFab	:= CTOD("  /  /  ")
Local lUsaLote   := IIf(GetMV("MV_RASTRO")== "S",.T.,.F.)
Local lUnlock    := .F.
Local lRet       := .T.
Local lRetPE     := .T.
Local nSaldoB8   := 0
Local aObjects   := {}
Local aPosObj    :={}
Local aSize      := MsAdvSize()
Local aInfo      := {aSize[1],aSize[2],aSize[3],aSize[4],3,3}
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local bCompSDC, bCompSDC2
Local bComparaSBF, cChaveSBF
Local oDlg, nOpca
Local nInd, oGet

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё MV_A175VLD - Parametro utilizado para verificar se o usuario  |
//|              podera realizar a alteraГЦo da data de validade  |
//|              do lote atraves da rotina de Baixas do CQ.       |
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local lA175Vld   :=	SuperGetMv("MV_A175VLD",.F.,.F.)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Walk-Thru						                              Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aYesFields := {	"B8_LOTECTL","B8_PRODUTO",;
					 	"B8_LOCAL"  ,"B8_DTVALID",;
					 	"B8_SALDO"  ,"B8_EMPENHO",;
					 	"B8_QTDORI" ,"B8_DFABRIC" }
Local bSeekWhile	:= {} //Condicao While para montar o aCols
Local cSeek		:= ""
Local lWmsNew  := SuperGetMV("MV_WMSNEW",.F.,.F.) .And. IntWMS(SD5->D5_PRODUTO)
Local nTotSald2UM := 0

Default dNewDtVld:= CTOD("  /  /  ")

If lWmsNew
	WmsHelp(STR0039,STR0041) //"Produto controlado pelo mСdulo WMS."//"Utilize a rotina de Troca de Data de Validade WMS (WMSA535)."
	Return
EndIf

If !lUsaLote
	Help(" ", 1,"NAOLOTE")
EndIf

If !Rastro(SD5->D5_PRODUTO)
	Help(" ",1,"NAORASTRO")
	Return
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё MV_A175VLD - Parametro utilizado para verificar se o usuario  |
//|              podera realizar a alteraГЦo da data de validade  |
//|              do lote atraves da rotina de Baixas do CQ.       |
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lA175Vld .And. !lBxCQ
	Help( " ", 1, "A390DTVAL" )
	Return(.T.)
EndIf

Private aLotes     := {}
Private aRecno     := {}
Private aRecnoSDD  := {}
Private aRecnoSDC  := {}
Private aRecnoSBF  := {}
Private aHeader    := {}
Private aCols      := {}
Private CurLen     := 0
Private nPosAtu    := 0
Private nPosAnt    := 9999
Private nColAnt    := 9999
Private cSavScrVT,cSavScrVP,cSavScrHT,cSavScrHP

Static dValidAnt	 := dDataBase

Set Key VK_F12 To

Pergunte("MTA390",.F.)

// ** Recupera rastreabilidade por lote ou sublote
If Rastro(SD5->D5_PRODUTO,"S")
	If mv_par01 == 1
		cSeek 	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL + B8_NUMLOTE }
	Else
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL + B8_NUMLOTE }
	EndIf
ElseIf Rastro(SD5->D5_PRODUTO,"L")
	If mv_par01 == 1
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL }
	Else
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL }
	EndIf
EndIf
dbSelectArea("SB8")
If mv_par01 == 1
	dbSetOrder(3)
Else
	dbSetOrder(5)
EndIf

If dbSeek(cSeek)
	//зддддддддддддддддддддддддддддд©
	//Ё Acerta a data de validade   |
	//юддддддддддддддддддддддддддддды
	If lBxCQ
		dDataValid:= dNewDtVld
	Else
		dDataValid:=SB8->B8_DTVALID
	EndIF

	dDataFab := SB8->B8_DFABRIC

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader e aCols utilizando a funcao FillGetDados c/ lotes a serem reavaliados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry ) |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(2,"SB8",If(mv_par01==1,3,5),cSeek,bSeekWhile,{||.T.},,aYesFields,,,,,,,{|| a390AfterC(nOpc,@nSaldoB8,lEmpPrev,@aRecno)} )

EndIf

If Len(aCols) = 0
	Help( " ", 1, "VAZIO" )
Else
	If Rastro(SD5->D5_PRODUTO,"S")
		If mv_par01 == 1
			cSeekDD  := xFilial( "SDD" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
			bBlockDD := { || DD_FILIAL + DD_PRODUTO + DD_LOCAL + DD_LOTECTL + DD_NUMLOTE == cSeekDD }
		Else
			cSeekDD  := xFilial( "SDD" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
			bBlockDD := { || DD_FILIAL + DD_PRODUTO + DD_LOTECTL + DD_NUMLOTE == cSeekDD }
		EndIf
	ElseIf Rastro(SD5->D5_PRODUTO,"L")
		If mv_par01 == 1
			cSeekDD  := xFilial( "SDD" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL
			bBlockDD := { || DD_FILIAL + DD_PRODUTO + DD_LOCAL + DD_LOTECTL == cSeekDD }
		Else
			cSeekDD  := xFilial( "SDD" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL
			bBlockDD := { || DD_FILIAL + DD_PRODUTO + DD_LOTECTL == cSeekDD }
		EndIf
	EndIf
	dbSelectArea("SDD")
	dbSetOrder(If(mv_par01==1,2,3))
	dbSeek(cSeekDD)
	nTotSaldoDD := 0
	nTotSald2UM := 0
	While !Eof() .And. Eval(bBlockDD)
		If DD_MOTIVO = "VV"
			nTotSaldoDD += DD_SALDO
			nTotSald2UM += DD_SALDO2
			//здддддддддддддддддддд©
			//Ё Faz a trava do SDC Ё
			//юдддддддддддддддддддды
			dbSelectArea( "SDC" )
			dbSetOrder( 1 )
			cChaveSDC:=xFilial("SDC")+SDD->DD_PRODUTO+SDD->DD_LOCAL+"SDD"+Left(SDD->DD_DOC, Len(SDC->DC_PEDIDO))+CriaVar("DC_ITEM")+CriaVar("DC_SEQ")+SDD->DD_LOTECTL
			bCompSDC:={|| cChaveSDC == DC_FILIAL+DC_PRODUTO+DC_LOCAL+DC_ORIGEM+DC_PEDIDO+DC_ITEM+DC_SEQ+DC_LOTECTL}
			If Rastro( SDD->DD_PRODUTO, "S" )
				bCompSDC2 := { || SDD->DD_NUMLOTE == DC_NUMLOTE }
			Elseif Rastro( SDD->DD_PRODUTO, "L" )
				bCompSDC2 := { || SDD->DD_LOTECTL == DC_LOTECTL }
			EndIf
			dbSeek(cChaveSDC)
			While !Eof() .And. Eval( bCompSDC )
				If Eval( bCompSDC2 )
					RecLock( "SDC", .f. )
					AAdd( aRecnoSDC, Recno() )
				EndIf
				//зддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Se mudou o lote faz a trava de registros no SBF Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддды
				cChaveSBF  := xFilial("SBF")+DC_PRODUTO+DC_LOCAL+DC_LOTECTL+If(Rastro(DC_PRODUTO,"S"),DC_NUMLOTE,"")
				bComparaSBF:= { || cChaveSBF ==	BF_FILIAL+BF_PRODUTO+BF_LOCAL+BF_LOTECTL+IF(Rastro(SDC->DC_PRODUTO,"S"),BF_NUMLOTE,"")}
				dbSelectArea( "SBF" )
				dbSetOrder( 2 )
				dbSeek( cChaveSBF )
				While !Eof() .And. Eval( bComparaSBF )
					 RecLock("SBF",.F.)
					 AADD(aRecnoSBF, Recno() )
					 dbSkip()
				EndDo
				dbSelectArea("SDC")
				dbSkip()
			EndDo
			DbSelectArea("SDD")
		EndIf
		AADD(aRecnoSDD,Recno())
		DbSelectArea("SDD")
		dbSkip()
	EndDo
	dbSelectArea("SB8")

	lUnlock := .T.

	If lBxCQ
		nOpca := 2
	Else
		If !l390Auto
			AADD(aObjects,{100,100,.T.,.T.,.F.})
			AADD(aObjects,{100,030,.T.,.F.,.F.})
			aPosObj:=MsObjSize(aInfo,aObjects)

			cCadastro := OemToAnsi( STR0010 ) // "Alteracao de lotes"
			nOpca := 0
			DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]
			@ 0.5 , 00 TO aPosObj[1,3],aPosObj[1,4] LABEL OemtoAnsi(STR0011) OF oDlg //  "  Lotes afetados  "
			If nSaldoB8 > 0
				@ aPosObj[2,1]+2.8,10 SAY OemtoAnsi(STR0038)  SIZE 63, 9 OF oDlg PIXEL //"Nova data de fabricaГЦo"
				@ aPosObj[2,1]+2,75 MSGET dDataFab VALID M390dFab(dDataFab) SIZE 40,  9 OF oDlg PIXEL
				@ aPosObj[2,1]+2.8,140 SAY OemtoAnsi(STR0012)  SIZE 63, 9 OF oDlg PIXEL
				@ aPosObj[2,1]+2,205 MSGET dDataValid VALID M390dValid(dDataValid) SIZE 40,  9 OF oDlg PIXEL
			Else
				oDlg:nBottom -= 40
			EndIf
			oGet := MSGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3],aPosObj[1,4],2,"Allwaystrue","Allwaystrue","",.F.,{})

			DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-60 TYPE 1 ACTION (nOpca :=2,oDlg:End()) ENABLE OF oDlg
			DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-30 TYPE 2 ACTION (nOpca :=1,oDlg:End()) ENABLE OF oDlg

			ACTIVATE MSDIALOG oDlg On INIT If(nSaldoB8 = 0, (MsUnlockAll(), Help(" ",1,"M390SSALDO"), oDlg:End()), Nil)
		Else
			If nSaldoB8 > 0
				nDtValid	:= aScan(aRotAuto,{|x| x[1] == "B8_DTVALID"})
				If nDtValid > 0
					dDataValid := aRotAuto[nDtValid,2]
				Endif

				If !(M390dValid(dDataValid))
					Help(" ",1,'M390DTVALID',,STR0035 + DtoC(dDataValid) + STR0036 + DtoC(dDataBase),1,0) //"Data informada: "#" И menor que "
					nOpca := 1
				Else
					nOpca := 2
				Endif

				If nOpca == 2 .And. !(M390dFab(dDataFab))
					Help(" ",1,'M390DTFABR') //"и obrigatСrio informar a data de fabricaГЦo"
					nOpca := 1
				Else
					nOpca := 2
				Endif

			Else
				Help(" ",1,"M390SSALDO")
				nOpca := 1
			Endif
		Endif
	EndIf

	If nSaldoB8 = 0 .And. !lBxCQ
		lUnLock := .F.
	Else
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Execblock MT390VLV para Validar a Validade (O Ret.pode ser L╒gico)Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nOpcA == 2 .And. ExistTemplate('MT390VLV')
			lRet  := If(ValType(lRetPE:=ExecTemplate('MT390VLV',.F.,.F.,{dDataValid}))=='L',lRetPE,.T.)
			nOpcA := If(lRet,2,1)
		EndIf

		If nOpcA == 2 .And. ExistBlock('MT390VLV')
			lRet  := If(ValType(lRetPE:=ExecBlock('MT390VLV',.F.,.F.,{dDataValid}))=='L',lRetPE,.T.)
			nOpcA := If(lRet,2,1)
		EndIf

		If nOpca == 2
			dValidAnt := SB8->B8_DTVALID
			A390PrcVal(dDataValid,nTotSaldoDD,dDataFab,nTotSald2UM)
		EndIf

		// IntegraГЦo com TOTVS MES - UPSERT
		// Considerar 0 na tag QuantityUpdated - somente alteraГЦo de data de validade do lote
		If Findfunction("PCP390PPI")
		   PCP390PPI(SD5->D5_PRODUTO, SD5->D5_LOCAL, SD5->D5_SERIE, SD5->D5_LOTECTL,SD5->D5_NUMLOTE,;
			            dDataValid,0,'3',SD5->D5_NUMSEQ, SD5->D5_DOC)
		EndIf
	EndIf

EndIf

If lUnLock
	nInd := 1
	While nInd <= Len(aRecno)
		dbSelectArea("SB8")
		dbGoTO(aRecno[nInd])
		MSUnLock()
		nInd++
	EndDo
EndIf

If !lBxCQ
	SetKey(VK_F12, {||MTA390PERG()})
EndIf

dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoTo(nReg)
Return (.T.)


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390Preco Ё Autor Ё Henry Fila            Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de manutencao de precos do lote                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Preco(ExpC1,ExpN1,ExpN2)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function A390Preco(cAlias,nReg,nOpc)

Local aRecno     := {}
Local aObjects   := {},aPosObj  :={}
Local aSize      := MsAdvSize()
Local aInfo      := {aSize[1],aSize[2],aSize[3],aSize[4],3,3}

Local oDlg

Local nOpca
Local nInd
Local nPrcVen    := 0
Local nOrder     := IndexOrd()
Local nSaldoB8   := 0

Local oGet

Local lUsaLote   := IIf(GetMV("MV_RASTRO")== "S",.T.,.F.)
Local lUnlock    := .F.
Local lEmpPrev   := IIf(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Walk-Thru						                              Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aYesFields := {"B8_LOTECTL",;
					 "B8_PRODUTO",;
					 "B8_LOCAL"  ,;
					 "B8_DTVALID",;
					 "B8_SALDO"  ,;
					 "B8_EMPENHO",;
					 "B8_QTDORI" }
Local bSeekWhile := {} //Condicao While para montar o aCols
Local cSeek		 := ""

If !lUsaLote
	Help(" ", 1,"NAOLOTE")
EndIf

If !Rastro(SD5->D5_PRODUTO)
	Help(" ",1,"NAORASTRO")
	Return
EndIf

Private aHeader    := {}
Private aCols      := {}

Set Key VK_F12 To

Pergunte("MTA390",.F.)

// ** Recupera rastreabilidade por lote ou sublote
If Rastro(SD5->D5_PRODUTO,"S")
	If mv_par01 == 1
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL + B8_NUMLOTE }
	Else
		cSeek  	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL + B8_NUMLOTE }
	EndIf
ElseIf Rastro(SD5->D5_PRODUTO,"L")
	If mv_par01 == 1
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL }
	Else
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL }
	EndIf
EndIf

dbSelectArea("SB8")
If mv_par01 == 1
	dbSetOrder(3)
Else
	dbSetOrder(5)
EndIf

If dbSeek(cSeek)
	nPrcVen := SB8->B8_PRCLOT
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader e aCols utilizando a funcao FillGetDados c/ lotes a serem reavaliados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry ) |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	FillGetDados(2,"SB8",If(mv_par01==1,3,5),cSeek,bSeekWhile,{||.T.},,aYesFields,,,,,,,{|| a390AfterC(nOpc,@nSaldoB8,lEmpPrev,@aRecno)} )
EndIf

If Len(aCols) = 0
	Help( " ", 1, "VAZIO" )
Else
	lUnlock := .T.

	If !l390Auto
		AADD(aObjects,{100,100,.T.,.T.,.F.})
		AADD(aObjects,{100,030,.T.,.F.,.F.})
		aPosObj:=MsObjSize(aInfo,aObjects)

		cCadastro := OemToAnsi( STR0022 ) // "Alteracao de precos"
		nOpca := 0
		DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]
		@ 0.5 , 00 TO aPosObj[1,3],aPosObj[1,4] LABEL OemtoAnsi(STR0011) OF oDlg //  "  Lotes afetados  "
		If nSaldoB8 > 0
			@ aPosObj[2,1]+2.8,10 SAY OemtoAnsi(STR0023)  SIZE 63, 9 OF oDlg PIXEL //Preco atual
			@ aPosObj[2,1]+2,75 MSGET nPrcVen Picture PesqPict("SB8","B8_PRCLOT") Valid (nPrcVen >= 0) SIZE 60,  9 OF oDlg PIXEL
		Else
			oDlg:nBottom -= 40
		Endif
		oGet := MSGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3],aPosObj[1,4],2,"Allwaystrue","Allwaystrue","",.F.,{})

		DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-60 TYPE 1 ACTION (nOpca :=2,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-30 TYPE 2 ACTION (nOpca :=1,oDlg:End()) ENABLE OF oDlg

		ACTIVATE MSDIALOG oDlg On INIT If(nSaldoB8 = 0, (MsUnlockAll(), Help(" ",1,"M390SSALDO"), oDlg:End()), Nil)
	Else
		If nSaldoB8 > 0
			nPLote	:= aScan(aRotAuto,{|x| x[1] == "B8_PRCLOT"})
			If nPLote > 0
				nPrcVen := aRotAuto[nPLote,2]
			Endif

			If Positivo(nPrcVen)
				nOpca := 2
			Else
				nOpca := 1
			Endif
		Else
			Help(" ",1,"M390SSALDO")
			nOpca := 1
		Endif
	Endif

	If nSaldoB8 = 0
		lUnLock := .F.
	Else
		If nOpca == 2
			A390PrcAtu(nPrcVen,aRecno)
		Endif
	EndIf
EndIf

If lUnLock
	nInd := 1
	While nInd <= Len(aRecno)
		dbSelectArea("SB8")
		dbGoTO(aRecno[nInd])
		MSUnLock()
		nInd++
	EndDo
EndIf

SetKey(VK_F12, {||MTA390PERG()})

dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoTo(nReg)
Return (.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390PrcValЁ Autor Ё Marcos Patricio       Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Processa alteracao da data de validade                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390PrcVal( ExpD1, ExpN1 )                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpD1 = Nova data de validade                              Ё╠╠
╠╠Ё          Ё ExpN1 = Indica qtd bloqueada                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390PrcVal(dDataValid,nQtdEmpenho,dDataFab,nQtdEmp2UM)
Local nInd,nLoop
Local nBaixa:=0
Local nBaixa2:=0
Local nBackEmpenho:=nQtdEmpenho
Local nBackEmp2UM := 0
Local cCodSB2 := ''
Local cLocSB2 := ''
Local aProdSB2 := {}
Local lIntegDef := FWHasEAI("MATA390",.T.,,.T.)
Local lIntegPCP := Findfunction("A390PCPInt")
Local lAGRX390  := Findfunction("AGRX390VFL")
Local nP := 0

Default nQtdEmp2UM := 0

nBackEmp2UM := nQtdEmp2UM

Begin Transaction
	nInd :=1
	While nInd <= Len(aRecno)
		// Atualiza data de validade (Requisicao Lote)
		dbSelectArea("SB8")
		dbGoTo(aRecno[nInd])
		
		If AScan(aProdSB2, {|p| p[1] == B8_PRODUTO .And. p[2] == B8_LOCAL }) == 0
			AAdd(aProdSB2, {B8_PRODUTO, B8_LOCAL})
		EndIf

		RecLock("SB8",.F.)
		Replace B8_DTVALID With dDataValid
		Replace B8_DFABRIC With dDataFab
		If QtdComp(nQtdEmpenho) > QtdComp(0)
			nBaixa := Min(QtdComp(nQtdEmpenho),QtdComp(B8_EMPENHO))
			nBaixa2 := Min(QtdComp(ConvUm(SB8->B8_PRODUTO, nQtdEmp2UM, 0, 2)),QtdComp(SB8->B8_EMPENH2))
			Replace B8_EMPENHO With B8_EMPENHO - nBaixa
			Replace B8_EMPENH2 With B8_EMPENH2 - nBaixa2
			nQtdEmpenho -= nBaixa
			nQtdEmp2UM  -= nBaixa2
		EndIf
		MsUnlock()

		//IntegraГЦo com novo MRP
		If lIntegPCP
			A390PCPInt(B8_FILIAL, B8_PRODUTO, B8_LOCAL)
		EndIf
		//IntegraГЦo AGRO
		If lAGRX390 
			AGRX390VFL(SB8->B8_FILIAL,SB8->B8_PRODUTO,SB8->B8_LOCAL,SB8->B8_LOTECTL,SB8->B8_NUMLOTE,SB8->B8_DTVALID,SB8->B8_DFABRIC)
		EndIf
		nInd++
	End
	nInd :=1
	While nInd <= Len(aRecnoSDD)
		dbSelectArea("SDD")
		dbGoto(aRecnoSDD[nInd])

		If AScan(aProdSB2, {|p| p[1] == DD_PRODUTO .And. p[2] == DD_LOCAL }) == 0
			AAdd(aProdSB2, {DD_PRODUTO, DD_LOCAL})
		EndIf

		If DD_MOTIVO == "VV"
			RecLock( "SDD", .F., .T. )
			dbDelete()
			MsUnlock()
		EndIf
		nInd++
	EndDo
	//здддддддддддддддддддддд©
	//Ё Zera empenhos do SBF Ё
	//юдддддддддддддддддддддды
	If Len( aRecnoSBF ) > 0
		nQtdEmpenho:=nBackEmpenho
		nQtdEmp2UM := nBackEmp2UM
		dbSelectArea("SBF")
		For nLoop := 1 To Len( aRecnoSBF )
			dbGoTo( aRecnoSBF[ nLoop ] )
			
			If AScan(aProdSB2, {|p| p[1] == BF_PRODUTO .And. p[2] == BF_LOCAL }) == 0
				AAdd(aProdSB2, {BF_PRODUTO, BF_LOCAL})
			EndIf

			nBaixa := Min(nQtdEmpenho,BF_EMPENHO)
			nBaixa2 := Min((ConvUm(SBF->BF_PRODUTO, nQtdEmp2UM, 0, 2)),SBF->BF_EMPEN2)

			RecLock("SBF",.F.)
			Replace BF_EMPENHO With BF_EMPENHO - nBaixa
			Replace BF_EMPEN2 With BF_EMPEN2 - nBaixa2
			nQtdEmpenho -= nBaixa
			nQtdEmp2UM  -= nBaixa2
			MsUnlock()
		Next
	EndIf
	//зддддддддддддддддддддддддд©
	//Ё Acertar Empenhos no SB2 Ё
	//юддддддддддддддддддддддддды
	nQtdEmpenho:=nBackEmpenho
	nQtdEmp2UM := nBackEmp2UM
	If QtdComp(nQtdEmpenho) > QtdComp(0) .And. Len(aProdSB2) > 0
		SB2->(dbSetOrder(1))
		
		For nP := 1 To Len(aProdSB2)

			cCodSB2 := aProdSB2[nP][1]
			cLocSB2 := aProdSB2[nP][2]
			
			If SB2->(dbSeek(xFilial('SB2')+cCodSB2+cLocSB2, .F.))
				nBaixa := Min(nQtdEmpenho, SB2->B2_QEMP)
				nBaixa2 := Min((ConvUm(SB2->B2_COD, nQtdEmp2UM, 0, 2)),SB2->B2_QEMP2)
				If QtdComp(nBaixa) > QtdComp(0)
					RecLock('SB2', .F.)
					Replace B2_QEMP With (B2_QEMP-nBaixa)
					nQtdEmpenho -= nBaixa
					MsUnlock()
				EndIf
				If QtdComp(nBaixa2) > QtdComp(0)
					RecLock('SB2', .F.)
					Replace B2_QEMP2 With (B2_QEMP2-nBaixa2)
					nQtdEmp2UM -= nBaixa2
					MsUnlock()
				EndIf
			EndIf

		Next nP

	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exclui regsitros do SDC referentes ao empenho do SDD Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Len( aRecnoSDC ) > 0
		dbSelectArea("SDC")
		For nLoop := 1 To Len( aRecnoSDC )
			dbGoTo( aRecnoSDC[ nLoop ] )
			//здддддддддддддддддддддддддддддддддддддддд©
			//Ё Destrava e utiliza a trava de exclusao Ё
			//юдддддддддддддддддддддддддддддддддддддддды
			RecLock( "SDC", .F., .T. )
			dbDelete()
			MsUnlock()
		Next
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Execblock MT390DTV localizado ap╒s a Altera┤фo da Data de ValidadeЁ
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock('MT390DTV')
		ExecBlock('MT390DTV', .F., .F.,dDataValid)
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁIntegraГЦo Mensagem зnica.                                              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lIntegDef
		FwIntegDef("MATA390",,,," MATA390")
	EndIf
End Transaction
Return


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390PrcAtuЁ Autor Ё Henry Fila            Ё Data Ё          Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Processa alteracao do preco do lote                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390PrcVal( ExpD1, ExpN1 )                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpD1 = Nova data de validade                              Ё╠╠
╠╠Ё          Ё ExpN1 = Indica qtd bloqueada                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A390PrcAtu(nPrcVen,aRecno)

Local aArea := GetArea()
Local nX    := 0

Begin Transaction

	For nX := 1 to Len(aRecno)

		dbSelectArea("SB8")
		dbGoTo(aRecno[nX])
		RecLock("SB8",.F.)
		Replace B8_PRCLOT With nPrcVen
		MsUnlock()
	Next
	If ExistBlock('MT390PRC')
		ExecBlock('MT390PRC', .F., .F.,{aRecno})
	EndIf

End Transaction

RestArea(aArea)

Return


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390Poten Ё Autor ЁRodrigo de A. Sartorio Ё Data Ё18/06/02  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de alteracao de potencia de lotes                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390Poten(ExpC1,ExpN1,ExpN2)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390Poten(cAlias,nReg,nOpc)
Local oDlg,lOk   := .T.
Local nOpca
Local nOrder     := IndexOrd()
Local nOrderSB8
Local nInd
Local nPotencia  := 0
Local lUsaLote   := IIf(GetMV("MV_RASTRO")== "S",.T.,.F.)
Local lUnlock    := .F.
Local oGet
Local nSaldoB8   := 0
Local aRecno     := {}
Local aObjects   := {},aPosObj  :={}
Local aSize      := MsAdvSize()
Local aInfo      := {aSize[1],aSize[2],aSize[3],aSize[4],3,3}
Local aHeader    := {}
Local aCols      := {}
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lPotcArmLt := .T.
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Walk-Thru						                              Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local aYesFields := {	"B8_LOTECTL",;
						"B8_PRODUTO",;
						"B8_LOCAL"  ,;
						"B8_DTVALID",;
						"B8_SALDO"  ,;
						"B8_EMPENHO",;
						"B8_POTENCI",;
						"B8_QTDORI" }
Local bSeekWhile := {} //Condicao While para montar o aCols
Local cSeek		 := ""

If !lUsaLote
	Help(" ", 1,"NAOLOTE")
endif

If !PotencLote(SD5->D5_PRODUTO)
	Help(" ",1,"NAOCPOTENC")
	lOK:=.F.
EndIf

If !Rastro(SD5->D5_PRODUTO)
	Help(" ",1,"NAORASTRO")
	lOK:=.F.
EndIf

If !lOK
	Return .F.
EndIf

Private aLotes     := {}
Private CurLen     := 0
Private nPosAtu    := 0
Private nPosAnt    := 9999
Private nColAnt    := 9999
Private cSavScrVT,cSavScrVP,cSavScrHT,cSavScrHP

If __lPergPtc == NIL
	__lPergPtc := pergPotenc()
EndIf

If __lPergPtc
	Set Key VK_F12 To

	Pergunte("MTA390",.F.)

	lPotcArmLt := Empty(mv_par02) .Or. mv_par02 == 1 // Ajustar Potencia do Lote ? .T. = Armazem do Lote, .F. = Todos Armazens
EndIf

// ** Recupera rastreabilidade por lote ou sublote
If Rastro(SD5->D5_PRODUTO,"S")
	If lPotcArmLt
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL + B8_NUMLOTE }
	Else
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL + SD5->D5_NUMLOTE
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL + B8_NUMLOTE }
	EndIf
ElseIf Rastro(SD5->D5_PRODUTO,"L")
	If lPotcArmLt
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOCAL + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOCAL + B8_LOTECTL }
	Else
		cSeek	   := xFilial( "SB8" ) + SD5->D5_PRODUTO + SD5->D5_LOTECTL
		bSeekWhile := { || B8_FILIAL + B8_PRODUTO + B8_LOTECTL }
	EndIf
EndIf

If lPotcArmLt
	nOrderSB8 := 3 // B8_FILIAL+B8_PRODUTO+B8_LOCAL+B8_LOTECTL+B8_NUMLOTE+B8_DTVALID
Else
	nOrderSB8 := 5 // B8_FILIAL+B8_PRODUTO+B8_LOTECTL+B8_NUMLOTE+B8_DTVALID
EndIf

dbSelectArea("SB8")
dbSetOrder(nOrderSB8)

If dbSeek(cSeek)
	nPotencia:=SB8->B8_POTENCI
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader e aCols utilizando a funcao FillGetDados c/ lotes a serem reavaliados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Sintaxe da FillGetDados( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields, lOnlyYes, cQuery, bMontCols, lEmpty, aHeaderAux, aColsAux, bAfterCols, bBeforeCols, bAfterHeader, cAliasQry ) |
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	FillGetDados(0,"SB8",nOrderSB8,cSeek,bSeekWhile,{||.T.},,aYesFields,,,,,aHeader,aCols,{|| a390AfterC(nOpc,@nSaldoB8,lEmpPrev,@aRecno)} )

EndIf

If Len(aCols) = 0
	Help( " ", 1, "VAZIO" )
Else
	lUnlock := .T.

	If !l390Auto
		AADD(aObjects,{100,100,.T.,.T.,.F.})
		AADD(aObjects,{100,030,.T.,.F.,.F.})
		aPosObj:=MsObjSize(aInfo,aObjects)

		cCadastro := OemToAnsi( STR0010 ) // "Alteracao de lotes"
		nOpca := 0
		DEFINE MSDIALOG oDlg TITLE cCadastro OF oMainWnd PIXEL FROM aSize[7],0 TO aSize[6],aSize[5]
		@ 0.5 , 00 TO aPosObj[1,3],aPosObj[1,4] LABEL OemtoAnsi(STR0011) OF oDlg //  "  Lotes afetados  "
		If nSaldoB8 > 0
			@ aPosObj[2,1]+2.8,10 SAY OemtoAnsi(STR0019)  SIZE 55, 9 OF oDlg PIXEL
			@ aPosObj[2,1]+2,70 MSGET nPotencia Valid Positivo(nPotencia) PICTURE PesqPict("SB8","B8_POTENCI") SIZE 40, 9  OF oDlg PIXEL
		Else
			oDlg:nBottom -= 40
		EndIf
		oGet := MsNewGetDados():New(aPosObj[1,1],aPosObj[1,2],aPosObj[1,3],aPosObj[1,4],0,"Allwaystrue","Allwaystrue","",,,,,,,oDlg,aHeader,aCols)

		DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-60 TYPE 1 ACTION (nOpca :=2,oDlg:End()) ENABLE OF oDlg
		DEFINE SBUTTON FROM aPosObj[2,1]+2,aPosObj[2,4]-30 TYPE 2 ACTION (nOpca :=1,oDlg:End()) ENABLE OF oDlg

		ACTIVATE MSDIALOG oDlg On INIT If(nSaldoB8 = 0, (MsUnlockAll(), Help(" ",1,"M390SSALDO"), oDlg:End()), Nil)
	Else
		If nSaldoB8 > 0
			nPPot	:= aScan(aRotAuto,{|x| x[1] == "B8_POTENCI"})
			If nPPot > 0
				nPotencia := aRotAuto[nPPot,2]
			Endif

			If Positivo(nPotencia)
				nOpca := 2
			Else
				nOpca := 1
			Endif
		Else
			Help(" ",1,"M390SSALDO")
			nOpca := 1
		Endif
	Endif

	If nSaldoB8 = 0
		lUnLock := .F.
	Else
		If nOpca == 2
			A390PrcPot(nPotencia,aRecno)
		EndIf
	EndIf
EndIf

If lUnLock
	nInd := 1
	While nInd <= Len(aRecno)
		dbSelectArea("SB8")
		dbGoTo(aRecno[nInd])
		MSUnLock()
		nInd++
	EndDo
EndIf

dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoTo(nReg)
Return (.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁA390PrcPotЁ Autor ЁRodrigo de A. Sartorio Ё Data Ё18/06/02  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Processa alteracao da potencia                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A390PrcPot( ExpN1, ExpA1 )                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 = Nova potencia                                      Ё╠╠
╠╠Ё          Ё ExpA1 = Array com registros a serem alterados              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390PrcPot(nPotencia,aRecno)
Local nInd
Begin Transaction
	For nInd:=1 to Len(aRecno)
		// Atualiza potencia do Lote
		dbSelectArea("SB8")
		dbGoTo(aRecno[nInd])
		RecLock("SB8",.F.)
		Replace B8_POTENCI With nPotencia
		MsUnlock()
	Next nInd
End Transaction
Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁMTA390PERGЁ Autor Ё Larson Zordan         Ё Data Ё 27/08/02 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Chamada da funcao PERGUNTE                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA685                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function MTA390PERG()
Pergunte("MTA390",.T.)
Return NIL

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA390AjusEnd Ё Autor Ё Marcos V. Ferreira  Ё Data Ё 18/08/05 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Rotina utilizada para ajustar o saldo do SBF(Enderecamento)Ё╠╠
╠╠Ё          Ё com o saldo do SB8 (Rastreabilidade)						  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A390AjusEnd(cAlias,cProd,cLocal,cLoteCtl,cNumLot,nQuantSD5)
Local cSeek		:= ''
Local lRet		:= .T.
Local nSaldo	:= 0
Local nQuant	:= 0
Local nX		:= 0
Local nPos		:= 0
Local aArrayDB	:= {}
Local aAtuSBK   := {}
Local cChvDB	:= ''
Local cNumLote	:= cNumLot
Local aAreaAnt	:= GetArea()
Local lSubLote	:= Rastro(cProd,'S')
Local dUlMes    := GetMV('MV_ULMES')
Local lHelp     := .T.

cNumLote := If(Empty(cNumLote),ProxNum(),cNumLote)

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё A390VLDSBF - Verifica se eh possivel realizar o ajuste.			|
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If A390VldSBF(cProd,cLocal,.F.,nQuantSD5,.F.)

	Begin Transaction
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Analisa Saldos a Distribuir								|
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SDA")
		DbSetOrder(1)
		DbSeek(cSeek:=xFilial("SDA")+cProd+cLocal)
		Do While !Eof() .And. cSeek == (SDA->DA_FILIAL+SDA->DA_PRODUTO+SDA->DA_LOCAL)
			If SDA->DA_SALDO == 0 .And. SDA->DA_EMPENHO == 0
				If Empty(SDA->DA_LOTECTL) .And. Empty(SDA->DA_NUMLOTE)
					A390GrvLote("SDA",cLoteCtl,cNumLote,lSubLote)
				EndIf
			EndIf
			dbSelectArea("SDA")
			dbSkip()
		EndDo

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Analisa Saldo Inicial por Endereco      				|
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SBK->(dbSetorder(1))
		If SBK->(MsSeek(cSeek:=xFilial('SBK')+cProd+cLocal,.F.))
			While SBK->(!Eof()) .And. cSeek == SBK->(BK_FILIAL+BK_COD+BK_LOCAL) .And. Empty(SBK->BK_LOTECTL)
				If SBK->BK_DATA == dUlMes
					aAdd(aAtuSBK,SBK->(Recno()))
				EndIf
				SBK->(dbSkip())
			EndDo
			//-- Atualiza Lote/SubLote na tabela SBK
			For nX := 1 to Len(aAtuSBK)
				dbSelectArea('SBK')
				dbGoto(aAtuSBK[nX])
				A390GrvLote("SBK",cLoteCtl,cNumLote,lSubLote)
			Next
		EndIf

		dbSelectArea("SDB")
		dbSetOrder(1)
		dbSeek(cSeek:=xFilial("SDB")+cProd+cLocal)
		While !Eof() .And. cSeek == DB_FILIAL+DB_PRODUTO+DB_LOCAL

			If Empty(SDB->DB_LOTECTL) .And. Empty(SDB->DB_NUMLOTE) .And. SDB->DB_ATUEST == "S"
				cChvDB := DB_FILIAL+DB_LOCAL+DB_LOCALIZ+DB_PRODUTO+DB_NUMSERI
				nPos := Ascan(aArrayDB,{ |x| x[1] == cChvDB } )
				If nPos > 0
					//Entrada
					If DB_TM <= '500'
						aArrayDB[nPos,2] := aArrayDB[nPos,2] + SDB->DB_QUANT
						A390GrvLote("SDB",cLoteCtl,cNumLote,lSubLote)
					//Saida
					ElseIf DB_TM > '500'
						aArrayDB[nPos,2] := aArrayDB[nPos,2] - DB_QUANT
						A390GrvLote("SDB",cLoteCtl,cNumLote,lSubLote)
					EndIf
				Else
					//Entrada
					If DB_TM <= '500'
						aAdd(aArrayDB,{cChvDB,DB_QUANT,DB_LOCAL,DB_LOCALIZ,DB_PRODUTO,DB_NUMSERI,DB_LOTECTL,DB_NUMLOTE})
						A390GrvLote("SDB",cLoteCtl,cNumLote,lSubLote)
					//Saida
					ElseIf DB_TM > '500'
						aAdd(aArrayDB,{cChvDB,(DB_QUANT*(-1)),DB_LOCAL,DB_LOCALIZ,DB_PRODUTO,DB_NUMSERI,DB_LOTECTL,DB_NUMLOTE})
						A390GrvLote("SDB",cLoteCtl,cNumLote,lSubLote)
					EndIf
				EndIf
			EndIf
			dbSkip()
		EndDo

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica Divergencias entre SBF x Saldo Calculado		|
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SBF")
		dbSetOrder(1)
		For nX := 1 to Len(aArrayDB)
			If dbSeek(SDB->(xFilial("SBF")+aArrayDB[nX,3]+aArrayDB[nX,4]+aArrayDB[nX,5]+aArrayDB[nX,6]+aArrayDB[nX,7]+aArrayDB[nX,8]))
				nSaldo:=SBFSaldo()
				If nSaldo < aArrayDB[nX,2]
					nQuant += nSaldo
				Else
					If aArrayDB[nX,2] > 0
						nQuant += aArrayDB[nX,2]
					EndIf
				EndIf
				A390GrvLote("SBF",cLoteCtl,cNumLote,lSubLote)
			EndIf
		Next nX

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava Lote/SubLote 										|
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If nQuant > 0
			dbSelectArea(cAlias)
			CriaLote("SD5",SD5->D5_PRODUTO,SD5->D5_LOCAL,SD5->D5_LOTECTL,cNumLote,SD5->D5_LOTEFOR,;
					 SD5->D5_CLIFOR,SD5->D5_LOJA,"MAN","MN",CriaVar("B8_PRODUTO")+CriaVar("B8_DOC")+;
					 SerieNfId("SB8",5,"B8_SERIE")+CriaVar("B8_CLIFOR")+CriaVar("B8_LOJA"),SD5->D5_NUMSEQ,SD5->D5_DOC,;
					 SD5->D5_SERIE,SD5->D5_OP,nQuant,ConvUm(SD5->D5_PRODUTO,nQuant,0,2),SD5->D5_DATA,SD5->D5_DTVALID,.T.,SD5->D5_POTENCI)
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava flag de identificacao para inclusao manual.                 Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			Reclock("SD5",.F.)
			Replace D5_ORIGLAN With "MAN"
			Replace D5_NUMSEQ  With cNumLote
			If nQuant <> nQuantSD5
				Replace D5_QUANT   With nQuant
				Replace D5_QTSEGUM With ConvUm(SD5->D5_PRODUTO,nQuant,0,2)
			EndIf
			If Empty(D5_DTVALID)
				Replace D5_DTVALID With SB8->B8_DTVALID
			EndIf
		EndIf
	End Transaction
Else
	If lHelp
		Help(" ",1,"A390ESALDO")
	EndIf
	lRet := .F.
EndIf

RestArea(aAreaAnt)
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA390VldSBF Ё Autor Ё Marcos V. Ferreira   Ё Data Ё 18/08/05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao utilizada para validar se e possivel realizar o      |╠╠
╠╠Ё          Ёajuste entre Enderecamento x Rastreabilidade.      		  |╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A390VldSBF(cProd,cLocal,lHelp,nQuantD5,lTudok)
Local aAreaAnt	:= GetArea()
Local aAreaSB8	:= SB8->(GetArea())
Local aAreaSB2	:= SB2->(GetArea())
Local aAreaSBF	:= SBF->(GetArea())
Local nSaldoSBF := 0
Local nSaldoSB8 := 0
Local nSaldoSB2 := 0
Local lVerifica := .F.
Local lRet		:= .T.
Local lEmpPrev  := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)

Default lHelp := .T.

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Analisa se existe Saldo a Distribuir					|
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("SB2")
dbSetOrder(1)
If dbSeek(xFilial("SB2")+cProd+cLocal) .And. lTudok
	If !(SB2->B2_QACLASS == 0)
		If lHelp .And. !l390Auto
			Aviso('MATA390',STR0024+" "+STR0026+" "+ AllTrim(Str(SB2->B2_QACLASS)), {'Ok'})
		EndIf
		lRet := .F.
	EndIf
EndIf

If lRet

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Analisa se existe Saldo Lote/SubLote					|
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SB8")
	dbSetOrder(1)
	If	dbSeek(xFilial("SB8")+cProd+cLocal)
		Do While !Eof() .And. B8_FILIAL+B8_PRODUTO+B8_LOCAL == xFilial("SB8")+cProd+cLocal
			nSaldoSB8+=(SB8SALDO(,,,,,lEmpPrev,,,.T.)-(SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+IIF(lTudok,B8_QACLASS,0)+AvalQtdPre("SB8",1)))
			dbSkip()
		EndDo
    EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Analisa o saldo do produto								|
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SB2")
	If	dbSeek(xFilial("SB2")+cProd+cLocal)
		nSaldoSB2:=SaldoSB2()
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Analisa o saldo por endereco							|
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nSaldoSBF := SaldoSBF(cLocal,"",cProd,"")

	If nSaldoSBF > nSaldoSB8 .And. nSaldoSBF == nSaldoSB2 .And. nSaldoSBF > 0

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Analisa se a quantidade informada eh o total disponivel, |
		//| caso contrario nao deixa efetuar ajuste.				 |
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !((nSaldoSBF - nSaldoSB8) == nQuantD5)
			If lHelp .And. !l390Auto
				Aviso('MATA390',+STR0027+" "+AllTrim(Str(nSaldoSBF - nSaldoSB8)), {'Ok'})
			EndIf
			lRet := .F.
		EndIF

		If lRet
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Analisa Saldos por Endereco								|
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			dbSelectArea("SBF")
			dbSetOrder(2)
			dbSeek(cSeek:=xFilial("SBF")+cProd+cLocal)
			Do While !Eof() .And. cSeek == SBF->(BF_FILIAL+BF_PRODUTO+BF_LOCAL)
				If Empty(BF_LOTECTL) .And. Empty(BF_NUMLOTE)
					lVerifica := .T.
					If lHelp .And. !l390Auto
						If Aviso("MATA390",STR0025,{STR0006,STR0007}) == 1
							lRet := .F.
						EndIf
						Exit
					Else
						Exit
					EndIf
				EndIf
				dbSkip()
			EndDo
			If !lVerifica
				lRet := .F.
			EndIf
    	EndIf
	Else
		//-- Divergencia de saldos entre SBF x SB2
		If ((nSaldoSBF+nQuantD5) > nSaldoSB2) .And. (nSaldoSBF > nSaldoSB8)
			Aviso('MATA390',STR0033, {'Ok'})
			lRet := .F.
		EndIf
	EndIf
EndIf
RestArea(aAreaSBF)
RestArea(aAreaSB2)
RestArea(aAreaSB8)
RestArea(aAreaAnt)
Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA390GrvLoteЁ Autor Ё Marcos V. Ferreira   Ё Data Ё 21/08/05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁRotina utilizada para gravacao dos campos LOTECTL/NUMLOTE   |╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function A390GrvLote(cAlias,cLoteCtl,cNumLote,lSubLote)

Default cLoteCtl:= ""
Default cNumLote:= ""
Default cAlias	:= ""
Default lSubLote:= .F.

If cAlias == "SDB"
	RecLock(cAlias,.F.)
	Replace DB_LOTECTL With cLoteCtl
	If lSubLote
		Replace DB_NUMLOTE With cNumLote
	EndIf
	MsUnLock()
ElseIf cAlias == "SDA"
	RecLock(cAlias,.F.)
	Replace DA_LOTECTL With cLoteCtl
	If lSubLote
		Replace DA_NUMLOTE With cNumLote
	EndIf
	MsUnLock()
ElseIf cAlias == "SBF"
	RecLock(cAlias,.F.)
	Replace BF_LOTECTL With cLoteCtl
	If lSubLote
		Replace BF_NUMLOTE With cNumLote
	EndIf
	MsUnLock()
ElseIf cAlias == "SBK"
	RecLock(cAlias,.F.)
	Replace BK_LOTECTL With cLoteCtl
	If lSubLote
		Replace BK_NUMLOTE With cNumLote
	EndIf
	MsUnLock()
EndIf
Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁA390ATU()  Ё Autor Ё Marcos V. Ferreira   Ё Data Ё 21/08/05 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁRotina utilizada para ajustar o conteudo do campo D5_ORIGLAN|╠╠
╠╠Ё          Ёantes da inclusao para respeitar a chave da integridade     |╠╠
╠╠Ё          Ёreferencial (SX9).                                          |╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function A390ATU()
SD5->D5_ORIGLAN := "MAN"
Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ёa390AfterCЁ Autor Ё Ricardo Berti         Ё Data Ё 02/01/07 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Tratamento de excecoes na montagem automatica do aCols pelaЁ╠╠
╠╠Ё          Ё FillGetDados, executada APOS gerar cada elemento do aCols. Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁExpL1 := a390AfterC(ExpN1,ExpN2,ExpL2,ExpA1)				  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 = Numero da opcao selecionada                        Ё╠╠
╠╠Ё          Ё ExpN2 = Var. para carga do saldo do produto			 	  Ё╠╠
╠╠Ё          Ё ExpL2 = .T. se parametro "MV_QTDPREV" ativado			  Ё╠╠
╠╠Ё          Ё ExpA1 = Array contendo os registros do SB8  	              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 = .F. aborta a FillGetDados (montagem do aCols)  	  Ё╠╠
╠╠Ё          Ё         .T. continua a montagem do aCols pela FillGetDados Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё MATA390                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function a390AfterC(nOpc,nSaldoB8,lEmpPrev,aRecno)

Local lRet := (nOpc<>2)
If lRet
	Reclock("SB8",.F.)
	AADD(aRecno,Recno())
	nSaldoB8 += SB8SALDO(,,,,,lEmpPrev,,,.T.)
EndIf
Return (lRet)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁPrograma  ЁMenuDef   Ё Autor Ё Fabio Alves Silva     Ё Data Ё05/10/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Utilizacao de menu Funcional                               Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁArray com opcoes da rotina.                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁParametros do array a Rotina:                               Ё╠╠
╠╠Ё          Ё1. Nome a aparecer no cabecalho                             Ё╠╠
╠╠Ё          Ё2. Nome da Rotina associada                                 Ё╠╠
╠╠Ё          Ё3. Reservado                                                Ё╠╠
╠╠Ё          Ё4. Tipo de Transa┤└o a ser efetuada:                        Ё╠╠
╠╠Ё          Ё	  1 - Pesquisa e Posiciona em um Banco de Dados    		  Ё╠╠
╠╠Ё          Ё    2 - Simplesmente Mostra os Campos                       Ё╠╠
╠╠Ё          Ё    3 - Inclui registros no Bancos de Dados                 Ё╠╠
╠╠Ё          Ё    4 - Altera o registro corrente                          Ё╠╠
╠╠Ё          Ё    5 - Remove o registro corrente do Banco de Dados        Ё╠╠
╠╠Ё          Ё5. Nivel de acesso                                          Ё╠╠
╠╠Ё          Ё6. Habilita Menu Funcional                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MenuDef()
//Local lPyme		:= Iif(Type("__lPyme") <> "U",__lPyme,.F.)
Private aRotina
aRotina := {	{STR0001 ,"AxPesqui"   , 0 , 1,0,.F.},;	// "Pesquisar"
               {STR0002 ,"A390Visual" , 0 , 2,0,nil},;	// "Visualizar"
   	           {STR0003 ,"A390Inclui" , 0 , 3,0,nil},;	// "Incluir"
       	    	{STR0004 ,"A390Exclui" , 0 , 5,0,nil},;	// "Excluir"
				{STR0037 ,"A390Valid"  , 0 , 4,0,nil},;	// "Validade"
				{STR0021 ,"A390Preco"  , 0 , 6,0,nil},;	// "Precos"
				{STR0018 ,"A390Poten"  , 0 , 7,0,nil} }	// "Potencia"

//If !lPyme  <<== Inibido serie 3 tem banco do conhecimento
	AAdd( aRotina, { STR0032, "MsDocument", 0, 4,0,nil} )  //"Conhecimento"
//EndIf

If ExistBlock ("MTA390MNU")
	ExecBlock ("MTA390MNU",.F.,.F.)
Endif
Return (aRotina)

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠имммммммммммяммммммммммкмммммммяммммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨ Function  Ё IntegDef ╨ Autor Ё Flavio San Miguel    ╨ Data Ё  09/11/12   ╨╠╠
╠╠лмммммммммммьммммммммммймммммммоммммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨ Descricao Ё Funcao de tratamento para o recebimento/envio de mensagem    ╨╠╠
╠╠╨           Ё unica de Manutencao de Lote.                                 ╨╠╠
╠╠лмммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨ Uso       Ё MATA390                                                      ╨╠╠
╠╠хмммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function IntegDef(cXML,nTypeTrans,cTypeMessage)
Return MATI390(cXml,nTypeTrans,cTypeMessage,dValidAnt)

//-------------------------------------------------------------------
/*/{Protheus.doc} M390dValid()
Valida o Prenchimento da nova data de validade
@param dDataValid
@author JosИ EulАlio
@since 22/05/2018
@version P12
@return lRet
/*/
//--------------------------------------------------------------------
Function M390dValid(dDataValid)
Local lRet	:= .T.

If dDataValid < dDataBase .And. dDataValid <> SB8->B8_DTVALID
	Help(" ",1,'M390DTVALID',,STR0035 + DtoC(dDataValid) + STR0036 + DtoC(dDataBase),1,0) //"Data informada: "#" И menor que "
	lRet := .F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} M390dFab()
Valida o Prenchimento da nova data de fabricaГЦo
@param dDataFab
@author JosИ EulАlio
@since 22/05/2018
@version P12
@return lRet
/*/
//--------------------------------------------------------------------
Function M390dFab(dDataFab)
Local lRet	:= .T.

If Empty(dDataFab)
	Help(" ",1,'M390DTFABR') //"и obrigatСrio informar a data de fabricaГЦo"
	lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} pergPotenc
	Verifica se existe o pergunte "Ajustar Potencia do Lote ?" no dicionario
	@type Function
	@author Squad Entradas
	@since 13/04/2025
	@version P12
	@return lExistItem, logical, Verdadeiro se existe o pergunte "Ajustar Potencia do Lote ?" no dicionario
/*/
Static Function pergPotenc()
Local oFwSX1Util 		as Object
Local aPergunte	 := {}  as Array
Local lExistItem := .F. as Logical

//Valida pergunta do MTA390
oFwSX1Util := FwSX1Util():New()
oFwSX1Util:AddGroup("MTA390")
oFwSX1Util:SearchGroup()
aPergunte := oFwSX1Util:GetGroup("MTA390")

If Len(aPergunte[2]) == 2
	If aPergunte[2][2]:CX1_GSC+aPergunte[2][2]:CX1_ORDEM+aPergunte[2][2]:CX1_TIPO == "C02N"
		lExistItem := .T.
	EndIf
EndIf 

FwFreeArray(aPergunte)
FreeObj(oFwSX1Util)

Return lExistItem
