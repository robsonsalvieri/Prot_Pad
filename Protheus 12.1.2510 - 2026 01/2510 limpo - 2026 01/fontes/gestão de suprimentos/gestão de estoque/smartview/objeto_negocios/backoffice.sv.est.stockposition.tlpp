#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.stockposition.ch"
#include "totvs.framework.treports.integratedprovider.th"
#DEFINE SX1GRUPO 'ESTT001'

using namespace totvs.framework.treports.integratedprovider

namespace totvs.protheus.backoffice.est.posicaoestoque.integratedprovider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SB2,SB9", customTables="SB1,SB2",name="Posicao do Estoque", country="ALL")
 
//-------------------------------------------------------------------
/*{Protheus.doc} PosicaoEstoqueSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Posição do Estoque
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class PosicaoEstoqueSmartViewBusinessObject From IntegratedProvider

	Public Method new()				as object
	Public Method getData()			as object
	Public Method getSchema()		as object

	Protected data jItems			as Json
	Protected data cWhere			as Character
	Protected data lExistPergunte	as logical
	Protected data aFields			as Array	
	Protected data cQuery			as Array
    Protected data cWhereFiltroSV	as character
    Protected data cIsNullSV		as character

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class PosicaoEstoqueSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea(STR0001) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002) //"Posicao do Estoque"
 
	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0003) //"Este relatorio emite a posicao dos saldos e empenhos de cada produto em estoque."

	//Indica o pergunte que será utilizado no relatório
    self:lExistPergunte := self:setPergunte( SX1GRUPO )//ESTT013
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0008,i18n(STR0009,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0009,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf
    
	//Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class PosicaoEstoqueSmartViewBusinessObject

	Local oQuery							as object

	Local jParams							as Json

	Local cQuery							as Character
	Local cFields							as Character
	Local cLocProc							as Character
	Local cAliasTmp							as Character
	Local cSb1Excl := FWModeAccess("SB1")	as Character
	Local cSb2Excl := FWModeAccess("SB2")	as Character
	Local cSb9Excl := FWModeAccess("SB9")	as Character
	Local cSd1Excl := FWModeAccess("SD1")	as Character
	Local cSd2Excl := FWModeAccess("SD2")	as Character
	Local cSd3Excl := FWModeAccess("SD3")	as Character
	Local cNnrExcl := FWModeAccess("NNR")	as Character
	Local cCustom							as Character
	Local cFilExec							as Character
	Local cEmpExec							as Character

	Local nQuant							as Numeric
	Local nValor							as Numeric
	Local nQuantR							as Numeric
	Local nValorR							as Numeric
	Local nX								as Numeric
	Local nS								as Numeric

	Local dDataRef							as Date

	Local lVeic								as Logical
	Local lNoRunCalc						as Logical

	Local aSaldo							as Array
	Local aCustom							as Array
	Local aStrings							as Array

	//Valida o pergunte
	If !self:lExistPergunte	
		return self:oData
	Endif

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))
	cFilExec := AllTrim(FWFilialName())

	//Se tiver custom
	cCustom := self:getSQLFields(.f.,nil,.t.)	
	if !empty(cCustom)	
		aCustom := StrTokArr(cCustom,',')
		For nX := 1 to len(aCustom)		
			aadd(self:aFields,aCustom[nX])
		Next
	EndIf

	//Campos que farão parte do select da query
	cFields := StrTran(ArrTokStr(self:aFields),"|",",")	

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta MTR260R2
	//--------------------------------------------------------------------------
	// MV_PAR01 - Da Filial
	// MV_PAR02 - Ate a Filial    
	// MV_PAR03 - Do Produto      
	// MV_PAR04 - Ate Poduto      
	// MV_PAR05 - Do Tipo
	// MV_PAR06 - Ate Tipo
	// MV_PAR07 - Do Grupo
	// MV_PAR08 - Ate Grupo
	// MV_PAR09 - Da Descricao    
	// MV_PAR10 - Ate Descricao   
	// MV_PAR11 - Codigo Item De  
	// MV_PAR12 - Codigo Item Ate 
	// MV_PAR13 - Imprime produtos: Todos /Positivos /Negativos
	// MV_PAR14 - Saldo Do Prod a Considerar: Atual / Fechamento / Movimento
	// MV_PAR15 - Data Referencia
	// MV_PAR16 - Qual Moeda
	// MV_PAR17 - Listar Pords C/ Saldo Zerado: (S)im (N)ao
	// MV_PAR18 - Imprimir o Valor: Custo / Custo Std / Ult Prc Compr
	// MV_PAR19 - Listar Prods C/ Valor Zerado: (S)im (N)ao

	//--------------------------------------------------------------------------
	//Pego os parametros
	jParams	:= oFilter:getParameters() //metodo para retorno do json dos parâmetros

    //Carrega as intruções de filtro recebidas pelo Smart View
    self:cWhereFiltroSV := "AND "
	If !oFilter:hasFilter()
        self:cWhereFiltroSV := ""
    EndIf
	self:cWhereFiltroSV += oFilter:getSQLExpression()

	//Retorno da expressão 'NULL' verificando o banco de dados utilizado (ex: SQL = ISNULL)
	self:cIsNullSV := MatIsNull()

	//Seto variaveis
	cLocProc    := GetMvNNR( 'MV_LOCPROC' , '99' )
	lVeic		:= Upper(SuperGetMV( 'MV_VEICULO' ,.F., 'N' ) ) == "S"
	dDataRef := Dtos(FwDateTimeToLocal(jParams['MV_PAR15'][1])[1])

	aStrings := {}

	cQuery := " SELECT ? "
	aadd(aStrings,{cFields,"U"})

	If (jParams['MV_PAR14'][1] == 3)
		cQuery += ", " + self:cIsNullSV + "( SB9.B9_COD, ' ' ) B9_COD,  	"
		cQuery += self:cIsNullSV + "( SB9.B9_QINI,  0  ) B9_QINI,  "
		cQuery += self:cIsNullSV + "( SB9.B9_QISEGUM, 0  ) B9_QISEGUM,"
		cQuery += self:cIsNullSV + "( SB9.B9_VINI1, 0  ) B9_VINI1, "
		cQuery += self:cIsNullSV + "( SB9.B9_VINI2, 0  ) B9_VINI2, "
		cQuery += self:cIsNullSV + "( SB9.B9_VINI3, 0  ) B9_VINI3, "
		cQuery += self:cIsNullSV + "( SB9.B9_VINI4, 0  ) B9_VINI4, "
		cQuery += self:cIsNullSV + "( SB9.B9_VINI5, 0  ) B9_VINI5, "
		cQuery += "	( 	SELECT " + self:cIsNullSV + "( MAX( SD1.D1_COD ), ' ' ) D1_COD "
		cQuery += "	FROM "+ RetSQLName( 'SD1' ) +"  SD1 "
		cQuery += "	WHERE "		
		If cSd1Excl == "E"
			If cSb2Excl == "E"
				cQuery += " SD1.D1_FILIAL = SB2.B2_FILIAL "
			Else
				cQuery += " SD1.D1_FILIAL >= ? AND  SD1.D1_FILIAL <= ? "
				aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
				aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
			EndIf
		Else
			cQuery += " SD1.D1_FILIAL = ? "
			aadd(aStrings,{xFilial("SD1"),"S"})
		EndIf
		cQuery += "	AND SD1.D1_COD =  " + self:cIsNullSV + "( SB9.B9_COD, ' ' )  "
		cQuery += "	AND SD1.D1_LOCAL = " + self:cIsNullSV + "( SB9.B9_LOCAL, ' ' )"
		cQuery += "	AND SD1.D1_DTDIGIT >= " + self:cIsNullSV + "( SB9.B9_DATA, ' ' ) "
		cQuery += "	AND SD1.D1_DTDIGIT <= ? "
		aadd(aStrings, { dDataRef,"S"})
		cQuery += "	AND SD1.D_E_L_E_T_ = ?  "
		aadd(aStrings, { ' ',"S"})
		cQuery += "	) D1_COD,  "

		cQuery += "	( 	SELECT " + self:cIsNullSV + "( MAX( SD2.D2_COD ), ' ' ) D2_COD  "
		cQuery += "	FROM "+ RetSQLName( 'SD2' ) +"   SD2  "
		cQuery += "	WHERE "		
		If cSd2Excl == "E"
			If cSb2Excl == "E"
				cQuery += " SD2.D2_FILIAL = SB2.B2_FILIAL "
			Else
				cQuery += " SD2.D2_FILIAL >= ? AND  SD2.D2_FILIAL >= ? "
				aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
				aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
			EndIf
		Else
			cQuery += " SD2.D2_FILIAL = ? "
			aadd(aStrings,{xFilial("SD2"),"S"})
		EndIf
		cQuery += "	AND SD2.D2_COD =  " + self:cIsNullSV + "( SB9.B9_COD, ' ' ) "
		cQuery += "	AND SD2.D2_LOCAL =  " + self:cIsNullSV + "( SB9.B9_LOCAL, ' ' ) "
		cQuery += "	AND SD2.D2_EMISSAO BETWEEN  " + self:cIsNullSV + "( SB9.B9_DATA, ' ' ) AND ?  "
		aadd(aStrings, { dDataRef ,"S"})
		cQuery += "	AND SD2.D_E_L_E_T_ = ?  "
		aadd(aStrings, { ' ' ,"S"})
		cQuery += "	) D2_COD,  "
		cQuery += "	( 	SELECT  " + self:cIsNullSV + "( MAX( SD3.D3_COD ), ' ' )  "
		cQuery += "	FROM  "+ RetSQLName( 'SD3' ) +"   SD3  "
		cQuery += "	WHERE "		 
		If cSd3Excl == "E"
			If cSb2Excl == "E"
				cQuery += " SD3.D3_FILIAL = SB2.B2_FILIAL "
			Else
				cQuery += " SD3.D3_FILIAL >= ? AND  SD3.D3_FILIAL <= ? "
				aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
				aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
			EndIf
		Else
			cQuery += " SD3.D3_FILIAL = ? "
			aadd(aStrings,{xFilial("SD3"),"S"})
		EndIf
		cQuery += "				  AND SD3.D3_COD =  " + self:cIsNullSV + "( SB9.B9_COD, ' ' )  "
		cQuery += "	   			  AND SD3.D3_LOCAL =  " + self:cIsNullSV + "( SB9.B9_LOCAL, ' ' )  "
		cQuery += "	   			  AND SD3.D3_EMISSAO >=  " + self:cIsNullSV + "( SB9.B9_DATA, ' ' ) "
		cQuery += "	   			  AND SD3.D3_EMISSAO <=  ? "
		aadd(aStrings,{ dDataRef ,"S"})
		cQuery += " AND SD3.D_E_L_E_T_ = ?  "
		aadd(aStrings, { ' ' ,"S"})
		cQuery += ") D3_COD " 
	EndIf
	cQuery += " FROM "+ RetSQLName( 'SB2' ) +" SB2 "
	cQuery += "	INNER JOIN "+ RetSQLName( 'SB1' ) +" SB1 "
	cQuery += "	ON ( "
	If cSb1Excl == "E"
		If cSb2Excl == "E"
			cQuery += " SB1.B1_FILIAL = SB2.B2_FILIAL AND SB1.B1_COD = SB2.B2_COD "
		Else
			cQuery += " SB1.B1_FILIAL >= ? AND SB1.B1_FILIAL <= ? AND SB1.B1_COD = SB2.B2_COD "
			aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
			aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
		EndIf
	Else
		cQuery += " SB1.B1_FILIAL = ? AND SB1.B1_COD = SB2.B2_COD "
		aadd(aStrings,{xFilial("SB1"),"S"})
	EndIf

	cQuery += " ) AND SB1.D_E_L_E_T_ = ? "
	aadd(aStrings, { ' ' ,"S"})
	cQuery += " INNER JOIN "+ RetSQLName( 'NNR' ) +" NNR "
	cQuery += " ON ( "
	If cNnrExcl == "E"
		If cSb2Excl == "E"
			cQuery += " NNR.NNR_FILIAL = SB2.B2_FILIAL AND NNR.NNR_CODIGO = SB2.B2_LOCAL "
		Else		
			cQuery += " NNR.NNR_FILIAL >= ? AND NNR.NNR_FILIAL <= ? AND NNR.NNR_CODIGO = SB2.B2_LOCAL "
			aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
			aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
		EndIf
	Else
		cQuery += " NNR.NNR_FILIAL = ? AND NNR.NNR_CODIGO = SB2.B2_LOCAL "
		aadd(aStrings,{xFilial("NNR"),"S"})
	EndIf		 
	cQuery += " ) AND NNR.D_E_L_E_T_ = ? "
	aadd(aStrings, { ' ' ,"S"})

	If (jParams['MV_PAR14'][1] == 3)
		cQuery += " LEFT OUTER JOIN (  "
		cQuery += " SELECT B9_FILIAL, B9_COD, B9_LOCAL, B9_DATA, B9_QINI, B9_VINI1, B9_QISEGUM, B9_VINI2, B9_VINI3, B9_VINI4, B9_VINI5 "
		cQuery += " FROM "+ RetSQLName( 'SB9' ) +" SB9A "
		cQuery += " WHERE SB9A.D_E_L_E_T_ = ? "
		aadd(aStrings, { ' ' ,"S"})
		If cSb9Excl == "E"
			cQuery += " AND SB9A.B9_FILIAL >= ? AND SB9A.B9_FILIAL <= ? "
			aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
			aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
		Else
			cQuery += " AND SB9A.B9_FILIAL = ? "
			aadd(aStrings,{xFilial("SB9"),"S"})
		EndIf

		cQuery += " AND SB9A.B9_DATA = (  "
		cQuery += " SELECT " + self:cIsNullSV + "( MAX( SB9B.B9_DATA ), ' ' )  "
		cQuery += " FROM "+ RetSQLName( 'SB9' ) +" SB9B  "
		cQuery += " WHERE SB9B.B9_FILIAL = SB9A.B9_FILIAL  "
		cQuery += " AND SB9B.B9_COD  = SB9A.B9_COD  "
		cQuery += " AND SB9B.B9_LOCAL = SB9A.B9_LOCAL"
		cQuery += " AND SB9B.B9_DATA <= ? "
		cQuery += " AND SB9B.D_E_L_E_T_ = ? ) ) "	
		aadd(aStrings, { ' ' ,"S"})	
		cQuery += " SB9 ON ( SB9.B9_FILIAL = SB2.B2_FILIAL AND SB9.B9_COD = SB2.B2_COD AND SB9.B9_LOCAL = SB2.B2_LOCAL )  "
		aadd(aStrings, { dDataRef ,"S"})
	EndIf

	cQuery += " WHERE"
	If cSb2Excl == "E"
		cQuery += " SB2.B2_FILIAL >= ? AND  SB2.B2_FILIAL <= ? "
		aadd(aStrings,{jParams['MV_PAR01'][1],"S"})
		aadd(aStrings,{jParams['MV_PAR02'][1],"S"})
	Else
		cQuery += " SB2.B2_FILIAL = ? "
		aadd(aStrings,{FWxFilial("SB2"),"S"})
	EndIf	

	If lVeic
		cQuery += " AND SB1.B1_CODITE >= ? AND SB1.B1_CODITE <= ? "
		aadd(aStrings,{jParams['MV_PAR11'][1],"S"})
		aadd(aStrings,{jParams['MV_PAR12'][1],"S"})
	Else
		cQuery += " AND SB1.B1_COD >= ? AND SB1.B1_COD <= ? "
		aadd(aStrings,{jParams['MV_PAR03'][1],"S"})
		aadd(aStrings,{jParams['MV_PAR04'][1],"S"})
	EndIf

	cQuery += " AND SB1.B1_GRUPO >= ? AND SB1.B1_GRUPO <= ? "
	aadd(aStrings,{jParams['MV_PAR07'][1],"S"})
	aadd(aStrings,{jParams['MV_PAR08'][1],"S"})

	cQuery += " AND SB1.B1_TIPO  >= ? AND SB1.B1_TIPO  <= ? "
	aadd(aStrings,{jParams['MV_PAR05'][1],"S"})
	aadd(aStrings,{jParams['MV_PAR06'][1],"S"})

	cQuery += " AND SB1.B1_DESC >=  ? AND SB1.B1_DESC <= ? "
	aadd(aStrings,{jParams['MV_PAR09'][1],"S"})
	aadd(aStrings,{jParams['MV_PAR10'][1],"S"})

	If jParams['MV_PAR13'][1] == 3 //-- Somente Negativos
		If jParams['MV_PAR17'][1] == 2 //-- Imprime Zerados
			If jParams['MV_PAR14'][1] == 1 //-- Saldo Atual
				cQuery += " AND (SB2.B2_QATU < ?)"
			ElseIf jParams['MV_PAR14'][1] == 2 .Or. jParams['MV_PAR14'][1] == 4 //-- Saldo Final //--Fechamento FIFO
				cQuery += " AND (SB2.B2_QFIM < ?)"
			ElseIf jParams['MV_PAR14'][1] == 3 //-- Saldo Movimento
				cQuery += " AND (SB9.B9_QINI < ?)"
			Else
				cQuery += " AND (SB2.B2_QATU < ?)"
			EndIf
		Else //-- Nao Imprime Zerados
			If jParams['MV_PAR14'][1] == 1 //-- Saldo Atual
				cQuery += " AND (SB2.B2_QATU <= ?)"
			ElseIf jParams['MV_PAR14'][1] == 2 .Or. jParams['MV_PAR14'][1] == 4 //-- Saldo Final //--Fechamento FIFO
				cQuery += " AND (SB2.B2_QFIM <= ?)"
			ElseIf jParams['MV_PAR14'][1] == 3 //-- Saldo Movimento
				cQuery += " AND (SB9.B9_QINI <= ?)"
			Else
				cQuery += " AND (SB2.B2_QATU <= ?)"
			EndIf
		EndIf
		aadd(aStrings,{0,"N"})
	ElseIf jParams['MV_PAR17'][1] == 2 //-- Nao Imprime Zerados
		If jParams['MV_PAR14'][1] == 1 //-- Saldo Atual
			cQuery += " AND (SB2.B2_QATU <> ?)"
		ElseIf jParams['MV_PAR14'][1] == 2 .Or. jParams['MV_PAR14'][1] == 4 //-- Saldo Final //--Fechamento FIFO
			cQuery += " AND (SB2.B2_QFIM <> ?)"
		ElseIf jParams['MV_PAR14'][1] == 3 //-- Saldo Movimento
			cQuery += " AND (SB9.B9_QINI <> ?)"
		Else
			cQuery += " AND (SB2.B2_QATU <> ?)"
		EndIf
		aadd(aStrings,{0,"N"})
	ElseIf jParams['MV_PAR19'][1] == 2 
		If jParams['MV_PAR14'][1] == 1 
			cQuery += " AND (SB2.B2_QATU <> ?)"
		ElseIf jParams['MV_PAR14'][1] == 2 .Or. jParams['MV_PAR14'][1] == 4
			cQuery += " AND (SB2.B2_QFIM <> ?)"
		ElseIf jParams['MV_PAR14'][1] == 3
			cQuery += " AND (SB9.B9_QINI <> ?)"
		Else
			cQuery += " AND (SB2.B2_QATU <> ?)"
		EndIf
		aadd(aStrings,{0,"N"})
	EndIf
	cQuery += self:cWhereFiltroSV
	cQuery += self:cWhere
	cQuery += " AND SB2.D_E_L_E_T_ = ? "
	aadd(aStrings, { ' ' ,"S"})
	cQuery += " ORDER BY B2_FILIAL, B2_COD, B2_LOCAL "
	cQuery := changeQuery(cQuery)

	oQuery := FwExecStatement():New(cQuery)

	//Binding
	For nS := 1 to len(aStrings)
		If aStrings[nS][2] == "U"
			oQuery:setUnsafe(nS,aStrings[nS][1])
		ElseIf aStrings[nS][2] == "S"
			oQuery:setString(nS,aStrings[nS][1])
		elseIf aStrings[nS][2] == "N"
			oQuery:setNumeric(nS,aStrings[nS][1])
		EndIf
	next nS	

	cAliasTmp := oQuery:OpenAlias()

	(cAliasTmp)->(dbGoTop())

	While !(cAliasTmp)->(Eof())
		If (jParams['MV_PAR14'][1] == 3)
			lNoRunCalc := ( !Empty( ( cAliasTmp )->B9_COD ) ) .And. ( Empty( ( cAliasTmp )->D1_COD ) .And. Empty( ( cAliasTmp )->D2_COD ) .And. Empty( ( cAliasTmp )->D3_COD ) ) .And. (cAliasTmp)->B2_LOCAL <> cLocProc
		EndIf

		Do Case
			Case (jParams['MV_PAR14'][1] == 1)
				nQuant := (cAliasTmp)->B2_QATU

			Case (jParams['MV_PAR14'][1] == 2)
				nQuant := (cAliasTmp)->B2_QFIM

			Case (jParams['MV_PAR14'][1] == 3)
				If lNoRunCalc
					nQuant := ( cAliasTmp )->B9_QINI
				Else
					nQuant := (aSaldo := CalcEst( (cAliasTmp)->B2_COD,(cAliasTmp)->B2_LOCAL,STOD(dDataRef)+1,(cAliasTmp)->B2_FILIAL ))[ 1 ]
				EndIf

			Case (jParams['MV_PAR14'][1] == 4)
				nQuant := (cAliasTmp)->B2_QFIM

			Case (jParams['MV_PAR14'][1] == 5)
				nQuant := (aSaldo := CalcEstFF( (cAliasTmp)->B2_COD,(cAliasTmp)->B2_LOCAL,STOD(dDataRef)+1,(cAliasTmp)->B2_FILIAL ))[ 1 ]
		EndCase

		If (jParams['MV_PAR18'][1] == 1)
			Do Case
				Case (jParams['MV_PAR14'][1] == 1)
					nValor := (cAliasTmp)->(FieldGet( FieldPos( "B2_VATU"+Str( jParams['MV_PAR16'][1],1 ) ) ))

				Case (jParams['MV_PAR14'][1] == 2)
					nValor := (cAliasTmp)->(FieldGet( FieldPos( "B2_VFIM"+Str( jParams['MV_PAR16'][1],1 ) ) ))

				Case (jParams['MV_PAR14'][1] == 3)
					If lNoRunCalc
						nValor := (cAliasTmp)->( FieldGet( FieldPos( "B9_VINI"+Str( jParams['MV_PAR16'][1],1 ) ) ))
					Else
						nValor := aSaldo[ 1+jParams['MV_PAR16'][1] ]
					EndIf

				Case (jParams['MV_PAR14'][1] == 4)
					nValor := (cAliasTmp)->(FieldGet( FieldPos( "B2_VFIMFF"+Str( jParams['MV_PAR16'][1],1 ) ) ))

				Case (jParams['MV_PAR14'][1] == 5)
						nValor := aSaldo[ 1+jParams['MV_PAR16'][1] ]

			EndCase
		Else
			// Converte valores para a moeda do relatorio (C.St. e U.Pr.Compra)
			Do Case
				Case (jParams['MV_PAR18'][1] == 2)
					nValor := nQuant * xMoeda( RetFldProd((cAliasTmp)->B1_COD,"B1_CUSTD",cAliasTmp),Val( (cAliasTmp)->B1_MCUSTD ),jParams['MV_PAR16'][1],STOD(dDataRef),4 )
				Case (jParams['MV_PAR18'][1] == 3)  // Ult.Pr.Compra sempre na Moeda 1
					nValor := nQuant * xMoeda( RetFldProd((cAliasTmp)->B1_COD,"B1_UPRC" ,cAliasTmp),1,jParams['MV_PAR16'][1],STOD(dDataRef),4 )
			EndCase
		EndIf

		nQuantR := (cAliasTmp)->B2_QEMP + AvalQtdPre("SB2",1,NIL,cAliasTmp) + (cAliasTmp)->B2_RESERVA + (cAliasTmp)->B2_QEMPSA + (cAliasTmp)->B2_QEMPPRJ
		nValorR := (QtdComp(nValor) / QtdComp(nQuant)) * QtdComp(nQuantR)

		//APPENDS
		self:jItems		:= JsonObject():New()

		self:jItems["FILIAL"]	:= cFilAnt
		self:jItems["EMPNOME"]	:= cEmpExec
		self:jItems["FILNOME"]	:= cFilExec

		For nX := 1 To Len(self:aFields)
			if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
				If !Empty( (cAliasTmp)->&(self:aFields[nX]) )
					self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAliasTmp)->&(self:aFields[nX])))	
				EndIf
			Else
				self:jItems[self:aFields[nX]] := (cAliasTmp)->&(self:aFields[nX])
			EndIf
		Next nX

		self:jItems[ "QUANT"  ] := nQuant
		self:jItems[ "QUANTD" ] := nQuant-nQuantR
		self:jItems[ "QTDEMP" ] := nQuantR
		self:jItems[ "VALIMP" ] := nValor
		self:jItems[ "VLREMP" ] := nValorR

		self:processData()
		self:oData:appendData(self:jItems)

		(cAliasTmp)->(DBSkip())
	EndDo

	(cAliasTmp)->( DBCloseArea() )

	oQuery:Destroy()
	FWFreeArray(aStrings)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class PosicaoEstoqueSmartViewBusinessObject

	Local aFldSB1 as Array
	Local aFldSB2 as Array
	Local aFldNNR as Array
	Local aFldMoe as Array
	Local cFields as Character		
	aFldMoe := {"B2_VATU1","B2_VFIM1","B2_CM1","B2_VFIMFF1","B2_VATU2","B2_VFIM2","B2_CM2","B2_VFIMFF2","B2_VATU3","B2_VFIM3","B2_CM3","B2_VFIMFF3","B2_VATU4","B2_VFIM4","B2_CM4","B2_VFIMFF4","B2_VATU5","B2_VFIM5","B2_CM5","B2_VFIMFF5"}
	aFldSB1 := {"B1_COD","B1_DESC","B1_TIPO","B1_UM","B1_GRUPO","B1_MCUSTD","B1_UPRC","B1_CUSTD","B1_CODITE"}

	If SB2->(FieldPos("B2_DMOV")) > 0
		aFldSB2 := {"B2_FILIAL","B2_COD","B2_QATU","B2_QFIM","B2_QEMP","B2_RESERVA","B2_QEMPSA","B2_QEMPPRJ","B2_DMOV","B2_LOCAL"}
	Else
		aFldSB2 := {"B2_FILIAL","B2_COD","B2_QATU","B2_QFIM","B2_QEMP","B2_RESERVA","B2_QEMPSA","B2_QEMPPRJ","B2_LOCAL"}
	EndIf
	aFldNNR := {"NNR_DESCRI"}
	
	
	self:AliasToSchema("SB1", aFldSB1)
	self:AliasToSchema("SB2", aFldSB2)
	self:AliasToSchema("SB2", aFldMoe)
	self:AliasToSchema("NNR", aFldNNR)
	
	
	cFields := ArrTokStr(aFldSB1)+"|"+ArrTokStr(aFldSB2)+"|"+ArrTokStr(aFldMoe)+"|"+ArrTokStr(aFldNNR)
	self:aFields := StrTokArr(cFields,"|")

	self:addProperty( "FILIAL"  , STR0010 	, "string", STR0010 , "FILIAL"	) //"Filial"
	self:addProperty( "EMPNOME" , STR0011 	, "string", STR0011 , "EMPNOME"	) //"Nome Empresa"
	self:addProperty( "FILNOME" , STR0012 	, "string", STR0012 , "FILNOME"	) //"Nome Filial"
	self:addProperty( "QUANT"	, STR0013	, "number", STR0013	,"QUANT"	) //"Saldo Atual"
	self:addProperty( "QUANTD"	, STR0004	, "number", STR0004	,"QUANTD"	) //"Saldo Disponivel"
	self:addProperty( "QTDEMP"	, STR0005	, "number", STR0005	,"QTDEMP"	) //"Qtd. em Empenho"
	self:addProperty( "VALIMP"	, STR0006	, "number", STR0006	,"VALIMP"	) //"Vlr. em Estoque"
	self:addProperty( "VLREMP"	, STR0007	, "number", STR0007	,"VLREMP"	) //"Vlr. em Empenho"

Return self:oSchema
