#include "msobject.ch"
#include "backoffice.sv.est.materialsfromandwith3rdparty.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT008" 

namespace totvs.protheus.backoffice.est.materialsfromandwith3rdparty.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB1,SB6,SBM",name="Materiais De/Em Terceiros",country="ALL",customTables="SB6")

class MaterialsFromAndWith3rdPartySmartViewBusinessObject from totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	public method new() as object
	public method getDescription() as character
	public method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields as array
	Protected data jItens as json
	Protected data cAlias as character
	Protected data cWhere as character
	Protected data lExistPergunte as Logical 

endclass

/*/{Protheus.doc} New 
Metodo de instancia da classe.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method new() class MaterialsFromAndWith3rdPartySmartViewBusinessObject
	
	LOCAL cMsgSX1 as Character 

	_Super:new()

	//Define a Área
	self:appendArea(STR0001) // Estoque/Custos

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002) // Materiais De/Em Terceiros

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
   	   cMsgSX1 := OemToAnsi(I18N(STR0015,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
	   self:setErrorStatus(400,STR0016,cMsgSX1)		//#Sem Pergunte
	   FwLogMsg("WARN",, "SmartView ESTSV016",,,,cMsgSX1,,,)
	EndIf

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method getDescription() as character class MaterialsFromAndWith3rdPartySmartViewBusinessObject
return (STR0003) // Emite um relatório de materiais de terceiros em nosso poder e/ou nosso material em poder de terceiros.

/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class MaterialsFromAndWith3rdPartySmartViewBusinessObject

	local oQuery as object
	local cQuery as character
	local cFilExec as character
	local cEmpExec as character
	local cDataDe as character
	local cDataAte as character
	local cDataDeDev as character
	local cDataAtDev as character
	local cLastFor as character
	local cLastCli as character
	local cFields as character
	local cBkpFil as Character
	local nX as numeric
	local nPrUnit as numeric
	local nSaldo as numeric
	local nCusto as numeric
	local nFil as numeric
	local nDbPar as numeric
	local jParams as json
	local aSaldo as array
	local aDadCliFor := ARRAY(2) as array

	If !self:lExistPergunte
	   Return Self:oData 
	EndIf 

	//Variável com o nome do Grupo de Empresa
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

	//Metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'])

	self:cAlias:= GetNextAlias()
	cFields    := self:getSQLFields(.T.)
	cDataDe    := FwDateTimeToLocal(jParams["MV_PAR07"][1])[1]
	cDataAte   := FwDateTimeToLocal(jParams["MV_PAR08"][1])[1]
	cDataDeDev := FwDateTimeToLocal(jParams["MV_PAR12"][1])[1]
	cDataAtDev := FwDateTimeToLocal(jParams["MV_PAR13"][1])[1]

	self:aFields := estGetCpo(self:getArrayFields())

	cFields += ",SB6.R_E_C_N_O_ AS RECNO,SB1.B1_DESC,SB1.B1_GRUPO,SB1.B1_UM,SBM.BM_DESC "

	cQuery := "SELECT ? "
	cQuery += "FROM " + RetSqlName("SB6") + " SB6 "
	cQuery += "INNER JOIN " + RetSqlName("SB1") + " SB1 ON "
	cQuery += "( SB1.B1_FILIAL = ? AND "
	cQuery += "SB1.B1_COD = SB6.B6_PRODUTO AND "
	cQuery += "SB1.D_E_L_E_T_ = ? ) "
	cQuery += "LEFT JOIN " + RetSqlName("SBM") + " SBM ON "
	cQuery += "(SBM.BM_FILIAL = ? AND "
	cQuery += "SBM.BM_GRUPO = SB1.B1_GRUPO AND "
	cQuery += "SBM.D_E_L_E_T_ = ? ) "
	cQuery += "INNER JOIN " + RetSqlName("SF4") + " SF4 ON "
	cQuery += "( SF4.F4_FILIAL = ? AND "
	cQuery += "SF4.F4_CODIGO = SB6.B6_TES AND "
	If jParams["MV_PAR11"][1] == 2
		cQuery += "SF4.F4_PODER3 <> ? AND "
	EndIf
	cQuery += "SF4.D_E_L_E_T_ = ? ) "
	cQuery += "WHERE B6_FILIAL = ? AND "
	cQuery += "SB6.B6_PRODUTO >= ? AND "
	cQuery += "SB6.B6_PRODUTO <= ? AND "
	cQuery += "((SB6.B6_TPCF = ? AND "
	cQuery += "B6_CLIFOR >= ? AND "
	cQuery += "B6_CLIFOR <= ? ) OR "
	cQuery += "(SB6.B6_TPCF = ? AND "
	cQuery += "B6_CLIFOR >= ? AND "
	cQuery += "B6_CLIFOR <= ? )) AND "
	cQuery += "((SB6.B6_PODER3 = ? AND "
	cQuery += "SB6.B6_DTDIGIT >= ? AND "
	cQuery += "SB6.B6_DTDIGIT <= ? )"
	If jParams["MV_PAR11"][1] == 1
		cQuery += " OR (SB6.B6_PODER3 = ? AND "
		cQuery += "SB6.B6_DTDIGIT >= ? AND "
		cQuery += "SB6.B6_DTDIGIT <= ? )"
	EndIf
	cQuery += ") AND SB6.B6_QUANT <> ? "
	If jParams["MV_PAR10"][1] <> 3
		cQuery += "AND SB6.B6_TIPO = ? "
	EndIf

	//Os filtros serão setados na interface do novo TReports
	If oFilter:hasFilter()
		cQuery += " AND " + oFilter:getSQLExpression()
	Endif

	cQuery += self:cWhere
	cQuery += " AND SB6.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY B6_FILIAL, B6_PRODUTO, B6_LOCAL, B6_IDENT"

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		nDbPar := 1
		oQuery:SetUnsafe(nDbPar++, cFields)
		oQuery:SetString(nDbPar++, FWxFilial("SB1"))
		oQuery:SetString(nDbPar++, " ")
		oQuery:SetString(nDbPar++, FWxFilial("SBM"))
		oQuery:SetString(nDbPar++, " ")
		oQuery:SetString(nDbPar++, FWxFilial("SF4"))
		If jParams["MV_PAR11"][1] == 2
			oQuery:SetString(nDbPar++, "D")
		EndIf
		oQuery:SetString(nDbPar++, " ")
		oQuery:SetString(nDbPar++, FWxFilial("SB6"))
		oQuery:SetString(nDbPar++, jParams['MV_PAR05'][1])
		oQuery:SetString(nDbPar++, jParams['MV_PAR06'][1])
		oQuery:SetString(nDbPar++, "C")
		oQuery:SetString(nDbPar++, jParams["MV_PAR01"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR02"][1])
		oQuery:SetString(nDbPar++, "F")
		oQuery:SetString(nDbPar++, jParams['MV_PAR03'][1])
		oQuery:SetString(nDbPar++, jParams['MV_PAR04'][1])

		oQuery:SetString(nDbPar++, "R")
		oQuery:SetString(nDbPar++, Dtos(cDataDe))
		oQuery:SetString(nDbPar++, Dtos(cDataAte))
		If jParams["MV_PAR11"][1] == 1
			oQuery:SetString(nDbPar++, "D")
			oQuery:SetString(nDbPar++, Dtos(cDataDeDev))
			oQuery:SetString(nDbPar++, Dtos(cDataAtDev))
		EndIf
		oQuery:SetNumeric(nDbPar++, 0)
		If jParams["MV_PAR10"][1] <> 3
			If jParams["MV_PAR10"][1] == 1
				oQuery:SetString(nDbPar++, "D")
			Else
				oQuery:SetString(nDbPar++, "E")
			EndIf
		EndIf
		oQuery:SetString(nDbPar++, " ")

		oQuery:OpenAlias(self:cAlias)

		(self:cAlias)->(DBGoTop())

		Do While !(self:cAlias)->(Eof())

			self:jItens     := JsonObject():New()

			If (self:cAlias)->B6_PODER3 == 'R'
				aSaldo  := (self:cAlias)->(ESTCalcTerc(B6_PRODUTO,B6_CLIFOR,B6_LOJA,B6_IDENT,B6_TES,,cDataDe,cDataAte))
				nSaldo  := aSaldo[1]
				nPrUnit := aSaldo[3]
				If aSaldo[3] == 0
					nPrUnit := (self:cAlias)->(B6_PRUNIT)
				EndIf
			Else
				nSaldo := 0
				aSaldo := {0, 0, 0, 0, Round((self:cAlias)->B6_QUANT * (self:cAlias)->B6_PRUNIT,2), 0}
			EndIf

			If jParams["MV_PAR09"][1] == 2 .And. nSaldo <= 0
				(self:cAlias)->(dbSkip())
				Loop
			EndIf

			If (self:cAlias)->(B6_TPCF) == "C"
				If cLastCli <> (self:cAlias)->(B6_CLIFOR+B6_LOJA)
					If SA1->(MsSeek(xFilial("SA1")+(self:cAlias)->(B6_CLIFOR+B6_LOJA)))
						aDadCliFor[1] := STR0013 //"CLIENTE "
						aDadCliFor[2] := SA1->A1_NOME
					EndIf
					cLastCli := (self:cAlias)->(B6_CLIFOR+B6_LOJA)
				EndIf
			Else
				If cLastFor <> (self:cAlias)->(B6_CLIFOR+B6_LOJA)
					If SA2->(MsSeek(xFilial("SA2")+(self:cAlias)->(B6_CLIFOR+B6_LOJA)))
						aDadCliFor[1] := STR0014 //"FORNECEDOR "
						aDadCliFor[2] := SA2->A2_NOME
					EndIf
					cLastFor := (self:cAlias)->(B6_CLIFOR+B6_LOJA)
				EndIf
			Endif

			nCusto :=  (self:cAlias)->(&("B6_CUSTO1")) / (self:cAlias)->B6_QUANT * nSaldo

			self:jItens["EMPNOME"]  := cEmpExec
			self:jItens["FILNOME"]  := cFilExec
			self:jItens["B1_DESC"]  := (self:cAlias)->B1_DESC
			//If !Empty((self:cAlias)->B1_GRUPO)
				self:jItens["B1_GRUPO"] := (self:cAlias)->B1_GRUPO
				self:jItens["BM_DESC"]  := (self:cAlias)->BM_DESC
			//EndIf
			self:jItens["TIPODESC"] := aDadCliFor[1]
			self:jItens["NTOTNF"]   := (Round(aSaldo[5],2))

			If (self:cAlias)->B6_PODER3 == 'R'
				self:jItens["NSALDO"]   := nSaldo
				self:jItens["NQUJE"]    := (self:cAlias)->B6_QUANT - nSaldo
				self:jItens["NTOTDEV"]  := ( (self:cAlias)->(B6_QUANT) - nSaldo) * nPrUnit
				self:jItens["NCUSTO"]   := nCusto
			EndIf

			If (self:cAlias)->(B6_TPCF) == "C"
				self:jItens["RAZAOSOCIAL"] := SA1->A1_NOME
			Else
				self:jItens["RAZAOSOCIAL"] := SA2->A2_NOME
			EndIf

			For nX := 1 To Len(self:aFields)
				If self:aFields[nX][2] == "date"
					If !Empty( (self:cAlias)->&(self:aFields[nX][1]) )
						self:jItens[self:aFields[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFields[nX][1])))
					EndIf
				ElseIf self:aFields[nX][2] == "boolean"
					If (self:cAlias)->&(self:aFields[nX][1]) == "F"
						self:jItens[self:aFields[nX][1]] := .F.
					Else
						self:jItens[self:aFields[nX][1]] := .T.
					EndIf
				Else
					self:jItens[self:aFields[nX][1]] := (self:cAlias)->&(self:aFields[nX][1])
				EndIf
			Next nX

			self:processData()
			self:appendData(self:jItens)

			(self:cAlias)->(dbSkip())
		EndDo

		(self:cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

/*/{Protheus.doc} oNGetSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method oNGetSchema() class MaterialsFromAndWith3rdPartySmartViewBusinessObject
	local aFieldsSB1 as array
	local aFieldsSBM as array

	aFieldsSB1	 := {"B1_DESC","B1_GRUPO"}
	aFieldsSBM	 := {"BM_DESC"}
	self:aFields := {"B6_FILIAL","B6_CLIFOR","B6_LOJA","B6_PRODUTO","B6_LOCAL","B6_DOC","B6_SERIE",;
					 "B6_EMISSAO","B6_QUANT","B6_QTSEGUM","B6_SEGUM","B6_PRUNIT","B6_TES","B6_TIPO",;
					 "B6_UENT","B6_CUSTO1","B6_CUSTO2","B6_CUSTO3","B6_CUSTO4","B6_CUSTO5","B6_DTDIGIT",;
					 "B6_UM","B6_QULIB","B6_IDENT","B6_TPCF","B6_SALDO","B6_PODER3","B6_ATEND","B6_ORIGLAN",;
					 "B6_ESTOQUE","B6_IDENTB6"}

	self:AliasToSchema("SB1",aFieldsSB1)
	self:AliasToSchema("SBM",aFieldsSBM)
	self:AliasToSchema("SB6",self:aFields)

	self:addProperty("EMPNOME"     , STR0004, "string", STR0004, "EMPNOME")
	self:addProperty("FILNOME"     , STR0005, "string", STR0005, "FILNOME")
	self:addProperty("TIPODESC"    , STR0006, "string", STR0006, "TIPODESC")
	self:addProperty("RAZAOSOCIAL" , STR0007, "string", STR0007, "RAZAOSOCIAL")
	self:addProperty("NQUJE"       , STR0008, "number", STR0008, "NQUJE")
	self:addProperty("NSALDO"      , STR0009, "number", STR0009, "NSALDO")
	self:addProperty("NTOTNF"      , STR0010, "number", STR0010, "NTOTNF")
	self:addProperty("NTOTDEV"     , STR0011, "number", STR0011, "NTOTDEV")
	self:addProperty("NCUSTO"      , STR0012, "number", STR0012, "NCUSTO")

return

/*/{Protheus.doc} estGetCpo
Converte o tipo dos campos para os tipos utilizados no TReports
@type  Função
@author Squad Entradas
@since  Junho 15,2023
/*/
Static Function estGetCpo(aFields as array)
	local aDeParaCpo as array
	local aCpoTmp := {} as array
	local cTipR as character
	local cTipo as character
	local nPos as numeric
	local nX as numeric

	aDeParaCpo := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}}

	For nX := 1 To Len(aFields)
		cTipo := FWSX3Util():GetFieldType( aFields[nX] )
		cTipR := "string"
		If (nPos := aScan( aDeParaCpo, {|c| c[01] = cTipo } ) ) > 0
			cTipR := aDeParaCpo[nPos, 02]
		EndIf

		AAdd( aCpoTmp,;
			{aFields[nX],;
			cTipR})
	Next nX

Return (aCpoTmp)
