#include "msobject.ch"
#include "backoffice.sv.est.kardex.ch"
#include "totvs.framework.treports.integratedprovider.th"

//Grupo de perguntas
#define SX1GRUPO "ESTT004"

namespace totvs.protheus.backoffice.est.kardex.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB2,SB9,SD1,SD2,SD3",name="Kardex",country="ALL")

class KardexTReportsBusinessObject from totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider
    public method new() as object
    public method getDescription() as character
    public method getData() as object

	Protected Method oNGetSchema()

	Protected data aFieldsSB1 as array
	Protected data aFieldsSD1 as array
	Protected data aFieldsSD2 as array
	Protected data aFieldsSD3 as array
	Protected data aFieldsQry as array
	Protected data cWhere as character
	Protected data cAlias as character
	Protected data cMoeda as character
	Protected data lExistPerg as logical
	public data jItens as json

endclass

/*/{Protheus.doc} New
Metodo de instancia da classe.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method new() class KardexTReportsBusinessObject
    _Super:new()

    //Define a Área
    self:appendArea(STR0001) // Estoque/Custos
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) // Kardex

    //Indica o pergunte que será utilizado no relatório
	self:lExistPerg := self:setPergunte( SX1GRUPO )
	If !self:lExistPerg
		self:setErrorStatus(400,STR0023,i18n(STR0022,{SX1GRUPO}))		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0022,{SX1GRUPO}), , , )
	EndIf

		//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method getDescription() as character class KardexTReportsBusinessObject
return (STR0003) // Emite uma relação com as movimentações dos produtos selecionados, ordenados por sequência, cálculo ou dia.

/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class KardexTReportsBusinessObject

    local cQuery as character
    local cFilExec as character
    local cEmpExec as character
	local cProduto as character
	local cDescLocal as character
	local cLocProc as character
	local cIdent as character
    local nFil as numeric
	local nCmInicial as numeric
	local nQtdSegum as numeric
	local nX as numeric
	local nY as numeric
	local nSldEnt as numeric
	local nSldSai as numeric
	local nSldVTot as numeric
	local nMoeda as numeric
	local nLstSMov as numeric
	local nSeqImp as numeric
	local nLisTrfArm as numeric
	local nSelFil as numeric
	local nQualCus as numeric
    local jParams as json
    local lCusRep as logical
    local lVeic as logical
    local lCusFil as logical
    local lCusEmp as logical
    local cDadosProd as character
    local lEstorno as logical
    local lWmsNew as logical
	local lListaDev as logical
    local cProdImp as character
	local cMvCQ as character
	local cRemito as character
	local lD3Servi as logical
    local lLocProc as logical
	local lInverteMov as logical
	local lContinua as logical
	local lDev as logical
	local lPriApropri := .T. as logical
	local lMovSaida as logical
	local lCustoUnif as logical
	local cLocalAnt as character
	local cTipoNf as character
	local cNumSeqTr as character
	local cDataDe as character
	local cDataAte as character
	local dData as date
	local dDataDe as date
	local dDataAte as date
	local aSalAtu as array
	local aDadosTran as array
	local aAliasSB1 as array
	local aAliasSD3 as array
	local aFieldsDoc as array
	local aTableDoc as array
	local aPrepared as array
	local nPosPrepared as numeric
	local cBkpFil as character

	private oQuerySB2_1 as object
	private oQuerySB2_2 as object

	//Valida o pergunte
	If !self:lExistPerg
		return self:oData
	Endif

    //Variáveis com o nome da Filial e Grupo de Empresa
    cFilExec := AllTrim(FWFilialName())
    cEmpExec := AllTrim(FWEmpName(cEmpAnt))

    //Metodo para retorno do json dos parâmetros
    jParams := oFilter:getParameters()

	//Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	//Inicializa as variáveis do objeto de negocio
	self:cAlias	:= GetNextAlias()

	dDataDe		:= FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1]
	dDataAte	:= FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1]
	cDataDe  	:= dTos(dDataDe)
	cDataAte 	:= dTos(dDataAte)
	dData	 	:= dDataDe
	lCustoUnif	:= (jParams['MV_PAR08'][1] == Repl("*",TamSX3("B2_LOCAL")[1])) .Or. (jParams['MV_PAR08'][1] == Repl("#",TamSX3("B2_LOCAL")[1]))
	aAliasSB1	:= {"PRODUTO","B1_DESC","B1_CODITE","TIPO","UM","GRUPO","B1_POSIPI"}
	aAliasSD3   := {"PRODUTO","ARMAZEM","SEQCALC","DTDIGIT","TES","CF","SEQUENCIA","DOCUMENTO","QUANTIDADE","QUANT2UM","PROJETO","OP","TRT","TIPO","LOTE","CUSTO"}
	aFieldsDoc	:= {"PRODUTO","ARMAZEM","SEQCALC","DTDIGIT","TES","CF","SEQUENCIA","DOCUMENTO","SERIE","QUANTIDADE","QUANT2UM","FORNECEDOR","LOJA","TIPO","LOTE","CUSTO"}
	aTableDoc   := {self:aFieldsSD1,self:aFieldsSD2}
	self:aFieldsQry	:= {}

	//Proteção para os perguntes numéricos que podem ser inicializados com 0
	nLstSMov	:= iniVarNum(jParams['MV_PAR07'][1])
	nMoeda      := iniVarNum(jParams['MV_PAR10'][1])
	nSeqImp		:= iniVarNum(jParams['MV_PAR11'][1])
	nLisTrfArm	:= iniVarNum(jParams['MV_PAR12'][1])
	nSelFil		:= iniVarNum(jParams['MV_PAR15'][1])
	nQualCus	:= iniVarNum(jParams['MV_PAR16'][1])

	self:cMoeda := Str(nMoeda,1,0)

	aPrepared		:= {}
	nPosPrepared	:= 0
	cBkpFil			:= cFilAnt

	For nFil := 1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Busca conteúdo dos parâmetros da SX6 na mudança de filial
		cLocProc	:= GetMvNNR('MV_LOCPROC','99')
		lCusRep		:= SuperGetMv("MV_CUSREP",.F.,.F.)
		lVeic		:= SuperGetMv("MV_VEICULO",.F.,.F.) == "S"
		lCusFil		:= SuperGetMV("MV_CUSFIL" ,.F.,"A") == "F"
		lCusEmp		:= SuperGetMv("MV_CUSFIL" ,.F.,"A") == "E"
		cDadosProd	:= SuperGetMV("MV_ARQPROD",.F.,"SB1")
		lEstorno	:= SuperGetMV('MV_D3ESTOR', .F., 'N') == 'N'
		lWmsNew		:= SuperGetMv("MV_WMSNEW",.F.,.F.)
		If lWmsNew
			lD3Servi := .F.
		Else
			lD3Servi := SuperGetMV('MV_D3SERVI',.F.,'N') == 'N'
		EndIf
		lListaDev	:= SuperGetMV("MV_LISTDEV",.F.,.F.)
		cProdImp	:= GetMV("MV_PRODIMP")
		cMvCQ		:= GetMvNNR('MV_CQ','98')

		lLocProc	:= jParams['MV_PAR08'][1] == cLocProc
		lCusFil		:= lCusFil .And. jParams['MV_PAR08'][1] == Repl("*",TamSX3("B2_LOCAL")[1])
		lCusEmp		:= lCusEmp .And. jParams['MV_PAR08'][1] == Repl("#",TamSX3("B2_LOCAL")[1])

		// Montagem da query principal do objeto de negocio - SD1
		cQuery := "SELECT 'SD1' ARQ, SB1.B1_COD PRODUTO, SB1.B1_TIPO TIPO, SB1.B1_UM UM, SB1.B1_GRUPO GRUPO, "
		cQuery += "SB1.B1_DESC, SB1.B1_POSIPI, D1_SEQCALC SEQCALC, D1_DTDIGIT DTDIGIT, D1_TES TES, "
		cQuery += "D1_CF CF, D1_NUMSEQ SEQUENCIA, D1_DOC DOCUMENTO, D1_SERIE SERIE, D1_QUANT QUANTIDADE, "
		cQuery += "D1_QTSEGUM QUANT2UM, D1_LOCAL ARMAZEM, ' ' PROJETO, ' ' OP, ' ' CC, D1_FORNECE FORNECEDOR, "
		cQuery += "D1_LOJA LOJA, ' ' PEDIDO, D1_TIPO TIPONF, "

		//Adiciona o campo de custo conforme moeda informada
		If lCusRep .And. nQualCus == 2
			cQuery += "D1_CUSRP" + self:cMoeda
		Else
			cQuery += "D1_CUSTO"
			If nMoeda > 1
				cQuery += self:cMoeda
			EndIf
		EndIf
		cQuery += " CUSTO,"

		cQuery += " ' ' TRT, D1_LOTECTL LOTE, SB1.B1_CODITE B1_CODITE, "
		cQuery += "SD1.R_E_C_N_O_ NRECNO, SD1.D1_LOCAL ARMLOC "

		cQuery += "FROM "
		cQuery += RetSqlName('SB1')+ " SB1 "

		// Ajuste do From SB1 / SBZ
		If lCusEmp .And. cDadosProd == 'SBZ'
			cQuery += "INNER JOIN " + RetSqlName("SBZ") + " SBZ "
			cQuery += "ON SBZ.BZ_FILIAL = ? "
			cQuery += "AND SBZ.BZ_COD = SB1.B1_COD "
			cQuery += "AND SBZ.D_E_L_E_T_ = ? "
		EndIf

		cQuery += "INNER JOIN " + RetSqlName("SD1") + " SD1 "
		cQuery += "ON SD1.D1_FILIAL = ? "
		cQuery += "AND SD1.D1_COD = SB1.B1_COD "
		If !lCustoUnif
			cQuery += "AND SD1.D1_LOCAL = ? "
		EndIf
		cQuery += "AND SD1.D1_DTDIGIT >= ? "
		cQuery += "AND SD1.D1_DTDIGIT <= ? "
		cQuery += "AND SD1.D1_ORIGLAN <> ? "
		cQuery += "AND SD1.D1_REMITO = ? "
		cQuery += "AND SD1.D_E_L_E_T_ = ? "

		cQuery += "INNER JOIN " + RetSqlName("SF4") + " SF4 "
		cQuery += "ON SF4.F4_FILIAL = ? "
		cQuery += "AND SF4.F4_CODIGO = SD1.D1_TES "
		cQuery += "AND SF4.F4_ESTOQUE = ? "
		cQuery += "AND SF4.D_E_L_E_T_ = ? "

		cQuery += "WHERE "
		If !lCusEmp
			cQuery += "SB1.B1_FILIAL = ? AND "
		EndIf
		cQuery += "SB1.B1_GRUPO >= ? "
		cQuery += "AND SB1.B1_GRUPO <= ? "
		If !lVeic
			cQuery += "AND SB1.B1_COD >= ? "
			cQuery += "AND SB1.B1_COD <= ? "
		Else
			cQuery += "AND SB1.B1_CODITE >= ? "
			cQuery += "AND SB1.B1_CODITE <= ? "
		EndIf
		cQuery += "AND SB1.B1_TIPO >= ? "
		cQuery += "AND SB1.B1_TIPO <= ? "
		cQuery += "AND SB1.B1_COD <> ? "
		cQuery += "AND SB1.D_E_L_E_T_ = ? "

		cQuery += "UNION "

		// Montagem da query principal do objeto de negocio - SD2
		cQuery += "SELECT 'SD2', SB1.B1_COD, SB1.B1_TIPO, SB1.B1_UM, SB1.B1_GRUPO, SB1.B1_DESC, "
		cQuery += "SB1.B1_POSIPI, D2_SEQCALC, D2_EMISSAO, D2_TES, D2_CF, D2_NUMSEQ, D2_DOC,	D2_SERIE, "
		cQuery += "D2_QUANT, D2_QTSEGUM, D2_LOCAL, ' ', ' ',' ', D2_CLIENTE, D2_LOJA, D2_PEDIDO,D2_TIPO, "

		//Adiciona o campo de custo conforme moeda informada
		If lCusRep .And. nQualCus == 2
			cQuery += "D2_CUSRP" + self:cMoeda
		Else
			cQuery += "D2_CUSTO" + self:cMoeda
		EndIf
		cQuery += " CUSTO,"

		cQuery +=  " ' ', D2_LOTECTL, "

		cQuery += "SB1.B1_CODITE B1_CODITE, SD2.R_E_C_N_O_ SD2RECNO, SD2.D2_LOCAL "

		cQuery += "FROM "
		cQuery += RetSqlName('SB1')+ " SB1 "

		// Ajuste do From SB1 / SBZ
		If lCusEmp .And. cDadosProd == 'SBZ'
			cQuery += "INNER JOIN " + RetSqlName("SBZ") + " SBZ "
			cQuery += "ON SBZ.BZ_FILIAL = ? "
			cQuery += "AND SBZ.BZ_COD = SB1.B1_COD "
			cQuery += "AND SBZ.D_E_L_E_T_ = ? "
		EndIf

		cQuery += "INNER JOIN " + RetSqlName("SD2") + " SD2 "
		cQuery += "ON SD2.D2_FILIAL = ? "
		cQuery += "AND SD2.D2_COD = SB1.B1_COD "
		If !lCustoUnif
			cQuery += "AND SD2.D2_LOCAL = ? "
		EndIf
		cQuery += "AND SD2.D2_EMISSAO >= ? "
		cQuery += "AND SD2.D2_EMISSAO <= ? "
		cQuery += "AND SD2.D2_ORIGLAN <> ? "
		cQuery += "AND SD2.D2_REMITO = ? "
		cQuery += "AND SD2.D_E_L_E_T_ = ? "

		cQuery += "INNER JOIN " + RetSqlName("SF4") + " SF4 "
		cQuery += "ON SF4.F4_FILIAL = ? "
		cQuery += "AND SF4.F4_CODIGO = SD2.D2_TES "
		cQuery += "AND SF4.F4_ESTOQUE = ? "
		cQuery += "AND SF4.D_E_L_E_T_ = ? "

		cQuery += "WHERE "
		If !lCusEmp
			cQuery += "SB1.B1_FILIAL = ? AND "
		EndIf
		cQuery += "SB1.B1_GRUPO >= ? "
		cQuery += "AND SB1.B1_GRUPO <= ? "

		If !lVeic
			cQuery += "AND SB1.B1_COD >= ? "
			cQuery += "AND SB1.B1_COD <= ? "
		Else
			cQuery += "AND SB1.B1_CODITE >= ? "
			cQuery += "AND SB1.B1_CODITE <= ? "
		EndIf
		cQuery += "AND SB1.B1_TIPO >= ? "
		cQuery += "AND SB1.B1_TIPO <= ? "
		cQuery += "AND SB1.B1_COD <> ? "
		cQuery += "AND SB1.D_E_L_E_T_ = ? "

		cQuery += "UNION "

		// Montagem da query principal do objeto de negocio - SD3
		cQuery += "SELECT 'SD3', SB1.B1_COD, SB1.B1_TIPO, SB1.B1_UM UM, SB1.B1_GRUPO, SB1.B1_DESC, "
		cQuery += "SB1.B1_POSIPI, D3_SEQCALC, D3_EMISSAO, D3_TM, D3_CF, D3_NUMSEQ, D3_DOC, ' ', "
		cQuery += "D3_QUANT, D3_QTSEGUM, D3_LOCAL, D3_PROJPMS, D3_OP, D3_CC, ' ', ' ', ' ', ' ', "

		//Adiciona o campo de custo conforme moeda informada
		If lCusRep .And. nQualCus == 2
			cQuery += "D3_CUSRP" + self:cMoeda
		Else
			cQuery += "D3_CUSTO" + self:cMoeda
		EndIf
		cQuery += " CUSTO,"

		cQuery += " D3_TRT, D3_LOTECTL, SB1.B1_CODITE B1_CODITE, SD3.R_E_C_N_O_ SD3RECNO, SD3.D3_LOCAL "

		cQuery += "FROM "
		cQuery += RetSqlName('SB1')+ " SB1 "

		// Ajuste do From SB1 / SBZ
		If lCusEmp .And. cDadosProd == 'SBZ'
			cQuery += "INNER JOIN " + RetSqlName("SBZ") + " SBZ "
			cQuery += "ON SBZ.BZ_FILIAL = ? "
			cQuery += "AND SBZ.BZ_COD = SB1.B1_COD "
			cQuery += "AND SBZ.D_E_L_E_T_ = ? "
		EndIf

		cQuery += "INNER JOIN " + RetSqlName("SD3") + " SD3 "
		cQuery += "ON SD3.D3_FILIAL = ? "
		cQuery += "AND SD3.D3_COD = SB1.B1_COD "
		If !lCustoUnif .And. !lLocProc
			cQuery += "AND SD3.D3_LOCAL = ? "
		EndIf
		cQuery += "AND SD3.D3_EMISSAO >= ? "
		cQuery += "AND SD3.D3_EMISSAO <= ? "
		If lEstorno
			cQuery += "AND SD3.D3_ESTORNO <> ? "
		EndIf
		If lD3Servi .And. IntDL()
			cQuery += "AND ( "
			cQuery += "(SD3.D3_SERVIC = ?) "
			cQuery += "OR "
			cQuery += "(SD3.D3_SERVIC <> ? AND SD3.D3_TM <= ?) "
			cQuery += "OR "
			cQuery += "(SD3.D3_SERVIC <> ? AND SD3.D3_TM > ? AND SD3.D3_LOCAL = ?) "
			cQuery += ") "
		EndIf
		cQuery += "AND SD3.D_E_L_E_T_ = ? "

		cQuery += "WHERE "
		If !lCusEmp
			cQuery += "SB1.B1_FILIAL = ? AND "
		EndIf
		cQuery += "SB1.B1_GRUPO >= ? "
		cQuery += "AND SB1.B1_GRUPO <= ? "

		If !lVeic
			cQuery += "AND SB1.B1_COD >= ? "
			cQuery += "AND SB1.B1_COD <= ? "
		Else
			cQuery += "AND SB1.B1_CODITE >= ? "
			cQuery += "AND SB1.B1_CODITE <= ? "
		EndIf
		cQuery += "AND SB1.B1_TIPO >= ? "
		cQuery += "AND SB1.B1_TIPO <= ? "
		cQuery += "AND SB1.B1_COD <> ? "
		cQuery += "AND SB1.D_E_L_E_T_ = ? "

		If nLstSMov == 1
			cQuery += " UNION "
			cQuery += "SELECT 'SB1', SB1EXS.B1_COD, SB1EXS.B1_TIPO, SB1EXS.B1_UM, SB1EXS.B1_GRUPO, SB1EXS.B1_DESC,"
			cQuery += " SB1EXS.B1_POSIPI, '', '', '', '', '', '', '', 0, 0, '', '', '', '', '', '', '', '', 0,"
			cQuery += " '', '', SB1EXS.B1_CODITE CODITE, 0, ''"
			cQuery += " FROM "+RetSqlName("SB1") + " SB1EXS "

			cQuery += "WHERE "
			If !lCusEmp
				cQuery += "SB1EXS.B1_FILIAL = ? AND "
			EndIf
			cQuery += "SB1EXS.B1_GRUPO >= ? "
			cQuery += "AND SB1EXS.B1_GRUPO <= ? "
			If !lVeic
				cQuery += "AND SB1EXS.B1_COD >= ? "
				cQuery += "AND SB1EXS.B1_COD <= ? "
			Else
				cQuery += "AND SB1EXS.B1_CODITE >= ? "
				cQuery += "AND SB1EXS.B1_CODITE <= ? "
			EndIf
			cQuery += "AND SB1EXS.B1_TIPO >= ? "
			cQuery += "AND SB1EXS.B1_TIPO <= ? "
			cQuery += "AND SB1EXS.B1_COD <> ? "
			cQuery += "AND SB1EXS.D_E_L_E_T_ = ? "

			cQuery += " AND NOT EXISTS ( "
			cQuery += "SELECT 1 FROM " + RetSqlName("SB1") + " SB1 "

			cQuery += "INNER JOIN " + RetSqlName("SD1") + " SD1 "
			cQuery += "ON SD1.D1_FILIAL = ? "
			cQuery += "AND SD1.D1_COD = SB1.B1_COD "
			If !lCustoUnif
				cQuery += "AND SD1.D1_LOCAL = ? "
			EndIf
			cQuery += "AND SD1.D1_DTDIGIT >= ? "
			cQuery += "AND SD1.D1_DTDIGIT <= ? "
			cQuery += "AND SD1.D1_ORIGLAN <> ? "
			cQuery += "AND SD1.D1_REMITO = ? "
			cQuery += "AND SD1.D_E_L_E_T_ = ? "

			cQuery += "INNER JOIN " + RetSqlName("SF4") + " SF4 "
			cQuery += "ON SF4.F4_FILIAL = ? "
			cQuery += "AND SF4.F4_CODIGO = SD1.D1_TES "
			cQuery += "AND SF4.F4_ESTOQUE = ? "
			cQuery += "AND SF4.D_E_L_E_T_ = ? "

			cQuery += "WHERE "
			If !lCusEmp
				cQuery += "SB1.B1_FILIAL = ? AND "
			EndIf
			cQuery += "SB1.B1_GRUPO >= ? "
			cQuery += "AND SB1.B1_GRUPO <= ? "
			cQuery += "AND SB1.B1_COD = SB1EXS.B1_COD "
			cQuery += "AND SB1.B1_TIPO >= ? "
			cQuery += "AND SB1.B1_TIPO <= ? "
			cQuery += "AND SB1.B1_COD <> ? "
			cQuery += "AND SB1.D_E_L_E_T_ = ? "
			cQuery += " ) "

			cQuery += " AND NOT EXISTS ( "
			cQuery += "SELECT 1 FROM " + RetSqlName("SB1") + " SB1 "

			cQuery += "INNER JOIN " + RetSqlName("SD2") + " SD2 "
			cQuery += "ON SD2.D2_FILIAL = ? "
			cQuery += "AND SD2.D2_COD = SB1.B1_COD "
			If !lCustoUnif
				cQuery += "AND SD2.D2_LOCAL = ? "
			EndIf
			cQuery += "AND SD2.D2_EMISSAO >= ? "
			cQuery += "AND SD2.D2_EMISSAO <= ? "
			cQuery += "AND SD2.D2_ORIGLAN <> ? "
			cQuery += "AND SD2.D2_REMITO = ? "
			cQuery += "AND SD2.D_E_L_E_T_ = ? "

			cQuery += "INNER JOIN " + RetSqlName("SF4") + " SF4 "
			cQuery += "ON SF4.F4_FILIAL = ? "
			cQuery += "AND SF4.F4_CODIGO = SD2.D2_TES "
			cQuery += "AND SF4.F4_ESTOQUE = ? "
			cQuery += "AND SF4.D_E_L_E_T_ = ? "

			cQuery += "WHERE "
			If !lCusEmp
				cQuery += "SB1.B1_FILIAL = ? AND "
			EndIf
			cQuery += "SB1.B1_GRUPO >= ? "
			cQuery += "AND SB1.B1_GRUPO <= ? "
			cQuery += "AND SB1.B1_COD = SB1EXS.B1_COD "
			cQuery += "AND SB1.B1_TIPO >= ? "
			cQuery += "AND SB1.B1_TIPO <= ? "
			cQuery += "AND SB1.B1_COD <> ? "
			cQuery += "AND SB1.D_E_L_E_T_ = ? "
			cQuery += " ) "

			cQuery += " AND NOT EXISTS ( "
			cQuery += "SELECT 1 FROM " + RetSqlName("SB1") + " SB1 "

			cQuery += "INNER JOIN " + RetSqlName("SD3") + " SD3 "
			cQuery += "ON SD3.D3_FILIAL = ? "
			cQuery += "AND SD3.D3_COD = SB1.B1_COD "
			If !lCustoUnif .And. !lLocProc
				cQuery += "AND SD3.D3_LOCAL = ? "
			EndIf
			cQuery += "AND SD3.D3_EMISSAO >= ? "
			cQuery += "AND SD3.D3_EMISSAO <= ? "
			If lEstorno
				cQuery += "AND SD3.D3_ESTORNO <> ? "
			EndIf
			If lD3Servi .And. IntDL()
				cQuery += "AND ( "
				cQuery += "(SD3.D3_SERVIC = ?) "
				cQuery += "OR "
				cQuery += "(SD3.D3_SERVIC <> ? AND SD3.D3_TM <= ?) "
				cQuery += "OR "
				cQuery += "(SD3.D3_SERVIC <> ? AND SD3.D3_TM > ? AND SD3.D3_LOCAL = ?) "
				cQuery += ") "
			EndIf
			cQuery += "AND SD3.D_E_L_E_T_ = ? "

			cQuery += "WHERE "
			If !lCusEmp
				cQuery += "SB1.B1_FILIAL = ? AND "
			EndIf
			cQuery += "SB1.B1_GRUPO >= ? "
			cQuery += "AND SB1.B1_GRUPO <= ? "
			cQuery += "AND SB1.B1_COD = SB1EXS.B1_COD "
			cQuery += "AND SB1.B1_TIPO >= ? "
			cQuery += "AND SB1.B1_TIPO <= ? "
			cQuery += "AND SB1.B1_COD <> ? "
			cQuery += "AND SB1.D_E_L_E_T_ = ? "
			cQuery += " ) "

			cQuery += " AND SB1EXS.D_E_L_E_T_= ? "
		EndIf

		cQuery += " ORDER BY " 

		If !lVEIC
			cQuery += " 2,"
		Else
			cQuery += " 27,"
		EndIf

		If nSeqImp == 1
			cQuery += "12"
			If lVEIC
				cQuery += ",29"
			Else
				cQuery += ",28"
			EndIf
		Elseif nSeqImp == 2
			If lCusFil .Or. lCusEmp
				cQuery += "8,12"
			Else
				cQuery += "8"
			EndIf
			If lVEIC
				cQuery += ",29"
			Else
				cQuery += ",28"
			EndIf
		Else
			If !lVEIC
				cQuery += " 9, 12, 28"
			Else
				cQuery += " 9, 12, 29"
			EndIf
		EndIf

		cMD5 := MD5(cQuery)
		If (nPosPrepared := Ascan(aPrepared,{|x| x[2] == cMD5})) == 0
			cQuery := changeQuery(cQuery)
			Aadd(aPrepared,{FwExecStatement():New(cQuery),cMD5})
			nPosPrepared := Len(aPrepared)
		Endif

		//Binding
		nDbPar := 1
		If lCusEmp .And. cDadosProd == 'SBZ'
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SBZ"))
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD1"))
		If !lCustoUnif
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'LF')
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cRemito)
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SF4"))
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If !lCusEmp
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR01"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR02"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If lCusEmp .And. cDadosProd == 'SBZ'
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SBZ"))
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD2"))
		If !lCustoUnif
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'LF')
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cRemito)
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SF4"))
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If !lCusEmp
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR01"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR02"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If lCusEmp .And. cDadosProd == 'SBZ'
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SBZ"))
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD3"))
		If !lCustoUnif .And. !lLocProc
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
		If lEstorno
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
		EndIf
		If lD3Servi .And. IntDL()
			aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, '500')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, '500')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, cMvCQ)
		EndIf
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If !lCusEmp
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
		EndIf
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR01"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR02"][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
		aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
		aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		If nLstSMov == 1
			If !lCusEmp
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR01"][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams["MV_PAR02"][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD1"))
			If !lCustoUnif
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'LF')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cRemito)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SF4"))
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			If !lCusEmp
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD2"))
			If !lCustoUnif
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'LF')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cRemito)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SF4"))
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			If !lCusEmp
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SD3"))
			If !lCustoUnif .And. !lLocProc
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR08'][1])
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataDe)
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cDataAte)
			If lEstorno
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, 'S')
			EndIf
			If lD3Servi .And. IntDL()
				aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
				aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
				aPrepared[nPosPrepared][1]:setString(nDbPar++, '500')
				aPrepared[nPosPrepared][1]:setString(nDbPar++, '   ')
				aPrepared[nPosPrepared][1]:setString(nDbPar++, '500')
				aPrepared[nPosPrepared][1]:setString(nDbPar++, cMvCQ)
			EndIf
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			If !lCusEmp
				aPrepared[nPosPrepared][1]:SetString(nDbPar++, FWxFilial("SB1"))
			EndIf
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR13'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR14'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR03'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, jParams['MV_PAR04'][1])
			aPrepared[nPosPrepared][1]:SetString(nDbPar++, cProdimp)
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
			aPrepared[nPosPrepared][1]:setString(nDbPar++, ' ')
		EndIf

		self:cAlias := aPrepared[nPosPrepared][1]:OpenAlias()
		
		(self:cAlias)->(dbGoTop())

		Do While !(self:cAlias)->(Eof())

			aDadosTran := {}
			lContinua  := .F.
			self:jItens:= JsonObject():New()
			
			If cProduto <> (self:cAlias)->PRODUTO
				cProduto  := (self:cAlias)->PRODUTO
				aSalAtu   := calcSalIni(cProduto,lCusFil,lCusEmp,self:aFils,lCusRep,jParams,dData,nQualCus)
				nQtdSegum := 0
				nSldEnt   := 0
				nSldSai   := 0
				nSldVTot  := 0

				If lLocProc
					cLocalAnt := cLocProc
				Else
					cLocalAnt := (self:cAlias)->ARMAZEM
				EndIf
		
				If Empty(cDescLocal)
					If lCusFil .Or. lCusEmp
						cDescLocal := jParams['MV_PAR08'][1]
					Else
						cDescLocal := Posicione("NNR",1,xFilial("NNR")+jParams['MV_PAR08'][1],"NNR_DESCRI")
					EndIf
				EndIf
				
				If aSalAtu[nMoeda+1] > 0
					nCmInicial := (aSalAtu[nMoeda+1] / aSalAtu[1])
				EndIf

				If aSalAtu[7] <> 0
					nQtdSegum := aSalAtu[7]
				EndIf
			ElseIf (!Empty(cProduto)) .And. !lLocProc
				cLocalAnt:= (self:cAlias)->ARMAZEM
			EndIf

			If Alltrim((self:cAlias)->ARQ) == "SD3"
				//Quando movimento ref apropr. indireta, so considera os
				//movimentos com destino ao almoxarifado de apropriacao indireta
				lInverteMov:=.F.
				If Alltrim((self:cAlias)->ARMAZEM) != cLocalAnt .Or. lCusFil .Or. lCusEmp
					If !(Substr((self:cAlias)->CF,3,1) == "3")
						If !(lCusFil .Or. lCusEmp)
							(self:cAlias)->(DbSkip())
							Loop
						EndIf
					ElseIf lPriApropri
						lInverteMov:=.T.
					EndIf
				EndIf
				
				//Caso seja uma transferencia de localizacao verifica se lista o movimento
				If nLisTrfArm == 2 .And. Substr((self:cAlias)->CF,3,1) == "4"
					If Localiza(cProduto)
						cNumSeqTr  := cProduto+(self:cAlias)->(SEQUENCIA+ARMAZEM)
						aDadosTran := {(self:cAlias)->TES,;
						(self:cAlias)->QUANTIDADE,;
						(self:cAlias)->CUSTO,;
						(self:cAlias)->QUANT2UM,;
						(self:cAlias)->TIPO,;
						(self:cAlias)->DTDIGIT,;
						(self:cAlias)->CF,;
						(self:cAlias)->SEQUENCIA,;
						(self:cAlias)->DOCUMENTO,;
						cProduto,;
						(self:cAlias)->OP,;
						(self:cAlias)->PROJETO,;
						(self:cAlias)->CC,;
						Alltrim((self:cAlias)->ARQ),;
						(self:cAlias)->ARMAZEM}
						(self:cAlias)->(DbSkip())
						If cProduto+(self:cAlias)->(SEQUENCIA+ARMAZEM) == cNumSeqTr
							(self:cAlias)->(DbSkip())
							Loop
						Else
							lContinua := .T.
							self:jItens["D1_DTDIGIT"]:= totvs.framework.treports.date.stringToTimeStamp(aDadosTran[6])
							self:jItens["D1_TES"] := aDadosTran[1]
							self:jItens["D1_LOCAL"] := aDadosTran[15]
							self:jItens["D1_CF"] := Iif( lInverteMov , Substr(aDadosTran[7],1,3)+"*" , aDadosTran[7]) 
							If jParams['MV_PAR09'][1] $ "Ss"
								self:jItens["D1_DOC"] := aDadosTran[8]
							Else
								self:jItens["D1_DOC"] := aDadosTran[9]
							Endif
							If aDadosTran[1] <= "500"
								gravaMov(aDadosTran[2],aDadosTran[3],.F.,.T.,aDadosTran[4],lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
							Else
								gravaMov(aDadosTran[2],aDadosTran[3],.T.,.T.,aDadosTran[4],lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf
			
			If !lContinua
				self:jItens["D1_DTDIGIT"]:= totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->DTDIGIT)
				self:jItens["D1_TES"] 	:= (self:cAlias)->TES
				self:jItens["D1_LOCAL"]	:= (self:cAlias)->ARMAZEM
				If lInverteMov
					self:jItens["D1_CF"] := Substr((self:cAlias)->CF,1,3)+"*"
				Else
					self:jItens["D1_CF"] := (self:cAlias)->CF
				EndIf
				If jParams['MV_PAR09'][1] $ "Ss"
					self:jItens["D1_NUMSEQ"] := (self:cAlias)->SEQUENCIA
				Else
					self:jItens["D1_NUMSEQ"] := (self:cAlias)->DOCUMENTO
				Endif
			EndIf

			Do Case
				Case Alltrim((self:cAlias)->ARQ) == "SD1" .And. !lContinua
					lDev:= lListaDev .And. (self:cAlias)->TIPONF == "D"
					If (self:cAlias)->TES <= "500" .And. !lDev
						gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,.F.,(self:cAlias)->TIPONF != "C",(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
					Else
						If lDev
							gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,.T.,(self:cAlias)->TIPONF != "C",(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
						EndIf
					EndIf
				Case Alltrim((self:cAlias)->ARQ) = "SD2" .And. !lContinua
					lDev:= lListaDev .And. (self:cAlias)->TIPONF == "D"
					If (self:cAlias)->TES <= "500" .Or. lDev
						If lDev
							gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,.F.,(self:cAlias)->TIPONF != "C",(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
						EndIf
					Else
						gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,.T.,(self:cAlias)->TIPONF != "C",(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
					EndIf

				Case Alltrim((self:cAlias)->ARQ) == "SD3" .And. !lContinua
					lDev := .F.
					If	lInverteMov
						If (self:cAlias)->TES > "500"
							lMovSaida := .F.
						Else
							lMovSaida := .T.
						EndIf
						gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,lMovSaida,.T.,(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)
					Else
						If (self:cAlias)->TES <= "500"
							lMovSaida := .F.
						Else
							lMovSaida := .T.
						EndIf
						gravaMov((self:cAlias)->QUANTIDADE,(self:cAlias)->CUSTO,lMovSaida,.T.,(self:cAlias)->QUANT2UM,lDev,@nSldEnt,@nSldSai,@nSldVTot,@self)

						If lCusFil .Or. lCusEmp
							lPriApropri:=.T.
						EndIf
					EndIf
			EndCase

			Do Case
				Case Alltrim((self:cAlias)->ARQ) == "SD3" .And. !lContinua
					If Empty((self:cAlias)->OP) .And. Empty((self:cAlias)->PROJETO)
						self:jItens["CCPVPJOP"] := 'CC'+(self:cAlias)->CC
					ElseIf !Empty((self:cAlias)->PROJETO)
						self:jItens["CCPVPJOP"] := 'PJ'+(self:cAlias)->PROJETO
					ElseIf !Empty((self:cAlias)->OP)
						cIdent := fIdentOS( (self:cAlias)->OP )
						self:jItens["CCPVPJOP"] := cIdent
					EndIf
							
				Case Alltrim((self:cAlias)->ARQ) == "SD1" .And. !lContinua
					cTipoNf := 'F-'
					SD1->(dbGoTo((self:cAlias)->NRECNO))
					SD2->(dbSetOrder(3))
					If SD2->(dbSeek(xFilial("SD2")+SD1->D1_NFORI+SD1->D1_SERIORI+SD1->D1_FORNECE+SD1->D1_LOJA))
						If SD2->D2_TIPO <> 'B'
							cTipoNf := 'C-'
						EndIf
					EndIf
					self:jItens["CCPVPJOP"] := cTipoNf+(self:cAlias)->FORNECEDOR
				Case Alltrim((self:cAlias)->ARQ) == "SD2" .And. !lContinua
					If ((self:cAlias)->TIPONF) $ "B|D"
						self:jItens["CCPVPJOP"] := 'F-'+(self:cAlias)->FORNECEDOR
					Else
						self:jItens["CCPVPJOP"] := 'C-'+(self:cAlias)->FORNECEDOR
					EndIf
				Case lContinua .And. aDadosTran[14] == "SD3"
					If Empty(aDadosTran[11]) .And. Empty(aDadosTran[12])
						self:jItens["CCPVPJOP"] := 'CC'+aDadosTran[13]
					ElseIf !Empty(aDadosTran[12])
						self:jItens["CCPVPJOP"] := 'PJ'+aDadosTran[12]
					ElseIf !Empty(aDadosTran[11])
						cIdent := fIdentOS( aDadosTran[11] )
						self:jItens["CCPVPJOP"] := cIdent
					EndIf
			EndCase

			//Realiza a gravação dos campos da SB1
			For nX := 1 To Len(self:aFieldsSB1)
				self:jItens[self:aFieldsSB1[nX]] := (self:cAlias)->&(aAliasSB1[nX])
			Next nX

			//Realiza a gravação dos campos da SD1 e SD2
			If (self:cAlias)->ARQ $ "SD1|SD2"
				If (self:cAlias)->ARQ == "SD1"
					nY := 1
				Else
					nY := 2
				EndIf

				For nX := 1 To Len(aTableDoc[nY])
					If Empty(self:jItens[aTableDoc[nY][nX]])
						If aFieldsDoc[nX] <> "DTDIGIT"
							self:jItens[aTableDoc[nY][nX]] := (self:cAlias)->&(aFieldsDoc[nX])
						Else
							self:jItens[aTableDoc[nY][nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(aFieldsDoc[nX])))
						EndIf
					EndIf
				Next nX
			EndIf

			For nX := 1 To Len(self:aFieldsSD3)
				If Empty(self:jItens[self:aFieldsSD3[nX]])
					If aAliasSD3[nX] <> "DTDIGIT"
						self:jItens[self:aFieldsSD3[nX]] := (self:cAlias)->&(aAliasSD3[nX])
					Else
						self:jItens[self:aFieldsSD3[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(aAliasSD3[nX])))
					EndIf
				EndIf
			Next nX

			self:jItens["EMPNOME"]		:= cEmpExec
			self:jItens["FILIAL"]		:= cFilAnt
			self:jItens["FILNOME"]		:= cFilExec
			self:jItens["QTSEGUMINI"]	:= nQtdSegum
			self:jItens["QTDINICIAL"]	:= aSalAtu[1]
			self:jItens["CUSTOINICIAL"]	:= aSalAtu[nMoeda+1]
			self:jItens["CMINICIAL"]	:= nCmInicial
			self:jItens["NNRDESCRI"]	:= cDescLocal

			self:processData()
			self:appendData(self:jItens)

			If lInverteMov .And. lPriApropri
				If lCusFil .Or. lCusEmp
					lPriApropri:= .F.
					If !lLocProc
						Loop
					EndIf
				EndIf
			EndIf
			
			(self:cAlias)->( dbSkip() )
		EndDo

		(self:cAlias)->( DBCloseArea() )
	Next nFil
 
	cFilAnt := cBkpFil 

	For nX := 1 To Len(aPrepared)
		aPrepared[nX][1]:Destroy()
	Next
	FWFreeArray(aPrepared)

	If oQuerySB2_1 <> Nil
		oQuerySB2_1:Destroy()
	EndIf
	If oQuerySB2_2 <> Nil
		oQuerySB2_2:Destroy()
	EndIf

return self:oData

/*/{Protheus.doc} oNGetSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Junho 01,2023
/*/
method oNGetSchema() class KardexTReportsBusinessObject

	self:aFieldsSB1 := {"B1_COD","B1_DESC","B1_CODITE","B1_TIPO","B1_UM","B1_GRUPO","B1_POSIPI"}
	self:aFieldsSD1 := {"D1_COD","D1_LOCAL","D1_SEQCALC","D1_DTDIGIT","D1_TES","D1_CF","D1_NUMSEQ","D1_DOC","D1_SERIE","D1_QUANT","D1_QTSEGUM","D1_FORNECE","D1_LOJA","D1_TIPO","D1_LOTECTL","D1_CUSTO"}
	self:aFieldsSD2 := {"D2_COD","D2_LOCAL","D2_SEQCALC","D2_EMISSAO","D2_TES","D2_CF","D2_NUMSEQ","D2_DOC","D2_SERIE","D2_QUANT","D2_QTSEGUM","D2_CLIENTE","D2_LOJA","D2_TIPO","D2_LOTECTL","D2_CUSTO1"}		
	self:aFieldsSD3 := {"D3_COD","D3_LOCAL","D3_SEQCALC","D3_EMISSAO","D3_TM","D3_CF","D3_NUMSEQ","D3_DOC","D3_QUANT","D3_QTSEGUM","D3_PROJPMS","D3_OP","D3_TRT","D3_TIPO","D3_LOTECTL","D3_CUSTO1"}
    
	self:AliasToSchema("SB1",self:aFieldsSB1)
	self:AliasToSchema("SD1",self:aFieldsSD1)
    self:AliasToSchema("SD2",self:aFieldsSD2)
    self:AliasToSchema("SD3",self:aFieldsSD3)

    self:addProperty("EMPNOME"     , STR0004, "string", STR0004, "EMPNOME")
	self:addProperty("FILIAL"      , STR0024, "string", STR0024, "FILIAL")
    self:addProperty("FILNOME"     , STR0005, "string", STR0005, "FILNOME")
	self:addProperty("NNRDESCRI"   , STR0006, "string", STR0006, "NNRDESCRI")
	self:addProperty("CCPVPJOP"    , STR0007, "string", STR0007, "CCPVPJOP")
	self:addProperty("TPMOVIMENTO" , STR0008, "string", STR0008, "TPMOVIMENTO")
	self:addProperty("QTDINICIAL"  , STR0009, "number", STR0009, "QTDINICIAL")
	self:addProperty("QTSEGUMINI"  , STR0018, "number", STR0018, "QTSEGUMINI")
	self:addProperty("CUSTOINICIAL", STR0011, "number", STR0011, "CUSTOINICIAL")
	self:addProperty("CMINICIAL"   , STR0012, "number", STR0012, "CMINICIAL")
	self:addProperty("ENTQTD"      , STR0013, "number", STR0013, "ENTQTD")
	self:addProperty("NSLDENT"     , STR0019, "number", STR0019, "NSLDENT")
	self:addProperty("NSLDSAI"     , STR0020, "number", STR0020, "NSLDSAI")
	self:addProperty("NSLDVTOT"    , STR0021, "number", STR0021, "NSLDVTOT")
	self:addProperty("ENTCUS"      , STR0014, "number", STR0014, "ENTCUS")
	self:addProperty("CUSMOV"      , STR0015, "number", STR0015, "CUSMOV")
	self:addProperty("SAIQTD"      , STR0016, "number", STR0016, "SAIQTD")
	self:addProperty("SAICUS"      , STR0017, "number", STR0017, "SAICUS")

return

/*/{Protheus.doc} fIdentOS
Retorna descrição referente a OP ou Ordem de serviço
@type  Função
@author Squad Entradas
@since  Junho 01,2023
/*/
Static Function fIdentOS( cOp )

	local aArea := {} as array
	local cRet  := 'OP' + cOp as character

	If SubStr(cOP,7,2) == "OS"
		aArea := GetArea()

		dbSelectArea('STJ')
		dbSetOrder(1)
		If dbSeek( xFilial('STJ') + SubStr(cOp,1,6) )
			cRet := MNTDESCOS( STJ->TJ_ORDEM, STJ->TJ_CCUSTO )
		EndIf
			
		RestArea( aArea )
	EndIf

Return cRet

/*/{Protheus.doc} calcSalIni
Função que calcula o saldo inicial do produto no período
@type  Função
@author Squad Entradas
@since  Junho 01,2023
/*/
Static Function calcSalIni(cProduto as character, lCusFil as logical, lCusEmp as logical, aFils as array, lCusRep as logical, jParams as json, dData as date, nQualCus as numeric)

	local cAlias as character
	Local cQuery as character
	local cFil as character
	Local aSalAlmox as array
	local aSalAtu as array
	Local lSB2Mode as logical
	Local i as numeric

	If lCusFil

		cAlias := GetNextAlias()

		If oQuerySB2_1 == Nil
			cQuery := "SELECT B2_FILIAL FILIAL, B2_COD PRODUTO, B2_LOCAL ARMAZEM "
			cQuery += "FROM " + RetSqlName("SB2") + " SB2 "
			cQuery += "WHERE SB2.B2_FILIAL = ? AND "
			cQuery += "SB2.B2_COD = ? AND "
			cQuery += "SB2.D_E_L_E_T_ = ? "
			cQuery += "ORDER BY B2_FILIAL, B2_COD "

			oQuerySB2_1 := FwExecStatement():New(cQuery)
		EndIf

		oQuerySB2_1:SetString(1, FWxFilial('SB2'))
		oQuerySB2_1:SetString(2, cProduto)
		oQuerySB2_1:SetString(3, ' ')

		oQuerySB2_1:OpenAlias(cAlias)

		aSalAtu := { 0,0,0,0,0,0,0 }

		Do While (cAlias)->(!Eof())
			aSalAlmox := CalcEst(cProduto,(cAlias)->ARMAZEM,dData,,, ( lCusRep .And. nQualCus == 2 ) )
			For i:=1 to Len(aSalAtu)
				aSalAtu[i] += aSalAlmox[i]
			Next i
			(cAlias)->(dbSkip())
		EndDo

		(cAlias)->(DbCloseArea())

	ElseIf lCusEmp
		
		cAlias := GetNextAlias()
		
		If oQuerySB2_2 == Nil
			cQuery := "SELECT B2_FILIAL FILIAL, B2_COD PRODUTO, B2_LOCAL ARMAZEM "
			cQuery += "FROM " + RetSqlName("SB2") + " SB2 "
			cQuery += "WHERE SB2.B2_COD = ? AND "
			cQuery += "SB2.D_E_L_E_T_ = ? "
			cQuery += "ORDER BY B2_COD "

			oQuerySB2_2 := FwExecStatement():New(cQuery)
		EndIf

		oQuerySB2_2:SetString(1, cProduto)
		oQuerySB2_2:SetString(2, ' ')

		oQuerySB2_2:OpenAlias(cAlias)

		aSalAtu := { 0,0,0,0,0,0,0 }

		Do While (cAlias)->(!Eof())
			If !Empty(xFilial("SB2"))
				cFil := (cAlias)->FILIAL
			Else
				lSB2Mode := .T.
			EndIf
			If (!lSB2Mode .And. aScan(aFils, {|x| AllTrim(cFil) $  AllTrim(x) }) > 0) .Or. lSB2Mode
				aSalAlmox := CalcEst(cProduto,(cAlias)->ARMAZEM,dData,,,( lCusRep .And. nQualCus == 2 ) )
				For i:=1 to Len(aSalAtu)
					aSalAtu[i] += aSalAlmox[i]
				Next i
			EndIf
			(cAlias)->(dbSkip())
		EndDo

		(cAlias)->(DbCloseArea())
	Else
		aSalAtu := CalcEst(cProduto,jParams['MV_PAR08'][1],dData,,, ( lCusRep .And. nQualCus == 2 ) )
	EndIf

	// Calcula o Custo de Reposicao do Produto
	If lCusRep .And. nQualCus == 2
		aSalAtu := {aSalAtu[1],aSalAtu[18],aSalAtu[19],aSalAtu[20],aSalAtu[21],aSalAtu[22],aSalAtu[07]}
	EndIf

Return aSalAtu

/*/{Protheus.doc} gravaMov
Função que calcula o saldo inicial do produto no período
@type  Função
@author Squad Entradas
@since  Junho 01,2023
/*/
Static Function gravaMov(nQuant as numeric, nCusto as numeric, lMovSaida as logical, lGravaCM as logical,nQuant2M as numeric, lDev as logical, nSldEnt as numeric,nSldSai as numeric,nSldVTot as numeric, self as object)

	If lMovSaida
		If lDev
			self:jItens["SAIQTD"] := nQuant * -1
			self:jItens["SAICUS"] := nCusto * -1
			self:jItens["D1_QTSEGUM"] := nQuant2M
			nSldSai  += nQuant * -1
			nSldVTot += nCusto
		Else
			self:jItens["SAIQTD"] := nQuant
			self:jItens["SAICUS"] := nCusto
			self:jItens["D1_QTSEGUM"] := nQuant2M * -1
			nSldSai  += nQuant
			nSldVTot -= nCusto
		EndIf
		self:jItens["ENTQTD"] := 0
		self:jItens["ENTCUS"] := 0
		self:jItens["TPMOVIMENTO"] := "S"

		self:jItens["NSLDENT"] := nSldEnt
		self:jItens["NSLDSAI"] := nSldSai
		self:jItens["NSLDVTOT"]:= nSldVTot
	Else
		If lDev
			self:jItens["ENTQTD"] := nQuant * -1
			self:jItens["ENTCUS"] := nCusto * -1
			self:jItens["D1_QTSEGUM"] := nQuant2M * -1
			nSldEnt  += nQuant * -1
			nSldVTot -= nCusto
		Else
			self:jItens["ENTQTD"] := nQuant
			self:jItens["ENTCUS"] := nCusto
			self:jItens["D1_QTSEGUM"] := nQuant2M
			nSldEnt  += nQuant
			nSldVTot += nCusto 
		EndIf
		self:jItens["SAIQTD"] := 0
		self:jItens["SAICUS"] := 0
		self:jItens["TPMOVIMENTO"] := "E"

		self:jItens["NSLDSAI"] := nSldSai
		self:jItens["NSLDENT"] := nSldEnt
		self:jItens["NSLDVTOT"]:= nSldVTot
	EndIf

	If lGravaCM
		If nQuant > 0
			self:jItens["CUSMOV"] := nCusto / nQuant
		Else
			self:jItens["CUSMOV"] := 0
		EndIf
	EndIf

Return

/*/{Protheus.doc} iniVarNum
Função responsável por inicializar os perguntes numéricos
não preenchidos pelo usuário na visão de dados/tabela dinâmica
@type  Função
@author Squad Entradas
@since  Setembro 29,2023
/*/
Static Function iniVarNum(nVar)

	If ValType(nVar) == "C" .Or. nVar == 0
		nVar := 1
	EndIf

Return nVar
