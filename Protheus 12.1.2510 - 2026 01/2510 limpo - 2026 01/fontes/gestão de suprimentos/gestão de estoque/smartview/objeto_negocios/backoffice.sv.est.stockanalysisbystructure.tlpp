#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.stockanalysisbystructure.ch"
#include "totvs.framework.treports.integratedprovider.th"
#DEFINE SX1GRUPO 'ESTT036'

namespace totvs.protheus.backoffice.est.stockanalysisbystructure.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SB2,SB3,SC1,SC2,SC6,SC7,SG1",name="Análise de Estoques Por Estrutura", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} StockAnalysisByStructureSmartViewBusinessObject
Classe para criação do Objeto de Negócio Análise de Estoques Por Estrutura
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class StockAnalysisByStructureSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data jItems	as json
	Protected data cAlias	as character
	Protected data cWhere	as character
	Protected data lExistPergunte	as logical

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
*/
//-------------------------------------------------------------------   
Method new() Class StockAnalysisByStructureSmartViewBusinessObject

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 ) //Estoque/Custos

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //Análise de Estoques Por Estrutura

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //Lista os estoques a partir de um produto acabado e os insumos necessarios para a produção dele

	//Indica o pergunte que será utilizado no relatório
    self:lExistPergunte := self:setPergunte( SX1GRUPO )//ESTT036
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0004,i18n(STR0005,{SX1GRUPO}))//O Grupo de perguntas #1 não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0005,{SX1GRUPO}), , , )//O Grupo de perguntas #1 não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.) 

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
 
@author BackOffice
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class StockAnalysisByStructureSmartViewBusinessObject
	
	local oQuery	as Object
	Local jParams	as Json
	Local nNivel	as Numeric	
	Local nPulo		as Numeric
	Local nFil		as numeric
	Local cAlias	as Character
	Local cNivel    as character	
	Local cFilho    as character
	Local cNeto     as character
	Local cProdutos as character	
	Local cQuery    as character
	Local cFilExec	as character
	Local cEmpExec	as character
	Local cBkpFil	as character

	Private __oQrySB1	as Object
	Private __oQrySG1	as Object

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

	//Variável com o nome do Grupo de Empresa
	cEmpExec := AllTrim(FWEmpName(cEmpAnt)) 

	////metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters() 

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cQuery := " SELECT G1_COD,G1_COMP, "
	cQuery += " (SELECT SUM(B2_QATU) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = G1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_= ?) B2_QATU, "
	cQuery += " (SELECT B1_DESC FROM " + RetSQLName("SB1") + " SB1 WHERE B1_FILIAL = G1_FILIAL AND B1_COD = G1_COD AND SB1.D_E_L_E_T_ = ?) B1_DESC, "
	cQuery += " (SELECT B1_FANTASM FROM " + RetSQLName("SB1") + " SB1 WHERE B1_FILIAL = G1_FILIAL AND B1_COD = G1_COD AND SB1.D_E_L_E_T_ = ?) B1_FANTASM "
	cQuery += " FROM " + RetSQLName("SG1") + " SG1 "
	cQuery += " WHERE SG1.G1_FILIAL = ? "
	cQuery += " AND SG1.G1_COD >= ? "
	cQuery += " AND SG1.G1_COD <= ? "	
	//Os filtros serão setados na interface do novo TReports
	cQuery += self:cWhereFiltroSV
	cQuery += self:cWhere
	cQuery += " AND SG1.D_E_L_E_T_ = ? "
	cQuery += " GROUP BY G1_COD,G1_COMP,G1_FILIAL "
	cQuery += " ORDER BY G1_COD "
	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Inicia Variaveis
		nNivel  := 0
		nPulo   := 1

		//Binding	
		oQuery:setString(1,FWxFilial( "SB2" ))
		oQuery:setString(2,jParams['MV_PAR03'][1])
		oQuery:setString(3,jParams['MV_PAR04'][1])
		oQuery:setString(4,' ')
		oQuery:setString(5,' ')
		oQuery:setString(6,' ')
		oQuery:setString(7,FWxFilial( "SG1" ))
		oQuery:setString(8,jParams['MV_PAR01'][1])
		oQuery:setString(9,jParams['MV_PAR02'][1])	
		oQuery:setString(10,' ')

		self:cAlias := oQuery:OpenAlias()	
		
		(self:cAlias)->(dbGoTop())
		While !(self:cAlias)->(Eof())
			if !((self:cAlias)->B1_FANTASM == "S" .and. jParams['MV_PAR06'][1]==2)	.or. !((self:cAlias)->B1_FANTASM == "S" .and. jParams['MV_PAR05'][1]==2)			
				cAlias := getProd((self:cAlias)->G1_COMP,jParams)					
				While !(cAlias)->(Eof()) 
					if !((cAlias)->B1_FANTASM == "S" .and. jParams['MV_PAR05'][1] == 2)
						if cFilho <> (cAlias)->B1_COD .and. !((cAlias)->B1_COD $ (cProdutos)) 							
							self:jItems	:= JsonObject():New()
							self:jItems["EMPNOME"] := cEmpExec
							self:jItems["FILIAL"] := cFilAnt
							self:jItems["FILNOME"] := cFilExec
							self:jItems["COD_PAI"] := (self:cAlias)->G1_COD
							self:jItems["SLD_PAI"] := (self:cAlias)->B2_QATU
							self:jItems["DSC_PAI"] := (self:cAlias)->B1_DESC	
							self:jItems["B1_COD"] := (cAlias)->B1_COD
							self:jItems["B1_DESC"] := (cAlias)->B1_DESC	
							self:jItems["B1_TIPO"] := (cAlias)->B1_TIPO
							self:jItems["B1_GRUPO"] := (cAlias)->B1_GRUPO
							self:jItems["B1_UM"] := (cAlias)->B1_UM
							self:jItems["B1_EMIN"] := (cAlias)->B1_EMIN
							self:jItems["B1_LE"] := (cAlias)->B1_LE
							self:jItems["B1_PE"] := (cAlias)->B1_PE
							self:jItems["B1_TIPE"] := (cAlias)->B1_TIPE							
							self:jItems["B1_FANTASM"] := (cAlias)->B1_FANTASM
							self:jItems["B2_USAI"] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->B2_USAI))							
							self:jItems["B2_QATU"] := (cAlias)->B2_QATU 	
							self:jItems["B2_QEMP"] := (cAlias)->B2_QEMP	
							self:jItems["B3_MEDIA"] := (cAlias)->B3_MEDIA	
							self:jItems["B3_CLASSE"] := (cAlias)->B3_CLASSE
							self:jItems["QTD_C1"] := (cAlias)->C1_QUANT	
							self:jItems["QTD_C2"] := (cAlias)->C2_QUANT
							self:jItems["QTD_C6"] := (cAlias)->C6_QTDVEN
							self:jItems["QTD_C7"] := (cAlias)->C7_QUANT
							self:processData()	
							self:oData:appendData(self:jItems)							
							cProdutos += (cAlias)->B1_COD + "|"
						EndIf
					EndIf
					cNivel := getNivel((cAlias)->B1_COD,nNivel)
					cFilho := (cAlias)->B1_COD			
					(cAlias)->(DBSkip())					
					//Volta ao nivel anterior
					if (!empty(cNeto) .and. (cNivel)->(Eof()))
						(cNivel)->(DBCloseArea())
						cNivel := getNivel(cNeto,nNivel-1)
						(cNivel)->(DBSkip(nPulo))
						++nPulo
						--nNivel
						if (cNivel)->(Eof()) .and. nNivel > 0																					
							("NIVEL"+cValtoChar(nNivel-1))->(DbSkip())													 
							cAlias := getProd(("NIVEL"+cValtoChar(nNivel-1))->G1_COMP,jParams)
						EndIf					
					EndIf

					if !(cNivel)->(Eof())
						cAlias := getProd((cNivel)->G1_COMP,jParams)
						cNeto := (cNivel)->G1_COD
						++nNivel					
					else						
						(cNivel)->(DBCloseArea())
						nPulo := 1																		
					EndIf					
				ENDDO
				(cAlias)->(DBCloseArea())
				//Limpa todas as variaveis
				nPulo := 1
				nNivel := 0
				cProdutos := ""	
				cFilho := ""					
			EndIf
			(Self:cAlias)->(DBSkip())
		EndDo 
		(Self:cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()
	If __oQrySB1 <> Nil
		__oQrySB1:Destroy()
	EndIf
	If __oQrySG1 <> Nil
		__oQrySG1:Destroy()
	EndIf

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class StockAnalysisByStructureSmartViewBusinessObject

	Local Sb1Fld    as Array
	Local Sb2Fld    as Array
	Local Sb3Fld    as Array	

	Sb1Fld	:= {"B1_COD","B1_DESC","B1_TIPO","B1_GRUPO","B1_UM","B1_EMIN","B1_LE","B1_PE","B1_TIPE","B1_FANTASM"}
	Sb2Fld	:= {"B2_QATU","B2_QEMP","B2_USAI"}
	Sb3Fld	:= {"B3_MEDIA","B3_CLASSE"}

	self:AliasToSchema("SB1", Sb1Fld)
	self:AliasToSchema("SB2", Sb2Fld)
	self:AliasToSchema("SB3", Sb3Fld)		

	self:addProperty( "EMPNOME"	, STR0013 , "string", STR0013 , "EMPNOME" ) //"Nome Empresa"
	self:addProperty( "FILIAL"  , STR0014 , "string", STR0014 , "FILIAL"  ) //"Filial"
	self:addProperty( "FILNOME"	, STR0015 , "string", STR0015 , "FILNOME" ) //"Nome Filial"

	self:addProperty("COD_PAI", STR0006, "string", STR0006,"COD_PAI")//Produto Pai
	self:addProperty("SLD_PAI", STR0007, "number", STR0007,"SLD_PAI")//Saldo Pai
	self:addProperty("DSC_PAI", STR0008, "string", STR0008,"DSC_PAI")//Descrição Pai
	self:addProperty("QTD_C1", STR0009, "number", STR0009,"QTD_C1")//"Em SC
	self:addProperty("QTD_C2", STR0010, "number", STR0010,"QTD_C2")//"Em OP
	self:addProperty("QTD_C6", STR0011, "number", STR0011,"QTD_C6")//"Em PV
	self:addProperty("QTD_C7", STR0012, "number", STR0012,"QTD_C7")//"Em PC

Return


//-------------------------------------------------------------------
/*{Protheus.doc} getProd
Retorna os dados do produtos
@author BackOffice
*/
//-------------------------------------------------------------------   
Static Function  getProd(cProd as Character,jParams as Json) 	
	Local cAlias    as Character
	Local cQuery    as Character
	Local nBind		as Numeric

	If __oQrySB1 == Nil
		cQuery += " SELECT B1_COD,B1_DESC,B1_TIPO,B1_GRUPO,B1_UM,B1_EMIN, "
		cQuery += " (SELECT SUM(B2_QATU) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = B1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_= ?) B2_QATU, "

		if jParams['MV_PAR07'][1] == 1
			cQuery += " (SELECT SUM(B2_QEMP)+SUM(B2_RESERVA) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = B1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_= ?) B2_QEMP, "
			cQuery += " (SELECT SUM(C1_QUANT)-SUM(C1_QUJE)  FROM " + RetSQLName("SC1") + " SC1 WHERE C1_FILIAL = ? AND C1_PRODUTO = B1_COD AND C1_RESIDUO = ? AND C1_PEDIDO = ? AND C1_TPOP IN (?) AND C1_LOCAL >= ? AND C1_LOCAL <= ? AND SC1.D_E_L_E_T_ = ?) C1_QUANT, "
			cQuery += " (SELECT SUM(C2_QUANT)-SUM(C2_QUJE) FROM " + RetSQLName("SC2") + " SC2 WHERE C2_FILIAL = ? AND C2_PRODUTO = B1_COD AND C2_TPOP IN (?) AND C2_LOCAL >= ? AND C2_LOCAL <= ? AND SC2.D_E_L_E_T_ = ?) C2_QUANT, "
			cQuery += " (SELECT SUM(C6_QTDVEN)-(SUM(C6_QTDEMP)+SUM(C6_QTDENT)) FROM " + RetSQLName("SC6") + " SC6 WHERE C6_FILIAL = ? AND C6_PRODUTO = B1_COD AND C6_TPOP IN (?) AND C6_LOCAL >= ? AND C6_LOCAL <= ? AND SC6.D_E_L_E_T_ = ?) C6_QTDVEN, "
			cQuery += " (SELECT SUM(C7_QUANT)-SUM(C7_QUJE)  FROM " + RetSQLName("SC7") + " SC7 WHERE C7_FILIAL = ? AND C7_PRODUTO = B1_COD AND C7_RESIDUO = ? AND C7_TPOP IN (?) AND C7_LOCAL >= ? AND C7_LOCAL <= ? AND SC7.D_E_L_E_T_ = ?) C7_QUANT, "

		ElseIf jParams['MV_PAR07'][1] == 2
			cQuery += " (SELECT SUM(B2_QEMPPRE)+SUM(B2_RESERVA) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = B1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_= ?) B2_QEMP, "
			cQuery += " (SELECT SUM(C1_QUANT)-SUM(C1_QUJE)  FROM " + RetSQLName("SC1") + " SC1 WHERE C1_FILIAL = ? AND C1_PRODUTO = B1_COD AND C1_RESIDUO = ? AND C1_PEDIDO = ? AND C1_TPOP = ? AND C1_LOCAL >= ? AND C1_LOCAL <= ? AND SC1.D_E_L_E_T_ = ?) C1_QUANT, "
			cQuery += " (SELECT SUM(C2_QUANT)-SUM(C2_QUJE)  FROM " + RetSQLName("SC2") + " SC2 WHERE C2_FILIAL = ? AND C2_PRODUTO = B1_COD AND C2_TPOP = ? AND C2_LOCAL >= ? AND C2_LOCAL <= ? AND SC2.D_E_L_E_T_ = ?) C2_QUANT, "
			cQuery += " (SELECT SUM(C6_QTDVEN)-(SUM(C6_QTDEMP)+SUM(C6_QTDENT)) FROM " + RetSQLName("SC6") + " SC6 WHERE C6_FILIAL = ? AND C6_PRODUTO = B1_COD AND C6_TPOP = ? AND C6_LOCAL >= ? AND C6_LOCAL <= ? AND SC6.D_E_L_E_T_ = ?) C6_QTDVEN, "
			cQuery += " (SELECT SUM(C7_QUANT)-SUM(C7_QUJE)  FROM " + RetSQLName("SC7") + " SC7 WHERE C7_FILIAL = ? AND C7_PRODUTO = B1_COD  AND C7_RESIDUO = ? AND C7_TPOP = ? AND C7_LOCAL >= ? AND C7_LOCAL <= ? AND SC7.D_E_L_E_T_ = ?) C7_QUANT, "
		Else
			cQuery += " (SELECT SUM(B2_QEMP)+SUM(B2_RESERVA)+SUM(B2_QEMPPRE) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = B1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_= ?) B2_QEMP, "
			cQuery += " (SELECT SUM(C1_QUANT)-SUM(C1_QUJE)  FROM " + RetSQLName("SC1") + " SC1 WHERE C1_FILIAL = ? AND C1_PRODUTO = B1_COD AND C1_RESIDUO = ? AND C1_PEDIDO = ? AND C1_LOCAL >= ? AND C1_LOCAL <= ? AND SC1.D_E_L_E_T_ = ?) C1_QUANT, "
			cQuery += " (SELECT SUM(C2_QUANT)-SUM(C2_QUJE)  FROM " + RetSQLName("SC2") + " SC2 WHERE C2_FILIAL = ? AND C2_PRODUTO = B1_COD AND C2_LOCAL >= ? AND C2_LOCAL <= ? AND SC2.D_E_L_E_T_ = ?) C2_QUANT, "
			cQuery += " (SELECT SUM(C6_QTDVEN)-(SUM(C6_QTDEMP)+SUM(C6_QTDENT)) FROM " + RetSQLName("SC6") + " SC6 WHERE C6_FILIAL = ? AND C6_PRODUTO = B1_COD AND C6_LOCAL >= ? AND C6_LOCAL <= ? AND SC6.D_E_L_E_T_ = ?) C6_QTDVEN, "
			cQuery += " (SELECT SUM(C7_QUANT)-SUM(C7_QUJE)  FROM " + RetSQLName("SC7") + " SC7 WHERE C7_FILIAL = ? AND C7_PRODUTO = B1_COD AND C7_RESIDUO = ? AND C7_LOCAL >= ? AND C7_LOCAL <= ? AND SC7.D_E_L_E_T_ = ?) C7_QUANT, "
		EndIf
		cQuery += " (SELECT MAX(B2_USAI) FROM " + RetSQLName("SB2") + " SB2 WHERE B2_FILIAL = ? AND B2_COD = B1_COD AND B2_LOCAL >= ? AND B2_LOCAL <= ? AND SB2.D_E_L_E_T_ = ?) B2_USAI, "
		cQuery += " B1_LE,B1_PE,B1_TIPE,B3_MEDIA,B3_CLASSE,B1_FANTASM "
		cQuery += " FROM " + RetSQLName("SB1") + " SB1 "	
		cQuery += " LEFT JOIN " + RetSQLName("SB3") + " SB3 ON B3_FILIAL = ? AND B3_COD = B1_COD AND SB3.D_E_L_E_T_ = ? "	
		cQuery += " WHERE SB1.B1_FILIAL = ? AND SB1.B1_COD = ? AND SB1.D_E_L_E_T_ = ? "

		cQuery := changeQuery(cQuery)
		__oQrySB1 := FwExecStatement():New(cQuery)
	EndIf

	//Binding
	nBind := 1
	__oQrySB1:setString(nBind++,FWxFilial( "SB2" ))
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	//If SB2
	__oQrySB1:setString(nBind++,FWxFilial( "SB2" ))
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	//SC1
	__oQrySB1:setString(nBind++,FWxFilial( "SC1" ))
	__oQrySB1:setString(nBind++,' ')
	__oQrySB1:setString(nBind++,' ')
	if jParams['MV_PAR07'][1] == 1
		__oQrySB1:setIn(nBind++,{' ','F'})
	ElseIf jParams['MV_PAR07'][1] == 2
		__oQrySB1:setString(nBind++,'P')
	EndIf
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	//SC2
	__oQrySB1:setString(nBind++,FWxFilial( "SC2" ))
	if jParams['MV_PAR07'][1] == 1
		__oQrySB1:setIn(nBind++,{' ','F'})
	ElseIf jParams['MV_PAR07'][1] == 2
		__oQrySB1:setString(nBind++,'P')
	EndIf
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	//SC6
	__oQrySB1:setString(nBind++,FWxFilial( "SC6" ))
	if jParams['MV_PAR07'][1] == 1
		__oQrySB1:setIn(nBind++,{' ','F'})
	ElseIf jParams['MV_PAR07'][1] == 2
		__oQrySB1:setString(nBind++,'P')
	EndIf
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	//SC7
	__oQrySB1:setString(nBind++,FWxFilial( "SC7" ))
	__oQrySB1:setString(nBind++,' ')
	if jParams['MV_PAR07'][1] == 1
		__oQrySB1:setIn(nBind++,{' ','F'})
	ElseIf jParams['MV_PAR07'][1] == 2
		__oQrySB1:setString(nBind++,'P')
	EndIf
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')

	__oQrySB1:setString(nBind++,FWxFilial( "SB2" ))
	__oQrySB1:setString(nBind++,jParams['MV_PAR03'][1])
	__oQrySB1:setString(nBind++,jParams['MV_PAR04'][1])
	__oQrySB1:setString(nBind++,' ')
	__oQrySB1:setString(nBind++,FWxFilial( "SB3" ))
	__oQrySB1:setString(nBind++,' ')
	__oQrySB1:setString(nBind++,FWxFilial( "SB1" ))
	__oQrySB1:setString(nBind++,cProd)
	__oQrySB1:setString(nBind++,' ')

	cAlias := __oQrySB1:OpenAlias("FILHO")
		
Return cAlias


//-------------------------------------------------------------------
/*{Protheus.doc} getNivel
Verificar se o produto tem niveis e retorna
@author BackOffice
*/
//-------------------------------------------------------------------   
Static Function  getNivel(cProd as Character,nNivel as Numeric) 
	Local cQuery    as Character	
	Local cRetorno  as Character	
	Local cAlias    as Character

	cAlias := "NIVEL"+CVALTOCHAR(nNivel)

	If __oQrySG1 == Nil
		cQuery := " SELECT G1_COD,G1_COMP "
		cQuery += " FROM " + RetSQLName("SG1") + " SG1 "
		cQuery += " WHERE SG1.G1_FILIAL = ? "
		cQuery += " AND SG1.G1_COD = ?  "
		cQuery += " AND SG1.D_E_L_E_T_ = ? "
		cQuery += " GROUP BY G1_COD,G1_COMP,G1_FILIAL "
		cQUery += " ORDER BY G1_COD "

		cQuery := changeQuery(cQuery)
		__oQrySG1 := FwExecStatement():New(cQuery)
	EndIf

	//Binding	
	__oQrySG1:setString(1,FWxFilial( "SG1" ))
	__oQrySG1:setString(2,cProd)
	__oQrySG1:setString(3,' ')

	cRetorno := __oQrySG1:OpenAlias(cAlias)		

Return cRetorno
