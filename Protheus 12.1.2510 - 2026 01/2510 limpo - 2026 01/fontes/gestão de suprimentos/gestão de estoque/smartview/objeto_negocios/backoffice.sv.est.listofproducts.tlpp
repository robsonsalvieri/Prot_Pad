#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.listofproducts.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.est.listadeprodutos.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1",name="Lista de Produtos", country="ALL", customTables="SB1,SB5,SBZ")

//-------------------------------------------------------------------
/*{Protheus.doc} ListadeProdutosSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Prodotos
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class ListadeProdutosSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object
 
	Protected Method oNGetSchema()

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class ListadeProdutosSmartViewBusinessObject

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 )

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 )

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 )

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class ListadeProdutosSmartViewBusinessObject
	
	Local jParams	as Json
	Local nX		as Numeric
	Local nFil		as Numeric
	Local cId		as Character
	Local cRealName as Character
	Local cQuery	as Character
	Local cAlias	as Character
	Local cBkpFil	as Character
	Local cFilExec	as Character
	Local cEmpExec	as Character
	Local aAllFields as Array
	Local aSX3Fields as Array
	Local oQuery as Object

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))

	////metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters() 

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cQuery := "SELECT ? "

	cQuery += "FROM " + RetSQLName("SB1") + " SB1 "

	cQuery += "LEFT JOIN " + RetSQLName("NNR") + " NNR "
	cQuery += "ON  NNR.NNR_FILIAL = ? "
	cQuery += "AND NNR.NNR_CODIGO = SB1.B1_LOCPAD "
	cQuery += "AND NNR.D_E_L_E_T_ = ? "

	cQuery += "LEFT JOIN " + RetSQLName("SBM") + " SBM "
	cQuery += "ON  SBM.BM_FILIAL = ?  "
	cQuery += "AND SBM.BM_GRUPO = SB1.B1_GRUPO "
	cQuery += "AND SBM.D_E_L_E_T_ = ? "	

	cQuery += "LEFT JOIN " + RetSQLName("SB5") + " SB5 "
	cQuery += "ON  SB5.B5_FILIAL = ?  "
	cQuery += "AND SB5.B5_COD = SB1.B1_COD "
	cQuery += "AND SB5.D_E_L_E_T_ = ? "	

	cQuery += "LEFT JOIN " + RetSQLName("SBZ") + " SBZ "
	cQuery += "ON  SBZ.BZ_FILIAL = ?  "
	cQuery += "AND SBZ.BZ_COD = SB1.B1_COD "
	cQuery += "AND SBZ.D_E_L_E_T_ = ? "	

	cQuery += "WHERE SB1.B1_FILIAL = ?  "
	cQuery += "AND SB1.B1_COD >= ?  "
	cQuery += "AND SB1.B1_COD <= ?  "

	//Os filtros serão setados na interface do Smart View
	cQuery += self:cWhereFiltroSV

	cQuery += self:cWhere

	cQuery += " AND SB1.D_E_L_E_T_ = ? "	
	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		oQuery:setUnsafe(1,self:getSQLFields())
		oQuery:setString(2,FWxFilial( "NNR" ))
		oQuery:setString(3,' ')
		oQuery:setString(4,FWxFilial( "SBM" ))
		oQuery:setString(5,' ')
		oQuery:setString(6,FWxFilial( "SB5" ))
		oQuery:setString(7,' ')
		oQuery:setString(8,FWxFilial( "SBZ" ))
		oQuery:setString(9,' ')
		oQuery:setString(10,FWxFilial( "SB1" ))
		oQuery:setString(11,jParams['MV_PAR01'][1])
		oQuery:setString(12,jParams['MV_PAR02'][1])
		oQuery:setString(13,' ')

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		While !(cAlias)->(Eof())
			aAllFields	:= self:getStructFields()
			aSX3Fields	:= self:getArrayFields()
			self:jItems	:= JsonObject():New()

			For nX := 1 To Len(aAllFields)
				cId := aAllFields[nX]:getName()
				cRealName := aAllFields[nX]:getRealName()

				If aScan( aSX3Fields , { | x | x ==  cRealName } ) > 0
					If aAllFields[nX]:getType() == "date" //Campo tipo data tem um tratamento diferente para envio
						if !Empty((cAlias)->&(cRealName))
							self:jItems[cId] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(cRealName)))					
						EndIf
					Else
						self:jItems[cId] := (cAlias)->&(cRealName)
					EndIf
				EndIf
			Next nX

			self:jItems["FILIAL"]	:= cFilAnt
			self:jItems["EMPNOME"]	:= cEmpExec
			self:jItems["FILNOME"]	:= cFilExec

			self:processData()
			self:oData:appendData(self:jItems)

			(cAlias)->(DBSkip())
		EndDo 

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class ListadeProdutosSmartViewBusinessObject

	Local aFields	as Array
	
	aFields	:= {}

	aAdd( aFields , "B1_COD"   )
	aAdd( aFields , "B1_DESC"  )
	aAdd( aFields , "B1_TIPO"  )
	aAdd( aFields , "B1_UM"    )
	aAdd( aFields , "B1_GRUPO" )
	aAdd( aFields , "B1_PRV1"  )
	aAdd( aFields , "B1_PRV2"  )
	aAdd( aFields , "B1_LOCPAD" )

	self:AliasToSchema("SB1", aFields)

	aFields	:= {}

	aAdd( aFields , "B5_COD"   )
	aAdd( aFields , "B5_CEME"  )
	aAdd( aFields , "B5_PRV2" )
	aAdd( aFields , "B5_DTREFP2"  )
	aAdd( aFields , "B5_DIASES"  )
	
	self:AliasToSchema("SB5", aFields)	

	self:AliasToSchema("SBM","BM_DESC")
	self:AliasToSchema("NNR","NNR_DESCRI")
	self:AliasToSchema("SBZ","BZ_COD")

	self:addProperty( "FILIAL" , STR0010 , "string" , STR0010 , "FILIAL"	) //"Filial"
	self:addProperty( "EMPNOME", STR0008 , "string" , STR0008 , "EMPNOME"	) //"Nome Empresa"
	self:addProperty( "FILNOME", STR0009 , "string" , STR0009 , "FILNOME"	) //"Nome Filial"

	//1º parâmetro - nome (identificador)
	//2º parâmetro - nome de exibição
	//3º parâmetro - tipo (string, number, date e boolean)
	//4º parâmetro - informa se recebe múltiplos valores
	self:addParameter("MV_PAR01", STR0006 ,"string", .F.) //Produto De?
	self:addParameter("MV_PAR02", STR0007 ,"string", .F.) //Produto Ate?

Return
