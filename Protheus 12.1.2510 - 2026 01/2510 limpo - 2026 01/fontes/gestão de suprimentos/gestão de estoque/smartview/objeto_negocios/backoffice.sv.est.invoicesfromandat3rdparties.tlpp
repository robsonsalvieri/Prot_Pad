#include "msobject.ch"
#include "backoffice.sv.est.invoicesfromandat3rdparties.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "MTR485"

using namespace totvs.protheus.backoffice.est.smartView.integratedProvider
namespace totvs.protheus.backoffice.est.invoicesfromandat3rdparties.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAEST",tables="SB6",name="Nfs De/Em Terceiro",country="ALL")

class InvoicesFromAndAt3rdPartiesSmartViewBusinessObject from totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider
	public method new() as object
	public method getDescription() as character
	public method getData() as object

	Protected Method oNGetSchema()
	Protected data aFieldsSB6 as array
	Protected data cAlias as character
	Protected data cWhere as character
	Protected data jItens as json
	Protected data lExistPergunte as logical

endclass

/*/{Protheus.doc} New  
Metodo de instancia da classe.
@type  Metodo
@author Squad Entradas
@since  Abril 19,2023
/*/
method new() class InvoicesFromAndAt3rdPartiesSmartViewBusinessObject
	_Super:new()

	//Define a Área
	self:appendArea(STR0001)

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002)

	//Indica o pergunte que será utilizado no relatório
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0007,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0006,cMsgSX1)   	  //#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV025",,,,cMsgSX1,,,)
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

	//Carrega os campos da SB6 que farão parte do ON
	self:aFieldsSB6 := FWSX3Util():GetAllFields("SB6",.F.)

return self

/*/{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type  Metodo
@author Squad Entradas
@since  Abril 19,2023
/*/
method getDescription() as character class InvoicesFromAndAt3rdPartiesSmartViewBusinessObject
return (STR0003)

/*/{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type  Metodo
@author Squad Entradas
@since  Abril 19,2023
/*/
method getData(nPage as numeric, oFilter as object) as object class InvoicesFromAndAt3rdPartiesSmartViewBusinessObject
	local oQuery     as object
	local cQuery     as character
	local cFilExec   as character
	local cFields    as character
	local cEmpExec   as character
	local cBkpFil    as character
	local cListaNF_F as character
	local cListaNF_C as character
	local cB6Tipo_D  as character
	local cB6Tipo_E  as character
	local dDataDe    as date
	local dDataAte   as date
	local nX         as numeric
	local nFil       as numeric
	local nBind      as numeric
	local jParams    as json

	//Variáveis com o nome da Filial e Grupo de Empresa
	cFilExec := AllTrim(FWFilialName())
	cEmpExec := AllTrim(FWEmpName(cEmpAnt))

	//Cria um alias disponível para utilização
	self:cAlias := GetNextAlias()

	//Metodo para retorno do json dos parâmetros 
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente 
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cListaNF_F := 'F'
	cListaNF_C := 'C'
	cB6Tipo_D  := 'D'
	cB6Tipo_E  := 'E'
	dDataDe    := FwDateTimeToLocal(jParams[ 'MV_PAR06' ][1] )[1]
	dDataAte   := FwDateTimeToLocal(jParams[ 'MV_PAR07' ][1] )[1]

	//Campos que farão parte do select da query
	cFields := ArrTokStr(self:aFieldsSB6,",")

	//Retorna o tipo do campos da SB9 convertidos para o Smart View
	self:aFieldsSB6 := estGetCpo(self:aFieldsSB6)

	//Montagem do Select da Query
	cQuery := "SELECT ? "

	//Montagem do From da Query
	cQuery  += "FROM " + RetSqlName("SB6") + " SB6 "

	//Montagem do Where da Query
	cQuery += "WHERE SB6.B6_FILIAL = ? AND "
	cQuery += "SB6.B6_PRODUTO >= ? AND "
	cQuery += "SB6.B6_PRODUTO <= ? AND "
	cQuery += "SB6.B6_CLIFOR >= ? AND "
	cQuery += "SB6.B6_CLIFOR <= ? AND "
	cQuery += "SB6.B6_DOC >= ? AND "
	cQuery += "SB6.B6_DOC <= ? AND "
	cQuery += "SB6.B6_SERIE >= ? AND "
	cQuery += "SB6.B6_SERIE <= ? AND "
	cQuery += "SB6.B6_EMISSAO >= ? AND "
	cQuery += "SB6.B6_EMISSAO <= ? AND "

	If jParams['MV_PAR01'][1] == 1
		cQuery += "SB6.B6_TPCF = ? "
	ElseIf jParams['MV_PAR01'][1] == 2
		cQuery += "SB6.B6_TPCF = ? "
	Else
		cQuery += "SB6.B6_TPCF IN (?,?) "
	EndIf

	If jParams['MV_PAR12'][1] == 1
		cQuery += " AND SB6.B6_TIPO = ? "
	ElseIf jParams['MV_PAR12'][1] == 2
		cQuery += " AND SB6.B6_TIPO = ? "
	EndIf

	cQuery += " AND SB6.D_E_L_E_T_ = ? "
	cQuery += self:cWhere
	cQuery += self:cWhereFiltroSV
	cQuery += " ORDER BY SB6.B6_FILIAL,SB6.B6_DOC,SB6.B6_SERIE,SB6.B6_TIPO,SB6.B6_CLIFOR,SB6.B6_TPCF"
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilant 

	//Colocar while ou for de filiais aqui 
	For nFil :=1 to len(self:aFils)

		cFilAnt  := self:aFils[nFil]
		cFilExec := AllTrim(FWFilialName())
		nBind    := 1

		oQuery:SetUnsafe(nBind++, cFields)
		oQuery:SetString(nBind++, FWxFilial('SB6'))
		oQuery:SetString(nBind++, jParams['MV_PAR08'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR09'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR10'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR11'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR02'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR03'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR04'][1])
		oQuery:SetString(nBind++, jParams['MV_PAR05'][1])
		oQuery:SetString(nBind++, dtos(dDataDe))
		oQuery:SetString(nBind++, dtos(dDataAte))

		If jParams['MV_PAR01'][1] == 1
			oQuery:SetString(nBind++, cListaNF_F)
		ElseIf jParams['MV_PAR01'][1] == 2
			oQuery:SetString(nBind++, cListaNF_C)
		Else
			oQuery:SetString(nBind++, cListaNF_F)
			oQuery:SetString(nBind++, cListaNF_C)
		EndIf

		If jParams['MV_PAR12'][1] == 1
			oQuery:SetString(nBind++, cB6Tipo_D)
		ElseIf jParams['MV_PAR12'][1] == 2
			oQuery:SetString(nBind++, cB6Tipo_E)
		EndIf

		oQuery:SetString(nBind++, ' ')
		oQuery:OpenAlias(self:cAlias)

		Do While !(self:cAlias)->(Eof())

			self:jItens := JsonObject():New()

			For nX := 1 To Len(self:aFieldsSB6)
				If self:aFieldsSB6[nX][2] == "date"
					If !Empty((self:cAlias)->&(self:aFieldsSB6[nX][1]))
						self:jItens[self:aFieldsSB6[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(StoD((self:cAlias)->&(self:aFieldsSB6[nX][1])))
					EndIf
				Else
					self:jItens[self:aFieldsSB6[nX][1]] := (self:cAlias)->&(self:aFieldsSB6[nX][1])
				EndIf
			Next nX

			self:jItens["EMPNOME"] := cEmpExec
			self:jItens["FILNOME"] := cFilExec

			self:processData()
			self:oData:appendData(self:jItens)

			(self:cAlias)->( dbSkip() )

		EndDo

		(self:cAlias)->( DBCloseArea() )

	Next 

	cFilAnt := cBkpFil
	oQuery:Destroy()

return self:oData

/*/{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type  Metodo
@author Squad Entradas
@since  Abril 19,2023
/*/
method oNGetSchema() class InvoicesFromAndAt3rdPartiesSmartViewBusinessObject

	self:oSchema:aliasToSchema("SB6",self:aFieldsSB6)

	self:oSchema:addProperty("EMPNOME", STR0004, "string", STR0004, "EMPNOME")
	self:oSchema:addProperty("FILNOME", STR0005, "string", STR0005, "FILNOME")

return

/*/{Protheus.doc} estGetStruct
Prepara a estrutura dos campos
@type  Função
@author Squad Entradas
@since  Maio 16,2023
/*/
Static Function estGetCpo(aFields as array)
	local aDeParaCpo as array
	local aCpoTmp := {} as array
	local cTipR as character
	local cTipo as character
	local nPos as numeric
	local nX as numeric

	aDeParaCpo := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}}

	For nX := 1 To Len(aFields)
		cTipo := FWSX3Util():GetFieldType( aFields[nX] )

		If (nPos := aScan( aDeParaCpo, {|c| c[01] = cTipo } ) ) > 0
			cTipR := aDeParaCpo[nPos, 02]
		Else
			cTipR := "string"
		EndIf

		AAdd( aCpoTmp,;
			{aFields[nX],;
			cTipR})
	Next nX

Return (aCpoTmp)
