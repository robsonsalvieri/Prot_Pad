#include "msobject.ch"
#include "backoffice.sv.est.qccasualtyratio.ch"
#include "totvs.framework.treports.integratedprovider.th"

#define SX1GRUPO "ESTT028"

namespace totvs.protheus.backoffice.est.qccasualtyratio.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SD7", name="Relação de Baixas do CQ", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} QCCasualtyRatioSmartViewBusinessObject
Classe para criação do Objeto de Negócio

@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class QCCasualtyRatioSmartViewBusinessObject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields	as array
	Protected data jItems	as json
	Protected data cWhere	as character
	Protected data lExistPergunte	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} QCCasualtyRatioSmartViewBusinessObject
Método de instância da classe

@return object: self
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class QCCasualtyRatioSmartViewBusinessObject

	_Super:new()

	//Define a Área
	self:appendArea( STR0001 )// "Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) // "Relação de Baixas do CQ."

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) // "Este relatório realiza uma avaliação sobre as baixas realizadas no armazém"
	
	//Define o conjunto de perguntas e faz o teste se existe
	self:lExistPergunte := self:setPergunte( SX1GRUPO )
	If !self:lExistPergunte
		self:setErrorStatus(400,STR0005,i18n(STR0006,{SX1GRUPO}))//O Grupo de perguntas não existe		
		FWLogMsg('WARN', , 'SmartView', , , , i18n(STR0006,{SX1GRUPO}), , , )//O Grupo de perguntas não existe
	EndIf

	//Indica que o LookUp será do tipo padrão LookUp
	self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@return object: self:oData
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as Numeric, oFilter as object) as object Class QCCasualtyRatioSmartViewBusinessObject

	Local cQuery	as Character
	Local cAlias	as Character
	Local cFields	as Character
	Local cBkpFil	as Character
	Local cFilExec	as Character
	Local cEmpExec	as Character
	Local nX		as Numeric
	Local nFil		as Numeric
	Local nDbPar	as Numeric
	Local jParams	as Json
	Local dDataDe	as Date
	Local dDataAte	as Date
	Local oQuery    	

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	//Valida o pergunte
	If !self:lExistPergunte
		return self:oData
	Endif

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))

	cFields := StrTran(ArrTokStr(self:aFields),"|",",")	

	dDataDe  := FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1]
	dDataAte := FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1]

	cQuery := " SELECT ? "
	cQuery += " FROM " + RetSQLName("SD7") + " SD7 "
	cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 ON (SB1.B1_COD = SD7.D7_PRODUTO)"
	cQuery += " WHERE SD7.D7_FILIAL = ? "
	cQuery += " AND SD7.D7_DOC >= ? "
	cQuery += " AND SD7.D7_DOC <= ? "
	cQuery += " AND SD7.D7_DATA >= ? "
	cQuery += " AND SD7.D7_DATA <= ? "
    cQuery += " AND SD7.D7_FORNECE >= ? "
    cQuery += " AND SD7.D7_FORNECE <= ? "

    If jParams['MV_PAR07'][1] == 1
		cQuery += " AND D7_LIBERA = ?"
	ElseIf jParams['MV_PAR07'][1] == 2
		cQuery += " AND D7_LIBERA = ?"
	ElseIf jParams['MV_PAR07'][1] == 3
		cQuery += " AND D7_LIBERA IN (?)"
	EndIf
    
    If jParams['MV_PAR08'][1] == 1
		cQuery += " AND D7_ESTORNO IN (?)"
	Else
		cQuery += " AND D7_ESTORNO = ?"
	EndIf

	//Os filtros serão setados na interface do Smart View
	cQuery += self:cWhereFiltroSV

	//Internacionalização
	cQuery += self:cWhere

	cQuery += " AND SD7.D_E_L_E_T_ = ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		nDbPar := 1
		oQuery:setUnsafe(nDbPar++,cFields)
		oQuery:setString(nDbPar++,FWxFilial( "SD7" ))
		oQuery:setString(nDbPar++,jParams['MV_PAR01'][1])
		oQuery:setString(nDbPar++,jParams['MV_PAR02'][1])
		oQuery:setString(nDbPar++,Dtos(dDataDe))
		oQuery:setString(nDbPar++,Dtos(dDataAte))
		oQuery:setString(nDbPar++,jParams['MV_PAR05'][1])
		oQuery:setString(nDbPar++,jParams['MV_PAR06'][1])

		If jParams['MV_PAR07'][1] == 1
			oQuery:setString(nDbPar++,'S')
		ElseIf jParams['MV_PAR07'][1] == 2
			oQuery:setString(nDbPar++,'N')
		ElseIf jParams['MV_PAR07'][1] == 3
			oQuery:SetIn(nDbPar++,{' ','S','N'})
		EndIf

		If jParams['MV_PAR08'][1] == 1
			oQuery:SetIn(nDbPar++,{'S',' '})
		Else
			oQuery:setString(nDbPar++,' ')
		EndIf

		oQuery:setString(nDbPar++,' ')

		cAlias := oQuery:OpenAlias()

		(cAlias)->(dbGoTop())

		While !(cAlias)->(Eof())
			self:jItems := JsonObject():New()

			For nX := 1 To Len(self:aFields)			
				if FWSX3Util():GetFieldType(self:aFields[nX]) == "D" //Campo tipo data tem um tratamento diferente para envio
					If !Empty( (cAlias)->&(self:aFields[nX]) )
						self:jItems[self:aFields[nX]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->&(self:aFields[nX])))
					EndIf
				Else
					self:jItems[self:aFields[nX]] := (cAlias)->&(self:aFields[nX])
				EndIf
			Next nX		

			self:jItems["FILIAL"]	:= cFilAnt
			self:jItems["EMPNOME"]	:= cEmpExec
			self:jItems["FILNOME"]	:= cFilExec

			self:processData()
			self:oData:appendData(self:jItems)

			(cAlias)->(DBSkip())
		EndDo

		(cAlias)->(DBCloseArea())
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author Vicente Gomes
@since 11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method oNGetSchema() Class QCCasualtyRatioSmartViewBusinessObject
	
	self:aFields :={"D7_NUMERO","D7_PRODUTO","B1_DESC","B1_TIPO","B1_UM","D7_LOCAL","D7_DOC","D7_ORIGLAN","D7_FORNECE","D7_DATA","D7_TIPO","D7_USUARIO","D7_LOCDEST","D7_LOTECTL","D7_NUMLOTE"}
	self:AliasToSchema("SD7", self:aFields)
	self:AliasToSchema("SB1", self:aFields)

	self:addProperty( "FILIAL" , STR0007 , "string" , STR0007 , "FILIAL"	) //"Filial"
	self:addProperty( "FILNOME", STR0008 , "string" , STR0008 , "FILNOME"	) //"Nome Filial"
	self:addProperty( "EMPNOME", STR0009 , "string" , STR0009 , "EMPNOME"	) //"Nome Empresa"

Return
