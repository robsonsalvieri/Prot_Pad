#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.est.stockpositionbylotsublot.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE SX1GRUPO "ESTT002"

namespace totvs.protheus.backoffice.est.Posicaoestoqueporlotesublote.integratedprovider
using namespace totvs.protheus.backoffice.est.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAEST", tables="SB1,SB8",name="Posição de Estoque por Lote/Sub-Lote", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} Posicaoestoqueporlotesublote
Classe para criação do Objeto de Negócio de Posicao de Estoque por Lote/Sub-Lote
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class PosicaoestoqueporlotesubloteSmartViewbusinessobject From totvs.protheus.backoffice.est.smartView.integratedProvider.EstIntegratedProvider

	Public Method new() as object
	Public Method getData() as object

	Protected Method oNGetSchema()

	Protected data aFields	as Array
	Protected data jItems	as Json
	Protected data cWhere	as Character
	Protected data lExistPergunte as Logical 

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method New() Class PosicaoestoqueporlotesubloteSmartViewbusinessobject

	LOCAL cMsgSX1 as Character 

	_Super:new()
	
	//Define a Área
	self:appendArea( STR0001 ) //"Estoque/Custos"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0002 ) //"Posição de Estoque por Lote/Sub-Lote"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0003 ) //"Este programa emitira uma relacao com a posição de estoque por Lote/Sub-Lote."

	//Grupo de Perguntas
	self:lExistPergunte := self:SetPergunte(SX1GRUPO)
	If !self:lExistPergunte
		cMsgSX1 := OemToAnsi(I18N(STR0010,{SX1GRUPO})) //#Grupo de perguntas #1[SX1GRUPO]# nao encontrado. Verifique na tabela SX1, o grupo de perguntas informado
		self:setErrorStatus(400,STR0011,cMsgSX1)		//#Sem Pergunte
		FwLogMsg("WARN",, "SmartView ESTSV018",,,,cMsgSX1,,,)
	EndIf

	self:aFields := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData( nPage as Numeric, oFilter as object) as object Class PosicaoestoqueporlotesubloteSmartViewbusinessobject

	Local oQuery	as object
	Local cQuery	as Character
	Local nX		as Numeric
	Local jParams	as Json
	Local cFields	as Character
	Local cAliasSB8	as Character
	Local dDtValDe	as Date
	Local dDtValAte	as Date
	Local cFilExec	as Character
	Local cEmpExec	as Character
	Local cBkpFil	as Character
	Local lEmpPrev	as Logical
	Local nF		as Numeric
	Local nFil		as Numeric
	Local nDbPar	as Numeric
	Local nSaldo	as Numeric
	Local nEmpenho	as Numeric
	Local nSaldo2	as Numeric
	Local nEmpenho2	as Numeric

	If !self:lExistPergunte
		Return self:oData
	EndIf

	//Variável com o nome do Grupo de Empresa
	cEmpExec  := AllTrim(FWEmpName(cEmpAnt))

	lEmpPrev  := (SuperGetMV("MV_QTDPREV") == "S")

	//--------------------------------------------------------------------------
	// Variaveis utilizadas para parametros no grupo de pergunta MTR260R2
	//--------------------------------------------------------------------------
	// MV_PAR01 - Do  Produto ?                 
	// MV_PAR02 - Ate o Produto ?               
	// MV_PAR03 - Do Tipo ?                     
	// MV_PAR04 - Ate o Tipo ?                  
	// MV_PAR05 - Do Grupo ?                    
	// MV_PAR06 - Ate o Grupo ?                 
	// MV_PAR07 - Do Armazem ?                  
	// MV_PAR08 - Ate o Armazem ?               
	// MV_PAR09 - Do  Lote ?                    
	// MV_PAR10 - Ate o Lote ?                  
	// MV_PAR11 - Do  Sub-Lote ?                
	// MV_PAR12 - Ate o Sub-Lote ?              
	// MV_PAR13 - Da Data de Validade ?         
	// MV_PAR14 - Ate a Data de Validade ?      
	// MV_PAR15 - Listar Lote/Sub Com Sld Zero ?
	//--------------------------------------------------------------------------

	//Metodo para retorno do json dos parâmetros
	jParams := oFilter:getParameters()

    //Retorno da tela de multi filiais com as filiais selecionadas ou somente a filial corrente
    self:aFils := self:retFils(jParams['SV_MULTBRANCH'], oFilter)

	cAliasSB8 := GetNextAlias()

	For nF := 1 To Len( self:aFields )
		cFields += Alltrim( self:aFields[nF][1] ) + ","
	Next nF

	cFields   := SubStr( cFields , 1 , Len( cFields ) -1 )
	dDtValDe  := FwDateTimeToLocal(jParams["MV_PAR13"][1])[1]
	dDtValAte := FwDateTimeToLocal(jParams["MV_PAR14"][1])[1]

	cQuery := " SELECT ? "

	cQuery += " FROM "+ RetSQLName( 'SB8' ) +" SB8 "
	cQuery += " LEFT JOIN "+ RetSQLName( 'NNR' ) +" NNR "
	cQuery += " ON NNR_FILIAL = ? "
	cQuery += " AND NNR_CODIGO  = B8_LOCAL "
	cQuery += " AND NNR.D_E_L_E_T_ = ? "

	cQuery += " LEFT JOIN "+ RetSQLName( 'SB1' ) +" SB1 "
	cQuery += " ON B1_FILIAL =  ? "
	cQuery += " AND B1_TIPO >= ? "
	cQuery += " AND B1_TIPO <= ? "
	cQuery += " AND B1_COD = B8_PRODUTO "
	cQuery += " AND B1_GRUPO >= ? "
	cQuery += " AND B1_GRUPO <= ? "
	cQuery += " AND SB1.D_E_L_E_T_ = ? "

	cQuery += " WHERE B8_FILIAL = ? "
	cQuery += " AND B8_PRODUTO >= ? "
	cQuery += " AND B8_PRODUTO <= ? "
	cQuery += " AND B8_LOCAL >= ? "
	cQuery += " AND B8_LOCAL <= ? "
	cQuery += " AND B8_DTVALID >= ? "
	cQuery += " AND B8_DTVALID <= ? "
	cQuery += " AND B8_LOTECTL >= ? "
	cQuery += " AND B8_LOTECTL <= ? "
	cQuery += " AND B8_NUMLOTE >= ? "
	cQuery += " AND B8_NUMLOTE <= ? "

	If jParams["MV_PAR15"][1] == 2
		cQuery += " AND	B8_SALDO > ? "
	EndIf

	//Os filtros serão setados na interface do novo TReports 
	cQuery += self:cWhereFiltroSV

 	cQuery += self:cWhere
	cQuery += " AND SB8.D_E_L_E_T_ = ? "

	cQuery := changeQuery(cQuery)
	oQuery := FwExecStatement():New(cQuery)

	cBkpFil := cFilAnt

	For nFil :=1 to len(self:aFils)
		cFilAnt := self:aFils[nFil]

		//Variável com o nome da Filial
		cFilExec := AllTrim(FWFilialName())

		//Binding
		nDbPar := 1
		oQuery:SetUnsafe(nDbPar++, cFields)
		oQuery:SetString(nDbPar++, FWxFilial("NNR"))
		oQuery:SetString(nDbPar++, " ")
		oQuery:SetString(nDbPar++, FWxFilial("SB1"))
		oQuery:SetString(nDbPar++, jParams["MV_PAR03"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR04"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR05"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR06"][1])
		oQuery:SetString(nDbPar++, " ")
		oQuery:SetString(nDbPar++, FWxFilial("SB8"))
		oQuery:SetString(nDbPar++, jParams["MV_PAR01"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR02"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR07"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR08"][1])
		oQuery:SetString(nDbPar++, Dtos(dDtValDe))
		oQuery:SetString(nDbPar++, Dtos(dDtValAte))
		oQuery:SetString(nDbPar++, jParams["MV_PAR09"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR10"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR11"][1])
		oQuery:SetString(nDbPar++, jParams["MV_PAR12"][1])
		If jParams["MV_PAR15"][1] == 2
			oQuery:SetNumeric(nDbPar++, 0)
		EndIf
		oQuery:SetString(nDbPar++, " ")

		oQuery:OpenAlias(cAliasSB8)

		(cAliasSB8)->(dbGoTop())

		While !(cAliasSB8)->(Eof())

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  Saldo do Lote ou Lote/Sublote                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nSaldo		:= SB8SALDO(,,,,cAliasSB8,lEmpPrev,,,.T.)
			nEmpenho	:= SB8SALDO(.T.,,,,cAliasSB8,lEmpPrev,,,.T.)
			nSaldo2		:= SB8SALDO(,,,.T.,cAliasSB8,lEmpPrev,,,.T.) // Quando passado .T. no 4o. Parametro a funcao retorna a 2a. UM. 
			nEmpenho2	:= SB8SALDO(.T.,,,.T.,cAliasSB8,lEmpPrev,,,.T.)

			self:jItems		:= JsonObject():New()

			For nX := 1 To Len(self:aFields)

				If self:aFields[nX][2] == "D"
					If !Empty((cAliasSB8)->&(self:aFields[nX][1]))
						self:jItems[self:aFields[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(StoD((cAliasSB8)->&(self:aFields[nX][1])))
					EndIf
				Else
					self:jItems[self:aFields[nX][1]] := (cAliasSB8)->&(self:aFields[nX][1])
				EndIf

			Next nX

			self:jItems["EMPNOME" ] := cEmpExec
			self:jItems["FILNOME" ] := cFilExec
			self:jItems["SLD1UM"  ] := nSaldo
			self:jItems["EMP1UM"  ] := nEmpenho
			self:jItems["SLD2UM"  ] := nSaldo2
			self:jItems["EMP2UM"  ] := nEmpenho2

			self:processData()
			self:oData:appendData(self:jItems)
		
			(cAliasSB8)->(DBSkip())
		EndDo 
		
		(cAliasSB8)->( DBCloseArea() )
	Next

	cFilAnt := cBkpFil

	oQuery:Destroy()

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} oNGetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author BackOffice
@since 04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method oNGetSchema() Class PosicaoestoqueporlotesubloteSmartViewbusinessobject

	Local aFields as Array
	Local aStrAux as Array
	Local nC      as Numeric

	self:AliasToSchema("SB8")

	aFields := {}
	aAdd( aFields , "NNR_DESCRI"	)
	aAdd( aFields , "B1_COD"		)
	aAdd( aFields , "B1_DESC"		)
	aAdd( aFields , "B1_TIPO"		)
	aAdd( aFields , "B1_GRUPO"		)

	self:AliasToSchema("SB1", aFields)
	self:AliasToSchema("NNR", "NNR_DESCRI"	)

	self:addProperty( "EMPNOME", STR0004 , "string" , STR0004 , "EMPNOME"	) //"Nome Empresa"
	self:addProperty( "FILNOME", STR0005 , "string" , STR0005 , "FILNOME"	) //"Nome Filial"
	self:addProperty( "SLD1UM" , STR0006 , "number" , STR0006 , "SLD1UM"	) //"Saldo Primeira U.M."
	self:addProperty( "EMP1UM" , STR0007 , "number" , STR0007 , "EMP1UM"	) //"Empenho Primeira U.M."
	self:addProperty( "SLD2UM" , STR0008 , "number" , STR0008 , "SLD2UM"	) //"Saldo Segunda U.M."
	self:addProperty( "EMP2UM" , STR0009 , "number" , STR0009 , "EMP2UM"	) //"Empenho Segunda U.M."

	DbSelectArea( "SB8" )
	aStrAux := SB8->( DBStruct() )

	For nC := 1 To Len( aStrAux )
		If aStrAux[ nC , 02 ] != "M"
			aAdd( self:aFields , { Alltrim( aStrAux[ nC , 01 ] ) , aStrAux[ nC , 02 ] } )
		EndIf
	Next nC

	aAdd( self:aFields , {"NNR_DESCRI"	, "C" } )
	aAdd( self:aFields , {"B1_COD"	 	, "C" } )
	aAdd( self:aFields , {"B1_DESC"	 	, "C" } )
	aAdd( self:aFields , {"B1_TIPO"	 	, "C" } )
	aAdd( self:aFields , {"B1_GRUPO"	, "C" } )

Return
