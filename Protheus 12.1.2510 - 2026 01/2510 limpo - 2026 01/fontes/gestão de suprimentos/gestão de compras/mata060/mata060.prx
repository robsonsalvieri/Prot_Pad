#INCLUDE "MATA060.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
/*


Ŀ
Funo     MATA060   Autor  Jorge Queiroz          Data           
Ĵ
Descrio  Programa de atualizacao da Amarracao Produto x Fornecedor  
Ĵ
Sintaxe e  Void MATA060(void)                                         
Ĵ
 Uso       Generico                                                   
Ĵ
 PROGRAMADOR    DATA    BOPS   MOTIVO DA ALTERACAO                  
Ĵ
Marcelo Piment15/09/00 Meta  Retirada funcoes referente ao  Modulo  
                       Meta  Insp. de Entregas e incluida no QIEA330
                       Meta  ficou apenas a funcao A060F3Pr().      
ٱ


*/
Function MATA060(xRotAuto,nOpcAuto)
 
Local lMA060FIL	:= ExistBlock("MA060FIL")
Local cFiltraSA5	:= ""
Local aIndexSA5	:= {}
Local bFiltraBrw	:= Nil

Default nOpcAuto := 3

Private aRotAuto := Nil
//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
Private aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
Private cCadastro := OemtoAnsi(STR0008)		//"Atualizao Produto x Fornecedor"

//Verificao Release e data de corte
If !A060CclVda()
	Help( Nil, Nil, STR0028, NIL, STR0029, 1, 0,NIL, NIL, NIL, NIL, NIL,{STR0030})
	Return .F.
EndIf

//Ŀ
// Fecha Area													 
//
DbSelectArea("SA5")
DbCloseArea() 

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If xRotAuto <> Nil
	aRotAuto := xRotAuto
	MBrowseAuto(nOpcAuto,aRotAuto,"SA5")
Else
	If lMA060FIL
		cFiltraSA5 := ExecBlock("MA060FIL",.F.,.F.)
		bFiltraBrw := {|| FilBrowse("SA5",@aIndexSA5,@cFiltraSA5)}
		Eval(bFiltraBrw)
	EndIf
	
	mBrowse(6,1,22,75,"SA5")
	
	If lMA060FIL
		EndFilBrw("SA5",aIndexSA5)
	EndIf
EndIf   

//Ŀ
// Fecha Area													 
//
DbSelectArea("SA5")
DbCloseArea() 
Return 

/*


Ŀ
Funo     A060Manut Autor  Vera Lucia S. Simoes   Data 26/02/1999
Ĵ
Descrio  Inclui e Altera uma amarracao Produto X Fornecedor         
Ĵ
Parametros ExpC1: Alias posicionado                                   
           ExpN1: Numero do registro posicionado                      
           ExpN2: Opcao do menu selecionada                           
Ĵ
Retorno                                                               
Ĵ
Uso                                                                   
ٱ


*/
Function A060Manut(cAlias,nReg,nOpc)

Local nOpca 			:= 0
Local lHistTab   		:= SuperGetMV("MV_HISTTAB", .F., .F.)
Local cFilialAIF 		:= xFilial("AIF")
Local cFilialSA5 		:= xFilial("SA5")
Local dDataAlt
Local cHoraAlt
Local nX				:= 0

Private aCpoAltSA5	:= {} // vetor usado na gravacao do historico de alteracoes
                                          
If nModulo == 21	// SIGAQIE
	cSitAnt := If(!INCLUI,A5_SITU,  Space(TamSX3("A5_SITU")[1]  )) //Situacao anterior
	cSklAnt := If(!INCLUI,A5_SKPLOT,Space(TamSX3("A5_SKPLOT")[1])) //Skip-Lote anterior
	cChvHis := If(!INCLUI,A5_CHAVE, Space(TamSX3("A5_CHAVE")[1] )) //Chave ligacao com as justificativas
EndIf
If nOpc == 3                       
	nOpcA:=  AxInclui(cAlias,nReg,nOpc,/*aAcho*/,,/*aCpos*/,"A060Valid() .And. MA060TUDOK().and.A060FABR()",/*lF3*/,/*cTransact*/,/*aButtons*/,/*aParam*/,aRotAuto,.T.)
Else
	nOpcA := AxAltera(cAlias,nReg,nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,"A060Valid().and.MA060TUDOK()",/*cTransact*/,/*cFunc*/,/*aButtons*/,/*aParam*/,aRotAuto,.T.)
EndIf                        
//Ŀ
// Grava a chave do Historico utilizado na Inspecao de Entradas 
//
If nOpca == 1
	If (nModulo == 21) //SIGAQIE
		If Empty(SA5->A5_CHAVE)
			If Empty(cChvHis) 
				cChvHis := A060Chave()
			EndIf
			RecLock("SA5",.F.)
			SA5->A5_CHAVE := cChvHis
			MsUnLock()
		EndIf	
	
		If cSitAnt <> SA5->A5_SITU .And. !Empty(cSitAnt)
			// Monta/Obtem a chave para o Historico/Justificativa
			If Empty(cChvHis)
				cChvHis := A060Chave()
			EndIf
		
			//Ŀ
			// Digita a Justificativa e gera Historico da Situacao             
			//
			QA_JUST(OemToAnsi(STR0018),"MATA060T",cChvHis,.F.,cSitAnt) //"Justificativa Situao"
		EndIf	
   	
		If cSklAnt <> SA5->A5_SKPLOT .And. !Empty(cSklAnt)
			// Monta/Obtem a chave para o Historico/Justificativa
			If Empty(cChvHis)
				cChvHis := A060Chave()
			EndIf
		
			If Empty(SA5->A5_SKPLOT)	// Alter. automat. pelo sistema, ao alterar a Situacao
				RecLock("QA3",.T.)
				QA3->QA3_FILIAL := xFilial("QA3")
				QA3->QA3_ESPEC  := "MATA060L"
				QA3->QA3_CHAVE  := cChvHis
				QA3->QA3_TEXTO  := STR0019 //"Atualizado automaticamente pelo sistema."
				QA3->QA3_DATA   := dDataBase
				QA3->QA3_DATINV := Inverte(dDataBase)
				QA3->QA3_ANT    := cSklAnt
			Else
				//Ŀ
				// Digita a Justificativa e gera Historico do Skip-Lote            
				//
				QA_JUST(STR0020,"MATA060L",cChvHis,.F.,cSklAnt) //"Justificativa Skip-Lote"
			EndIf
		EndIf 
	EndIf  
	If ExistTemplate("MT060GRV")		
		ExecTemplate("MT060GRV",.F.,.F.)
	EndIf
	If ExistBlock("MT060GRV")		
		ExecBlock("MT060GRV",.F.,.F.)
	EndIf
	
	//Ŀ
	// Aciona integrao via mensagem nica          				
	//	
	FwIntegDef( 'MATA060' )
		
EndIf

//Ŀ
//Gravar o historico das alteracoes realizadas pelo usuario    
//na tabela AIF usando o vetor aCpoAltSA5 que foi carregado na 
//funcao A060TudOk().                                          
//
If lHistTab .And. Len(aCpoAltSA5) > 0
	dDataAlt := Date()
	cHoraAlt := Time()
	For nX := 1 To Len(aCpoAltSA5)
		MSGrvHist(cFilialAIF,;			// Filial de AIF
		cFilialSA5,;						// Filial da tabela SA5
		"SA5",;							// Tabela SA5
		SA5->A5_FORNECE,;					// Codigo do cliente
		SA5->A5_LOJA,;								// Loja do cliente
		aCpoAltSA5[nX][1],;				// Campo alterado
		aCpoAltSA5[nX][2],;				// Conteudo antes da alteracao
		dDataAlt,;							// Data da alteracao
		cHoraAlt,;							// Hora da alteracao
		SA5->A5_PRODUTO)					// Codigo do produto							
	Next nX
	aCpoAltSA5 := {} // limpar o vetor
EndIf

dbSelectArea(cAlias)

Return

/*


Ŀ
Funo     A060Valid Autor  Vera Lucia S. Simoes   Data 26/02/1999
Ĵ
Descrio  Validacao da inclusao/alteracao                            
Ĵ
Parametros ExpC1: Nome do campo                                       
Ĵ
Uso                                                                   
ٱ

*/
Function A060Valid()
Local aAreaSA5 	  := SA5->(GetArea())
Local cSeekSA5    := ""
Local nRec        := SA5->(RecNo())
Local lRet        := .T.
Local lRetPE      := .T.
Local lReferencia := .F.
Local lGrade	  := MaGrade()
Local oModel	  := FWModelActive()
Local lIsInMVC	  := IsInMVC()
Local oField  	  := If(lIsInMVC,oModel:GetModel("MdFieldSA5"),NIL)
Local oGrid		  := If(lIsInMVC,oModel:GetModel("MdGridSA5"),NIL)
//Ŀ
//Tratamento para e-Commerce      
//
Local lECommerce  := SuperGetMV("MV_LJECOMM",.F.,.F.)
Local lPrForFab	  := SuperGetMV('MV_PRFOFAB',.F.,.F.)
Local cChave      := ''
Local cCampoNRed  := ""

cNomeCampo := READVAR()
cNomeCampo := If(Left(cNomeCampo,3)="M->",Subs(cNomeCampo,4),cNomeCampo)
//Ŀ
// Ateno: A validao da amarrao Produto X Fornecedor tem comportamento      |
// diferente quando  efetuada atravs do mdulo de Compras/outros mdulos       |
// do que quando  efetuada atravs do mdulo SigaEIC.							  |		 
//

If cNomeCampo == 'A5_FABR'
	If Empty(If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR))
		Help(" ",1,"A060FABR")
		lRet := .F.
	ElseIf !SA2->(dbSeek(xFilial("SA2")+If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR)))
		Help(" ",1,"REGNOIS")
		lRet := .F.
	ElseIf LEFT(SA2->A2_ID_FBFN,1) = '2'
		Help(" ",1,"A060FABRNO")
		lRet := .F.
	ElseIf Inclui
		//Ŀ
		// Verifica a chave para pesquisa                               
		//
		SA5->(DbSetOrder(3))
		If StrTran(SA5->(IndexKey())," ","") == "A5_FILIAL+A5_PRODUTO+A5_FABR+A5_FALOJA+A5_FORNECE+A5_LOJA"
			If lIsInMVC
				cSeekSA5 := xFilial("SA5")+getCodProd(oModel)+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FALOJA")+oGrid:GetValue("A5_FORNECE")
			Else
				cSeekSA5 := xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FALOJA+M->A5_FORNECE
			EndIf
		Else
			If lIsInMVC
				cSeekSA5 := xFilial("SA5")+getCodProd(oModel)+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FORNECE")
			Else
				cSeekSA5 := xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FORNECE
			EndIf
		EndIf
		
		If SA5->(DbSeek(cSeekSA5))
			If ((IsComex()) .And. EicLoja()) .Or. (nModulo == 2 .Or. nModulo == 4) 			
            	While SA5->(!EOF()) .And. SA5->A5_FILIAL  == xFilial("SA5")						.And.;
            	      SA5->A5_PRODUTO == If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)	.And.;
            	      SA5->A5_FABR    == If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR)   		.And.;
            	      SA5->A5_FORNECE == If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE)

	                If SA5->(A5_LOJA+A5_FALOJA) == If(lIsInMVC,oGrid:GetValue("A5_LOJA")+oGrid:GetValue("A5_FALOJA"),M->A5_LOJA+M->A5_FALOJA)
                		lRet := .F.
                    	Exit
            		EndIf   
					SA5->(DBSkip())
				End
				If !lRet
					SA5->(DbSetOrder(1))
					SA5->(DbGoTo(nRec))
					Help(" ",1,"A060EXISTE")
				EndIf
			Else
				SA5->(DbSetOrder(1))
				SA5->(DbGoTo(nRec))
				Help(" ",1,"A060EXISTE")
				lRet := .F.
			EndIf
		EndIf
	ElseIf If (lIsInMVC, !oGrid:IsInserted(), .T.) .And. SA5->A5_FABR # If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR) ;
			.And. !Empty(SA5->A5_FABR)
		Help(" ",1,"A060FABREX")
		lRet := .F.
	EndIf
	SA5->(DbSetOrder(1))
	SA5->(DbGoTo(nRec))
Else
	If cNomeCampo == 'A5_FORNECE' .OR. cNomeCampo == 'A5_LOJA' .OR. cNomeCampo == 'A5_PRODUTO'.OR. cNomeCampo == 'A5_REFGRD'
		If cNomeCampo=='A5_REFGRD'
			cProdRef := &(ReadVar())
			If !Empty(cProdRef)
				If lGrade // utilizao de grade ativa
					lReferencia := MatGrdPrrf(@cProdRef)
					If !lReferencia //
						Help(" ",1,"REFGRADE")
						lRet :=.F.
					Else
						If nModulo # 17 .and. nModulo # 29  // SIGAEIC
							If lIsInMVC
								If oField==NIL
									lRet := (ExistChav("SA5",oGrid:GetValue("A5_FORNECE")+oGrid:GetValue("A5_LOJA")+oGrid:GetValue("A5_REFGRD"),9) .And. A060ProFor())
								Else
									lRet := (ExistChav("SA5",oGrid:GetValue("A5_FORNECE")+oGrid:GetValue("A5_LOJA")+oField:GetValue("A5_REFGRD"),9) .And. A060ProFor())
								EndIf
							Else
								lRet := (ExistChav("SA5",M->A5_FORNECE+M->A5_LOJA+M->A5_REFGRD,9) .And. A060ProFor())
							EndIf
						EndIf            
					Endif
				Else
					Help(" ",1,"NOGRADE")  
					If lIsInMVC
						If oField==NIL
							// faz nada
						Else
							oField:LoadValue("A5_REFGRD",SPACE(14))
						Endif
					Else
						M->A5_REFGRD := SPACE(14)
					EndIf
					lRet := .F.
				EndIf
			EndIf
		EndIf
		
		If nModulo # 17 .and. nModulo # 29 
			//Ŀ
			// Tratamento quando no modulo SIGACOM                          
			//
			
			
			If cNomeCampo =='A5_PRODUTO'  
				SA5->(DbSetOrder(2))
				If lIsInMVC
					If COLPRDAMA("SA5", getCodProd(oModel), oGrid:GetValue("A5_FORNECE"), oGrid:GetValue("A5_LOJA"), oGrid:GetValue("A5_CODPRF"))
						Help('',1,'MATA060PRDJE', , ,1,0)
						lRet := .F.
					EndIf 	
				Else
					cChave := xFilial("SA5")+M->A5_PRODUTO+M->A5_FORNECE+M->A5_LOJA
				EndIf	
				
				If !lIsInMVC .And. SA5->(dbSeek(cChave))
					Help('',1,'MATA060PRDJE', , ,1,0)
					lRet := .F.														  
				EndIf
		
			ElseIf cNomeCampo == 'A5_REFGRD'
				SA5->(DbSetOrder(10))
				If lIsInMVC
					If oField == NIL
						cChave := oGrid:GetValue("A5_REFGRD")
					Else
						cChave := oField:GetValue("A5_REFGRD")
					EndIf
				Else
					cChave := M->A5_REFGRD
				EndIf
				If SA5->(DbSeek(xFilial("SA5")+cChave))
					lRet := .F.
					Help('',1,'MATA060REFJE')
				EndIf
			EndIf	
			
           If lGrade
				If lRet				
				    If !cNomeCampo =='A5_PRODUTO' 
						SA5->(DbSetOrder(11))
						If StrTran(SA5->(IndexKey())," ","") == "A5_FILIAL+A5_REFGRD+A5_FABR+A5_FALOJA+A5_FORNECE+A5_LOJA"
							If lIsInMVC
								If oField == NIL
									cSeekSA5 := xFilial("SA5")+oGrid:GetValue("A5_REFGRD")+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FALOJA")+oGrid:GetValue("A5_FORNECE")
								Else
									cSeekSA5 := xFilial("SA5")+oField:GetValue("A5_REFGRD")+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FALOJA")+oGrid:GetValue("A5_FORNECE")
								EndIf
							Else
								cSeekSA5 := xFilial("SA5")+M->A5_REFGRD+M->A5_FABR+M->A5_FALOJA+M->A5_FORNECE
							EndIf
						Else
							If lIsInMVC
								If oField == NIL
									cSeekSA5 := xFilial("SA5")+oGrid:GetValue("A5_REFGRD")+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FORNECE")
								Else
									cSeekSA5 := xFilial("SA5")+oField:GetValue("A5_REFGRD")+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FORNECE")
								EndIf
							Else
								cSeekSA5 := xFilial("SA5")+M->A5_REFGRD+M->A5_FABR+M->A5_FORNECE
							EndIf
						EndIf
						
						If SA5->(DbSeek( cSeekSA5 ) ) .AND. nRec#SA5->(RecNo()).And. !Empty(SA5->A5_REFGRD)
							SA5->(DbSetOrder(1))
							SA5->(DbGoTo(nRec))
							Help(" ",1,"A060EXISTE")
							lRet := .F.
						EndIf
				    Else                    
					    If !Empty(If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO))
					    	If lIsInMVC
						    	lRet := (ExistChav("SA5",oGrid:GetValue("A5_FORNECE")+oGrid:GetValue("A5_LOJA")+getCodProd(oModel)) .And. A060ProFor())
						    Else
							    lRet := (ExistChav("SA5",M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO) .And. A060ProFor())
						    EndIf
						EndIf
				    EndIf 

				EndIf
            Else        
            	If lRet
					If !Empty(If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO))
						If lPrForFab
							If !lIsInMVC
								lRet := (ExistChav("SA5",M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO) .And. A060ProFor())
							EndIf
						Else
							SA5->(dbsetorder(1))
							If lIsInMVC
								SA5->(dbseek(xFilial("SA5")+oGrid:GetValue("A5_FORNECE")+oGrid:GetValue("A5_LOJA")+getCodProd(oModel)))
							Else
								SA5->(dbseek(xFilial("SA5")+M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO))
							EndIf
							While SA5->(A5_FILIAL+A5_FORNECE+A5_LOJA+A5_PRODUTO) = xFilial("SA5")+If(lIsInMVC,;
									oGrid:GetValue("A5_FORNECE")+oGrid:GetValue("A5_LOJA")+getCodProd(oModel),;
									M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO)
							    If Empty(SA5->A5_FABR)
							    	If !lIsInMVC
			   							lRet := ExistChav("SA5",M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO) .And. A060ProFor()
			   						EndIf
									Exit
								Endif
								SA5->(dbSkip())
							EndDo
						EndIf
					EndIf
				EndIf					
            EndIf
		Else
			//Ŀ
			// Tratamento quando nos modulos Average SIGAEIC                
			//
			If cNomeCampo == 'A5_FORNECE' .Or. cNomeCampo == 'A5_LOJA'
				If !SA2->(dbSeek(xFilial("SA2")+If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE)+If(EicLoja() .And. !EICEmptyLJ("M", "A5_LOJA"), EICRetLoja("M", "A5_LOJA"), "")))
					Help(" ",1,"REGNOIS")
					lRet := .F.
				ElseIf LEFT(SA2->A2_ID_FBFN,1) = '1'
					Help(" ",1,"A060FORNNO")
					lRet := .F.
				EndIf
			EndIf
			If lRet
			    If !Empty(If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR))
					//Ŀ
					// Verifica a chave para pesquisa                               
					//
					SA5->(DbSetOrder(3))
					If StrTran(SA5->(IndexKey())," ","") == "A5_FILIAL+A5_PRODUTO+A5_FABR+A5_FALOJA+A5_FORNECE+A5_LOJA"
						If lIsInMVC
							cSeekSA5 := xFilial("SA5")+getCodProd(oModel)+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FALOJA")+oGrid:GetValue("A5_FORNECE")
						Else
							cSeekSA5 := xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FALOJA+M->A5_FORNECE
						EndIf						
					Else
						If lIsInMVC
							cSeekSA5 := xFilial("SA5")+getCodProd(oModel)+oGrid:GetValue("A5_FABR")+oGrid:GetValue("A5_FORNECE")
						Else
							cSeekSA5 := xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FORNECE
						EndIf
					EndIf
					
					If SA5->(dbSeek(cSeekSA5)) .And. EicLoja()
		            	While SA5->(!Eof()) .And. SA5->A5_FILIAL  == xFilial("SA5")						.And.;
           	    			  SA5->A5_PRODUTO == If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)	.And.;
	            		      SA5->A5_FABR    == If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR) 		.And.;
       		    	    	  SA5->A5_FORNECE == If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE)
							  SA5->(dbSkip())
						End
					EndIf
			    EndIf
			EndIf
			SA5->(DbSetOrder(1))
			SA5->(DbGoTo(nRec))
		EndIf
	EndIf
EndIf


/**************
 Mdulos Trade-Easy
 **********************/
If IsComex() .And. (cNomeCampo == "A5_FALOJA" .Or. cNomeCampo == "A5_LOJA")
	If cNomeCampo == 'A5_FALOJA'
		cCampoNRed:= "A5_FABRRED"
		If !SA2->(DBSeek(xFilial() +If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR) + If(!EICEmptyLJ("M", "A5_FALOJA"), EICRetLoja("M", "A5_FALOJA"), "")))
			Help(" ",1,"REGNOIS")
			lRet := .F.  
		ElseIf Left(SA2->A2_ID_FBFN, 1) == '2'
			Help(" ",1,"A060FABRNO")
			lRet := .F.
		EndIf
	Else
		cCampoNRed:= "A5_NOMERED"
	EndIf

	If lRet
		If lIsInMVC
			oGrid:LoadValue(cCampoNRed, SA2->A2_NREDUZ)
		Else
			M->&(cCampoNRed):= SA2->A2_NREDUZ
		EndIf
	EndIf
EndIf
/////////////////////

//Ŀ
// Verifica se os dados referentes ao RIAI estao preenchidos    
//
If lRet .And. !(	(Empty(If(lIsInMVC,oGrid:GetValue("A5_RIAI"),M->A5_RIAI)) .And.;
					Empty(If(lIsInMVC,oGrid:GetValue("A5_DTRIAI"),M->A5_DTRIAI)) .And.;
					Empty(If(lIsInMVC,oGrid:GetValue("A5_VALRIAI"),M->A5_VALRIAI))) .Or.;
					(!Empty(If(lIsInMVC,oGrid:GetValue("A5_RIAI"),M->A5_RIAI)) .And.;
					!Empty(If(lIsInMVC,oGrid:GetValue("A5_DTRIAI"),M->A5_DTRIAI)) .And.;
					!Empty(If(lIsInMVC,oGrid:GetValue("A5_VALRIAI"),M->A5_VALRIAI))))
	Help(" ",1,"A100RIAOBR")	// Data do RIAI e de sua validade sao obrigatorias
	lRet := .F.
EndIf

//Ŀ
//Tratamento para e-Commerce      
//
If lRet .And. lECommerce
	//Ŀ
	// Nao permitir alteracao do codigo do produto para produto ecommerce
	//
	If ALTERA .And. cNomeCampo =='A5_PRODUTO' .And. SA5->A5_PRODUTO # If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)
	    If !Empty(Posicione("SB5",1,xFilial("SB5")+SA5->A5_PRODUTO,"B5_ECFLAG"))
	        Help("",1,'HELP',,STR0022,1,0)  //"No pode alterar o produto desta amarrao pois  um produto do e-commerce!"
	        If lIsInMVC
	        	oField:LoadValue("A5_PRODUTO",SA5->A5_PRODUTO)
	        Else
	        	M->A5_PRODUTO := SA5->A5_PRODUTO
	        EndIf
	        lRet := .F.
	    EndIf
	EndIf
	//Ŀ
	// Nao permitir mais de uma amarracao e-commerce para fornecedor diferente
	//
	If cNomeCampo == 'A5_ECFLAG' .And.	!Empty(If(lIsInMVC,oGrid:GetValue("A5_ECFLAG"),M->A5_ECFLAG)) .And.;
										!Empty(If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)) .And.;
										!Empty(If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
        SA5->(dbSetOrder(2))  //A5_FILIAL+A5_PRODUTO+A5_FORNECE+A5_LOJA
        SA5->(dbSeek(xFilial("SA5")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
        While SA5->(!EOF() .And. A5_FILIAL+A5_PRODUTO == xFilial("SA5")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO))
            If SA5->A5_FORNECE # If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE) .And. !Empty(SA5->A5_ECFLAG)
                Help(,,'HELP',,STR0023 + SA5->A5_FORNECE,1,0)  //"Amarracao e-commerce j configurado com o fornecedor: "
                lRet := .F.
                Exit
            EndIf
                
        	SA5->(dbSkip())
        End
		SA5->(DbSetOrder(1))
		SA5->(DbGoTo(nRec))
	EndIf
EndIf

//Ŀ
// Execblock MT060VLD aps valid.da Incluso - O Ret.pode ser Lgico 
//
If !lIsInMVC
	If ExistTemplate('MT060VLD')
		lRet := If(ValType(lRetPE := ExecTemplate('MT060VLD', .F., .F.))=='L', lRetPE, lRet)
	EndIf
	If ExistBlock('MT060VLD')
		lRet := If(ValType(lRetPE := ExecBlock('MT060VLD', .F., .F.))=='L', lRetPE, lRet)
	EndIf
EndIf

SA5->(RestArea(aAreaSA5))
Return lRet

/*

Ŀ
Funo    A060FABR   Autor  Julio C.Guerato        Data  25/01/09 
Ĵ
Descrio  Valida se o Campo Fabricante est preenchido               
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function A060FABR()
Local lRet := .T.

//Ŀ
// Efetuada consistncia a fim de verificar se o Fabricante est sendo digitado  |
// quando o cadastro for efetuado atravs do SigaEIC. Quando est posicionado em |
// em outro campo por exemplo, a validao passa como verdadeira e sem rotina    |		 
// abaixo gera error log.													      |		 
//
If nModulo # 17 .and. nModulo # 29  // SIGAEIC
ElseIF !GETMV("MV_EIC_EAI",,.F.)
   If Empty(M->A5_FABR) // AWF - 29/04/2014 - Nao validar quando vir da integrcao da Logix
      Help(" ",1,"A060FABR")
      lRet := .F.
  EndIf
EndIf
Return lRet

/*

Ŀ
Funo    MA060Delet Autor  Jorge Queiroz          Data           
Ĵ
Descrio  Programa de atualizacao de Amarracao Produto x Fornecedor  
Ĵ
Sintaxe e  Void MA060Delet(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function MA060Delet(cAlias,nReg,nOpc)

Local nOpcA	:= 0
Local lRet 	:= .T.
Local aArea	:= GetArea()
//Ŀ
//Tratamento para e-Commerce      
//
Local lECommerce := SuperGetMV("MV_LJECOMM",.F.,.F.)

Begin Transaction 

	nOpcA := AxDeleta(cAlias,nReg,nOpc,,,,,aRotAuto)
	lRet:= (lRet .AND. nOpcA == 2)

	If lRet .AND. GetMv("MV_EASY") = "S" 
		lRet := A060DELE()
	EndIf		         
	
	//Ŀ
	// Verifica as amarracoes referentes ao Quality	  			      
	//
	If lRet 
		SB1->(dbSetOrder(1))
		If SB1->(dbSeek(xFilial("SB1")+SA5->A5_PRODUTO))
			If RetFldProd(SB1->B1_COD,"B1_TIPOCQ") == 'Q'	
			   //verifica se existem entradas cadastradas	
			   QEK->(dbSetOrder(1))
			   If QEK->(dbSeek(xFilial("QEK")+SA5->A5_FORNECE+SA5->A5_LOJA+SA5->A5_PRODUTO))
				   Help(" ",1,"QEXISTENTR")
				   lRet := .F.			
			   EndIf
		
			   //Verifica se existem Planos de Amostragens por Ensaios associados ao Fornecedor			   
			   If lRet
				   QF4->(dbSetOrder(1)) 
				   If QF4->(dbSeek(xFilial("QF4")+SA5->A5_FORNECE+SA5->A5_LOJA+SA5->A5_PRODUTO))
				   		Help(" ",1,"QEXISTPLAM") 
			  			lRet := .F.			
				   EndIf
				EndIf				   
			EndIf
			If lRet .AND. SB1->B1_MONO=='S' .AND. (SB1->B1_PROC == SA5->A5_FORNECE)
			   Help(" ",1,"EXISTFDC")
			   lRet := .F.
			EndIf	
		EndIf
	EndIf
	
	//Ŀ
	// Limpa o campo para a exportacao da exclusao para o e-commerce.    
	//
	If lRet .And. lECommerce .And. ColumnPos( "A5_ECDTEX" )
	    RecLock("SA5",.F.)
	    SA5->A5_ECDTEX := " "
	    MsUnLock()
	EndIf
	
	If lRet .AND. ExistBlock("MT060DEL") 
		nOpcA := ExecBlock("MT060DEL",.F.,.F.,nOpcA)
		If ValType(nOpcA) == "N" .AND. nOpcA == 1
			lRet := .F.
		EndIf
	EndIf
	
    If !lRet
    	DisarmTransaction()
    EndIf	
    
End Transaction

//Ŀ
// Aciona integrao via mensagem nica          				
//	
FwIntegDef( 'MATA060' )

RestArea(aArea)
Return

/*

Ŀ
Funo    A060ProFor Autor  Jose Lucas             Data  22/06/93 
Ĵ
Descrio  Exibir e inicializar a Descricao do Produto/Fornecedor.    
Ĵ
Sintaxe e  A060ProFor()                                               
Ĵ
 Uso       A5_PRODUTO,A5_FORNECE                                      
ٱ


*/

/*
Function A060ProFor()
Local lRet		:=.T.
 Local nReg		:= 0
Local nNomeFor	:= 0
Local oModel	:= FWModelActive()
Local lIsInMVC	:= IsInMVC()
Local oField	:= If(lIsInMVC,oModel:GetModel("MdFieldSA5"),NIL)
Local oGrid		:= If(lIsInMVC,oModel:GetModel("MdGridSA5"),NIL)
Local cCampo 	:= ReadVar()
Local cConteudo := If(lIsInMVC,oGrid:GetValue(substr(cCampo,4)),&(ReadVar()))
*/
Function A060ProFor()
Local lRet		:=.T.
Local nReg		:= 0
Local cCampo 	:= ReadVar()
Local cConteudo := &(ReadVar())
Local nNomeProd := 0
Local nNomeFor	:= 0
Local oModel	:= FWModelActive()
Local lIsInMVC	:= IsInMVC()
Local oField	:= If(lIsInMVC,oModel:GetModel("MdFieldSA5"),NIL)
Local oGrid		:= If(lIsInMVC,oModel:GetModel("MdGridSA5"),NIL)
Local lFoundSB1	:= .F.


If Alltrim(cCampo) == "M->A5_PRODUTO"
	dbSelectArea("SB1")
	dbSetOrder(1) 				// Forca ordem principal
	If Altera
		dbSeek(cFilial+SA5->A5_PRODUTO)
		If (B1_MONO=="S") .and. (B1_PROC == If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
			Help("",1,"A06003")
			lRet:=.F.
		Endif
	EndIf
	dbSeek(cFilial+cConteudo)
	lFoundSB1 := SB1->(Found())

	//Ŀ
	// Verifica se o Produto tem Forn. de Dominio do Canal.                 
	//
	If lRet .And. (B1_MONO == "S") .And. (B1_PROC != If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
		Help(" ",1,"A06002")
		lRet:=.F.
	EndIf
	
	//Ŀ
	// Verifica se o Produto esta cadastrado nas Especificacoes do SIGAQIE  
	//	
	If lRet .And. RetFldProd(SB1->B1_COD,"B1_TIPOCQ") == "Q" // Quality Celerina
		lRet:= A060VldQIE()
	EndIf	
	
	If lRet .And. lFoundSB1  
		If !Altera
			If lIsInMVC
				oField:LoadValue("A5_NOMPROD",SB1->B1_DESC)
			Else
				M->A5_NOMPROD := SB1->B1_DESC
				nNomeProd := Ascan(aGets,{ |x| Subs(x,9,10) == "A5_NOMPROD" } )
				If nNomeProd > 0
					aTela[Val(Subs(aGets[nNomeProd],1,2))][Val(Subs(aGets[nNomeProd],3,1))*2] := M->A5_NOMPROD
				EndIf
			EndIf
		EndIf
	EndIf

ElseIf Alltrim(cCampo) == "M->A5_FORNECE"
	//Ŀ
	// Faz a consistencia entre o Produto e o Fornecedo de Dominio de canal
	//
	If !Empty(If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)) .And. !Empty(cConteudo)
		SB1->(dbSeek(xFilial("SB1")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
		If (SB1->B1_MONO=="S") .And. (SB1->B1_PROC != cConteudo) 
			Help(" ",1,"A06002")
			lRet:=.F.
		Endif
	Endif
	If lRet
		dbSelectArea("SA2")
		If dbSeek(cFilial+cConteudo+SA2->A2_LOJA)
			If lIsInMVC
				oGrid:LoadValue("A5_NOMEFOR",SA2->A2_NOME)
			Else
				M->A5_NOMEFOR := SA2->A2_NOME
				nNomeFor := Ascan(aGets,{ |x| Subs(x,9,10) == "A5_NOMEFOR" } )
				If nNomeFor > 0
					aTela[Val(Subs(aGets[nNomeFor],1,2))][Val(Subs(aGets[nNomeFor],3,1))*2] := M->A5_NOMEFOR
				EndIf
			EndIf
		EndIf	
	EndIf
ElseIf Alltrim(cCampo) == "M->A5_LOJA"	
	//Ŀ
	// Faz a consistencia entre o Produto e o Fornecedo de Dominio de canal
	//
	If !Empty(If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)) .And. !Empty(cConteudo)
		SB1->(dbSeek(xFilial("SB1")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
		If (SB1->B1_MONO=="S") .And. (SB1->B1_PROC != cConteudo) 
			Help(" ",1,"A06002")
			lRet:=.F.
		EndIf
	EndIf
	If lRet
		dbSelectArea("SA2")
		If dbSeek(cFilial+SA2->A2_COD+cConteudo)
			If lIsInMVC
				oGrid:LoadValue("A5_NOMEFOR",SA2->A2_NOME)
			Else
				M->A5_NOMEFOR := SA2->A2_NOME			
				nNomeFor := Ascan(aGets,{ |x| Subs(x,9,10) == "A5_NOMEFOR" } )
				If nNomeFor > 0
					aTela[Val(Subs(aGets[nNomeFor],1,2))][Val(Subs(aGets[nNomeFor],3,1))*2] := M->A5_NOMEFOR
				EndIf
			EndIf
		EndIf	
	EndIf
ElseIf Alltrim(cCampo) =="M->A5_REFGRD"
	
	dbSelectArea("SB1")
	dbSetOrder(1) 				// Forca ordem principal
	If Altera
		dbSeek(xFilial("SB1")+SA5->A5_REFGRD,.T.)
		While !Eof().And.xFilial("SB1")==SB1->B1_FILIAL.And.Alltrim(SA5->A5_REFGRD)==Left(SB1->B1_COD,LEN(Alltrim(SA5->A5_REFGRD)))
			If (B1_MONO=="S") .and. (B1_PROC == If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
				Help("",1,"A06003")
				Return .F.
			EndIf
			SB1->(Dbskip())
		EndDo
		
	EndIf
	dbSeek(xFilial()+cConteudo,.T.)
	While !Eof().And.xFilial()==SB1->B1_FILIAL.And.Alltrim(cConteudo)==Left(SB1->B1_COD,LEN(Alltrim(cConteudo)))
		
		//Ŀ
		// Verifica se o Produto tem Forn. de Dominio do Canal.                 
		//
		If (B1_MONO == "S") .And. (B1_PROC != If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
			Help(" ",1,"A06004",,SB1->B1_COD,1,11)
			Return .F.
		EndIf
		
		//Ŀ
		// Verifica se o Produto esta cadastrado nas Especificacoes do SIGAQIE  
		//
		If RetFldProd(SB1->B1_COD,"B1_TIPOCQ") == "Q" // Quality Celerina
			lRet:= A060VldQIE(.T.)
		EndIf
		SB1->(Dbskip())
	EndDo
	
	If lRet 
		If lIsInMVC
			If oField == NIL
				oGrid:GetValue("A5_DESREF") := DescPrRf(cConteudo)
			Else
				oField:GetValue("A5_DESREF") := DescPrRf(cConteudo)
			EndIf
		Else
			M->A5_DESREF := DescPrRf(cConteudo)
			nNomeProd := Ascan(aGets,{ |x| Subs(x,9,10) == "A5_DESREF" } )
			If nNomeProd > 0
				aTela[Val(Subs(aGets[nNomeProd],1,2))][Val(Subs(aGets[nNomeProd],3,1))*2] := M->A5_DESREF
			EndIf
		EndIf
	EndIf
EndIf
dbSelectArea("SA5")
Return lRet

/*


Ŀ
Funo    A060F3Pr   Autor  Vera Lucia S. Simoes   Data 19/08/1998
Ĵ
Descrio Monta consulta Produtos a partir do SB1 ou do QE6           
Ĵ
ParametrosNenhum                                                      
Ĵ
Retorno   Nil                                                         
Ĵ
Uso       Mata060.PRX (Amarracao ProdXForn) - Chamada Cons PRO (SXB)  
ٱ


*/
Function A060F3Pr()
Local lRet	:= .T.
Local lTpPesq := GetNewPar("MV_QPESQE6",.T.)

//Ŀ
// Se o modulo for o SIGAQIE, usa o QE6, senao usa o SB1  
//
If nModulo == 21 .and. lTpPesq  // SigaQie
	lRet := ConPad1(,,,"QE6")
	// Posiciona no SB1, porque o retorno da consulta PRO  o campo B1_COD
	SB1->(dbSeek(xFilial("SB1")+QE6->QE6_PRODUT))
Else
	lRet := ConPad1(,,,"SB1")
EndIf
Return lRet

/*


Ŀ
Funo    A060DELE   Autor  Average                Data 28/07/2000
Ĵ
Descrio Verifica a existentia do produtoXfornecedor no SW3          
Ĵ
ParametrosNenhum                                                      
Ĵ
Retorno   True ou False                                               
Ĵ
Uso       Mata060.PRX (Amarracao ProdXForn)                           
ٱ


*/
Function A060dele()

Local nTotalReg := 0

SetPrvt("COLDORDER,LACHOU,")
cOldOrder:=SW3->(INDEXORD())
lAchou   :=.F.

//Verifica vinculao do mdulo SIGAEIC
SW3->(dbSetOrder(3))
If SW3->(dbSeek(XFILIAL("SW3")+SA5->A5_PRODUTO))

   Do While SW3->(!EOF()) .AND. SW3->W3_FILIAL == XFILIAL("SW3") .AND.;
            SW3->W3_COD_I == SA5->A5_PRODUTO

      If SW3->W3_COD_I == SA5->A5_PRODUTO .AND.;
         SW3->W3_FORN  == SA5->A5_FORNECE .AND.;
         SW3->W3_FABR  == SA5->A5_FABR
         lAchou:=.T.
         Exit
      EndIf

      SW3->(dbSkip())

   EndDo
   
EndIf

//Verifica vinculao do mdulo SIGAEEC
If Select("TOTALREG") > 0
   TOTALREG->( dbCloseArea() )
Endif

BeginSql Alias "TOTALREG"
      
   SELECT COUNT(*) AS TOTAL
   FROM %table:EE8% 
   WHERE  EE8_COD_I  = %Exp:SA5->A5_PRODUTO% AND 
          EE8_FORN   = %Exp:SA5->A5_FORNECE% AND
          EE8_FABR   = %Exp:SA5->A5_FABR% AND
          D_E_L_E_T_ <> '*'

EndSql

nTotalReg := TOTALREG->TOTAL

If nTotalReg > 0
   lAchou := .T.
EndIf

TOTALREG->( dbCloseArea() )

DBSELECTAREA("EE8")

If lAchou
   Help(" ",1,"E_DELFOFA2")
EndIf

SW3->(ORDSETFOCUS(cOldOrder))

Return(!lAchou) 
              
/*


Ŀ
Funo    A060Chave  Autor  Paulo Emidio de Barros Data 13/05/2002
Ĵ
Descrio  Gera a chave de ligacao utilizada na Amarracao Produto x   
			  Fornecedor.						 						  
Ĵ
ParametrosNenhum                                                      
Ĵ
Retorno   Nulo			                                              
Ĵ
Uso       MATA060.PRX (Amarracao Produto x Fornecedor)                
ٱ


*/
Function A060Chave()
Local aAreaSA5 := SA5->(GetArea()) 
Local nSaveSx8 := GetSx8Len()

//Ŀ
// Gera a chave de ligacao									 
//          
dbSelectArea("SA5")
dbSetOrder(6)
cChave := GetSxeNum("SA5","A5_CHAVE")
While dbSeek(xFilial("SA5")+cChave)
  	While ( GetSx8Len() > nSaveSX8 )
     	ConfirmSX8()
    End
    cChave := GetSxeNum("SA5","A5_CHAVE")
End

SA5->(RestArea(aAreaSA5))
Return(cChave)

/*

Ŀ
Funo    A060VldQIE Autor  Eduardo de Souza       Data  28/03/03 
Ĵ
Descrio  Verifica a existencia do produto no cadastro de especifica-
			  coes / ensaios.          		 						  
Ĵ
Retorno    Logico                                                     
Ĵ
Uso       MATA060.PRX (Amarracao Produto x Fornecedor)                
ٱ

*/
Function A060VldQIE(lReferencia)

Local nTam  	:= 0
Local cMvUtSk25	:= GetMV("MV_QUTSK25")
Local oModel	:= FWModelActive()
Local lIsInMVC	:= IsInMVC()
Local oField	:= If(lIsInMVC,oModel:GetModel("MdFieldSA5"),NIL)
Local oGrid 	:= If(lIsInMVC,oModel:GetModel("MdGridSA5"),NIL)

Default lReferencia :=.F.

If lReferencia
	If !QE6->(dbSeek(xFilial("QE6")+SB1->B1_COD))
		Help(" ",1,"A06005",,SB1->B1_COD,1,11)	// "Produto no cadastrado pelo SIGAQIE"
		Return(.F.)
	EndIf
	
	If !QE7->(dbSeek(xFilial("QE7")+SB1->B1_COD))
		If !QE8->(dbSeek(xFilial("QE8")+SB1->B1_COD))
			Help(" ",1,"A06006",,SB1->B1_COD,1,11)	// "Produto no tem Ensaios associados."
			Return(.F.)
		EndIf
	EndIf

Else
	If !QE6->(dbSeek(xFilial("QE6")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
		Help(" ",1,"Q_060PRNAO")	// "Produto no cadastrado pelo SIGAQIE"
		Return(.F.)
	EndIf

	If !QE7->(dbSeek(xFilial("QE7")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
		If !QE8->(dbSeek(xFilial("QE8")+If(lIsInMVC,getCodProd(oModel),M->A5_PRODUTO)))
			Help(" ",1,"Q_060ENNAO")	// "Produto no tem Ensaios associados."
			Return(.F.)
		EndIf
	EndIf
Endif
//Ŀ
// Verifica se o Skip-Lote foi especificado                     
//
QEG->(dbSeek(xFilial("QEG")+If(lIsInMVC,oGrid:GetValue("A5_SITU"),M->A5_SITU)))
If QEG->QEG_CATEG $ "1.2"
	If Empty(If(lIsInMVC,oGrid:GetValue("A5_SKPLOT"),M->A5_SKPLOT))
		Help(" ",1,"A100SKPOBR")	// Skip-Lote  obrigatorio
		Return(.F.)
	EndIf
Else
	//Limpa o Skip-Lote
	nTam := TamSX3("A5_SKPLOT")[1]
	If lIsInMVC
		oGrid:LoadValue("A5_SKPLOT",Space(nTam))
	Else
		M->A5_SKPLOT := Space(nTam)
	EndIf
EndIf

If !Empty(If(lIsInMVC,oGrid:GetValue("A5_SKPLOT"),M->A5_SKPLOT))
	//Ŀ
	// Verifica se a categoria Situacao  Skip-Lote 25%             
	//
	If QEG->QEG_CATEG == "2"
		QEF->(dbSeek(xFilial("QEF")+If(lIsInMVC,oField:GetValue("A5_SKPLOT"),M->A5_SKPLOT)))
		If QEF->QEF_UNSKLT == "E"	// Skip-Lote por entregas
			If cMvUtSk25 == "S"
				If QEF->QEF_QTDE > 4
					Help(" ",1,"A100SKLINV")	// "Skip-Lote no permitido para a Situao"
					Return(.F.)
				EndIf
			EndIf
		ElseIf QEF->QEF_UNSKLT <> "L"	// Skip-Lote por Documento Entrada
			Help(" ",1,"A100SKLINV")	// "Skip-Lote no permitido para a Situao"
			Return(.F.)
		EndIf
	EndIf
EndIf

Return (.T.)
/*

Ŀ
Funo    A060VldCpo Autor  Marcos V. Ferreira     Data  01/09/05 
Ĵ
Descrio Verifica se campo conteudo do campo A5_FORNECE esta 		  
          cadastrado na tabela SA2.                            		  
Ĵ
Uso       MATA060                                                     
ٱ

*/
Function A060VldCpo()
Local lRet 	   := .T.
Local aAreaAnt := GetArea()
Local aAreaSA2 := SA2->(GetArea())
Local cCampo   := ReadVar()
Local cChave   := ""
Local oModel   := FWModelActive()
Local lIsInMVC := IsInMVC()
Local oGrid	   := If(lIsInMVC,oModel:GetModel("MdGridSA5"),NIL)

If cCampo == "M->A5_FORNECE"
	cChave := &(ReadVar())
	If !Empty(If(lIsInMVC,oGrid:GetValue("A5_LOJA"),M->A5_LOJA))
		cChave += If(lIsInMVC,oGrid:GetValue("A5_LOJA"),M->A5_LOJA)
	Else
		SA2->(dBSetOrder(1))
		If SA2->(dBSeek(xFilial("SA2")+cChave))
		 	If SA2->A2_MSBLQL="1"
		 		While SA2->A2_COD=cChave .And. !SA2->(Eof())
		 			If SA2->A2_MSBLQL<>"1"
		 				M->A5_LOJA := SA2->A2_LOJA
		 				cChave := cChave+M->A5_LOJA
						If lIsInMVC .And. nModulo == 02 .Or. IsComex()
							oGrid:SetValue("A5_LOJA",SA2->A2_LOJA)
						EndIf		 				
		 				Exit
		 			Else
		 				SA2->(dBSkip())
		 			EndIf
		 		EndDo
		 	EndIf
		EndIf
	EndIf
ElseIf cCampo == "M->A5_LOJA" 
	If !Empty(If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE))
		cChave += If(lIsInMVC,oGrid:GetValue("A5_FORNECE"),M->A5_FORNECE) +&(ReadVar())
		SA2->(dBSetOrder(1))
		If SA2->(dBSeek(xFilial("SA2")+cChave))
		 	If SA2->A2_MSBLQL="1"
		 		ShowHelpDlg(STR0025, {STR0026},1,{STR0027},1)     							 		
		 		While SA2->A2_COD=Substr(cChave,1,Len(AllTrim(SA2->A2_COD))) .And. !SA2->(Eof())
		 			If SA2->A2_MSBLQL<>"1"
		 				M->A5_LOJA := SA2->A2_LOJA
		 				cChave := Substr(cChave,1,Len(AllTrim(SA2->A2_COD)))+M->A5_LOJA
						If lIsInMVC .And. nModulo == 02 .Or. IsComex()
							oGrid:SetValue("A5_LOJA",SA2->A2_LOJA)
						EndIf		 				
		 				Exit
		 			Else
		 				SA2->(dBSkip())
		 			EndIf
		 		EndDo
		 	EndIf
		EndIf
	EndIf
ElseIf cCampo == "M->A5_FABR"
	cChave := &(ReadVar())
	If !Empty(If(lIsInMVC,oGrid:GetValue("A5_FALOJA"),M->A5_FALOJA))
		cChave += If(lIsInMVC,oGrid:GetValue("A5_FALOJA"),M->A5_FALOJA)
	Else
		SA2->(dBSetOrder(1))
		If SA2->(dBSeek(xFilial("SA2")+cChave))
		 	If SA2->A2_MSBLQL="1"
		 		While SA2->A2_COD=cChave .And. !SA2->(Eof())
		 			If SA2->A2_MSBLQL<>"1"
		 				M->A5_FALOJA := SA2->A2_LOJA
		 				cChave := cChave+M->A5_FALOJA
						If lIsInMVC .And. nModulo == 02 .Or. IsComex()
							oGrid:SetValue("A5_FALOJA", SA2->A2_LOJA)
						EndIf		 				
		 				Exit
		 			Else
		 				SA2->(dBSkip())
		 			EndIf
		 		EndDo
		 	EndIf
		EndIf
	EndIf
ElseIf cCampo == "M->A5_FALOJA"
	If !Empty(If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR))
		cChave += If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR) +&(ReadVar())
	EndIf
EndIf

lRet := ExistCpo("SA2",cChave)  

/**************
Mdulos Trade-Easy
**********************/
If IsComex()
	If lRet .And. cCampo == "M->A5_FALOJA" .And. SA2->(DBSeek(xFilial() +If(lIsInMVC,oGrid:GetValue("A5_FABR"),M->A5_FABR) + If(lIsInMVC, oGrid:GetValue("A5_FALOJA"), M->A5_FALOJA)))
		If lIsInMVC
			oGrid:LoadValue("A5_FABRRED", SA2->A2_NREDUZ)
		Else
			M->A5_FABRRED:= SA2->A2_NREDUZ
		EndIf
	EndIf
EndIf

RestArea(aAreaSA2)
RestArea(aAreaAnt)
Return lRet

/*


ͻ
Programa  MA060TudokAutor  Patricia D. Aguiar   Data   06/03/07   
͹
Desc.      No permitir a gravao de registro sem referencia de grade
           ou codigo do produto em branco                             
͹
Retorno    .T. verdadeiro ou .F. falso                                
ͼ


*/
Function MA060TudOk()

Local lRet 		:= .T.
Local lHistTab	:= SuperGetMV("MV_HISTTAB", .F., .F.)
Local bCampoSA5	:= { |x| SA5->(Field(x)) }
Local nX			:= 0
Local aOrdSA5		:= SaveOrd({"SA5"})   //TRP - 01/04/2013
Static nRecno		:= 0

If Empty(M->A5_PRODUTO).And. Empty(M->A5_REFGRD)
	Help(" ",1,"A060OBRIGA")
	lRet := .F.
Endif

If Altera 
	If nModulo == 17 .And. EicLoja()
	DbSelectArea("SA5")	
		nRecno := Recno()
		SA5->(DbSetOrder(3))
		If SA5->(MsSeek(xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FORNECE )) .And. SA5->(Recno()) <> nRecno
			lRet := .F.
			Help(" ",1,"A060FORNEC")
		EndIf 
	Else
		DbSelectArea("SA5")	
		nRecno := Recno()
		SA5->(DbSetOrder(1))
		If SA5->(MsSeek(xFilial("SA5")+M->A5_FORNECE+M->A5_LOJA+M->A5_PRODUTO )) .And. SA5->(Recno()) <> nRecno
			lRet := .F.
			Help(" ",1,"A060FORNEC")
		EndIf 
	EndIf     
EndIf    

//TRP - 10/01/2013 - Valida se o registro j existe na base de dados. 
If nModulo == 17  .And. EicLoja() .AND. Inclui
   SA5->(DbSetOrder(3))
   
   If SA5->(DbSeek(xFilial("SA5")+M->A5_PRODUTO+M->A5_FABR+M->A5_FORNECE ))                                                                                                                         
      While SA5->(!Eof()) .And.;
         SA5->A5_FILIAL  == xFilial("SA5") .And.;
         SA5->A5_PRODUTO == M->A5_PRODUTO .And.;
         SA5->A5_FABR    == M->A5_FABR .And.;
         SA5->A5_FORNECE == M->A5_FORNECE

         If M->A5_LOJA == SA5->A5_LOJA .AND. M->A5_FALOJA == SA5->A5_FALOJA
            lRet := .F.
            Help(" ",1,"A060EXISTE")
            Exit
         EndIf   
         SA5->(DBSkip())
      EndDo  

      RestOrd(aOrdSA5,.T.)
   Endif
Endif

//Ŀ
//Carregar o vetor aCpoAltSA5 com os campos que sofreram  
//alguma alteracao. Somente serao gravados na tabela AIF  
//apos a validacao completa da rotina.                    
//
If lRet .And. !Inclui .And. lHistTab
	aCpoAltSA5 := {}   
	DbSelectArea("SA5")
	For nX := 1 to FCount()
		If !(M->&( eVal( bCampoSA5, nX) ) == &( eVal( bCampoSA5, nX) ))
			aAdd( aCpoAltSA5, { eVal( bCampoSA5, nX), &( eVal( bCampoSA5, nX) ) } )
		EndIf
	Next nX
EndIf

Return lRet

/*


ͻ
Programa  APesqRefA5Autor  Patricia D. Aguiar   Data   07/03/07   
͹
Desc.     Pesquisa amarrao Produto x Fornecedor para a referencia   
          de Grade                                                    
ĺ
Parametros ExpC1: Codigo da Referencia de Grade                       
           ExpA2: Array com os fornecedores encontrados pelo cod pro- 
                  to                                                  
͹
Retorno    Array com os forncedores encontrados                       
ͼ


*/
Function APesqRefA5(cProdRef,aFornece)
Local aAreaSA5 	:= SA5->(GetArea())
Local aAux	 	:= {}
Local nExist 	:= 0

Default aFornece   := {}                                     

Dbselectarea("SA5")
Dbsetorder(10)
DbSeek(xFilial("SA5")+cProdRef)
While !eof().And. Xfilial("SA5")== SA5->A5_FILIAL .And. cProdRef == Alltrim(SA5->A5_REFGRD)
	nExist := Ascan(aFornece,{|x| Alltrim(x[1]) == SA5->A5_FORNECE .And. Alltrim(x[2]) ==SA5->A5_LOJA})
	If nExist ==0
		Aadd(aAux,{SA5->A5_FORNECE,SA5->A5_LOJA,SA5->A5_NOMEFOR,SA5->(Recno())})
	Endif
	SA5->(Dbskip())
End

SA5->(RestArea(aAreaSA5))
Return(aAux) 

/*


ͻ
Programa  MATA060   Autor  Rodrigo Toledo       Data   10/06/11   
͹
Desc.      Rotina de validacao da importao de XML de Nota Fiscal    
           Eletronica.                                                
͹
Uso        MATA060                                                    
ͼ


*/
Function MT060VlNFe()
Local aAreaSF4 := SF4->(GetArea()) 
Local lRetorno := .T.

SF4->(DbSetOrder(1))
If SF4->(DbSeek(xFilial("SF4")+M->A5_TESBP)) 
	IF SF4->F4_DUPLIC <> "N" .Or. SF4->F4_PODER3 <> "N" .Or. SF4->F4_TRANFIL <> "2"
		Help(" ",1,"A060TEB")
		lRetorno := .F.
  	EndIf
EndIf

RestArea(aAreaSF4)	
Return lRetorno

/*


ͻ
 Funcao    IntegDef     Autor  Danilo Dias        Data  03/05/2012  
͹
 Desc.     Funcao para processamento de mensagem unica.                 
͹
 Param     cXML - Variavel com conteudo xml para envio/recebimento.     
           nTypeTrans - Tipo de transacao. (Envio/Recebimento)          
           cTypeMessage - Tipo de mensagem. (Business Type, WhoIs, etc) 
͹
 Uso                                                                    
ͼ


*/
Static Function IntegDef( cXML, nTypeTrans, cTypeMessage, cVersion )

Local aRet	:= {}

aRet := MATI060( cXML, nTypeTrans, cTypeMessage, cVersion )

Return aRet

/*/


Ŀ
Programa  MenuDef    Autor  Fabio Alves Silva      Data 03/10/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/ 
Static Function MenuDef() 

Private aRotina := { { STR0003,"AxPesqui"	,0	, 1,0,.F.},;	//"Pesquisar"
                     { STR0004,	"AxVisual"	,0	, 2,0,Nil},;	//"Visualizar"
                     { STR0005, "A060Manut"	,0	, 3,0,Nil},;	//"Incluir"
                     { STR0006, "A060Manut"	,0	, 4,0,Nil},;	//"Alterar"
                     { STR0007, "MA060Delet",0	, 5,3,Nil} }	//"Excluir"

If ExistBlock ("MTA060MNU")
	ExecBlock ("MTA060MNU",.F.,.F.)
EndIf
Return (aRotina)

/*


ͻ
 Funcao    Mt060CodFis  Autor  Rafael Duram Santos Data  11/02/2013 
͹
 Desc.     Retorna o codigo fiscal definido na Amarracao Prod x Forn    
͹
 Param     cProd - Codigo do Produto								    
           cForn - Codigo do Fornecedorn							    
           cLoja - Codigo da Loja do Fornecedor							
͹
 Uso                                                                    
ͼ


*/

Function Mt060CodFis(cProd,cForn,cLoja)
Local cCod 		:= ""
Local aArea		:= GetArea()
Local aAreaSA5	:= SA5->(GetArea())
Local cAltCFis	:= Substr(GetNewPar("MV_ALTCFIS",'00'),1,1)

DbSelectArea('SA5')
SA5->(DbSetOrder(1))

If SA5->(DbSeek(xFilial('SA5')+cForn+cLoja+cProd)) .And. cAltCFis == '1' 
	// Carrega referencia do NCM da amarracao Produto x Fornecedor (SA5)
	If !Empty(SA5->A5_NCMPRF)
		MaFisRef("IT_POSIPI","MT100",SA5->A5_NCMPRF)
	Endif
	cCod := SA5->A5_CODFIS
Endif

RestArea(aAreaSA5)
RestArea(aArea)

Return cCod

/*


ͻ
 Funcao    Mt060VldFis  Autor Rafael Duram Santos Data  14/02/2013  
͹
 Desc.     Valida o campo do codigo fiscal do produto				    
͹
 Param     cCodFis - Codigo Fiscal do Produto						    
͹
 Uso                                                                    
ͼ


*/
Function Mt060VldFis(cCodFis,cForn,cLoja)

Local lRet 		:= .F.
Local nPosProd	:= aScan(aHeader,{|x| AllTrim(x[2]) == "D1_COD"} )
Local cProd		:= aCols[n,nPosProd]

If !Empty(cCodFis) .And. !ExistCpo('SA5',cCodFis+cProd+cForn+cLoja,15)
	Return lRet
Endif

If Empty(MafisScan("IT_PRDFIS",.F.))
	If IsInCallStack("Mt060CodFis").Or.ReadVar()=='M->D1_CODFIS'
		lRet := .T.
	Endif
Else
	If MaFisRef("IT_PRDFIS","MT100",cCodFis) .And. (IsInCallStack("Mt060CodFis") .Or. ReadVar()=='M->D1_CODFIS')
		lRet := .T.
	Endif
Endif

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VMoeUs
Funcao de validacao do campo A5_MOE_US

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VMoeUs()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := IIf(!Empty(FwFldGet('A5_MOE_US')),ExistCpo('SYF',FwFldGet('A5_MOE_US')),.T.) .And. AVMATA060VAL('A5_MOE_US')
Else
	lRet := IIf(!Empty(M->A5_MOE_US),ExistCpo('SYF',M->A5_MOE_US),.T.) .And. AVMATA060VAL('A5_MOE_US')	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060WCotus
Funcao de when do campo A5_VLCOTUS

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060WCotus()
Local lRet := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := !Empty(FwFldGet('A5_MOE_US'))
Else
	lRet := !Empty(M->A5_MOE_US)
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VFLoja
Funcao de validacao do campo A5_FALOJA

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VFLoja()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	If  nModulo == 02
		lRet := (ExistCpo('SA2',FwFldGet('A5_FABR')+FwFldGet('A5_FALOJA')) .And. A060Valid('A5_FALOJA'))
	Else
		lRet := Vazio() .Or. (ExistCpo('SA2',FwFldGet('A5_FABR')+FwFldGet('A5_FALOJA')) .And. A060Valid('A5_FALOJA'))
	EndIf
Else
	If  nModulo == 02
		lRet := (ExistCpo('SA2',FwFldGet('A5_FABR')+FwFldGet('A5_FALOJA')) .And. A060Valid('A5_FALOJA'))
	Else
		lRet := Vazio() .Or. (ExistCpo('SA2',FwFldGet('A5_FABR')+FwFldGet('A5_FALOJA')) .And. A060Valid('A5_FALOJA'))
	EndIf	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VCodTa
Funcao de validacao do campo A5_CODTAB

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VCodTa()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := Vazio() .Or. ExistCpo('AIA',FwFldGet('A5_FORNECE')+FwFldGet('A5_LOJA')+FwFldGet('A5_CODTAB'))
Else
	lRet := Vazio() .Or. ExistCpo('AIA',M->A5_FORNECE+M->A5_LOJA+M->A5_CODTAB)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VIncot
Funcao de validacao do campo A5_INCOTER

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VIncot()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := IIf(!EMPTY(FwFldGet('A5_INCOTER')),ExistCpo('SYJ',FwFldGet('A5_INCOTER')),.T.)
Else
	lRet := IIf(!EMPTY(M->A5_INCOTER),ExistCpo('SYJ',M->A5_INCOTER),.T.)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VNQA1
Funcao de validacao do campo A5_NQA1

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VNQA1()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := ExistCpo('SX5','Q1'+FwFldGet('A5_NQA1'))
Else
	lRet := ExistCpo('SX5','Q1'+M->A5_NQA1)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VNQA2
Funcao de validacao do campo A5_NQA2

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VNQA2()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := ExistCpo('SX5','Q1'+FwFldGet('A5_NQA2'))
Else
	lRet := ExistCpo('SX5','Q1'+M->A5_NQA2)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VCodFi
Funcao de validacao do campo A5_CODFIS

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VCodFi()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := Vazio() .Or. ExistCpo('SB1',FwFldGet('A5_CODFIS'))
Else
	lRet := Vazio() .Or. ExistCpo('SB1',M->A5_CODFIS)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VNCMPF
Funcao de validacao do campo A5_NCMPRF

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VNCMPF()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := Vazio() .Or. ExistCpo('SYD',FwFldGet('A5_NCMPRF'))
Else
	lRet := Vazio() .Or. ExistCpo('SYD',M->A5_NCMPRF)	
EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} A060VTesBP
Funcao de validacao do campo A5_TESBP

@since 02/07/2013
@version P11
@author Cleber Maldonado
*/
//-------------------------------------------------------------------
Function A060VTesBP()
Local lRet   := .T.
Local oModel := FWModelActive()

If IsInMVC()
	lRet := MaAvalTes('E',FwFldGet('A5_TESBP')) .And. MT060VlNFe()
Else
	lRet := MaAvalTes('E',M->A5_TESBP) .And. MT060VlNFe()	
EndIf

Return lRet

/*


ͻ
 Funcao    Mt060Cno	    Autor  Rafael Duram Santos Data  07/10/2013 
͹
 Desc.     Retorna o codigo nacional de obras da Amarracao Prod x Forn   
͹
 Param     cProd - Codigo do Produto								    		
           cForn - Codigo do Fornecedorn							    		
           cLoja - Codigo da Loja do Fornecedor								
͹
 Uso                                                                    
ͼ


*/

Function Mt060Cno(cProd,cForn,cLoja)
Local cCod 		:= ""
Local aArea		:= GetArea()
Local aAreaSA5		:= SA5->(GetArea())


DbSelectArea('SA5')
SA5->(DbSetOrder(1))

If SA5->(MsSeek(xFilial('SA5')+cForn+cLoja+cProd)) 
	// Carrega referencia do CNO da amarracao Produto x Fornecedor (SA5)
	cCod := SA5->A5_CNO
Endif

RestArea(aAreaSA5)
RestArea(aArea)

Return cCod

/*/{Protheus.doc} IsInMVC
Verifica se tem a rotina principal em execuo est escrita em MVC.
A rotina pode ser a MATA061 (Prod x Fornecedor) ou o MATA010(Produto), pois o MATA010
pode ser um grid com a relao de fornecedores do produto. 

@type metodo
 
@author Juliane Venteu
@since 14/03/2017
@version P12.1.17
 
/*/
Static Function IsInMVC()
Local oModel	  := FWModelActive()
Local lIsInMVC	  := ValType(oModel) <> "U" .And. (oModel:GetId() == "MATA061" .Or. oModel:GetID() == "ITEM" .And. 'SA5'$SuperGetMV('MV_CADPROD'))  
Return lIsInMVC

/*/{Protheus.doc} getCodProd
Essa funo  um workround para tratar as validaes do fonte MATA060.
As validaes podem ser executadas em trs situaes:
	- Dentro da rotina MATA060
	- Dentro da rotina MATA061 (MVC): SA5 relacionado com SA5
	- Dentro da rotina MATA010 (MVC): SB1 relacionado com SA5

Quando a rotina em execuo  o MATA010 o cabealho da rotina  a tabela SB1 e no a tabela SA5, por esse motivo
se for feito um "getvalue('A5_PRODUTO')" vai gerar erro, j que esse campo no est carregado no modelo.
Por esse motivo a funo getCodProd foi criada, quando  o MATA010 o codigo do produto  obtido do campo B1_COD e 
quando  MATA061, o codigo  obtido do A5_PRODUTO.

Qualquer alterao que envolva manipular o A5_PRODUTO deve ser testada tambm no MATA010.

@type metodo
 
@author Juliane Venteu
@since 14/03/2017
@version P12.1.17
 
/*/
Static Function getCodProd(oModel)
Local cCodigo := ""

	If oModel:GetID() == "ITEM"
		cCodigo := oModel:GetValue("SB1MASTER","B1_COD")
	Else
		cCodigo := oModel:GetValue("MdFieldSA5", "A5_PRODUTO")
	EndIf
	
Return cCodigo

//----------------------------------------------------------------------
/*/{Protheus.doc} A060CclVda()
Informa a validade de descontinuao da rotina
@author Romulo Batista
@since 11/04/2019
@version 1.0
@return .T.
/*/
//----------------------------------------------------------------------
Function A060CclVda()

Local cRelease := GetRpoRelease()
Local cRelCut  := "12.1.025"
Local dData	   := DDataBase
Local dDtCut   := cTod("01/07/2019")
Local lRet     := .T.

//Aviso Ciclo de Vida
Help( NIL, NIL, STR0031, NIL, + Chr(13) + Chr(10) + STR0032, 1, 0,NIL, NIL, NIL, NIL, NIL,;
 	{+ Chr(13) + Chr(10) + STR0033 + Chr(13) + Chr(10) + STR0035 + Chr(13) + Chr(10) + Chr(13) + Chr(10) +  STR0034})
 	
//Verificao Release e data de corte	
If cRelease >= cRelCut .And. dData >= dDtCut
	lRet := .F.
ElseIf cRelease < cRelCut .And. dData >= dDtCut
	lRet := .F.
ElseIf cRelease >= cRelCut .And. dData < dDtCut
	lRet := .F.
EndIf	

Return lRet


//----------------------------------------------------------------------
/*/{Protheus.doc} IsComex()
Verifica se  algum dos mdulos de comrcio exterior
@author Wilsimar Fabricio da Silva
@since 03/09/2019
@version 1.0
@return lgico
/*/
//----------------------------------------------------------------------
Static Function IsComex()
Return nModulo == 17 .Or. nModulo == 29 .Or.nModulo == 30 .Or. nModulo == 50 .Or. nModulo == 85
