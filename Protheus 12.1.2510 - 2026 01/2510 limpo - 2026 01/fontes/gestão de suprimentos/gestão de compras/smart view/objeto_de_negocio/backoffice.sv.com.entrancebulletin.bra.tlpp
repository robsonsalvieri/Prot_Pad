#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.entrancebulletin.ch" 
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SF1", customTables="ALL", name="Boletim de Entrada - Localizado", country="BRA")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} entrancebulletinReportsBusinessObjectsBRA
Classe para criação do Objeto de Negócio Localizado
Boletim de Entrada
	MATR170 - Boletim de Entrada
@author Ivan Casagrande 
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class entrancebulletinReportsBusinessObjectsBRA from totvs.protheus.backoffice.com.smartView.integratedProvider.entrancebulletinReportsBusinessObjects
    
	public method new() as object

	protected method preSchema() as object
	protected method preData() as object
	protected method processData() as object

	private data aFldSF1 as array

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
	Método de instância da classe
@return object: self
@author Ivan Casagrande 
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method New() as object class entrancebulletinReportsBusinessObjectsBRA

    _Super:new()
	
	::aFldSF1 := { "F1_ISS" }

return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Retorna a pré-estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande 
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method preSchema() as object class entrancebulletinReportsBusinessObjectsBRA

local aFields	as array

	//Aninhado -> Demonstrativo dos Livros Fiscais
	aFields := {{"IMP1"		, STR0057, "string", STR0057},;
 				{"CF"		, STR0058, "string", STR0058},;
 				{"ALIQ"		, STR0059, "number", STR0059},;
 				{"BASEIMP1"	, STR0060, "number", STR0060},;
 				{"VALIMP1"	, STR0061, "number", STR0061},;
 				{"ISENTAS1"	, STR0062, "number", STR0062},;
 				{"OUTRAS1"	, STR0063, "number", STR0063},;
 				{"OBS1"		, STR0064, "string", STR0064}}

	self:comAddNestedProperty("LivrosFiscais", "Demonstrativo dos Livros Fiscais (SF3)", "Demonstrativo dos Livros Fiscais (SF3)", , aFields)
	
	self:aliasToSchema("SF1", ::aFldSF1)
	
return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} preData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande 
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method preData() as object class entrancebulletinReportsBusinessObjectsBRA

Local nX as numeric

	for nX := 1 to len(::aFldSF1)
		::cSelectComplement += ", " + ::aFldSF1[nX]
	next nX

return self:oData


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} processData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Ivan Casagrande 
@since 30/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method processData() as object class entrancebulletinReportsBusinessObjectsBRA

local aArea		as array
local nX		as numeric

	aArea := { SF3->(GetArea()) }

	if (self:cAlias)->(!EOF())
		for nX := 1 to len(::aFldSF1)
			if At("_",::aFldSF1[nX]) > 0
				self:jData[::aFldSF1[nX]] := (self:cAlias)->&(::aFldSF1[nX])
			endif
		next nX

		dbSelectArea("SF3")
		SF3->(dbSetOrder(4))	// F3_FILIAL + F3_CLIEFOR + F3_LOJA + F3_NFISCAL + F3_SERIE
		
		if SF3->(dbSeek( (self:cAlias)->(F1_FILIAL + F1_FORNECE + F1_LOJA + F1_DOC + F1_SERIE)) )

			self:jData["LivrosFiscais"] := {}

			while SF3->(!Eof()) .And. SF3->(F3_FILiAL + F3_CLiEFOR + F3_LOJA + F3_NFISCAL + F3_SERIE) == (self:cAlias)->(F1_FILIAL + F1_FORNECE + F1_LOJA + F1_DOC + F1_SERIE)
				
				if val(Substr(SF3->F3_CFO,1,1)) < 5
					
					aAdd(self:jData["LivrosFiscais"],{;
						"IMP1":		iif( (self:cAlias)->F1_ISS > 0 .And. SF3->F3_TIPO == "S" , STR0055, STR0056),;
						"CF":		SF3->F3_CFO		,;
						"ALIQ":		SF3->F3_ALIQICM	,;
						"BASEIMP1":	SF3->F3_BASEICM	,;
						"VALIMP1":	SF3->F3_VALICM	,;
						"ISENTAS1":	SF3->F3_ISENICM	,;
						"OUTRAS1":	SF3->F3_OUTRICM	,;
						"OBS1":		iif(!empty(SF3->F3_ICMSRET), "RET " + Transform(SF3->F3_ICMSRET,"@E 9,999,999,999,999,999,999.99"), "");
						};
					)

					self:jData["LivrosFiscais"][Len(self:jData["LivrosFiscais"])]["OBS1"] += if(SF3->F3_ICMSCOM > 0, CRLF + "Compl " + Transform(SF3->F3_ICMSCOM,"@E 9,999,999,999,999,999,999.99"), "")
					
					if SF3->F3_BASEIPI > 0 .Or. SF3->F3_ISENIPI > 0 .Or. SF3->F3_OUTRIPI > 0
						
						aAdd(self:jData["LivrosFiscais"],{;
							"IMP1":		"IPI"			,;
							"CF":		SF3->F3_CFO		,;
							"ALIQ":		SF3->F3_ALIQIPI ,;
							"BASEIMP1":	SF3->F3_BASEIPI	,;
							"VALIMP1":	SF3->F3_VALIPI	,;
							"ISENTAS1":	SF3->F3_ISENIPI	,;
							"OUTRAS1":	SF3->F3_OUTRIPI	,;
							"OBS1":		iif(!empty(SF3->F3_VALOBSE), "Obs. " + Transform(SF3->F3_VALOBSE,"@E 9,999,999,999,999,999,999.99"), "");
							};
						)
					endif
				
				endif
				
				SF3->(dbSkip())
			
			enddo
		endif
	endif
	
	AEval(aArea,{|x| restArea(x)})

	FwFreeArray(aArea)

return
