#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.listofopenquotes.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC8,SA2", customTables="ALL", name="Relação de Cotações em Aberto", country="ALL")

//-------------------------------------------------------------------
/*{Protheus.doc} listofopenquotesReportsBusinessObject
Classe para criação do Objeto de Negócio da Relação de Cotações em Aberto
 
@author Leandro Nishihata
@since 08/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class listofopenquotesReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object  

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 08/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class listofopenquotesReportsBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)  // "Cotações em Aberto" 
    self:setDescription(STR0003)  // "Listagem de Cotações em Aberto" 
    self:setPergunte("COMT000024")

	self:enableFilterByBranch("SC8")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Nishihata
@since 08/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class listofopenquotesReportsBusinessObject

	self:comAddProperty("cFilial", STR0004, "string")	// "Filial"
	self:comAddProperty("cAgrupa", STR0006, "string")	// "Agrupamento"

	self:aliasToSchema("SC8", {	"C8_FILIAL","C8_EMISSAO","C8_NUM","C8_COND","C8_FORNECE","C8_LOJA","C8_VALIDA","C8_ITEM","C8_PRODUTO",;
								"C8_UM","C8_QUANT","C8_TES","C8_MOEDA","C8_TXMOEDA","C8_PRECO","C8_TOTAL","C8_VLDESC","C8_DATPRF","C8_PRAZO"} )
	self:aliasToSchema("SA2", {	"A2_NOME","A2_TIPO","A2_CGC","A2_DESVIO"} )
	self:aliasToSchema("SB1", {	"B1_DESC"} )

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Retorna a estrutura dos campos
@return object: self:oSchema
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class listofopenquotesReportsBusinessObject
	
local cQuery	:= ""	as character
local nX		:= 1	as numeric
local lMvpar12  := FWSX1Util():ExistItem("COMT000024","Listar Quais ?") as logical

    cQuery := "	SELECT " + self:getAllFieldsToSQL()
    cQuery += "	FROM " + RetSQLName("SC8") +" SC8 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") +" SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC8")
	cQuery += "			AND	A2_COD			= C8_FORNECE "
	cQuery += "			AND	A2_LOJA			= C8_LOJA "
	cQuery += "			AND	SA2.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") +" SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC8")
	cQuery += "			AND	B1_COD			= C8_PRODUTO "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += " "
    cQuery += "	WHERE "
	cQuery += "			C8_FILIAL		IN (?)  " 
    cQuery += "		AND	C8_NUM			>= ? "
    cQuery += "		AND	C8_NUM			<= ? "
    cQuery += "		AND	C8_PRODUTO		>= ? "
    cQuery += "		AND	C8_PRODUTO		<= ? "
    cQuery += "		AND	C8_FORNECE		>= ? "
    cQuery += "		AND	C8_FORNECE		<= ? "
    cQuery += "		AND	C8_LOJA			>= ? "
    cQuery += "		AND	C8_LOJA			<= ? "
    cQuery += "		AND	C8_EMISSAO		>= ? "
    cQuery += "		AND	C8_EMISSAO		<= ? "
    cQuery += "		AND	SC8.D_E_L_E_T_	= ?  "
    
	if lMvpar12
		// Listar Quais ?
    	if MV_PAR12 == 1 		// Em análise
    	    cQuery += "			AND	C8_NUMPED		 = ? "  // GREEN
			cQuery += " 		AND C8_PRECO		!= ? "
			cQuery += " 		AND	C8_COND			!= ? "
		elseif MV_PAR12 == 2 	// Em aberto
    	    cQuery += " 		AND	C8_NUMPED		 = ? "  // YELLOW
			cQuery += " 		AND C8_PRECO		 = ? "
			cQuery += " 		AND	C8_COND		     = ? "
    	elseif MV_PAR12 == 3 	// Gerada no NFC
    	    cQuery += "			AND	C8_NUMPED		 = ? "  // WHITE
			cQuery += " 		AND C8_ORIGEM 		 = ? " 
		elseif MV_PAR12 == 4 	// Todas
    	    cQuery += "			AND	C8_NUMPED		 = ? "
    	endif
	else 
		cQuery += "		AND	C8_NUMPED		 = ?  "
    	cQuery += "		AND	C8_COND			!= ?  "
    	cQuery += "		AND	C8_TOTAL		 > ?  "
	endif
	
	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC8->(IndexKey(1))) 
    
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC8")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	if lMvpar12
		if MV_PAR12 == 1		// Em análise
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
			self:jStatement['PARAM_'+strZero(nX++,3)] := 0
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		elseif MV_PAR12 == 2	// Em aberto
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
			self:jStatement['PARAM_'+strZero(nX++,3)] := 0
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		elseif MV_PAR12 == 3	// Gerada no NFC
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
			self:jStatement['PARAM_'+strZero(nX++,3)] := 'PGCA020'
		elseif MV_PAR12 == 4	// Todas
			self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		endif

	else 
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif

    cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class listofopenquotesReportsBusinessObject

	self:jData["cFilial"] := alltrim((self:cAlias)->C8_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", (self:cAlias)->C8_FILIAL)

	if MV_PAR11 == 1		// Por Fornecedor
		self:jData["cAgrupa"] := (self:cAlias)->(C8_FORNECE + "/" + C8_LOJA) + " - " + alltrim((self:cAlias)->A2_NOME)
	elseif MV_PAR11 == 2 .OR. MV_PAR11 == 0    // Por Produto
		self:jData["cAgrupa"] := alltrim((self:cAlias)->C8_PRODUTO) + " - " + alltrim((self:cAlias)->B1_DESC)
	endif

return
