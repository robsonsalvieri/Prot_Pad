#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.invoiceitems.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SB1,SD1,SF1", name="Relação de Itens de Notas Fiscais", country="ALL", customTables="SB1")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} invoiceitemsSmartViewBusinessObject
Classe para criação do Objeto de Negócio 
Itens das notas fiscais

MATR080 - Relação de Itens das Notas Fiscais
Invoice Items List  

@author Everton Fregonezi Diniz
@since 06/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class invoiceitemsSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
    protected method setCustomInfoToData()

endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class invoiceitemsSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Relação de Itens da Nota Fiscal"
	self:setDescription(STR0003) 	// "A relação de itens de notas fiscais exibirá detalhadamente as informações de quantidades, valores e impostos de cada item. A informação dos itens será visualizada através da Visão de Dados."
	self:setPergunte("COMT000005")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 06/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class invoiceitemsSmartViewBusinessObject
	
	self:comAddProperty("TABELA", STR0039, "string")
	self:comAddProperty("FILIAL", STR0005, "string")
	self:comAddProperty("TIPDOC", STR0006, "string")
	self:comAddProperty("NUMDOC", STR0007, "string")
	self:comAddProperty("SERDOC", STR0008, "string")
	self:comAddProperty("FORCLI", STR0009, "string")
	self:comAddProperty("LJCLFO", STR0010, "string")
	self:comAddProperty("RAZSOC", STR0011, "string")
	self:comAddProperty("DTEMISSA",STR0040, "date")
	self:comAddProperty("DTDIGI", STR0012, "date"  )
	self:comAddProperty("CODITE", STR0013, "string")
	self:comAddProperty("CODPRO", STR0014, "string")
	self:comAddProperty("CODLOC", STR0015, "string")
	self:comAddProperty("QUANTI", STR0016, "number")
	self:comAddProperty("QTDDEV", STR0017, "number")
	self:comAddProperty("VLRUNI", STR0018, "number")
	self:comAddProperty("VLRTOT", STR0019, "number")
	self:comAddProperty("CODTES", STR0020, "string")
	self:comAddProperty("CODMOE", STR0021, "number")
	self:comAddProperty("TXMOED", STR0022, "number")
	self:comAddProperty("VLRCUS", STR0023, "number")
	self:comAddProperty("NFDEVO", STR0024, "string")
	self:comAddProperty("SERNFD", STR0025, "string")
	self:comAddProperty("ITNFDE", STR0026, "string")

	self:aliasToSchema("SB1", { "B1_DESC","B1_UM" } )

	self:enableFilterByBranch("SF1")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 06/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class invoiceitemsSmartViewBusinessObject

	local aGroup	:= {}	as array
	local aSelect	:= {}	as array
	local cQuery	:= ""	as character
	local nX		:= 1	as numeric

	if !empty(self:cSelectComplement)
		aSelect := StrTokArr2(self:cSelectComplement, "|")
		self:cSelectComplement := ""
	endif

	if !empty(self:cWhereComplement)
		aGroup	:= StrTokArr2(self:cWhereComplement, "|")
		self:cWhereComplement := ""
	endif

	cQuery := " SELECT "
	cQuery += "			 'SF1' 		AS TABELA"
	cQuery += "			,F1_FILIAL	AS FILIAL"
	cQuery += "			,F1_TIPO	AS TIPDOC"
	cQuery += "			,F1_DOC		AS NUMDOC"
	cQuery += "			,F1_SERIE	AS SERDOC"
	cQuery += "			,F1_FORNECE	AS FORCLI"
	cQuery += "			,F1_LOJA	AS LJCLFO"
	cQuery += "			,A2_NOME	AS RAZSOC"
	cQuery += "			,D1_EMISSAO	AS DTEMISSA"
	cQuery += "			,F1_DTDIGIT	AS DTDIGI"
	cQuery += "			,D1_ITEM	AS CODITE"
	cQuery += "			,D1_COD		AS CODPRO"
	cQuery += "			,D1_LOCAL	AS CODLOC"
	cQuery += "			,D1_QUANT	AS QUANTI"
	cQuery += "			,D1_QTDEDEV	AS QTDDEV"
	cQuery += "			,D1_VUNIT	AS VLRUNI"
	cQuery += "			,D1_TOTAL	AS VLRTOT"
	cQuery += "			,D1_TES		AS CODTES"
	cQuery += "			,F1_MOEDA	AS CODMOE"
	cQuery += "			,F1_TXMOEDA	AS TXMOED"
	cQuery += "			,D1_CUSTO	AS VLRCUS"
	cQuery += "			,D1_NFORI  	AS NFDEVO"
	cQuery += "			,D1_SERIORI	AS SERNFD"
	cQuery += "			,D1_ITEMORI	AS ITNFDE"
	cQuery += "			," + self:getAllFieldsToSQL()
	
	if len(aSelect) > 0
		cQuery += "		" + aSelect[1]
	endif
 	
	cQuery += " FROM " + RetSQLName("SF1") + " SF1 "
 	cQuery += "     INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SF1")
 	cQuery += "			AND	A2_COD          = F1_FORNECE "
 	cQuery += "			AND	A2_LOJA         = F1_LOJA "
 	cQuery += "			AND	SA2.D_E_L_E_T_  = ? "
 	cQuery += "     INNER JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += "			ON	" + FWJoinFilial("SD1", "SF1")
	cQuery += "			AND	D1_DOC			= F1_DOC "
 	cQuery += "			AND	D1_SERIE        = F1_SERIE "
 	cQuery += "			AND	D1_FORNECE      = F1_FORNECE "
 	cQuery += "			AND	D1_LOJA         = F1_LOJA "
 	cQuery += "			AND	D1_TIPO         = F1_TIPO "
 	cQuery += "			AND	SD1.D_E_L_E_T_  = ? "
	cQuery += "     INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SD1")
 	cQuery += "			AND	B1_COD			= D1_COD "
 	cQuery += "			AND	SB1.D_E_L_E_T_  = ? "
 	cQuery += " WHERE "
 	cQuery += "         F1_FILIAL       IN (?) "
	cQuery += "		AND NOT ("+IsRemito(3,'SF1.F1_TIPODOC')+ ") " 
 	
	if mv_par09 == 2
		cQuery += "     AND F1_TIPO         NOT IN (?) "
	endif

 	cQuery += "     AND SF1.D_E_L_E_T_  = ? "
 	cQuery += "     AND D1_COD          >= ? "
 	cQuery += "     AND D1_COD          <= ? "
 	cQuery += "     AND D1_FORNECE		>= ? "
 	cQuery += "     AND D1_FORNECE		<= ? "
 	cQuery += "     AND D1_DTDIGIT      >= ? "
 	cQuery += "     AND D1_DTDIGIT      <= ? "
	
	if mv_par08 == 1
		cQuery += "		AND D1_TES			!= ? "
	endif

	cQuery += " GROUP BY F1_FILIAL,F1_TIPO,F1_DOC,F1_SERIE,F1_FORNECE,F1_LOJA,A2_NOME,F1_DTDIGIT,D1_ITEM,D1_COD,D1_LOCAL,D1_QUANT,D1_QTDEDEV,D1_VUNIT,D1_TOTAL,D1_TES,F1_MOEDA,F1_TXMOEDA,D1_CUSTO,D1_NFORI,D1_SERIORI,D1_ITEMORI,D1_EMISSAO,"
	cQuery += self:getAllFieldsToSQL()
	
	if len(aGroup) > 0
		cQuery += aGroup[1]
	endif

	// Lista Devoluções de Compras?
	if mv_par07 == 1
		cQuery += "	"
		cQuery += "	 UNION ALL"
		cQuery += "	"
		cQuery += " SELECT "
		cQuery += "		'SD2' 		AS TABELA"
		cQuery += "		,D2_FILIAL	AS FILIAL"
		cQuery += "		,D2_TIPO	AS TIPDOC"
		cQuery += "		,D2_DOC		AS NUMDOC"
		cQuery += "		,D2_SERIE	AS SERDOC"
		cQuery += "		,D2_CLIENTE	AS FORCLI"
		cQuery += "		,D2_LOJA	AS LJCLFO"
		cQuery += "		,A2_NOME	AS RAZSOC"
		cQuery += "		,D2_EMISSAO	AS DTEMISSA"
		cQuery += "		,D2_DTDIGIT	AS DTDIGI"
		cQuery += "		,D2_ITEM	AS CODITE"
		cQuery += "		,D2_COD		AS CODPRO"
		cQuery += "		,D2_LOCAL	AS CODLOC"
		cQuery += "		,D2_QUANT	AS QUANTI"
		cQuery += "		,D2_QUANT	AS QTDDEV"
		cQuery += "		,D2_PRCVEN 	AS VLRUNI"
		cQuery += "		,D2_TOTAL	AS VLRTOT"
		cQuery += "		,D2_TES		AS CODTES"
		cQuery += "		,0			AS CODMOE"
		cQuery += "		,0			AS TXMOED"
		cQuery += "		,D2_CUSTO1 	AS VLRCUS"
		cQuery += "		,D2_NFORI  	AS NFDEVO"
		cQuery += "		,D2_SERIORI	AS SERNFD"
		cQuery += "		,D2_ITEMORI	AS ITNFDE"
		cQuery += "		," + self:getAllFieldsToSQL()
		
		if len(aSelect) > 1
			cQuery += "		" + aSelect[2]
		endif

		cQuery += " FROM " + RetSQLName("SF1") + " SF1 "
		cQuery += "     INNER JOIN " + RetSQLName("SA2") + " SA2 "
		cQuery += "			ON	" + FWJoinFilial("SA2", "SF1")
		cQuery += "			AND	A2_COD          = F1_FORNECE "
		cQuery += "			AND	A2_LOJA         = F1_LOJA "
		cQuery += "			AND	SA2.D_E_L_E_T_  = ? "
		cQuery += "     INNER JOIN " + RetSQLName("SD1") + " SD1 "
		cQuery += "			ON	" + FWJoinFilial("SD1", "SF1")
		cQuery += "			AND	D1_DOC			= F1_DOC "
		cQuery += "			AND	D1_SERIE        = F1_SERIE "
		cQuery += "			AND	D1_FORNECE      = F1_FORNECE "
		cQuery += "			AND	D1_LOJA         = F1_LOJA "
		cQuery += "			AND	D1_DTDIGIT		= F1_DTDIGIT "
		cQuery += "			AND	D1_TIPO         = F1_TIPO "
		cQuery += "			AND	SD1.D_E_L_E_T_  = ? "
		cQuery += "     INNER JOIN " + RetSQLName("SD2") + " SD2 "
		cQuery += "			ON	" + FWJoinFilial("SD2", "SD1")
		cQuery += "  		AND	D2_COD  		= D1_COD "
		cQuery += "  		AND	D2_CLIENTE  	= D1_FORNECE "
		cQuery += "  		AND	D2_LOJA     	= D1_LOJA "
		cQuery += "  		AND	D2_TIPO     	= ? "
		cQuery += "  		AND	D2_NFORI    	= D1_DOC "
		cQuery += "  		AND	D2_SERIORI  	= D1_SERIE "
		cQuery += "  		AND	D2_ITEMORI  	= D1_ITEM "
		cQuery += "			AND	SD2.D_E_L_E_T_  = ? "
		cQuery += "     INNER JOIN " + RetSQLName("SB1") + " SB1 "
		cQuery += "			ON	" + FWJoinFilial("SB1", "SD1")
		cQuery += "			AND	B1_COD			= D1_COD "
		cQuery += "			AND	SB1.D_E_L_E_T_  = ? "
		cQuery += " WHERE "
		cQuery += "         F1_FILIAL       IN (?)	"
		cQuery += "		AND NOT ("+IsRemito(3,'SF1.F1_TIPODOC')+ ") " 
		cQuery += "		AND SF1.D_E_L_E_T_  = ? "
		cQuery += "		AND D1_COD          >= ? "
		cQuery += "		AND D1_COD          <= ? "
		cQuery += "		AND D1_FORNECE		>= ? "
		cQuery += "		AND D1_FORNECE		<= ? "
		cQuery += "		AND D1_DTDIGIT      >= ? "
		cQuery += "		AND D1_DTDIGIT      <= ? "

		cQuery += " 	GROUP BY D2_FILIAL,D2_TIPO,D2_DOC,D2_SERIE,D2_CLIENTE,D2_LOJA,A2_NOME,D2_DTDIGIT,D2_ITEM,D2_COD	,D2_LOCAL,D2_QUANT,D2_QTDEDEV,D2_PRCVEN,D2_TOTAL,D2_TES,D2_CUSTO1,D2_NFORI,D2_SERIORI,D2_ITEMORI,D2_EMISSAO,"
		cQuery += self:getAllFieldsToSQL()
		
		if len(aGroup) > 1
			cQuery += aGroup[2]
		endif

	endif

	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY TIPDOC DESC, FILIAL, DTDIGI, NUMDOC, SERDOC, CODITE "

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")

	if mv_par09 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := strToKarr2("D", "|")
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04

	if mv_par08 == 1
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	endif

	if mv_par07 == 1
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := 'D'
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
		self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
		self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	endif

	cQuery := self:processSQLStatement(cQuery)	

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Manipula informação de campos do JData
 
@return Nill 
@author Guilherme Futro 
@since 05/03/2025
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method setCustomInfoToData() class invoiceitemsSmartViewBusinessObject
	
	If (self:cAlias) -> TIPDOC $ "D|B" .And. (self:cAlias) -> TABELA == "SF1" 
		self:jData["RAZSOC"] := GetAdvFVal("SA1","A1_NOME", FwxFilial("SA1") + Padr(self:jData["FORCLI"],GetSX3Cache("A1_COD", "X3_TAMANHO")) + Padr(self:jData["LJCLFO"],GetSX3Cache("A1_LOJA", "X3_TAMANHO")) , 1) 
	EndIf 

return

