
#include "protheus.ch"
#include "backoffice.sv.com.quoteanalysis.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC8,SB1,SA2,SE4", customTables="ALL" ,name="Relação de análise de cotações", country="ALL")
 
//-------------------------------------------------------------------
/*{Protheus.doc} quoteanalysisReportsBusinessObject
Classe para criação do Objeto de Negócio 
relacao de análise de cotações
 MATR210 - Análise de Cotações
 quoteanalysis
@author Leandro Fini
@since 16/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class quoteanalysisReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new()					as object
	public method getSchema()			as object

	protected method getQuery() 		as character
	protected method setCustomInfoToData()	as object 

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Fini
@since 16/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class quoteanalysisReportsBusinessObject

	_Super:new()
	
	self:setDisplayName(STR0002)  // "Análise de cotações" 
	self:setDescription(STR0002)  // "Análise de cotações" 
	self:setPergunte("COMT000004")

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Fini
@since 16/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class quoteanalysisReportsBusinessObject

	self:comAddProperty("nJuros"   , STR0003, "number", "")	// "Juros"
	self:comAddProperty("nVlrPres" , STR0004, "number", "")	// "Valor Presente"

	self:aliasToSchema("SC8", { "C8_NUMPED", "C8_DESPESA","C8_SEGURO","C8_VLDESC","C8_TPFRETE","C8_VALFRE","C8_FILIAL","C8_NUM","C8_PRODUTO",;
								"C8_FORNECE","C8_LOJA","C8_TAXAFIN", "C8_EMISSAO", "C8_PRAZO", "C8_DATPRF", "C8_ITEM", "C8_NUMPRO", "C8_TES",;
								"C8_MOEDA", "C8_QUANT", "C8_UM", "C8_PRECO", "C8_COND", "C8_TOTAL"})
	self:aliasToSchema("SB1", {"B1_DESC"})
	self:aliasToSchema("SA2", {"A2_NOME","A2_DESVIO"})    
	self:aliasToSchema("SE4", {"E4_DESCRI"})

	self:enableFilterByBranch("SC8")
	
return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class quoteanalysisReportsBusinessObject

	local cQuery 		as character
	local nX	 := 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	"
	cQuery += "	FROM " + RetSQLName("SC8") +" SC8 "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SC8")		
	cQuery += "			AND	B1_COD			= C8_PRODUTO "
	cQuery += "			AND SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SC8")		
	cQuery += "			AND	A2_COD			= C8_FORNECE "
	cQuery += "			AND A2_LOJA			= C8_LOJA "
	cQuery += "			AND SA2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT JOIN " + RetSQLName("SE4") + " SE4 "
	cQuery += "			ON	" + FWJoinFilial("SE4", "SC8")		
	cQuery += "			AND	E4_CODIGO		= C8_COND "
	cQuery += "			AND SE4.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "	WHERE "
	cQuery += "			C8_FILIAL	IN (?) "
	cQuery += "		AND C8_NUM		>= ? "
	cQuery += "		AND	C8_NUM		<= ? "
	cQuery += "		AND C8_PRODUTO	>= ? "
	cQuery += "		AND	C8_PRODUTO	<= ? "
	cQuery += "		AND C8_FORNECE	>= ? "
	cQuery += "		AND	C8_FORNECE	<= ? "
	cQuery += "		AND C8_LOJA		>= ? "
	cQuery += "		AND	C8_LOJA		<= ? "

	if mv_par11 == 2
		cQuery += "		AND	C8_NUMPED	= ? "
    endif

	cQuery += "		AND	C8_TOTAL	>  ? "
	cQuery += "		AND C8_EMISSAO	>= ? "
	cQuery += "		AND	C8_EMISSAO	<= ? "
	cQuery += "		AND	SC8.D_E_L_E_T_ = ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder( SC8->(IndexKey(1)) )

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC8")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06

	if mv_par11 == 2
		self:jStatement['PARAM_'+strZero(nX++,3)] := '      '
	endif

	self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
    
	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class quoteanalysisReportsBusinessObject

local aArea		as array
local aRelImp   as array
local cTes      as character
local nJuros    as numeric
local nX		as numeric
	
	aArea	:= {SB1->(getArea()), SC8->(getArea())}
	aRelImp := MaFisRelImp("MT150",{"SC8"})
	nJuros  := SuperGetMV("MV_JUROS")

	dbSelectArea('SB1')
	SB1->(DbSetOrder(1))

	dbSelectArea('SC8')
	SC8->(DbSetOrder(1))

	SC8->(MsSeek(FWxFilial('SC8') + (self:cAlias)->(C8_NUM + C8_FORNECE + C8_LOJA + C8_ITEM)))
	SB1->(MsSeek(FWxFilial('SB1') + (self:cAlias)->C8_PRODUTO))

	cTes := if( MV_PAR12 == 1 .And. SB1->(!EOF()), RetFldProd(SB1->B1_COD,"B1_TE"), SC8->C8_TES )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calculo do custo da Cotacao                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisIni(SC8->C8_FORNECE, SC8->C8_LOJA, "F", "N", "R", aRelImp)
	MaFisIniLoad(1)

	for nX := 1 To len(aRelImp)
		MaFisLoad(aRelImp[nX][3], SC8->(FieldGet(FieldPos(aRelImp[nX][2]))), 1)
	next nX

	MaFisEndLoad(1)
	MaFisAlt("IT_TES", cTes, 1)

	self:jData["nJuros"]    := nJuros
	self:jData["nVlrPres"]  := Round(Ma160Custo("SC8",1),2)

	aEval(aArea,{|x| restArea(x)})

	FwFreeArray(aRelImp)
	FwFreeArray(aArea)
    
return
