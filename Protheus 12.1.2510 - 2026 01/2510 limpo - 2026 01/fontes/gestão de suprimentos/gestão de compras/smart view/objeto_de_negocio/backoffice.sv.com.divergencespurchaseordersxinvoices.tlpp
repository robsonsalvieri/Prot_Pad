#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.divergencespurchaseordersxinvoices.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2,SF1,SD1,SC7,SB1", name="Divergências PC x NF", country="ALL", customTables="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} divergencespurchaseordersxinvoicesReportsBusinessObject
Classe para criação do Objeto de Negócio da Listagem de divergências entre o Pedido de Compras e Notas Fiscais

MATR130 - Divergências PC x NF
Divergences Purchase Orders X Invoices 

@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class divergencespurchaseordersxinvoicesReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object
     
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class divergencespurchaseordersxinvoicesReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0002) 	// "Divergencias de Pedidos de Compras x Notas Fiscais"
	self:setDescription(STR0003) 	// "Listagem das divergências de Pedidos de Compras x Notas Fiscais"
	self:setPergunte("COMT000006")
	self:enableFilterByBranch("SF1")
	
return self
 

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class divergencespurchaseordersxinvoicesReportsBusinessObject

	self:comAddProperty("DIVERG", STR0004, "string")	// "Divergencia"

	self:aliasToSchema("SF1", { "F1_FILIAL","F1_TIPO","F1_DOC","F1_SERIE","F1_EMISSAO","F1_DTDIGIT","F1_COND","F1_FORNECE","F1_LOJA" } )
	self:aliasToSchema("SD1", { "D1_ITEM","D1_COD","D1_UM","D1_QUANT","D1_VUNIT" } )
	self:aliasToSchema("SC7", { "C7_COND","C7_NUM","C7_ITEM","C7_QUANT","C7_PRECO","C7_DATPRF" } )
	self:aliasToSchema("SA2", { "A2_NOME" } )
	self:aliasToSchema("SB1", { "B1_DESC" } )

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class divergencespurchaseordersxinvoicesReportsBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT	" + self:getAllFieldsToSQL()
	cQuery += " FROM	" + RetSQLName("SF1") +" SF1 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += "			ON	" + FWJoinFilial("SD1", "SF1")
	cQuery += "			AND D1_DOC			= F1_DOC "
	cQuery += "			AND	D1_SERIE		= F1_SERIE "
	cQuery += "			AND	D1_FORNECE		= F1_FORNECE "
	cQuery += "			AND	D1_LOJA			= F1_LOJA "
	cQuery += "			AND D1_TIPO			= F1_TIPO "
	cQuery += "			AND D1_FORMUL		= F1_FORMUL "
	cQuery += "			AND D1_TIPODOC		= F1_TIPODOC "
	cQuery += "			AND	SD1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1", "SD1")
	cQuery += "			AND	B1_COD			= D1_COD "
	cQuery += "			AND	SB1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SC7") + " SC7 "
	cQuery += "			ON	" + FWJoinFilial("SC7", "SD1")
	cQuery += "			AND	C7_NUM			= D1_PEDIDO	"
	cQuery += "			AND	C7_ITEM			= D1_ITEMPC	"
	cQuery += "			AND	C7_PRODUTO		= D1_COD	"
	cQuery += "			AND	SC7.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2", "SF1")
	cQuery += "			AND	A2_COD			= F1_FORNECE "
	cQuery += "			AND	A2_LOJA			= F1_LOJA "
	cQuery += "			AND SA2.D_E_L_E_T_ 	= ? "
	cQuery += " "
	cQuery += " WHERE "
	cQuery += " 		F1_FILIAL		IN (?) " 
	cQuery += "		AND	F1_DOC			>= ? "	// "N. Fiscal de ?"
	cQuery += "		AND	F1_DOC			<= ? "	// "N. Fiscal até ?"
	cQuery += "		AND	F1_SERIE		>= ? "	// "Série de ?"
	cQuery += "		AND	F1_SERIE		<= ? "	// "Série até ?"
	cQuery += "		AND	F1_FORNECE		>= ? "	// "Fornecedor de ?"
	cQuery += "		AND	F1_FORNECE		<= ? "	// "Fornecedor até ?"
	cQuery += "		AND	F1_LOJA			>= ? "	// "Loja de ?"
	cQuery += "		AND	F1_LOJA			<= ? "	// "Loja até ?"
	cQuery += "		AND	F1_TIPO			= ? "
	cQuery += "		AND	F1_DTDIGIT		>= ? "	// "Dt. Entrada NF de ?"
	cQuery += "		AND	F1_DTDIGIT		<= ? "	// "Dt. Entrada NF até ?"
	cQuery += "		AND	SF1.D_E_L_E_T_	= ? "
	cQuery += "		AND	D1_COD			>= ? "	// "Produto de ?"
	cQuery += "		AND	D1_COD			<= ? "	// "Produto até ?"
	cQuery += "		AND	D1_PEDIDO		>= ? "	// "Pedido de ?"
	cQuery += "		AND	D1_PEDIDO		<= ? "	// "Pedido até ?"

	if mv_par15 == 1		// Todas Divergências
		cQuery += "		AND (	C7_PRECO	<> D1_VUNIT "
		cQuery += "			OR	C7_COND		<> F1_COND	"
		cQuery += "			OR	C7_QUANT	<> C7_QUJE "
		cQuery += "			OR	C7_DATPRF	<> F1_DTDIGIT ) "
	elseif mv_par15 == 2	// Preço
		cQuery += "		AND C7_PRECO	<> D1_VUNIT "
	elseif mv_par15 == 3	// Condição de Pagamento
		cQuery += "		AND	C7_COND		<> F1_COND	"
	elseif mv_par15 == 4	//  Quantidade
		cQuery += "		AND	C7_QUANT	<> C7_QUJE "
	else					// Dt. Entrega
		cQuery += "		AND	C7_DATPRF	<> F1_DTDIGIT
	endif

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SD1->(IndexKey(3)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SF1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR09
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'N'
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR12
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR13
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR14
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 01/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class divergencespurchaseordersxinvoicesReportsBusinessObject

	if !((self:cAlias)->(C7_PRECO == D1_VUNIT))
		self:jData["DIVERG"] := STR0005	// "Preço"
	elseif !((self:cAlias)->(C7_COND == F1_COND))
		self:jData["DIVERG"] := STR0006	// "Cond. Pgto."
	elseif !((self:cAlias)->(C7_QUANT == D1_QUANT))
		self:jData["DIVERG"] := STR0007	// "Quantidade"
	elseif !((self:cAlias)->(C7_DATPRF == F1_DTDIGIT))
		self:jData["DIVERG"] := STR0008	// "Entrega"
	endif

	self:jData["F1_FILIAL"] := alltrim((self:cAlias)->(F1_FILIAL)) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->F1_FILIAL))
	self:jData["A2_NOME"] := (self:cAlias)->( alltrim(F1_FORNECE) + '/' + alltrim(F1_LOJA) + ' - ' + alltrim(A2_NOME) )
	self:jData["C7_COND"] := (self:cAlias)->( if( !empty(C7_COND), alltrim(C7_COND) + ' - ' + alltrim( GetAdvFVal( "SE4", "E4_DESCRI", FWxFilial("SE4") + PadR(C7_COND, FwTamSX3("C7_COND")[1]), 1 ) ), "" ))
	self:jData["F1_COND"] := (self:cAlias)->( if( !empty(F1_COND), alltrim(F1_COND) + ' - ' + alltrim( GetAdvFVal( "SE4", "E4_DESCRI", FWxFilial("SE4") + PadR(F1_COND, FwTamSX3("F1_COND")[1]), 1 ) ), "" ))

return
