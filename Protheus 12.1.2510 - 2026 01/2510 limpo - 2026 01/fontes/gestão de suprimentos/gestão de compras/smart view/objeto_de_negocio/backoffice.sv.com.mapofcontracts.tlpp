#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.mapofcontracts.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC3,SA2,SC7,SF1,SD1,SE2", customTables="ALL", name="Mapa de Contratos", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} mapofcontractsReportsBusinessObjects
Classe para criação do Objeto de Negócio Listagem de contrato de parceria

MATR126 - Mapa de Contratos   (Contratos de Parceria x Títulos)                                             
          Map Of Contracts (mapofcontracts)

@author Everton Fregonezi Diniz
@since 17/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class mapofcontractsReportsBusinessObjects from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object
	
	protected method getQuery() as character
	protected method setCustomInfoToData() as object

endclass


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Everton Fregonezi Diniz
@since 17/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class mapofcontractsReportsBusinessObjects

	_Super:new()
	self:setDisplayName(STR0002) // "Mapa de Contratos"
	self:setDescription(STR0003) // "Contratos de Parceria x Títulos"
	self:setPergunte("MTR126")
	
return self


//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 17/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class mapofcontractsReportsBusinessObjects

	self:comAddProperty("SaldoSE2", STR0004, "number", "E2_VALOR","SaldoSE2") // "Saldo"

	self:aliasToSchema("SA2", {"A2_COD","A2_LOJA","A2_NREDUZ"})
	self:aliasToSchema("SC3", {"C3_FILIAL","C3_NUM","C3_EMISSAO","C3_ITEM","C3_PRODUTO","C3_QUANT","C3_TOTAL"})
	self:aliasToSchema("SB1", {"B1_DESC"})
	self:aliasToSchema("SC7", {"C7_NUM","C7_QUANT","C7_TOTAL"})
	self:aliasToSchema("SD1", {"D1_SERIE","D1_DOC"})
	self:aliasToSchema("SE2", {"E2_TIPO","E2_PREFIXO","E2_NUM","E2_PARCELA","E2_VENCTO","E2_MOEDA","E2_SALDO"})

	self:enableFilterByBranch("SC3")

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 17/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getQuery(oFilter as object) as character class mapofcontractsReportsBusinessObjects

local cQuery	:= ""	as character
local cPrefix	:= SuperGetMV("MV_2DUPREF") as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += "	"
	cQuery += " FROM " + RetSQLName("SC3") + " SC3 "
	cQuery += "	"
	cQuery += " 	INNER JOIN " + RetSQLName("SB1") + " SB1 "
	cQuery += "			ON	" + FWJoinFilial("SB1","SC3")
	cQuery += " 		AND	B1_COD			= C3_PRODUTO "
	cQuery += " 		AND	SB1.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += " 	INNER JOIN " + RetSQLName("SA2") + " SA2 "
	cQuery += "			ON	" + FWJoinFilial("SA2","SC3")
	cQuery += " 		AND	A2_COD			= C3_FORNECE "
	cQuery += " 		AND	A2_LOJA			= C3_LOJA "
	cQuery += " 		AND	SA2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT  JOIN " + RetSQLName("SC7") + " SC7 "
	cQuery += "			ON	" + FWJoinFilial("SC7", "SC3")
	cQuery += "			AND	C7_TIPO			= ? "
	cQuery += "			AND	C7_NUMSC		= C3_NUM "
	cQuery += "			AND	C7_ITEMSC		= C3_ITEM "
	cQuery += "			AND	SC7.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += "		LEFT  JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += "			ON	" + FWJoinFilial("SD1", "SC7")
	cQuery += "			AND D1_PEDIDO		= C7_NUM "
	cQuery += "			AND D1_ITEMPC		= C7_ITEM "
	cQuery += "			AND SD1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT  JOIN " + RetSQLName("SF1") + " SF1 "
	cQuery += "			ON	" + FWJoinFilial("SF1", "SD1")
	cQuery += "			AND	F1_DOC			= D1_DOC "
	cQuery += "			AND F1_SERIE		= D1_SERIE "
	cQuery += "			AND F1_FORNECE		= D1_FORNECE "
	cQuery += "			AND F1_LOJA			= D1_LOJA " 
	cQuery += "			AND F1_TIPO			= D1_TIPO "
	cQuery += "			AND F1_FORMUL		= D1_FORMUL "
	cQuery += "			AND SF1.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "		LEFT  JOIN " + RetSQLName("SE2") + " SE2 "
	cQuery += "			ON	" + FWJoinFilial("SF1", "SE2")
	cQuery += "			AND	E2_PREFIXO		= CASE F1_PREFIXO WHEN '' THEN ? ELSE F1_PREFIXO END "
	cQuery += "			AND E2_NUM			= F1_DOC "
	cQuery += "			AND E2_FORNECE		= F1_FORNECE "
	cQuery += "			AND E2_LOJA			= F1_LOJA "
	cQuery += "			AND SE2.D_E_L_E_T_	= ? "
	cQuery += "	"
	cQuery += " WHERE "
	cQuery += "			C3_FILIAL		IN (?) "
	cQuery += "		AND	C3_NUM			>= ? "
	cQuery += "		AND	C3_NUM			<= ? "
	cQuery += "		AND	C3_FORNECE		>= ? "
	cQuery += "		AND	C3_FORNECE		<= ? "
	cQuery += "		AND	C3_LOJA			>= ? "
	cQuery += "		AND	C3_LOJA			<= ? "
	cQuery += "		AND	C3_EMISSAO		>= ? "
	cQuery += "		AND	C3_EMISSAO		<= ? "

	// Imprime Contratos ?
	if mv_par09 == 2		// Em aberto
		cQuery += "		AND	C3_QUJE			= ? " 
	elseif mv_par09 == 3	// Atendidos
		cQuery += "		AND	C3_QUANT		<= C3_QUJE  "
	elseif mv_par09 == 4	// Parcialm.Atend.
		cQuery += "		AND	C3_QUANT		> C3_QUJE "
	endif

	cQuery += "		AND	C3_RESIDUO		!= ? "
	cQuery += "		AND	C3_CC			>= ? "
	cQuery += "		AND	C3_CC			<= ? "
	cQuery += "		AND	SC3.D_E_L_E_T_	= ? "

	cQuery += self:setFilterOnWhere()

	cQuery += " ORDER BY " + SqlOrder(SC3->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := '2'
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := cPrefix
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC3")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR07
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR08
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02

	// Imprime Contratos ?
	if mv_par09 == 2		// Em aberto
		self:jStatement['PARAM_'+strZero(nX++,3)] := 0
	endif
		
	self:jStatement['PARAM_'+strZero(nX++,3)] := 'S'
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR10
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR11
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	
	cQuery := self:processSQLStatement(cQuery)

return cQuery


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Everton Fregonezi Diniz
@since 17/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------
method setCustomInfoToData(oFilter as object) as object class mapofcontractsReportsBusinessObjects

local nVlrAbat	as numeric

	self:jData["C3_FILIAL"] := (self:cAlias)->(alltrim(C3_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", C3_FILIAL))
	
	nVlrAbat := FaAbatCP( (self:cAlias)->E2_PREFIXO, (self:cAlias)->E2_NUM, (self:cAlias)->E2_PARCELA, (self:cAlias)->A2_COD, (self:cAlias)->A2_LOJA, (self:cAlias)->E2_MOEDA, "S", dDataBase)
	self:jData["SaldoSE2"] := Max( (self:cAlias)->E2_SALDO - nVlrAbat, 0)

return
