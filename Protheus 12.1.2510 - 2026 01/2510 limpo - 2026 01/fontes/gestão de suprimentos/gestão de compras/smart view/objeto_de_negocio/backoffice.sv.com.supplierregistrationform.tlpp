#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.supplierregistrationform.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider
  
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SA2", customTables="All", name="Ficha Cadastral Fornecedores", country="ALL")
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} supplierregistrationformSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Ficha cadastral de fornecedores
 
@author Leandro Nishihata
@since 19/04/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class supplierregistrationformSmartViewBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider
                                                            
    public method new() as object
    public method getSchema() as object
    
    protected method getQuery() as character
	protected method setCustomInfoToData() as object
 
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 19/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class supplierregistrationformSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0002)	// "Ficha Cadastral Fornecedores"
    self:setDescription(STR0003)	// "Ficha Cadastral"
    self:setPergunte("COMT000009")	//Indica o pergunte que será utilizado no relatório

return self
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 19/04/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   
method getSchema() as object class supplierregistrationformSmartViewBusinessObject

	self:aliasToSchema("SA2", {	"A2_FILIAL","A2_COD","A2_LOJA","A2_NOME","A2_NREDUZ","A2_END","A2_NR_END","A2_BAIRRO","A2_EST","A2_CEP",;
								"A2_ESTADO","A2_COD_MUN","A2_PAIS","A2_TIPO","A2_CGC","A2_DDI","A2_DDD","A2_TEL","A2_INSCR",;
								"A2_CONTATO","A2_BANCO","A2_AGENCIA","A2_DVAGE","A2_NUMCON","A2_DVCTA","A2_SWIFT","A2_NATUREZ",;
								"A2_TRANSP","A2_PRIOR","A2_RISCO","A2_LC","A2_MATR","A2_MCOMPRA","A2_METR","A2_MSALDO","A2_NROCOM","A2_PRICOM",;
								"A2_SALDUP","A2_DESVIO","A2_SALDUPM" } )

	self:enableFilterByBranch("SA2")

return self:oSchema

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFilter as object) as character class supplierregistrationformSmartViewBusinessObject

local cQuery	:= ''	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " FROM " + RetSQLName("SA2") + " SA2 "
	cQuery += " WHERE " 
	cQuery += "			A2_FILIAL		IN	(?) "
	cQuery += "		AND	A2_COD			>=	? "  
	cQuery += "		AND	A2_COD			<=	? " 
	cQuery += "     AND	SA2.D_E_L_E_T_	=	? " 
	cQuery += self:setFilterOnWhere()
	cQuery += " ORDER BY " + SqlOrder(SA2->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SA2")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' ' 

	cQuery := self:processSQLStatement(cQuery)

return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
@return object: self:oSchema
@author Everton Fregonezi Diniz
@since 05/09/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

method setCustomInfoToData(oFilter as object) as object class supplierregistrationformSmartViewBusinessObject

	self:jData["A2_FILIAL"] := alltrim((self:cAlias)->A2_FILIAL) + " - " + alltrim(self:getInfoEmp("M0_FILIAL", (self:cAlias)->A2_FILIAL))

return
