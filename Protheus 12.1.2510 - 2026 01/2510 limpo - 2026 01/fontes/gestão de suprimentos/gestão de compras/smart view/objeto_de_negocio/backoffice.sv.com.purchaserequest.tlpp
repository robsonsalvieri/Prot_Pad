#include "msobject.ch"
#include "protheus.ch"
#include "backoffice.sv.com.purchaserequest.ch"
#include "totvs.framework.treports.integratedprovider.th"
    
namespace totvs.protheus.backoffice.com.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACOM", tables="SC1,SB3,SB1,SC7,SD4,SB2,SA5,SB5", customTables="ALL", name="Solicitação de Compras", country="ALL")

//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} purchaserequestReportsBusinessObject
Classe para criação do Objeto de Negócio 

MATR140 - Solicitação de Compras
purchase request

@author Leandro Nishihata
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------  
class purchaserequestReportsBusinessObject from totvs.protheus.backoffice.com.smartView.integratedProvider.ComIntegratedProvider

	public method new() as object
	public method getSchema() as object

	protected method getQuery() as character
	protected method setCustomInfoToData() as object
         
endclass
 
//-----------------------------------------------------------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leandro Nishihata
@since 22/11/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class purchaserequestReportsBusinessObject

	_Super:new()
	self:setDisplayName(STR0001)	// "Relatório de Solicitações de Compras"
	self:setDescription(STR0002)	// "Relatório de solicitações de compras baseado no MATR140 exibindo..."
	self:setPergunte("COMT000019")

	self:enableFilterByBranch("SC1")
		
return self

//-----------------------------------------------------------------------------------------------------------------------------------

/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getSchema() as object class purchaserequestReportsBusinessObject

local aFldSC1	    as array
local aFldSB3	    as array
local aFldSB1	    as array
local aFldSB5	    as array    
local aFldSB2	    as array
local aFlPedido	    as array
local aFldEmpenho	as array
local aFldFornece	as array

	aFldSC1		:= {"C1_FILIAL", "C1_NUM","C1_ITEM","C1_PRODUTO","C1_DESCRI","C1_QUANT","C1_QUJE","C1_UM","C1_LOCAL","C1_CC","C1_SOLICIT","C1_DATPRF"}
	aFldSB3		:= {"B3_MEDIA","B3_CLASSE","B3_Q01","B3_Q02","B3_Q03","B3_Q04","B3_Q05","B3_Q06","B3_Q07","B3_Q08","B3_Q09","B3_Q10","B3_Q11","B3_Q12"}
	aFldSB1		:= {"B1_DESC","B1_GRUPO","B1_QE","B1_UPRC","B1_EMIN"}
	aFlPedido	:= {"C7_NUM","C7_ITEM","C7_FORNECE","C7_LOJA","C7_QUANT","C7_UM","C7_PRECO","C7_TOTAL","C7_EMISSAO","C7_DATPRF","C7_COND","C7_QUJE","C7_RESIDUO","C7_PRODUTO","A2_NOME"}
	aFldEmpenho := {"D4_COD","D4_OP","D4_DATA","D4_QUANT","D4_PRODUTO", "B1_DESC"}
	aFldSB5		:= {"B5_CEME"}
	aFldSB2		:= {"B2_QATU"}
	aFldFornece := {"A5_FORNECE","A5_LOJA","A5_CODPRF","A5_PRODUTO","A2_NOME","A2_TEL","A2_CONTATO","A2_FAX","A2_ULTCOM","A2_MUN","A2_EST","A2_RISCO"}
  
	self:aliasToSchema("SC1", aFldSC1)
	self:aliasToSchema("SB3", aFldSB3)
	self:aliasToSchema("SB1", aFldSB1)
	self:aliasToSchema("SB2", aFldSB2)
	self:aliasToSchema("SB5", aFldSB5)

	self:comAddProperty("nSldSC",	STR0042, "number", "") //"Saldo da SC"
	self:comAddProperty("Filial",	STR0043, "string", "") //"Descrição da Filial"
	self:comAddProperty("LEADTIME",	STR0044, "number", "") //"Lead Time"
	self:comAddProperty("DTNECESS", STR0045, "date"	 , "") //"Data da Necessidade"
	self:comAddProperty("cDescri",  STR0046, "string"	 , "") //"Descrição do Produto"


	self:comAddProperty("nMes1" , STR0003, "number", STR0003, "" ) // "Mes1"
	self:comAddProperty("nMes2" , STR0004, "number", STR0004, "" ) // "Mes2"
	self:comAddProperty("nMes3" , STR0005, "number", STR0005, "" ) // "Mes3"
	self:comAddProperty("nMes4" , STR0006, "number", STR0006, "" ) // "Mes4"
	self:comAddProperty("nMes5" , STR0007, "number", STR0007, "" ) // "Mes5"
	self:comAddProperty("nMes6" , STR0008, "number", STR0008, "" ) // "Mes6"
	self:comAddProperty("nMes7" , STR0009, "number", STR0009, "" ) // "Mes7"
	self:comAddProperty("nMes8" , STR0010, "number", STR0010, "" ) // "Mes8"
	self:comAddProperty("nMes9" , STR0011, "number", STR0011, "" ) // "Mes9"
	self:comAddProperty("nMes10", STR0012, "number", STR0012, "" ) // "Mes10"
	self:comAddProperty("nMes11", STR0013, "number", STR0013, "" ) // "Mes11"
	self:comAddProperty("nMes12", STR0014, "number", STR0014, "" ) // "Mes12"

	self:comAddProperty("cdmes1" , STR0015, "string", STR0015, "" ) // "Descricao mes 1"
	self:comAddProperty("cdmes2" , STR0016, "string", STR0016, "" ) // "Descricao mes 2"
	self:comAddProperty("cdmes3" , STR0017, "string", STR0017, "" ) // "Descricao mes 3"
	self:comAddProperty("cdmes4" , STR0018, "string", STR0018, "" ) // "Descricao mes 4"
	self:comAddProperty("cdmes5" , STR0019, "string", STR0019, "" ) // "Descricao mes 5"
	self:comAddProperty("cdmes6" , STR0020, "string", STR0020, "" ) // "Descricao mes 6"
	self:comAddProperty("cdmes7" , STR0021, "string", STR0021, "" ) // "Descricao mes 7"
	self:comAddProperty("cdmes8" , STR0022, "string", STR0022, "" ) // "Descricao mes 8"
	self:comAddProperty("cdmes9" , STR0023, "string", STR0023, "" ) // "Descricao mes 9"
	self:comAddProperty("cdmes10", STR0024, "string", STR0024, "" ) // "Descricao mes 10"
	self:comAddProperty("cdmes11", STR0025, "string", STR0025, "" ) // "Descricao mes 11"
	self:comAddProperty("cdmes12", STR0026, "string", STR0026, "" ) // "Descricao mes 12"

	self:comAddNestedProperty("Empenho"	 	, STR0027, STR0027, , aFldEmpenho)	// "Empenho (SD4)"
	self:comAddNestedProperty("Fornecedores", STR0028, STR0028, , aFldFornece)	// "Fornecedores"
	self:comAddNestedProperty("Pedidos"	 	, STR0029, STR0029, , aFlPedido)	// "Pedidos (SC7)"
    
	FwFreeArray(aFldSC1)
	FwFreeArray(aFldSB3)
	FwFreeArray(aFldSB1)
	FwFreeArray(aFldSB2)
	FwFreeArray(aFldSB5)
	FwFreeArray(aFldEmpenho)
	FwFreeArray(aFldFornece)
	FwFreeArray(aFlPedido)

return self:oSchema


//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------   

method getQuery(oFil as object) as character class purchaserequestReportsBusinessObject

local cQuery	:= ""	as character
local nX		:= 1	as numeric

	cQuery := " SELECT " + self:getAllFieldsToSQL()
	cQuery += " "
	cQuery += "	FROM " + RetSQLName("SC1") +" SC1 "
	cQuery += " "
	cQuery += "		INNER JOIN " + RetSQLName("SB1") + " SB1 ON " + FWJoinFilial("SB1", "SC1") + " AND B1_COD = C1_PRODUTO AND SB1.D_E_L_E_T_ = ? "
	cQuery += "		LEFT JOIN  " + RetSQLName("SB5") + " SB5 ON " + FWJoinFilial("SB5", "SC1") + " AND B5_COD = C1_PRODUTO AND SB5.D_E_L_E_T_ = ? "
	cQuery += "		LEFT JOIN  " + RetSQLName("SB3") + " SB3 ON " + FWJoinFilial("SB3", "SC1") + " AND B3_COD = C1_PRODUTO AND SB3.D_E_L_E_T_ = ? "
	cQuery += "		LEFT JOIN  " + RetSQLName("SB2") + " SB2 ON " + FWJoinFilial("SB2", "SC1") + " AND B2_COD = C1_PRODUTO AND B2_LOCAL = C1_LOCAL AND SB2.D_E_L_E_T_ = ? "
	cQuery += "		LEFT JOIN  " + RetSQLName("SC7") + " SC7 ON " + FWJoinFilial("SC7", "SC1") + " AND C7_NUMSC = C1_NUM AND C7_ITEM = C1_ITEM AND C7_PRODUTO = C1_PRODUTO AND SC7.D_E_L_E_T_ = ? "
	cQuery += " "
	cQuery += " WHERE "
	cQuery += "			C1_FILIAL	IN (?) "
	cQuery += "		AND	C1_NUM		>= ? "
	cQuery += "		AND	C1_NUM		<= ? "
	cQuery += "		AND	C1_ITEM		>= ? "
	cQuery += "		AND	C1_ITEM		<= ? "

	if MV_PAR07 == 2
		cQuery += "		AND	C1_QUANT > C1_QUJE "
	endif
	
	cQuery += "		AND	C1_EMISSAO	>= ? "
	cQuery += "		AND	C1_EMISSAO	<= ? "
	cQuery += "		AND	C1_IMPORT IN (?) "
	cQuery += "		AND	C1_TPOP IN (?) "
	cQuery += "		AND	C1_RESIDUO = ? "
	cQuery += "		AND	SC1.D_E_L_E_T_ = ?
	cQuery += " "
	cQuery += " ORDER BY " + SqlOrder(SC1->(IndexKey(1)))

	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := self:getCustomParam('SV_MULTBRANCH', .T., "SC1")
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR01
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR02
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR03
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR04
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR05
	self:jStatement['PARAM_'+strZero(nX++,3)] := MV_PAR06
	self:jStatement['PARAM_'+strZero(nX++,3)] := iif(mv_par11 == 1, {'S', 'N', ''}, {'N', ''})
	self:jStatement['PARAM_'+strZero(nX++,3)] := iif(mv_par09 == 1, {'F'},iif(mv_par09 == 2, {'P'}, {'F', 'P'}))
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '
	self:jStatement['PARAM_'+strZero(nX++,3)] := ' '

	cQuery := self:processSQLStatement(cQuery)

Return cQuery

//-----------------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Leandro Nishihata
@since 22/11/2023
@version 1.0
*/
//-----------------------------------------------------------------------------------------------------------------------------------

Method setCustomInfoToData(oFilter as object) as object class purchaserequestReportsBusinessObject

local aMeses	as array
local aOrdem	as array
local aPedido	as array
local aFornece	as array
local aEmpenho	as array
local cCampoMes as character
local nX		as numeric
local nAno		as numeric
local nMes		as numeric
local nB3Count	as numeric

	aMeses	:= {STR0030,STR0031,STR0032,STR0033,STR0034,STR0035,STR0036,STR0037,STR0038,STR0039,STR0040,STR0041}		//"Jan"###"Fev"###"Mar"###"Abr"###"Mai"###"Jun"###"Jul"###"Ago"###"Set"###"Out"###"Nov"###"Dez"   
		
	aEmpenho := {}
	aFornece := {}
	aPedido  := {}
	aOrdem   := {}

	nAno	 := Year(dDataBase)
	nMes	 := Month(dDataBase)
	nB3Count := 1

	self:jData["Empenho"]		:= {}
	self:jData["Pedidos"]		:= {}
	self:jData["Fornecedores"]	:= {}
	self:jData["cDescri"]		:= iif(MV_PAR08 == 1,(self:cAlias)->B1_DESC, iif(MV_PAR08==2,(self:cAlias)->B1_DESC, (self:cAlias)->C1_DESCRI))
	self:jData["nSldSC"]    	:= (self:cAlias)->C1_QUANT-(self:cAlias)->C1_QUJE
	self:jData["Filial"]    	:= alltrim((self:cAlias)->C1_FILIAL) + " - " + self:getInfoEmp("M0_FILIAL", (self:cAlias)->C1_FILIAL)
	self:jData["LEADTIME"]  	:= CalcPrazo((self:cAlias)->C1_PRODUTO,(self:cAlias)->C1_QUANT)
	self:jData["DTNECESS"]  	:= self:varToTimeStamp(If(Empty((self:cAlias)->C1_DATPRF),(self:cAlias)->C1_EMISSAO,(self:cAlias)->C1_DATPRF))
	
	if mv_par12 > 0
		dbSelectArea("SC7")
		SC7->(dbSetOrder(4))		
		SC7->(dbSeek(FWxFilial("SC7")+(self:cAlias)->C1_PRODUTO))
		while SC7->(!Eof()) .And. SC7->C7_FILIAL + SC7->C7_PRODUTO == (self:cAlias)->C1_FILIAL + (self:cAlias)->C1_PRODUTO	
			if len(self:jData["Pedidos"]) < mv_par12
				aAdd(self:jData["Pedidos"], {;
								"C7_NUM"      : SC7->C7_NUM,;
								"C7_ITEM"     : SC7->C7_ITEM,;
								"C7_FORNECE"  : SC7->C7_FORNECE,;
								"C7_LOJA"     : SC7->C7_LOJA,;
								"C7_QUANT"    : SC7->C7_QUANT,;
								"C7_UM"       : SC7->C7_UM,;
								"C7_PRECO"    : SC7->C7_PRECO,;
								"C7_TOTAL"    : SC7->C7_TOTAL,;
								"C7_EMISSAO"  : self:varToTimeStamp(SC7->C7_EMISSAO),;
								"C7_DATPRF"   : self:varToTimeStamp(SC7->C7_DATPRF) ,;
								"C7_COND"     : SC7->C7_COND,;
								"C7_QUJE"     : SC7->C7_QUJE,;
								"C7_RESIDUO"  : SC7->C7_RESIDUO,;
								"C7_PRODUTO"  : SC7->C7_PRODUTO,;
								"A2_NOME"     : GetAdvFVal("SA2","A2_NOME",xFilial("SA2")+SC7->C7_FORNECE+SC7->C7_LOJA,1)})
			endif
			SC7->(DBSkip()) 
		enddo
	endif

	if mv_par13 > 0
		dbSelectArea("SA5")
		SA5->(dbSetOrder(2))
		SA5->(dbSeek(FWxFilial("SA5")+(self:cAlias)->C1_PRODUTO))
		while SA5->(!Eof()) .And. FWxFilial("SA5") + (self:cAlias)->C1_PRODUTO == SA5->A5_FILIAL + SA5->A5_PRODUTO
			if SA2->(dbSeek(FWxFilial("SA2")+SA5->A5_FORNECE+SA5->A5_LOJA))
				if len(self:jData["Fornecedores"]) < mv_par13
					aAdd(self:jData["Fornecedores"], { ;
									"A5_FORNECE"  : SA5->A5_FORNECE ,;
									"A5_LOJA"     : SA5->A5_LOJA ,;
									"A5_PRODUTO"  : SA5->A5_PRODUTO ,;
									"A5_CODPRF"   : SA5->A5_CODPRF,;
									"A2_NOME"     : SA2->A2_NOME ,;
									"A2_TEL"      : SA2->A2_TEL ,;
									"A2_CONTATO"  : SA2->A2_CONTATO ,;
									"A2_FAX"      : SA2->A2_FAX ,;
									"A2_ULTCOM"   : self:varToTimeStamp(SA2->A2_ULTCOM) ,;
									"A2_MUN"      : SA2->A2_MUN ,;
									"A2_EST"      : SA2->A2_EST ,;
									"A2_RISCO"    : SA2->A2_RISCO  })
				endif
			
			EndIf
			SA5->(DBSkip())    
		enddo
	endif

	if MV_PAR10 == 1
		dbSelectArea("SD4")
		SD4->(dbSetOrder(1))
		SD4->(dbSeek(FWxFilial("SD4")+(self:cAlias)->C1_PRODUTO))
		while SD4->(!Eof()) .And. SD4->D4_FILIAL + SD4->D4_COD == (self:cAlias)->C1_FILIAL + (self:cAlias)->C1_PRODUTO    
			if SD4->D4_QUANT <> 0

				aAdd(self:jData["Empenho"], {"D4_PRODUTO"   : SD4->D4_PRODUTO,;
											"D4_OP"    : SD4->D4_OP,;
											"B1_DESC"  : GetAdvFVal("SB1", "B1_DESC", FWxFilial("SB1")+SD4->D4_PRODUTO, 1),;												
											"D4_DATA"  : self:varToTimeStamp(SD4->D4_DATA),;
											"D4_QUANT" : SD4->D4_QUANT})
			endif  
			SD4->(DBSkip()) 
		enddo
	endif
		
	for nX := nMes to 1 step -1    
		cCampoMes := "cdmes"+alltrim(str(nB3Count))
		self:jData[cCampoMes] := aMeses[nX]+"/"+StrZero(nAno,4)
		AADD(aOrdem,nX)
		nB3Count++
	next nX

	nAno--                                 

	for nX := 12 to nMes+1 step -1
		cCampoMes := "cdmes"+alltrim(str(nB3Count))
		self:jData[cCampoMes] := aMeses[nX]+"/"+StrZero(nAno,4)
		AADD(aOrdem,nX)
		nB3Count++
	next nX

	for nX := 1 to Len(aOrdem)        
		cMes    := StrZero(aOrdem[nX],2)
		cCampos := (self:cAlias)+"->B3_Q"+cMes
		cCampoMes := "nMes"+alltrim(str(nX))
		self:jData[cCampoMes] := &cCampos
	next nX

	FwFreeArray(aMeses)
	FwFreeArray(aOrdem)
	FwFreeArray(aPedido)
	FwFreeArray(aFornece)
	FwFreeArray(aEmpenho)

Return
