#include "protheus.ch"
#include "tbiconn.ch"
#include 'FWLIBVERSION.CH'

namespace backoffice.com.generictaxes

/*/{Protheus.doc} generictaxes
    (Classe genérica que retorna os dados do configurador de tributos do compras.
    @author Thiago Rodrigues
    @since 01/07/2025
 /*/

Class generictaxes

    Public data lConfiguratorenabled As logical

    Public method new()
    Public method configuratorenabled()
    Public method getallRules( nItem As numeric)
    Public method isGuideFromConfigurator(cOrigem As Character, cImposto As Character, nIcms As Numeric,;
                                          nIcmsSt As Numeric, cItem As Character)
    Public method importpiscofins(nItem As numeric)
    Public method isIcmsCreditedForCost(cIdtrib As Character)
    Public method isIcmsDifalFromCFG(nItemNF As Numeric )
    Public method getRetentionIss( nItemNF As Numeric)
    Public method getPresumedCredit(nItemNF As Numeric)
    Public method getAgregType(cIdTrib As Character)
    Public method getTaxesWritten(cIdTrib As Character)
endclass


/*/{Protheus.doc} New
    (Método de instância da classe)
    @author Thiago Rodrigues
    @since 16/06/2025
    @return Self
    /*/
Method New() Class generictaxes
    self:lConfiguratorenabled := self:configuratorenabled()
Return self


/*/{Protheus.doc} New
    (Retorna se o configurador de tributos esta habilitado)
    
    @author Thiago Rodrigues
    @since 16/06/2025
    @return logico, se .T. configurador esta habilitado.
 /*/
Method configuratorenabled() Class generictaxes
    Local lCalculationUsingConfigurator As logical
    Local cLibVersion                   As Character
    Local lIsInitialRelease2510         As logical
    
    lCalculationUsingConfigurator := .F.
    lIsInitialRelease2510         := .F.

    //Versão da Lib
    cLibVersion := FWLibVersion()
    
    //Validação da release Inicial
    If cLibVersion >= "20250224"
        lIsInitialRelease2510 := (totvs.framework.systeminfo.MPFetchInitialRelease() >= "12.1.2510")
    Endif

    //Validações configurador de tributos.
    if FindFunction("ChkTrbGen") .And. ChkTrbGen("SD1", "D1_IDTRIB") .And. lIsInitialRelease2510
        if AliasDisp({"F2B", "CJ4", "CIQ", "CIR", "CIS", "CIT", "CIU", "CIX", "CIY", "CJ0", "CJ1", "CJ2","FKO","FKK"})
            lCalculationUsingConfigurator := .T.                            
        EndIf
    endif

Return lCalculationUsingConfigurator


/*/{Protheus.doc} getallRules
    (Retorna as regras do configurador de tributos em tempo de execução.
    
    @Params  
    nItem - Item da nota fiscal 
    ARegras -  Array com 1 ou mais Regras do configurador  regras_base, regras_aliquota, regras_escrituracao, detalhe_livro)
    
    @author Thiago Rodrigues
    @since 15/07/2025
    @return Json com as regras configuradas na rotina FISA170
 /*/
Method getallRules( nItem as numeric, aRegras As Array) Class generictaxes
    Local cJsonRet As Character 
    Local oDados   As Object
    Local aRegras  As Array

    Default nItem := 1
    Default aRegras := {"regras_base", "regras_aliquota", "regras_escrituracao", "detalhe_livro"}

    //Aciona a classe do fiscal
    oDados := totvs.protheus.backoffice.fiscal.tciclass.TCIProcessing():New()
    oDados:setItems({nItem})
    oDados:setDataItems(aRegras)
    cJsonRet  := oDados:GetDataItems()

    FreeObj(oDados)
    FwFreeArray(aRegras)
Return cJsonRet


/*/{Protheus.doc} isGuideFromConfigurator
    (Verifica se a guia está sendo gerada para um tributo que consta na lista do Configurador de Tributos. 
     Caso isso ocorra deverá prevalecer a regra do configurador na função: xFisAddGNRE)

    @author Thiago Rodrigues
    @since 10/07/2025
    @return logico
 /*/
Method isGuideFromConfigurator(cOrigem As Character, cImposto As Character, nIcms As Numeric, nIcmsSt As Numeric,; 
                               cItem   As Character ) Class generictaxes 

    Local IsCFGTrib As logical

    Default cOrigem  := "MATA103"
    Default cImposto := "IC"
    Default nIcms    := 0 
    Default nIcmsSt  := 0 
    Default cItem    := ""

    IsCFGTrib := .F.
    if FindFunction("ChkCfgTrib")
        IsCFGTrib := ChkCfgTrib(cOrigem, cImposto,nIcms ,nIcmsSt,,,cItem) // verifica se a Geração de Guia será pelo configurador de tributos
    endif

Return IsCFGTrib


/*/{Protheus.doc} importpiscofins
    (Verifica se tem pis ou cofins calculado pelo CFG)

    @author Thiago Rodrigues
    @since 15/07/2025
    @return logico
 /*/
Method importpiscofins(nItem as numeric) Class generictaxes 

    Local aRegraCalc    As Array
    Local cRegraCalc    As Character
    Local nCalculo      As Numeric
    Local jRegraB       As json
    Local cRegrasCFG    As Character
    Local oObjJson      As object
    Local jRegraBase    As json
    Local cItem         As Character
    Local lRet          As logical
    Local cIdentTrib    As Character

    Default nItem = 1 

    aRegraCalc    := {}
    cRegraCalc    := ""
    nCalculo      := 1
    jRegraB       := nil
    cRegrasCFG    := ""
    oObjJson      := Nil
    jRegraBase    := Nil
    lRet          := .F.
    cIdentTrib    := ""

    //Busca regras base do configurador de tributos
    cRegrasCFG := self:getallRules(nItem,{"regras_base"})
    
    If !Empty(cRegrasCFG)
        oObjJson := JsonObject():New()
        oObjJson:FromJson(cRegrasCFG)

        //Cria Objeto com todas as regras base
        cItem      := cValToChar(nItem)
        jRegraBase := oObjJson["dados_itens"][cItem]
        aRegraCalc := jRegraBase:GetNames()

        if !jRegraBase:HasProperty("Aviso") // Quando não existem tributos calculados recebemos a propriedade aviso

            for nCalculo := 1 to Len(aRegraCalc)

                //Regra base
                cRegraCalc := aRegraCalc[nCalculo]
                jRegraB := jRegraBase[cRegraCalc]

                //Verifica se existe tributos de importação: "000045" (PIS-Importação) ou "000043" (COFINS-Importação) 
                if jRegraB:HasProperty("ident_trib")
                    cIdentTrib := jRegraB["ident_trib"]

                    if cIdentTrib == "000045" .Or. cIdentTrib == "000043"
                        lRet := .T.
                        Exit
                    endif
                EndIf

            Next nCalculo
        endif
    endif

    FreeObj(oObjJson)
    FwFreeArray(aRegraCalc)
Return lRet



/*/{Protheus.doc} isIcmsCreditedForCost
    (Verifica através do CFG se Credita ou não o ICMS para cálculo do custo.
    (S) - (sim) para crédito de ICMS.
    (N) - (não) para o não crédito de ICMS)

    @author Leandro Nishihata
    @since 15/07/2025
    @return Retorna "S" se o ICMS é creditado para cálculo do custo, "N" caso contrário.
 /*/
Method isIcmsCreditedForCost(cIdtrib As Character) Class generictaxes 

    Local lATFCGTrb  As logical
    Local cCrIcmTrib As Character

    lATFCGTrb  := FindFunction('backoffice.stock.calculationcost.CalculationNFETrbGen') .and. FindFunction("ATFVlCGTrb")
    cCrIcmTrib := ""

    Default cIdtrib := ""

    //Verifica através da função de custo se irá ou não creditar o ICMS.
    If lATFCGTrb 
        ATFVlCGTrb(cIdtrib, 0,@cCrIcmTrib)
    Endif

Return cCrIcmTrib


/*/{Protheus.doc} isIcmsDifalFromCFG
    (Verifica se existe diferencial de alíquota de ICMS (DIFAL) configurado via Configurador de Tributos)

    @author Thiago Rodrigues
    @since 17/07/2025
    @return "S" ? Resposta afirmativa / "N" ? Resposta negativa
/*/
Method isIcmsDifalFromCFG(nItemNF As Numeric) Class generictaxes 
    
    Local aRegraCalc    As Array
    Local cRegraCalc    As Character
    Local nCalculo      As Numeric
    Local jRegraB       As json
    Local cRegrasCFG    As Character
    Local oObjJson      As Object
    Local jRegraBase    As json
    Local cIdentTrib    As Character
    Local cICMCompl     As Character

    Default nItemNF := 1

    // Inicializações
    aRegraCalc := {}
    cICMCompl  := "N" // Considera negativa por padrão

    // Busca regras base do configurador de tributos
    cRegrasCFG := self:getallRules(nItemNF, {"regras_base"})
    
    If !Empty(cRegrasCFG)
        oObjJson := JsonObject():New()
        oObjJson:FromJson(cRegrasCFG)

        jRegraBase := oObjJson["dados_itens"][cValToChar(nItemNF)]
        aRegraCalc := jRegraBase:GetNames()

         If !jRegraBase:HasProperty("Aviso") // Quando não existem tributos calculados recebemos a propriedade aviso
            For nCalculo := 1 To Len(aRegraCalc)

                cRegraCalc := aRegraCalc[nCalculo]
                jRegraB    := jRegraBase[cRegraCalc]

                If jRegraB:HasProperty("ident_trib")
                    cIdentTrib := jRegraB["ident_trib"]

                    // Verifica se é DIFAL (ID TOTVS 000037)
                    If cIdentTrib == "000037"
                        cICMCompl := "S"
                        Exit
                    EndIf

                EndIf

            Next nCalculo
        Endif

        FreeObj(oObjJson)
    EndIf

    FwFreeArray(aRegraCalc)

Return cICMCompl

/*/{Protheus.doc} AliasDisp
    (Verifica se os alias utilizados para calculo via CFG estão disponiveis)
    @author Thiago Rodrigues
    @since 04/08/2025
    @param aAlias Array com nomes dos alias
    @return logico
/*/
Static Function AliasDisp(aAlias)
    Local Nx   As Numeric
    Local lRet As Logical

    lRet := .T. 
    For Nx := 1 To Len(aAlias)
        If !FWAliasIndic(aAlias[Nx])
            lRet := .F. 
            Exit
        EndIf
    Next

Return lRet

/*/{Protheus.doc} getRetentionIss
    (Verifica retenção do ISS (FKO_CUMULA) nas regras financeiras do Configurador de Tributos)

    @author Leandro nishihata
    @since 11/08/2025
    @return logical, .T. se houver retenção do ISS, senão .F
/*/
method getRetentionIss(nItemNF As Numeric) Class generictaxes 

    Local aRegraCalculo As Array
    Local jRegraBase    As json
    Local nRegras       As numeric
    Local lretiss       As logical
    Local cRegrasCFG    As character
    Local aArea         As Array
    Local cRegraFin     As character
    Local oObjJson      As json
    Local cRegraCalculo As character
    Local jRegraCalculo As json

    default nItemNF    := 1

    //Inicialização das variaveis 
    aRegraCalculo := {}
    jRegraBase    := Nil
    nRegras       := 1 
    lretiss       := .F.
    aArea         := GetArea()
    cRegraFin     := ""
    oObjJson      := Nil
    cRegraCalculo := ""
    jRegraCalculo := Nil

    //Busca regras de base
    cRegrasCFG := self:getallRules(nItemNF, {"regras_base"})

    // Busca regras base do configurador de tributos
    oObjJson := JsonObject():New()
    oObjJson:FromJson(cRegrasCFG)

    jRegraBase := oObjJson["dados_itens"][cValToChar(nItemNF)]
    aRegraCalculo := jRegraBase:GetNames()
    
    //Existem tributos
    if !jRegraBase:HasProperty("Aviso") 

        for nRegras := 1 to len(aRegraCalculo)

            // Regra de Calculo
            cRegraCalculo := aRegraCalculo[nRegras]
            jRegraCalculo := jRegraBase[cRegraCalculo]
            
            // Identificador do IDTOTVS ISS
            IF jRegraCalculo["ident_trib"] == "000020" 
                cRegraFin := Alltrim(jRegraCalculo["regr_financ"])
                Exit
            Endif
        next nRegras


        If !Empty(cRegraFin)  
            FKK->(dbSelectArea("FKK"))
		    FKK->(dbSetOrder(2)) //FKK_FILIAL+FKK_CODIGO+FKK_VERSAO

            If FKK->(dbSeek(xFilial("FKK") + cRegraFin ))  //Regra Financeira
                FKO->(dbSelectArea("FKO"))
		        FKO->(dbSetOrder(2)) //FKO_FILIAL+FKO_CODIGO

                If FKO->(DbSeek(xFilial("FKO") + FKK->FKK_CODFKO)) // Regra de retenção
                    lretiss := FKO->FKO_CUMULA == "2" 
                Endif
            Endif
        Endif 
        
    Endif

    FwFreeArray(aRegraCalculo) 
    RestArea(aArea)
    FwFreeArray(aArea) 
                                         
Return lretiss



/*/{Protheus.doc} getPresumedCredit
    (Verifica se existe credito presumido calculado pelo configurador de tributos)

    @author Thiago Rodrigues
    @since 12/08/2025
    @return Array, .T. Se encontrou o credito presumido e  aliquota do credito presumido
/*/
method getPresumedCredit(nItemNF As Numeric) Class generictaxes 

    Local aRegraCalculo As Array
    Local jRegraBase    As json
    Local nRegras       As numeric
    Local lretiss       As logical
    Local cRegrasCFG    As character
    Local oObjJson      As json
    Local cRegraCalculo As character
    Local jRegraCalculo As json
    Local aRet          As Array

    default nItemNF    := 1

    //Inicialização das variaveis 
    aRegraCalculo := {}
    jRegraBase    := Nil
    lretiss       := .F.
    oObjJson      := Nil
    cRegraCalculo := ""
    jRegraCalculo := Nil
    aRet          := {.F.,0}

    //Busca regras de base
    cRegrasCFG := self:getallRules(nItemNF, {"regras_base"})

    // Busca regras base do configurador de tributos
    oObjJson := JsonObject():New()
    oObjJson:FromJson(cRegrasCFG)

    jRegraBase := oObjJson["dados_itens"][cValToChar(nItemNF)]
    aRegraCalculo := jRegraBase:GetNames()
    
    //Existem tributos
    if !jRegraBase:HasProperty("Aviso") 

        for nRegras := 1 to len(aRegraCalculo)

            // Regra de Calculo
            cRegraCalculo := aRegraCalculo[nRegras]
            jRegraCalculo := jRegraBase[cRegraCalculo]
            
            // Identificador do IDTOTVS  (CREDITO PRESUMIDO PARA ICMS)
            IF jRegraCalculo["ident_trib"] == "000030" 
                aRet[1] := .T.
                aRet[2] := jRegraCalculo["aliq_trib"]
                Exit
            Endif
        next nRegras

    Endif

    FwFreeArray(aRegraCalculo) 
    FreeObj(oObjJson)
    
    jRegraCalculo := Nil
    jRegraBase    := Nil 
                                         
Return aRet

/*/{Protheus.doc} getAgregType
    (Verifica se o icms ou iss será deduzido do total da nota. 
     Este metodo busca a resposta referente ao campo F4_AGREG opção: 

     D - Deduz o valor do ICMS ou ISS, no caso de redução de base de cálculo, do valor contábil. Atenção! 
     a dedução do Imposto será subtraída na base do PIS e COFINS. 
     Deduzirá também no caso de Isenção por operação com cliente órgão público. )

    @author Thiago Rodrigues
    @since 13/08/2025
    @return logico, .T. caso campo CJ2_STOTNF seja = 3=Subtrai do total da nota e da duplicata
/*/
method getAgregType(cIdTrib As character) Class generictaxes 
    Local oDadosCFG  As Object
    Local oObjJson   As Object
    Local aRegEscr   As Array
    Local jRegEscr   As Json
    Local jRegra     As Json
    Local lRet       As Logical
    Local cEscritura As character
    Local cRegra     As character
    Local nI         As Numeric
    Local aData      As Array

    Default cIdTrib := ""

    oDadosCFG  := Nil
    oObjJson   := Nil
    aRegEscr   := {}
    jRegEscr   := Nil
    jRegra     := Nil
    lRet       := .F.
    cEscritura := ""
    aData      := {}
    cRegra     := ""

    If !Empty(cIdTrib)
        aData := {cIdTrib} // Id do tributo

        //Classe Fiscal que retorna os dados do CFG que já foram gravados
        oDadosCFG := totvs.protheus.backoffice.fiscal.tciclass.TCIWritten():New()
        oDadosCFG:SetId(aData)
        oDadosCFG:setDataItems({'regras_escrituracao'})
        cEscritura := oDadosCFG:getAgregTypeByTax()
        FreeObj(oDadosCFG)

       If !Empty(cEscritura)
            oObjJson := JsonObject():New()
            oObjJson:FromJson(cEscritura)

            jRegEscr := oObjJson["data"][cIdTrib]
            aRegEscr := oObjJson["data"][cIdTrib]:Getnames()
            
            
            //Regras de escrituração para o id informado
            For nI := 1 to Len(aRegEscr)
                
                cRegra := aRegEscr[nI]
                jRegra := jRegEscr[cRegra]
                
                //Ação = 3 (Subtrai do total da nota e da duplicata) e tributo ICMS ou ISS
                if jRegra["acao_tot_nf"] == "3" .And. (jRegra["id_totvs"] == "000021" .Or. jRegra["id_totvs"] == "000020")
                    lRet := .T.
                    Exit
                endif

            Next nI
        Endif

        FreeObj(oObjJson)
    Endif

    FwFreeArray(aRegEscr)          
    FwFreeArray(aData)   
     
    jRegEscr := Nil
    jRegra   := Nil

Return lRet


/*/{Protheus.doc} getTaxesWritten
    (Retorna as regras do configurador de tributos em tempo de execução.
    
    @Params  
    nItem - Item da nota fiscal 
    ARegras -  Array com 1 ou mais Regras do configurador  regras_base, regras_aliquota, regras_escrituracao, detalhe_livro)
    
    @author renan.martins
    @since 08/2025
    @return Json com as regras configuradas na rotina FISA170
 /*/
Method getTaxesWritten( cIdTrib As Character) Class generictaxes
    Local oDadosCFG := Nil  As Object
    Local cJsonRet  := ""   As Character
    
    if ( !Empty(cIdTrib) )
        //Classe Fiscal que retorna os dados do CFG que já foram gravados
        oDadosCFG := totvs.protheus.backoffice.fiscal.tciclass.TCIWritten():New()
        oDadosCFG:SetId({cIdTrib})
		cJsonRet := oDadosCFG:GetDataId()
    endif   

    FreeObj(oDadosCFG)

Return cJsonRet
