#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace pgc.historicalsController
using namespace pgc.historicalsService
using namespace pgc.utils

/*/{Protheus.doc} pgcHistoricalsController
	API de análise da cotação.
@author renan.martins
@since 02/2024
/*/
Class pgcHistoricalsController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/pgcHistoricals/mostpurchased")
    public Method getMostPurchased()

    @Get("/api/com/v1/pgcHistoricals/mostpurchasedcriteria")
    public Method getcriteriaMostPurchased()

    @Get("/api/com/v1/pgcHistoricals/latestpurchases")
    public Method getLatestPurchases()

    @Get("/api/com/v1/pgcHistoricals/latestprices")
    public Method getLatestPrices()

    @Get("/api/com/v1/pgcHistoricals/lateststock")
    public Method getLatestStock()

    @Get("/api/com/v1/pgcHistoricals/latestorders")
    public Method getLatestOrders()

    @Get("/api/com/v1/pgcHistoricals/openpurchaserequests")
    public Method getOpenPurchaseRequests()

    @Get("/api/com/v1/pgcHistoricals/productsvssuppliers")
    public Method getProductsvsSuppliers()

    @Get("/api/com/v1/pgcHistoricals/lastsuppliersOrders")
    public Method getLastSuppliersOrders()

    @Get("/api/com/v1/pgcHistoricals/lateopenorders")
    public Method getLateOpenOrders()

    @Get("/api/com/v1/pgcHistoricals/supplierpurchasedetails")
    public Method getSupplierPurchaseDetails()

    @Get("/api/com/v1/pgcHistoricals/purchasereturn")
    public Method getPurchaseReturn()

    @Get("/api/com/v1/pgcHistoricals/mostpurchasedsuppliers")
    public Method getMostPurchasedSuppliers()
EndClass

/*/{Protheus.doc} New
	Método construtor.
@author renan.martins
@since 02/2024
/*/
Method New() Class pgcHistoricalsController
Return Self


/*/{Protheus.doc} setAnswer
	Monta resposta JSON.
@author renan.martins
@since 02/2024
@param nQuery, numeric, 1=Lista geral dos mais comprados, 2=Lista geral dos mais comprados com filtro, 3=Últimas compras, 4=Últimos preços, 5=Saldo em Estoque,
                        6=Pedidos em Aberto, 7=SCs em Aberto, 8=Produtos Fornecidos, 9=Últimos fornecimentos, 10=Pedidos em Atraso
@return oJsonQryParam, object, objeto json com query params.
@return oJsonResponse, object, objeto json com path params.
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(nQuery, oJsonQryParam, oJsonPathParam) Class pgcHistoricalsController
    Local lOk As Logical
    Local cMessage As Character
    Local nCode As Numeric
    Local oHistoricalsService As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    Default nQuery := 1
    Default oJsonQryParam := Nil
    Default oJsonPathParam := Nil

    oHistoricalsService := pgcHistoricalsService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    
    oHistoricalsService:getQueryResponse(Self:nPage, Self:nPageSize, nQuery, oJsonQryParam, oJsonPathParam)
    
    oJsonResponse := oHistoricalsService:oJsonResponse
    lOk := oHistoricalsService:lOk
    nCode := oHistoricalsService:nCode
    cMessage := oHistoricalsService:cMessage

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, nCode, cMessage)

    FreeObj(oHistoricalsService)
    FreeObj(oUtils)
Return .T.


/*/{Protheus.doc} getMostPurchased
Consulta de Itens mais comprados, baseado na SD1. Listagem simples, apenas com count.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getMostPurchased() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(1, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getCriteriaMostPurchased
Consulta de Itens mais comprados, baseado na SD1. Listagem completa, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getCriteriaMostPurchased() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(2, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLatestPurchases
Consulta de últimos itens comprados, baseado na SD1, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLatestPurchases() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(3, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLatestPrices
Consulta de últimos preços para os produtos, baseado na SC8, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLatestPrices() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(4, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLatestStock
Consulta itens no estoque, baseado na tabela SB2, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLatestStock() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(5, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLatestOrders
Consulta Pedidos de Compra que estão abertos, baseados na tabela SC7, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLatestOrders() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(6, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getOpenPurchaseRequests
Consulta Solciitaçõe de Compra que estão abertas, baseados na tabela SC1, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getOpenPurchaseRequests() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(7, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getProductsvsSuppliers
Consulta que exibe os produtos versus fornecedores, baseado na tabela SA5, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getProductsvsSuppliers() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(8, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLastSuppliersOrders
Consulta os últimos pedidos realziados pelos fornecedores, baseado na tabela SD1, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLastSuppliersOrders() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(9, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getLateOpenOrders
Consulta Pedidos de Compras em atraso, na tabela SC7, com possibilidade de filtros diversos.
@author renan.martins
@since 02/2024
@return .T.
/*/
Method getLateOpenOrders() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(10, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getSupplierPurchaseDetails
Consulta Detalhes de compra do Fornecedor.
@author ali.neto
@since 02/2024
@return .T.
/*/
Method getSupplierPurchaseDetails() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(11, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getPurchaseReturn
Consulta Devolução de compra
@author ali.neto
@since 02/2024
@return .T.
/*/
Method getPurchaseReturn() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(12, oJsonQryParam) 
Return .T.


/*/{Protheus.doc} getMostPurchasedSuppliers
Consulta Principais fornecedores
@author ali.neto
@since 02/2024
@return .T.
/*/
Method getMostPurchasedSuppliers() Class pgcHistoricalsController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(13, oJsonQryParam) 
Return .T.

