#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.UTILS.SERVICE.CH'

namespace pgc.utilsService
using namespace pgc.utilsRepository
using namespace pgc.utils

/*/{Protheus.doc} pgcUtilsService
	Classe de serviços utilitários
@author juan.felipe
@since 18/11/2021
/*/
Class pgcUtilsService
    public Data lOk As Logical
    public Method New()

    public Method fieldsProperties()
    public Method getUserInfo()
    public Method postSendMetric()
	public Method companyInfo()
	public Method calcDiscount()

    public Method verifyMultiProt()
    public Method postWizardParam()
    public Method postWizardEmail()
    public method getpurchaserList()
	public Method postMigrateLegacyQuote()
	public Method getValidateRoutine()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 22/12/2021
/*/
Method New() Class pgcUtilsService
Return Self

/*/{Protheus.doc} postFieldsProperties
	Consulta propriedades dos campos via SX3.
@author juan.felipe
@since 22/12/2021
@return oJsonResponse, object, resposta no formato json.
/*/
Method fieldsProperties(cAlias, aFields) Class pgcUtilsService
    Local oUtilsRepository As Object
    Local oJsonResponse As Object
    Default cAlias  := ""
    Default aFields := {}

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:fieldsProperties(cAlias, aFields)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse

/*/{Protheus.doc} companyInfo
	Consulta dados da empresa logada, tais como endereço de entrega e CNPJ
@author Leandro Fini
@since 13/12/2022
@return oJsonResponse, object, resposta no formato json.
/*/
Method companyInfo(cCompanyId, cBranchId) Class pgcUtilsService

    Local oUtilsRepository As Object
    Local oJsonResponse As Object
    Default cCompanyId := ""
    Default cBranchId  := ""

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:companyInfo(cCompanyId, cBranchId)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse

/*/{Protheus.doc} postFieldsProperties
	Consulta propriedades dos campos via SX3.
@author juan.felipe
@since 13/01/2022
@return oJsonResponse, object, informações do usuário.
/*/
Method getUserInfo(cLogin) Class pgcUtilsService
    local cUserId       As Character
    local cName         As Character
    local cMessage      As Character
    local cEmailUser    as character
    local cFolder       as character
    local lAdmin        as Character
    local lParamBuyer   as logical
    local oJsonResponse As Object
    local oUtils        As Object
    local oVrfWF7       := pgcUtilsRepository():New()
    Default cLogin      := ''

    oJsonResponse := JsonObject():New()
    oUtils      := pgcUtils():New()
    cUserId     := RetCodUsr(cLogin)
    cName       := UsrRetName(cUserId)
    lAdmin      := FWIsAdmin(cUserId)
    cEmailUser  := Alltrim(UsrRetMail(cUserId))
    lParamBuyer := WFGetMV( "MV_PGCWF", .F. )
    cFolder     := ""

    If Self:lOk := GetRpoRelease() >= '12.1.2210'
        If Self:lOk := PGCVldTables(@cMessage)
            oJsonResponse['userId']         := cUserId
            oJsonResponse['userName']       := Iif(Empty(cName), cLogin, cName)
            oJsonResponse['userAdmin']      := lAdmin
            oJsonResponse['userEmail']      := cEmailUser
            oJsonResponse['paramWfBuyer']   := lParamBuyer
            if lParamBuyer .and. !empty(cEmailUser)
                oVrfWF7:existsWF7email(cEmailUser, @cFolder, "")
            endif
            oJsonResponse['postalBoxName']  := cFolder
        Else
            oJsonResponse := oUtils:setClassResponse(400, cMessage)
        EndIf
    Else
        cMessage :=  STR0001 //-- Para acessar o PGC é necessário utilizar um Release igual ou superior ao 12.1.2210.
    EndIf
       
    If !Self:lOk
        oJsonResponse := oUtils:setClassResponse(400, cMessage)
    EndIf

	oJsonResponse['userId'] := cUserId
    oJsonResponse['userName'] := Iif(Empty(cName), cLogin, cName)
    oJsonResponse['userAdmin'] := lAdmin

    FreeObj(oUtils)
Return oJsonResponse

/*/{Protheus.doc} postSendMetric
	Consulta propriedades dos campos via SX3.
@author juan.felipe
@since 22/11/2022
@param oJsonRequest, object, body da requisição
@return oJsonResponse, object, resposta da requisição.
/*/
Method postSendMetric(oJsonRequest) Class pgcUtilsService
    Local oJsonResponse As Object
    Local oUtils As Object
    Local nValue As Numeric
    Default oJsonRequest := JsonObject():New()

    cIdMetric := oJsonRequest:idMetric
    nValue := oJsonRequest:value
    oUtils := pgcUtils():New()
    oJsonResponse := JsonObject():New()

    oUtils:addMetric('backoffice.pgc.utils.service', 'postSendMetric', cIdMetric, nValue)
    
    oJsonResponse['idMetric'] := oJsonRequest:idMetric
    oJsonResponse['value'] := oJsonRequest:value
Return oJsonResponse

/*/{Protheus.doc} calcDiscount
	Realiza o cálculo de desconto de acordo com parâmetors enviados
    nPerc = Percentual de desconto
    nTotDiscount = Total do desconto em valor
    nTotValue = Total do documento
    aItems = Items que serão aplicados o desconto
@author Leandro Fini
@since 22/12/2022
@return oJsonResponse, object, resposta no formato json.
/*/
Method calcDiscount(nPerc, nTotDiscount, nTotValue, aItems) Class pgcUtilsService

    Local oUtilsRepository As Object
    Local oJsonResponse As Object

    Default nPerc := 0
    Default nTotDiscount := 0
    Default aItems := {}

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:calcDiscount(nPerc, nTotDiscount, nTotValue, aItems)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse


/*/{Protheus.doc} verifyMultiProt
Verifica se a porta multiprotocolo existe, bem como realzia verificação de parâmetros
@author Renan Martins 
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method verifyMultiProt() Class pgcUtilsService
    local oUtilsRepository as object
    local oJsonResponse as Object

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:verifyMultiProtocol()
    
    FreeObj(oUtilsRepository)
Return oJsonResponse


/*/{Protheus.doc} postWizardParam
Realiza a gravação dos dados do Wizard de administrador.
@author Renan Martins 
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizardParam(oJsonRequest) Class pgcUtilsService
    local oUtilsRepository as object
    local oJsonResponse as Object
    Default oJsonRequest := JsonObject():New()

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:postWizardParamSv(oJsonRequest)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse


/*/{Protheus.doc} postWizardEmail
Realiza a gravação dos dados do Wizard de comprador.
@author Renan Martins
@since 11/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postWizardEmail(oJsonRequest) Class pgcUtilsService
    local oUtilsRepository as object
    local oJsonResponse as Object
    Default oJsonRequest := JsonObject():New()

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:postWizardEmailSv(oJsonRequest)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse


/*/{Protheus.doc} getPurchaserList
Retorna lista com os compradores cadastrados no sistema - tabela SY1
@author Renan Martins
@since 12/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method getPurchaserList(oJsonRequest) Class pgcUtilsService
    local oUtilsRepository as object
    local oJsonResponse as Object
    Default oJsonRequest := JsonObject():New()

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:getPurchaserList(oJsonRequest)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse

/*/{Protheus.doc} postMigrateLegacyQuote
Realiza a migração da cotação legada para estrutura do NFC
@author Leandro Fini
@since 02/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postMigrateLegacyQuote(oJsonRequest) Class pgcUtilsService
    local oUtilsRepository as object
    local oJsonResponse as Object
    Default oJsonRequest := JsonObject():New()

    oUtilsRepository := pgcUtilsRepository():New()
    oJsonResponse := oUtilsRepository:postMigrateLegacyQuote(oJsonRequest)
    
    FreeObj(oUtilsRepository)
Return oJsonResponse

/*/{Protheus.doc} getValidateRoutine
    Valida rotina através da chamada da função PGCVldTables.
@author juan.felipe
@since 25/09/2025
@return oJsonResponse, object, resposta no formato json.
/*/
Method getValidateRoutine(oJsonPath) Class pgcUtilsService
    Local oJsonResponse as Object
    Local oUtils As Object
    Local cMessage As Character
    Local cHelp As Character
    Local lOk As Logical
    
    Default oJsonPath := JsonObject():New()

    oUtils := pgcUtils():New()
    oJsonResponse := JsonObject():New()

    Self:lOk := .T.
    lOk := PGCVldTables(@cMessage, oJsonPath['routine'], @cHelp)

    oJsonResponse['help'] := cHelp
    oJsonResponse['message'] := cMessage
    oJsonResponse['isValid'] := lOk
Return oJsonResponse
