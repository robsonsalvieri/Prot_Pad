#include "tlpp-core.th"

namespace pgc.quotationsService
using namespace pgc.quotationsRepository

CLASS pgcQuotationsService
    public Data oJsonResponse As Object
    public Data lOk   As Logical
    public Data nCode As Numeric
    public Data cMessage As Character

    public Method New()
    public Method getQueryResponse()
    public Method deleteQuotation()
    public Method putQuotationHeader()
EndClass

/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New() CLASS pgcQuotationsService
    Self:oJsonResponse := JsonObject():new()
    Self:lOk := .F.
    Self:nCode := 0
    Self:cMessage := ""
Return Nil

/*/{Protheus.doc} getQueryResponse
	Metodo responsavel por validar e chamar a consulta dos dados.
@author juan.felipe
@since 27/10/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de cotações
@return oJsonResponse, object, objeto json com os dados das solicitações.
/*/
Method getQueryResponse(nPage, nPageSize, nQuery) Class pgcQuotationsService
    Local oQuotationsRepository As Object
    Default nPage       := 1
	Default nPageSize   := 10
    Default nQuery      := 1

    oQuotationsRepository := pgcQuotationsRepository():New("GET")
    oQuotationsRepository:Get(nPage, nPageSize, nQuery)

    Self:lOk := oQuotationsRepository:lOk
    Self:nCode := oQuotationsRepository:GetCode()
    Self:cMessage := oQuotationsRepository:GetMessage()
    Self:oJsonResponse := oQuotationsRepository:getJSONResponse()

    oQuotationsRepository:DeActivate()
    FreeObj(oQuotationsRepository)
Return Self:oJsonResponse

/*/{Protheus.doc} deleteQuotation
	Deleta cotação.
@author juan.felipe
@since 16/01/2023
@param cCode, object, código da cotação.
@return oJsonResponse, object, resposta no formato json.
/*/
Method deleteQuotation(cCode) Class pgcQuotationsService
    Local oQuotationsRepository As Object
    Default cCode := ''

    oQuotationsRepository := pgcQuotationsRepository():New("DELETE")
    Self:oJsonResponse := oQuotationsRepository:deleteQuotation(cCode)
    Self:lOk := oQuotationsRepository:lOk

    FreeObj(oQuotationsRepository)
Return Self:oJsonResponse 

/*/{Protheus.doc} putQuotationHeader
	Realiza atualização do cabeçalho da cotação.
@author juan.felipe
@since 15/07/2025
@param oJsonRequest, object, body no formato json.
@return oJsonResponse, object, resposta no formato json.
/*/
Method putQuotationHeader(oJsonRequest) Class pgcQuotationsService
    Local oQuotationsRepository As Object

    oQuotationsRepository := pgcQuotationsRepository():New("PUT")
    oQuotationsRepository:oJsonRequest := oJsonRequest

    Self:oJsonResponse := oQuotationsRepository:putQuotationHeader()
    Self:lOk := oQuotationsRepository:lOk

    FreeObj(oQuotationsRepository)
Return Self:oJsonResponse 
