#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#INCLUDE 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'
#include 'BACKOFFICE.PGC.UPDATE.QUOTATION.REPOSITORY.CH'

namespace pgc.updateQuotationRepository
using namespace pgc.utils
using namespace pgc.generateQuotationRepository
using NameSpace backoffice.com.generictaxes

/*/{Protheus.doc} pgcUpdateQuotationRepository
	Classe Adapter para o serviço de atualização da cotação.
@author juan.felipe
@since 27/10/2022
/*/
Class pgcUpdateQuotationRepository FROM FWAdapterBaseV2
    public Data lOk             As Logical
    public Data oJsonRequest    As Object
    public Data lNewProposal    As Logical
    public Data lNewParticipant As Logical
    public Data lCleanProposal  As Logical
    public Data oFieldsExs      as Object
    private Data aRefTax        as Array
    private Data lNFCCost       as Logical

    public Method New()
    public Method Get()
    private Method TreatJSONResponse()
    private Method setQuotesStatus()
    private Method addOthersDataFields()
    private Method getProposalsHistory()
    private Method ApportionmentDiffValues()
	public Method getListCurrency()
    public Method putUpdateQuotation()
    public Method putChangeQuotationSituation()
    public Method postCalcTax()
    public Method postNewProposal()
    public Method deleteProposal()
	public Method getCurrencyName()
    public Method vldFieldsSX3()
    public Method QueryOperationAnalysis()
    public Method getSupplierBinding()
    public Method postNewSupplierBinding()
    public Method NFCRetMoed()
    public Method addTaxesTCIWritten()
    public Method addProductDescription()
EndClass


/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New(cVerb) Class pgcUpdateQuotationRepository
    Default cVerb := "GET"
    Self:lOk := .F.
    Self:oJsonRequest := Nil
    Self:lNewProposal := .F.
    Self:lNewParticipant := .F.
    Self:lCleanProposal := .F.
    Self:oFieldsExs := nil
    Self:oFieldsExs := JsonObject():New()
    Self:aRefTax := {}
    Self:lNFCCost := FindFunction('NFCGetCost')

    _Super:New(cVerb, .T.)
Return Self

/*/{Protheus.doc} Get
	Consulta fornecedores/itens da cotação
@author juan.felipe
@since 27/10/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada,
    6- Listagem de Impostos.
@return Nil, nulo.
/*/
Method Get(nPage, nPageSize, nQuery) class pgcUpdateQuotationRepository
    Local aArea         As Array
    Local aQueryRequest As Array
    Local cWhere        As Character
    Local oUtils        As Object
    local nPosTmp       as numeric 
    local cQuotation    As Character
    local nRecnoSC8     As numeric
    local cIdentCode    as character
    Default nQuery := 1

    Default nPage        := 1
	Default nPageSize    := 10
    
    aArea           := FwGetArea()
    aQueryRequest   := {}
    nPosTmp         := {}
    cQuotation      := ""
    nRecnoSC8       := 0
    cIdentCode      := ""

    oUtils := pgcUtils():New()
    aQueryRequest := oUtils:queryRequestToArray(oRest:getQueryRequest())
    
    if nQuery == 5 .and. len(aQueryRequest) > 0
        nPosTmp     := aScan(aQueryRequest,{|x| lower(x[1]) == "quotation"})
        cQuotation  := iif( nPosTmp > 0, aQueryRequest[nPosTmp, 2], "0")
        nPosTmp     := aScan(aQueryRequest,{|x| lower(x[1]) == "identcodect"})
        cIdentCode  := iif( nPosTmp > 0, aQueryRequest[nPosTmp, 2], "0")
        aQueryRequest := {}
    endif

    cWhere := GetWhere(nQuery, Self:lNewProposal, cQuotation, cIdentCode)

    AddMapFields(self, nQuery)

    Self:setPage(nPage)
    Self:setPageSize(nPageSize)
    Self:SetQuery(GetQuery(nQuery, Self))
    Self:SetWhere(cWhere)
    Self:SetUseSpaces(.T.)
    
    If nQuery == 1 .Or. nQuery == 3
        Self:SetOrder('C8_FILIAL, C8_NUM, C8_FORNECE, C8_LOJA, C8_FORNOME, C8_NUMPRO')
    EndIf

    If Len(aQueryRequest) > 0
        Self:SetUrlFilter(aQueryRequest)
    EndIf

    Self:SetOrderQuery(oUtils:getQueryParam('ORDER'))
    Self:SetFields(oUtils:getQueryParam('FIELDS'))
    
    If Self:Execute()
        Self:FillGetResponse()
        Self:TreatJSONResponse(nQuery, Self:oJsonObj:oJsonObj)
    EndIf

    FwRestArea(aArea)
    FwFreeArray(aArea)
    FreeObj(oUtils)
Return Nil

/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API para dos fornecedores/itens da cotação.
@author juan.felipe
@since 27/10/2022
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2.
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada, 6- Listagem de Impostos
@return Nil, nulo.
/*/
Static Function AddMapFields(oSelf, nQuery)
    Local cDataBase := Upper(Alltrim(TcGetDb()))

    If nQuery == 1 .Or. nQuery == 3
        oSelf:AddMapFields('branch'            , 'C8_FILIAL' , .T., .T., {'C8_FILIAL'   , 'C', TamSX3('C8_FILIAL' )[1], 0})
        oSelf:AddMapFields('quotationCode'     , 'C8_NUM'    , .T., .T., {'C8_NUM'      , 'C', TamSX3('C8_NUM'    )[1], 0})
        oSelf:AddMapFields('supplier'          , 'C8_FORNECE', .T., .T., {'C8_FORNECE'  , 'C', TamSX3('C8_FORNECE')[1], 0})
        oSelf:AddMapFields('store'             , 'C8_LOJA'   , .T., .T., {'C8_LOJA'     , 'C', TamSX3('C8_LOJA'   )[1], 0})
        oSelf:AddMapFields('proposal'          , 'C8_NUMPRO' , .T., .T., {'C8_NUMPRO'   , 'C', TamSX3('C8_NUMPRO' )[1], 0})
        oSelf:AddMapFields('corporateName'     , 'C8_FORNOME', .T., .F., {'C8_FORNOME'  , 'C', TamSX3('C8_FORNOME')[1], 0})
        oSelf:AddMapFields('cnpj'              , 'A2_CGC'    , .T., .F., {'A2_CGC'      , 'C', TamSX3('A2_CGC'    )[1], 0})
        oSelf:AddMapFields('type'              , 'A2_TIPO'   , .T., .F., {'A2_TIPO'     , 'C', TamSX3('A2_TIPO'   )[1], 0})
        oSelf:AddMapFields('address'           , 'A2_END'    , .T., .F., {'A2_END'      , 'C', TamSX3('A2_END'    )[1], 0})
        oSelf:AddMapFields('district'          , 'A2_BAIRRO' , .T., .F., {'A2_BAIRRO'   , 'C', TamSX3('A2_BAIRRO' )[1], 0})
        oSelf:AddMapFields('state'             , 'A2_EST'    , .T., .F., {'A2_EST'      , 'C', TamSX3('A2_EST'    )[1], 0})
        oSelf:AddMapFields('county'            , 'A2_MUN'    , .T., .F., {'A2_MUN'      , 'C', TamSX3('A2_MUN'    )[1], 0})
        oSelf:AddMapFields('zipCode'           , 'A2_CEP'    , .T., .F., {'A2_CEP'      , 'C', TamSX3('A2_CEP'    )[1], 0})
        oSelf:AddMapFields('countryCode'       , 'A2_DDI'    , .T., .F., {'A2_DDI'      , 'C', TamSX3('A2_DDI'    )[1], 0})
        oSelf:AddMapFields('areaCode'          , 'A2_DDD'    , .T., .F., {'A2_DDD'      , 'C', TamSX3('A2_DDD'    )[1], 0})
        oSelf:AddMapFields('telephone'         , 'A2_TEL'    , .T., .F., {'A2_TEL'      , 'C', TamSX3('A2_TEL'    )[1], 0})
        oSelf:AddMapFields('email'             , 'C8_FORMAIL', .T., .F., {'C8_FORMAIL'  , 'C', TamSX3('C8_FORMAIL')[1], 0})
        oSelf:AddMapFields('paymentCondition'  , 'C8_COND'   , .T., .F., {'C8_COND'     , 'C', TamSX3('C8_COND'   )[1], 0})
        oSelf:AddMapFields('paymentDescription', 'E4_DESCRI' , .T., .F., {'E4_DESCRI'   , 'C', TamSX3('E4_DESCRI' )[1], 0})
        oSelf:AddMapFields('typeShip'          , 'C8_TPFRETE', .T., .F., {'C8_TPFRETE'  , 'C', TamSX3('C8_TPFRETE')[1], 0})
        oSelf:AddMapFields('emissionDate'      , 'C8_EMISSAO', .T., .F., {'C8_EMISSAO'  , 'C', TamSX3('C8_EMISSAO')[1], 0})
        oSelf:AddMapFields('currency'          , 'C8_MOEDA'  , .T., .F., {'C8_MOEDA'    , 'N', TamSX3('C8_MOEDA'  )[1], 0})
        oSelf:AddMapFields('deadline'          , 'DEADLINE'  , .T., .F., {'DEADLINE'    , 'N', TamSX3('C8_PRAZO'  )[1], 0}, 'AVG(C8_PRAZO)')
        oSelf:AddMapFields('total'             , 'TOTAL'     , .T., .F., {'TOTAL'       , 'N', TamSX3('C8_TOTAL'  )[1], 0}, oSelf:QueryOperationAnalysis("SUM(", {'C8_TOTAL', 'C8_VALFRE', 'C8_DESPESA', 'C8_SEGURO', 'C8_VALIPI', 'C8_VALSOL'}) + "-C8_VLDESC)" )
        oSelf:AddMapFields('totalItems'        , 'TOTALITEMS', .T., .F., {'TOTALITEMS'  , 'N', TamSX3('C8_TOTAL'  )[1], 0}, 'SUM(C8_TOTAL)')
        oSelf:AddMapFields('totalShip'         , 'TOTALSHIP' , .T., .F., {'TOTALSHIP'   , 'N', TamSX3('C8_VALFRE' )[1], 0}, 'SUM(C8_VALFRE)')
        oSelf:AddMapFields('totalExpense'      , 'TOTALEXPEN', .T., .F., {'TOTALEXPEN'  , 'N', TamSX3('C8_DESPESA')[1], 0}, 'SUM(C8_DESPESA)')
        oSelf:AddMapFields('totalInsurance'    , 'TOTALINSUR', .T., .F., {'TOTALINSUR'  , 'N', TamSX3('C8_SEGURO' )[1], 0}, 'SUM(C8_SEGURO)')
        oSelf:AddMapFields('totalDiscount'     , 'TOTALDISC' , .T., .F., {'TOTALDISC'   , 'N', TamSX3('C8_VLDESC' )[1], 0}, 'SUM(C8_VLDESC)')
        if oSelf:vldFieldsSX3('SC8', 'C8_VALIPI')
            oSelf:AddMapFields('totalIpi'          , 'TOTALIPI'  , .T., .F., {'TOTALIPI'    , 'N', TamSX3('C8_VALIPI' )[1], 0}, 'SUM(C8_VALIPI)')
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALICM')
            oSelf:AddMapFields('totalIcms'         , 'TOTALICMS' , .T., .F., {'TOTALICMS'   , 'N', TamSX3('C8_VALICM' )[1], 0}, 'SUM(C8_VALICM)')
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALSOL')
            oSelf:AddMapFields('totalIcmsst'       , 'TOTALICMSST',.T., .F., {'TOTALICMSST' , 'N', TamSX3('C8_VALSOL' )[1], 0}, 'SUM(C8_VALSOL)')
		endif
        oSelf:AddMapFields('quantityItems'     , 'QUANTITEMS', .T., .F., {'QUANTITEMS'  , 'N', 10                     , 0}, 'COUNT(C8_ITEM)')
    ElseIf nQuery == 2 .Or. nQuery == 4
        oSelf:AddMapFields('branch'            , 'C8_FILIAL' , .T., .T., {'C8_FILIAL' , 'C', TamSX3('C8_FILIAL' )[1], 0})
        oSelf:AddMapFields('deliveryBranch'    , 'C8_FILENT' , .T., .T., {'C8_FILENT' , 'C', TamSX3('C8_FILENT' )[1], 0})
        oSelf:AddMapFields('quotationCode'     , 'C8_NUM'    , .T., .T., {'C8_NUM'    , 'C', TamSX3('C8_NUM'    )[1], 0})
        oSelf:AddMapFields('supplier'          , 'C8_FORNECE', .T., .T., {'C8_FORNECE', 'C', TamSX3('C8_FORNECE')[1], 0})
        oSelf:AddMapFields('store'             , 'C8_LOJA'   , .T., .T., {'C8_LOJA'   , 'C', TamSX3('C8_LOJA'   )[1], 0})
        oSelf:AddMapFields('productCode'       , 'C8_PRODUTO', .T., .T., {'C8_PRODUTO', 'C', TamSX3('C8_PRODUTO')[1], 0})
        oSelf:AddMapFields('item'              , 'C8_ITEM'   , .T., .T., {'C8_ITEM'   , 'C', TamSX3('C8_ITEM'   )[1], 0})
        oSelf:AddMapFields('proposal'          , 'C8_NUMPRO' , .T., .T., {'C8_NUMPRO' , 'C', TamSX3('C8_NUMPRO' )[1], 0})
        oSelf:AddMapFields('gridItem'          , 'C8_ITEMGRD', .T., .T., {'C8_ITEMGRD', 'C', TamSX3('C8_ITEMGRD')[1], 0})
        oSelf:AddMapFields('productDescription', 'B1_DESC'   , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('corporateName'     , 'C8_FORNOME', .T., .F., {'C8_FORNOME', 'C', TamSX3('C8_FORNOME')[1], 0})
        oSelf:AddMapFields('cnpj'              , 'A2_CGC'    , .T., .F., {'A2_CGC'    , 'C', TamSX3('A2_CGC'    )[1], 0})
        oSelf:AddMapFields('supplierType'      , 'A2_TIPO'   , .T., .F., {'A2_TIPO'   , 'C', TamSX3('A2_TIPO'   )[1], 0})
		oSelf:AddMapFields('email'             , 'C8_FORMAIL', .T., .F., {'C8_FORMAIL', 'C', TamSX3('C8_FORMAIL')[1], 0})
		oSelf:AddMapFields('contact'           , 'C8_CONTATO', .T., .F., {'C8_CONTATO', 'C', TamSX3('C8_CONTATO')[1], 0})
        oSelf:AddMapFields('situation'         , 'C8_SITUAC' , .T., .F., {'C8_SITUAC' , 'C', TamSX3('C8_SITUAC' )[1], 0})
        oSelf:AddMapFields('unity'             , 'C8_UM'     , .T., .F., {'C8_UM'     , 'C', TamSX3('C8_UM'     )[1], 0})
        oSelf:AddMapFields('quantity'          , 'C8_QUANT'  , .T., .F., {'C8_QUANT'  , 'N', TamSX3('C8_QUANT'  )[1], 0})
        oSelf:AddMapFields('availableQuantity' , 'C8_QTDISP' , .T., .F., {'C8_QTDISP' , 'N', TamSX3('C8_QTDISP' )[1], 0})
        oSelf:AddMapFields('unityValue'        , 'C8_PRECO'  , .T., .F., {'C8_PRECO'  , 'N', TamSX3('C8_PRECO'  )[1], 0})
        oSelf:AddMapFields('TES'               , 'C8_TES'    , .T., .F., {'C8_TES'    , 'N', TamSX3('C8_TES'    )[1], 0})
        oSelf:AddMapFields('discount'          , 'C8_DESC'   , .T., .F., {'C8_DESC'   , 'N', TamSX3('C8_DESC'   )[1], 0})
        oSelf:AddMapFields('discountValue'     , 'C8_VLDESC' , .T., .F., {'C8_VLDESC' , 'N', TamSX3('C8_VLDESC' )[1], 0})
        oSelf:AddMapFields('total'             , 'C8_TOTAL'  , .T., .F., {'C8_TOTAL'  , 'N', TamSX3('C8_TOTAL'  )[1], 0})
		oSelf:AddMapFields('originalpurchase'  , 'C1_SCORI'  , .T., .F., {'C1_SCORI'  , 'C', TamSX3('C1_SCORI'  )[1], 0})
        oSelf:AddMapFields('cidentcode'        , 'C8_IDENT'  , .T., .F., {'C8_IDENT'  , 'C', TamSX3('C8_IDENT'  )[1], 0})
        oSelf:AddMapFields('numsc'             , 'C8_NUMSC'  , .T., .F., {'C8_NUMSC'  , 'C', TamSX3('C8_NUMSC'  )[1], 0})
        oSelf:AddMapFields('itemsc'            , 'C8_ITEMSC' , .T., .F., {'C8_ITEMSC' , 'C', TamSX3('C8_ITEMSC' )[1], 0})
        
        if oSelf:vldFieldsSX3('SC8', 'C8_PICM')
            oSelf:AddMapFields('icmsAliquot'       , 'C8_PICM'   , .T., .F., {'C8_PICM'   , 'N', TamSX3('C8_PICM'   )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_ALIIPI')
            oSelf:AddMapFields('ipiAliquot'        , 'C8_ALIIPI' , .T., .F., {'C8_ALIIPI' , 'N', TamSX3('C8_ALIIPI' )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALICM')
            oSelf:AddMapFields('icmsValue'         , 'C8_VALICM' , .T., .F., {'C8_VALICM' , 'N', TamSX3('C8_VALICM' )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALIPI')
            oSelf:AddMapFields('ipiValue'          , 'C8_VALIPI' , .T., .F., {'C8_VALIPI' , 'N', TamSX3('C8_VALIPI' )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_BASEICM')
            oSelf:AddMapFields('icmsBase'          , 'C8_BASEICM', .T., .F., {'C8_BASEICM', 'N', TamSX3('C8_BASEICM')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_BASEIPI')
            oSelf:AddMapFields('ipiBase'           , 'C8_BASEIPI', .T., .F., {'C8_BASEIPI', 'N', TamSX3('C8_BASEIPI')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_BASESOL')
            oSelf:AddMapFields('icmssolbase'       , 'C8_BASESOL', .T., .F., {'C8_BASESOL', 'N', TamSX3('C8_BASESOL')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_ICMSCOM')
            oSelf:AddMapFields('icmscomp'          , 'C8_ICMSCOM', .T., .F., {'C8_ICMSCOM', 'N', TamSX3('C8_ICMSCOM')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALSOL')
            oSelf:AddMapFields('icmsst'            , 'C8_VALSOL' , .T., .F., {'C8_VALSOL' , 'N', TamSX3('C8_VALSOL' )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_ALIQCMP')
            oSelf:AddMapFields('aliqicmscomp'      , 'C8_ALIQCMP', .T., .F., {'C8_ALIQCMP', 'N', TamSX3('C8_ALIQCMP')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_VALISS')
            oSelf:AddMapFields('iss'               , 'C8_VALISS' , .T., .F., {'C8_VALISS' , 'N', TamSX3('C8_VALISS' )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_ALIQISS')
            oSelf:AddMapFields('aliqiss'           , 'C8_ALIQISS', .T., .F., {'C8_ALIQISS', 'N', TamSX3('C8_ALIQISS')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_DIFAL')
            oSelf:AddMapFields('difal'             , 'C8_DIFAL'  , .T., .F., {'C8_DIFAL'  , 'N', TamSX3('C8_DIFAL'  )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_BASEDES')
            oSelf:AddMapFields('difalbase'         , 'C8_BASEDES', .T., .F., {'C8_BASEDES', 'N', TamSX3('C8_BASEDES')[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_PDORI')
            oSelf:AddMapFields('difalpori'         , 'C8_PDORI'  , .T., .F., {'C8_PDORI'  , 'N', TamSX3('C8_PDORI'  )[1], 0})
        endif
        if oSelf:vldFieldsSX3('SC8', 'C8_PDDES')
            oSelf:AddMapFields('difalpdes'         , 'C8_PDDES'  , .T., .F., {'C8_PDDES'  , 'N', TamSX3('C8_PDDES'  )[1], 0})
        endif
        oSelf:AddMapFields('typeShip'          , 'C8_TPFRETE', .T., .F., {'C8_TPFRETE', 'C', TamSX3('C8_TPFRETE')[1], 0})
        oSelf:AddMapFields('shipValue'         , 'C8_VALFRE' , .T., .F., {'C8_VALFRE' , 'N', TamSX3('C8_VALFRE' )[1], 0})
        oSelf:AddMapFields('totalShip'         , 'C8_TOTFRE' , .T., .F., {'C8_TOTFRE' , 'N', TamSX3('C8_TOTFRE' )[1], 0})
        oSelf:AddMapFields('totalShip'         , 'C8_TOTFRE' , .T., .F., {'C8_TOTFRE' , 'N', TamSX3('C8_TOTFRE' )[1], 0})
        oSelf:AddMapFields('insurance'         , 'C8_SEGURO' , .T., .F., {'C8_SEGURO' , 'N', TamSX3('C8_SEGURO' )[1], 0})
        oSelf:AddMapFields('expense'           , 'C8_DESPESA', .T., .F., {'C8_DESPESA', 'N', TamSX3('C8_DESPESA')[1], 0})
        oSelf:AddMapFields('deadline'          , 'C8_PRAZO'  , .T., .F., {'C8_PRAZO'  , 'N', TamSX3('C8_PRAZO'  )[1], 0})
        oSelf:AddMapFields('emissionDate'      , 'C8_EMISSAO', .T., .F., {'C8_EMISSAO', 'C', TamSX3('C8_EMISSAO')[1], 0})
        oSelf:AddMapFields('expirationDate'    , 'C8_VALIDA' , .T., .F., {'C8_VALIDA' , 'C', TamSX3('C8_VALIDA' )[1], 0})
        oSelf:AddMapFields('needDate'          , 'C8_DATPRF' , .T., .F., {'C8_DATPRF' , 'C', TamSX3('C8_DATPRF' )[1], 0})
        oSelf:AddMapFields('paymentCondition'  , 'C8_COND'   , .T., .F., {'C8_COND'   , 'C', TamSX3('C8_COND'   )[1], 0})
        oSelf:AddMapFields('paymentDescription', 'E4_DESCRI' , .T., .F., {'E4_DESCRI' , 'C', TamSX3('E4_DESCRI' )[1], 0})
		oSelf:AddMapFields('currency'          , 'C8_MOEDA'  , .T., .F., {'C8_MOEDA'  , 'N', TamSX3('C8_MOEDA'  )[1], 0})
        oSelf:AddMapFields('currencyRate'      , 'C8_TXMOEDA', .T., .F., {'C8_TXMOEDA', 'N', TamSX3('C8_TXMOEDA')[1], 0})
        
        If cDataBase  == 'MSSQL'
			oSelf:AddMapFields('recno', 'R_E_C_N_O_',.T.,.T.,{'R_E_C_N_O_', 'N', ,0},'SC8.')
		ElseIf cDataBase  $ ('ORACLE|POSTGRES') 
			oSelf:AddMapFields('recno', 'R_E_C_N_O_',.T.,.T.,{'R_E_C_N_O_', 'N', ,0},'SC8.R_E_C_N_O_')
		EndIf

	elseif nQuery == 5
		oSelf:AddMapFields('branch'            , 'C1_FILIAL' , .T., .T., {'C1_FILIAL' , 'C', TamSX3('C1_FILIAL' )[1], 0} )
		oSelf:AddMapFields('quotationCode'     , 'C1_COTACAO', .T., .T., {'C1_COTACAO', 'C', TamSX3('C1_COTACAO')[1], 0} )
		oSelf:AddMapFields('productCode'       , 'C1_PRODUTO', .T., .T., {'C1_PRODUTO', 'C', TamSX3('C1_PRODUTO')[1], 0} )
		oSelf:AddMapFields('item'              , 'C1_ITEM'   , .T., .T., {'C1_ITEM'   , 'C', TamSX3('C1_ITEM'   )[1], 0} )
		oSelf:AddMapFields('quantity'          , 'C1_QUANT'  , .T., .F., {'C1_QUANT'  , 'N', TamSX3('C1_QUANT'  )[1], 0} ) 
		oSelf:AddMapFields('deliveryBranch'    , 'C1_FILENT' , .T., .F., {'C1_FILENT' , 'C', TamSX3('C1_FILENT' )[1], 0} )
		oSelf:AddMapFields('productName'       , 'C1_DESCRI' , .T., .F., {'C1_DESCRI' , 'C', TamSX3('C1_DESCRI' )[1], 0} )
		oSelf:AddMapFields('purchasenumber'    , 'C1_NUM'    , .T., .F., {'C1_NUM'    , 'C', TamSX3('C1_NUM'    )[1], 0} )
		oSelf:AddMapFields('originalquantity'  , 'C1_QTDORIG', .T., .F., {'C1_QTDORIG', 'C', TamSX3('C1_QTDORIG')[1], 0} )

    elseif nQuery == 6
        oSelf:AddMapFields('branch'            , 'C8_FILIAL' , .T., .T., {'C8_FILIAL' , 'C', TamSX3('C8_FILIAL' )[1], 0})
        oSelf:AddMapFields('quotationCode'     , 'C8_NUM'    , .T., .T., {'C8_NUM'    , 'C', TamSX3('C8_NUM'    )[1], 0})
        oSelf:AddMapFields('productCode'       , 'C8_PRODUTO', .T., .T., {'C8_PRODUTO', 'C', TamSX3('C8_PRODUTO')[1], 0})
        oSelf:AddMapFields('itemseq'           , 'C8_ITEM'   , .T., .T., {'C8_ITEM'   , 'C', TamSX3('C8_ITEM'   )[1], 0})
        oSelf:AddMapFields('productDescription', 'B1_DESC'   , .T., .F., {'B1_DESC'   , 'C', TamSX3('B1_DESC'   )[1], 0})
        oSelf:AddMapFields('productIDTrib'     , 'C8_IDTRIB' , .T., .F., {'C8_IDTRIB' , 'C', TamSX3('C8_IDTRIB' )[1], 0})
        oSelf:AddMapFields('corporateName'     , 'C8_FORNOME', .T., .F., {'C8_FORNOME', 'C', TamSX3('C8_FORNOME')[1], 0})
        oSelf:AddMapFields('supplier'          , 'C8_FORNECE', .T., .T., {'C8_FORNECE', 'C', TamSX3('C8_FORNECE')[1], 0})
        oSelf:AddMapFields('store'             , 'C8_LOJA'   , .T., .T., {'C8_LOJA'   , 'C', TamSX3('C8_LOJA'   )[1], 0})
        oSelf:AddMapFields('numsc'             , 'C8_NUMSC'  , .T., .F., {'C8_NUMSC'  , 'C', TamSX3('C8_NUMSC'  )[1], 0})        
        oSelf:AddMapFields('itemsc'            , 'C8_ITEMSC' , .T., .F., {'C8_ITEMSC' , 'C', TamSX3('C8_ITEMSC' )[1], 0})  

        If cDataBase  == 'MSSQL'
			oSelf:AddMapFields('recno', 'R_E_C_N_O_', .T., .T., {'R_E_C_N_O_', 'N', ,0},'SC8.')
		ElseIf cDataBase  $ ('ORACLE|POSTGRES') 
			oSelf:AddMapFields('recno', 'R_E_C_N_O_', .T., .T., {'R_E_C_N_O_', 'N', ,0},'SC8.R_E_C_N_O_')
		EndIf

    EndIf
Return Nil

/*/{Protheus.doc} GetQuery
	Monta a expressão SQL para consulta dos fornecedores/itens da cotação.
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada, 6- Listagem de Impostos.
@return cQuery, string, query dos fornecedores/itens da cotação.
/*/
Static Function GetQuery(nQuery, oSelf)
    Local cQuery    As Character
    Local nFor      := 0
    Local nTamMap   := len(oSelf:oJsonObj:aMapFields)
    Default nQuery  := 0
    
    If nQuery == 1 .Or. nQuery == 3
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SC8' ) + " SC8"
        cQuery += " LEFT JOIN "+ RetSqlName( 'SA2' ) + " SA2"
        cQuery += "     ON SA2.A2_FILIAL = '" + FWxFilial('SA2') + "'"
        cQuery += "     AND SA2.A2_COD = SC8.C8_FORNECE"
        cQuery += "     AND SA2.A2_LOJA = SC8.C8_LOJA"
        cQuery += "     AND SA2.D_E_L_E_T_ = ' '"
        cQuery += " LEFT JOIN " + RetSqlName( 'SE4' ) + " SE4 ON"
        cQuery += "     SE4.E4_FILIAL = '" + FWxFilial('SE4') + "'"
        cQuery += "     AND SE4.E4_CODIGO = SC8.C8_COND"
        cQuery += "     AND SE4.D_E_L_E_T_ = ' '"
        cQuery += " WHERE #QueryWhere#"
        cQuery += " GROUP BY"
        cQuery += "     SC8.C8_FILIAL, SC8.C8_NUM, SC8.C8_NUMPRO, SC8.C8_FORNECE,"
        cQuery += "     SC8.C8_LOJA, SC8.C8_MOEDA, SC8.C8_EMISSAO, SC8.C8_FORNOME, SA2.A2_CGC, SA2.A2_TIPO, SA2.A2_END,"
        cQuery += "     SA2.A2_BAIRRO, SA2.A2_EST, SA2.A2_MUN, SA2.A2_CEP, SA2.A2_DDI,"
        cQuery += "     SA2.A2_DDD, SA2.A2_TEL, SC8.C8_FORMAIL, SC8.C8_COND, SC8.C8_TPFRETE,"
        cQuery += "     SE4.E4_DESCRI"
    ElseIf nQuery == 2 .Or. nQuery == 4
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SC8' ) + " SC8"
        cQuery += " INNER JOIN " + RetSqlName( 'SB1' ) + " SB1 ON"
        cQuery += "     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "'" 
        cQuery += "     AND SB1.B1_COD = SC8.C8_PRODUTO"
        cQuery += "     AND SB1.D_E_L_E_T_ = ' '"
        cQuery += " LEFT JOIN " + RetSqlName( 'SA2' ) + " SA2 ON"
        cQuery += "     SA2.A2_FILIAL = '" + FWxFilial('SA2') + "'" 
        cQuery += "     AND SA2.A2_COD = SC8.C8_FORNECE"
        cQuery += "     AND SA2.A2_LOJA = SC8.C8_LOJA"
        cQuery += "     AND SA2.D_E_L_E_T_ = ' '"
        cQuery += " LEFT JOIN " + RetSqlName( 'SE4' ) + " SE4 ON"
        cQuery += "     SE4.E4_FILIAL = '" + FWxFilial('SE4') + "'"
        cQuery += "     AND SE4.E4_CODIGO = SC8.C8_COND"
        cQuery += "     AND SE4.D_E_L_E_T_ = ' '"
        cQuery += " INNER JOIN " + RetSqlName( 'SC1' ) + " SC1 ON"
        cQuery += "     SC1.C1_FILIAL = '" + FWxFilial('SC1') + "'"
        cQuery += "     AND SC1.C1_NUM = SC8.C8_NUMSC"
        cQuery += "     AND SC1.C1_PRODUTO = SC8.C8_PRODUTO"
        cQuery += "     AND SC1.C1_IDENT = SC8.C8_IDENT"
        cQuery += "     AND SC1.D_E_L_E_T_ = ' '"
        cQuery += " WHERE #QueryWhere#"
        cQuery += " GROUP BY "
        for nFor := 1 to nTamMap
            if ( lower(oSelf:oJsonObj:aMapFields[nFor][1]) != 'recno' )
                cQuery += oSelf:oJsonObj:aMapFields[nFor][2] + ","
            else
                cQuery += "SC8.R_E_C_N_O_," //para evitar o apelido no group by
            endif
        next
        if ( Right(cQuery, 1) == "," )
            cQuery := substr(cQuery, 1, len(cQuery) -1)
        endif
      
    elseif nQuery == 5
        cQuery := " SELECT #QueryFields#"
        cQuery += "     FROM " + RetSqlName( 'SC1' ) + " SC1 "
        cQuery += " WHERE #QueryWhere#"

    elseif nQuery == 6
        cQuery := " SELECT #QueryFields#"
        cQuery += " FROM " + RetSqlName( 'SC8' ) + " SC8"
        cQuery += " INNER JOIN " + RetSqlName( 'SB1' ) + " SB1 ON"
        cQuery += "     SB1.B1_FILIAL = '" + FWxFilial('SB1') + "'" 
        cQuery += "     AND SB1.B1_COD = SC8.C8_PRODUTO"
        cQuery += "     AND SB1.D_E_L_E_T_ = ' '"
        cQuery += " LEFT JOIN " + RetSqlName( 'SA2' ) + " SA2 ON"
        cQuery += "     SA2.A2_FILIAL = '" + FWxFilial('SA2') + "'" 
        cQuery += "     AND SA2.A2_COD = SC8.C8_FORNECE"
        cQuery += "     AND SA2.A2_LOJA = SC8.C8_LOJA"
        cQuery += "     AND SA2.D_E_L_E_T_ = ' '"
        cQuery += " WHERE #QueryWhere#"
        cQuery += " GROUP BY "
        for nFor := 1 to nTamMap
            if ( lower(oSelf:oJsonObj:aMapFields[nFor][1]) != 'recno' )
                cQuery += oSelf:oJsonObj:aMapFields[nFor][2] + ","
            else
                cQuery += "SC8.R_E_C_N_O_," //para evitar o apelido no group by
            endif
        next
        if ( Right(cQuery, 1) == "," )
            cQuery := substr(cQuery, 1, len(cQuery) -1)
        endif
    EndIf
Return cQuery
 
/*/{Protheus.doc} GetWhere
	Monta a expressão SQL WHERE para consulta dos fornecedores/itens da cotação.
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada, 6- Listagem de Impostos.
@param lNewProposal, logical, indica se é nova proposta.
@param cCodeQuot, string, número da cotação
@param cCodIdent, string, código de identificação e rastreio
@return cQuery, string, expressão where para consulta dos fornecedores/itens da cotação.
/*/
Static Function GetWhere(nQuery, lNewProposal, cCodeQuot, cCodIdent)
    Local cWhere        As Character
    default cCodeQuot   := ""

    cWhere := " SC8.C8_FILIAL = '"+ FWxFilial('SC8') +"'"

    If nQuery == 1 .Or. (nQuery == 2 .And. !lNewProposal) .Or. nQuery == 6//-- Cláusula Where para retornar apenas a última proposta dos fornecedores
        cWhere += " AND SC8.C8_NUMPRO = "
    ElseIf nQuery == 3 //-- Histórico de propostadas, não deve considerar a proposta atual
        cWhere += " AND SC8.C8_NUMPRO NOT IN "
    EndIf
    
    If nQuery == 1 .Or. (nQuery == 2 .And. !lNewProposal) .Or. nQuery == 3 .Or. nQuery == 6
        cWhere += " ("
        cWhere += "     SELECT MAX(SC8_2.C8_NUMPRO)"
        cWhere += "     FROM "+ RetSQLName("SC8") +" SC8_2"
        cWhere += "     WHERE SC8_2.C8_FILIAL = SC8.C8_FILIAL AND"
        cWhere += "         SC8_2.C8_NUM = SC8.C8_NUM AND"
        cWhere += "         SC8_2.C8_FORNECE = SC8.C8_FORNECE AND"
        cWhere += "         SC8_2.C8_FORNOME = SC8.C8_FORNOME AND"
        cWhere += "         SC8_2.C8_LOJA = SC8.C8_LOJA AND"
        cWhere += "         SC8_2.D_E_L_E_T_ = ' '"
        cWhere += " )"
    EndIf

    cWhere += " AND SC8.D_E_L_E_T_ = ' '"

	if nQuery == 5
        cWhere := " SC1.C1_FILIAL = '" + xFilial("SC1") +  "' "
        cWhere += "    AND SC1.C1_COTACAO = '" + cCodeQuot + "' "
        cWhere += "    AND SC1.C1_IDENT = '"   + cCodIdent + "' "
        cWhere += "    AND SC1.D_E_L_E_T_ = ' ' "
    endif

Return cWhere

/*/{Protheus.doc} TreatJSONResponse
	Trata retorno do JSON gerado pelo FWAdapter.
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de Fornecedores; 2- Listagem de itens, 3- Histórico de propostas, 4- Histórico de itens da proposta, 5- Itens originais da Compra Centralizada, 6- Listagem de Impostos.
@param oJsonResponse, object, JSON gerado pelo FWAdapter
@return Nil, nulo.
/*/
Method TreatJSONResponse(nQuery, oJsonResponse) Class pgcUpdateQuotationRepository
    Default nQuery := 1
    Default oJsonResponse := Nil

    If nQuery == 1 .Or. nQuery == 2
        Self:aRefTax := MaFisRelImp("MT161", {"SC8"})
    EndIf

    If nQuery == 1 .Or. nQuery == 3 //-- Fornecedores
        Self:setQuotesStatus(oJsonResponse)
		NFCSetContactList(oJsonResponse,2)

        If nQuery == 1
            oJsonResponse['mv_selfor'] := SuperGetMv('MV_SELFOR',.F.,'N') == 'S' //-- Adiciona informação do parâmetro MV_SELFOR
            Self:getProposalsHistory(oJsonResponse)
			Self:getCurrencyName(oJsonResponse)
        EndIf
    
    ElseIf nQuery == 2 .Or. nQuery == 4 //-- Itens da cotação
        Self:addOthersDataFields(oJsonResponse)
    
    elseif nQuery == 5
        Self:addProductDescription(oJsonResponse)
    elseif nQuery == 6
        Self:addTaxesTCIWritten(oJsonResponse)

    EndIf

    FwFreeArray(Self:aRefTax)
Return Nil

/*/{Protheus.doc} setQuotesStatus
	Adiciona as propriedades de status e statuscode aos fornecedores da cotação
@author juan.felipe
@since 22/12/2022
@param oJsonResponse, object, JSON que receberá as propriedades.
@return Nil, nulo.
/*/
Method setQuotesStatus(oJsonResponse) Class pgcUpdateQuotationRepository
    Local aItems As Array
    Local nX As Numeric
    Local cAliasTemp As Character
    Local cStatus As Character
    Local cStatusCode As Character
    Local cQuery As Character
    Local oQuery As Object
    Local lWorkflow As Logical
    Default oJsonResponse := Nil

    If oJsonResponse <> Nil .And. oJsonResponse:hasProperty('items')
        aItems := oJsonResponse['items']

        If Len(aItems) > 0
            cQuery := "SELECT"
            cQuery += "   SC8.C8_FILIAL,"
            cQuery += "   SC8.C8_NUM,"
            cQuery += "   SC8.C8_FORNECE,"
            cQuery += "   SC8.C8_FORNOME,"
            cQuery += "   SC8.C8_LOJA, "
            cQuery += "   SC8.C8_NUMPRO,"
            cQuery += "   (   SELECT COUNT(SC8_3.C8_ITEM)"
            cQuery += "       FROM "+ RetSQLName("SC8") +" SC8_3"
            cQuery += "       WHERE SC8_3.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "           AND SC8_3.C8_NUM = SC8.C8_NUM"
            cQuery += "           AND SC8_3.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "           AND SC8_3.C8_LOJA = SC8.C8_LOJA"
            cQuery += "           AND SC8_3.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "           AND SC8_3.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "           AND SC8_3.C8_SITUAC IN(' ','1')"
            cQuery += "           AND SC8_3.C8_TOTAL = 0"
            cQuery += "           AND SC8_3.D_E_L_E_T_ = ' '"
            cQuery += "   ) PENDINGITEMS," //-- Quantidade de itens pendentes
            cQuery += "   (   SELECT COUNT(SC8_8.C8_ITEM)"
            cQuery += "       FROM "+ RetSQLName("SC8") +" SC8_8"
            cQuery += "    INNER JOIN "+ RetSQLName("DHV") +" DHV"
            cQuery += "            ON DHV.DHV_FILIAL = SC8_8.C8_FILIAL"
            cQuery += "           AND DHV.DHV_NUM = SC8_8.C8_NUM"
            cQuery += "           AND DHV.DHV_ITEM = SC8_8.C8_ITEM"
            cQuery += "           AND DHV.D_E_L_E_T_ = ' '"
            cQuery += "       WHERE SC8_8.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "           AND SC8_8.C8_NUM = SC8.C8_NUM"
            cQuery += "           AND SC8_8.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "           AND SC8_8.C8_LOJA = SC8.C8_LOJA"
            cQuery += "           AND SC8_8.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "           AND SC8_8.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "           AND SC8_8.C8_SITUAC IN(' ','1')"
            cQuery += "           AND ( SC8_8.C8_QUANT > SC8_8.C8_QTDISP OR SC8_8.C8_QTDISP < DHV.DHV_SALDO )"
            cQuery += "           AND SC8_8.C8_TOTAL > 0"
            cQuery += "           AND SC8_8.D_E_L_E_T_ = ' '"
            cQuery += "   ) PARTIALLYATTENDED," //-- Quantidade de itens parcialmente atendidos
            cQuery += "   (    SELECT COUNT(SC8_4.C8_ITEM)"
            cQuery += "        FROM "+ RetSQLName("SC8") +" SC8_4"
            cQuery += "    INNER JOIN "+ RetSQLName("DHV") +" DHV"
            cQuery += "            ON DHV.DHV_FILIAL = SC8_4.C8_FILIAL"
            cQuery += "           AND DHV.DHV_NUM = SC8_4.C8_NUM"
            cQuery += "           AND DHV.DHV_ITEM = SC8_4.C8_ITEM"
            cQuery += "           AND DHV.D_E_L_E_T_ = ' '"
            cQuery += "        WHERE SC8_4.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "            AND SC8_4.C8_NUM = SC8.C8_NUM"
            cQuery += "            AND SC8_4.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "            AND SC8_4.C8_LOJA = SC8.C8_LOJA"
            cQuery += "            AND SC8_4.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "            AND SC8_4.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "            AND ( SC8_4.C8_QUANT = SC8_4.C8_QTDISP OR SC8_4.C8_QTDISP = DHV.DHV_SALDO OR DHV.DHV_SALDO = 0 )"
            cQuery += "            AND SC8_4.C8_TOTAL > 0"
            cQuery += "            AND SC8_4.D_E_L_E_T_ = ' '"
            cQuery += "   ) ATTENDITEMS," //-- Quantidade de itens atendidos
            cQuery += "   (    SELECT COUNT(SC8_5.C8_ITEM)"
            cQuery += "        FROM "+ RetSQLName("SC8") +" SC8_5"
            cQuery += "        WHERE SC8_5.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "            AND SC8_5.C8_NUM = SC8.C8_NUM"
            cQuery += "            AND SC8_5.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "            AND SC8_5.C8_LOJA = SC8.C8_LOJA"
            cQuery += "            AND SC8_5.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "            AND SC8_5.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "            AND SC8_5.C8_SITUAC = '4'"
            cQuery += "            AND SC8_5.D_E_L_E_T_ = ' '"
            cQuery += "   ) REFUSED," //-- Cotação recusada
            cQuery += "   (    SELECT COUNT(SC8_6.C8_ITEM)"
            cQuery += "        FROM "+ RetSQLName("SC8") +" SC8_6"
            cQuery += "        WHERE SC8_6.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "            AND SC8_6.C8_NUM = SC8.C8_NUM"
            cQuery += "            AND SC8_6.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "            AND SC8_6.C8_LOJA = SC8.C8_LOJA"
            cQuery += "            AND SC8_6.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "            AND SC8_6.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "            AND SC8_6.C8_SITUAC = '5'"
            cQuery += "            AND SC8_6.D_E_L_E_T_ = ' '"
            cQuery += "   ) DISQUALIFIED," //-- Cotação desqualificada
            cQuery += "   (    SELECT COUNT(SC8_6.C8_ITEM)"
            cQuery += "        FROM "+ RetSQLName("SC8") +" SC8_6"
            cQuery += "        WHERE SC8_6.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "            AND SC8_6.C8_NUM = SC8.C8_NUM"
            cQuery += "            AND SC8_6.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "            AND SC8_6.C8_LOJA = SC8.C8_LOJA"
            cQuery += "            AND SC8_6.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "            AND SC8_6.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "            AND SC8_6.C8_SITUAC IN ('2','3')"
            cQuery += "            AND SC8_6.D_E_L_E_T_ = ' '"
            cQuery += "   ) NOTATTEND," //-- Cotações sem estoque/não vende
            cQuery += "   (    SELECT COUNT(SC8_7.C8_ITEM)"
            cQuery += "        FROM "+ RetSQLName("SC8") +" SC8_7"
            cQuery += "        WHERE SC8_7.C8_FILIAL = SC8.C8_FILIAL"
            cQuery += "            AND SC8_7.C8_NUM = SC8.C8_NUM"
            cQuery += "            AND SC8_7.C8_FORNECE = SC8.C8_FORNECE"
            cQuery += "            AND SC8_7.C8_LOJA = SC8.C8_LOJA"
            cQuery += "            AND SC8_7.C8_NUMPRO = SC8.C8_NUMPRO"
            cQuery += "            AND SC8_7.C8_FORNOME = SC8.C8_FORNOME"
            cQuery += "            AND SC8_7.C8_WF = 'T'"
            cQuery += "            AND SC8_7.D_E_L_E_T_ = ' '"
            cQuery += "   ) WORKFLOW" //-- Cotação respondida via workflow
            
            cQuery += " FROM "+ RetSQLName("SC8") +" SC8"
            cQuery += " WHERE SC8.C8_FILIAL = ?"
            cQuery += "     AND SC8.C8_NUM = ?"
            cQuery += "     AND SC8.C8_FORNECE = ?"
            cQuery += "     AND SC8.C8_LOJA = ?"
            cQuery += "     AND SC8.C8_NUMPRO = ?"
            cQuery += "     AND SC8.C8_FORNOME = ?"
            cQuery += "     AND SC8.D_E_L_E_T_ = ' '"
            cQuery += " GROUP BY SC8.C8_FILIAL, SC8.C8_NUM, SC8.C8_FORNECE,"
            cQuery += " SC8.C8_LOJA, SC8.C8_NUMPRO, C8_FORNOME"
        
            oQuery := FWPreparedStatement():New(cQuery)

            For nX := 1 To Len(aItems)
                oQuery:SetString(1, FWxFilial('SC8'))
                oQuery:SetString(2, aItems[nX]['quotationcode'])
                oQuery:SetString(3, aItems[nX]['supplier'])
                oQuery:SetString(4, aItems[nX]['store'])
                oQuery:SetString(5, aItems[nX]['proposal'])
                oQuery:SetString(6, aItems[nX]['corporatename'])

				cAliasTemp := GetNextAlias()
                cAliasTemp := MpSysOpenQuery(oQuery:getFixQuery())

                aItems[nX]['quantitypendingitems'] := 0 //-- Adiciona propriedade de itens pendentes
                aItems[nX]['quantityattenditems'] := 0 //-- Adiciona propriedade de itens atendidos
                aItems[nX]['status'] := STR0008 //-- Adiciona propriedade status (por padrão "Sem status")
                aItems[nX]['statuscode'] := "0" //-- Adiciona propriedade statuscode (por padrão "0")

                If !(cAliasTemp)->(Eof())
                    aItems[nX]['quantitypendingitems'] := (cAliasTemp)->PENDINGITEMS //-- Adiciona propriedade de itens pendentes
                    aItems[nX]['quantityattenditems'] := (cAliasTemp)->ATTENDITEMS

                    Do Case //-- Trata propriedade status
                        Case (cAliasTemp)->REFUSED >= 1
                            aItems[nX]['status'] := STR0009 //-- Recusada
                            aItems[nX]['statuscode'] := "4"

                        Case (cAliasTemp)->DISQUALIFIED >= 1
                            aItems[nX]['status'] := STR0010 //-- Desqualificada
                            aItems[nX]['statuscode'] := "5"

                        Case aItems[nX]['quantityitems'] == (cAliasTemp)->NOTATTEND
                            lWorkflow := (cAliasTemp)->WORKFLOW > 0
                            cStatus := Iif(lWorkflow, STR0031, STR0029) //-- Não atende (E-mail) | Não atende
                            cStatusCode := Iif(lWorkflow, "9", "8")
                            aItems[nX]['status'] := cStatus
                            aItems[nX]['statuscode'] := cStatusCode

                        Case (cAliasTemp)->ATTENDITEMS == 0 .And. (cAliasTemp)->PARTIALLYATTENDED == 0
                            aItems[nX]['status'] := STR0011 //-- Pendente
                            aItems[nX]['statuscode'] := "1"

                        Case aItems[nX]['quantityitems'] > (cAliasTemp)->ATTENDITEMS .And. ((cAliasTemp)->ATTENDITEMS > 0 .Or. (cAliasTemp)->PARTIALLYATTENDED > 0)
                            lWorkflow := (cAliasTemp)->WORKFLOW > 0
                            cStatus := Iif(lWorkflow, STR0016, STR0012) //-- Parcial (E-mail) | Parcial
                            cStatusCode := Iif(lWorkflow, "6", "2")
                            aItems[nX]['status'] := cStatus
                            aItems[nX]['statuscode'] := cStatusCode

                        Case aItems[nX]['quantityitems'] == (cAliasTemp)->ATTENDITEMS
                            lWorkflow := (cAliasTemp)->WORKFLOW > 0
                            cStatus := Iif(lWorkflow, STR0017, STR0013) //-- Completa (E-mail) | Completa
                            cStatusCode := Iif(lWorkflow, "7", "3")
                            aItems[nX]['status'] := cStatus
                            aItems[nX]['statuscode'] := cStatusCode
                    EndCase
                EndIf

                (cAliasTemp)->(dbCloseArea())
            Next nX

            oQuery:Destroy()
        EndIf
    EndIf
Return Nil

/*/{Protheus.doc} addOthersDataFields
	Adiciona a propriedade observation aos itens da cotação.
@author juan.felipe
@since 22/12/2022
@param oJsonResponse, object, JSON que receberá a propriedade.
@return Nil, nulo.
/*/
Method addOthersDataFields(oJsonResponse) Class pgcUpdateQuotationRepository
    Local aAreas As Array
    Local aItems As Array
    Local aCustomFields As Array
    Local aCustomData As Array
	Local cCopyFields As Character
    Local cMessage As Character
    Local cNewProposal As Character
    Local lNewProposalOk As Logical
    Local lHasSupObs As Logical
    Local nX As Numeric
    Local nZ As Numeric
    Local nLenNumPro As Numeric
    Local nCost As Numeric
    Local oUtils As Object
    Local dDtCustom As Date
    Default oJsonResponse := Nil
    Private l150Auto := .F.

    A150SetPGC(.T.)

    oUtils := pgcUtils():New()

    aCustomFields  := {}
    aCustomFields  := oUtils:getCustomFields('SC8')
	cCopyFields    := PGCSC8Copy(.T., Self:lNewParticipant .Or. Self:lCleanProposal)
    cMessage       := ''
    lNewProposalOk := .T.
    nLenNumPro     := TamSX3('C8_NUMPRO')[1]
    lHasSupObs     := SC8->(FieldPos('C8_OBSFOR')) > 0

    If oJsonResponse <> Nil .And. oJsonResponse:hasProperty('items')
        aItems := oJsonResponse['items']

        If Len(aItems) > 0
            Self:aRefTax := MaFisRelImp("MT161", {"SC8"})

            aAreas := {SC8->(GetArea()), GetArea()}

            For nX := 1 To Len(aItems)
                SC8->(dbGoTo(aItems[nX]['recno']))
                lNewProposalOk := Self:lNewProposal .And. A150Propos(,,,@cMessage) == 1

                If Self:lOk := !Self:lNewProposal .Or. lNewProposalOk

                    aItems[nX]['observation'] := FWHttpEncode(SC8->C8_OBS) //-- Adiciona propriedade observação
                    
                    If FindFunction('NFCProdDesc')
                        aItems[nX]['productdescription'] := NFCProdDesc(SC8->C8_PRODUTO, SC8->C8_NUMSC, SC8->C8_ITEMSC)
                    EndIf

                    If lHasSupObs
                        aItems[nX]['supplierobservation'] := FwHttpEncode(SC8->C8_OBSFOR)
                    EndIf

                    If Self:lNewProposal .Or. Self:lNewParticipant //-- Trata informações a serem retornadas na geração de nova proposta
                        If nX == 1 .And. !Self:lNewParticipant
                            cNewProposal := Soma1(PGCLastProp(SC8->C8_NUM, SC8->C8_FORNECE, SC8->C8_LOJA, SC8->C8_FORNOME)) //-- Retorna o número da próxima proposta a ser gerada
                        EndIf
                        
						If !Self:lNewParticipant
							aItems[nX]['proposal'] := cNewProposal
						EndIf

                        If Self:lCleanProposal .Or. Self:lNewParticipant //-- Limpa campos da nova proposta
                            oUtils:clearJsonFields(aItems[nX], cCopyFields)
							
							If Self:lNewParticipant //-- Limpa campos para o novo participante
								aItems[nX]['originsupplier'] := JsonObject():New()
								aItems[nX]['originsupplier']['supplier'     ] := aItems[nX]['supplier']
								aItems[nX]['originsupplier']['store'        ] := aItems[nX]['store']
								aItems[nX]['originsupplier']['corporatename'] := aItems[nX]['corporatename']
								aItems[nX]['originsupplier']['proposal'     ] := aItems[nX]['proposal']

								aItems[nX]['supplier']      := ''
								aItems[nX]['store']         := ''
								aItems[nX]['corporatename'] := ''
								aItems[nX]['proposal']      := PadL('1', nLenNumPro, '0')
								aItems[nX]['email']         := ''
								aItems[nX]['contact']       := ''
							EndIf

                            aItems[nX]['availablequantity'] := aItems[nX]['quantity']
                            aItems[nX]['situation'] := '1'
                            aItems[nX]['currency'] := 1
						EndIf
					EndIf

					aItems[nX]['situation'] := IIf(aItems[nX]['situation'] == '4', '1', aItems[nX]['situation'])

                    if len(aCustomFields) > 0
                        aCustomData := {}
                        aAdd(aCustomData, JsonObject():New())
                        for nZ := 1 to len(aCustomFields)
                            if( valtype(&("SC8->"+aCustomFields[nZ])) == 'D' )
                                dDtCustom := &("SC8->"+aCustomFields[nZ])
                                aCustomData[1][UPPER(aCustomFields[nZ])] := Year2Str(dDtCustom)+'-'+Month2Str(dDtCustom)+'-'+Day2Str(dDtCustom)
                            elseif valtype(&("SC8->"+aCustomFields[nZ])) == 'C'
                                aCustomData[1][UPPER(aCustomFields[nZ])] := Alltrim(&("SC8->"+aCustomFields[nZ]))
                            else
                                aCustomData[1][UPPER(aCustomFields[nZ])] := &("SC8->"+aCustomFields[nZ])
                            endif
                            
                        next nZ

                        If Self:lNewProposal .And. Self:lCleanProposal //-- Limpa campos customizados
                            oUtils:clearJsonFields(aCustomData[1])
                        EndIf

                        aItems[nX]['customfields'] := {}
                        aItems[nX]['customfields'] := aCustomData
                    endif
                Else
                    Self:nCode := 400
                    Self:cMessage := cMessage
                EndIf

                If Self:lNFCCost .And. !Self:lNewProposal .And. !Self:lNewParticipant
                    nCost := NFCGetCost(Self:aRefTax, SC8->C8_NUM, SC8->C8_FORNECE, SC8->C8_LOJA, SC8->C8_FORNOME, SC8->C8_NUMPRO, .F.)

                    If nCost > 0
                        aItems[nX]['total'] := nCost
                    EndIf
                EndIf
            Next nX

            aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
        EndIf
    EndIf
Return Nil

/*/{Protheus.doc} putUpdateQuotation
	Atualiza cotação.
@author juan.felipe
@since 08/11/2022
/*/
Method putUpdateQuotation() Class pgcUpdateQuotationRepository
    Local aAreas As Array
    Local aError As Array
    Local aItems As Array
    Local cQuotationCode As Character
    Local cProduct As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cProposal As Character
    Local cProposalAux As Character
    Local cCorporateName As Character
    lOCAL cCorporateNameAux As Character
    Local cItem As Character
    Local cGridItem As Character
    Local cSituation As Character
	Local cCopyFields As Character
    Local cMessage As Character
    Local nX As Numeric
	Local nZ As Numeric
    Local nDeadLine As Numeric
    Local nTotal As Numeric
    Local nTotalItems As Numeric
    Local nShipValue As Numeric
    Local nTotalQuoteShip As Numeric
    Local nTotalQuoteExpense As Numeric
    Local nTotalQuoteInsurance As Numeric
    Local nInsuranceValue As Numeric
    Local nExpenseValue As Numeric
    Local nTotalInsurance As Numeric
    Local nTotalExpense As Numeric
    Local nLenProd As Numeric
    Local nLenSup As Numeric
    Local nLenStore As Numeric
    Local nLenItem As Numeric
    Local nLenNumPro As Numeric
    Local nLenSupName As Numeric
    Local nLenItemGrid As Numeric
    Local nCode As Numeric
    Local lNewProposalOk As Logical
    Local oModel As Object
    Local oModelDHU As Object
    Local oModelSC8 As Object
    Local oJsonRet As Object
    Local oJsonFieldsData As Object
    Local oUtils As Object
	Local nMoeda As Numeric
	Local aCustomFields As Array
    Private l150Auto := .F.
    
    A150SetPGC(.T.)
    
    aAreas := {DHU->(GetArea()), GetArea()}
    aError   := {}
	cCopyFields := PGCSC8Copy(.F., Self:lNewParticipant)
    cMessage := ''
    lNewProposalOk := .F.
    nLenProd := TamSX3('C8_PRODUTO')[1]
    nLenSup := TamSX3('C8_FORNECE')[1]
    nLenStore := TamSX3('C8_LOJA'   )[1]
    nLenItem := TamSX3('C8_ITEM'   )[1]
    nLenNumPro := TamSX3('C8_NUMPRO' )[1]
    nLenSupName := TamSX3('C8_FORNOME')[1]
    nLenItemGrid := TamSX3('C8_ITEMGRD')[1]

    oJsonRet := JsonObject():New()

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    cQuotationCode := Self:oJsonRequest:quotationcode

    If Self:lOk := DHU->(MsSeek(FWxFilial('DHU') + cQuotationCode))
        PG020SetOp(MODEL_OPERATION_UPDATE)

        aItems          := Self:oJsonRequest:items
        nDeadLine       := Self:oJsonRequest:deadline
        nTotalItems     := Self:oJsonRequest:totalitems
        nTotalShip      := Self:oJsonRequest:totalship
        nTotalExpense   := Self:oJsonRequest:totalexpense
        nTotalInsurance := Self:oJsonRequest:totalinsurance
		nMoeda          := Self:oJsonRequest:currency
        nTotalQuoteShip      := 0
        nTotalQuoteExpense   := 0
        nTotalQuoteInsurance := 0

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()
        oModelDHU := oModel:GetModel('DHUMASTER')
        oModelSC8 := oModel:GetModel('SC8DETAIL')
        oUtils := pgcUtils():New()

        For nX := 1 To Len(aItems) //-- Percorre itens da cotação para preencher modelo de dados da SC8
            If Self:lNewParticipant
                cSupplier      := aItems[nX]:originsupplier:supplier
                cStore         := aItems[nX]:originsupplier:store
                cProposal      := aItems[nX]:originsupplier:proposal
                cCorporateName := Upper(aItems[nX]:originsupplier:corporatename)
            Else
                cSupplier      := aItems[nX]:supplier
                cStore         := aItems[nX]:store
                cProposal      := aItems[nX]:proposal
                cCorporateName := Upper(aItems[nX]:corporatename)
            EndIf

            cProduct  := aItems[nX]:productcode
            cItem     := aItems[nX]:item
            cGridItem := aItems[nX]:griditem

            cProduct       := PadR(cProduct      , nLenProd    , ' ')
            cSupplier      := PadR(cSupplier     , nLenSup     , ' ')
            cStore         := PadR(cStore        , nLenStore   , ' ')
            cItem          := PadR(cItem         , nLenItem    , ' ')
            cProposal      := PadR(cProposal     , nLenNumPro  , ' ')
            cCorporateName := PadR(cCorporateName, nLenSupName , ' ')
            cGridItem      := PadR(cGridItem     , nLenItemGrid, ' ')
            cSituation     := aItems[nX]:situation
            cProposalAux   := IIf(Self:lNewProposal, Tira1(cProposal), cProposal)
            cCorporateNameAux := PGCCarEsp(FwCutOff(Upper(AllTrim(aItems[nX]:corporatename)), .T.))
            
            If Self:lOk := oModelSC8:SeekLine({{"C8_PRODUTO",cProduct},{"C8_FORNECE",cSupplier},{"C8_LOJA",cStore},{"C8_ITEM",cItem},{"C8_NUMPRO",cProposalAux},{"C8_FORNOME",cCorporateName},{"C8_ITEMGRD",cGridItem}}) //-- Busca linha referente a proposta da cotação
                If nX == 1 .And. !Self:lNewParticipant .And. Self:lNewProposal
                    SC8->(dbGoTo(oModelSC8:GetDataId()))
                    lNewProposalOk := A150Propos(,,,@cMessage) == 1
                EndIf

                If Self:lOk := !Self:lNewProposal .Or. lNewProposalOk
                    If Self:lNewProposal .Or. Self:lNewParticipant //-- Adiciona nova proposta
                        oJsonFieldsData := oUtils:lineMVCToJson(oModelSC8, oModelSC8:GetLine(), cCopyFields)

                        If Self:lOk := oUtils:AddLineMVCFromJson(oModelSC8, oJsonFieldsData)
                            oModelSC8:LoadValue('C8_ORIGEM' , 'PGCA020')
							oModelSC8:SetValue('C8_EMISSAO' , dDataBase)
                            oModelSC8:SetValue('C8_WF'      , .F.)

                            If Self:lNewParticipant
                                oModelSC8:LoadValue('C8_FORNECE', '')
                                oModelSC8:LoadValue('C8_LOJA'   , '')

                                If !Empty(aItems[nX]:supplier + aItems[nX]:store)
                                    oModelSC8:SetValue('C8_FORNECE', aItems[nX]:supplier)
                                    oModelSC8:SetValue('C8_LOJA'   , aItems[nX]:store)
                                EndIf

                                oModelSC8:SetValue('C8_FORNOME', cCorporateNameAux)
                                oModelSC8:SetValue('C8_FORMAIL', aItems[nX]:email)
                                oModelSC8:SetValue('C8_CONTATO', aItems[nX]:contact)
                                oModelSC8:LoadValue('C8_NUMPRO', aItems[nX]:proposal)
                            Else
                                oModelSC8:LoadValue('C8_NUMPRO', cProposal)
                            EndIf
                        EndIf
                    EndIf

                    If Self:lOk
                        oModelSC8:SetValue('C8_TPFRETE', Self:oJsonRequest:typeship)
                        oModelSC8:SetValue('C8_COND'   , Self:oJsonRequest:paymentcondition)
                        oModelSC8:LoadValue('C8_MOEDA' , nMoeda)
                        
                        If nMoeda > 1
                            oModelSC8:SetValue('C8_TXMOEDA', Self:oJsonRequest:currencyrate)
                        Endif

                        oModelSC8:LoadValue('C8_PRECO'  , aItems[nX]:unityvalue)
                        oModelSC8:LoadValue('C8_QTDISP' , aItems[nX]:availablequantity)
                        oModelSC8:LoadValue('C8_DESC'   , aItems[nX]:discount)
                        oModelSC8:LoadValue('C8_VLDESC' , aItems[nX]:discountvalue)
                        oModelSC8:LoadValue('C8_TOTAL'  , aItems[nX]:total)
                        
                        if Self:vldFieldsSX3('SC8', 'C8_PICM')
                            oModelSC8:LoadValue('C8_PICM'   , aItems[nX]:icmsaliquot)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_ALIIPI')
                            oModelSC8:LoadValue('C8_ALIIPI' , aItems[nX]:ipialiquot)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_VALICM')
                            oModelSC8:LoadValue('C8_VALICM' , aItems[nX]:icmsvalue)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_VALIPI')
                            oModelSC8:LoadValue('C8_VALIPI' , aItems[nX]:ipivalue)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_BASEICM')
                            oModelSC8:LoadValue('C8_BASEICM', aItems[nX]:icmsbase)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_BASEIPI')
                            oModelSC8:LoadValue('C8_BASEIPI', aItems[nX]:ipibase)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_BASESOL')
                            oModelSC8:LoadValue('C8_BASESOL', aItems[nX]:icmssolbase)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_ICMSCOM')
                            oModelSC8:LoadValue('C8_ICMSCOM', aItems[nX]:icmscomp)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_VALSOL')
                            oModelSC8:LoadValue('C8_VALSOL' , aItems[nX]:icmsst)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_ALIQCMP')
                            oModelSC8:LoadValue('C8_ALIQCMP', aItems[nX]:aliqicmscomp)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_VALISS')
                            oModelSC8:LoadValue('C8_VALISS' , aItems[nX]:iss)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_ALIQISS')
                            oModelSC8:LoadValue('C8_ALIQISS', aItems[nX]:aliqiss)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_DIFAL')
                            oModelSC8:LoadValue('C8_DIFAL'  , aItems[nX]:difal)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_BASEDES')
                            oModelSC8:LoadValue('C8_BASEDES', aItems[nX]:difalbase)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_PDORI')
                            oModelSC8:LoadValue('C8_PDORI'  , aItems[nX]:difalpori)
                        endif
                        if Self:vldFieldsSX3('SC8', 'C8_PDDES')
                            oModelSC8:LoadValue('C8_PDDES'  , aItems[nX]:difalpdes)
                        endif

                        oModelSC8:SetValue('C8_VALIDA' , SToD(aItems[nX]:expirationdate))
                        oModelSC8:SetValue('C8_PRAZO'  , IIf(nDeadLine > 0, nDeadLine, aItems[nX]:deadline))
                        oModelSC8:SetValue('C8_OBS'    , aItems[nX]:observation)
                        oModelSC8:SetValue('C8_SITUAC' , cSituation)

                        oModelSC8:SetValue('C8_TES'    , aItems[nX]:tes)

                        nTotal               := aItems[nX]:total
                        nExpenseValue        := Round((nTotal/nTotalItems)*nTotalExpense, 2) //-- Cálculos de despesa e seguro
                        nInsuranceValue      := Round((nTotal/nTotalItems)*nTotalInsurance, 2)
                        nTotalQuoteExpense   += nExpenseValue
                        nTotalQuoteInsurance += nInsuranceValue

                        oModelSC8:LoadValue('C8_DESPESA', nExpenseValue)
                        oModelSC8:LoadValue('C8_SEGURO' , nInsuranceValue)

                        If Self:oJsonRequest:typeship == 'C'
                            nShipValue      := Round((nTotal/nTotalItems)*nTotalShip, 2) //-- Cáculo de frete proporcional para cada item
                            nTotalQuoteShip += nShipValue

                            oModelSC8:LoadValue('C8_VALFRE', nShipValue)
                            oModelSC8:LoadValue('C8_TOTFRE', nTotalShip)
                        EndIf

                        //-- Verifica se há campos customizados a serem gravados e realiza a gravação.
                        If (AttIsMemberOf(aItems[nX], 'customfields') .and. len(aItems[nX]:customfields) > 0 )
                            oCustomAux := JsonObject():New()
                            oCustomAux:fromJson(aItems[nX]:customfields)
                            If(Len(oCustomAux) > 0)
                                aCustomFields := oCustomAux[1]:GetNames()
                                For nZ := 1 to len(aCustomFields)
                                    if (FWSX3Util():GetFieldType( aCustomFields[nZ]) == 'D')//Tratativa para campo Data, pois recebe do front formato yyyy-mm-aa
                                        oModelSC8:LoadValue(aCustomFields[nZ],StoD(StrTran(oCustomAux[1][aCustomFields[nZ]],'-','')))
                                    else
                                        oModelSC8:LoadValue(aCustomFields[nZ],oCustomAux[1][aCustomFields[nZ]])
                                    endif
                                
                                Next nZ
                            EndIf
                        Endif
                    EndIf
               
                    Self:lOk := !oModel:HasErrorMessage() .And. oModelSC8:VldLineData()

                    If !Self:lOk
                        cMessage := STR0001 + AllTrim(oModelSC8:GetValue('C8_PRODUTO')) + '-' + AllTrim(oModelSC8:GetValue('C8_ITEM')) //-- Erro no item XXXXX-XXXX
                        Exit
                    EndIf
                Else
                    cMessage := STR0022 //-- Não é possível incluir uma nova proposta, pois a última proposta ainda não foi atualizada.
                    Exit
                EndIf
            Else
                cMessage := STR0002 + cItem + STR0003 //-- Item XXX não foi localizado na base de dados.
                Exit
            EndIf
        Next nX

        If Self:lOk
            //-- Tratamento para pegar a diferença dos valores totais gerados para somar ou subitrair no último item.
            nShipValue := Self:ApportionmentDiffValues(nTotalQuoteShip, nTotalShip, oModelSC8:GetValue('C8_VALFRE'))
            oModelSC8:LoadValue('C8_VALFRE', nShipValue)

            nExpenseValue := Self:ApportionmentDiffValues(nTotalQuoteExpense, nTotalExpense, oModelSC8:GetValue('C8_DESPESA'))
            oModelSC8:LoadValue('C8_DESPESA', nExpenseValue)

            nInsuranceValue := Self:ApportionmentDiffValues(nTotalQuoteInsurance, nTotalInsurance, oModelSC8:GetValue('C8_SEGURO'))
            oModelSC8:LoadValue('C8_SEGURO', nInsuranceValue)

            If Self:lNewParticipant //-- Atualiza quantidade de fornecedores da cotação
                oModelDHU:SetValue('DHU_QTDFOR', oModelDHU:GetValue('DHU_QTDFOR') += 1)
            EndIf

            Self:lOk := oModel:VldData() .And. oModel:CommitData()
            
            If Self:lOk
                cMessage := IIf(Self:lNewProposal, STR0021, STR0004) + AllTrim(cCorporateNameAux) + STR0005 //-- Cotação do fornecedor XXX editada com sucesso.
            EndIf
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel)
        
        oModel:Destroy()
    Else
        cMessage := STR0006 + cQuotationCode + STR0007 //-- A cotação XXX não foi localizada na base de dados.
        oJsonRet := oUtils:setClassResponse(400, cMessage)
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
Return oJsonRet

/*/{Protheus.doc} putChangeQuotationSituation
	Altera situação da cotação.
    @param cSituation, character, situação da cotação 1-Considera; 4-Rejeitada; 5-Desqualificada; R-Requalifica.
@author juan.felipe
@since 15/12/2022
/*/
Method putChangeQuotationSituation(cSituation) Class pgcUpdateQuotationRepository
    Local aAreas As Array
    Local cQuotationCode As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cProposal As Character
    Local cCorporateName As Character
    Local cItem As Character
    Local cObservation as Character
    Local cMessage As Character
    Local cKeyRequest As Character
    Local cKeyModel As Character
    Local cSituationAux As Character
    Local cPurchaseCode As Character
    Local nX As Numeric
    Local nCode As Numeric
    Local lDisqBlocked As Logical
    Local oModel As Object
    Local oModelSC8 As Object
    Local oJsonRet As Object
    Local oUtils As Object
    Local cQuery := ''
	Local dDtrCot As Date

    Default cSituation := '1'
    
    aAreas := {DHU->(GetArea()), GetArea()}
    cMessage := ''
    lDisqBlocked := .F.
    oJsonRet := JsonObject():New()
    oUtils := pgcUtils():New()

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM
      
    cQuotationCode := PadR(Self:oJsonRequest:quotationcode, TamSX3('C8_NUM'    )[1], " ")
    cSupplier      := PadR(Self:oJsonRequest:supplier     , TamSX3('C8_FORNECE')[1], " ")
    cStore         := PadR(Self:oJsonRequest:store        , TamSX3('C8_LOJA'   )[1], " ")
    cProposal      := PadR(Self:oJsonRequest:proposal     , TamSX3('C8_NUMPRO' )[1], " ")
    cCorporateName := PadR(Self:oJsonRequest:corporatename, TamSX3('C8_FORNOME')[1], " ")
    cObservation   := ""
    if AttIsMemberOf(Self:oJsonRequest , "observation")
        cObservation := Alltrim(Self:oJsonRequest:observation)
    endif

    //Faz a consulta buscando pela SCE
    cQuery := "SELECT CE_NUMPED FROM "+ RetSQLName("SCE") +" SCE"
    cQuery += " WHERE SCE.CE_FILIAL = ?"
    cQuery += "   AND SCE.CE_NUMCOT = ?"
    cQuery += "   AND SCE.CE_FORNECE = ?"
    cQuery += "   AND SCE.CE_LOJA = ?"
    cQuery += "   AND SCE.D_E_L_E_T_ = ' '"

    oQuery := FWPreparedStatement():New(cQuery)
    
    oQuery:SetString(1, FWxFilial('SCE'))
    oQuery:SetString(2, cQuotationCode)
    oQuery:SetString(3, cSupplier)
    oQuery:SetString(4, cStore)

    cAliasSCE := MpSysOpenQuery(oQuery:getFixQuery())
    cPurchaseCode := (cAliasSCE)->CE_NUMPED

    If !Empty(cPurchaseCode) //-- Se já existir pedido gerado 
        lDisqBlocked := .T.
    EndIf

    (cAliasSCE)->(DbCloseArea())

    If Self:lOk := DHU->(MsSeek(FWxFilial('DHU') + cQuotationCode))
        PG020SetOp(MODEL_OPERATION_UPDATE)

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()
        oModelSC8 := oModel:GetModel('SC8DETAIL')
        cSituationAux := cSituation
		dDtrCot := DHU->DHU_DTRCOT

        For nX := 1 To oModelSC8:Length()
            oModelSC8:GoLine(nX)
            
            cKeyRequest := cSupplier + cStore + cProposal + cCorporateName
            cKeyModel := oModelSC8:GetValue("C8_FORNECE") + oModelSC8:GetValue("C8_LOJA") + oModelSC8:GetValue("C8_NUMPRO") + oModelSC8:GetValue("C8_FORNOME")
        
            If cKeyRequest == cKeyModel .And. Self:lOk //-- Busca linha referente ao fornecedor e proposta da cotação
                If cSituation == 'R' //-- Limpa campos para requalificar o fornecedor
                    cSituationAux := '1'
                    oModelSC8:LoadValue('C8_TPFRETE', '')
                    oModelSC8:LoadValue('C8_COND'   , '')
                    oModelSC8:LoadValue('C8_TES'    , '')
                    oModelSC8:LoadValue('C8_PRECO'  , 0 )
                    oModelSC8:LoadValue('C8_QTDISP' , oModelSC8:GetValue('C8_QUANT'))
                    oModelSC8:LoadValue('C8_DESC'   , 0 )
                    oModelSC8:LoadValue('C8_VLDESC' , 0 )
                    oModelSC8:LoadValue('C8_TOTAL'  , 0 )

                    if Self:vldFieldsSX3('SC8', 'C8_PICM')
                        oModelSC8:LoadValue('C8_PICM'   , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_ALIIPI')
                        oModelSC8:LoadValue('C8_ALIIPI' , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_VALICM')
                        oModelSC8:LoadValue('C8_VALICM' , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_VALIPI')
                        oModelSC8:LoadValue('C8_VALIPI' , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_BASEICM')
                        oModelSC8:LoadValue('C8_BASEICM', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_BASEIPI')
                        oModelSC8:LoadValue('C8_BASEIPI', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_BASESOL')
                        oModelSC8:LoadValue('C8_BASESOL', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_ICMSCOM')
                        oModelSC8:LoadValue('C8_ICMSCOM', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_VALSOL')
                        oModelSC8:LoadValue('C8_VALSOL' , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_ALIQCMP')
                        oModelSC8:LoadValue('C8_ALIQCMP', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_VALISS')
                        oModelSC8:LoadValue('C8_VALISS' , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_ALIQISS')
                        oModelSC8:LoadValue('C8_ALIQISS', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_DIFAL')
                        oModelSC8:LoadValue('C8_DIFAL'  , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_BASEDES')
                        oModelSC8:LoadValue('C8_BASEDES', 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_PDORI')
                        oModelSC8:LoadValue('C8_PDORI'  , 0)
                    endif
                    if Self:vldFieldsSX3('SC8', 'C8_PDDES')
                        oModelSC8:LoadValue('C8_PDDES'  , 0)
                    endif

                    oModelSC8:LoadValue('C8_PRAZO'  , 0 )
                    oModelSC8:LoadValue('C8_VALFRE' , 0 )
                    oModelSC8:LoadValue('C8_TOTFRE' , 0 )
                    oModelSC8:LoadValue('C8_VALIDA' , dDtrCot)
					oModelSC8:LoadValue('C8_OBS'    , cObservation)
                    oModelSC8:SetValue('C8_WF'      ,.F.)
                ElseIf cSituation == "5" //Desqualificação
                    oModelSC8:LoadValue('C8_OBS'   , cObservation )
                    oModelSC8:LoadValue('C8_VALIDA', SToD(''))
				EndIf
  
                oModelSC8:SetValue('C8_SITUAC', cSituationAux) //-- Seta nova situação

                Self:lOk := !oModel:HasErrorMessage()

                cItem := oModelSC8:GetValue('C8_ITEM')
                cMessage := Iif(!Self:lOk, STR0001 + cItem, '') //-- Erro no item XXX
            EndIf
        Next nX

        If lDisqBlocked 
            Self:lOk := .F.
            If cSituation == '5' //-- Desqualificada
                cMessage := Iif(!Self:lOk, STR0033, '')
            ElseIf cSituation == 'R' //-- Requalificada
                cMessage := Iif(!Self:lOk, STR0034, '')
            EndIf
        EndIf

        If Self:lOk
            If Self:lOk := oModel:VldData() .And. oModel:CommitData()
                If cSituation == '5' //-- Desqualificada
                    cMessage := STR0004 + AllTrim(cCorporateName) + STR0014 //-- Cotação do fornecedor XXX desqualificada com sucesso.
                ElseIf cSituation == 'R' //-- Requalificada
                    cMessage := STR0004 + AllTrim(cCorporateName) + STR0015 //-- Cotação do fornecedor XXX requalificada com sucesso.
                EndIf
            EndIf
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.
        
        oModel:Destroy()
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0006 + cQuotationCode + STR0007) //-- A cotação XXX não foi localizada na base de dados.
    EndIf

    oQuery:Destroy()

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
    FreeObj(oQuery)

Return oJsonRet

/*/{Protheus.doc} postCalcTax
	Realiza o cálculo dos impostos através da execauto do mata150.
@author Leandro Fini
@since 19/01/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method postCalcTax(oJsonRequest) Class pgcUpdateQuotationRepository
    
    Local nX            As Numeric
    Local nZ            As Numeric
    Local oJsonResponse As Object
    Local oUtils        As Object
    Local aCabec        As Array
    Local aItens        As Array
    Local aItemsRet     As Array
    Local cMessage      As Character

    Private aImpVal        As Array
    Private lMsErroAuto    As Logical
    Private lMsHelpAuto    As Logical
    Private lAutoErrNoFile As Logical

    aCabec := {}
    aItens := {}
    aImpVal := {}
    cMessage := ''
    lMsErroAuto := .F.
    lMsHelpAuto := .T.
    lAutoErrNoFile := .T.
    oUtils := pgcUtils():New()

    A150SetPGC(.T.) //-- Define que o MATA150 é chamado do PGC
    DbSelectArea("SC8")
    SC8->(DbSetOrder(1))
    if Self:lOk := (SC8->(MsSeek(FWxFilial('SC8')+PADR(oJsonRequest:quotationcode, TAMSX3("C8_NUM")[1]))))

        aadd(aCabec ,{"C8_FORNECE"   ,PADR(oJsonRequest:supplier        , TAMSX3("C8_FORNECE")[1])})
        aadd(aCabec ,{"C8_LOJA"      ,PADR(oJsonRequest:store           , TAMSX3("C8_LOJA"   )[1])})
        aadd(aCabec ,{"C8_FORNOME"   ,PADR(oJsonRequest:corporatename   , TAMSX3("C8_FORNOME")[1])})
        aadd(aCabec ,{"C8_COND"      ,PADR(oJsonRequest:paymentcondition, TAMSX3("C8_COND"   )[1])})
        aadd(aCabec ,{"C8_FILENT"    ,PADR(oJsonRequest:deliverybranch  , TAMSX3("C8_FILENT" )[1])})
        aadd(aCabec ,{"C8_CONTATO"   ,"AUTO"                                })
        aadd(aCabec ,{"C8_MOEDA"     ,oJsonRequest:currency                 })
        aadd(aCabec ,{"C8_TXMOEDA"   ,oJsonRequest:currencyrate             })
        aadd(aCabec ,{"C8_EMISSAO"   ,StoD(oJsonRequest:emissiondate)       })
        aadd(aCabec ,{"C8_TPFRETE"   ,oJsonRequest:typeship                 })
        aadd(aCabec ,{"C8_VALDESC"   ,oJsonRequest:discountvalue            })
        aadd(aCabec ,{"C8_DESPESA"   ,oJsonRequest:expensesvalue            })
        aadd(aCabec ,{"C8_SEGURO"    ,oJsonRequest:insurancevalue           })

        for nX := 1 to len(oJsonRequest:items)
           aadd(aItens,{{"C8_NUMPRO"    ,PADR(oJsonRequest:items[nX]:proposal   , TAMSX3("C8_NUMPRO" )[1]),Nil},;
                        {"C8_PRODUTO"   ,PADR(oJsonRequest:items[nX]:productcode, TAMSX3("C8_PRODUTO")[1]),Nil},;
                        {"C8_ITEM"      ,PADR(oJsonRequest:items[nX]:item       , TAMSX3("C8_ITEM"   )[1]),Nil},;
                        {"C8_TES"       ,PADR(oJsonRequest:items[nX]:tes        , TAMSX3("C8_TES"    )[1]),Nil},;
                        {"C8_UM"        ,PADR(oJsonRequest:items[nX]:unity      , TAMSX3("C8_UM"     )[1]),Nil},;
                        {"C8_QTDISP"    ,oJsonRequest:items[nX]:availablequantity          ,Nil},;
                        {"C8_PRECO"     ,oJsonRequest:items[nX]:unityvalue                 ,NIL},;
                        {"C8_TOTAL"     ,oJsonRequest:items[nX]:total                      ,NIL}})

        next nX
        
        MSExecAuto({|v,x,y,k| MATA150(v,x,y,,,,k)},aCabec,aItens,3,.T.)

        If Self:lOk := !lMsErroAuto .And. Len(aImpVal) > 0
            oJsonResponse := JsonObject():New()
            oJsonResponse["branch"]         := FWxFilial('SC8')
            oJsonResponse["quotationcode"]  := oJsonRequest:quotationcode
            oJsonResponse["supplier"]       := oJsonRequest:supplier
            oJsonResponse["store"]          := oJsonRequest:store
            oJsonResponse["items"]          := {}

            aItemsRet := {}
            for nX := 1 to len(aImpVal)
                aAdd(aItemsRet, JsonObject():New())
                aItemsRet[nX]["item"] := aImpVal[nX][1]
                aItemsRet[nX]["productcode"] := oJsonRequest:items[nX]:productcode

                //Roda cada imposto e atribui o valor no retorno.
                for nZ := 1 to len(aImpVal[nX][2])
                    aItemsRet[nX][aImpVal[nX][2][nZ][1]] := aImpVal[nX][2][nZ][3]
                next nZ
            next nX
            oJsonResponse["items"] := aItemsRet
        Else
            cMessage := oUtils:getMessageExecAuto()
            cMessage := IIf(Empty(cMessage), STR0020, cMessage) //-- Não foi possível carregar os impostos.
            oJsonResponse := oUtils:setClassResponse(400, cMessage) //-- Seta mensagem de erro do execauto
        Endif
    else 
        oJsonResponse := oUtils:setClassResponse(400, STR0006 + oJsonRequest:quotationcode + STR0007) //-- A cotação XXX não foi localizada na base de dados.
    endif

    FreeObj(oUtils)
Return oJsonResponse

/*/{Protheus.doc} getListCurrency
	Retorna a lista de moedas utilizadas no financeiro
@author rd.santos
@since 31/01/2023
@return oJsonResponse, object, resposta no formato json.
/*/
Method getListCurrency() Class pgcUpdateQuotationRepository
    Local aItems        As Array
    Local oItem         As Object
    Local nX            As Numeric
    Local cDescription  As Character
    Local oJsonResponse As Object
    
    oJsonResponse := JsonObject():New()
    aItems := {} 
    
    For nX := 1 To MoedFin()
        A150DescMoed(nX,,@cDescription)
        oItem := JsonObject():New()
        oItem["currency"]    := nX
        oItem["description"] := cDescription
        aAdd(aItems, oItem)
    Next nX

    oJsonResponse["currencies"] := aItems 

Return oJsonResponse

/*/{Protheus.doc} postNewProposal
	Realiza a geração de uma nova proposta para o fornecedor.
@author juan.felipe
@since 21/03/2023
/*/
Method postNewProposal() Class pgcUpdateQuotationRepository
    Local aAreas As Array
    Local aItems As Array
    Local cQuotationCode As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cProposal As Character
    Local cCorporateName As Character
    Local cProductCode As Character
    Local cItem As Character
    Local cGridItem As Character
    Local cNewProposal As Character
	Local cCopyFields As Character
    Local cMessage As Character
	Local cSituation As Character
    Local nX As Numeric
    Local lCleanProposal As Logical
    Local oModel As Object
    Local oModelSC8 As Object
    Local oModelDHV As Object
    Local oJsonRet As Object
    Local oJsonLineData As Object
    Local oUtils As Object
    Private l150Auto := .F.
    
    A150SetPGC(.T.)

    aAreas   := {DHU->(GetArea()), GetArea()}
    oJsonRet := JsonObject():New()
    oUtils   := pgcUtils():New()

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    aItems          := Self:oJsonRequest:items
    cQuotationCode  := PadR(Self:oJsonRequest:quotationcode , TamSX3('C8_NUM'    )[1], " ")
    cSupplier       := PadR(Self:oJsonRequest:supplier      , TamSX3('C8_FORNECE')[1], " ")
    cStore          := PadR(Self:oJsonRequest:store         , TamSX3('C8_LOJA'   )[1], " ")
    cCorporateName  := PadR(Self:oJsonRequest:corporatename , TamSX3('C8_FORNOME')[1], " ")
    lCleanProposal  := IIf(AttIsMemberOf(Self:oJsonRequest, 'cleanproposal'), Self:oJsonRequest:cleanproposal, .F.)
    cProposal       := PGCLastProp(cQuotationCode,cSupplier,cStore,cCorporateName)
	cCopyFields		:= PGCSC8Copy(.F., lCleanProposal)
    cMessage        := ''
	cSituation      := ''

    If !Empty(cProposal)
        cNewProposal := Soma1(cProposal)
    Endif

    If Self:lOk := DHU->(MsSeek(FWxFilial('DHU') + cQuotationCode))
        PG020SetOp(MODEL_OPERATION_UPDATE)

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()
        oModelSC8 := oModel:GetModel('SC8DETAIL')
        oModelDHV := oModel:GetModel('DHVDETAIL')

        PGCReqFlds(oModel, 'SC8DETAIL') //-- Remove obrigatoriedade dos campos da SC8

        For nX := 1 To Len(aItems)
            cProductCode := PadR(aItems[nX]:productcode, TamSX3('C8_PRODUTO')[1], " ")
            cItem := PadR(aItems[nX]:item, TamSX3('C8_ITEM')[1], " ")
            cGridItem := PadR(aItems[nX]:griditem, TamSX3('C8_ITEMGRD')[1], " ")

            //-- Busca linha referente ao item de proposta da cotação anterior
            If Self:lOk := oModelSC8:SeekLine({{"C8_PRODUTO",cProductCode},{"C8_ITEM",cItem},{"C8_ITEMGRD",cGridItem},{"C8_FORNECE",cSupplier},{"C8_LOJA",cStore},{"C8_NUMPRO",cProposal},{"C8_FORNOME",cCorporateName}}) 
                If nX == 1 //-- Valida se a nova proposta pode ser incluída
                    SC8->(dbGoTo(oModelSC8:GetDataId()))
                    Self:lOk := A150Propos(,,,@cMessage) == 1
                EndIf

                If Self:lOk
                    oJsonLineData := oUtils:lineMVCToJson(oModelSC8, oModelSC8:GetLine(), cCopyFields)

                    If Self:lOk := oUtils:AddLineMVCFromJson(oModelSC8, oJsonLineData)
                        oModelSC8:LoadValue('C8_NUMPRO' , cNewProposal)
                        oModelSC8:LoadValue('C8_ORIGEM' , 'PGCA020')
                        oModelSC8:SetValue('C8_EMISSAO' , dDataBase)
                        oModelSC8:SetValue('C8_WF'      , .F.)

                        If lCleanProposal
                            oModelSC8:LoadValue('C8_QTDISP' , oModelSC8:GetValue('C8_QUANT'))
                            oModelSC8:SetValue('C8_SITUAC'  , '1') //-- Reseta status do item da cotação
                        EndIf

						cSituation := oModelSC8:GetValue('C8_SITUAC')
						cSituation := IIf(cSituation == '4', '1', cSituation)
						oModelSC8:SetValue('C8_SITUAC', cSituation)
                    EndIf
                Else
                    Exit
                EndIf
            Else
                cMessage := STR0023 + aItems[nX]:productcode + STR0019 //-- O produto XXX não foi localizado na base dados.
            EndIf

            If !Self:lOk
                Exit
            EndIf
        Next nX

        If Self:lOk
            If Self:lOk := oModel:VldData() .And. oModel:CommitData()
                cMessage := STR0025 + AllTrim(cCorporateName) + STR0026 //-- A nova proposta do fornecedor XXX foi gerado com sucesso.
            EndIf
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.
        
        oModel:Destroy()
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0006 + cQuotationCode + STR0007) //-- A cotação XXX não foi localizada na base de dados.
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oUtils)
    FreeObj(oModel)
Return oJsonRet

/*/{Protheus.doc} getProposalsHistory
	Retorna históricos de propostas do fornecedor.
@author juan.felipe
@since 03/04/2023
@return Nil, nulo.
/*/
Method getProposalsHistory(oJsonResponse) Class pgcUpdateQuotationRepository
    Local aProposalsItems As Array
    Local nLoop As Numeric
    Local nX As Numeric
    Local cAliasSC8 := GetNextAlias()
    Local aArea     := GetArea()
    Local cQuery := ''
    Local cQuotationCode := ""
    Local cSupplier := ""
    Local cStore := ""
    Local cCorporateName := ""
    Local oUtils As Object
    Local oQuery As Object
    Default oJsonResponse := JsonObject():New()

    aProposalsItems := {}
    oUtils := pgcUtils():New()

    If oJsonResponse <> Nil .And. oJsonResponse:hasProperty('items') .And. Len(oJsonResponse['items']) > 0
        aItems := oJsonResponse['items']

        cQuery := " SELECT SC8.C8_FILIAL,"
        cQuery += "    SC8.C8_NUM,"
        cQuery += "    SC8.C8_FORNECE,"
        cQuery += "    SC8.C8_LOJA,"
        cQuery += "    SC8.C8_NUMPRO,"
        cQuery += "    SC8.C8_FORNOME,"
        cQuery += "    SC8.C8_EMISSAO,"
        cQuery += "    SC8.C8_MOEDA,"
        cQuery += "    AVG(SC8.C8_PRAZO) DEADLINE,"
        cQuery += Self:QueryOperationAnalysis("SUM(", {"C8_TOTAL", "C8_VALFRE", "C8_VALIPI", "C8_VALSOL"}) + "-SC8.C8_VLDESC) TOTAL, "
        cQuery += "    SUM(SC8.C8_TOTAL) TOTALITEMS,"
        cQuery += "    SUM(SC8.C8_VALFRE) TOTALSHIP,"
        cQuery += "    SUM(SC8.C8_VLDESC) TOTALDISC,"
        cQuery += "    COUNT(SC8.C8_ITEM) QUANTITEMS"
        cQuery += " FROM "+ RetSQLName("SC8") +" SC8"
        cQuery += " WHERE SC8.C8_FILIAL = ?"
        cQuery += " AND SC8.C8_NUM = ?"
        cQuery += " AND SC8.C8_FORNECE = ?"
        cQuery += " AND SC8.C8_LOJA = ?"
        cQuery += " AND SC8.C8_NUMPRO NOT IN"
        cQuery += "     (SELECT MAX(SC8_2.C8_NUMPRO)"
        cQuery += "     FROM "+ RetSQLName("SC8") +" SC8_2"
        cQuery += "     WHERE SC8_2.C8_FILIAL = SC8.C8_FILIAL"
        cQuery += "     AND SC8_2.C8_NUM = SC8.C8_NUM"
        cQuery += "     AND SC8_2.C8_FORNECE = SC8.C8_FORNECE"
        cQuery += "     AND SC8_2.C8_LOJA = SC8.C8_LOJA"
        cQuery += "     AND SC8_2.C8_FORNOME = SC8.C8_FORNOME"
        cQuery += "     AND SC8_2.D_E_L_E_T_ = ' ')"
        cQuery += " AND SC8.C8_FORNOME = ?"
        cQuery += " AND SC8.D_E_L_E_T_ = ' '"
        cQuery += " GROUP BY SC8.C8_FILIAL,"
        cQuery += "         SC8.C8_NUM,"
        cQuery += "         SC8.C8_FORNECE,"
        cQuery += "         SC8.C8_LOJA,"
        cQuery += "         SC8.C8_NUMPRO,"
        cQuery += "         SC8.C8_FORNOME,"
        cQuery += "         SC8.C8_EMISSAO,"
        cQuery += "         SC8.C8_MOEDA"

        oQuery := FWPreparedStatement():New(cQuery)

        For nX := 1 To Len(aItems)
            cQuotationCode := aItems[nX]['quotationcode']
            cSupplier := aItems[nX]['supplier']
            cStore := aItems[nX]['store']
            cCorporateName := aItems[nX]['corporatename']
        
            oQuery:SetString(1, FWxFilial('SC8'))
            oQuery:SetString(2, cQuotationCode)
            oQuery:SetString(3, cSupplier)
            oQuery:SetString(4, cStore)
            oQuery:SetString(5, cCorporateName)

            cAliasSC8 := MpSysOpenQuery(oQuery:getFixQuery())

            nLoop := 1

            While ((cAliasSC8)->(!Eof()))
                aAdd(aProposalsItems, JsonObject():New())
				aProposalsItems[nLoop]['branch'            ] := (cAliasSC8)->C8_FILIAL
                aProposalsItems[nLoop]['quotationcode'     ] := (cAliasSC8)->C8_NUM
                aProposalsItems[nLoop]['supplier'          ] := (cAliasSC8)->C8_FORNECE
                aProposalsItems[nLoop]['store'             ] := (cAliasSC8)->C8_LOJA
                aProposalsItems[nLoop]['proposal'          ] := (cAliasSC8)->C8_NUMPRO
				aProposalsItems[nLoop]['corporatename'     ] := (cAliasSC8)->C8_FORNOME
                aProposalsItems[nLoop]['currency'          ] := (cAliasSC8)->C8_MOEDA
                aProposalsItems[nLoop]['deadline'          ] := (cAliasSC8)->DEADLINE
                aProposalsItems[nLoop]['emissiondate'      ] := (cAliasSC8)->C8_EMISSAO
                aProposalsItems[nLoop]['total'             ] := (cAliasSC8)->TOTAL
                aProposalsItems[nLoop]['totalitems'        ] := (cAliasSC8)->TOTALITEMS
                aProposalsItems[nLoop]['totalship'         ] := (cAliasSC8)->TOTALSHIP
                aProposalsItems[nLoop]['totaldiscount'     ] := (cAliasSC8)->TOTALDISC
                aProposalsItems[nLoop]['quantityitems'     ] := (cAliasSC8)->QUANTITEMS

                nLoop++
                (cAliasSC8)->(DbSkip())
            Enddo

            aItems[nX]["proposals"] := aClone(aProposalsItems)
            aProposalsItems := {}

            (cAliasSC8)->(DbCloseArea())
        Next nX

        oQuery:Destroy()
        FreeObj(oQuery)
    EndIf

    RestArea(aArea)
Return Nil

/*/{Protheus.doc} ApportionmentDiffValues
	Retorna a diferença no rateio de valores por item.
@author juan.felipe
@since 17/04/2023
@param nTotalValueGenerated, numeric, valor total gerado no rateio dos itens.
@param nRealTotalValue, numeric, valor total real a ser comparado com o total gerado.
@param nItemValue, numeric, valor do item.
@return nRetDiff, numeric, diferença entre os valores.
/*/
Method ApportionmentDiffValues(nTotalValueGenerated, nRealTotalValue, nItemValue) Class pgcUpdateQuotationRepository
    Local nDiff As Numeric
    Local nRetDiff As Numeric
	Default nTotalValueGenerated := 0
	Default nRealTotalValue := 0
	Default nItemValue := 0

    nRetDiff := nItemValue

    If nTotalValueGenerated != nRealTotalValue
        nDiff := nTotalValueGenerated - nRealTotalValue
        nRetDiff := nItemValue + (nDiff*-1)
    EndIf
Return nRetDiff


/*/{Protheus.doc} deleteProposal
	Deleta proposta atual da cotação.
@author juan.felipe
@since 17/05/2023
/*/
Method deleteProposal() Class pgcUpdateQuotationRepository
    Local aAreas As Array
    Local aSeek As Array
    Local cQuotationCode As Character
    Local cProduct As Character
    Local cItem As Character
    Local cSupplier As Character
    Local cStore As Character
    Local cProposal As Character
    Local cCorporateName As Character
    Local cMessage As Character
    Local nX As Numeric
    Local nLen As Numeric
    Local nValidItems As Numeric
    Local nTamCodPro As Numeric
    Local oModel As Object
    Local oModelSC8 As Object
    Local oModelDHV As Object
    Local oJsonRet As Object
    Local oUtils As Object
    Private l150Auto := .F.
    
    A150SetPGC(.T.)

    aAreas   := {DHU->(GetArea()), GetArea()}
    oJsonRet := JsonObject():New()
    oUtils   := pgcUtils():New()

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    cQuotationCode := PadR(Self:oJsonRequest:quotationcode, TamSX3('C8_NUM'    )[1], " ")
    cSupplier      := PadR(Self:oJsonRequest:supplier     , TamSX3('C8_FORNECE')[1], " ")
    cStore         := PadR(Self:oJsonRequest:store        , TamSX3('C8_LOJA'   )[1], " ")
    cCorporateName := PadR(Self:oJsonRequest:corporatename, TamSX3('C8_FORNOME')[1], " ")
    cProposal      := PGCLastProp(cQuotationCode,cSupplier,cStore,cCorporateName)
    cMessage       := ''
    nTamCodPro     := TamSX3("C8_PRODUTO")[1]
    nX             := 1
    nValidItems    := 0
    
    If Self:lOk := DHU->(MsSeek(FWxFilial('DHU') + cQuotationCode))
        PG020SetOp(10)

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()
        oModelSC8 := oModel:GetModel('SC8DETAIL')
        oModelDHV := oModel:GetModel('DHVDETAIL')
        nLen := oModelDHV:Length()

        While nX <= nLen
            oModelDHV:GoLine(nX)

            cProduct := PadR(oModelDHV:GetValue('DHV_CODPRO'),nTamCodPro)
            cItem := oModelDHV:GetValue('DHV_ITEM')                
            aSeek := {{'C8_PRODUTO', cProduct}, {'C8_ITEM', cItem}, {'C8_FORNECE', cSupplier}, {'C8_LOJA', cStore}, {'C8_NUMPRO', cProposal}, {'C8_FORNOME', cCorporateName}}
            
            If oModelSC8:SeekLine(aSeek, .T.)
                If !oModelSC8:IsDeleted()
                    Self:lOk := oModelSC8:DeleteLine(.F.,.F.) //-- Deleta item da cotação
                    nValidItems ++
                EndIf
                nX ++
            ElseIf nValidItems > 0 //-- Se o item não for encontrado na proposta, deve posicionar no item anterior
                nX -= 1
                nLen --
            Else //-- Caso a proposta não possua o item, deve posicionar no próximo item
                nX ++
            EndIf
        EndDo

        If Self:lOk .And. nValidItems == 0
            cMessage := STR0030 //-- Não foi possível excluir a proposta, pois os itens da cotação não foram localizados base de dados.
            Self:lOk := .F.
        EndIf

        If Self:lOk
            If Self:lOk := oModel:VldData() .And. oModel:CommitData()
                cMessage := STR0028 //-- A proposta foi excluída com sucesso.
            EndIf
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.
        
        oModel:Destroy()
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0006 + cQuotationCode + STR0007) //-- A cotação XXX não foi localizada na base de dados.
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oUtils)
    FreeObj(oModel)
Return oJsonRet


/*/{Protheus.doc} getCurrencyName
Retorna o nome da moeda utilizada na cotação.
@author renan.martins
@since 12/2023
@param oJsonResponse, objeto, objeto da resposta.
@return nil, nil, nulo.
/*/
Method getCurrencyName(oJsonResponse) Class pgcUpdateQuotationRepository
Local aItems        as Array
local cDescription  as character
local cTempValue    as character
local nFor          as Numeric
local nSizeCmp      as Numeric
local nCurrency     as Numeric
local nCost         as Numeric
local oHashJson     as Object
local lFormalization As Logical

Local cNFCMoed      := SuperGetMV("MV_NFCMOED", .F., "1")

oHashJson := JsonObject():New()
nSizeCmp := TamSX3("CTO_MOEDA")[1]

if oJsonResponse != nil .and. oJsonResponse:hasProperty('items')
    lFormalization := 'formalization' $ oRest:getHeaderRequest()['Referer']
    aItems := oJsonResponse['items']
    for nFor := 1 To Len(aItems)
        cTempValue := StrZero(aItems[nFor]["currency"], nSizeCmp)

        if oHashJson[cTempValue] != Nil	
            aItems[nFor]['currencydescription'] := oHashJson[cTempValue]
        else   
            //Parametro (MV_NFCMOED); 1=CTO(Default);2=MV_MOEDA 
            If cNFCMoed == "1"
                cDescription := alltrim(Posicione("CTO", 1, xFilial("CTO") + cTempValue,"CTO_DESC"))
            Else
                nCurrency := val(cTempValue)
                cDescription := Self:NFCRetMoed(nCurrency)
            EndIf

            aItems[nFor]['currencydescription'] := cDescription
            oHashJson[cTempValue] := cDescription      
        endif

        If lFormalization .And. Self:lNFCCost
            nCost := NFCGetCost(Self:aRefTax, aItems[nFor]['quotationcode'],aItems[nFor]['supplier'], aItems[nFor]['store'], aItems[nFor]['corporatename'], aItems[nFor]['proposal'], .T.)

            If nCost > 0
                aItems[nFor]['total'] := nCost
            EndIf
        EndIf
    next
endif

return nil


/*/{Protheus.doc} NFCRetMoed
	Método retorna a descricao da moeda posicionada
@author ali.neto
@since 26/03/2025
/*/
Method NFCRetMoed(nCurrency) Class pgcUpdateQuotationRepository

    Local cDescription := ""
    Default nCurrency  := 1
    
    cDescription := SuperGetMV("MV_MOEDA"+AllTrim(Str(nCurrency,2)), .F., "")
Return cDescription


/*/{Protheus.doc} vldFieldsSX3
Método que valida se o campo existe na SX3 e armazena em um JSON, para uso posterior
@author renan.martins
@since 02/2024
@param cAlias, string, Alias a ser pesquisado o campo.
@param cFieldName, string, Nome do campo a sr pesquisado no alias informado.
@return lRet, lógico, retorna se o campo existe ou não na SX3.
/*/
Method vldFieldsSX3(cAlias, cFieldName) Class pgcUpdateQuotationRepository
local lRet          as logical
default cAlias      := 'SC8'
default cFieldName  := "C8_VALSOL"

//Valido se o objeto oFieldsExs é JSON. Se não, recrio para evitar erros.
lRet := .f.
if Self:oFieldsExs[cFieldName] == Nil
    if ( &(cAlias)->(FieldPos(cFieldName)) > 0 )
        lRet := .t.
    endif
    Self:oFieldsExs[cFieldName] := lRet
else
    lRet := Self:oFieldsExs[cFieldName]
endif

return lRet


/*/{Protheus.doc} QueryOperationAnalysis
Método que monta a string para as funções SUM ou COUNT do SQL, verificando se os campos informados existem na SX3, para a junção positiva
desses valores nessas funções de agregação do SQL.
@author renan.martins
@since 02/2024
@param cOperation, string, tipo de operação SQL (SUM ou COUNT), com o parêntese de abertura: 'SUM(' ou 'COUNT('.
@param aFields, array, nome dos campos que devem ser analisados na SX3, para concatenação.
@param cClosePar, string, se for apenas concatenação, informar o parênteses ')' para fechar a operação: 'SUM(XXX)', 'COUNT(XXX)'
@return cRet, string, string que contêm o retorno formatado para a query.
/*/
Method QueryOperationAnalysis(cOperation, aFields, cClosePar) Class pgcUpdateQuotationRepository
local cRet          as character
local nFor          as numeric
local nSize         as numeric
default aFields     := {}
default cOperation  := "SUM("
default cClosePar   := ""

nSize := len(aFields)
cRet := ""

for nFor := 1 to nSize
    if Self:vldFieldsSX3(nil, aFields[nFor])
        cRet += aFields[nFor] + "+"
    endif
next

if !empty(cRet)
    cRet := cOperation + substr(cRet, 1, len(cRet)-1) + cClosePar
endif

return cRet

/*/{Protheus.doc} getSupplierBinding
	Obtém amarração de produtos x fornecedores.
@author juan.felipe
@since 06/05/2024
@param cQuotationCode, character, código da cotação.
@param cBindingType, character, tipo de amarração, 1- Produto x Fornecedor, 2- Grupo X Fornecedor, 3- Ambos.
@return oJsonResponse, object, itens com amarração.
/*/
Method getSupplierBinding(cQuotationCode, cBindingType) Class pgcUpdateQuotationRepository
    Local oGenerateQuotation As Object
    Local oJsonResponse As Object
    Local oQuery As Object
    Local cQuery As Character
    Local cAliasSC1 As Character
    Local cErrorMessage As Character
    Local aItems As Array
    Local nX As Numeric
    Default cQuotationCode := ''
    Default cBindingType := '1'

    oGenerateQuotation := pgcGenerateQuotationRepository():New()
    oJsonResponse := JsonObject():New()
    Self:lOk := .T.
    aItems := {}
    cErrorMessage := ''
    nX := 1

    cQuery := "SELECT "
    cQuery += "     SC1.C1_FILIAL, "
    cQuery += "     SC1.C1_FILENT, "
    cQuery += "     SC1.C1_NUM, "
    cQuery += "     SC1.C1_PRODUTO, "
    cQuery += "     SC1.C1_ITEM, "
    cQuery += "     SC1.C1_ITEMGRD, "
    cQuery += "     C1_ITEMGRD, "
    cQuery += "     C1_DATPRF, "
    cQuery += "     C1_DESCRI, "
    cQuery += "     C1_UM, "
    cQuery += "     C1_CC, "
    cQuery += "     C1_CONTA, "
    cQuery += "     C1_ITEMCTA, "
    cQuery += "     C1_CLVL, "
    cQuery += "     C1_QUANT, "
    cQuery += "     C1_QTSEGUM, "
    cQuery += "     SC1.R_E_C_N_O_ RECNO "
    cQuery += "FROM "+ RetSQLName("SC1") +" SC1 "
    cQuery += "WHERE "
    cQuery += "     SC1.C1_FILIAL = ? "
    cQuery += "     AND SC1.C1_COTACAO = ? "
    cQuery += "     AND SC1.D_E_L_E_T_ = ' ' "

    oQuery := FWPreparedStatement():New(cQuery)

    oQuery:SetString(1, FWxFilial('SC1'))
    oQuery:SetString(2, cQuotationCode)

    cAliasSC1 := MpSysOpenQuery(oQuery:getFixQuery())
    
    A131SetPGC(.T., Nil)
    Pergunte("MTA130",.F.)

    While (cAliasSC1)->(!Eof())
        aAdd(aItems, JsonObject():New())

        aItems[nX]['c1_filial'   ] := (cAliasSC1)->C1_FILIAL
        aItems[nX]['c1_filent'   ] := (cAliasSC1)->C1_FILENT
        aItems[nX]['c1_num'      ] := (cAliasSC1)->C1_NUM
        aItems[nX]['c1_produto'  ] := (cAliasSC1)->C1_PRODUTO
        aItems[nX]['c1_item'     ] := (cAliasSC1)->C1_ITEM
        aItems[nX]['c1_itemgrd'  ] := (cAliasSC1)->C1_ITEMGRD
        aItems[nX]['c1_datprf'   ] := (cAliasSC1)->C1_DATPRF
        aItems[nX]['c1_descri'   ] := (cAliasSC1)->C1_DESCRI
        aItems[nX]['c1_um'       ] := (cAliasSC1)->C1_UM
        aItems[nX]['c1_cc'       ] := (cAliasSC1)->C1_CC
        aItems[nX]['c1_conta'    ] := (cAliasSC1)->C1_CONTA
        aItems[nX]['c1_itemcta'  ] := (cAliasSC1)->C1_ITEMCTA
        aItems[nX]['c1_clvl'     ] := (cAliasSC1)->C1_CLVL
        aItems[nX]['c1_quant'    ] := (cAliasSC1)->C1_QUANT
        aItems[nX]['c1_qtsegum'  ] := (cAliasSC1)->C1_QTSEGUM
        aItems[nX]['requestrecno'] := (cAliasSC1)->RECNO
        aItems[nX]['c1_obs'      ] := ''
        aItems[nX]['errormessage'] := ''
        aItems[nX]['suppliers'   ] := {}
        
        (cAliasSC1)->(dbSkip())
        nX ++
    EndDo

    (cAliasSC1)->(DbCloseArea())

    oGenerateQuotation:oJsonRequest := JsonObject():New()
    oGenerateQuotation:oJsonRequest['bindingtype'] := cBindingType
    oGenerateQuotation:oJsonRequest['items'] := aItems

    oJsonResponse := oGenerateQuotation:postAgglutinateQuotation(cQuotationCode)

    For nX := 1 To Len(oJsonResponse['invaliditems']) //-- Concatena o array de itens inválidos com o array de itens válidos
        AAdd(oJsonResponse['items'], oJsonResponse['invaliditems'][nX])
    Next nX

    oJsonResponse['invaliditems'] := {} //-- Limpa o array de itens inválidos, pois o mesmo não é necessário.

    oQuery:Destroy()
    FreeObj(oQuery)
Return oJsonResponse

/*/{Protheus.doc} postNewSupplierBinding
	Realiza gravação de novos participantes da cotação amarrados por Produto x Fornecedor.
@author juan.felipe
@since 08/05/2024
@return oJsonResponse, object, resposta no formato json.
/*/
Method postNewSupplierBinding() Class pgcUpdateQuotationRepository
    Local cQuotationCode As Character
    Local oGenerateQuotation As Object
    Local oJsonResponse As Object

    cQuotationCode := Self:oJsonRequest:quotationCode

    oGenerateQuotation := pgcGenerateQuotationRepository():New()
    oGenerateQuotation:oJsonRequest := Self:oJsonRequest

    oJsonResponse := oGenerateQuotation:postGenerateQuotation(cQuotationCode)
    Self:lOk := oGenerateQuotation:lOk
Return oJsonResponse


/*/{Protheus.doc} addTaxesTCIWritten
	Adiciona as propriedade referentes aos impostos genéricos - Código, descrição e valor, conforme classe TCIWRITTEN
@author renan.martins
@since 08/2025
@param oJsonResponse, object, JSON que receberá a propriedade.
@return Nil, nulo.
/*/
Method addTaxesTCIWritten(oJsonResponse) Class pgcUpdateQuotationRepository
    Local aItems        := {}                   As Array
    Local aDados        := {}                   As Array
    Local aDadJson      := {}                   As Array
    Local cNumIDTrib    := ""                   As Character
    Local cRetorno      := ""                   As Character
    Local nX            := 0                    As Numeric
    Local nFor          := 0                    As Numeric
    Local nPosArray     := 0                    As Numeric
    Local oGenTax       := generictaxes():New() As Object
    Local oJsonImp      := JsonObject():New()   As Object
    Local oTributos     := Nil                  As Object
    Default oJsonResponse := Nil

    If oJsonResponse <> Nil .And. oJsonResponse:hasProperty('items')
        aItems := oJsonResponse['items']

        If Len(aItems) > 0

            For nX := 1 To Len(aItems)
                cNumIDTrib := aItems[nX]['productidtrib']
                cCodeProd  := aItems[nX]['productcode']
                cCodeSeq   := aItems[nX]['itemseq']

                If FindFunction('NFCProdDesc')
                    aItems[nX]['productdescription'] := NFCProdDesc(aItems[nX]['productcode'], aItems[nX]['numsc'], aItems[nX]['itemsc'])
                EndIf

                if !empty(cNumIDTrib)
                    cRetorno   := oGenTax:getTaxesWritten(cNumIDTrib)

		            oJsonImp:FromJson(cRetorno)

                    if oJsonImp:HasProperty("dados_Id")
                        if ( !(oJsonImp:HasProperty("Aviso")) .and. !(oJsonImp["dados_Id"][cNumIDTrib]:HasProperty("Aviso")) )
                            
                            aDadJson := oJsonImp["dados_Id"][cNumIDTrib]:GetNames()

                            for nFor := 1 to Len(aDadJson)
                                aAdd(aDados, JsonObject():New())
                                nPosArray := len(aDados)

                                oTributos := oJsonImp["dados_Id"][cNumIDTrib][ aDadJson[nFor] ]
                                
                                aDados[nPosArray]['productCode']     := cCodeProd
                                aDados[nPosArray]['itemSeq']         := cCodeSeq
                                aDados[nPosArray]['taxecode']        := aDadJson[nFor]
                                aDados[nPosArray]['taxedescription'] := oTributos["tributo"]
                                aDados[nPosArray]['value']           := oTributos["valor_tributo"]
                                aDados[nPosArray]["idtribtax"]       := oTributos["codigo_tributo_relacionado"]
                            next nFor++

                        endif
                    endif
                endif
            Next nX

        EndIf

        oJsonResponse['items'] := aClone(aDados)

    EndIf
    FwFreeArray(aItems)
    FwFreeArray(aDados)
    FwFreeArray(aDadJson)
    FWFreeObj(oTributos)
    FWFreeObj(oJsonImp)
    FWFreeObj(oGenTax)
Return Nil


/*/{Protheus.doc} addProductDescription
	Adiciona a propriedade productname com a descrição do produto.
@author juan.felipe
@since 01/10/2025
@param oJsonResponse, object, JSON que receberá a propriedade.
@return Nil, nulo.
/*/
Method addProductDescription(oJsonResponse) Class pgcUpdateQuotationRepository
    Local aItems        := {}                 As Array
    Local nX            := 0                  As Numeric
    Default oJsonResponse := Nil

    If oJsonResponse <> Nil .And. oJsonResponse:hasProperty('items') .And. FindFunction('NFCProdDesc')
        aItems := oJsonResponse['items']

        For nX := 1 To Len(aItems)
            aItems[nX]['productname'] := NFCProdDesc(aItems[nX]['productcode'], aItems[nX]['purchasenumber'], aItems[nX]['item'])
        Next nX
    EndIf
Return Nil
