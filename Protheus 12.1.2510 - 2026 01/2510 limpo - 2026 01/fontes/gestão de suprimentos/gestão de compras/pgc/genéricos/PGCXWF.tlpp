#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'PROTHEUS.CH'
#include "FWMVCDEF.CH"
#include "PGCXWF.CH"

using namespace pgc.updateQuotationRepository
//-------------------------------------------------------------------
/*/{Protheus.doc} PGCWFQuote
    Realiza atualização das propostas de cotação de acordo com as informações inseridas no processo de workflow.
@author juan.felipe
@since 01/08/2022
@version 1.0
@param oWorkflow, object, workflow.
@return Nil, nulo.
/*/
//-------------------------------------------------------------------
Function PGCWFQuote(oWorkflow)
    Local aAreas            := {DHU->(GetArea()), GetArea()}
    Local cBranch           := PADR(AllTrim(oWorkflow:oHtml:RetByName('cFilCot')), TAMSX3("C8_FILENT" )[1])  
    Local cNumQuote         := PADR(AllTrim(oWorkflow:oHtml:RetByName('cNumCot')), TAMSX3("C8_NUM" )[1])
    Local cRecusa           := AllTrim(oWorkFlow:oHtml:RetByName('cRecusa'))
    Local cSupplier         := PadR(RTrim(oWorkFlow:oHtml:RetByName('cFornece')), TAMSX3("C8_FORNECE")[1])
    Local cStore            := PadR(RTrim(oWorkFlow:oHtml:RetByName('cLoja'   )), TAMSX3("C8_LOJA"   )[1])
    Local cSupName          := PadR(RTrim(oWorkFlow:oHtml:RetByName('cForNome')), TAMSX3("C8_FORNOME")[1])
    Local cProduct          := ''
    Local nQuantity         := 0
    Local nUnitValue        := 0
    Local nTotal            := 0
    Local nTotItems         := 0
    Local nDesc             := 0
    Local nValDesc          := 0
    Local cTypeShip         := ''
    Local nTotalShip        := 0
    Local cPayCond          := ''
    Local nCurrency         := 1
    Local nTxCurrency       := 0
    Local cObs              := ''
    Local cSupObs           := ''
    Local cSituacao         := ""
    Local cItem             := ""
    Local dValidade         := CTOD(" / / ")
    Local nX                := 0
    Local nY                := 0
    Local oModel            := Nil
    Local oModelDHU         := Nil
    Local oModelSC8         := Nil
    Local nShipProRated     := 0    As Numeric
    Local nQtdItens         := 0    As Numeric
    Local nDescontoTotal    := 0    As Numeric
    Local nExpenditure      := 0    As Numeric
    Local nExpendRated      := 0    As Numeric
    Local nExpendValue      := 0    As Numeric
    Local nInsurance        := 0    As Numeric
    Local nInsuRated        := 0    As Numeric
    Local nInsuValue        := 0    As Numeric
    Local nShipValue        := 0    As Numeric
    Local cUnitMed          := ''   As Character
    Local cBody             := ''   As Character
    Local cEmail            := ''   As Character
    Local cEmails           := ''   As Character
    Local cMessage          := ''   As Character
    Local aEmails           := {}   As Array
    local oMetSX3           := pgcUpdateQuotationRepository():New()
    Local oHtml             := Nil  As Object
    local nValIPI           := 0
    local nValICMSSOL       := 0
    local nBaseValor        := 0
    local nAliqIpiCl        := 0
    Local lBrazil           := cPaisLoc == 'BRA'
    Local cDataEnt          := CTOD(" / / ")
    Local nAverDays         := 0
    Local nZ                := 0
    Local lNFCWFGRV         := Existblock("NFCWFGRV")
    Local aCpoIt            := {}
    Local aCpoRod           := {}
    Local aCpoCab           := {}
    Local nTamProd          := TAMSX3("C8_PRODUTO")[1]
    Local nTamItem          := TAMSX3("C8_ITEM")[1]
    Local aRetSB1           := {}
    Local cFilSB1           := xFilial('SB1', cBranch)
    Local lFinali           := .F.
      
    // Variáveis de Impostos
    Local cTES              As Character
    Local nIPI              As Numeric
    Local nICMS             As Numeric
    Local nISS              As Numeric
    Local nICMSComp         As Numeric
    Local nICMSST           As Numeric
    Local nDifal            As Numeric
    Local nBaseDifal        As Numeric
    Local nPDifalOri        As Numeric
    Local nPDifalDes        As Numeric
    Local nAliqIPI          As Numeric
    Local nAliqICMS         As Numeric
    Local nAliqISS          As Numeric
    Local nAlICMSCp         As Numeric
    Local nBaseICMS         As Numeric
    Local nBaseIPI          As Numeric
    Local nBasICMSol        As Numeric
    Local lHasIPI           As Logical
    Local lHasICMS          As Logical
    Local lGravImpWF        := SuperGetMV("MV_GRIMPWF",.F.,.T.) As Logical
    Local lHasSupObs        := SC8->(FieldPos('C8_OBSFOR')) > 0
    Local lHasResumWf       As Logical
    Local lNFC030Ht         AS Logical
    
    // Variáveis rotina Mata150
    Local aCabec            As Array
    Local aItens            As Array    
    Local aItemsRet         As Array
    Private aImpVal         As Array
    Private lMsErroAuto     As Logical
    Private lMsHelpAuto     As Logical   

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    If DHU->(DbSeek(cBranch + cNumQuote))
        _nOperation := MODEL_OPERATION_UPDATE
        PG020SetOp(_nOperation) 
        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(_nOperation)
        oModel:Activate()
        oModelDHU := oModel:GetModel('DHUMASTER')
        oModelSC8 := oModel:GetModel('SC8DETAIL')

        PGCReqFlds(oModel, 'SC8DETAIL') //-- Remove obrigatoriedade dos campos da SC8

        // -- Pega os campos que foram inseridos no template WF
        lNFC030Ht := FwAliasInDic("DKK") .and. FwAliasInDic("DKL") .and. FindFunction("NF030FldWF")
        lFinali   := IIf(DHU->DHU_STATUS == "4", .T., .F.) //-- Verifica se a cotação já esta finalizada

        if ( lNFC030Ht )
            aCpoIt  := NF030FldWF("2")//-- Itens
        endif

        cProposal := PadR(AllTrim(oWorkFlow:oHtml:RetByName('cProposta')), TAMSX3("C8_NUMPRO")[1]) //-- Número da proposta
        nQtdItens := Len(oWorkFlow:oHtml:RetByName('It.cProDesc'))
        lHasResumWf := oWorkFlow:oHtml:RetByName('cHasResumWf') == '1'

        //-- Calcula a media do prazo de entrega
        nAverDays := calcAvrDays(oModelSC8, oWorkFlow, nQtdItens);

        aCabec      := {}
        aItens      := {}
        aImpVal     := {}
        aItemsRet   := {}
        lMsErroAuto := .F.
        lMsHelpAuto := .T.

        If cRecusa == "2"
            cTypeShip   := oWorkFlow:oHtml:RetByName('cTipoFrete') //-- Frete
            nTotalShip  := Val(oWorkFlow:oHtml:RetByName('nValorFrete'))
            nExpenditure:= Val(oWorkFlow:oHtml:RetByName('nValorDespesa'))
            nInsurance  := Val(oWorkFlow:oHtml:RetByName('nValorSeguro'))
            cPayCond    := PADR(AllTrim(oWorkFlow:oHtml:RetByName('cCondicao')) ,TAMSX3("C8_COND"   )[1])  //-- Condição de pagamento
            nCurrency   := Val(oWorkFlow:oHtml:RetByName('cMoeda1'))  //-- Moeda
            nTxCurrency := Val(oWorkFlow:oHtml:RetByName('nTaxaMoeda'))  //-- Taxa da Moeda
            dValidade   := Alltrim(oWorkFlow:oHtml:RetByName('dValidade'))  //-- Data de Validade da Cotação
            dValidade   := STOD(StrTran(dValidade,'-',''))
            
            For nX := 1 To Len(oWorkFlow:oHtml:RetByName('It.cProDesc')) //-- Percorre itens da cotação para calculo do valor total dos itens
                nQuantity  := Val(oWorkFlow:oHtml:RetByName('It.nQtDisp')[nX])
                nUnitValue := Val(oWorkFlow:oHtml:RetByName('It.nValor')[nX])
                nTotal     := nUnitValue * nQuantity
                nTotItems  += nTotal
            Next nX

            If lBrazil
                A150SetPGC(.T.) //-- Define que o MATA150 é chamado do PGC
                DbSelectArea("SC8")
                SC8->(DbSetOrder(1))
                SC8->(MsSeek(xFilial('SC8',cBranch) + cNumQuote))

                aadd(aCabec, {"C8_FORNECE", cSupplier  })
                aadd(aCabec, {"C8_LOJA"   , cStore     })
                aadd(aCabec, {"C8_FORNOME", cSupName   })
                aadd(aCabec, {"C8_COND"   , cPayCond   })
                aadd(aCabec, {"C8_FILENT" , cBranch    })
                aadd(aCabec, {"C8_CONTATO", "AUTO"     })
                aadd(aCabec, {"C8_MOEDA"  , nCurrency  })
                aadd(aCabec, {"C8_TXMOEDA", nTxCurrency})
                aadd(aCabec, {"C8_EMISSAO", dDataBase  })
                aadd(aCabec, {"C8_TPFRETE", cTypeShip  })
                
                For nX := 1 to nQtdItens                                             
                    cProduct    := PADR(AllTrim(oWorkFlow:oHtml:RetByName('It.cCodProd')[nX]), nTamProd)
                    cItem       := PADR(AllTrim(oWorkFlow:oHtml:RetByName('It.cItem')[nX]), nTamItem)
                    nQuantity   := Val(oWorkFlow:oHtml:RetByName('It.nQtDisp')[nX])
                    nUnitValue  := Val(oWorkFlow:oHtml:RetByName('It.nValor')[nX])
                    nValDesc    := Val(oWorkFlow:oHtml:RetByName('It.nValDesc')[nX])
                    nTotal      := nQuantity * nUnitValue
                    aRetSB1     := GetAdvFval("SB1", {"B1_UM", "B1_TE"}, cFilSB1 + cProduct, 1)
                    cUnitMed    := aRetSB1[1]
                    cTES        := aRetSB1[2]
					nValIPI     := Val(oWorkFlow:oHtml:RetByName('It.nValIPI')[nX])
                    nValICMSSOL := Val(oWorkFlow:oHtml:RetByName('It.nValICMSSOL')[nX])

                    aadd(aItens,{{"C8_NUMPRO" , cProposal , Nil},;
                            {"C8_PRODUTO", cProduct  , Nil},;
                            {"C8_ITEM"   , cItem     , Nil},;
                            {"C8_TES"    , cTES      , Nil},;
                            {"C8_UM"     , cUnitMed  , Nil},;
                            {"C8_QTDISP" , nQuantity , Nil},;
                            {"C8_PRECO"  , nUnitValue, NIL},;
                            {"C8_TOTAL"  , nTotal    , NIL}})

                    if nValIPI > 0 
                        aadd(aItens[nx], {"C8_VALIPI", nValIPI    , NIL})
                    endif

                    if nValICMSSOL > 0
                        aadd(aItens[nx], {"C8_VALSOL", nValICMSSOL, NIL})
                    endif

                    nDescontoTotal += nValDesc
                Next nX

                Aadd(aCabec ,{"C8_VALDESC"   ,nDescontoTotal            })
            
                MSExecAuto({|v,x,y,k| MATA150(v,x,y,,,,k)},aCabec,aItens,3,.T.)

                // Carrega impostos
                If !lMsErroAuto .And. Len(aImpVal) > 0               
                
                    For nX := 1 to len(aImpVal)
                        cProduct := PADR(AllTrim(oWorkFlow:oHtml:RetByName('It.cCodProd')[nX]), nTamProd)

                        aAdd(aItemsRet, JsonObject():New())
                        aItemsRet[nX]["item"] := aImpVal[nX][1]
                        aItemsRet[nX]["productcode"] := cProduct

                        //Roda cada imposto e atribui o valor no retorno.
                        For nY := 1 to len(aImpVal[nX][2])
                            aItemsRet[nX][aImpVal[nX][2][nY][1]] := aImpVal[nX][2][nY][3]
                        Next nZ
                    Next nX
            
                Endif
                
            Endif
        EndIf     

        For nX := 1 To nQtdItens //-- Percorre itens da cotação.
            cProduct    := PADR(AllTrim(oWorkFlow:oHtml:RetByName('It.cCodProd')[nX]), nTamProd) //-- Informações do produto
            nQuantity   := Val(oWorkFlow:oHtml:RetByName('It.nQtDisp')[nX])
            nUnitValue  := Val(oWorkFlow:oHtml:RetByName('It.nValor')[nX])
            nDesc       := Val(oWorkFlow:oHtml:RetByName('It.nPercDesc')[nX])
            nValDesc    := Val(oWorkFlow:oHtml:RetByName('It.nValDesc')[nX])
            nTotal      := nUnitValue * nQuantity
            cObs        := Alltrim(oWorkFlow:oHtml:RetByName('It.cObservacao')[nX]) //-- Observação
            cSupObs     := IIf(lHasSupObs, Alltrim(oWorkFlow:oHtml:RetByName('It.cObsForn')[nX]), '') //-- Observação para o fornecedor
            cSituacao   := Alltrim(oWorkFlow:oHtml:RetByName('Stat.cSituacao')[nX])  //-- Situação
            cItem       := PADR(Alltrim(oWorkFlow:oHtml:RetByName('It.cItem')[nX]), nTamItem)      //-- Item
            cTES        := GetAdvFval("SB1", "B1_TE", cFilSB1 + cProduct,1)
            cDataEnt    := StoD(StrTran( Alltrim(oWorkFlow:oHtml:RetByName('It.cDtEnt')[nX]), '-', '') )
            
            nIPI        := 0
            nICMSST     := 0
            nICMS       := 0
            nICMSComp   := 0
            nAliqICMS   := 0
            nAliqIPI    := 0
            nAlICMSCp   := 0
            nBaseICMS   := 0
            nBaseIPI    := 0
            nBasICMSol  := 0

            // Carrega impostos
            If lBrazil .And. Len(aItemsRet) > 0
                //Se o parametro estiver ativo ou os valores preenchidos, calcula os impostos
                lHasIPI  := Val(oWorkFlow:oHtml:RetByName('It.nValIPI')[nX]) > 0 
                lHasICMS := Val(oWorkFlow:oHtml:RetByName('It.nValICMSSOL')[nX]) > 0
                nBaseValor  := (nQuantity * nUnitValue)
                If lHasIPI .Or. lGravImpWF
                    nValIPI     := Val(oWorkFlow:oHtml:RetByName('It.nValIPI')[nX])
                    nAliqIpiCl  := round( ((nValIPI * 100) / nBaseValor), 2)
                    nIPI        := iif(nValIPI > 0 .and. aItemsRet[nX]["ipi"] != nValIPI, nValIPI, aItemsRet[nX]["ipi"])
                    nAliqIPI    := iif( nValIPI > 0, nAliqIpiCl, aItemsRet[nX]["aliqipi"] )
                    nBaseIPI    := iif( nValIPI > 0 .and. aItemsRet[nX]["ipibase"] == 0, nBaseValor, aItemsRet[nX]["ipibase"])
                EndIf
                If lHasICMS .Or. lGravImpWF
                    nValICMSSOL := Val(oWorkFlow:oHtml:RetByName('It.nValICMSSOL')[nX])
                    nICMS       := aItemsRet[nX]["icms"]   
                    nICMSComp   := aItemsRet[nX]["icmscomp"]
                    nICMSST     := iif(nValICMSSOL > 0 .and. aItemsRet[nX]["icmsst"] != nValICMSSOL, nValICMSSOL, aItemsRet[nX]["icmsst"])
                    nAliqICMS   := aItemsRet[nX]["aliqicms"]
                    nAlICMSCp   := aItemsRet[nX]["aliqicmscomp"]
                    nBaseICMS   := aItemsRet[nX]["icmsbase"]
                    nBasICMSol  := iif( nValICMSSOL > 0 .and. aItemsRet[nX]["icmssolbase"] == 0, nBaseValor, aItemsRet[nX]["icmssolbase"] )
                EndIf
                nISS        := aItemsRet[nX]["iss"]      
                nDifal      := aItemsRet[nX]["difal"]
                nBaseDifal  := aItemsRet[nX]["difalbase"]
                nPDifalOri  := aItemsRet[nX]["difalpori"]
                nPDifalDes  := aItemsRet[nX]["difalpdes"]   
                nAliqISS    := aItemsRet[nX]["aliqiss"] 
            Endif

            //-- C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_ITEM+C8_NUMPRO+C8_ITEMGRD
            If oModelSC8:SeekLine({{"C8_NUM",cNumQuote},{"C8_FORNECE",cSupplier},{"C8_LOJA",cStore},{"C8_FORNOME", cSupName}})
                If Empty(cEmails)
                    cEmails := oModelSC8:GetValue('C8_EMAILWF')
                    aEmails := StrToArray(cEmails, ';')
                EndIf

                For nY:= oModelSC8:GetLine() To oModelSC8:Length()
                    oModelSC8:GoLine(nY)
                    // Posiciona no item que será atualizado
                    If !(oModelSC8:GetValue('C8_NUM')       == cNumQuote    .And. ;
                         oModelSC8:GetValue('C8_FORNECE')   == cSupplier    .And. ;
                         oModelSC8:GetValue('C8_LOJA')      == cStore       .And. ;
                         oModelSC8:GetValue('C8_ITEM')      == cItem        .And. ;
                         oModelSC8:GetValue('C8_NUMPRO')    == cProposal .And.;
                         oModelSC8:GetValue('C8_FORNOME')    == cSupName)
                        Loop // Se for outro produto vai para o proximo
                    Endif
                    oModelSC8:SetValue('C8_WF' , .T.)
                    If cRecusa == "1"
                        oModelSC8:SetValue('C8_SITUAC', "4")
                    Else
                        oModelSC8:SetValue('C8_TPFRETE' , cTypeShip)
                        oModelSC8:SetValue('C8_COND'    , cPayCond)
                        oModelSC8:SetValue('C8_PRECO'   , nUnitValue)
                        oModelSC8:LoadValue('C8_QTDISP' , nQuantity)
                        oModelSC8:SetValue('C8_DESC'    , nDesc)
                        oModelSC8:SetValue('C8_VLDESC'  , nValDesc)
                        oModelSC8:SetValue('C8_TOTAL'   , nTotal)
                        oModelSC8:SetValue('C8_VALIDA'  , dValidade)
                        oModelSC8:SetValue('C8_PRAZO'   , nAverDays)
                        oModelSC8:SetValue('C8_OBS'     , cObs)
                        oModelSC8:SetValue('C8_SITUAC'  , cSituacao)                    
                        oModelSC8:LoadValue('C8_MOEDA'  , nCurrency)
                        oModelSC8:SetValue('C8_TXMOEDA' , nTxCurrency)
                        oModelSC8:SetValue('C8_DATPRF'  , cDataEnt)

						If lHasSupObs
                            oModelSC8:LoadValue('C8_OBSFOR', cSupObs)
                        EndIf

                        If cTypeShip == 'C'
                            nShipValue := NoRound((nTotal/nTotItems)*nTotalShip, 2)
                            nShipProRated += nShipValue
                            
                            // Ajusta diferença de arredondamento no ultimo item
                            If nX == nQtdItens .And. nShipProRated <> nTotalShip
                                nShipValue += nTotalShip-nShipProRated
                            Endif
                        EndIf
                        
                        oModelSC8:LoadValue('C8_VALFRE', nShipValue)
                        oModelSC8:LoadValue('C8_TOTFRE', nTotalShip)

                        // Rateio de Despesas
                        If nExpenditure > 0
                            nExpendValue := NoRound((nTotal/nTotItems)*nExpenditure, 2)
                            nExpendRated += nExpendValue
                            
                            // Ajusta diferença de arredondamento no ultimo item
                            If nX == nQtdItens .And. nExpendRated <> nExpenditure
                                nExpendValue += nExpenditure-nExpendRated
                            Endif
                        Endif

                        oModelSC8:LoadValue('C8_DESPESA', nExpendValue)

                        // Rateio do Seguro
                        If nInsurance > 0
                            nInsuValue := NoRound((nTotal/nTotItems)*nInsurance, 2)
                            nInsuRated += nInsuValue
                            
                            // Ajusta diferença de arredondamento no ultimo item
                            If nX == nQtdItens .And. nInsuRated <> nInsurance
                                nInsuValue += nInsurance-nInsuRated
                            Endif
                        Endif

                        oModelSC8:LoadValue('C8_SEGURO', nInsuValue)

                        // -- Preenche campos customizados dos itens.
                        If ( lNFC030Ht )
                            if len(aCpoIt) > 0
                                for nZ := 1 to len(aCpoIt)
                                    if aCpoIt[nZ][1] == "SC8" .and. aCpoIt[nZ][4]//.T. se edit, .F. se visual
                                        if aCpoIt[nZ][3] == "N"
                                            oModelSC8:LoadValue(aCpoIt[nZ][2], val(oWorkFlow:oHtml:RetByName('It.'+aCpoIt[nZ][2])[nX]))
                                        elseif aCpoIt[nZ][3] == "L"
                                            oModelSC8:LoadValue(aCpoIt[nZ][2], oWorkFlow:oHtml:RetByName('It.'+aCpoIt[nZ][2]+'_')[nX] == 'true')
                                        elseif ( aCpoIt[nZ][3] == "D" )
                                            oModelSC8:LoadValue(aCpoIt[nZ][2], StoD(StrTran( Alltrim(oWorkFlow:oHtml:RetByName('It.'+aCpoIt[nZ][2])[nX]), '-', '') ))
                                        else
                                            oModelSC8:LoadValue(aCpoIt[nZ][2], Alltrim(oWorkFlow:oHtml:RetByName('It.'+aCpoIt[nZ][2])[nX]))
                                        endif
                                    endif
                                next nZ
                            endif
                        Endif

                        // Impostos
                        If lBrazil .And. Len(aItemsRet) > 0
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_TES')
                                oModelSC8:LoadValue('C8_TES'    , cTES)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_VALIPI')
                                oModelSC8:LoadValue('C8_VALIPI' , nIPI)
                            endif            
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_VALICM')
                                oModelSC8:LoadValue('C8_VALICM' , nICMS)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_VALISS')
                                oModelSC8:LoadValue('C8_VALISS' , nISS)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_ICMSCOM')
                                oModelSC8:LoadValue('C8_ICMSCOM', nICMSComp)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_VALSOL')
                                oModelSC8:LoadValue('C8_VALSOL' , nICMSST)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_DIFAL')
                                oModelSC8:LoadValue('C8_DIFAL'  , nDifal)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_BASEDES')
                                oModelSC8:LoadValue('C8_BASEDES', nBaseDifal)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_PDORI')
                                oModelSC8:LoadValue('C8_PDORI'  , nPDifalOri)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_PDDES')
                                oModelSC8:LoadValue('C8_PDDES'  , nPDifalDes)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_ALIIPI')
                                oModelSC8:LoadValue('C8_ALIIPI' , nAliqIPI)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_PICM')
                                oModelSC8:LoadValue('C8_PICM'   , nAliqICMS)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_ALIQISS')
                                oModelSC8:LoadValue('C8_ALIQISS', nAliqISS)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_ALIQCMP')
                                oModelSC8:LoadValue('C8_ALIQCMP', nAlICMSCp)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_BASEICM')
                                oModelSC8:LoadValue('C8_BASEICM', nBaseICMS)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_BASEIPI')
                                oModelSC8:LoadValue('C8_BASEIPI', nBaseIPI)
                            endif
                            if oMetSX3:vldFieldsSX3('SC8', 'C8_BASESOL')
                                oModelSC8:LoadValue('C8_BASESOL', nBasICMSol)
                            endif
                        Endif 
                    Endif
                    
                    Exit // Quando atualizar ir direto para o proximo item
                Next nY
            EndIf
        Next nX

        //Cotação já foi finalizada, não atualizar cotação
        If lFinali  
            //-- Notificação - Novo Fluxo de Compras | Novo Fluxo de Compras (PGCA010) | A cotação | "foi finalizada. Não é possível enviar a resposta."
            cBody   := NFCHtmlMsg(STR0001, cFilAnt, STR0002, STR0003 + ' ' + AllTrim(oModelDHU:GetValue('DHU_NUM')) + ' - ' + alltrim(oModelDHU:GetValue('DHU_AGPCOT')) + ', ' + STR0023)
            cEmails := oModelSC8:GetValue('C8_EMAILWF')

            If !Empty(cEmails)
                aEmails := StrToArray(cEmails, ';')

                For nX := 1 To Len(aEmails) //-- Envia e-mail para o fornecedor
                    NFCSendMail(AllTrim(aEmails[nX]), cBody, STR0005) //-- Cotação Respondida pelo Fornecedor
                Next nX  
            EndIf
        EndIf
        
        if ( !lFinali )
            If !(oModel:VldData() .And. oModel:CommitData()) //-- Valida e commita a atualização da cotação.
                aError := oModel:GetErrorMessage()
                cMessage := AllTrim(aError[5]) + " - " + AllTrim(aError[6]) + AllTrim(aError[7])
                MsgStop(cMessage, AllTrim(aError[5]))
                FwLogMsg("ERROR",,AllTrim(aError[5]),,,, cMessage)
                oModel:CancelData() //-- Cancela alteração.

                cBody := NFCHtmlMsg(STR0001, cFilAnt, STR0002, 'Ocorreu um erro no workflow quando o fornecedor' + ' ' + AllTrim(cSupName) + ' ' + 'respondeu a cotação' + ' ' +AllTrim(oModelDHU:GetValue('DHU_NUM')) + ' - ' + alltrim(oModelDHU:GetValue('DHU_AGPCOT')) + '. ' + 'Mensagem de erro:' + ' ' + AllTrim(cMessage))
                cEmail := UsrRetMail(oModelDHU:GetValue('DHU_CODCOM'))

                //-- Envia e-mail para o comprador
                NFCSendMail(cEmail, cBody, STR0005) //-- Cotação Respondida pelo Fornecedor
            Else
                If DHU->(FieldPos('DHU_COMPWF')) > 0 .And. oModelDHU:GetValue('DHU_COMPWF') == '1'
                    //-- Notificação - Novo Fluxo de Compras | Novo Fluxo de Compras (PGCA010) | A cotação | foi respondida pelo fornecedor
                    cBody := NFCHtmlMsg(STR0001, cFilAnt, STR0002, STR0003 + ' ' + AllTrim(oModelDHU:GetValue('DHU_NUM')) + ' - ' + alltrim(oModelDHU:GetValue('DHU_AGPCOT')) + ', ' + STR0004 + ' ' + AllTrim(cSupName))
                    cEmail := UsrRetMail(oModelDHU:GetValue('DHU_CODCOM'))

                    //-- Envia e-mail para o comprador
                    NFCSendMail(cEmail, cBody, STR0005) //-- Cotação Respondida pelo Fornecedor
                EndIf

                If lHasResumWf
                    oHtml := BuildResume(cNumQuote, cSupplier, cStore, cSupName, cProposal)

                    For nX := 1 To Len(aEmails) //-- Envia e-mail para o fornecedor
                        NFCSendMail(AllTrim(aEmails[nX]),, STR0006 + ' - ' + alltrim(oHtml:RetByName('cNomeCli')), oHtml) //-- Remumo de itens enviados para cotação
                    Next nX
                EndIf

                if lNFCWFGRV .and. ( lNFC030Ht )
                        aCpoCab := NF030FldWF("1","2")//-- Cabeçalho
                        aCpoIt  := NF030FldWF("2","2")//-- Itens
                        aCpoRod := NF030FldWF("3","2")//-- Rodapé

                    ExecBlock("NFCWFGRV",.F.,.F.,{cNumQuote,cSupplier,cStore,cSupName,cProposal,oWorkFlow:oHtml,nQtdItens,aCpoCab,aCpoIt,aCpoRod})
                endif
            EndIf
        endif
        oModel:Destroy()
        FreeObj(oModel)
    EndIf
    
    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FwFreeArray(aEmails)
    FwFreeArray(aCabec)
    FwFreeArray(aItens)
    FwFreeArray(aItemsRet)
    FwFreeArray(aCpoIt)
    FwFreeArray(aCpoRod)
    FwFreeArray(aCpoCab)
    FwFreeArray(aRetSB1)
    FwFreeObj(oHtml)
    FwFreeObj(oModelDHU)
    FwFreeObj(oModelSC8)
    FwFreeObj(oMetSX3)
Return Nil


/*/{Protheus.doc} calcAvrDays
	Calcula a media do prazo de entrega pelo numero de itens workflow
@author ali.neto
@since 01/2025
@param oModel, object, modelo de dados.
@return nAvDays
/*/
Static Function calcAvrDays(oModelSC8, oWorkFlow, nQtdItens)
 
    Local nItem 	:= 0
	Local nTotDays 	:= 0
	Local nAverDays := 0
    Local cDtEnt    := CTOD(" / / ")

    For nItem := 1 To nQtdItens
		oModelSC8:GoLine(nItem)
        cDtEnt := StoD(StrTran( Alltrim(oWorkFlow:oHtml:RetByName('It.cDtEnt')[nItem]), '-', '') )

		If !Empty(cDtEnt) .and. cDtEnt >= dDataBase
			nTotDays += DateDiffDay(cDtEnt, dDataBase)
		EndIf
	Next nItem

    //-- Calcula a media dos prazos de entra pelo numero de itens
	nAverDays := round(nTotDays / nQtdItens, 0)

Return nAverDays


/*/{Protheus.doc} BuildResume
	Monta objeto HTML com o resumo dos itens cotados pelo fornecedor.
@author juan.felipe
@since 26/02/2025
@param cQuote, character, código da cotação.
@param cSup, character,  código do fornecedor.
@param cStore, character, loja do fornecedor.
@param cName, character, nome do fornecedor.
@param cProposal, character, número da proposta
@return oHtml, object, estrutura do resumo dos itens cotados pelo fornecedor.
/*/
Static Function BuildResume(cQuote, cSup, cStore, cName, cProposal)
    Local cBody         As Character
    Local cFilialSC8    As Character
    Local cLastProp     As Character
    Local oHtml         As Object
    Local oJsonTotals   As Object
    Local aTQtDisp      As Array
    Local aTPreco       As Array
    Local aTTotal       As Array
    Local aAreaSM0      As Array
    Default cQuote      := ''
    Default cSup        := ''
    Default cStore      := ''
    Default cName       := ''
    Default cProposal   := ''

    aAreaSM0 := SM0->(FwGetArea())
    cFilialSC8 := FWxFilial('SC8')
    cLastProp := Padr(PGCLastProp(cQuote, cSup, cStore, cName), TamSX3('C8_NUMPRO' )[1])
    oJsonTotals := PGCQuotTot(cQuote, cSup, cStore, cName)

    aTQtDisp := TamSX3('C8_QTDISP')
    aTPreco := TamSX3('C8_PRECO')
    aTTotal := TamSX3('C8_TOTAL')

    cBody := '<html>'
    cBody += '    <head>'
    cBody += '        <meta charset="windows-1252">'
    cBody += '        <meta charset="UTF-8">'
    cBody += '        <style>'
    cBody += '        </style>'
    cBody += '    </head>'

    cBody += "    <body style='width: 100%; font: normal normal normal 14px 'open sans', sans-serif;overflow-x: hidden'>"
    cBody += '        
    cBody += "        <h1 style='font: normal normal normal 22px 'open sans', sans-serif;color: rgb(0, 136, 203);padding-top: 5px;padding-bottom:3px;margin: 5px auto 5px 10px;'>"
    cBody += '            <span style="color: rgb(0, 136, 203);">%cSTR0007% %cNumCot% - %cSTR0022% %cProposal%</span>'
    cBody += '        </h1>'

    cBody += '        <!-- Cabeçalho do Pedido --> 
    cBody += '        <div style="border-top: solid rgb(0, 136, 203) 2px;padding: 10px 5px 0 10px;margin-right: 15px;margin-top: 10px;">'
    cBody += "            <table style='font: normal normal normal 14px 'open sans', sans-serif;text-align: left;border-width:0px;width:100%;'>"
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0008%</td>'
    cBody += '                    <td style="width:500px;">%cNomeFor%</td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0009% </td>'
    cBody += '                    <td style="width:500px;">%cDataEmis%</td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0010%</td>'
    cBody += '                    <td style="width:500px;">%cCNPJFor%</td>'
    cBody += '                </tr>'
    cBody += '                <tr><td colspan="04"><hr></td></tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0011%</td>'
    cBody += '                    <td style="width:500px;">%cNomeCli%</td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0010%</td>'
    cBody += '                    <td style="width:500px;">%cCNPJCli%</td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0012%</td> '
    cBody += '                    <td colspan="03">%cEndeCli%</td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0013%</td>'
    cBody += '                    <td style="width:500px;">%cCepCli% </td>'
    cBody += '                </tr>'
    cBody += '                <tr>'
    cBody += '                    <td style="width:100px;">%cSTR0014%</td>'
    cBody += '                    <td style="width:500px;">%cFoneCli%</td>'
    cBody += '                </tr>'
    cBody += '            </table>'
    cBody += '        </div>'

    cBody += '        <!-- Itens do Pedido -->'
    cBody += '        <form name="FrontPage_Form1" method="post" action="mailto:%25WFMailTo%25">'
    cBody += '            <div style="border-top: solid rgb(0, 136, 203) 2px;padding: 10px 5px 0 10px;margin-right: 15px;margin-top: 10px;">'
    cBody += "                <table style='font: normal normal normal 14px 'open sans', sans-serif;text-align: left;width:100%;border-collapse: collapse;'>"
    cBody += "                  <thead style='font: normal normal normal 12px 'open sans', sans-serif; background-color: #0c9abe !important; color: White !important; text-align: left; border-width: 0px;'>"
    cBody += '                        <tr style="background-color: #0c9abe !important; color: White !important;">'
    cBody += '                            <th align="center" style="width:300px;">%cSTR0015%</th>'
    cBody += '                            <th align="center" style="width:100px;">%cSTR0016%</th>'
    cBody += '                            <th align="center" style="width:100px;">%cSTR0017%</th>'
    cBody += '                            <th align="center" style="width:100px;">%cSTR0018%</th>'
    cBody += '                            <th align="center" style="width:100px;">%cSTR0019%</th>'
    cBody += '                            <th align="center" style="width:100px;">%cSTR0020%</th>'
    cBody += '                        </tr>'
    cBody += '                    </thead>'
    cBody += '                    <tbody>'
    cBody += '                        <tr style="height: 28px;">'
    cBody += '                            <td style="width:250px; border-bottom: 1px solid #ddd;">%It.cProDesc%</td>'
    cBody += '                            <td align="center" style="width:100px; border-bottom: 1px solid #ddd;">%It.cUnMedida%</td>'
    cBody += '                            <td align="center" style="width:100px; border-bottom: 1px solid #ddd;">%It.nQtDisp%</td>'
    cBody += '                            <td align="center" style="width:100px; border-bottom: 1px solid #ddd;">%It.nVlUnit%</td>'
    cBody += '                            <td align="center" style="width:100px; border-bottom: 1px solid #ddd;">%It.nTotal%</td>'
    cBody += '                            <td align="center" style="width:100px; border-bottom: 1px solid #ddd;">%It.cDtEnt%</td>'
    cBody += '                        </tr>'
    cBody += '                    </tbody>'
    cBody += '                </table>'
    cBody += '            </div>'
    cBody += '        </form>'
    
    cBody += '        <!-- Valor total da cotação -->'
	cBody += "	      <div style='border-top: solid rgb(0, 136, 203) 2px; padding: 10px 5px 10px 10px; margin-right: 15px; margin-top: 10px; font: normal normal normal 16px 'open sans', sans-serif;'>"
	cBody += '	      	<strong>%cSTR0021%:</strong> <span>%cTotalCot%</span>'
	cBody += '	      </div>'

    cBody += '    </body>'
    cBody += '</html>'

    oHtml := TWFHtml():New()
    oHtml:LoadStream(cBody)

    SM0->(MsSeek(cEmpAnt+cFilAnt))

    oHTML:ValByName('cNumCot'  , cQuote)
    oHTML:ValByName('cNomeFor' , cName)
    oHTML:ValByName('cProposal' , cProposal)
    
    oHTML:ValByName('cCNPJFor' , SA2->A2_CGC)
    oHTML:ValByName('cNomeCli' , SM0->M0_NOMECOM)
    oHTML:ValByName('cCNPJCli' , SM0->M0_CGC)
    oHTML:ValByName('cEndeCli' , iif( !empty(DHU->DHU_ENTREG), DHU->DHU_ENTREG, SM0->M0_ENDENT) )
    oHTML:ValByName('cCEPCli'  , iif( !empty(DHU->DHU_ENTREG), "--------" , SM0->M0_CEPENT) )
    oHTML:ValByName('cFoneCli' , SM0->M0_TEL)

    oHTML:ValByName('cSTR0007'  , STR0007)  //-- Solicitação de Cotação
    oHTML:ValByName('cSTR0008'  , STR0008)  //-- Fornecedor
    oHTML:ValByName('cSTR0009'  , STR0009)  //-- Emissão
    oHTML:ValByName('cSTR0010'  , STR0010)  //-- CNPJ
    oHTML:ValByName('cSTR0011'  , STR0011)  //-- Cliente
    oHTML:ValByName('cSTR0012'  , STR0012)  //-- Endereço
    oHTML:ValByName('cSTR0013'  , STR0013)  //-- CEP
    oHTML:ValByName('cSTR0014'  , STR0014)  //-- Telefone
    oHTML:ValByName('cSTR0015'  , STR0015)  //-- Descrição
    oHTML:ValByName('cSTR0016'  , STR0016)  //-- Un. Medida
    oHTML:ValByName('cSTR0017'  , STR0017)  //-- Qtd. Disp
    oHTML:ValByName('cSTR0018'  , STR0018)  //-- Vl. Unitário
    oHTML:ValByName('cSTR0019'  , STR0019)  //-- Total
    oHTML:ValByName('cSTR0020'  , STR0020)  //-- Necessidade
    oHTML:ValByName('cSTR0021'  , STR0021)  //-- Valor total da cotação
    oHTML:ValByName('cSTR0022'  , STR0022)  //-- PROPOSTA

    oHTML:ValByName('cTotalCot' , Alltrim(STR(oJsonTotals['total'] ,aTTotal[1] ,aTTotal[2])))

    SC8->(DbSetOrder(8))
    SC8->(DbSeek(cFilialSC8+cQuote+cSup+cStore+cName)) // Posiciona na Cotação do Fornecedor

    oHTML:ValByName('cDataEmis', SC8->C8_EMISSAO)

    //Percorre itens da cotação
    While SC8->(!Eof()) .And. (cFilialSC8+cQuote+cSup+cStore+cName) == SC8->(C8_FILIAL+C8_NUM+C8_FORNECE+C8_LOJA+C8_FORNOME)
        If cLastProp == SC8->C8_NUMPRO //-- Apenas a última proposta
            Aadd(oHTML:ValByName('It.cProDesc' ), GetAdvFval("SB1","B1_DESC",xFilial('SB1') + SC8->C8_PRODUTO,1))
            Aadd(oHTML:ValByName('It.cUnMedida'), SC8->C8_UM)
            Aadd(oHTML:ValByName('It.nQtDisp'  ), Alltrim(STR(SC8->C8_QTDISP,aTQtDisp[1],aTQtDisp[2])))
            Aadd(oHTML:ValByName('It.nVlUnit'  ), Alltrim(STR(SC8->C8_PRECO ,aTPreco[1] ,aTPreco[2])))
            Aadd(oHTML:ValByName('It.nTotal'   ), Alltrim(STR(SC8->C8_TOTAL ,aTTotal[1] ,aTTotal[2])))
            Aadd(oHTML:ValByName('It.cDtEnt'   ), SC8->C8_DATPRF)
        EndIf

        SC8->(DbSkip())
    EndDo
    FwRestArea(aAreaSM0)
    FwFreeArray(aAreaSM0)
    FwFreeArray(aTQtDisp)
    FwFreeArray(aTPreco)
    FwFreeArray(aTTotal)
    FwFreeObj(oJsonTotals)
Return oHtml
