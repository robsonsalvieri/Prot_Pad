#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#INCLUDE 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'
#INCLUDE 'BACKOFFICE.PGC.QUOTATIONS.REPOSITORY.CH'

namespace pgc.quotationsRepository
using namespace pgc.utils

/*/{Protheus.doc} pgcQuotationsRepository
	Classe Adapter para o serviço de cotações.
@author juan.felipe
@since 27/10/2022
/*/
Class pgcQuotationsRepository FROM FWAdapterBaseV2
    public Data lOk             As Logical
    public Data oJsonRequest    As Object

    public  Method New()
    public  Method Get()
    private Method TreatJSONResponse()
    public  Method getQuotationList()
    public  Method setAttachments()
    public  Method setAnswersQuantity()
    public  Method deleteQuotation()
    public  Method putQuotationHeader()
    public  Method setAttachAnswersQnt()
EndClass


/*/{Protheus.doc} New
	Método construtor
@author juan.felipe
@since 27/10/2022
/*/
Method New(cVerb) Class pgcQuotationsRepository
    Default cVerb       := "GET"
    Self:lOk            := .F.
    Self:oJsonRequest   := Nil
    
    _Super:New(cVerb, .T.)
Return Self

/*/{Protheus.doc} Get
	Processa a consulta de acordo com a opção passada
@author juan.felipe
@since 27/10/2022
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return Nil, nulo.
/*/
Method Get(nPage, nPageSize, nQuery) class pgcQuotationsRepository
    Local aArea  As Array
    Local aQueryRequest As Array
    Local cWhere As Character
    Local oUtils As Object

    Private aDHUFields := {}
    Default nQuery := 1

    Default nPage      := 1
	Default nPageSize  := 10

    aArea := FwGetArea()
    aQueryRequest := {}
    cWhere := GetWhere(nQuery)
    oUtils := pgcUtils():New()

    AddMapFields(self, nQuery)

    Self:setPage(nPage)
    Self:setPageSize(nPageSize)
    Self:SetQuery(GetQuery(nQuery, Self))
    Self:SetWhere(cWhere)
    Self:SetUseSpaces(.T.)

    aQueryRequest := oUtils:queryRequestToArray(oRest:getQueryRequest())

    If Len(aQueryRequest) > 0
        Self:SetUrlFilter(aQueryRequest)
    EndIf

    if nQuery == 1
        Self:SetOrder("DHU_NUM")
    else 
        Self:SetOrderQuery(oUtils:getQueryParam('ORDER'))
    endif
    
    Self:SetFields(oUtils:getQueryParam('FIELDS'))
    
    If Self:Execute()
        Self:FillGetResponse()
        Self:TreatJSONResponse(nQuery, Self:oJsonObj:oJsonObj)
    EndIf

    FwRestArea(aArea)
    FwFreeArray(aArea)
    FreeObj(oUtils)
Return Nil

/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API de acordo com a opção passada
@author juan.felipe
@since 27/10/2022
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2.
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return Nil, nulo.
/*/
Static Function AddMapFields(oSelf, nQuery)

Local aMapFields := {}
Local cFieldType := ""
Local cX3Type    := ""
Local nX         := 1

    if type("aDHUFields") == "A" .and. len(aDHUFields) == 0
        aDHUFields := FWSX3Util():GetAllFields("DHU",.F.)
    endif

    if nQuery == 1 //Listagem de cotações (DHU)
        oSelf:AddMapFields('DHU_FILIAL'     , 'DHU_FILIAL', .T., .T., {'DHU_FILIAL' , 'C', TamSX3('DHU_FILIAL')[1], 0})//branch
        oSelf:AddMapFields('DHU_NUM'        , 'DHU_NUM'   , .T., .T., {'DHU_NUM'    , 'C', TamSX3('DHU_NUM'  )[1], 0})//quotationCode
        oSelf:AddMapFields('DHU_TPDOC'      , 'DHU_TPDOC' , .T., .T., {'DHU_TPDOC'  , 'C', TamSX3('DHU_TPDOC')[1], 0})//typeDoc
        oSelf:AddMapFields('DHU_AVAVEN'     , 'DHU_AVAVEN', .T., .T., {'DHU_AVAVEN' , 'C', TamSX3('DHU_AVAVEN')[1], 0})//overdueProp
        oSelf:AddMapFields('DHU_STATUS'     , 'DHU_STATUS', .T., .T., {'DHU_STATUS' , 'C', TamSX3('DHU_STATUS')[1], 0})//status
        oSelf:AddMapFields('DHU_DTEMIS'     , 'DHU_DTEMIS', .T., .T., {'DHU_DTEMIS' , 'C', TamSX3('DHU_DTEMIS')[1], 0})//emissionDate
        oSelf:AddMapFields('DHU_AGPCOT'     , 'DHU_AGPCOT', .T., .T., {'DHU_AGPCOT' , 'C', TamSX3('DHU_AGPCOT')[1], 0})//quoteNickName
        oSelf:AddMapFields('DHU_DTRCOT'     , 'DHU_DTRCOT', .T., .T., {'DHU_DTRCOT' , 'C', TamSX3('DHU_DTRCOT')[1], 0})//receiptQuoteDate
        oSelf:AddMapFields('DHU_QTDFOR'     , 'DHU_QTDFOR', .T., .T., {'DHU_QTDFOR' , 'N', TamSX3('DHU_QTDFOR')[1], 0})//numberSuppliers
        oSelf:AddMapFields('DHU_QTDPRO'     , 'DHU_QTDPRO', .T., .T., {'DHU_QTDPRO' , 'N', TamSX3('DHU_QTDPRO')[1], 0})//numberProducts
        oSelf:AddMapFields('C7_NUM'         , 'C7_NUM', .T., .T., {'C7_NUM'     , 'C', TamSX3('C7_NUM' )[1]   , 0})//PurchaseOrder
        oSelf:AddMapFields('CN9_NUMERO'     , 'CN9_NUMERO', .T., .T., {'CN9_NUMERO'     , 'C', TamSX3('CN9_NUMERO' )[1]   , 0})//Contract
        
        dbSelectArea('DHU')
        If DHU->(FieldPos('DHU_TPAMR')) > 0
            oSelf:AddMapFields('DHU_TPAMR', 'DHU_TPAMR' , .T., .T., {'DHU_TPAMR'  , 'C', TamSX3('DHU_TPAMR' )[1], 0})
        EndIf

        If DHU->(FieldPos('DHU_COMPRA')) > 0
            oSelf:AddMapFields('DHU_COMPRA'     , 'DHU_COMPRA', .T., .T., {'DHU_COMPRA' , 'C', TamSX3('DHU_COMPRA')[1], 0})//purchaser
        endif

        if DHU->(FieldPos('DHU_CODCOM')) > 0
            oSelf:AddMapFields('DHU_CODCOM'     , 'DHU_CODCOM', .T., .T., {'DHU_CODCOM' , 'C', TamSX3('DHU_CODCOM')[1], 0})//purchaserCode
        endif 
        
        if DHU->(FieldPos('DHU_COMPWF')) > 0
            oSelf:AddMapFields('DHU_COMPWF'     , 'DHU_COMPWF', .T., .T., {'DHU_COMPWF' , 'C', TamSX3('DHU_COMPWF')[1], 0})//envia workflow para o comprador
        endif 
       
        oSelf:AddMapFields('AC9_CODENT'         , 'AC9_CODENT', .T., .F., {'AC9_CODENT', 'C', 10, 0}, )//attachmentQuantity
        
        if type("aDHUFields") == "A" .and. len(aDHUFields) > 0
            aMapFields := oSelf:oJsonObj:aMapFields
            for nX := 1 to len(aDHUFields)
                if aScan(aMapFields,{|x| x[2]==aDHUFields[nX]}) == 0 .and. GetSx3Cache(aDHUFields[nX], "X3_BROWSE") == "S"
                    cX3Type := GetSx3Cache(aDHUFields[nX],"X3_TIPO")
                    If cX3Type != 'M'
                        cFieldType := If(cX3Type == "N", "N", "C")
                        oSelf:AddMapFields(aDHUFields[nX],aDHUFields[nX],.T.,.F.,{aDHUFields[nX],cFieldType,TamSX3(aDHUFields[nX])[1], 0})
                    EndIf
                endif
            next nX  
        endif 

	elseif nQuery == 2
        oSelf:AddMapFields('C7_FILIAL'      , 'C7_FILIAL'   , .T., .T., {'C7_FILIAL'    , 'C', TamSX3('C7_FILIAL')[1], 0})
        oSelf:AddMapFields('C7_NUM'         , 'C7_NUM'      , .T., .T., {'C7_NUM'       , 'C', TamSX3('C7_NUM'  )[1], 0})
        oSelf:AddMapFields('C7_ITEM'        , 'C7_ITEM'     , .T., .T., {'C7_ITEM'      , 'C', TamSX3('C7_ITEM')[1],  0})
        oSelf:AddMapFields('C7_PRODUTO'     , 'C7_PRODUTO'  , .T., .T., {'C7_PRODUTO'   , 'C', TamSX3('C7_PRODUTO')[1], 0})
        oSelf:AddMapFields('C7_DESCRI'      , 'C7_DESCRI'   , .T., .T., {'C7_DESCRI'    , 'C', TamSX3('C7_DESCRI')[1], 0})
        oSelf:AddMapFields('C7_QUANT'       , 'C7_QUANT'    , .T., .T., {'C7_QUANT'     , 'C', TamSX3('C7_QUANT')[1], 0})
        oSelf:AddMapFields('C7_TOTAL'       , 'C7_TOTAL'    , .T., .T., {'C7_TOTAL'     , 'C', TamSX3('C7_TOTAL')[1], 0})
        oSelf:AddMapFields('C7_DATPRF'      , 'C7_DATPRF'   , .T., .T., {'C7_DATPRF'    , 'C', TamSX3('C7_DATPRF')[1], 0})
        oSelf:AddMapFields('C7_FORNECE'     , 'C7_FORNECE'  , .T., .T., {'C7_FORNECE'   , 'C', TamSX3('C7_FORNECE')[1], 0})
        oSelf:AddMapFields('C7_LOJA'        , 'C7_LOJA'     , .T., .T., {'C7_LOJA'      , 'C', TamSX3('C7_LOJA')[1], 0})
        oSelf:AddMapFields('A2_NOME'        , 'A2_NOME'     , .T., .T., {'A2_NOME'      , 'C', TamSX3('A2_NOME')[1], 0})
        oSelf:AddMapFields('quotationcode'  , 'C7_NUMCOT'   , .T., .T., {'C7_NUMCOT'     , 'C', TamSX3('C7_NUMCOT')[1], 0})

    elseif nQuery == 3
        oSelf:AddMapFields('CN9_FILIAL'      , 'CN9_FILIAL'   , .T., .T., {'CN9_FILIAL'    , 'C', TamSX3('CN9_FILIAL')[1], 0})
        oSelf:AddMapFields('CN9_NUMERO'      , 'CN9_NUMERO'   , .T., .T., {'CN9_NUMERO'    , 'C', TamSX3('CN9_NUMERO'  )[1], 0})
        oSelf:AddMapFields('CN1_DESCRI'      , 'CN1_DESCRI'   , .T., .T., {'CN1_DESCRI'  , 'C', TamSX3('CN1_DESCRI')[1], 0})
        oSelf:AddMapFields('CN9_VLINI'       , 'CN9_VLINI'    , .T., .T., {'CN9_VLINI'     , 'C', TamSX3('CN9_VLINI')[1], 0})
        oSelf:AddMapFields('CN9_DTINIC'      , 'CN9_DTINIC'   , .T., .T., {'CN9_DTINIC'  , 'C', TamSX3('CN9_DTINIC')[1],  0})
        oSelf:AddMapFields('CN9_DTFIM'       , 'CN9_DTFIM'   , .T., .T., {'CN9_DTFIM'    , 'C', TamSX3('CN9_DTFIM')[1], 0})
        oSelf:AddMapFields('quotationcode'   , 'CN9_NUMCOT'  , .T., .T., {'CN9_NUMCOT'   , 'C', TamSX3('CN9_NUMCOT')[1], 0})

    EndIf
Return Nil

/*/{Protheus.doc} GetQuery
	Monta a expressão SQL de acordo com a opção passada
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return cQuery, string, query dos fornecedores/itens da cotação.
/*/
Static Function GetQuery(nQuery, oSelf)
    Local cQuery            := ""   As Character
    Local nFor              := 0    As Numeric
    Local nTamMap           := len(oSelf:oJsonObj:aMapFields)
    Local cSelectFields     := ""   As character
    Local cGroupByFields    := ""   As Character
    Local lOracle           := .F.  As Logical

    Default nQuery := 0
    
    If nQuery == 1
        
        for nFor := 1 to nTamMap
            cField := oSelf:oJsonObj:aMapFields[nFor][1]
            cSelectField := oSelf:oJsonObj:aMapFields[nFor][2]
            
            if cField == 'C7_NUM'
                cSelectFields += "MAX(C7_NUM) AS C7_NUM,"
            elseif cField == 'CN9_NUMERO'
                cSelectFields += "MAX(CN9_NUMERO) AS CN9_NUMERO,"
            else 
                cSelectFields += cSelectField + ","
                cGroupByFields += cSelectField + ","
            endif
        next

        // Remove a última vírgula
        cSelectFields := substr(cSelectFields, 1, len(cSelectFields) - 1)
        cGroupByFields := substr(cGroupByFields, 1, len(cGroupByFields) - 1)

        cQuery := "SELECT " + cSelectFields + " FROM ( "
        cQuery += "    SELECT #QueryFields# "
        cQuery += "    FROM " + RetSqlName('DHU') + " DHU "

        cQuery += " LEFT JOIN "+ RetSqlName( 'AC9' ) + " AC9"
        cQuery += "     ON AC9.AC9_FILIAL = '" + FWxFilial('AC9') + "'"
        cQuery += "        AND AC9.AC9_ENTIDA = 'DHU'"
        cQuery += "        AND AC9.AC9_FILENT= '" + FWxFilial('DHU') + "'"

        lOracle := Upper(Alltrim(TcGetDb())) == "ORACLE"
        
        If lOracle
            cQuery += "    AND AC9.AC9_CODENT = DHU.DHU_FILIAL || DHU.DHU_NUM"
        Else
            cQuery += "    AND AC9.AC9_CODENT = CONCAT(DHU.DHU_FILIAL, DHU.DHU_NUM)"
        EndIf
        cQuery += "     AND AC9.D_E_L_E_T_ = ' '"

        cQuery += "    LEFT JOIN " + RetSqlName('SC7') + " SC7 "
        cQuery += "       ON SC7.C7_FILENT = '" + FWxFilial('SC7') + "' "
        cQuery += "         AND SC7.C7_NUMCOT = DHU.DHU_NUM "
        cQuery += "         AND SC7.D_E_L_E_T_ = ' ' "
        cQuery += "    LEFT JOIN " + RetSqlName('SCE') + " SCE "
        cQuery += "        ON "
        cQuery += "         (SCE.CE_FILIAL = '" + FWxFilial('SCE') + "' "
        cQuery += "         OR SCE.CE_FILENT = '" + FWxFilial('SCE') + "') "
        cQuery += "         AND SCE.CE_NUMCOT = DHU.DHU_NUM "
        cQuery += "         AND SCE.D_E_L_E_T_ = ' ' "
        cQuery += "    LEFT JOIN " + RetSqlName('CN9') + " CN9 "
        cQuery += "        ON CN9.CN9_FILIAL = '" + FWxFilial('CN9') + "' "
        cQuery += "         AND CN9.CN9_NUMERO = SCE.CE_NUMCTR "
        cQuery += "         AND CN9.D_E_L_E_T_ = ' ' "
        cQuery += "    WHERE #QueryWhere# "
        cQuery += ") SUBQUERY "
        cQuery += "GROUP BY " + cGroupByFields

	elseif nQuery == 2
		cQuery := " SELECT #QueryFields#"
		cQuery += " FROM " + RetSqlName( 'SC7' ) + " SC7 "
		cQuery += " INNER JOIN " + RetSqlName( 'SA2' ) + " SA2 ON "
        cQuery += "     SA2.A2_FILIAL   = '" + FWxFilial('SA2') + "'" 
        cQuery += "     AND SA2.A2_COD  = C7_FORNECE "
        cQuery += "     AND SA2.A2_LOJA = C7_LOJA "
        cQuery += "     AND SA2.D_E_L_E_T_ = ' ' "
		cQuery += " WHERE #QueryWhere#"
    
	elseif nQuery == 3
		cQuery := " SELECT #QueryFields#"
		cQuery += " FROM " + RetSqlName( 'CN9' ) + " CN9 "
		cQuery += " INNER JOIN " + RetSqlName( 'CN1' ) + " CN1 ON"
		cQuery += "     CN1.CN1_FILIAL = '" + FWxFilial('CN1') + "'" 
		cQuery += "     AND CN1.CN1_CODIGO = CN9.CN9_TPCTO"
		cQuery += "     AND CN1.D_E_L_E_T_ = ' '"
		cQuery += " WHERE #QueryWhere#"
	EndIf
Return cQuery

/*/{Protheus.doc} GetWhere
	Monta a expressão SQL WHERE para consulta dos dados
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@return cQuery, string, expressão where para consulta dos dados
/*/
Static Function GetWhere(nQuery)
    Local cWhere As Character
   
    if nQuery == 1 //Listagem de cotações (DHU)
        cWhere := " DHU.DHU_FILIAL = '"+ FWxFilial('DHU') +"'"
        cWhere += " AND DHU.D_E_L_E_T_ = ' '"

    elseif nQuery == 2 //Listagem pedidos de compras
        cWhere := " SC7.C7_FISCORI = '"+ FWxFilial('SC7') +"'"
        cWhere += " AND SC7.D_E_L_E_T_ = ' '"

    elseif nQuery == 3 //Listagem de contratos
        cWhere := " CN9.CN9_FILIAL = '"+ FWxFilial('CN9') +"'"
        cWhere += " AND CN9.D_E_L_E_T_ = ' '"
    endif
Return cWhere

/*/{Protheus.doc} TreatJSONResponse
	Trata retorno do JSON gerado pelo FWAdapter.
@author juan.felipe
@since 27/10/2022
@param nQuery, numeric, 1- Listagem de cotações, 2- Listagem de Pedidos de Compras, 3- Listagem de Contratos
@param oJsonResponse, object, JSON gerado pelo FWAdapter
@return Nil, nulo.
/*/
Method TreatJSONResponse(nQuery, oJsonResponse) Class pgcQuotationsRepository
    Default nQuery := 1
    Default oJsonResponse := Nil

    If nQuery == 1 //-- Listagem de cotações
        Self:setAttachAnswersQnt(oJsonResponse)
        oJsonResponse['mv_selfor'] := SuperGetMv('MV_SELFOR',.F.,'N') == 'S' //-- Adiciona informação do parâmetro MV_SELFOR
    EndIf
Return Nil


/*/{Protheus.doc} setAttachAnswersQnt
	Adiciona propriedade que indica a existência de anexos aos itens da cotação.
    Adiciona propriedade que indica a quantidade de respostas recebidas dos fornecedores da cotação
@author Ali Ahmad
@since 03/12/2025
@param oJson, object, JSON que receberá as propriedades.
@return Nil, nulo.
/*/
Method setAttachAnswersQnt(oJson) Class pgcQuotationsRepository
    Local aItems    As Array
    Local nX            As Numeric
    Local oJsonAnswers  As Object

    Default oJson := JsonObject():New()

    If oJson <> Nil .And. oJson:hasProperty('items')
        aItems := oJson['items']

        For nX := 1 To Len(aItems)
            aItems[nX]['hasattach'] := .F.
            oJsonAnswers := PGCQtAnswers(aItems[nX]['dhu_num'])

            if !empty(aItems[nX]['ac9_codent'])
                aItems[nX]['hasattach'] := .T.
            endif

            If oJsonAnswers:hasProperty('answered')
                aItems[nX]['proposalsanswered'] := oJsonAnswers['answered']
            EndIf

            if oJsonAnswers:hasProperty('discarded')
			    aItems[nX]['discardedproposals'] := oJsonAnswers['discarded']
            EndIf
        Next nX 
    EndIf
Return Nil


/*/{Protheus.doc} setAttachments
	Adiciona propriedade que indica a existência de anexos aos itens da cotação.
@author Leandro Fini
@since 06/01/2022
@param oJson, object, JSON que receberá as propriedades.
@return Nil, nulo.
/*/
Method setAttachments(oJson) Class pgcQuotationsRepository
    Local aItems    As Array
    Local nX        As Numeric
    Default oJson := JsonObject():New()

    If oJson <> Nil .And. oJson:hasProperty('items')
        aItems := oJson['items']

        For nX := 1 To Len(aItems)
            aItems[nX]['hasattach'] := .F.
            if !empty(aItems[nX]['ac9_codent'])
                aItems[nX]['hasattach'] := .T.
            endif
        Next nX 
    EndIf
Return Nil

/*/{Protheus.doc} setAnswersQuantity
	Adiciona propriedade que indica a quantidade de respostas recebidas dos fornecedores da cotação
@author juan.felipe
@since 04/08/2023
@param oJson, object, JSON que receberá as propriedades.
@return Nil, nulo.
/*/
Method setAnswersQuantity(oJson) Class pgcQuotationsRepository
    Local aItems As Array
    Local nX As Numeric
    Local oJsonAnswers As Object
    Default oJson := JsonObject():New()

    If oJson <> Nil .And. oJson:hasProperty('items')
        aItems := oJson['items']

        For nX := 1 To Len(aItems)
            oJsonAnswers := PGCQtAnswers(aItems[nX]['dhu_num'])
            aItems[nX]['proposalsanswered'] := oJsonAnswers['answered']
			aItems[nX]['discardedproposals'] := oJsonAnswers['discarded']
        Next nX
    EndIf
Return Nil

/*/{Protheus.doc} deleteQuotation
	Deleta cotação.
@author juan.felipe
@since 16/01/2023
@param cCode, object, código da cotação.
@return oJsonRet, object, resposta no formato json.
/*/
Method deleteQuotation(cCode) Class pgcQuotationsRepository
    Local aAreas As Array
    Local cMessage As Character
    Local nCode As Numeric
    Local oJsonRet As Object
    Local oModel As Object
    Local oUtils As Object
    Default cCode := ''

    aAreas := {DHU->(GetArea()), GetArea()}
    oJsonRet := JsonObject():New()
    oUtils := pgcUtils():New()
    Self:lOk := .T.
    
    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM

    If Self:lOk := !Empty(cCode) .And. DHU->(MsSeek(FWxFilial('DHU') + cCode))
        PG020SetOp(MODEL_OPERATION_DELETE) //-- Deleção completa da cotação

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_DELETE)
        oModel:Activate()

        If Self:lOk := oModel:VldData() .And.  oModel:CommitData() //-- Valida e realiza commit
            cMessage := STR0001 + cCode + STR0002 //-- Cotação XXX excluída com sucesso.
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.
        
        oModel:DeActivate()
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0003 + cCode + STR0004) //-- A cotação XXX não foi localizada na base de dados.
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
Return oJsonRet

/*/{Protheus.doc} putQuotationHeader
	Realiza atualização do cabeçalho da cotação.
@author juan.felipe
@since 15/07/2025
/*/
Method putQuotationHeader() Class pgcQuotationsRepository
    Local aAreas As Array
    Local aNames As Array
    Local cFieldName As Character
    Local cFieldNameAux As Character
    Local cFieldType As Character
    Local cQuotationCode As Character
    Local cMessage As Character
    Local nX As Numeric
    Local nCode As Numeric
    Local oModel As Object
    Local oModelDHU As Object
    Local oJsonRet As Object
    Local oUtils As Object

    aAreas := {DHU->(GetArea()), GetArea()}
    cMessage := ''
    oJsonRet := JsonObject():New()
    oUtils := pgcUtils():New()

    DHU->(dbSetOrder(1)) //-- DHU_FILIAL+DHU_NUM
      
    cQuotationCode := PadR(Self:oJsonRequest['dhu_num'], TamSX3('C8_NUM')[1], " ")

    If Self:lOk := DHU->(MsSeek(FWxFilial('DHU') + cQuotationCode))
        PG020SetOp(MODEL_OPERATION_UPDATE)

        oModel := FWLoadModel("PGCA020")
        oModel:SetOperation(MODEL_OPERATION_UPDATE)
        oModel:Activate()
        oModelDHU := oModel:GetModel('DHUMASTER')

        aNames := Self:oJsonRequest:GetNames()

        For nX := 1 To Len(aNames)
            cFieldName := aNames[nX]
            cFieldNameAux := Upper(aNames[nX])
            cFieldType := oModelDHU:GetStruct():GetProperty(cFieldNameAux, MODEL_FIELD_TIPO)

            If cFieldNameAux != 'DHU_NUM' .And. oModelDHU:CanSetValue(cFieldNameAux)
                If cFieldType == 'D'
                    oModelDHU:SetValue(cFieldNameAux, SToD(Self:oJsonRequest[cFieldName]))
                Else
                    oModelDHU:SetValue(cFieldNameAux, Self:oJsonRequest[cFieldName])
                EndIf
            EndIf
        Next nX

        If Self:lOk := !oModel:HasErrorMessage()
            If Self:lOk := oModel:VldData() .And. oModel:CommitData()
                cMessage := STR0005 //-- Cotação atualizada com sucesso.
            EndIf
        EndIf

        nCode := Iif(Self:lOk, 200, 400)
        oJsonRet := oUtils:setClassResponse(nCode, cMessage, oModel) //-- Retorna mensagem da classe no formato json.
        
        oModel:Destroy()
    Else
        oJsonRet := oUtils:setClassResponse(400, STR0003 + cQuotationCode + STR0004) //-- A cotação XXX não foi localizada na base de dados.
    EndIf

    aEval(aAreas, {|x| RestArea(x), FwFreeArray(x)})
    FreeObj(oModel)
    FreeObj(oUtils)
Return oJsonRet
