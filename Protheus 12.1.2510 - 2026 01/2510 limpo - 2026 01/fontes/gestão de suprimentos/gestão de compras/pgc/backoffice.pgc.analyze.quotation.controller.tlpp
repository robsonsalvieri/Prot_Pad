#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'BACKOFFICE.PGC.ANALYZE.QUOTATION.CONTROLLER.CH'

namespace pgc.analyzeQuotationController
using namespace pgc.analyzeQuotationService
using namespace pgc.utils

/*/{Protheus.doc} pgcAnalyzeQuotationController
	API de análise da cotação.
@author juan.felipe
@since 06/01/2023
/*/
Class pgcAnalyzeQuotationController
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public Method New()
    private Method setAnswer()

    @Get("/api/com/v1/pgcAnalyzeQuotation/unansweredQuotationItems")
    public Method getUnansweredQuotationItems()

    @Delete("/api/com/v1/pgcAnalyzeQuotation/deleteQuotationItem")
    public Method deleteQuotationItem()

    @Get("/api/com/v1/pgcAnalyzeQuotation/analysisByItem")
    public Method getAnalysisByItem()

    @Get("/api/com/v1/pgcAnalyzeQuotation/suppliersByQuotationProduct")
    public Method getSuppliersByQuotationProduct()

    @Get("/api/com/v1/pgcAnalyzeQuotation/suppliersByCriterion/:criterion")
    public Method getSuppliersByCriterion()

    @Get("/api/com/v1/pgcAnalyzeQuotation/quotationMap")
    public Method getQuotationMap()

    @Post("/api/com/v1/pgcAnalyzeQuotation/analyzeQuoteByProposal/:quotationcode/:supplier/:store/:documenttype")
    public Method postAnalyzeQuoteByProposal()

    @Post("/api/com/v1/pgcAnalyzeQuotation/analyzeQuoteByItem")
    public Method postAnalyzeQuoteByItem()

    @Get("/api/com/v1/pgcAnalyzeQuotation/purchaseRequestMap")
    public Method getPurchaseReqMap()    
EndClass

/*/{Protheus.doc} New
	Método construtor.
@author juan.felipe
@since 06/01/2023
/*/
Method New() Class pgcAnalyzeQuotationController
Return Self

/*/{Protheus.doc} getUnansweredQuotationItems
	Consulta itens não respondidos da cotação.
@author juan.felipe
@since 06/01/2023
@return .T., logical, consulta itens da cotação que não foram respondidos por nenhum fornecedor.
/*/
Method getUnansweredQuotationItems() Class pgcAnalyzeQuotationController
    Self:setAnswer(1) 
Return .T.

/*/{Protheus.doc} setAnswer
	Monta resposta JSON.
@author juan.felipe
@since 06/01/2023
@param nQuery, numeric, 1- Listagem de itens não respondidos; 2- Listagem de itens; 3- Listagem de fornecedores ordenados por critério.
@return oJsonQryParam, object, objeto json com query params.
@return oJsonResponse, object, objeto json com path params.
@return .T., logical, consulta realizada corretamente.
/*/
Method setAnswer(nQuery, oJsonQryParam, oJsonPathParam, lIsQuotationMap) Class pgcAnalyzeQuotationController
    Local lOk As Logical
    Local cMessage As Character
    Local nCode As Numeric
    Local oAnalyzeQuotationService As Object
    Local oJsonResponse As Object
    Local oUtils As Object

    Default nQuery := 1
    Default oJsonQryParam := Nil
    Default oJsonPathParam := Nil
    Default lIsQuotationMap := .F.

    oAnalyzeQuotationService := pgcAnalyzeQuotationService():New()
    oUtils := pgcUtils():New()

    oUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    
    oAnalyzeQuotationService:lIsQuotationMap := lIsQuotationMap
    oAnalyzeQuotationService:getQueryResponse(Self:nPage, Self:nPageSize, nQuery, oJsonQryParam, oJsonPathParam)
    
    oJsonResponse := oAnalyzeQuotationService:oJsonResponse
    lOk := oAnalyzeQuotationService:lOk
    nCode := oAnalyzeQuotationService:nCode
    cMessage := oAnalyzeQuotationService:cMessage

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk, nCode, cMessage)

    FreeObj(oAnalyzeQuotationService)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} deleteQuotationItem
	Deleta item da cotação.
@author juan.felipe
@since 06/01/2023
@return .T., logical, exclusão realizada corretamente.
/*/
Method deleteQuotationItem() Class pgcAnalyzeQuotationController
    Local lOk As Logical
    Local lBodyHeader As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oAnalyzeQuotationService As Object
    Local oUtils As Object

    lOk := .T.
    lBodyHeader := oRest:getHeaderRequest():HasProperty('body')
    cBody := IIf(lBodyHeader, oRest:getHeaderRequest()['body'], oRest:getBodyRequest())
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse)
        oAnalyzeQuotationService := pgcAnalyzeQuotationService():New()
        oJsonResponse := oAnalyzeQuotationService:deleteQuotationItem(oJsonRequest)
        lOk := oAnalyzeQuotationService:lOk
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oAnalyzeQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} getAnalysisByItem
	Consulta os itens a serem analisados na cotação.
@author Leandro Fini
@since 23/01/2023
@return .T.
/*/
Method getAnalysisByItem() Class pgcAnalyzeQuotationController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(2, oJsonQryParam) 
Return .T.

/*/{Protheus.doc} getSuppliersByQuotationProduct
	Consulta os dados dos fornecedores da cotação que ofertaram um produto.
@author Leandro Fini
@since 23/01/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getSuppliersByQuotationProduct() Class pgcAnalyzeQuotationController
    Local oJsonQryParam As Object
    Local cJsonResponse As Character
    Local oJsonResponse As Object
    Local cQuotationCode As Character
    Local cProductCode As Character
    Local oAnalyzeQuotationService As Object

    oJsonQryParam := oRest:getQueryRequest()
    if ValType(oJsonQryParam) == "J" .And. oJsonQryParam:hasProperty("quotationcode")
        cQuotationCode := oJsonQryParam["quotationcode"]
        cProductCode   := oJsonQryParam["productcode"]
    endif
    
    oAnalyzeQuotationService := pgcAnalyzeQuotationService():New()
    oJsonResponse := oAnalyzeQuotationService:suppliersByQuotationProduct(cQuotationCode,cProductCode) 

    cJsonResponse := FwJsonSerialize(oJsonResponse)
    oRest:SetResponse(cJsonResponse)
Return .T.

/*/{Protheus.doc} getSuppliersByCriterion
	Consulta fornecedores da cotação.
@author juan.felipe
@since 24/01/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method getSuppliersByCriterion() Class pgcAnalyzeQuotationController
    Local oJsonPath As Object

    oJsonPath := oRest:getPathParamsRequest()
    Self:setAnswer(3, Nil, oJsonPath) 
Return .T.

/*/{Protheus.doc} postAnalyzeQuoteByProposal
	Realiza a formalização da cotação por proposta 
    para geração do pedido de compra.
@author Leandro Fini
@since 07/02/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method postAnalyzeQuoteByProposal() Class pgcAnalyzeQuotationController
    
    Local oJsonPath As Object
    Local oJsonResponse As Object
    Local oJsonRequest As Object
    Local cJustification As Character
    Local lBodyHeader As Logical
    Local cBody As Character
    Local aPurchaseOrderObs As Array
    Local oAnalyzeQuotationService As Object
    Local oUtils as Object

    oUtils := pgcUtils():New()
    aPurchaseOrderObs := {}
    cBody := ""
    lBodyHeader := oRest:getHeaderRequest():HasProperty('body')
    cBody := IIf(lBodyHeader, oRest:getHeaderRequest()['body'], oRest:getBodyRequest())
    
    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)

        oJsonPath := oRest:getPathParamsRequest()

        If oJsonRequest:hasProperty('purchaseorderobs')
            aPurchaseOrderObs := oJsonRequest['purchaseorderobs']
        endif

        If lOk := ValType(oJsonPath) == 'J' .And. oJsonPath:hasProperty('quotationcode');
            .And. oJsonPath:hasProperty('supplier') .And. oJsonPath:hasProperty('store');
            .And. oJsonPath:hasProperty('documenttype') .And. oJsonRequest:hasProperty('justification')
            
            cJustification := Alltrim(oJsonRequest['justification'])

            oAnalyzeQuotationService := pgcAnalyzeQuotationService():New()
            oJsonResponse := oAnalyzeQuotationService:analyzeQuoteByProposal(oJsonPath, oJsonRequest, cJustification, aPurchaseOrderObs) 
            lOk := oAnalyzeQuotationService:lOk
        EndIf
    EndIf

    oUtils:setAPIResponse(oRest, oJsonResponse, lOk)

    FreeObj(oAnalyzeQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)
Return .T.

/*/{Protheus.doc} postAnalyzeQuoteByItem
	Realiza a formalização da cotação por item
    para geração do pedido de compra.
@author Leandro Fini
@since 07/02/2023
@return .T., logical, consulta realizada corretamente.
/*/
Method postAnalyzeQuoteByItem() Class pgcAnalyzeQuotationController

    Local lOk As Logical
    Local lBodyHeader As Logical
    Local cBody As Character
    Local oJsonRequest As Object
    Local oJsonResponse As Object
    Local oAnalyzeQuotationService As Object
    Local oUtils as Object

    lOk := .T.
    lBodyHeader := oRest:getHeaderRequest():HasProperty('body')
    cBody := IIf(lBodyHeader, oRest:getHeaderRequest()['body'], oRest:getBodyRequest())
    oJsonRequest := JsonObject():New()
    oJsonResponse := JsonObject():New()
    oUtils := pgcUtils():New()

    If lOk := oUtils:validateRequestBody(cBody, @oJsonRequest, @oJsonResponse, .F.)
        if !oJsonRequest:hasProperty("quotationcode") .or. !oJsonRequest:hasProperty("items")
            lOk := .F.
            oUtils:setAPIResponse(oRest, oJsonResponse, lOk, 400, STR0001 )//"Request enviado faltando uma das propriedades: 'quotationcode', 'proposal', 'items'"
        endif
        if lOk
            oAnalyzeQuotationService := pgcAnalyzeQuotationService():New()
            oJsonResponse := oAnalyzeQuotationService:analyzeQuoteByItem(oJsonRequest)
            lOk := oAnalyzeQuotationService:lOk
            oUtils:setAPIResponse(oRest, oJsonResponse, lOk)
        endif
    Else
        oUtils:setAPIResponse(oRest, oJsonResponse, lOk)
    EndIf

    FreeObj(oAnalyzeQuotationService)
    FreeObj(oJsonRequest)
    FreeObj(oJsonResponse)
    FreeObj(oUtils)

Return .T.

/*/{Protheus.doc} getQuotationMap
	Consulta itens/fornecedores para o mapa de cotação
@author juan.felipe
@since 16/05/2023
@return .T.
/*/
Method getQuotationMap() Class pgcAnalyzeQuotationController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(2, oJsonQryParam,, .T.) 
Return .T.


/*/{Protheus.doc} getPurchaseReqMap
Consulta itens/fornecedores para o mapa de cotação
@author renan.martins
@since 11/2023
@return .T.
/*/
Method getPurchaseReqMap() Class pgcAnalyzeQuotationController
    Local oJsonQryParam As Object

    oJsonQryParam := oRest:getQueryRequest()

    Self:setAnswer(4, oJsonQryParam,, .T.) 
Return .T.
