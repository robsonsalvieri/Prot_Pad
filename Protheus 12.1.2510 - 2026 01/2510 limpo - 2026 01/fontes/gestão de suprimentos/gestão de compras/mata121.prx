#INCLUDE "MATA121.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"

//Ultima atualização: 01/02/2021
/*/
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA121  ³ Autor ³Alexandre Inacio Lemes ³ Data ³06/11/2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao chamada pelo menu do SIGACOM que carrega a funcao    ³±±
±±³          ³ MATA120 com o Flag 1 indicando que a mesma executara as     ³±±
±±³          ³ funcoes de Pedidos de Compra - C7_TIPO = 1                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
/*/
Function Mata121(xAutoCab,xAutoItens,nOpcAuto,lWhenGet,xRatCTBPC,xAdtPC,xRatProj)

	PRIVATE aRotina	:= MenuDef()
	SetRotInteg('MATA120') 
	
	Mata120(1,xAutoCab,xAutoItens,nOpcAuto,lWhenGet,xRatCTBPC,xAdtPC,xRatProj)

Return Nil

/*/
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³Alexandre Inacio Lemes ³ Data ³06/11/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
/*/
Static Function MenuDef()     

PRIVATE aRotina	:= {}

aAdd(aRotina,{STR0001,"PesqBrw"   , 0, 1, 0, .F. }) //"Pesquisar"
aAdd(aRotina,{STR0002,"A120Pedido", 0, 2, 0, Nil }) //"Visualizar"
aAdd(aRotina,{STR0003,"A120Pedido", 0, 3, 0, Nil }) //"Incluir"
aAdd(aRotina,{STR0004,"A120Pedido", 0, 4, 6, Nil }) //"Alterar"
aAdd(aRotina,{STR0005,"A120Pedido", 0, 5, 7, Nil }) //"Excluir"
aAdd(aRotina,{STR0006,"A120Copia" , 0, 9, 0, Nil }) //"Copia"
If SuperGetMv("MV_ENVPED") $ "1|2"
	aAdd(aRotina,{"Reenvia e-mail","A120Mail"  , 0, 2, 0, Nil }) //"Reenvia e-mail"
EndIf
aAdd(aRotina,{STR0007,"A120Impri" , 0, 2, 0, Nil }) //"Imprimir"
aAdd(aRotina,{STR0008,"A120Legend", 0, 2, 0, .F. }) //"Legenda"
aAdd(aRotina,{STR0009,"MsDocument", 0, 4, 0, Nil }) //"Conhecimento" 
aAdd(aRotina,{OemToAnsi(STR0010),"A120Contr", 0, 2, 0, Nil }) //"Rastr.Contrato"
aAdd(aRotina,{STR0011,"CTBC662", 0, 7, 0, Nil }) //"Tracker Contábil" 

//-- Ponto de entrada para inclusão de novas opções no aRotina
If ExistBlock("MT121BRW")
	ExecBlock("MT121BRW",.F.,.F.)
EndIf

If ExistBlock("MT120BRW")
    ExecBlock("MT120BRW",.F.,.F.)
EndIf
Return(aRotina)  


/*
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MTA121TROPºAutor  ³Microsiga           º Data ³  08/24/13   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funçao para automatizar digitação de tipos de operações no º±±
±±º          ³ Pedido de Compra                                           º±±
±±º          ³ Parämetro: nItem - Linha do acols que está posicionado     º±±
±±º          ³ Incluído na validação do campo C7_OPER                     º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MTA121TROP(nItem)

Local lRet	:= .T.
Local nPosOpe	:= 0
Local nPosTes	:= 0
Local nPosCod	:= 0
Local lValOp	:= .F. 
Local cCodOpe	:= ""

Default nItem	:= 0
If INCLUI 
	lValOp	:= GetNewPar("MV_A121OPE",.F.)
	If lValOp .and. nItem > 1
		If ( nPosOpe:= aScan(aHeader, { |x| AllTrim(Upper(x[2])) == 'C7_OPER'}) ) > 0

			cCodOpe := aCols[nItem-1][nPosOpe]

			aCols[nItem][nPosOpe] := cCodOpe

			If MaFisFound("IT",nItem)
				MaFisRef("IT_TPOPER","MT120",cCodOpe)
			EndIf

			nPosTes:= aScan(aHeader, { |x| AllTrim(Upper(x[2])) == 'C7_TES'})
			nPosCod:= aScan(aHeader, { |x| AllTrim(Upper(x[2])) == 'C7_PRODUTO'})
		
			aCols[nItem][nPosTes]	:= MaTesInt(1,aCols[nItem][nPosOpe],cA120Forn,cA120Loj,"F",aCols[nItem][nPosCod],"C7_TES")

		EndIf
	EndIf
EndIf
	
Return(lRet)
