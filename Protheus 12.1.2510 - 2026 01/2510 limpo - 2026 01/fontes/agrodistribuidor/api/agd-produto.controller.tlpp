#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL_PRINCIPIO_ATIVO '/api/agd/v1/produto/principio-ativo/:produto'


namespace agd.produtoApi 
using namespace agd.restUtils
using namespace agd.produtoService

/*/{Protheus.doc} agdNegocioApi
Controller para operações no cadastro de produtos MATA010
Somente para NCR - Produtos Agronegócio
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
class agdProdutoApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Put(PATH_URL_PRINCIPIO_ATIVO)
	public method alterarPrincipioAtivo() as logical

	private method getPathParamProduto() as array

endClass


/*/{Protheus.doc} new
Inicializa instancia da classe
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
method new() class agdProdutoApi
return self


/*/{Protheus.doc} new
Altera produto de acordo com o body enviado
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
method alterarPrincipioAtivo() as logical class agdProdutoApi
	Local oProdutoService As Object
	Local oRestService    As Object
	Local oJsonPath       As Object
	Local jBodyProduto    As Json
	Local cIdProduto      As Character

	oRestService    := agdRestUtils():New()
	oJsonPath       := oRest:getPathParamsRequest()
	jBodyProduto    := oRestService:getRequestBody()
	cIdProduto      := oRestService:getIDWithPathParams(Self:getPathParamProduto())
	if !Empty(cIdProduto)
		cIdProduto := xfilial("SB1") + cIdProduto
		oProdutoService := agdProdutoService():New()
		oProdutoService:setPrincipioAtivo(jBodyProduto, cIdProduto)
		oRestService:setAPIResponse(oRest, oProdutoService)
	EndIf

	FreeObj(oProdutoService)
	FreeObj(oRestService)
	FreeObj(jBodyProduto)

return .T.

/*/{Protheus.doc} getPathParamProduto
Retorna a chave de busca da rota do produto
@type method
@version 12
@author carlos.augusto
@since 21/10/2025
@return array, lista de campos
/*/
Method getPathParamProduto() as array class agdProdutoApi
	Local aArraParams := {}

	AAdd(aArraParams, {'produto' ,'B1_COD'})
return aArraParams



