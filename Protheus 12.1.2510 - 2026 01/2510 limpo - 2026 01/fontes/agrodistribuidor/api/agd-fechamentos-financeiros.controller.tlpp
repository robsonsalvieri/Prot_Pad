#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#DEFINE PATH_URL '/api/agd/v1/fechamentos-financeiro'
#DEFINE PATH_URL_ID '/api/agd/v1/fechamentos-financeiro/:codigo'
#DEFINE PATH_URL_CONFIRMAR '/api/agd/v1/fechamentos-financeiro/:codigo/confirmar'
#DEFINE PATH_URL_CANCELAR '/api/agd/v1/fechamentos-financeiro/:codigo/cancelar'
#DEFINE PATH_VERIFICAR_EDICAO '/api/agd/v1/fechamentos-financeiro/:codigo/verificar-permissao-edicao'


namespace agd.fechamentoFinanceiroApiApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.fechamentoFinanceiroService /*shortCurt de funcao*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.fechamentoFinanceiroStatusService

/*/{Protheus.doc} agdFechamentoFinanceiroApi
Controller de Fechamentos Financeir Barter
@type class
@version 12
@author jean.schulze
@since 17/02/2025
/*/
class agdFechamentoFinanceiroApi
  public Data nPage     As Integer
  public Data nPageSize As Integer

  public method new()

  @Get(PATH_URL)
  public method listar() as logical

  @Get(PATH_URL_ID)
  public method buscarPorId() as logical
  
  @Post(PATH_URL)
  public method registrar() as logical

  @Delete(PATH_URL_ID)
  public method excluir() as logical

  @Put(PATH_URL_CONFIRMAR)
  public method confirmar() as logical
  
  @Put(PATH_URL_CANCELAR)
  public method cancelar() as logical

  @Put(PATH_URL_ID)
  public method atualizar() as logical

  @Get(PATH_VERIFICAR_EDICAO)
  public method permiteAtualizar() as logical

  private Method getPathParamFechamentoFinanceiro() as array

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return variant, instância
/*/
method new() class agdFechamentoFinanceiroApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar Venda Perdida de Negócios Barter
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdFechamentoFinanceiroApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oService := agdFechamentoFinanceiroService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} buscarPorId
Recurso REST para buscar  Venda Perdida de Negócios Barter por ID
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method buscarPorId() as logical class agdFechamentoFinanceiroApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonPath   As Object

    Local cCodigo as Character
   
    oRestService := agdRestUtils():New()
    oService := agdFechamentoFinanceiroService():New()

    //obrigar informar o tenantId

    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
        cCodigo := oJsonPath["codigo"]
    EndIf

    oService:buscarPorId(cCodigo)
    
    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} agdFechamentoFinanceiroApi::excluir
Serviço para excluir fechamento financeiro.
@type method
@version 12
@author jc.maldonado
@since 21/05/2025
/*/
method excluir() class agdFechamentoFinanceiroApi
	local jPath            as object
    local cChave           as character
    local oAGDRestUtils    as object
    local oFechaFinService as object

	if ValType(jPath := oRest:getPathParamsRequest()) == "J";
			.And. jPath:hasProperty("codigo")
		cChave := FWxFilial('NEJ') + jPath["codigo"]
	endIf
	
    oFechaFinService := agdFechamentoFinanceiroService():New()
    oFechaFinService:excluir(cChave)

    oAGDRestUtils := agdRestUtils():New()
	oAGDRestUtils:setAPIResponse(oRest, oFechaFinService)

	FWFreeObj(oAGDRestUtils)
	FWFreeObj(oFechaFinService)
return .t.

/*/{Protheus.doc} confirmar
API Rest para confirmar o Fechamnto Financeiro
@type method
@version P12
@author scheronlini.martins
@since 27/05/2025
@return logical, return_true
/*/
method confirmar() as logical class agdFechamentoFinanceiroApi
	Local oFechamFinanStatus   As Object
	Local oRestService          As Object
	Local oJsonPath             As Object
	Local cIdFechamento        As Character
	Local jCmdAutorizar        As Json

	oRestService  := agdRestUtils():New()

	oJsonPath := oRest:getPathParamsRequest()

	jCmdAutorizar  := oRestService:getRequestBody()

	cIdFechamento := FWxFilial("NEJ")

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
		cIdFechamento += Padr(oJsonPath["codigo"], TamSX3("NEJ_CODIGO")[1])
	EndIf

	oFechamFinanStatus := agdFechamentoFinanceiroStatusService():New(cIdFechamento)
	
	oFechamFinanStatus:confirmar(.F.,jCmdAutorizar["mensagem"] )	

	oRestService:setAPIResponse(oRest, oFechamFinanStatus)

	FreeObj(oFechamFinanStatus)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} agdFechamentoFinanceiroApi::cancelar
API Rest responsável para cancelar um Fechamento Financeiro
@type method
@version P12
@author scheronlini.martins
@since 27/05/2025
@return logical, return_true
/*/
method cancelar() as logical class agdFechamentoFinanceiroApi
	Local oFechamFinanStatus   As Object
	Local oRestService          As Object
	Local oJsonPath             As Object
	Local cIdFechamento        As Character
	Local jCmdCancelar        As Json

	oRestService  := agdRestUtils():New()

	oJsonPath := oRest:getPathParamsRequest()

	jCmdCancelar := oRestService:getRequestBody()

	cIdFechamento := FWxFilial("NEJ")

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
		cIdFechamento += Padr(oJsonPath["codigo"], TamSX3("NEJ_CODIGO")[1])
	EndIf

	oFechamFinanStatus := agdFechamentoFinanceiroStatusService():New(cIdFechamento)
	
	oFechamFinanStatus:cancelar(.F.,jCmdCancelar["mensagem"] )	

	oRestService:setAPIResponse(oRest, oFechamFinanStatus)

	FreeObj(oFechamFinanStatus)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} agdFechamentoFinanceiroApi::registrar
Serviço para registrar fechamento financeiro.
@type method
@version 12
@author jc.maldonado
@since 04/06/2025
/*/
method registrar() class agdFechamentoFinanceiroApi
	local oFechaFinService as object
	local oAGDRestUtils    as object
	local jPayload         as json

	oAGDrestUtils := agdRestUtils():New()

	if oAGDrestUtils:isFilialInformado()
		oFechaFinService := agdFechamentoFinanceiroService():New()
		oFechaFinService:registrar(jPayload := oAGDrestUtils:getRequestBody())

		oAGDRestUtils:setAPIResponse(oRest, oFechaFinService)

		FWFreeObj(oFechaFinService)
		FWFreeObj(jPayload)
	endif

	FWFreeObj(oAGDRestUtils)
return


/*/{Protheus.doc} getPathParamFechamentoFinanceiro
Retorna a chave de busca da rota do fechamento financeiro
@type method
@version 12
@author jean.schulze
@since 30/05/2025
@return array, lista de campos
/*/
Method getPathParamFechamentoFinanceiro() as array class agdFechamentoFinanceiroApi
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NEJ_CODIGO'})

return aArraParams

/*/{Protheus.doc} obterPermissaoEdicao
Verifica se o modelo de negociacao permite edicao com a chave enviada
@type method
@version 12
@author carlos.augusto
@since 05/08/2025
@return logical, true
/*/
method permiteAtualizar() as logical class agdFechamentoFinanceiroApi
	Local oFechaFinService  as Object
	Local oRestService     as Object
	Local cIdFechamento    as character

	oRestService := agdRestUtils():New()

	cIdFechamento := oRestService:getIDWithPathParams(Self:getPathParamFechamentoFinanceiro())
	If !Empty(cIdFechamento)
		cIdFechamento   := FWxFilial("NEJ") + cIdFechamento
		oFechaFinService := agdFechamentoFinanceiroService():New()
		oFechaFinService:verificaAlteracaoModelo(cIdFechamento)
		oRestService:setAPIResponse(oRest, oFechaFinService)
	EndIf

	FreeObj(oFechaFinService)
	FreeObj(oRestService)

Return .T.


/*/{Protheus.doc} atualizar
Atualização de Fechamento Financeiro
@type method
@version 12
@author carlos.augusto
@since 05/08/2025
@return logical, true
/*/
method atualizar() as logical class agdFechamentoFinanceiroApi
	Local oFechaFinService  as Object
	Local oRestService      as Object
	Local cIdFechamento     as Character
	
	oRestService := agdRestUtils():New()

	cIdFechamento := oRestService:getIDWithPathParams(Self:getPathParamFechamentoFinanceiro())
	If !Empty( cIdFechamento )
		cIdFechamento    := FWxFilial("NEJ") + cIdFechamento
		oFechaFinService := agdFechamentoFinanceiroService():New()
		oFechaFinService:atualizar( cIdFechamento, oRestService:getRequestBody())
		oRestService:setAPIResponse( oRest, oFechaFinService)
	EndIf

	FreeObj(oFechaFinService)
	FreeObj(oRestService)

return .T.
