#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'
#include 'AGD.TCO.CONTROLLER.CH'

#define PATH_URL '/api/agd/v1/tco'
#define PATH_URL_ID '/api/agd/v1/tco/:codigo'
#define PATH_URL_ATIVAR '/api/agd/v1/tco/:codigo/ativar'
#define PATH_URL_INATIVAR '/api/agd/v1/tco/:codigo/inativar'
#define PATH_URL_STATUS '/api/agd/v1/tco/status'
#DEFINE PATH_URL_FILIAL '/api/agd/v1/tco/filiais-modulo'
#DEFINE PATH_URL_FEATURES '/api/agd/v1/tco/features-disponiveis'
#DEFINE PATH_URL_IMCOMPATIBILIDADE '/api/agd/v1/tco/features-incompatibilidades'
#DEFINE PATH_URL_FEATURES_CADASTROS '/api/agd/v1/tco/features/:codigo/cadastros'
#DEFINE PATH_URL_FEATURES_PARAMETROS '/api/agd/v1/tco/features/:codigo/parametros'
#DEFINE PATH_URL_FINAL_CONFIG 'api/agd/v1/tco/finalizar-configuracao'
#DEFINE PATH_URL_PARAM_FEATURE '/api/agd/v1/tco/features/parametros/:codigo'
#DEFINE PATH_URL_FINALIZAR_FEATURE '/api/agd/v1/tco/features/:codigo/finalizar'
#define PATH_URL_DEPARA '/api/agd/v1/tco/:codigo/de-para'
#DEFINE PATH_URL_FEATURES_CONFIG '/api/agd/v1/tco/features/:codigoFeature/configuracao'
#DEFINE PATH_URL_FEATURES_ADAPTER '/api/agd/v1/tco/features/:codigoFeature/adapters'
#DEFINE PATH_URL_FEATURES_DEPARA '/api/agd/v1/tco/features/:codigoFeature/depara'
#DEFINE PATH_URL_TESTAR_CONEXAO '/api/agd/v1/tco/:codigo/testar-conexao'
#DEFINE PATH_URL_FEATURE_COMPLEMENTO '/api/agd/v1/tco/:codigo/complemento/:tipo/:contexto'
#DEFINE PATH_URL_FEATURE_COMPLEMENTO_ALL '/api/agd/v1/tco/:codigo/complemento/:tipo/:contexto/all'
#define PATH_URL_DOC_ENTRADA '/api/agd/v1/tco/:codigo/notas-entrada/:id'
#define PATH_URL_DOC_SAIDA '/api/agd/v1/tco/:codigo/notas-saida/:id'
#define PATH_URL_DEPARA_REMOVER '/api/agd/v1/tco/:codigo/de-para/:tabela/:campo/:idInterno/:idExterno'


namespace agd.tcoAPI
using namespace agd.restUtils
using namespace agd.TCOservice
using namespace agd.TCODeParaService
using namespace agd.wizardTCOService /*shortCurt de funcao*/
using namespace agd.TCOdocumentosFiscaisService

/*/{Protheus.doc} agdTCOapi
Controller TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdTCOapi

	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Get(PATH_URL)
	public method listar() as logical

	@Get(PATH_URL_ID)
	public method getById() as logical

	@Get(PATH_URL_STATUS)
	public method getStatus() as logical

	@Get(PATH_URL_FEATURES)
	public method getFeaturesDisponiveis() as logical

	@Get(PATH_URL_IMCOMPATIBILIDADE)
	public method getIncompatibilidadeFeatures() as logical

	@Get(PATH_URL_FEATURES_CADASTROS)
	public method getFeaturesCadastros() as logical

	@Get(PATH_URL_FEATURES_PARAMETROS)
	public method getFeaturesParametros() as logical

	@Get(PATH_URL_FEATURES_CONFIG)
	public method getFeatureRotas() as logical

	@Get(PATH_URL_FEATURES_ADAPTER)
	public method getFeatureAdapter() as logical

	@Get(PATH_URL_FEATURES_DEPARA)
	public method getFeatureDePara() as logical

	@Post(PATH_URL)
	public method registrarFeature() as logical

	@Post(PATH_URL_FILIAL)
	public method informarFilial() as logical

	@Put(PATH_URL_ATIVAR)
	public method ativar() as logical

	@Put(PATH_URL_INATIVAR)
	public method inativar() as logical

	@Post(PATH_URL_FINAL_CONFIG)
	public method finalizarConfiguracao() as logical

	@Post(PATH_URL_PARAM_FEATURE)
	public method parametrosFeature() as logical

	@Put(PATH_URL_FINALIZAR_FEATURE)
	public method finalizarFeature() as logical

	@Post(PATH_URL_DEPARA)
	public method informarDePara() as logical

	@PUT(PATH_URL_TESTAR_CONEXAO)
	public method testarConexaoIntegracao() as logical

	@Get(PATH_URL_FEATURE_COMPLEMENTO)
	public method listarFeatureComplemento() as logical

	@Post(PATH_URL_FEATURE_COMPLEMENTO)
	public method registrarFeatureComplemento() as logical

	@Post(PATH_URL_FEATURE_COMPLEMENTO_ALL)
	public method registrarFeatureComplementoAll() as logical

	@Get(PATH_URL_DOC_ENTRADA)
	public method getDocEntradaById() as logical

	@Get(PATH_URL_DOC_SAIDA)
	public method getDocSaidaById() as logical

	@Delete(PATH_URL_DEPARA_REMOVER)
	public method deleteFeatureDePara() as logical

	private method getPathParamFeature() as array
endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method new() class agdTCOapi
return self

/*/{Protheus.doc} getStatus
Retorna o status da configuraÁ„o do TCO
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return logical, always true
/*/
method getStatus() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object

	oAGDrestUtils := agdRestUtils():New()
	oTCOservice := agdTCOservice():New()

	oTCOservice:getStatus()

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)

return .t.

/*/{Protheus.doc} listar
Recurso REST para listar as Features Selecionadas do MÛdulo
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdTCOapi
	Local oService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil

	oRestService := agdRestUtils():New()
	oService := agdTCOservice():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oService:getFeatures(Self:nPage, Self:nPageSize, oJsonQryParam)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} getById
Retorna feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method getById() As logical Class agdTCOapi
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdTCOservice():New()
		oTCOservice:getById(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.


/*/{Protheus.doc} registrarFeature
Registra uma Feature
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return logical, retorno
/*/
method registrarFeature() as logical class agdTCOApi
	Local oTCOservice As Object
	Local oRestService      As Object
	Local jCmdRegistrar     as Json

	oRestService := agdRestUtils():New()
	oTCOservice := agdTCOservice():New()

	jCmdRegistrar := oRestService:getRequestBody()

	oTCOservice:registrarFeature(jCmdRegistrar)

	oRestService:setAPIResponse(oRest, oTCOservice)

	FreeObj(oTCOservice)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.

/*/{Protheus.doc} informarFilial
Recurso rest para informar as filiais q vao ser utilizadas no tco
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method informarFilial() as logical class agdTCOApi
	Local oWizardTCOService As Object
	Local oRestService      As Object
	Local jCmdRegistrar     as Json

	oRestService := agdRestUtils():New()
	oWizardTCOService := agdWizardTCOService():New()

	jCmdRegistrar := oRestService:getRequestBody()

	oWizardTCOService:informarFiliais(jCmdRegistrar['compartilhaFilial'], jCmdRegistrar['filiaisSelecionadas'] )

	oRestService:setAPIResponse(oRest, oWizardTCOService)

	FreeObj(oWizardTCOService)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.

/*/{Protheus.doc} getFeaturesDisponiveis
Retorna as features disponiveis
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return variant, retorno
/*/
method getFeaturesDisponiveis() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object

	oAGDrestUtils := agdRestUtils():New()
	oTCOservice := agdTCOservice():New()

	oTCOservice:getFeaturesDisponiveis()

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)

return .t.

/*/{Protheus.doc} getIncompatibilidadeFeatures
Verifica a incompatilidade no cadastro de Features
@type method
@version p12 
@author scheronlini.martins
@since 29/09/2025
@return variant, true
/*/
method getIncompatibilidadeFeatures() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local jPayLoad      as json

	oAGDrestUtils := agdRestUtils():New()

	oTCOservice := agdWizardTCOService():New()

	jPayLoad := JsonObject():New()
	jPayLoad:fromJson(oTCOservice:getIncompatibilidadeFeatures())

	oTCOservice:setSuccess(jPayLoad)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	oTCOservice := nil

	FWFreeObj(jPayLoad)
	jPayLoad := nil

	FWFreeObj(oAGDRestUtils)
	oAGDRestUtils := nil

return .t.

/*/{Protheus.doc} getFeaturesCadastros
Retorna os cadastros da feature
@type method
@version 12
@author jc.maldonado
@since 08/10/2025
/*/
method getFeaturesCadastros() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local jPathParams   as json

	oAGDRestUtils := agdRestUtils():New()
	oTCOservice   := agdTCOservice():New()
	jPathParams   := oRest:getPathParamsRequest()
	oTCOservice:getFeatureCadastro(jPathParams['codigo'])

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} getFeaturesParametros
Retorna os parametros da feature
@type method
@version 12
@author jc.maldonado
@since 08/10/2025
/*/
method getFeaturesParametros() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local jPathParams   as json

	oAGDRestUtils := agdRestUtils():New()
	oTCOservice   := agdTCOservice():New()
	jPathParams   := oRest:getPathParamsRequest()
	oTCOservice:getFeatureParametros(jPathParams['codigo'])

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} inativar
Inativar feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method inativar() As logical Class agdTCOapi
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdTCOservice():New()
		oTCOservice:inativar(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} ativar
Ativar Feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method ativar() As logical Class agdTCOapi
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdTCOservice():New()
		oTCOservice:ativar(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} getPathParamFeature
Retorna a chave de busca da rota da feature
@type method
@version 12
@author jean.schulze
@since 30/05/2025
@return array, lista de campos
/*/
Method getPathParamFeature() as array class agdTCOapi
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NE1_CODIGO'})
return aArraParams

/*/{Protheus.doc} finalizarConfiguracao
FunÁ„o responsavel por finalizar a configuraÁ„o DTO
@type method
@version p12
@author scheronlini.martins
@since 09/10/2025
@return variant, return_true
/*/
method finalizarConfiguracao() class agdTCOapi as logical
	local oAGDRestUtils as object
	local oTCOservice   as object

	oAGDrestUtils := agdRestUtils():New()

	oTCOservice := agdWizardTCOService():New()

	Begin Transaction

		oTCOservice:finalizarConfiguracao()
		if !oTCOservice:isSuccess()
			oTCOservice:setError(oTCOservice:getMessageError())
			disarmTransaction()
		endif

	End Transaction
	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	oTCOservice := nil


	FWFreeObj(oAGDRestUtils)
	oAGDRestUtils := nil

return .t.

/*/{Protheus.doc} informarDePara
Informa DE/PARA para as features do TCO
@type method
@version 12
@author jean.schulze
@since 30/10/2025
@return logical, true
/*/
method informarDePara() as logical class agdTCOapi
	local oTCOService   := agdTCOService():New()
	Local oRestService  := agdRestUtils():New()
	Local jCmdRegistrar as Json

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		jCmdRegistrar := oRestService:getRequestBody()
		jCmdRegistrar['feature'] := cFeature

		oTCOService:registrarDePara(jCmdRegistrar)

		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOService)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.

/*/{Protheus.doc} parametrosFeature
Atualiza os par‚metros configur·veis de uma Feature do mÛdulo TCO.
@type method
@version P12
@author scheronlini.martins
@since 10/10/2025
@return logical, sempre .T.
@description
Este mÈtodo recebe via API os par‚metros configur·veis de uma feature e
encaminha ao service agdTCOService, que realiza as validaÁıes e atualizaÁıes
na tabela SX6, retornando a resposta em formato JSON.
/*/
method parametrosFeature() class agdTCOApi as logical
	local oAGDRestUtils := agdRestUtils():New()
	local oTCOService   := agdTCOService():New()
	local cFeature      := ""
	local aParametros   := {}
	local cJsonResp     := ""

	cFeature := oAGDRestUtils:getIDWithPathParams(Self:getPathParamFeature())

	aParametros := oAGDRestUtils:getRequestBody(oRest)

	if ValType(aParametros) != "J"
		oTCOService:setError(STR0001, 400) //"Nenhum par‚metro foi informado no corpo da requisiÁ„o."
	else
		Begin Transaction
			cJsonResp := oTCOService:parametrosFeature(cFeature, aParametros)
			if !oTCOService:isSuccess()
				disarmTransaction()
				oTCOService:setError(oTCOService:getMessageError())
			endif
		End Transaction
	endif


	oAGDRestUtils:setAPIResponse(oRest, oTCOService)

	FWFreeObj(oTCOService)
	FWFreeObj(oAGDRestUtils)

return .T.

/*/{Protheus.doc} finalizarFeature
Endpoint PUT para finalizar a configuraÁ„o de uma Feature de NegÛcio no TCO.
Orquestra a chamada ao service (finalizarConfiguracao) e retorna a resposta
no padr„o da API (HTTP 200/400/404/500 conforme validaÁıes e erros).
@type  method
@author rodrigo.nsoledade
@since  14/10/2025
@return logical, sempre .T.
/*/
method finalizarFeature() Class agdTCOapi
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local jPathParams           as json

	oRestService := agdRestUtils():New()
	oTCOservice  := agdTCOservice():New()
	jPathParams  := oRest:getPathParamsRequest()

	if !Empty(jPathParams['codigo'])
		// Executa a finalizaÁ„o e valida cadastros
		oTCOservice:finalizarFeature(jPathParams['codigo'])
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} getFeatureRotas
Retornar a lista das rotas relacionado a feature
@type method
@version 12
@author Gison.Venturi
@since 10/10/2025
@return variant, retorno
/*/
method getFeatureRotas() class agdTCOapi as logical
	Local oAGDRestUtils as object
	Local oTCOservice   as object
	Local oJsonPath		as object
	Local cCodFeature	as character

	oAGDRestUtils := agdRestUtils():New()
	oTCOservice   := agdTCOservice():New()
	oJsonPath	  := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigoFeature")
		cCodFeature := oJsonPath["codigoFeature"]
	EndIf

	oTCOservice:getFeatureRotas(cCodFeature)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)

return .T.

/*/{Protheus.doc} getFeatureAdapter
Retornar a lista dos adapters relacionados a feature
@type method
@version 12
@author Gison.Venturi
@since 10/10/2025
@return variant, retorno
/*/
method getFeatureAdapter() class agdTCOapi as logical
	Local oAGDRestUtils	as object
	Local oTCOService	as object
	Local oJsonPath		as object
	Local cCodFeature	as character

	oAGDRestUtils := agdRestUtils():New()
	oTCOservice   := agdTCOservice():New()
	oJsonPath     := oRest:getPathParamsRequest()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigoFeature")
		cCodFeature := oJsonPath["codigoFeature"]
	EndIf

	oTCOService:getFeatureAdapter(cCodFeature)

	oAGDRestUtils:setAPIResponse(oRest, oTCOService)

	FWFreeObj(oTCOService)
	FWFreeObj(oAGDRestUtils)

return .T.

/*/{Protheus.doc} getFeatureDePara
Retornar a lista de-para relacionados a feature
@type method
@version 12
@author Gison.Venturi
@since 10/10/2025
@return variant, retorno
/*/
method getFeatureDePara() class agdTCOapi as logical
	Local oAGDRestUtils as object
	Local oTCOservice   as object
	Local oJsonPath		as object
	Local oJsonQryParam as object
	Local cCodFeature	as character

	oAGDRestUtils := agdRestUtils():New()
	oTCOservice   := agdTCODeParaService():New()
	oJsonPath     := oRest:getPathParamsRequest()

	oAGDRestUtils:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oAGDRestUtils:getQueryParams()

	If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigoFeature")
		cCodFeature := oJsonPath["codigoFeature"]
	EndIf

	oTCOservice:findByFeature(cCodFeature, oJsonQryParam, Self:nPage, Self:nPageSize )

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
	FWFreeObj(oJsonQryParam)

return .T.




/*/{Protheus.doc} testarConexaoIntegracao
Testa a conexao com a integracao (rota EAI)
@type method
@version 12
@author jc.maldonado
@since 27/10/2025
@return logical, always true
/*/
method testarConexaoIntegracao() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local cFeature      as character

	oAGDrestUtils := agdRestUtils():New()
	cFeature      := RTrim(oAGDrestUtils:getIDWithPathParams(::getPathParamFeature()))

	oTCOservice := agdTCOservice():New()
	oTCOservice:checkIntegrationConnectivity(cFeature)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} registrarFeatureComplemento
Registra um complemento da Feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@return logical, always true
/*/
method registrarFeatureComplemento() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local jComplemento  as json

	oAGDRestUtils := agdRestUtils():New()

	jComplemento             := oAGDRestUtils:getRequestBody()
	jComplemento['codigo']   := oAGDRestUtils:getIDWithPathParams({{'codigo'  , 'NE2_CODIGO'}})
	jComplemento['tipo']     := oAGDRestUtils:getIDWithPathParams({{'tipo'    , 'NE2_TIPO'  }})
	jComplemento['contexto'] := oAGDRestUtils:getIDWithPathParams({{'contexto', 'NE2_CONTEX'}})

	oTCOservice   := agdTCOservice():New()
	oTCOservice:registrarFeatureComplemento(jComplemento)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.


/*/{Protheus.doc} registrarFeatureComplemento
Registra V·rios de complemento da Feature
@type method
@version 12
@author lindembergson.pacheco
@since 14/11/2025
@return logical, always true
/*/
method registrarFeatureComplementoAll() as logical class agdTCOapi
	local oAGDRestUtils as object
	local oTCOservice   as object
	local jComplemento  as json

	oAGDRestUtils := agdRestUtils():New()

	jComplemento             := oAGDRestUtils:getRequestBody()
	jComplemento['codigo']   := oAGDRestUtils:getIDWithPathParams({{'codigo'  , 'NE2_CODIGO'}})
	jComplemento['tipo']     := oAGDRestUtils:getIDWithPathParams({{'tipo'    , 'NE2_TIPO'  }})
	jComplemento['contexto'] := oAGDRestUtils:getIDWithPathParams({{'contexto', 'NE2_CONTEX'}})

	oTCOservice   := agdTCOservice():New()
	oTCOservice:registrarFeatureComplementoAll(jComplemento)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} listarFeatureComplemento
Lista os complementos da feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@return logical, always true
/*/
method listarFeatureComplemento() as logical class agdTCOapi
	local oAGDRestUtils as object
	local jComplemento  as json
	local jQryParam     as json
	local aNamesJson    as array
	local nX            as numeric
	local oTCOservice   as object

	oAGDRestUtils            := agdRestUtils():New()
	jComplemento             := JsonObject():New()
	jComplemento['codigo']   := oAGDRestUtils:getIDWithPathParams({{'codigo'  , 'NE2_CODIGO'}})
	jComplemento['tipo']     := oAGDRestUtils:getIDWithPathParams({{'tipo'    , 'NE2_TIPO'  }})
	jComplemento['contexto'] := oAGDRestUtils:getIDWithPathParams({{'contexto', 'NE2_CONTEX'}})

	jQryParam  := oAGDRestUtils:getQueryParams()
	aNamesJson := jQryParam:GetNames()
	if ! empty(aNamesJson)
		for nX := 1 to Len(aNamesJson)
			if ! aNamesJson[nX] $ 'codigo;tipo;contexto'
				jComplemento[aNamesJson[nX]] := jQryParam[aNamesJson[nX]]
			endIf
		next nX
	endif

	oTCOservice := agdTCOservice():New()
	oTCOservice:listarFeatureComplemento(jComplemento)

	oAGDRestUtils:setAPIResponse(oRest, oTCOservice)

	FWFreeObj(oTCOservice)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} agdTCOapi::getDocEntradaById
Retorna informacoes do documento de entrada por ID
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@return logical, always true
/*/
method getDocEntradaById() as logical class agdTCOapi
	local oAGDRestUtils     as object
	local cFeature          as character
	local jPath             as json
	local cNotaId           as character
	local oDocFiscalService as object

	oAGDRestUtils := agdRestUtils():New()
	cFeature      := alltrim(oAGDRestUtils:getIDWithPathParams(::getPathParamFeature()))

	jPath   := oRest:getPathParamsRequest()
	cNotaId := jPath['id']

	if ! Empty(cFeature) .and. ! Empty(cNotaId)
		oDocFiscalService := agdTCOdocumentosFiscaisService():New()
		oDocFiscalService:getDocEntradaById(cFeature, cNotaId)
	endif

	oAGDRestUtils:setAPIResponse(oRest, oDocFiscalService)

	FWFreeObj(oDocFiscalService)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} agdTCOapi::getDocSaidaById
Retorna informacoes do documento de saùda por ID
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@return logical, always true
/*/
method getDocSaidaById() as logical class agdTCOapi
	local oAGDRestUtils     as object
	local cFeature          as character
	local jPath             as json
	local cNotaId           as character
	local oDocFiscalService as object

	oAGDRestUtils := agdRestUtils():New()
	cFeature      := alltrim(oAGDRestUtils:getIDWithPathParams(::getPathParamFeature()))

	jPath   := oRest:getPathParamsRequest()
	cNotaId := jPath['id']

	if ! Empty(cFeature) .and. ! Empty(cNotaId)
		oDocFiscalService := agdTCOdocumentosFiscaisService():New()
		oDocFiscalService:getDocSaidaById(cFeature, cNotaId)
	endif

	oAGDRestUtils:setAPIResponse(oRest, oDocFiscalService)

	FWFreeObj(oDocFiscalService)
	FWFreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} deleteFeatureDePara
Deleta um registro de-para de uma feature
@type method
@version 12
@author beatriz.dantas
@since 08/12/2025
@return logical, true
/*/
method deleteFeatureDePara() as logical class agdTCOapi
	Local oTCOService   as Object
	Local oRestService  as Object
	Local cFeature      as character
	Local jCmdDelete    as Json
	local jPath			as Json


	oRestService := agdRestUtils():New()
	oTCOService  := agdTCOservice():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())
	jPath			:= oRest:getPathParamsRequest()
	jCmdDelete             := JsonObject():New()
	if !Empty(cFeature)
		
		jCmdDelete['feature'] := cFeature
		jCmdDelete['tabela'] := jPath['tabela']
		jCmdDelete['campo'] := jPath['campo']
		jCmdDelete['idInterno'] := jPath['idInterno']
		jCmdDelete['idExterno'] := jPath['idExterno']

		oTCOService:deleteDePara(jCmdDelete)

		oRestService:setAPIResponse(oRest, oTCOService)
	endif

	FreeObj(oTCOService)
	FreeObj(oRestService)
	FreeObj(jCmdDelete)
return .T.
