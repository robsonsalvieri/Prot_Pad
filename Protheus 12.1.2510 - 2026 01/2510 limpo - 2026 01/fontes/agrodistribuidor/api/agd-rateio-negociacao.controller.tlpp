#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
///api/agd/v1/negociacoes-rateios-entregas
#define PATH_URL '/api/agd/v1/negociacoes/rateios-entregas'
#define PATH_URL_ID '/api/agd/v1/negociacoes/:idNegocio/:idInsumo/rateios-entregas'

namespace agd.rateioNegociacaoApi

using namespace agd.restUtils
using namespace agd.rateioNegociacaoService

/*/{Protheus.doc} agdRateioNegociacaoApi
API Rest de Rateio de Entrega da Negociação
@type class
@version 12
@author jc.maldonado
@since 26/05/2025
/*/
class agdRateioNegociacaoApi
	public data nPage     as integer
	public data nPageSize as integer

	public method new()

	@Get(PATH_URL)
	public method listar() as logical

	@Post(PATH_URL_ID)
	public method registrar() as logical

	@Put(PATH_URL_ID)
	public method atualizar() as logical

	@Delete(PATH_URL_ID)
	public method deletar() as logical
endClass

/*/{Protheus.doc} agdRateioNegociacaoApi::new
Construtor
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@return object, self
/*/
method new() class agdRateioNegociacaoApi
return self

/*/{Protheus.doc} agdRateioNegociacaoApi::listar
Lista Rateio Entrega Negociação
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@return logical, always true
/*/
method listar() as logical class agdRateioNegociacaoApi
	local oRateioNegociacaoService as object
	local oAGDRestUtils            as object
	local jQueryParam              as json

	oAGDRestUtils := agdRestUtils():New()
	oRateioNegociacaoService := agdRateioNegociacaoService():New()

	oAGDRestUtils:getPageAndPageSize(@::nPage, @::nPageSize)
	jQueryParam := oAGDRestUtils:getQueryParams()

	oRateioNegociacaoService:listar(::nPage, ::nPageSize, jQueryParam)

	oAGDRestUtils:setAPIResponse(oRest, oRateioNegociacaoService)

	FWFreeObj(oRateioNegociacaoService)
	FWFreeObj(oAGDRestUtils)
return .T.

/*/{Protheus.doc} agdRateioNegociacaoApi::registrar
Registra Rateio Entrega Negociação
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@return logical, always true
/*/
method registrar() as logical class agdRateioNegociacaoApi
	local oRateioNegociacaoService as object
	local oAGDRestUtils            as object
    Local jPathParams              As Json
    Local cIdNegocio               As Character
    Local cIdInsumo                As Character
    Local oJPayload                As Object

	oAGDRestUtils := agdRestUtils():New()

	oRateioNegociacaoService := agdRateioNegociacaoService():New()
 	
	// Path params
    jPathParams  := oRest:getPathParamsRequest()
    cIdNegocio   := jPathParams["idNegocio"]
    cIdInsumo    := jPathParams["idInsumo"]

	oJPayload := oRateioNegociacaoService:convertEntregasPayload(oAGDRestUtils:getRequestBody(),cIdNegocio, cIdInsumo)
	
	oRateioNegociacaoService:registrar(oJPayload)

	oAGDRestUtils:setAPIResponse(oRest, oRateioNegociacaoService)

	FreeObj(oRateioNegociacaoService)
	FreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} agdRateioNegociacaoApi::atualizar
Atualiza a quantidade do Rateio
@type method
@version P12
@author scheronlini.martins
@since 02/07/2025
@return logical, true
/*/
method atualizar() as logical class agdRateioNegociacaoApi
	local oRateioNegociacaoService as object
	local oAGDRestUtils            as object
    Local jPathParams              As Json
    Local cIdNegocio               As Character
    Local cIdInsumo                As Character
    Local oJPayload                As Object

	oAGDRestUtils := agdRestUtils():New()

	oRateioNegociacaoService := agdRateioNegociacaoService():New()

    // Path params
    jPathParams  := oRest:getPathParamsRequest()
    cIdNegocio   := jPathParams["idNegocio"]
    cIdInsumo    := jPathParams["idInsumo"]


	oJPayload := oRateioNegociacaoService:convertEntregasPayload(oAGDRestUtils:getRequestBody(),cIdNegocio ,cIdInsumo)

	oRateioNegociacaoService:updateRateioEntregas(oJPayload)

	oAGDRestUtils:setAPIResponse(oRest, oRateioNegociacaoService)

	FreeObj(oRateioNegociacaoService)
	FreeObj(oAGDRestUtils)
return .t.

/*/{Protheus.doc} agdRateioNegociacaoApi::deletar
Deleta o Rateio
@type method
@version P12
@author scheronlini.martins
@since 02/07/2025
@return logical, true
/*/
method deletar() as logical class agdRateioNegociacaoApi
	Local oRateioNegociacaoService As Object
    Local oAGDRestUtils            As Object
    Local jPathParams              As Json
    Local cIdNegocio               As Character
    Local cIdInsumo                As Character
    Local oJPayload                As Object

    oAGDRestUtils := agdRestUtils():New()
    oRateioNegociacaoService := agdRateioNegociacaoService():New()

    // Path params
    jPathParams  := oRest:getPathParamsRequest()
    cIdNegocio   := jPathParams["idNegocio"]
    cIdInsumo    := jPathParams["idInsumo"]

	oJPayload := oRateioNegociacaoService:convertEntregasPayload(,cIdNegocio ,cIdInsumo)

    // Chamada do service já com payload correto
    oRateioNegociacaoService:deleteRateioEntregas(oJPayload)

    oAGDRestUtils:setAPIResponse(oRest, oRateioNegociacaoService)

    FreeObj(oRateioNegociacaoService)
    FreeObj(oAGDRestUtils)

RETURN .T.
