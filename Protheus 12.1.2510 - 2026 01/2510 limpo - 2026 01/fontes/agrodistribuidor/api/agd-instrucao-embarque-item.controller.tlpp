#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#DEFINE PATH_URL '/api/agd/v1/instrucoes-embarque-itens'
#DEFINE PATH_URL_ID '/api/agd/v1/instrucoes-embarque-itens/:codigo'

namespace agd.instrucaoEmbarqueItemApi
using namespace agd.restUtils
using namespace agd.instrucaoEmbarqueItemService

/*/{Protheus.doc} agdInstrucaoEmbarqueItemApi
API REST para listagem dos itens da instrução de embarque (tabela NED).
@type class
@version 12
@author rodrigo.nsoledade
@since 14/05/2025
/*/
class agdInstrucaoEmbarqueItemApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method New() constructor

	@Get(PATH_URL)
	public method listarItens() as logical

	@Get(PATH_URL_ID)
	public method listarItensById() as logical
endclass

/*/{Protheus.doc} New
Construtor da classe controller
@type method
@version 12
@author rodrigo.nsoledade
@since 14/05/2025
@return Self
/*/
method New() class agdInstrucaoEmbarqueItemApi
return Self


/*/{Protheus.doc} listarItens
Método para listar os itens da instrução de embarque
@type method
@version 12
@author rodrigo.nsoledade
@since 14/05/2025
@return logical, sempre retorna .T.
/*/
method listarItens() as logical class agdInstrucaoEmbarqueItemApi
	Local oService     := agdInstrucaoEmbarqueItemService():New()
	Local oRestService := agdRestUtils():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oService:listar(Self:nPage, Self:nPageSize, oRestService:getQueryParams())
	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.


/*/{Protheus.doc} listarItens
Método para listar os itens da instrução de embarque por ID
@type method
@version 12
@author lindembergson.pacheco
@since 08/09/2025
@return logical, sempre retorna .T.
/*/
method listarItensById() as logical class agdInstrucaoEmbarqueItemApi
	Local oService     := agdInstrucaoEmbarqueItemService():New()
	Local oRestService := agdRestUtils():New()

    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("codigo")
        cCodigo := oJsonPath["codigo"]
    EndIf

    oService:buscarPorId(cCodigo)
    
    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.
