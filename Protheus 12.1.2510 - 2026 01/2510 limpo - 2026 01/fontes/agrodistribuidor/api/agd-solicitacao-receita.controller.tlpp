#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'

#DEFINE PATH_URL '/api/agd/v1/solicitacao-receitas'
#DEFINE PATH_URL_ENVIAR '/api/agd/v1/solicitacao-receitas/:codigo/enviar'
#DEFINE PATH_URL_VINCULAR_RECEITA '/api/agd/v1/solicitacao-receitas/:id/vincular-receita'

namespace agd.solicitacaoReceitaApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.solicitacaoReceitaService

/*/{Protheus.doc} agdSolicitacaoReceitaApi
Controller Receituarios
@version 12
@author carlos.augusto
@since 30/10/2025
/*/
class agdSolicitacaoReceitaApi
  public Data nPage     As Integer
  public Data nPageSize As Integer

  public method new()

  @Get(PATH_URL)
  public method listar() as logical

  @Post(PATH_URL_ENVIAR)
  public method enviarSolicitacao() as logical

  @Put(PATH_URL_VINCULAR_RECEITA)
  public method vincularReceita() as logical

  private Method getPathParamSolicitacaoPendente() as array


endClass

/*/{Protheus.doc} new
Constructor
@version 12
@author carlos.augusto
@since 30/10/2025
@return variant, instância
/*/
method new() class agdSolicitacaoReceitaApi
return self

/*/{Protheus.doc} listar
Recurso REST para listar receituario
@version 12
@author carlos.augusto
@since 30/10/2025
@return logical, true
/*/
method listar() as logical class agdSolicitacaoReceitaApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oService := agdSolicitacaoReceitaService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} enviarSolicitacao
Recurso REST para enviar uma solicitação de receita
@type method
@version 12
@author beatriz.dantas
@since 10/12/2025
@return logical, .T. sempre
/*/
method enviarSolicitacao() as logical class agdSolicitacaoReceitaApi
    Local oService     As Object
    Local oRestService As Object
    Local cCodigo      As Character
    Local jPathParams  As Json

    oRestService := agdRestUtils():New()
    oService     := agdSolicitacaoReceitaService():New()

    jPathParams := oRest:getPathParamsRequest()
    
    If jPathParams != Nil .And. jPathParams:HasProperty('codigo')
        cCodigo := jPathParams['codigo']
    EndIf

    oService:confirmarSolicitacao(cCodigo)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.


/*/{Protheus.doc} vincularReceita
Recebe código da solicitação e a receita para vincular
@type method
@version 12
@author carlos.augusto
@since 23/12/2025
@return logical, .T. 
/*/
method vincularReceita() as logical class agdSolicitacaoReceitaApi
	Local oRestService   As Object
	Local oService       As Object
	Local oJsonQryParam  As Json
	Local cIdSolicitacao As Character

	oRestService  := agdRestUtils():New()
	oJsonQryParam := oRestService:getQueryParams()
	cIdSolicitacao := oRestService:getIDWithPathParams(Self:getPathParamSolicitacaoPendente())

  //Recebe o id da solicitação PENDENTE
	If !Empty(cIdSolicitacao)
		cIdSolicitacao := FwXFilial("NET") + cIdSolicitacao
		oService := agdSolicitacaoReceitaService():New()
		oService:vincularReceita(cIdSolicitacao, oRestService:getRequestBody())
		oRestService:setAPIResponse(oRest, oService)
	EndIf

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(oJsonQryParam)
return .T.


/*/{Protheus.doc} getPathParamSolicitacaoPendente
Chave da solicitação
@type method
@version 12
@author carlos.augusto
@since 23/12/2025
@return logical, .T. 
/*/
Method getPathParamSolicitacaoPendente() as array class agdSolicitacaoReceitaApi
	Local aArrayParams := {}
	AAdd(aArrayParams, {'id' ,'NET_CODIGO'})
return aArrayParams
