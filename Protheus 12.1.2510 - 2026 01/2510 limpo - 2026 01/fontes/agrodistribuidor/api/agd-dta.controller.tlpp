#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'

#DEFINE PATH_URL_TOKEN '/api/agd/v1/DTA/key-active'
#DEFINE PATH_URL_START '/api/agd/v1/DTA/welcome'
#DEFINE PATH_URL_PROMPT '/api/agd/v1/DTA/completion'

namespace agd.dtaApi  /*nameSpace - para ser importado em shortcurt*/

using namespace agd.dtaTokenService /*shortCurt de funcao*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.repositoryUtils

/*/{Protheus.doc} agdDTAApi
Controller de Token DTA
@type class
@version 12
@author claudineia.reinert
@since 18/11/2025
/*/
class agdDTAApi
	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Get(PATH_URL_TOKEN)
	public method existeToken() as logical

	@Post(PATH_URL_TOKEN)
	public method registrarToken() as logical

	@Get(PATH_URL_START)
	public method iniciarConvera() as logical
	
	@POST(PATH_URL_PROMPT)
	public method promptConversa() as logical

	
endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author claudineia.reinert
@since 18/11/2025
@return variant, inst ncia
/*/
method new() class agdDTAApi
return self

/*/{Protheus.doc} existeToken
Verifica se existe token registrado no sistema
@type method
@version P12
@author claudineia.reinert
@since 18/11/2025
@return logical, .T. se sucesso, .F. se falha
/*/
method existeToken() as logical class agdDTAApi
	Local oService     := agdDTATokenService():New()
	Local oRestService := agdRestUtils():New()

	// Chama o serviço para verificar se existe token
	oService:existeToken()
	
	// Define a resposta da API baseada no resultado
	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} registrarToken
Registra o token no sistema
@type method
@version P12
@author claudineia.reinert
@since 18/11/2025
@return logical, .T. se sucesso, .F. se falha
/*/
method registrarToken() as logical class agdDTAApi
	Local oService     := agdDTATokenService():New()
	Local oRestService := agdRestUtils():New()
	Local jRequest 	   := oRestService:getRequestBody()

	If ValType(jRequest) == "J"
		jRequest := oRestService:getRequestBody()
		oService:registrarToken(jRequest)
		oRestService:setAPIResponse(oRest, oService)
	EndIf

	FreeObj(oService)
	FreeObj(oRestService)
	FreeObj(jRequest)
return .T.

/*/{Protheus.doc} iniciarConvera
Retorna os dados para abertura do DTA
@type method
@version 12
@author jean.schulze
@since 02/12/2025
@return logical, true
/*/
method iniciarConvera() as logical class agdDTAApi
	Local oService     := agdDTAChatService():New(oRest:getHeaderRequest())
	Local oRestService := agdRestUtils():New()
	Local aHeader      := {{"x-dta-session", oService:getSessionID()}, {"x-dta-thread", oService:getThreadID()}}

	oService:getStartChat()

	oRestService:setHeadersResponse(oRest, aHeader)
	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)

return .T.

/*/{Protheus.doc} promptConversa
Executa os prompts da Conversa
@type method
@version 12
@author jean.schulze
@since 02/12/2025
@return logical, true
/*/
method promptConversa() as logical class agdDTAApi
	Local oService     := agdDTAChatService():New(oRest:getHeaderRequest())
	Local oRestService := agdRestUtils():New()
	Local aHeader      := {{"x-dta-session", oService:getSessionID()}, {"x-dta-thread", oService:getThreadID()}}

	oService:postPromptChat(oRestService:getRequestBody())

	oRestService:setHeadersResponse(oRest, aHeader)
	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)

return .T.
