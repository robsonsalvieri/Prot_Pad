#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'

#DEFINE PATH_URL '/api/agd/v1/receituario'
#DEFINE PATH_URL_DISPONIVEIS '/api/agd/v1/receituario/disponiveis'
#DEFINE PATH_URL_ID_ITENS '/api/agd/v1/receituario/:id/itens'
#DEFINE PATH_URL_SOLICITACAO '/api/agd/v1/receituario/solicitacao'
#DEFINE PATH_URL_CANCELAR '/api/agd/v1/receituario/:idExterno/cancelar'

namespace agd.receituarioApi  /*nameSpace - para ser importado em shortcurt*/
using namespace agd.restUtils /*shortCurt de funcao*/
using namespace agd.receituarioService
using namespace agd.receituarioItensService

/*/{Protheus.doc} agdReceituarioApi
Controller de Receitas Agronômicas Geradas
@type class
@version 12
@author jean.schulze
@since 26/11/2025
/*/
class agdReceituarioApi
  public Data nPage     As Integer
  public Data nPageSize As Integer

  public method new()

  @Get(PATH_URL)
  public method listar() as logical

  @Get(PATH_URL_DISPONIVEIS)
  public method getReceitasDisponiveis() as logical

  @Get(PATH_URL_ID_ITENS)
  public method getIDReceitaItens() as logical

  @Post(PATH_URL_SOLICITACAO)
  public method criarReceitaPorSolicitacao() as logical

  @Put(PATH_URL_CANCELAR)
  public method cancelarReceita() as logical

endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 26/11/2025
@return variant, self
/*/
method new() class agdReceituarioApi
return self

/*/{Protheus.doc} listar
Listagem de Receitas Agronômicas
@type method
@version 12
@author jean.schulze
@since 26/11/2025
@return logical, nil
/*/
method listar() as logical class agdReceituarioApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oService := agdReceituarioService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oService:listar(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} getReceitasDisponiveis
Busca os registros de receituarios disponíveis para reaproveitamento
@type method
@version P12 
@author claudineia.reinert
@since 27/11/2025
/*/
method getReceitasDisponiveis() class agdReceituarioApi
    Local oService As Object
    Local oRestService As Object
    Local oJsonQryParam := Nil

    oRestService := agdRestUtils():New()
    oService := agdReceituarioService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
     
    oService:listarReceitasDisponiveis(Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oService)

    FreeObj(oService)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} getItensIDReceita
API rest para listar os itens da receita
@type method
@version  P12
@author claudineia.reinert
@since 27/11/2025
/*/
method getIDReceitaItens() class agdReceituarioApi
    Local oRestService  As Object
    Local oServiceItens As Object
    Local oJsonQryParam As Object
    Local oJsonPath     As Object
    Local cId           As Character

    oRestService := agdRestUtils():New()
    oServiceItens := agdReceituarioItensService():New()

    oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
    oJsonQryParam := oRestService:getQueryParams()
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("id")
      cId := oJsonPath["id"]
    EndIf
  
    oServiceItens:listarItensPorIdReceita(cId, Self:nPage, Self:nPageSize, oJsonQryParam)

    oRestService:setAPIResponse(oRest, oServiceItens)

    FreeObj(oServiceItens)
    FreeObj(oRestService)
return .T.

/*/{Protheus.doc} criarReceitaPorSolicitacao
API Rest para gerar Receita Agronômica a ser vinculada a Solicitação de Receita
@type method
@version P12
@author Gilson.Venturi
@since 18/11/2025
@return logical, return_true
/*/
method criarReceitaPorSolicitacao() as logical class agdReceituarioApi
    local oAGDRestUtils as object
    local oService      as object
    Local jBody         as json

    oAGDRestUtils := agdRestUtils():New()

    jBody      := oAGDRestUtils:getRequestBody()
    oService   := agdReceituarioService():New()

    oService:createReceitaBySolitacao(jBody)
    oAGDRestUtils:setAPIResponse( oRest, oService)

    FWFreeObj(oAGDRestUtils)
return .T.

/*/{Protheus.doc} cancelarReceita
Recurso Rest para cancelar uma receita agronômica
@type method
@version 12
@author Gilson.Venturi
@since 05/12/2025
@return logical, true
/*/
method cancelarReceita() as logical class agdReceituarioApi
    Local oService     as Object
    Local oRestService as Object
    Local oJsonPath    as Json

    oRestService := agdRestUtils():New()
    oJsonPath := oRest:getPathParamsRequest()

    If ValType(oJsonPath) == "J" .And. oJsonPath:hasProperty("idExterno")

        oService := agdReceituarioService():New()
        oService:cancelarReceita(oJsonPath["idExterno"])

        oRestService:setAPIResponse(oRest, oService)
    EndIf

    FreeObj(oService)
    FreeObj(oRestService)
    FreeObj(oJsonPath)

return .T.
