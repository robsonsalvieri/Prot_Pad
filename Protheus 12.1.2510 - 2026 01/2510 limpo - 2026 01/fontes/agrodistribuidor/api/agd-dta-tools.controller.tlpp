#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'TOTVS.CH'

#define PATH_URL_TOOLS '/api/agd/v1/DTA/tools/'
#define PATH_URL_TOOLS_DISPONIVEIS '/api/agd/v1/DTA/tools/disponiveis'
#define PATH_URL_TOOLS_ID '/api/agd/v1/DTA/tools/:codigo'
#define PATH_URL_TOOLS_ATIVAR '/api/agd/v1/DTA/tools/:codigo/ativar'
#define PATH_URL_TOOLS_INATIVAR '/api/agd/v1/DTA/tools/:codigo/inativar'
#define PATH_URL_TOOLS_PERMISSAO '/api/agd/v1/DTA/tools/:codigo/permissao'


namespace agd.DTAToolsAPI
using namespace agd.restUtils
using namespace agd.toolsDTAservice


/*/{Protheus.doc} agdDTAToolsAPI
Controller TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdDTAToolsAPI

	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	@Get(PATH_URL_TOOLS)
	public method listar() as logical

	@Get(PATH_URL_TOOLS_DISPONIVEIS)
	public method listarTools() as logical

	@Get(PATH_URL_TOOLS_ID)
	public method getById() as logical
	
	@Post(PATH_URL_TOOLS)
	public method habilitar() as logical

	@Put(PATH_URL_TOOLS_ATIVAR)
	public method ativar() as logical

	@Put(PATH_URL_TOOLS_INATIVAR)
	public method inativar() as logical

	@Get(PATH_URL_TOOLS_PERMISSAO)
	public method listarPermissao() as logical

	@Post(PATH_URL_TOOLS_PERMISSAO)
	public method informarPermissao() as logical

	private method getPathParamFeature() as array
endClass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method new() class agdDTAToolsAPI
return self

/*/{Protheus.doc} listar
Recurso REST para listar as Features Selecionadas do Módulo
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listar() as logical class agdDTAToolsAPI
	Local oService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil

	oRestService := agdRestUtils():New()
	oService := agdToolsDTAservice():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oService:getTools(Self:nPage, Self:nPageSize, oJsonQryParam)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} listarTools
Retorna as Tools Disponíveis no DTA
@type method
@version 12
@author jean.schulze
@since 12/11/2025
@return logical, true
/*/
method listarTools() as logical class agdDTAToolsAPI
	Local oService As Object
	Local oRestService As Object

	oRestService := agdRestUtils():New()
	oService := agdToolsDTAservice():New()

	oService:getDisponiveis()

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} getById
Retorna feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method getById() As logical Class agdDTAToolsAPI
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdToolsDTAservice():New()
		oTCOservice:getById(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} habilitar
Registra uma Feature
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return logical, retorno
/*/
method habilitar() as logical class agdDTAToolsAPI
	Local oTCOservice As Object
	Local oRestService      As Object
	Local jCmdRegistrar     as Json

	oRestService := agdRestUtils():New()
	oTCOservice := agdToolsDTAservice():New()

	jCmdRegistrar := oRestService:getRequestBody()

	oTCOservice:habilitar(jCmdRegistrar)

	oRestService:setAPIResponse(oRest, oTCOservice)

	FreeObj(oTCOservice)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.


/*/{Protheus.doc} inativar
Inativar feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method inativar() As logical Class agdDTAToolsAPI
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdToolsDTAservice():New()
		oTCOservice:inativar(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} ativar
Ativar Feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@return logical, retorno
/*/
method ativar() As logical Class agdDTAToolsAPI
	Local oTCOservice           as Object
	Local oRestService          as Object
	Local cFeature              as character

	oRestService := agdRestUtils():New()

	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		oTCOservice := agdToolsDTAservice():New()
		oTCOservice:ativar(cFeature)
		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
Return .T.

/*/{Protheus.doc} listarPermissao
Recurso REST para listar permissões das tools
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@return logical, true
/*/
method listarPermissao() as logical class agdDTAToolsAPI
	Local oService As Object
	Local oRestService As Object
	Local oJsonQryParam := Nil

	oRestService := agdRestUtils():New()
	oService := agdToolsDTAservice():New()

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)
	oJsonQryParam := oRestService:getQueryParams()

	oService:getPermissao(Self:nPage, Self:nPageSize, oJsonQryParam)

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.

/*/{Protheus.doc} informarPermissao
Registra uma Feature
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return logical, retorno
/*/
method informarPermissao() as logical class agdDTAToolsAPI
	Local oTCOservice As Object
	Local oRestService      As Object
	Local jCmdRegistrar     as Json

	oRestService := agdRestUtils():New()
	oTCOservice := agdToolsDTAservice():New()
	cFeature := oRestService:getIDWithPathParams(Self:getPathParamFeature())

	if !Empty(cFeature)
		jCmdRegistrar := oRestService:getRequestBody()
		jCmdRegistrar['codigo'] = cFeature
		oTCOservice:informarPermissao(jCmdRegistrar)

		oRestService:setAPIResponse(oRest, oTCOservice)
	endif

	FreeObj(oTCOservice)
	FreeObj(oRestService)
	FreeObj(jCmdRegistrar)
return .T.

/*/{Protheus.doc} getPathParamFeature
Retorna a chave de busca da rota da feature
@type method
@version 12
@author jean.schulze
@since 30/05/2025
@return array, lista de campos
/*/
Method getPathParamFeature() as array class agdDTAToolsAPI
	Local aArraParams := {}

	AAdd(aArraParams, {'codigo' ,'NE3_TOOL'})
return aArraParams
