#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"

// ROTAS (seguir padrão do projeto)
#DEFINE PATH_URL_MONITOR         '/api/agd/v1/tco/integracoes/monitor/:codigoFeature/'

namespace agd.TCOIntegracoesApi
using namespace agd.restUtils
using namespace agd.integracoesMonitorService

/*/{Protheus.doc} agdTCOIntegracaoApi
API REST para Monitor de Integração das Features (AgroDistribuidor).
Descrição:
- Lista as requisições do schedule (tabela XX3) filtradas pelo código da feature (XX3_PROUTE).
@type class
@version 12
@author rodrigo.nsoledade
@since 24/10/2025
/*/
class TCOIntegracoesApi 
    public Data nPage     As Integer
    public Data nPageSize As Integer

    public method New() constructor

    // Endpoints REST
    @Get(PATH_URL_MONITOR)
    public method listar() as logical

endclass

method New() class TCOIntegracoesApi
return Self

/*/{Protheus.doc} TCOIntegracoesApi::listar
Lista as requisições do Monitor de Integração (tabela XX3) filtradas por :codigo (XX3_PROUTE),
@type method
@version  P12
@author rodrigo.nsoledade
@since 24/10/2025
@return logical, .T. em caso de processamento
/*/
method listar() as logical class TCOIntegracoesApi
	Local oService As Object
	Local oRestService As Object
    local jPathParams   as json
    local cCodFeature   as Character

	oRestService := agdRestUtils():New()
	oService := agdIntegracoesMonitorService():New()

    jPathParams   := oRest:getPathParamsRequest()
    cCodFeature   := jPathParams['codigoFeature']

	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	oService:listar(cCodFeature, Self:nPage, Self:nPageSize, oRestService:getQueryParams())

	oRestService:setAPIResponse(oRest, oService)

	FreeObj(oService)
	FreeObj(oRestService)
return .T.
