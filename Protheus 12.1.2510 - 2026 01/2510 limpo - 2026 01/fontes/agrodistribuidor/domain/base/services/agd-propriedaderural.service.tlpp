#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.propriedadeRuralService

using namespace agd.utilsService
using namespace tlpp.regex

/*/{Protheus.doc} agdPropriedadeRuralService
Serviço de Propriedade Rural (NEP)
@author rodrigo.nsoledade
@since 24/09/2025
/*/
class agdPropriedadeRuralService FROM agdUtilsService

    public method new()
    public method validaGeoLocal(cValor as character, cTipo as character) as logical
    public method validaUfSc(cCliente as character, cLoja as character) as logical

end class

/*/{Protheus.doc} New
    Construtor padrão.
/*/
method New() class agdPropriedadeRuralService
    _Super:new()
return Self

/*/{Protheus.doc} validaGeoLocal
Valida coordenadas em graus decimais (ex.: -12.1234).
- cValor : string digitada
- cTipo  : "LAT" para latitude (-90..90) | "LON" para longitude (-180..180)
Obs.: Latitude limitada pelos polos; Longitude dá a volta no globo.
@author rodrigo.nsoledade
@since 23/09/2025
@return .T. se válido (formato e faixa), .F. caso contrário
/*/
method validaGeoLocal(cValor as character, cTipo as character) as logical class agdPropriedadeRuralService
    Local cValorRegex      as character
    Local cTipoCoordenada  as character
    Local nValorNumerico   as numeric
    Local lRet :=.T.       as logical
    Local oRegexFormat

    // Normaliza valor (remove espaços, vírgula -> ponto, maiúsculo)
    cValorRegex := StrTran(AllTrim(Upper(cValor)), ",", ".")
    cTipoCoordenada := IIf(Empty(cTipo), "LAT", Upper(AllTrim(cTipo)))

    // Regex: sinal opcional + até 3 dígitos + ponto + decimais
    oRegexFormat := Regex():New("^-?\d{1,3}\.\d+$")
    If !oRegexFormat:fullMatch(cValorRegex)
        return .F.
    EndIf

    // Latitude (-90..90) -> limitado pelos polos
    // Longitude (-180..180) -> dá a volta no globo
    nValorNumerico := Val(cValorRegex)

    If cTipoCoordenada == "LAT"
        If !(nValorNumerico >= -90 .and. nValorNumerico <= 90)
            lRet := .F.
        EndIf
    ElseIf cTipoCoordenada == "LON"
        If !(nValorNumerico >= -180 .and. nValorNumerico <= 180)
            lRet := .F.
        EndIf
    EndIf

return lRet

/*/{Protheus.doc} validaUfSc
Valida se a UF é SC para tornar campo altitude obrigatório.
cCliente : código do cliente
cLoja    : código da loja
@return .T. se a UF for SC (campo altitude obrigatório), .F. caso contrário
@author felipe.savelar
@since 23/12/2025
@return .T. se obrigatório, .F. caso contrário
/*/
method validaUfSc(cCliente as character, cLoja as character) as logical class agdPropriedadeRuralService
	Local cUF := "" as character
	Local lObrigatorio := .F. as logical

	dbSelectArea("SA1")
	SA1->(dbSetOrder(1))
	If SA1->(dbSeek(FWxFilial("SA1") + cCliente + cLoja))
		cUF := SA1->A1_EST
		If cUF == 'SC'
			lObrigatorio := .T.
		EndIf
	EndIf

return lObrigatorio
