#include "totvs.ch"
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'
#include 'agd.tabelapreco.service.ch'

namespace agd.TabelaPrecoService

using namespace agd.utilsService
using namespace agd.tabelaPrecoRepository

/*/{Protheus.doc} agdTabelaPrecoService
Classe de servico para gerar tabela de preço atraves do Modulo AgroDistribuidor. (Tabelas DA0/DA1 - Rotina OMSA010)
@type class
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
class agdTabelaPrecoService FROM agdUtilsService
	private data cIdTabelaPreco                as character
	private data cIdProgramaParidade           as character
	private data oModelOMSA010                 as object

	public method new()
	public method createFromProgramaParidade() as logical
	public method getIdTabelaPreco()           as character
	public method getProdutosPorTabelaPreco()
	public method getProdutoPorTabela()

	private method isValidProgramaParidade()   as logical
	private method setModelOMSA010()           
	private method loadModelDA0MASTER()
	private method loadModelDA1DETAIL()
	private method commitModelOMSA010()        as logical
	private method getErrorExecAuto()          as character


endClass

/*/{Protheus.doc} agdTabelaPrecoService::new
Construtor
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
method new() class agdTabelaPrecoService
	_Super:new()
return self

/*/{Protheus.doc} agdTabelaPrecoService::createFromProgramaParidade
Cria a tabela de preço a partir do programa de paridade.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
@param cIdProgramaParidade, character, codigo programa paridade
@return logical, resultado da operacao
/*/
method createFromProgramaParidade(cIdProgramaParidade as character) as logical class agdTabelaPrecoService
	local aArea 		   := fwGetArea() as array
	local cModuloAnterior  := cModulo     as character
	local nModuloAnterior  := nModulo     as numeric

	::cIdProgramaParidade := cIdProgramaParidade
	::cIdTabelaPreco	  := ''

	if ::isValidProgramaParidade()
		cModulo := 'FAT' //Executa a rotina pelo modulo de Faturamento.
		nModulo := 5

		::setModelOMSA010()

		if ::oModelOMSA010:VldData()
			::commitModelOMSA010()
		else
			::setError(::oModelOMSA010:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		endIf

		cModulo := cModuloAnterior
		nModulo := nModuloAnterior
	
		::oModelOMSA010:DeActivate()
		::oModelOMSA010:Destroy()
		::oModelOMSA010 := Nil
	else
		::setError(STR0001) //"Programa de paridade invalido."
	endif

	fwRestArea(aArea)
return ::isSuccess()

/*/{Protheus.doc} agdTabelaPrecoService::isValidProgramaParidade
Avalia se o programa de paridade definido é válido.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
@return logical, resultado da validação.
/*/
method isValidProgramaParidade() as logical class agdTabelaPrecoService
	local lIsValid as logical

	dbSelectArea('NEG')
	NEG->(dbSetOrder(retOrder('NEG_FILIAL+NEG_CODIGO') ))

	dbSelectArea('NEH')
	NEH->(dbSetOrder(retOrder('NEH_FILIAL+NEH_CODIGO+NEH_CODPRO')))

	lIsValid :=  NEG->(DBSeek(FWxFilial('NEG') + ::cIdProgramaParidade));
		.And. Empty(NEG->(NEG_TABPRP));
		.And. NEH->(DBSeek(FWxFilial('NEH') + ::cIdProgramaParidade))
return lIsValid

/*/{Protheus.doc} agdTabelaPrecoService::setModelOMSA010
Prepara o modelo de dados da tabela de preço.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
method setModelOMSA010() class agdTabelaPrecoService
	::oModelOMSA010 := FWLoadModel('OMSA010')
	::oModelOMSA010:SetOperation(MODEL_OPERATION_INSERT)
	::oModelOMSA010:Activate()

	::cIdTabelaPreco := ::oModelOMSA010:GetValue('DA0MASTER','DA0_CODTAB')

	::loadModelDA0MASTER()

	::loadModelDA1DETAIL()
return 

/*/{Protheus.doc} agdTabelaPrecoService::loadModelDA0MASTER
Carrega os dados do modelo DA0MASTER.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
method loadModelDA0MASTER() class agdTabelaPrecoService
	local oModelDA0	:= ::oModelOMSA010:GetModel('DA0MASTER') as object

	oModelDA0:SetValue('DA0_CODTAB', ::cIdTabelaPreco                  )
	oModelDA0:SetValue('DA0_DESCRI', AvKey(NEG->NEG_DESC, 'DA0_DESCRI'))
	oModelDA0:SetValue('DA0_DATDE' , NEG->NEG_DATA                     )
	oModelDA0:SetValue('DA0_HORADE', AvKey(TIME(), 'DA0_HORADE')       )
	oModelDA0:SetValue('DA0_DATATE', NEG->NEG_VENCTO                   )
	oModelDA0:SetValue('DA0_HORATE', '23:59'                           )
	oModelDA0:SetValue('DA0_TPHORA', '1'                               ) // '1'=Unico
	oModelDA0:SetValue('DA0_ATIVO' , '1'                               ) // '1'=Ativo
return

/*/{Protheus.doc} agdTabelaPrecoService::loadModelDA1DETAIL
Carrega os dados do modelo DA1DETAIL.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
method loadModelDA1DETAIL() class agdTabelaPrecoService
	local oModelGridDA1 := ::oModelOMSA010:GetModel('DA1DETAIL') as object
	local cFilialDA1    := fwXfilial('DA1')	                     as character
	local cEstado       := Space(2)                              as character
	local nQtdLot       := CriaVar('DA1_QTDLOT')                 as numeric

	while ! NEH->(Eof()) .and. NEH->NEH_CODIGO == ::cIdProgramaParidade
		oModelGridDA1:AddLine()

		oModelGridDA1:LoadValue('DA1_FILIAL', cFilialDA1     )
		oModelGridDA1:LoadValue('DA1_CODPRO', NEH->NEH_CODPRO)
		oModelGridDA1:LoadValue('DA1_PRCVEN', NEH->NEH_PRC   )
		oModelGridDA1:LoadValue('DA1_VLRDES', 0              )
		oModelGridDA1:LoadValue('DA1_PERDES', 0              )
		oModelGridDA1:LoadValue('DA1_ATIVO' , '1'            ) // '1'=Sim
		oModelGridDA1:LoadValue('DA1_ESTADO', cEstado        )
		oModelGridDA1:LoadValue('DA1_TPOPER', '4'            ) // '4'=Todos
		oModelGridDA1:LoadValue('DA1_QTDLOT', nQtdLot        )
		oModelGridDA1:LoadValue('DA1_MOEDA' , NEG->NEG_MOEDRT)
		oModelGridDA1:LoadValue('DA1_DATVIG', NEG->NEG_DATA  )

		NEH->(dbSkip())
	endDo
return

/*/{Protheus.doc} agdTabelaPrecoService::commitModelOMSA010
Realiza o commit do modelo de dados.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
/*/
method commitModelOMSA010() class agdTabelaPrecoService
	if ::oModelOMSA010:CommitData()
		::setSuccess(::cIdTabelaPreco)
	endif
return

/*/{Protheus.doc} agdTabelaPrecoService::getIdTabelaPreco
Retorna o codigo da tabela de preço.
@type method
@version 12
@author jc.maldonado
@since 05/02/2025
@return character, código da tabela de preço
/*/
method getIdTabelaPreco() as character class agdTabelaPrecoService
return ::cIdTabelaPreco

/*/{Protheus.doc} agdTabelaPrecoService::getProdutosPorTabelaPreco
retorna os produtos por tabela de preço
@type method
@version p12
@author scheronlini.martins
@since 06/08/2025
@param cTabela, character, codigo da tabela
@param oJsonParams, object, json com parametros
/*/
method getProdutosPorTabelaPreco(cTabela, oJsonParams, cMoeda, nPage, nPageSize) class agdTabelaPrecoService
	local oRepo       as object

	oRepo := agdtabelaPrecoRepository():New()
	// Filtros obrigatórios
	oJsonParams["codigoTabela"] := cTabela
	oJsonParams["order"]        := "descricao"
	oJsonParams["moeda"]      := cMoeda
	oJsonParams["ativo"]      := "1" // Somente ativos

	oRepo:find(nPage, nPageSize, oJsonParams)

	self:setFullResponse(oRepo)

	oRepo:DeActivate()
	FreeObj(oRepo)

return NIL

/*/{Protheus.doc} agdTabelaPrecoService::getProdutoPorTabela
retorna o produto por tabela de preço
@type method
@version p12 
@author scheronlini.martins
@since 06/08/2025
@param cTabela, character, codigo da tabela
@param cProduto, character, codigo do produto
/*/
method getProdutoPorTabela(cTabela as character, cProduto as character, cMoeda as character) class agdTabelaPrecoService
	local oRepo
	local aFieldsId

	oRepo := agdtabelaPrecoRepository():New()
	aFieldsId := {;
		{"DA0_CODTAB", cTabela},;
		{"DA1_CODPRO", cProduto},;
		{"DA1_MOEDA", cMoeda};
		}

	oRepo:findById(aFieldsId)

	self:setFullResponse(oRepo)

	oRepo:DeActivate()
	FreeObj(oRepo)

return NIL
