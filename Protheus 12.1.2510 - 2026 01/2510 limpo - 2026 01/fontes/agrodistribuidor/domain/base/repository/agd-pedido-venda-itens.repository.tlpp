#include 'tlpp-core.th'
#include 'totvs.ch'

namespace agd.pedidoVendaItensRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdPedidoVendaItensRepository
Repositório de itens de pedido de venda
@type class
@version 12
@author jc.maldonado
@since 15/01/2026
/*/
class agdPedidoVendaItensRepository from agdRepositoryUtils
	private data lIncludeOtherFields as logical

	public method new()

	private method getFields() as array
	private method getJoins()  as array
endclass

/*/{Protheus.doc} agdPedidoVendaItensRepository::new
Construtor
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@param lIncludeOtherFields, logical, mostra campos adicionais
@return object, Instância da classe
/*/
method new(lIncludeOtherFields as logical) class agdPedidoVendaItensRepository
	default lIncludeOtherFields := .f.

	::lIncludeOtherFields := lIncludeOtherFields

	_Super:New('SC6', self:getFields(), self:getJoins())
return Self

/*/{Protheus.doc} agdPedidoVendaItensRepository::getFields
Obtem os campos do repositório
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@return array, {{cCampoJson, cCampoProtheus, cTipoCampo, lMostraCampoConsulta}, ...}
/*/
method getFields() as array class agdPedidoVendaItensRepository
	local aFields as array

	aFields := {;
		{'filial'             , 'C6_FILIAL' , 'C', .F.},;
		{'codigoPedido'       , 'C6_NUM'    , 'C', .F.},;
		{'item'               , 'C6_ITEM'   , 'C', .T.},;
		{'codigoProduto'      , 'C6_PRODUTO', 'C', .T.},;
		{'quantidadeVenda'    , 'C6_QTDVEN' , 'N', .T.},;
		{'precoVenda'         , 'C6_PRCVEN' , 'N', .T.},;
		{'descontoPorcentagem', 'C6_DESCONT', 'N', .T.},;
		{'descontoValor'      , 'C6_VALDESC', 'N', .T.},;
		{'valorTotal'         , 'C6_VALOR'  , 'N', .T.},;
		{'numeroDocumento'    , 'C6_NOTA'   , 'N', .T.},;
		{'serieDocumento'     , 'C6_SERIE'  , 'N', .T.};
		}

	if ::lIncludeOtherFields
		aAdd(aFields, {'descricaoProduto', 'B1_DESC', 'C', .T.})
	endIf
return aFields

/*/{Protheus.doc} agdPedidoVendaItensRepository::getJoins
Obtem os joins do repositório
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@return array, {{cTipoJoin, cTabela, cWhere}, ...}
/*/
method getJoins() as array class agdPedidoVendaItensRepository
	local aJoins as array

	aJoins := {{"INNER", "SB1", Self:getWhereDefault("SB1.B1_COD = SC6.C6_PRODUTO", "SB1")}}
return aJoins
