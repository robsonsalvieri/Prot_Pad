#include 'tlpp-core.th'
#include 'totvs.ch'

namespace agd.pedidoVendaRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdPedidoVendaRepository
Repositório de pedidos de venda
@type class
@version 12
@author jc.maldonado
@since 15/01/2026
/*/
class agdPedidoVendaRepository from agdRepositoryUtils
	private data lIncludeOtherFields as logical

	public method new()

	public method getDocumentosSaida() as array

	private method getFields()            as array
	private method getJoins()             as array
	private method getQueryStatusPedido() as character
endclass

/*/{Protheus.doc} agdPedidoVendaRepository::new
Construtor
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@param lIncludeOtherFields, logical, mostra campos adicionais
@return object, Instância da classe
/*/
method new(lIncludeOtherFields as logical) class agdPedidoVendaRepository
	local xQueryAdapt := nil as variant
	local xJoins      := nil as variant

	default lIncludeOtherFields := .f.

	if (::lIncludeOtherFields := lIncludeOtherFields)
		xQueryAdapt := ::getQueryStatusPedido()
		xJoins      := ::getJoins()
	endif

	_Super:New('SC5', ::getFields(), xJoins, xQueryAdapt)
return Self

/*/{Protheus.doc} agdPedidoVendaRepository::getFields
Obtem os campos do repositório
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@return array, {{cCampoJson, cCampoProtheus, cTipoCampo, lMostraCampoConsulta}, ...}
/*/
method getFields() as array class agdPedidoVendaRepository
	local aFields as array

	aFields := {;
		{'filial'                 , 'C5_FILIAL' , 'C', .F.},;
		{'codigo'                 , 'C5_NUM'    , 'C', .T.},;
		{'tipo'                   , 'C5_TIPO'   , 'C', .T.},;
		{'codigoCliente'          , 'C5_CLIENTE', 'C', .T.},;
		{'codigoLojaCliente'      , 'C5_LOJACLI', 'C', .T.},;
		{'dataEmissao'            , 'C5_EMISSAO', 'D', .T.},;
		{'codigoCondicaoPagamento', 'C5_CONDPAG', 'C', .T.};
		}

	if ::lIncludeOtherFields
		aAdd(aFields, {'nomeCliente'               , 'A1_NOME'  , 'C', .T.})
		aAdd(aFields, {'statusPedido'              , 'STATUSPED', 'C', .T.})
		aAdd(aFields, {'descricaoCondicaoPagamento', 'E4_DESCRI', 'C', .T.})
	endIf
return aFields

/*/{Protheus.doc} agdPedidoVendaRepository::getJoins
Obtem os joins do repositório
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@return array, {{cTipoJoin, cTabela, cWhere}, ...}
/*/
method getJoins() as array class agdPedidoVendaRepository
	local aJoins := {} as array

	aAdd(aJoins, {'INNER', 'SA1' , Self:getWhereDefault('SA1.A1_COD = SC5.C5_CLIENTE AND SA1.A1_LOJA = SC5.C5_LOJACLI', 'SA1')})
	aAdd(aJoins, {'INNER', 'SE4' , Self:getWhereDefault('SE4.E4_CODIGO = SC5.C5_CONDPAG', 'SE4')})
return aJoins

/*/{Protheus.doc} agdPedidoVendaRepository::getQueryStatusPedido
Retorna a query para obter o status do pedido de venda
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@return character, query
/*/
method getQueryStatusPedido() as character class agdPedidoVendaRepository
	local cQuery     as character
	local cSTATUSPED as character

	cSTATUSPED := " CASE "
	cSTATUSPED += "    WHEN SC5.C5_NOTA <> '' AND SF2.F2_FIMP IN ('T','S') THEN 'FATURADO' "
	cSTATUSPED += "    WHEN SC5.C5_NOTA <> '' AND SF2.F2_FIMP NOT IN ('T','S') THEN 'FATURADO_SEM_NFE' "
	cSTATUSPED += "    WHEN SC5.C5_LIBEROK <> '' AND SC5.C5_NOTA = '' AND SC5.C5_BLQ = '' THEN 'PEDIDO_LIBERADO' "
	cSTATUSPED += "    WHEN SC5.C5_LIBEROK = '' AND SC5.C5_NOTA = '' AND SC5.C5_BLQ = '' THEN 'PEDIDO_ABERTO' "
	cSTATUSPED += "    WHEN SC5.C5_BLQ = '1' THEN 'PEDIDO_BLOQUEADO_REGRA' "
	cSTATUSPED += "    WHEN SC5.C5_BLQ = '2' THEN 'PEDIDO_BLOQUEADO_VERBA' "
	cSTATUSPED += " END AS STATUSPED "

	cQuery := "SELECT SC5.*, " + cSTATUSPED + " FROM " + RetSqlName("SC5") + " SC5 "
	cQuery += " LEFT JOIN " + RetSqlName("SF2") + " SF2 ON " + Self:getWhereDefault("SF2.F2_DOC = SC5.C5_NOTA AND SF2.F2_SERIE = SC5.C5_SERIE", "SF2")
return cQuery

/*/{Protheus.doc} agdPedidoVendaRepository::getDocumentosSaida
Retorna uma lista com os documentos de saída do pedido de venda
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@param cCodigoPedido, character, código do pedido
@return array, {{'numeroDocumento': C6_NOTA, 'serieDocumento': C6_SERIE}, ...}
/*/
method getDocumentosSaida(cCodigoPedido as character) as array class agdPedidoVendaRepository
	local cQuery               as character
	local aBindParams    := {} as array
	local cNewAlias            as character
	local aSalesInvoices := {} as array

	cQuery := "SELECT DISTINCT"
	cQuery += " C6_NOTA,"
	cQuery += " C6_SERIE "
	cQuery += "FROM "
	cQuery += retSQLName("SC6")
	cQuery += "WHERE "
	cQuery += "	C6_FILIAL = ?"  ; aAdd(aBindParams, FWxFilial("SC6"))
	cQuery += "	AND C6_NUM = ?" ; aAdd(aBindParams, cCodigoPedido)
	cQuery += "	AND D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.t., "TOPCONN", TcGenQry2(,, cQuery, aBindParams), (cNewAlias := getNextAlias()), .t., .t.)

	while (cNewAlias)->(!EOF())
		aAdd(aSalesInvoices, {'numeroDocumento': (cNewAlias)->C6_NOTA, 'serieDocumento': (cNewAlias)->C6_SERIE})
		(cNewAlias)->(DBSkip())
	endDo

	(cNewAlias)->(DBCloseArea())
return aSalesInvoices
