#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'

namespace agd.tabelaPrecoRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdtabelaPrecoRepository
Classe repository da tabela de preço
@type class
@version P12
@author scheronlini.martins
@since 06/08/2025
/*/
class agdtabelaPrecoRepository from agdRepositoryUtils
	public method new()
	public method find()

	private method getFields() as array
	private method getJoins()  as array
endclass

/*/{Protheus.doc} agdLookupRepository::New
Construtor
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return object, self
/*/
method new(cFields) class agdtabelaPrecoRepository
	_Super:New('DA0', self:getFields(), self:getJoins())
return self

/*/{Protheus.doc} agdtabelaPrecoRepository::getFields
Retorna a lista de campos relacionados
@type method
@version 12
@author scheronlini.martins
@since 06/08/2025
@return array, {{cCampoJson, cCampoTabela, cTipoCampo, lJsonField|lFixed}, ...}
/*/
method getFields() as array class agdtabelaPrecoRepository
	local aFields as array

	aFields := {;
		{"codigoTabela"  , "DA0_CODTAB", "C", .T., "DA0"},;
		{"codigoProduto" , "DA1_CODPRO", "C", .T., "DA1"},;
		{"descricao"     , "B1_DESC"   , "C", .T., "SB1"},;
		{"nomeCientifico", "B5_CEME"   , "C", .T., "SB5"},;
		{"precoVenda"    , "DA1_PRCVEN", "N", .T., "DA1"},;
		{"principioAtivo", "NCM_CODREG", "C", .T., "NCM"},;
		{"unidadeMedida" , "B1_UM"     , "C", .T., "SB1"},;
		{"fabricante"    , "B1_FABRIC" , "C", .T., "SB1"},;
		{"moeda"         , "DA1_MOEDA" , "C", .T., "DA1"};
		}
return aFields

/*/{Protheus.doc} agdtabelaPrecoRepository::getJoins
Retorna os joins das consultas
@type method
@version 12
@author scheronlini.martins
@since 06/08/2025
@return array, {{cTipoJoin, cTabela, cCondicao}, ...}
/*/
method getJoins() as array class agdtabelaPrecoRepository
	local aJoins as array
	local cToday := DtoS(Date()) as character //YYYYMMDD

	aJoins := {;
		{"INNER", "DA1",;
		"DA0.DA0_CODTAB = DA1.DA1_CODTAB" + ;
		" AND DA1.DA1_FILIAL = '"  + FWxFilial("DA1") + "'" + ;
		" AND DA1.D_E_L_E_T_ = ''" + ;
		" AND DA1.DA1_ATIVO = '1'" + ;
		" AND (DA1.DA1_DATVIG = ' ' OR DA1.DA1_DATVIG <= '"+ cToday +"')";
		},;
		{"INNER", "SB1",;
		"SB1.B1_COD = DA1.DA1_CODPRO" + ;
		" AND SB1.B1_FILIAL = '" + FWxFilial("SB1") + "'" + ;
		" AND SB1.D_E_L_E_T_ = ''";
		},;
		{"LEFT", "SB5",;
		"SB5.B5_COD = SB1.B1_COD" + ;
		" AND SB5.B5_FILIAL = '" + FWxFilial("SB5") + "'" + ;
		" AND SB5.D_E_L_E_T_ = ''";
		},;
		{"LEFT", "NCM",;
		"NCM.NCM_PROD = SB1.B1_COD" + ;
		" AND NCM.NCM_FILIAL = '" + FWxFilial("NCM") + "'" + ;
		" AND NCM.D_E_L_E_T_ = ''";
		};
		}
Return aJoins

/*/{Protheus.doc} agdtabelaPrecoRepository::find
Retorna os produtos da tabela de preço informada.
Se o parametro lIncludeDefaultCurrency for verdadeiro, e a moeda especificada for diferente da padrao ('1'=REAL),
será incluida a moeda padrao na consulta.
@type method
@version 12
@author jc.maldonado
@since 29/12/2025
@param nPage, numeric, Numero da pagina
@param nPageSize, numeric, Tamanho da pagina
@param jParams, json, parametros da consulta
@param lIncludeDefaultCurrency, logical, Especifica se irá incluir os dados da moeda padrao ('1'=REAL) na consulta
/*/
Method find(nPage as numeric, nPageSize as numeric, jParams as json, lIncludeDefaultCurrency as logical) class agdtabelaPrecoRepository
	local aArea                   as array
	local cOrder                  as character
	local aFields                 as array
	local nX                      as numeric
	local cMoeda                  as character
	local cWhereParam             as character
	local jResponse               as object
	local cDefaultCurrency := '1' as character //'1'=REAL

	default nPage                   := 1
	default nPageSize               := 10
	default lIncludeDefaultCurrency := .t.

	if self:lOk
		aArea  := FwGetArea()
		cOrder := self:getOrderQueryParam(jParams, self:aFieldsRepository)

		if lIncludeDefaultCurrency .and. jParams:hasProperty('moeda') .and. (cMoeda := alltrim(jParams['moeda'])) != cDefaultCurrency
			aFields := {}
			for nX := 1 to len(self:aFieldsRepository)
				if self:aFieldsRepository[nX][2] != 'DA1_MOEDA'
					aAdd(aFields, self:aFieldsRepository[nX])
				endIf
			next nX
			cWhereParam := self:getWhereQueryParam(jParams, aFields)
			cWhereParam += " AND DA1_MOEDA IN (" + cDefaultCurrency + ", " + cMoeda + ")"
		else
			cWhereParam := self:getWhereQueryParam(jParams, self:aFieldsRepository)
		endIf

		self:setFieldsConsulta(self:aFieldsRepository)
		self:setPage(nPage)
		self:setPageSize(nPageSize)
		self:setQuery(self:getQueryDefault(self:aQueryJoinsRepository, self:cQueryOptionalRepository))
		self:setWhere(self:getWhereDefault(cWhereParam))
		self:setUrlFilter(self:getFilterAdvancedQueryParam(jParams))
		self:setOrder(cOrder)

		if self:Execute()
			self:FillGetResponse()
			if self:lOk
				jResponse := self:convertToJson(self:getJSONResponse())
				self:setSuccess(jResponse)
			endIf
		endIf

		FWRestArea(aArea)
		FWFreeArray(aArea)
	endIf
return
