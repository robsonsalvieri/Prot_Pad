#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'
#include 'agd.produto.repository.ch'

namespace agd.produtoRepository
using namespace agd.repositoryUtils

/*/{Protheus.doc} agdProdutoRepository
Repository do cadastro de produtos MATA010
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
class agdProdutoRepository from agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates
	public method new()
	public method convertToFields()
	public method updateRepository()
	public method validaEntradaPrincipioAtivo()
	public method isItemControlaReceituario()

	private method getFields() as array
endclass

/*/{Protheus.doc} new
Inicializa com de/para atributo e campo Protheus
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
method New(pcChaveIDRepository as character) class agdProdutoRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('SB1', self:getFields())
return Self

/*/{Protheus.doc} getFields
De/para atributo e campo Protheus
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
method getFields() as array class agdProdutoRepository
	local aFields as array

	aFields := {;
		{'codigoProduto'  , 'B1_COD',     'C', .T.},;
		{'principioAtivo' , 'NCR_PRATIV', 'C', .T.},;
		{'observacaoMapa' , 'NCR_OBSMAP', 'C', .T.};
		}
return aFields


/*/{Protheus.doc} convertToFields
De/para Campo Protheus e valor enviado da api
@type class
@version 12
@author carlos.augusto
@since 22/10/2025
/*/
method convertToFields(jBodyProduto) Class agdProdutoRepository
	Local aFieldsCommit := {}

	If jBodyProduto:hasProperty("principioAtivo") .Or. !Empty(jBodyProduto:GetJsonObject("principioAtivo"))
		AAdd(aFieldsCommit, {'NCR_PRATIV', jBodyProduto:GetJsonObject("principioAtivo")})
	EndIf

	If jBodyProduto:hasProperty("observacaoMapa") .Or. !Empty(jBodyProduto:GetJsonObject("observacaoMapa"))
		AAdd(aFieldsCommit, {'NCR_OBSMAP', jBodyProduto:GetJsonObject("observacaoMapa")})
	EndIf

Return aFieldsCommit

/*/{Protheus.doc} updateRepository
Atualização do Repositório
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param aFieldsSaveComand, array, lista de campos a serem salvos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdProdutoRepository
	Local nField		as Numeric

	dbSelectArea('NCR')
	NCR->(dbSetOrder(1)) 
	If NCR->(dbSeek(Self:cChaveIDRepository))
		RecLock("NCR",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NCR->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NCR->(MsUnlock())
	endif

return nil


/*/{Protheus.doc} validaEntradaPrincipioAtivo
Validacao previa dos dados enviados pela api
@type class
@version 12
@author carlos.augusto
@since 21/10/2025
/*/
method validaEntradaPrincipioAtivo(jBodyProduto as json) class agdProdutoRepository
	Local lRet := .T.
	Local lProdAgro := "AGRO" $ SUPERGETMV("MV_CADPROD", .F., "")

	If !Self:existsById(Self:cChaveIDRepository)
		Return .F.
	EndIf

	If !lProdAgro
		Self:SetError(STR0002, 400) //"O parâmetro MV_CADPROD não contém AGRO."
		Return .F.
	EndIf

Return lRet


/*/{Protheus.doc} isItemControlaReceituario
Retorna se produto controla receituario
@type class
@version 12
@author carlos.augusto
@since 30/12/2025
/*/
Method isItemControlaReceituario(cChave) class agdProdutoRepository
	Local lRet     := .F.
	Local aAreaNCR := NCR->(GetArea())
	Default cChave := Self:cChaveIDRepository

	dbSelectArea('NCR')
	NCR->(dbSetOrder(1)) // NCR_FILIAL+NCR_PROD

	If NCR->(dbSeek(cChave))
		lRet := (NCR->NCR_EMREC == '1' .And. !Empty(NCR->NCR_MAPA))
	EndIf

	RestArea(aAreaNCR)
Return lRet
