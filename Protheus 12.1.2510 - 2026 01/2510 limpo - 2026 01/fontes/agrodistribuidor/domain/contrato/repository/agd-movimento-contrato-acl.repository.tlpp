#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"
#include 'tlpp-core.th'

namespace agd.movimentoContratoAclRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdMovimentoContratoAclRepository
Repositorio da tabela NEI
@type class
@version 12
@author Gilson.Venturi
@since 23/01/2025
/*/
class agdMovimentoContratoAclRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public Method New()
	public Method findByDocumento()
	public Method findByContrato() as Array
	public Method save()
	public Method deleteByIdCtr()

	private Method getFields()
	private Method getJoins() as Array

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author Gilson.Venturi
@since 23/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdMovimentoContratoAclRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEI', Self:getFields(.t.), Self:getJoins())
Return Self

/*/{Protheus.doc} findByContrato
Retorna array com os movimentos do contrato
@type method
@version 12
@author Gilson.Venturi
@since 23/01/2025
@param pcCodigoContrato, character, Codigo do Contrato
@return array, Lista de Contratos Gerados
/*/
method findByContrato(pcCodigoContrato as Character) as Array class agdMovimentoContratoAclRepository
	Local aArea		     := GetArea()
	Local aMovimentos    := {}

	dbSelectArea('NEI')
	NEI->(dbSetOrder(3)) //NEI_FILIAL+NEI_CODCTR+NEI_SEQ
	NEI->(DBGoTop())
	NEI->(dbSeek(xFilial('NEI')+pcCodigoContrato))

	While NEI->(!EoF()) .and. (xFilial('NEI')+pcCodigoContrato == NEI->NEI_FILIAL+NEI->NEI_CODCTR)
		AAdd(aMovimentos, {NEI->NEI_DOC, NEI->NEI_SERIE, NEI->NEI_FORNEC, NEI->NEI_LOJA, NEI->NEI_ITEM, NEI->NEI_TIPO, NEI_QUANT})
		NEI->(DBSkip(1))
	End

	RestArea(aArea)
return aMovimentos

/*/{Protheus.doc} findByDocumento
Busca pela Chave do documento
@type method
@version 12
@author Gilson Venturi
@since 15/05/2025
@param pcFilial, variant, filial
@param pcDocto, variant, documento
@param pcSerie, variant, serie
@param pcFornec, variant, fornecedor
@param pcLoja, variant, loja
@param pcItem, variant, item
@return variant, nil
/*/
Method findByDocumento(pcFilial,pcDocto,pcSerie,pcFornec,pcLoja,pcItem) class agdMovimentoContratoAclRepository
	Local aArea             As Array
	Local cWhereParam       As Character
	Local jResponseJson     as json
	Local jResponseItemJson as json

	If !Empty(pcDocto)
		If Self:lOk
			aArea := FwGetArea()
			aQueryRequest := {}
			cWhereParam := "NEI_FILIAL = '"+pcFilial+"' AND NEI_DOC = '"+pcDocto+"' AND NEI_SERIE = '"+pcSerie+"' AND "
			cWhereParam += "NEI_FORNEC = '"+pcFornec+"' AND NEI_LOJA = '"+pcLoja+"' AND NEI_ITEM = '"+pcItem+"'

			Self:setFieldsConsulta(self:getFields(.f.))
			Self:setPage(1)
			Self:setPageSize(1)
			Self:SetQuery(Self:getQueryDefault())
			Self:SetWhere(Self:getWhereDefault(cWhereParam))
			
			Self:SetStyleReturn("item")

			If Self:Execute()
				Self:FillGetResponse()
				if Self:lOk
					jResponseJson := Self:convertToJson(Self:getJSONResponse())
					if Self:validateGetByUnico(jResponseJson)
						Self:setSuccess(jResponseJson['item'][1])
					EndIf
				endif
			EndIf

			FwRestArea(aArea)
			FwFreeArray(aArea)
			FreeObj(jResponseJson)
			FreeObj(jResponseItemJson)
		EndIf
	EndIf
Return Nil


/*/{Protheus.doc} save
Salva um Modelo de Dados da tabela NEI
@type method
@version 12
@author Gilson.Venturi
@since 23/01/2025
@param aFieldsSaveComand, array, Lista de Campos a serem salvos
@return variant, nil
/*/
Method save(aFieldsSaveComand as Array)  Class agdMovimentoContratoAclRepository
	Local nField		As Numeric
	Local cError		As Character
	Local oModel		As object
	Local oModelNEI		As object
	Local cReturnID		As Character

	oModel := FwLoadModel("AGDA031")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()
	oModelNEI := oModel:GetModel("AGDA031_NEI")

	For nField := 1 to len(aFieldsSaveComand)
		oModelNEI:LoadValue( aFieldsSaveComand[nField][1] , aFieldsSaveComand[nField][2])
	Next

	cReturnID := xFilial('NEI') + oModelNEI:GetValue("NEI_CODCTR") + oModelNEI:GetValue("NEI_ITEM")

	If oModel:VldData()
		oModel:CommitData()
		Self:setSuccess(cReturnID)
	Else
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError, 404)
	EndIf

	oModel:DeActivate()
	oModel:Destroy()
	oModel := NIL
	FreeObj(oModel)
Return nil


/*/{Protheus.doc} deleteByIdCtr
Realiza a deleção de todos os registros com mesmo NEI_IDCTR
@type method
@version  P12
@author claudineia.reinert
@since 19/05/2025
@param pcIdCtr, character, Id do contrato, valor campo NEI_IDCTR
/*/
Method deleteByIdCtr(pcIdCtr As Character) Class agdMovimentoContratoAclRepository
	Local oModel	:= nil
	Local cFilNEI	:= FWxFilial("NEI")
	Local cIdCtr 	:= PadR(pcIdCtr,TamSX3("NEI_IDCTR")[1])
	Local cError 	:= ""
	Local aAreaNEI 	:= NEI->(GetArea())

	dbSelectArea('NEI')
	NEI->(dbSetOrder(1))
	If NEI->(dbSeek(cFilNEI + pcIdCtr))
		oModel := FwLoadModel("AGDA031")
		While NEI->(!EoF()) .and. NEI->(NEI_FILIAL+NEI_IDCTR) == (cFilNEI + cIdCtr)

			oModel:SetOperation(MODEL_OPERATION_DELETE)
			oModel:Activate()
			If oModel:VldData()
				oModel:CommitData()
				Self:setSuccess(self:cChaveIDRepository)
			Else
				cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
				cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
				Self:setError(cError, 400)
				EXIT
			EndIf

			NEI->(DBSkip())
			oModel:DeActivate()

		EndDo
		oModel:Destroy()
		oModel := NIL
		FreeObj(oModel)

	Endif

	RestArea(aAreaNEI)

Return nil

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositório
@type method
@version 12
@author Gilson.Venturi
@since 10/01/2025
@return variant, lista de campos
/*/
Method getFields(lCamposEstrangerios) Class agdMovimentoContratoAclRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'		,'NEI_FILIAL'	, 'C', .T.})
	AAdd(aArrayFields, {'codContrato'	,'NEI_CODCTR'	, 'C', .T.})
	AAdd(aArrayFields, {'sequen'		,'NEI_SEQ'		, 'C', .T.})
	AAdd(aArrayFields, {'documento'		,'NEI_DOC'		, 'C', .T.})
	AAdd(aArrayFields, {'serie'			,'NEI_SERIE'	, 'C', .T.})
	AAdd(aArrayFields, {'fornecedor'	,'NEI_FORNEC'	, 'C', .T.})
	AAdd(aArrayFields, {'loja'			,'NEI_LOJA'		, 'C', .T.})
	AAdd(aArrayFields, {'item'			,'NEI_ITEM'		, 'C', .T.})
	AAdd(aArrayFields, {'tipo'			,'NEI_TIPO'		, 'C', .T.})
	AAdd(aArrayFields, {'idContrato'	,'NEI_IDCTR'	, 'C', .T.})
	AAdd(aArrayFields, {'quantidade'	,'NEI_QUANT'	, 'N', .T.})
	if lCamposEstrangerios
		AAdd(aArrayFields, {'valorNF'		,'D1_TOTAL'		, 'N', .T.})
		AAdd(aArrayFields, {'tipo'			,'D1_TIPO'		, 'C', .T.})
		AAdd(aArrayFields, {'dataEmissao'	,'D1_EMISSAO'	, 'D', .T.})
	endif
Return aArrayFields


/*/{Protheus.doc} getJoins
Retorna os joins das consultas
@type method
@version 12
@author joao.vtargino
@since 26/082025
@return variant, array de dados
/*/
Method getJoins() Class agdMovimentoContratoAclRepository
	Local aArrayJoins  := {}

	AAdd(aArrayJoins, {'INNER', 'SD1' , Self:getWhereDefault('SD1.D1_DOC = NEI.NEI_DOC AND SD1.D1_SERIE = NEI.NEI_SERIE AND SD1.D1_FORNECE = NEI.NEI_FORNEC AND SD1.D1_LOJA = NEI.NEI_LOJA AND SD1.D1_ITEM = NEI.NEI_ITEM', 'SD1')})

Return aArrayJoins
