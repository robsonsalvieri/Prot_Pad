#include "totvs.ch"
#INCLUDE "FWMVCDEF.CH"
#include 'tlpp-core.th'
#include "AGD.CONTRATO.ACL.REPOSITORY.CH"

namespace agd.contratoAclRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdContratoAclRepository
Repositorio da tabela NEF
@type class
@version 12
@author jean.schulze
@since 06/01/2025
/*/
class agdContratoAclRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates


	public Method New()
	public Method findByContratoExterno()
	public Method save()
	public Method updateRepository()
	public Method findByNegocio() as Array
	public Method allContratoExternoCancelado() as Logical
	public Method delete()
	public Method getCountVinculoItemBarter() as Integer
	public Method getUltimaSequenciaNEF() as Character

	private Method getFields()
	private Method getQueryWithTotal()


endclass
/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdContratoAclRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEF', Self:getFields(.t.), , Self:getQueryWithTotal())
Return Self

/*/{Protheus.doc} findByContratoExterno
Busca pelo ID do Contrato Externo
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param cCodigo, character, ID contrato Externo
@return variant, nil
/*/
Method findByContratoExterno(cCodigo) class agdContratoAclRepository
	Local aArea             As Array
	Local cWhereParam       As Character
	Local jResponseJson     as json
	Local jResponseItemJson as json

	If !Empty(cCodigo)
		If Self:lOk
			aArea := FwGetArea()
			aQueryRequest := {}

			cWhereParam := "NEF.NEF_IDCTR = '"+cCodigo+"'

			Self:setFieldsConsulta(self:getFields())
			Self:setPage(1)
			Self:setPageSize(1)
			Self:SetQuery(Self:getQueryDefault())
			Self:SetWhere(Self:getWhereDefault(cWhereParam))
			Self:SetStyleReturn("item")

			If Self:Execute()
				Self:FillGetResponse()
				if Self:lOk
					jResponseJson := Self:convertToJson(Self:getJSONResponse())
					jResponseItemJson := jResponseJson['item']
					if Self:validateGetByUnico(jResponseJson)
						Self:setSuccess(jResponseItemJson[1])
					EndIf
				Endif
			EndIf

			FwRestArea(aArea)
			FwFreeArray(aArea)
			FreeObj(jResponseJson)
			FreeObj(jResponseItemJson)
		EndIf
	EndIf
Return Nil

/*/{Protheus.doc} findByNegocio
Retorna array com os contratos já gerados para o negócio
@type method
@version 12
@author jean.schulze
@since 10/01/2025
@param pcCodigoNegocio, character, Codigo do Negocio Barter
@return array, Lista de Contratos Gerados
/*/
method findByNegocio(pcCodigoNegocio as Character) as Array class agdContratoAclRepository
	Local aArea		     := GetArea()
	Local aContratos     := {}

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(DBGoTop())
	NEF->(dbSeek(xFilial('NEF')+pcCodigoNegocio))

	While NEF->(!EoF()) .and. (xFilial('NEF')+pcCodigoNegocio == NEF->NEF_FILIAL+NEF->NEF_CODBRT)
		AAdd(aContratos, {NEF->NEF_ITECTR, NEF->NEF_IDCTR, NEF->NEF_QUANT})
		NEF->(DBSkip(1))
	End

	RestArea(aArea)
return aContratos

/*/{Protheus.doc} save
Salva um Modelo de Dados da tabela NEF
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@param aFieldsSaveComand, array, Lista de Campos a serem salvos
@return variant, nil
/*/
Method save(aFieldsSaveComand as Array)  Class agdContratoAclRepository
	Local nField		as Numeric
	Local cError        as Character
	Local oModel 		as object
	Local oModelNFE  	as object
	Local cReturnID     as Character


	oModel := FwLoadModel("AGDA030")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()
	oModelNFE := oModel:GetModel("AGDA030_NEF")

	aFields := self:getFields()

	For nField := 1 to len(aFieldsSaveComand)
		oModelNFE:LoadValue( aFieldsSaveComand[nField][1] , aFieldsSaveComand[nField][2])
	Next

	cReturnID := xFilial('NEF') + oModelNFE:GetValue("NEF_CODBRT") + oModelNFE:GetValue("NEF_ITECTR")+ oModelNFE:GetValue("NEF_ITEM")

	If oModel:VldData()
		oModel:CommitData()
		Self:setSuccess(cReturnID)
	Else
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError, 404)
	EndIf

	oModel:DeActivate()
	oModel:Destroy()
	oModel := NIL
	FreeObj(oModel)
Return nil

/*/{Protheus.doc} updateRepository
Atualização do Repositório
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param aFieldsSaveComand, array, lista de campos a serem salvos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdContratoAclRepository
	Local nField		as Numeric

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(dbSeek(Self:cChaveIDRepository))

	If Self:cChaveIDRepository  == NEF->(NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM)
		RecLock("NEF",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NEF->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NEF->(MsUnlock())
	endif

return nil

/*/{Protheus.doc} allContratoExternoCancelado
Retorna ID da negociacao barter
@type method
@version 12
@author lindembergson.pacheco
@since 20/02/2025
@param cIdNegocioBarter, character, id da negociacao barte
@return Logical, valor logico
/*/
method allContratoExternoCancelado(cIdNegocioBarter as Character) as Logical class agdContratoAclRepository
	Local aArea	   := GetArea()
	Local lRet      := .F.
	Local aCanc		:= {}
	Local nReg		:= 0

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	NEF->(DBGoTop())
	IF NEF->(dbSeek(cIdNegocioBarter))
		nReg := 0
		While NEF->(!EoF()) .and. (cIdNegocioBarter == NEF->NEF_FILIAL+NEF->NEF_CODBRT)

			dbSelectArea('NJR')
			dbSetOrder(1) //NJR_FILIAL+NJR_CODCTR
			NJR->(DBGoTop())
			IF NJR->(dbSeek(xFilial('NJR')+NEF->NEF_CTREXT))
				IF ALLTRIM(NJR->NJR_STATUS) == 'E'
					aAdd(aCanc,{.T.})
				ENDIF
			ENDIF
			nReg++
			NEF->(DBSkip())
		End
	ENDIF

	if len(aCanc) == nReg
		lRet := .T.
	endif

	RestArea(aArea)
return lRet

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositório
@type method
@version 12
@author jean.schulze
@since 10/01/2025
@return variant, lista de campos
/*/
Method getFields(lFieldsAdd) Class agdContratoAclRepository
	Local aArrayFields := {}
	Default lFieldsAdd := .f.

	AAdd(aArrayFields, {'filial'                 , 'NEF_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'                 , 'NEF_CODBRT', 'C', .T.})
	AAdd(aArrayFields, {'itemLocalContrato'      , 'NEF_ITECTR', 'C', .T.})
	AAdd(aArrayFields, {'item'                   , 'NEF_ITEM'  , 'C', .T.})
	AAdd(aArrayFields, {'quantidade'             , 'NEF_QUANT',  'N', .T.})
	AAdd(aArrayFields, {'idContrato'             , 'NEF_IDCTR',  'C', .T.})
	AAdd(aArrayFields, {'descricaoContrato'      , 'NEF_CTREXT', 'C', .T.})

	if NEF->(FieldPos("NEF_DATA")) > 0
		AAdd(aArrayFields, {'data'  , 'NEF_DATA', 'D', .T.})
	endif

	if lFieldsAdd
		AAdd(aArrayFields, {'totalRecebido'     , 'TOTAL_MOV' , 'N', .T.})
		AAdd(aArrayFields, {'saldo'             , 'SALDO_MOV' , 'N', .T.})
		AAdd(aArrayFields, {'status'            , 'STATUS' , 'N', .T.})
		AAdd(aArrayFields, {'statusRecebimento' , 'STATUS_RECEBIMENTO', 'N', .T.})
	endif

Return aArrayFields


/*/{Protheus.doc} delete
Metodo deleção do registro da Tabela NEF
@type method
@version P12 
@author scheronlini.martins
@since 17/03/2025
/*/
Method delete() Class agdContratoAclRepository
	Local oModel:= nil
	Local aAreaNEF := NEF->(GetArea())

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	If NEF->(dbSeek(self:cChaveIDRepository))

		oModel := FwLoadModel("AGDA030")
		oModel:SetOperation(MODEL_OPERATION_DELETE)
		oModel:Activate()
		If oModel:VldData()
			oModel:CommitData()
			Self:setSuccess(self:cChaveIDRepository)
		Else
			cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	    + ' - '
			cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
			Self:setError(cError, 400)
		EndIf

		oModel:DeActivate()
		oModel:Destroy()
		oModel := NIL
		FreeObj(oModel)

	endif
	RestArea(aAreaNEF)
Return nil

/*/{Protheus.doc} getQueryWithTotal
Query com totalizadores
@type method
@version 12
@author jean.schulze
@since 06/05/2025
@return variant, query
/*/
Method getQueryWithTotal() Class agdContratoAclRepository
	Local cQuery    as Character
	Local cFuncNull := ""
	Local cTypeDB := Upper(TcGetDb())

If cTypeDB $ "INFORMIX|ORACLE"
    cFuncNull := "NVL"
ElseIf cTypeDB $ "DB2|POSTGRES"
    cFuncNull := "COALESCE"
Else
    cFuncNull := "ISNULL"
EndIf

cQuery := " SELECT NEF.*, "
cQuery += " " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) AS TOTAL_MOV, "
cQuery += " (NEF_QUANT - " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0)) AS SALDO_MOV, "

cQuery += " CASE "
cQuery += " WHEN NEF_QUANT = 0 THEN 'CANCELADO' "
cQuery += " WHEN " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) = 0 THEN 'ABERTO' "
cQuery += " WHEN (NEF_QUANT - " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0)) <= 0 THEN 'CONCLUIDO' "
cQuery += " WHEN " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) > 0 "
cQuery += " AND " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) < NEF_QUANT THEN 'INICIADO' "
cQuery += " END AS STATUS, "

cQuery += " CASE "
cQuery += " WHEN " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) = 0 THEN 'PENDENTE' "
cQuery += " WHEN (NEF_QUANT - " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0)) <= 0 THEN 'FINALIZADO' "
cQuery += " WHEN " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) > 0 "
cQuery += " AND " + cFuncNull + "((SELECT SUM(NEI_QUANT) FROM  " + retSqlName("NEI") + " WHERE NEI_IDCTR = NEF_IDCTR), 0) < NEF_QUANT THEN 'RECEBENDO' "
cQuery += " END AS STATUS_RECEBIMENTO "

cQuery += " FROM " + retSqlName("NEF") + " NEF"
	
Return cQuery

/*/{Protheus.doc} agdContratoAclRepository::getCountVinculoItemBarter
Retorna quantos registros de contratos(NEF) estão vinculados a um item de contrato(NEE) do barter
@type method
@version  P12
@author claudineia.reinert
@since 19/05/2025
@return Numeric, quantidade de registro
/*/
Method getCountVinculoItemBarter() As Integer Class agdContratoAclRepository
	Local cQuery    as Character
	Local nCont 	:= 0
	Local aAreaNEF	:= NEF->(GetArea())

	dbSelectArea('NEF')
	NEF->(dbSetOrder(3)) //NEF_FILIAL+NEF_CODBRT+NEF_ITECTR+NEF_ITEM
	If NEF->(dbSeek(self:cChaveIDRepository))

		cQuery := " SELECT COUNT(NEF_ITEM) AS nReg "
		cQuery += " FROM "+ retSqlName('NEF') +" NEF "
		cQuery += " WHERE NEF.D_E_L_E_T_ = ' ' "
		cQuery += " AND NEF_FILIAL = ? "
		cQuery += " AND NEF_CODBRT = ? "
		cQuery += " AND NEF_ITECTR = ? "
		oStatem := FWPreparedStatement():New()
		oStatem:SetQuery(cQuery)
		oStatem:SetString(1, NEF->NEF_FILIAL)
		oStatem:SetString(2, NEF->NEF_CODBRT)
		oStatem:SetString(3, NEF->NEF_ITECTR)
		cQuery := oStatem:GetFixQuery()
		cQuery := ChangeQuery( cQuery )

		nCont := MPSysExecScalar(cQuery,"nReg")
	EndIf
	RestArea(aAreaNEF)
Return nCont

/*/{Protheus.doc} agdContratoAclRepository::getUltimaSequenciaNEF
Retorna a ultima sequencia do item ACL do contrato (NEF_ITEM)
@type method
@version P12 
@author claudineia.reinert
@since 10/2/2025
@param cCodBrt, character, código da negociação de barter
@param cItemCtr, character, Item da negociação de barter
@return character, ultima sequência do item ACL do contrato(NEF_ITEM)
/*/
Method getUltimaSequenciaNEF(cCodBrt, cItemCtr) As Character Class agdContratoAclRepository
	Local cItemNEF as Character
	Local cQuery as Character
	Local oStatemNEF as Object
	
	cQuery := "SELECT MAX(NEF_ITEM) AS NEF_ITEM "
	cQuery += " FROM "+ retSqlName('NEF') +" NEF "
	cQuery += " WHERE NEF_FILIAL = ? "
	cQuery += " AND NEF_CODBRT = ?  "
	cQuery += " AND NEF_ITECTR = ? "
	cQuery += " AND NEF.D_E_L_E_T_ = '' "
	cQuery := ChangeQuery( cQuery )

	oStatemNEF := FWPreparedStatement():New()
	oStatemNEF:SetQuery(cQuery)
	oStatemNEF:SetString(1, FWxFilial('NEF'))
	oStatemNEF:SetString(2, cCodBrt)
	oStatemNEF:SetString(3, cItemCtr)
	cQuery := oStatemNEF:GetFixQuery()

	cItemNEF := MPSysExecScalar(cQuery,"NEF_ITEM")

Return cItemNEF
