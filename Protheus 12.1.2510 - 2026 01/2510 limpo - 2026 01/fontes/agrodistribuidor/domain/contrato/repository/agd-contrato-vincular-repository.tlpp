#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'

namespace agd.contratovincularrepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdcontratovincularrepository
Repository de contratos a vincular
@type class
@version P12 
@author scheronlini.martins
@since 19/08/2025
/*/
class agdcontratovincularrepository FROM agdRepositoryUtils

	public Method New()
	public Method getFields()
    public Method find()
	public method qryContratosOGVinculo()

endclass

/*/{Protheus.doc} agdcontratovincularrepository::New
Criação do objeto 
@type method
@version P12
@author scheronlini.martins
@since 19/08/2025
@return variant, return_objeto
/*/
Method New(lContrato, cCodContrato) Class agdcontratovincularrepository
	default cCodContrato := ""
	_Super:New("TMP_CONTRATOS",Self:getFields(),{}, self:qryContratosOGVinculo(lContrato,cCodContrato))
Return Self


/*/{Protheus.doc} agdcontratovincularrepository::getFields
Campos do contrato a vincular
@type method
@version P12
@author scheronlini.martins
@since 19/08/2025
@return variant, return_array de campos
/*/
Method getFields() Class agdcontratovincularrepository
    Local aFields := {}
    
aAdd(aFields, {"filial",                   "NJR_FILIAL", "C", 2})
aAdd(aFields, {"contrato",                 "NJR_CODCTR", "C", 6})
aAdd(aFields, {"propriedade",              "NJR_CODENT", "C", 6})
aAdd(aFields, {"propriedadeLoja",          "NJR_LOJENT", "C", 2})
aAdd(aFields, {"propriedadeNome",          "NJ0_NOME",   "C", 40})
aAdd(aFields, {"quantidade",               "NJR_QTDCTR", "N", 12})
aAdd(aFields, {"tipoEntrada",              "NJR_TESEST", "C", 6})
aAdd(aFields, {"operacaoFiscal",           "NJR_OPEFIS", "C", 6})
aAdd(aFields, {"dataInicio",               "NNY_DATINI", "D", 8})
aAdd(aFields, {"dataFim",                  "NNY_DATFIM", "D", 8})
aAdd(aFields, {"condicaoPagamento",        "NJR_CONDPG", "C", 6})
aAdd(aFields, {"condicaoPagamentoDescricao","E4_DESCRI", "C", 40})
aAdd(aFields, {"tipoFrete",                "NJR_TPFRET", "C", 1})
aAdd(aFields, {"tabela",                   "NJR_TABELA", "C", 6})
aAdd(aFields, {"tabelaDescricao",          "NNI_DESCRI", "C", 40})
aAdd(aFields, {"natureza",                 "NN7_NATURE", "C", 6})
aAdd(aFields, {"safra",                    "NJR_CODSAF", "C", 6})
aAdd(aFields, {"contratoDescricao",        "NJR_DESCRI", "C", 60})
aAdd(aFields, {"dataContrato",             "NJR_DATA",   "D", 8})

Return aFields

/*/{Protheus.doc} agdcontratovincularrepository::find
Método retorna todos os contratos OG disponiveis para o vinculo
@type method
@version P12
@author scheronlini.martins
@since 19/08/2025
@param nPage, numeric, página de resultados
@param nPageSize, numeric,registros por pagina
@param oJsonParam, object, param_objeto com parametros
/*/
Method find(nPage, nPageSize, oJsonParam) Class agdcontratovincularrepository
  	Local aArea         As Array
	Local cWhereParam   As Character
	Local cOrder        As Character
	Local jResponseJson as Object

	Default oJsonParam := Nil
	Default nPage          := 1
	Default nPageSize      := 10

	If Self:lOk
		aArea       := FwGetArea()
		cOrder      := Self:getOrderQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:aFieldsRepository)

		Self:setFieldsConsulta(Self:aFieldsRepository)
		Self:setPage(nPage)
		Self:setPageSize(nPageSize)
		Self:SetQuery(Self:getQueryDefault(Self:aQueryJoinsRepository, Self:cQueryOptionalRepository))
		Self:SetWhere("1=1")
		Self:SetUrlFilter(Self:getFilterAdvancedQueryParam(oJsonParam))
		Self:SetOrder( cOrder )

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf
    
return nil

/*/{Protheus.doc} qryContratosOGVinculo
Monta query para contratos OG disponiveis para vinculo
@type function
@version P12
@author scheronlini.martins
@since 19/08/2025
@param lContrato, logical, utiliza numero de contrato
@param cCodigoContratoext, character, numero de contrato
@return variant, return_query pronta
/*/
method qryContratosOGVinculo(lContrato, cCodigoContratoext) Class agdcontratovincularrepository
	Local aParams   := {}
	Local nX := 0
	Default cCodigoContratoext:= ""

	cQryDados := " SELECT DISTINCT NJR_FILIAL, "
	cQryDados += 	" NJR_CODCTR, "
	cQryDados += 	" NNY_ITEM , "
	cQryDados += 	" NJR_UM1PRO, "
	cQryDados += 	" NJR_UMPRC, "
	cQryDados += 	" NJR_CODPRO, "
	cQryDados += 	" NJR_CODENT, "
	cQryDados += 	" NJR_LOJENT, "
	cQryDados += 	" NJ0_NOME, "
	cQryDados += 	" NJR_QTDCTR, "
	cQryDados += 	" NJR_TESEST, "
	cQryDados += 	" NJR_OPEFIS, "
	cQryDados += 	" MIN_NNY_DATINI AS NNY_DATINI, "
	cQryDados += 	" MAX_NNY_DATFIM AS NNY_DATFIM, "
	cQryDados += 	" NJR_CONDPG, "
	cQryDados += 	" E4_DESCRI, "
	cQryDados += 	" NJR_TPFRET, "
	cQryDados += 	" NJR_TABELA, "
	cQryDados += 	" NNI_DESCRI, "
	cQryDados += 	" NN7_NATURE, "
	cQryDados += 	" NJR_CODSAF, "
	cQryDados += 	" NJR_DESCRI, "
	cQryDados += 	" NJR_DATA "
	cQryDados += " FROM "+ retSqlName('NEA') +" NEA "
	cQryDados += " INNER JOIN "+ retSqlName('SA1') +" SA1 "
	cQryDados += 	" ON SA1.D_E_L_E_T_ = '' "
	cQryDados += 	" AND SA1.A1_FILIAL = ? " ; aAdd(aParams, FWxFilial("SA1"))
	cQryDados += 	" AND SA1.A1_COD = NEA_CODCLI "
	cQryDados += 	" AND SA1.A1_LOJA = NEA.NEA_LOJCLI "
	cQryDados += " INNER JOIN "+ retSqlName('SA2') +" SA2 "
	cQryDados += 	" ON SA2.D_E_L_E_T_ = '' "
	cQryDados += 	" AND SA2.A2_FILIAL = ? " ; aAdd(aParams, FWxFilial("SA2"))
	cQryDados += 	" AND SA2.A2_CGC = SA1.A1_CGC "
	cQryDados += " INNER JOIN "+ retSqlName('NJR') +" NJR "
	cQryDados += 	" ON NJR.D_E_L_E_T_ = '' "
	cQryDados += 	" AND NJR.NJR_FILIAL = ? " ; aAdd(aParams, FWxFilial("NJR"))
	IF lContrato
		cQryDados += " and NJR.NJR_CODCTR = '"+ cCodigoContratoext +"'"
	endif
	cQryDados += 	" AND NJR_TIPO = '1' " //contato compra
	cQryDados += 	" AND NJR_TIPFIX = '1' " // contrato fixo
	cQryDados += 	" AND NJR_STATUS IN ( 'A', 'I' ) " //contrato aberto ou iniciado
	cQryDados += 	" AND NJR_STSFIN = 'A' " //financeiro aberto
	cQryDados += 	" AND NJR_CODPRO = NEA_CODPRO "
	cQryDados += 	" AND NJR_MOEDA = NEA_MOEDRT "
	cQryDados += 	" AND NJR_VLRBAS = NEA_VLRRTL "
	cQryDados += 	" AND NJR_QTDCTR > 0 "
	cQryDados += " INNER JOIN "+ retSqlName('NJ0') +" NJ0 "
	cQryDados += 	" ON NJ0.D_E_L_E_T_ = '' "
	cQryDados += 	" AND NJ0.NJ0_FILIAL = ? " ; aAdd(aParams, FWxFilial("NJ0"))
	cQryDados += 	" AND NJ0_CODENT = NJR_CODENT "
	cQryDados += 	" AND NJR_LOJENT = NJ0_LOJENT "
	cQryDados += 	" AND ( NJ0.NJ0_CGC = SA2.A2_CGC "
	cQryDados += 	" OR NJ0.NJ0_CODFOR = SA2.A2_COD ) "
	cQryDados += " INNER JOIN "
	cQryDados += 	" ( "
	cQryDados += 		"  SELECT NNY_FILIAL, "
	cQryDados += 			" NNY_CODCTR, "
	cQryDados += 			" MIN(NNY_ITEM) AS NNY_ITEM, "
	cQryDados += 			" MIN(NNY_DATINI) AS MIN_NNY_DATINI, "
	cQryDados += 			" MAX(NNY_DATFIM) AS MAX_NNY_DATFIM "
	cQryDados += 		" FROM "+ retSqlName('NNY') +" "
	cQryDados += 		" WHERE D_E_L_E_T_ = '' "
	cQryDados += 		" GROUP BY NNY_FILIAL, "
	cQryDados += 			" NNY_CODCTR "
	cQryDados += 	" ) NNY "
	cQryDados += 		" ON NNY_CODCTR = NJR_CODCTR "
	cQryDados += 		" AND NNY_FILIAL = NJR_FILIAL "
	cQryDados += 		" AND MAX_NNY_DATFIM > ? " ; aAdd(aParams, DTOS(DDATABASE))
	cQryDados += " LEFT JOIN "+ retSqlName('NN7') +" NN7 "
	cQryDados +=	" ON NN7_FILIAL = NJR_FILIAL "
	cQryDados +=		" AND NJR_CODCTR = NN7_CODCTR "
	cQryDados += 		" AND NNY_ITEM = NN7_ITEM "
	cQryDados += 		" AND NN7.D_E_L_E_T_ = '' "
	cQryDados += " LEFT JOIN "+ retSqlName('SE4') +" SE4 "
	cQryDados += 	" ON E4_FILIAL = ? " ; aAdd(aParams, FWxFilial("SE4"))
	cQryDados += 		" AND NJR_CONDPG = E4_CODIGO "
	cQryDados += 		" AND SE4.D_E_L_E_T_ = '' "
	cQryDados += " LEFT JOIN "+ retSqlName('NNI') +" NNI "
	cQryDados += 	" ON NNI.D_E_L_E_T_ = '' "
	cQryDados += 	" AND NNI.NNI_FILIAL = ? " ; aAdd(aParams, FWxFilial("NNI"))
	cQryDados += 	" AND NJR_TABELA = NNI_CODIGO "
	cQryDados += 		" AND NNI.D_E_L_E_T_ = '' "
	cQryDados += " WHERE NEA.D_E_L_E_T_ = ' ' "
	cQryDados += 	" AND NEA_CODIGO = ? " ; aAdd(aParams, NEA->NEA_CODIGO)
	cQryDados += 	" AND NEA_VERSAO = ? " ; aAdd(aParams, NEA->NEA_VERSAO)
	//NÃO TRAZ CONTRATOS JA VINCULADOS EM NEGOCIAÇÃO BARTER
	cQryDados += 	" AND NJR_CODCTR NOT IN ( "
	cQryDados += 		" SELECT NEF_CTREXT "
	cQryDados += 		" FROM "+ retSqlName('NEF') +" NEF "
	cQryDados += 		" WHERE NEF.D_E_L_E_T_ = '' "
	cQryDados += 			" AND NEF.NEF_FILIAL = ? " ; aAdd(aParams, FWxFilial("NEF"))
	cQryDados += 			" AND NEF.NEF_IDCTR <> '' "
	cQryDados += 			" AND NEF.NEF_CTREXT = NJR.NJR_CODCTR "
	cQryDados +=	 " ) "
	//NÃO TRAZ CONTRATOS COM ALGUM TITULOS BAIXADO
	cQryDados += 	" AND NJR.NJR_CODCTR NOT IN ( "
	cQryDados += 		" SELECT XNJR.NJR_CODCTR "
	cQryDados += 		" FROM "+ retSqlName('NJR') +" XNJR
	cQryDados += 		" INNER JOIN "+ retSqlName('NJM') +" NJM ON NJM.D_E_L_E_T_ = ''
	cQryDados += 			" AND NJM.NJM_FILIAL = XNJR.NJR_FILIAL "
	cQryDados += 			" AND NJM.NJM_CODCTR = XNJR.NJR_CODCTR "
	cQryDados += 		" INNER JOIN "+ retSqlName('NJ0') +" NJ0 ON NJ0_CODENT = NJM_CODENT AND NJ0_LOJENT = NJM_LOJENT "
	cQryDados += 		" INNER JOIN "+ retSqlName('SD1') +" SD1 ON SD1.D_E_L_E_T_ = '' AND SD1.D1_FILIAL = NJM_FILIAL "
	cQryDados += 			" AND NJM_DOCNUM = D1_DOC AND NJM_DOCSER = D1_SERIE "
	cQryDados += 			" AND D1_FORNECE = NJ0_CODFOR AND D1_LOJA = NJ0_LOJFOR "
	cQryDados += 			" AND D1_TIPO = 'N'  AND SD1.D1_COD = NJM_CODPRO "
	cQryDados += 		" INNER JOIN "+ retSqlName('SE2') +" SE2 "
	cQryDados += 			" ON  SE2.E2_FILIAL  = SD1.D1_FILIAL "
	cQryDados += 			" AND SE2.E2_NUM     = D1_DOC "
	cQryDados += 			" AND SE2.E2_PREFIXO = D1_SERIE "
	cQryDados += 			" AND SE2.E2_FORNECE = D1_FORNECE "
	cQryDados += 			" AND SE2.E2_LOJA    = D1_LOJA "
	cQryDados += 			" AND SE2.D_E_L_E_T_  = ' ' "
	cQryDados += 			" AND SE2.E2_SALDO <> SE2.E2_VALOR "
	cQryDados += 		" WHERE XNJR.D_E_L_E_T_ = '' "
	cQryDados += 			" AND XNJR.NJR_CODCTR = NJR.NJR_CODCTR  ) "
	cQryDados := ChangeQuery( cQryDados )

	oStateDad := FWPreparedStatement():New()
	oStateDad:SetQuery(cQryDados)
	aEval(aParams, {|cValue| (nX++, oStateDad:SetString(nX, cValue))})
	cQryDados := oStateDad:GetFixQuery()

	return cQryDados
