#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.toolsDTAService
using namespace agd.toolsDTARepository
using namespace agd.toolsDTAPermissaoRepository
using namespace agd.utilsService
using namespace agd.restUtils


/*/{Protheus.doc} agdToolsDTAService
Serviço para gerenciamento de Solicitação de Receita (NET/NEU)
@type class
@version 12
@author lindembergson.pacheco
@since 06/10/2025
/*/
class agdToolsDTAService FROM agdUtilsService

	public method New()
	public method getTools()
	public method getById()
	public method getDisponiveis()
	public method habilitar()
	public method inativar()
	public method ativar()
	public method getPermissao()
	public method informarPermissao()
	public method removerPermissao()
	public Method hasUserToolAuthorization() as Logical 

	Static Method getToolsCatalog() as Array
endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return variant, this
/*/
method New() class agdToolsDTAService
	_Super:New()
return Self


/*/{Protheus.doc} getTools
Listar Tools Habilitadas
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method getTools(nPage, nPageSize, oJsonQryParam) class agdToolsDTAService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdToolsDTARepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} getById
Busca no repositório TCO as informações completas de uma feature específica identificada pelo código informado.
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, param_feature
/*/
method getById(cFeature) class agdToolsDTAService
	Local oRepository As Object

	oRepository := agdToolsDTARepository():New()
	oRepository:findById(oRepository:getFilterById(cFeature))

	Self:setFullResponse(oRepository)
	
	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} getDisponiveis
Retorna os Tools Disponíveis no DTA
@type method
@version 12
@author jean.schulze
@since 12/11/2025
@return variant, nil
/*/
method getDisponiveis() class agdToolsDTAService
	Local aItens   := {}
	Local jReponse := JsonObject():New()
	Local nX	  as Numeric

	For nX:= 1 to Len(agdDTAToolsEnum():enums())
		aAdd(aItens, JsonObject():New() )
		aItens[nx]['codigo'] = agdDTAToolsEnum():enums()[nX][1]		
		aItens[nx]['descricao'] = agdDTAToolsEnum():enums()[nX][2]		
	Next
	
	jReponse['items'] := aItens
	jReponse['hasNext'] := .f.
	Self:setSuccess(jReponse)	

	FreeObj(jReponse)
Return nil

/*/{Protheus.doc} habilitar
Comando para registrar um Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method habilitar(jCmdRegistrar) class agdToolsDTAService
	Local oRepository As Object

	oRepository := agdToolsDTARepository():New()
	
	oRepository:save(jCmdRegistrar)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil


/*/{Protheus.doc} inativar
Comando para inativar uma feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, codigo feature
@return variant, retorno
/*/
method inativar(cFeature) class agdToolsDTAService
	Local oRepository As Object

	oRepository := agdToolsDTARepository():New(xfilial('NE3')+cFeature)

	oRepository:updateRepository({{"NE3_ATIVO", "2"}})

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} ativar
Comando para ativar uma feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, codigo da feature
@return variant, retorno
/*/
method ativar(cFeature) class agdToolsDTAService
	Local oRepository As Object

	oRepository := agdToolsDTARepository():New(xfilial('NE3')+cFeature)

	oRepository:updateRepository({{"NE3_ATIVO", "1"}})

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil


/*/{Protheus.doc} getPermissao
Listar Tools Habilitadas
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method getPermissao(nPage, nPageSize, oJsonQryParam) class agdToolsDTAService
	Local oRepository as Object
	Local nX          as Numeric
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdToolsDTAPermissaoRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)


	Self:setFullResponse(oRepository)

	if oRepository:isSuccess()
		oResponse := oRepository:getResponse()

		For nX:= 1 to Len(oResponse['items'])
			if !empty(oResponse['items'][nX]['usuario'] )
				oResponse['items'][nX]['name'] :=  UsrRetName(oResponse['items'][nX]['usuario'])
			elseif !empty(oResponse['items'][nX]['grupo'] )
				oResponse['items'][nX]['name']  := GrpRetName ( oResponse['items'][nX]['grupo'] )	
			endif	
		Next

	endif
	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} informarPermissao
Comando para registrar uma permissao de acesso a tool
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method informarPermissao(jCmdRegistrar) class agdToolsDTAService
	Local oRepository As Object

	oRepository := agdToolsDTAPermissaoRepository():New()
	
	oRepository:save(jCmdRegistrar)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} hasUserToolAuthorization
Retorna se o usuário tem autorização para usar a tool
@type function
@version 12
@author jean.schulze
@since 12/8/2025
@return variant, possui autorização
/*/
Method hasUserToolAuthorization(cFunction as Character) as Logical Class agdToolsDTAService
	Local lRetorno := .f.

	oRepository := agdToolsDTAPermissaoRepository():New()
	lRetorno := oRepository:hasByUsuario(cFunction, RetCodUsr())
	if !lRetorno 
		lRetorno :=oRepository:hasGrupoByUsuario(cFunction, RetCodUsr()) //cria funcao q verifica se tem para o grupo ou usuario //cria separado
	endif	

	oRepository:DeActivate()
	FreeObj(oRepository)

Return lRetorno

/*/{Protheus.doc} getToolsCatalog
Tools Ativas para o DTA
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return array, lista de tools
/*/
Method getToolsCatalog()  as Array Class agdToolsDTAService
	Local aTools   := {}  as Array
	Local jParams  := Nil as Json
	Local jEmbedd  := Nil as Json
	Local jTools   := Nil as Json

	dbSelectArea('NE3')
	NE3->(dbSetOrder(1))
	NE3->(DBGoTop())

	While NE3->(!EoF()) 
		if NE3->NE3_ATIVO == "1"		
			toolClass = agdDTAToolsEnum():getById(RTRIM(NE3->NE3_TOOL))
			if(!Empty(toolClass))
				jTools := JsonObject():New()
				jTools["name"       ] := RTRIM(NE3->NE3_TOOL)
				jTools["description"] := &(toolClass + ":getDescription()")
				jTools["strict"     ] := .f.
				jTools["rules"]       := &(toolClass + ":getRules()")
				jTools["parameters" ] := &(toolClass + ":getParameters()")
				jTools["embedding"  ] := {}
				jTools["score"      ] := 0
				aAdd(aTools, jTools)
			endif
		endif
		NE3->(DBSkip())	
	EndDo
	
	
	jTools  := Nil
	jParams := Nil
	FreeObj(jEmbedd)
Return aTools
