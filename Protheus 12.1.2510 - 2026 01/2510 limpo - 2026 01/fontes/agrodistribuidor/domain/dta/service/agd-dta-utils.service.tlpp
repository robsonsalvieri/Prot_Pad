#Include "agd.dta.utils.service.ch"
#Include "TOTVS.CH"
#Include "tlpp-core.th"

#DEFINE DTA_SESSION_PREFIX "DTA_PARAMETERS_"
#DEFINE DTA_CACHE_CHECK_UPDATE 300

using namespace agd.toolsDTAService
using namespace agd.dtaTokenService

/*/{Protheus.doc} agdDTAUtilsService
Utilitários para uso do DTA
@type class
@version 12 
@author jean.schulze
@since 12/8/2025
/*/
Class agdDTAUtilsService
				  	
	Static Method getEmbedding(cContent as Character) as Json 
	Static Method getEmbeddingVector(jEmbedding as Json) as Array 
	Static Method getLanguageName() as Character 
	Static Method getLoopLimit() as Numeric 
	Static Method getMaxToolCalls() as Numeric
	Static Method getGlobalParameters() as Json
	Static Method getCodCliente(cSearch as Character) as Array
	Static Method getGlobalSession() as Character
	Static Method getParameter(cParameter as Character) as Variant
	Static Method getParametersFromTable() as Json 
	Static Method getToolsCatalog()
	
	
	Static Method setChatToolsSteps(aToolsHistory as Array) as Array 
	Static Method setSuggestions(jSuggestions as Json, cFunction as Character, aSuggestions as Array) 
	Static Method setToolError(nCode as Numeric, cMessage as Character, cDetailedMessage as Character) as Array

	Static Method calculateVectorSimilarity(aVectorA as Array, aVectorB as Array) as Numeric 
	Static Method callToolFunction(cFunction as Character, cParams as Character, jSuggestions as Json) as Array 
	
	Static Method decodeUTF8(cText as Character) as Character 
	Static Method encodeUTF8(cText as Character) as Character 

	Static Method convertFieldsToGlossary(cFunction as Character, cData as Character) as Character 
	Static Method validateHTTPResponse(cAnswer as Character, cMethod as Character, cSendMessage as Character)

	Static Method clearGlobalCache() /*Limpeza de Variavel de sessao*/
	Static Method hasToken() as Logical
	Static Method canUseConversation(cMessage as Character) as Logical
	Static Method hasParametersByTool() as Logical /*Verifica se algum parâmetro foi passado para a ferramenta*/

EndClass

/*/{Protheus.doc} getLanguageName
Retorna o nome do idioma configurado no Protheus
@type method
@version 12 
@author jean.schulze
@since 12/8/2025
@return character, idioma
/*/
Method getLanguageName() as Character Class agdDTAUtilsService
	Local cLanguage     := "" as Character
	Local cNameLanguage := "" as Character

	cLanguage := FwRetIdiom()
	If cLanguage == "es"
		cNameLanguage := STR0001 //STR0001 //"Espanhol"
	ElseIf cLanguage == "en"
		cNameLanguage := STR0002 //STR0002 //"Inglês"
	ElseIf cLanguage == "ru"
		cNameLanguage := STR0003 //STR0003 //"Russo"
	ElseIf cLanguage == "pt-br"
		cNameLanguage := STR0004 //"Português do Brasil" //"Português"
	ElseIf cLanguage == "pt-pt"
		cNameLanguage := STR0005 //"Português de Portugal" //"Português Portugal"
	EndIf
Return cNameLanguage

/*/{Protheus.doc} getEmbedding
Retorna o embedding para o conteúdo informado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cContent, character, conteudo
@return json, embedding
/*/
Method getEmbedding(cContent as Character) as Json Class agdDTAUtilsService
	Local cAnswer    := ""                 as Character
	Local jEmbedding := JsonObject():New() as Json

	cAnswer := agdDTAProxyService():postDTAEmbedding(cContent)
	jEmbedding:FromJson(cAnswer)
Return jEmbedding

/*/{Protheus.doc} getEmbeddingVector
Retorna o vetor de embedding do JSON informado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param jEmbedding, json, embedding_json
@return array, embedding_vector
/*/
Method getEmbeddingVector(jEmbedding as Json) as Array Class agdDTAUtilsService
	Local aVector := {} as Array

	aVector := jEmbedding["data"][1]["embedding"]
Return aVector

/*/{Protheus.doc} getToolsCatalog
Retorna a lista de tools ativas para o DTA
@type method
@version 12
@author jean.schulze
@since 12/10/2025
@return variant, lista de tools
/*/
Method getToolsCatalog()  Class agdDTAUtilsService
Return agdToolsDTAService():getToolsCatalog()

/*/{Protheus.doc} getParametersFromTable
Retorna os parâmetros do DTA em JSON
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return json, parametros
/*/
Method getParametersFromTable() as Json Class agdDTAUtilsService
	Local aDefaultParameters := Nil as Json
	Local oToken             := agdDTATokenService():new()
	
	aDefaultParameters := JsonObject():New()
	aDefaultParameters["token"] =  Alltrim(oToken:descriptografarToken())
	aDefaultParameters["quantity_tools"] = 10
	aDefaultParameters["loop_limit"] = 3
	aDefaultParameters["max_tool_calls"] = 20
	aDefaultParameters["model_chat"] = "gpt-4.1-mini"
	aDefaultParameters["model_embedding"] = "text-embedding-3-small"
	aDefaultParameters["model_temperature"] = 0.5
	aDefaultParameters["proxy_url"] = "https://proxy.dta.totvs.ai/"
	aDefaultParameters["endpoint_chat"] = "v1/chat/completions"
	aDefaultParameters["endpoint_embedding"] = "v1/embeddings"

Return aDefaultParameters

/*/{Protheus.doc} getParameter
Retorna o valor do parâmetro informado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cParameter, character, parametro
@return variant, valor
/*/
Method getParameter(cParameter as Character) as Variant Class agdDTAUtilsService
	Local jParameters := Nil as Json
	Local xValue      := Nil as Variant

	jParameters := agdDTAUtilsService():getGlobalParameters()
	If jParameters:hasProperty(cParameter)
		xValue := jParameters[cParameter]
	EndIf
	FreeObj(jParameters)
Return xValue

/*/{Protheus.doc} getCodCliente
Retorna a existencia de um cliente
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cSearch, character, codigo do cliente
@return array, array de codigos
/*/
Method getCodCliente(cSearch as Character) as Array Class agdDTAUtilsService
	Local cAlias       := ""  as Character
	Local aRet         := {}  as Array
	
	dbSelectArea('SA1')
	SA1->(dbSetOrder(1)) 
	If SA1->(dbSeek(FWxFilial("SA1")+cSearch))
		aAdd(aRet, SA1->A1_COD)		
	EndIf

	if len(aRet) == 0		
		cAlias := GetNextAlias()

		cQuery := " SELECT A1_COD "
		cQuery += " FROM "+ RetSQLName("SA1") +" SA1 "
		cQuery += " WHERE SA1.A1_FILIAL = ? "
		cQuery += "   AND SA1.D_E_L_E_T_ = ' ' "
		cQuery += "   AND (SA1.A1_COD LIKE ? OR SA1.A1_NOME LIKE ?) "
		cQuery += " GROUP BY A1_COD "

		cQuery := ChangeQuery( cQuery )

		oStatement := FWPreparedStatement():New()
		oStatement:SetQuery(cQuery)
		oStatement:SetString( 	1, FWxFilial('SA1'))
		oStatement:SetString( 	2, Upper(cSearch))
		oStatement:SetString( 	3, Upper("%"+cSearch+"%"))
		cQuery := oStatement:GetFixQuery()
		dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAlias,.T.,.T.)

		While (cAlias)->(!EoF())
			aAdd(aRet, A1_COD)
			(cAlias)->(dbSkip())
		End
		
		(cAlias)->(dbCloseArea())
	EndIf

return aRet


/*/{Protheus.doc} getLoopLimit
Retorna o limite de loops configurado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return numeric, loops
/*/
Method getLoopLimit() as Numeric Class agdDTAUtilsService
Return agdDTAUtilsService():getParameter("loop_limit")

/*/{Protheus.doc} getMaxToolCalls
Retorna o número máximo de chamadas de ferramentas configurado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return numeric, max_tool_calls
/*/
Method getMaxToolCalls() as Numeric Class agdDTAUtilsService
Return agdDTAUtilsService():getParameter("max_tool_calls")

/*/{Protheus.doc} getGlobalParameters
Retorna os parâmetros globais do DTA em JSON
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return json, parametros_globais
/*/
Method getGlobalParameters() as Json Class agdDTAUtilsService
	Local cSessionId  := ""  as Character
	Local jParameters := Nil as Json
	Local lRet        := .T. as Logical

	cSessionId := agdDTAUtilsService():getGlobalSession()
	lRet       := GetGlbVars(cSessionId, @jParameters)
	If lRet
		If jParameters["created_at"] > Seconds() .Or. Seconds() - jParameters["created_at"] >= DTA_CACHE_CHECK_UPDATE
			lRet := .F.
			FreeObj(jParameters)
		EndIf
	EndIf
	
	If !lRet
		jParameters := agdDTAUtilsService():getParametersFromTable()
		jParameters["created_at"] := Seconds()
		PutGlbVars(cSessionId, jParameters)
	EndIf
Return jParameters

/*/{Protheus.doc} getGlobalSession
Retorna o nome da variável de sessão para armazenamento dos parâmetros do DTA
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return character, session_name
/*/
Method getGlobalSession() as Character Class agdDTAUtilsService
	Local cSessionId := "" as Character

	cSessionId := DTA_SESSION_PREFIX + cFilAnt
Return cSessionId

/*/{Protheus.doc} clearGlobalCache
Limpa o cache global
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return variant, nil
/*/
Method clearGlobalCache() Class agdDTAUtilsService
	ClearGlbValue(agdDTAUtilsService():getGlobalSession())
Return nil

/*/{Protheus.doc} convertFieldsToGlossary
Converte os campos técnicos para o glossário definido
@type function
@version 12
@author jean.schulze
@since 12/8/2025
@param cFunction, character, funcao
@param cData, character, dados a serem convertidos
@return variant, dados convertidos
/*/
Method convertFieldsToGlossary(cFunction as Character, cData as Character) as Character Class agdDTAUtilsService
	Local aGlossary := {} as Array
	Local nIndex    := 0  as Numeric

	aGlossary := &(agdDTAToolsEnum():getById(Alltrim(cFunction)) + ":getGlossary()")
	For nIndex := 1 To Len(aGlossary)
		cData := StrTran(cData, '"'+aGlossary[nIndex]["property"]+'"', '"'+aGlossary[nIndex]["description"]+'"')
		cData := StrTran(cData, "'"+aGlossary[nIndex]["property"]+"'", aGlossary[nIndex]["description"])
	Next nIndex
	FwFreeArray(aGlossary)
Return cData

/*/{Protheus.doc} setSuggestions
Informa sugestões para a função informada
@type function
@version 12
@author jean.schulze
@since 12/8/2025
@return variant, nil
/*/
Method setSuggestions(jSuggestions as Json, cFunction as Character, aSuggestions as Array) Class agdDTAUtilsService
	If !jSuggestions:hasProperty(cFunction)
		If !Empty(aSuggestions)
			jSuggestions[cFunction] := aSuggestions
		EndIf
	EndIf
Return

/*/{Protheus.doc} setToolError
Cria um retorno de erro para a ferramenta
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param nCode, numeric, codigo de erro
@param cMessage, character, mensagem de erro
@param cDetailedMessage, character, mensagem detalhada de erro
@return array, retorno da ferramenta
/*/
Method setToolError(nCode as Numeric, cMessage as Character, cDetailedMessage as Character) as Array Class agdDTAUtilsService
	Local aAPIReturn := {.F., Nil, nCode}  as Array
	Local jMessage   := JsonObject():New() as Json

	Default cDetailedMessage := cMessage

	jMessage["code"           ] := nCode
	jMessage["message"        ] := cMessage
	jMessage["detailedMessage"] := cDetailedMessage
	
	aAPIReturn[2] := jMessage
	jMessage := Nil
Return aAPIReturn

/*/{Protheus.doc} setChatToolsSteps
Informa os parametros que serão utilizados nas chamadas de ferramentas
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param aToolsHistory, array, json
@return array, params
/*/
Method setChatToolsSteps(aToolsHistory as Array) as Array Class agdDTAUtilsService
	Local aSteps           := {} as Array
	Local jArguments       := Nil as Json
	Local jToolCall        := Nil as Json
	Local nIndToolsHistory := 0 as Numeric
	Local nIndCallsHistory := 0 as Numeric

	For nIndToolsHistory := 1 To Len(aToolsHistory)
		If aToolsHistory[nIndToolsHistory]["role"] == "assistant"
			For nIndCallsHistory := 1 To Len(aToolsHistory[nIndToolsHistory]["tool_calls"])
				jToolCall  := JsonObject():New()
				jArguments := JsonObject():New()
				jArguments:FromJson(aToolsHistory[nIndToolsHistory]["tool_calls"][nIndCallsHistory]["function"]["arguments"])
				jToolCall["name"] := aToolsHistory[nIndToolsHistory]["tool_calls"][nIndCallsHistory]["function"]["name"]
				jToolCall["args"] := jArguments
				aAdd(aSteps, jToolCall)
			Next nIndCallsHistory
		EndIf
	Next nIndToolsHistory
	jArguments := Nil
	jToolCall  := Nil
Return aSteps

/*/{Protheus.doc} callToolFunction
Execução da função da ferramenta informada
@type function
@version 12
@author jean.schulze
@since 12/8/2025
@param cFunction, character, nome da função da ferramenta
@param cParams, character, parâmetros da função em formato JSON
@param jSuggestions, Json, sugestões para a função
@return variant, retorno da tool
/*/
Method callToolFunction(cFunction as Character, cParams as Character, jSuggestions as Json) as Array Class agdDTAUtilsService
	Local aReturn       := Array(2) as Array
	Local jParam        := JsonObject():New()  as Json
	Local oService      := agdToolsDTAService():New()
	Local cToolClass    := "" as Character
	Local aToolReturn   := Nil as Array

	oService:getById(cFunction)	
	
	If oService:isSuccess() 
		cToolClass := agdDTAToolsEnum():getById(Alltrim(cFunction))
		If !empty(cToolClass) .and. oService:hasUserToolAuthorization(cFunction)
			jParam:FromJson(cParams)

			aToolReturn := &(cToolClass + ":apply(jParam)")
		Else
			aToolReturn := agdDTAUtilsService():setToolError(401, I18N(STR0012, {RTrim(cFunction)})) //"Usuário não possui permissão para executar a ferramenta '#1[FERRAMENTA]#'."
		EndIf
	Else
		aToolReturn := agdDTAUtilsService():setToolError(404, I18N(STR0011, {RTrim(cFunction)})) //"A ferramenta '#1[FERRAMENTA]#' não existe. Utilize apenas as ferramentas disponibilizadas na requisição."
	EndIf

	If aToolReturn[1]
		aReturn[1] := aToolReturn[2]:toJson()
		aReturn[2] := agdDTAUtilsService():convertFieldsToGlossary(cFunction, aReturn[1])
		agdDTAUtilsService():setSuggestions(@jSuggestions, cFunction, aToolReturn[2]["suggestions"])
	Else
		aReturn[1] := ""
		aReturn[2] := aToolReturn[2]:toJson()
	EndIf

	FreeObj(jParam)
	FreeObj(oService)
	FwFreeArray(aToolReturn)
Return aReturn

/*/{Protheus.doc} calculateVectorSimilarity
Calcula a similaridade entre dois vetores
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param aVectorA, array, vetor A
@param aVectorB, array, vetor B
@return numeric, similaridade
/*/
Method calculateVectorSimilarity(aVectorA as Array, aVectorB as Array) as Numeric Class agdDTAUtilsService
	Local nIndex := 0 as Numeric
	Local nTotal := 0 as Numeric

	For nIndex := 1 To agdDTAProxyService():getVectorEmbeddingSize()
		nTotal := nTotal + (aVectorA[nIndex] * aVectorB[nIndex])
	Next nIndex
	nTotal := nTotal * 100
Return nTotal

/*/{Protheus.doc} validateHTTPResponse
Validação do Response HTTP
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cAnswer, character, pergunta
@param cMethod, character, método HTTP
@param cSendMessage, character, messagem
@return variant, nil
/*/
Method validateHTTPResponse(cAnswer as Character, cMethod as Character, cSendMessage as Character) Class agdDTAUtilsService
	Local cStatusError := "" as Character
	Local nStatus      := 0  as Numeric

	nStatus := HTTPGetStatus(@cStatusError, .F.)
	If !(nStatus >= 200 .And. nStatus <= 299)
		cAnswer := Iif(Empty(cAnswer), "", cAnswer)
		LogMsg(cMethod, 14, 3, 1, "", "", "["+cMethod+STR0006 + CHR(10) + CHR(10) +; //"] Erro ao executar requisição."
		                                 STR0007 + cValToChar(nStatus) + "]. " + CHR(10) + CHR(10) +; //"Status    : ["
		                                 STR0008 + cSendMessage + "]." + CHR(10) + CHR(10) +; //"Requisição: ["
		                                 STR0009 + cAnswer + "].") //"Retorno   : ["
		UserException(STR0010) //"Ocorreram erros ao executar a requisição. Contacte o administrador do sistema." //"Erros ao exeuctar a requisição"
	EndIf
Return

/*/{Protheus.doc} decodeUTF8
Decodifica um texto em UTF-8 para CP1252
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cText, character, texto em UTF-8
@return character, texto em CP1252
/*/
Method decodeUTF8(cText as Character) as Character Class agdDTAUtilsService
	Local oUnicode := tUnicode():New()  	
	oUnicode:ConvertEncoding(@cText, "utf-8", "cp1252")
	FreeObj(oUnicode)
Return cText

/*/{Protheus.doc} encodeUTF8
Codifica um texto de CP1252 para UTF-8
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cText, character, texto em CP1252
@return character, texto em UTF-8
/*/
Method encodeUTF8(cText as Character) as Character Class agdDTAUtilsService
	Local oUnicode := tUnicode():New()  	
	oUnicode:ConvertEncoding(@cText, "cp1252", "utf-8")
	FreeObj(oUnicode)
Return cText

/*/{Protheus.doc} canUseConversation
Retorna se o DTA está disponivel para o o usuário.
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@param cMessage, character, mensagem de retorno
@return logical, indica se pode usar a conversa
/*/
Method canUseConversation(cMessage as Character) as Logical Class agdDTAUtilsService
	/*If _lCanUseConversation == .F.
		_lCanUseConversation := .T.
		//verifica se tem a tabela do DTA
		//verifica se tem ao menos uma ferramenta cadastrada
		//verifica se tem permissão para ao menos uma ferramenta


	EndIf*/
Return .t.

/*/{Protheus.doc} hasToken
Verifica se o token do DTA está configurado
@type method
@version 12
@author jean.schulze
@since 12/8/2025
@return logical, indica se o token está configurado
/*/
Method hasToken() as Logical Class agdDTAUtilsService
	Local jParameters := Nil as Json
	Local lHasToken   := .F. as Logical

	jParameters := agdDTAUtilsService():getGlobalParameters()
	If !Empty(jParameters["token"])
		lHasToken := .T.
	EndIf
	FreeObj(jParameters)

Return lHasToken

/*/{Protheus.doc} hasParametersByTool
Retorna se foram passados algum parâmetros para a ferramenta
@type method
@version 12
@author jean.schulze
@since 1/9/2026
@param aParams, array, lista de parametros possiveis
@param jFilters, json, json de filtro da tool
@return logical, indica se tem parametros
/*/
Method hasParametersByTool(aParams as array, jFilters as Json ) as Logical Class agdDTAUtilsService
	Local nX as numeric

	For nX:= 1 to Len(aParams)
		if jFilters:HasProperty(aParams[nX]) .and. !Empty(jFilters[aParams[nX]])
			Return .T.
		EndIf
	Next
	

Return .f.
