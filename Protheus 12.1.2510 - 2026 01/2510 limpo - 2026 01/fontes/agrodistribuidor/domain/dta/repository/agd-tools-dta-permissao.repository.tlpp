#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"

namespace agd.toolsDTAPermissaoRepository
using namespace agd.repositoryUtils


/*/{Protheus.doc} agdToolsDTAPermissaoRepository 
Repositório de Tools do DTA
@type class
@version P12.1.2610
@author lindembergson.pacheco
@since 01/10/2025
/*/
class agdToolsDTAPermissaoRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository	As Object

	public Method New()
	public Method save()
	public Method getFields()
	public Method hasByUsuario() as Logical
	public Method hasGrupoByUsuario() as Logical

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@param pcChaveIDRepository, character, ID
@return variant, this
/*/
Method New(pcChaveIDRepository as character) Class agdToolsDTAPermissaoRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NE5', Self:getFields())
Return Self

/*/{Protheus.doc} save
Salvar via MVC
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@param jPayload, json, fields
@param lUpdate, logical, insercao/atualizacao
@return variant, result
/*/
method save(jPayload as json, lUpdate as logical) class agdToolsDTAPermissaoRepository
	Local oModel           as object
	Local aFieldsCabecalho := Self:getFields()
	
	Default lUpdate := .F.

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA090
		oModel := FwLoadModel("AGDP095")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()
	
		if Self:isSuccess()

			// Carregamento do cabeçalho
			aFields := Self:jsonToArrayFields(jPayload, aFieldsCabecalho)
			Self:setValueModel(oModel:GetModel("AGDP095_NE5"), aFields)
			    
			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDP095_NE5", "NE5_TOOL"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif			

		endif

		oModel:DeActivate()
		oModel:Destroy()
		FWFreeObj(oModel)
	endif
Return

/*/{Protheus.doc} getFields
Lista de Campos
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@return array, lista de campos 
/*/
Method getFields() Class agdToolsDTAPermissaoRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'    , 'NE5_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'    , 'NE5_TOOL', 'C', .T.})
	AAdd(aArrayFields, {'grupo'     , 'NE5_GRUPO',  'C', .T.})
	AAdd(aArrayFields, {'usuario'   , 'NE5_CODUSU', 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} hasByUsuario
Retorna se o usuário tem permissão para usar a tool
@type method
@version 12
@author jean.schulze
@since 1/5/2026
@param cFuncao, character, tool
@param cUsuario, character, usuario
@return variant, boolean, possui permissão
/*/
Method hasByUsuario(cFuncao as Character, cUsuario as Character) as logical Class agdToolsDTAPermissaoRepository
	Local cQuery      as Character

	cQuery := " SELECT NE5_CODUSU "
	cQuery += " FROM " + RetSqlName( "NE5" ) + "  NE5 "
	cQuery += " WHERE NE5.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND NE5.NE5_FILIAL = '" + xFilial('NE5')  + "' "
	cQuery += " 	AND NE5.NE5_TOOL   = '" + cFuncao  + "' "
	cQuery += " 	AND NE5.NE5_CODUSU = '" + cUsuario  + "' "

	if len(Self:freeQueryToArray(cQuery, {"NE5_CODUSU"})) > 0
		return .T.		
	endif

Return .f.

/*/{Protheus.doc} hasGrupoByUsuario
Retorna se o usuário está em um grupo o qual tem permissão para usar a tool
@type method
@version 12
@author jean.schulze
@since 1/5/2026
@param cFuncao, character, tool
@param cUsuario, character, usuario
@return variant, boolean, possui permissão
/*/
Method hasGrupoByUsuario(cFuncao as Character,cUsuario as Character) as logical Class agdToolsDTAPermissaoRepository
	Local cQuery      as Character
	Local aGruposTool as Array
	Local nX          as Numeric

	cQuery := " SELECT NE5_GRUPO "
	cQuery += " FROM " + RetSqlName( "NE5" ) + "  NE5 "
	cQuery += " WHERE NE5.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND NE5.NE5_FILIAL = '" + xFilial('NE5')  + "' "
	cQuery += " 	AND NE5.NE5_TOOL = '" + cFuncao  + "' "

	aGruposTool := Self:freeQueryToArray(cQuery, {"NE5_GRUPO"})
	if len(aGruposTool) > 0
		aGruposUsuario := FWSFUsrGrps(cUsuario)
		For nX:= 1 to Len(aGruposTool)
			If  aScan(aGruposUsuario,{ |x| Alltrim(x)==aGruposTool[nX]['NE5_GRUPO']}) > 0
				return .T.
			EndIf
		Next	
	EndIf

return .F.



