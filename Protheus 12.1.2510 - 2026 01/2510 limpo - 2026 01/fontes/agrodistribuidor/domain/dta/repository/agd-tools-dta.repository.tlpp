#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"

namespace agd.toolsDTARepository
using namespace agd.repositoryUtils


/*/{Protheus.doc} agdToolsDTARepository
Repositório de Tools do DTA
@type class
@version P12.1.2610
@author lindembergson.pacheco
@since 01/10/2025
/*/
class agdToolsDTARepository FROM agdRepositoryUtils
	public Data cChaveIDRepository	As Object

	public Method New()
	public Method save()
	public Method updateRepository()
	public Method getFields()
	public Method getFilterById() as Array
	
endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@param pcChaveIDRepository, character, ID
@return variant, this
/*/
Method New(pcChaveIDRepository as character) Class agdToolsDTARepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NE3', Self:getFields())
Return Self


/*/{Protheus.doc} save
Salvar via MVC
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@param jPayload, json, fields
@param lUpdate, logical, insercao/atualizacao
@return variant, result
/*/
method save(jPayload as json, lUpdate as logical) class agdToolsDTARepository
	Local oModel           as object
	Local aFieldsCabecalho := Self:getFields()
	
	Default lUpdate := .F.

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA090
		oModel := FwLoadModel("AGDP090")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()
	
		if Self:isSuccess()

			// Carregamento do cabeçalho
			aFields := Self:jsonToArrayFields(jPayload, aFieldsCabecalho)
			Self:setValueModel(oModel:GetModel("AGDP090_NE3"), aFields)
			    
			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDP090_NE3", "NE3_TOOL"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif			

		endif

		oModel:DeActivate()
		oModel:Destroy()
		FWFreeObj(oModel)
	endif
Return

/*/{Protheus.doc} updateRepository
Atualiza Repositório
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdToolsDTARepository
	Local nField		as Numeric
	Local aArea		:= GetArea()

	if Self:existsById(Self:cChaveIDRepository)

		RecLock("NE3",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NE3->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NE3->(MsUnlock())

		Self:setSuccess(Self:cChaveIDRepository)

	endif

	RestArea(aArea)

return nil
/*/{Protheus.doc} getFields
Lista de Campos
@type method
@version 12
@author jean.schulze
@since 06/11/2025
@return array, lista de campos 
/*/
Method getFields() Class agdToolsDTARepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'         , 'NE3_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'         , 'NE3_TOOL'  , 'C', .T.})
	AAdd(aArrayFields, {'ativo'          , 'NE3_ATIVO',  'C', .T.})
	AAdd(aArrayFields, {'data'           , 'NE3_DATA',   'D', .T.})
	AAdd(aArrayFields, {'descricaoCurta' , 'NE3_DESSHT', 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigo, character, codigo Negocio
@param cVersao, character, versao negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigo) as Array Class agdToolsDTARepository
    Local aFilterId := {}

	AAdd(aFilterId, {'NE3_TOOL', cCodigo}) 

return aFilterId
