#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} agdDTAToolsBuilder
Builder de Tools do DTA
@type class
@version 12
@author jean.schulze
@since 13/11/2025
/*/
Class agdDTAToolsBuilder
	Private Data jTool         as Json
	Private Data nIndExamples  as Numeric

	Public Method New() CONSTRUCTOR

	Public Method setDescription(cDescription as Character)
	Public Method setShortDescription(cShortDescription as Character)
	Public Method setName(cName as Character)
	Public Method setRules(cRules as Character)

	Public Method addGlossary(cProperty as Character, cDescription as Character)
	Public Method addParameter(cName as Character, xType as Variant, cDescription as Character, lRequired as Logical, aEnums as Array)
	Public Method addExample(cExample as Character)

	Public Method build() as Json
	
EndClass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@return variant, nil
/*/
Method New() Class agdDTAToolsBuilder
	Self:nIndExamples  := 0

	Self:jTool := JsonObject():New()
	Self:jTool["name"            ] := ""
	Self:jTool["description"     ] := ""
	Self:jTool["shortDescription"] := ""
	Self:jTool["rules"           ] := ""
	Self:jTool["parameters"      ] := JsonObject():New()
	Self:jTool["glossary"        ] := {}
	Self:jTool["examples"        ] := {}
	Self:jTool["embedding"       ] := ""

Return Self


/*/{Protheus.doc} setName
Informa o nome
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cName, character, nome
@return variant, nil
/*/
Method setName(cName as Character) Class agdDTAToolsBuilder
	Self:jTool["name"] := cName
Return


/*/{Protheus.doc} setDescription
Informa a descricao
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cDescription, character, descricao
@return variant, nil
/*/
Method setDescription(cDescription as Character) Class agdDTAToolsBuilder
	Self:jTool["description"] := cDescription
Return

/*/{Protheus.doc} setShortDescription
Informa a descricao curta
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cShortDescription, character, descricao curta
@return variant, nil
/*/
Method setShortDescription(cShortDescription as Character) Class agdDTAToolsBuilder
	Self:jTool["shortDescription"] := cShortDescription
Return

/*/{Protheus.doc} setRules
Informa as regras de execução
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cRules, character, regras
@return variant, nil
/*/
Method setRules(cRules as Character) Class agdDTAToolsBuilder
	Self:jTool["rules"] := cRules
Return

/*/{Protheus.doc} addParameter
Informa os parametros
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cName, character, nome
@param xType, variant, tipo
@param cDescription, character, desc
@param lRequired, logical, obrigatorio
@param aEnums, array, opcoes
@return variant, nil
/*/
Method addParameter(cName as Character, xType as Variant, cDescription as Character, lRequired as Logical, aEnums as Array) Class agdDTAToolsBuilder
	Default aEnums := {}
	
	If !Self:jTool["parameters"]:hasProperty("type")
		Self:jTool["parameters"]["type"      ] := "object"
		Self:jTool["parameters"]["properties"] := JsonObject():New()
		Self:jTool["parameters"]["required"  ] := {}
	EndIf

	Self:jTool["parameters"]["properties"][cName] := JsonObject():New()
	Self:jTool["parameters"]["properties"][cName]["type"       ] := xType
	Self:jTool["parameters"]["properties"][cName]["description"] := cDescription
	
	If lRequired
		aAdd(Self:jTool["parameters"]["required"], cName)
	EndIf
	
	If Len(aEnums) > 0
		Self:jTool["parameters"]["properties"][cName]["enum"] := aEnums
	EndIf
Return

/*/{Protheus.doc} addGlossary
Informa o glossario
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cProperty, character, propriedade
@param cDescription, character, descricao
@return variant, nil
/*/
Method addGlossary(cProperty as Character, cDescription as Character) Class agdDTAToolsBuilder
	Local jGlossary := JsonObject():New() as Json

	jGlossary["property"   ] := cProperty
	jGlossary["description"] := cDescription
	aAdd(Self:jTool["glossary"], jGlossary)
Return

/*/{Protheus.doc} addExample
Informa os Exemplos
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param cExample, character, exemplo
@return variant, nil
/*/
Method addExample(cExample as Character) Class agdDTAToolsBuilder
	Self:nIndExamples++
	aAdd(Self:jTool["examples"], {"sequence": StrZero(Self:nIndExamples, 2), "description": cExample})
Return

/*/{Protheus.doc} build
Builda o objeto final
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@return json, objeto de dados da tool
/*/
Method build() as Json Class agdDTAToolsBuilder	
Return Self:jTool
