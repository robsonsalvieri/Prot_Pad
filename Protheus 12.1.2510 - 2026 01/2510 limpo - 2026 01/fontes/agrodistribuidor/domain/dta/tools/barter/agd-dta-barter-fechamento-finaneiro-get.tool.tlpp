#INCLUDE 'totvs.ch'
#INCLUDE 'tlpp-core.th'
#Include "agd.dta.barter.fechamento.finaneiro.get.tool.ch"


/*/{Protheus.doc} agdDTABarterFechamentoFinaneiroGetTool
ToolTool do DTA para Fechamento Financeiro do Barter
@type class
@version 12
@author jean.schulze
@since 13/11/2025
/*/
Class agdDTABarterFechamentoFinaneiroGetTool 
	
	Static Method getSetup() as Json
	Static Method getParameters() as Json
	Static Method getRules() as Character 
	Static Method getDescription() as Character
	Static Method getDescriptionShort() as Character
	Static Method getExamples() as Array
	Static Method getGlossary() as Array

	Static Method apply(jFilters as Json) as Array
EndClass

/*/{Protheus.doc} getSetup
Dados para configuração da Tool
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@return json, dados de configuracao
/*/
Method getSetup() as Json Class agdDTABarterFechamentoFinaneiroGetTool
	Local cName         := ""  as Character
	Local oLoadTool     := Nil as Object

	cName             := "BFechamentoGET"

	oLoadTool := agdDTAToolsBuilder():New()
	oLoadTool:setName(cName)
	oLoadTool:setDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescription())
	oLoadTool:setShortDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescriptionShort()) 

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
Return jReturn

/*/{Protheus.doc} getDescription
Retorna a descrição completa da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
method getDescription()  as Character  Class agdDTABarterFechamentoFinaneiroGetTool
return STR0001 //"Fornece um resumo de informações sobre as fechamentos financeiros de negócios barter. O resumo de dados tem a lista de fechamento, com seu cliente, seus títulos financeiros e situação envolvidos."

/*/{Protheus.doc} getDescriptionShort
Retorna a descrição curta da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getDescriptionShort() as Character Class agdDTABarterFechamentoFinaneiroGetTool 
return STR0003 //"Detalhe de Fechamentos Financeiros do Barter"

/*/{Protheus.doc} getRules
Retorna as regras de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getRules()  as Character Class agdDTABarterFechamentoFinaneiroGetTool
return I18N(STR0002, { "customer", "bussines_code"})  //"Se o período não for informado, então solicite ao usuário que informe o argumento '#1[customer]#' ou '#1[bussines_code]#' antes de realizar a pesquisa."

/*/{Protheus.doc} getParameters
Retorna os parâmetros de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return json, params
/*/
Method getParameters() as Json Class agdDTABarterFechamentoFinaneiroGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addParameter("customer"      , "string" , STR0005 + ". \n " + STR0004, .F.)  //"O código/nome do cliente pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'R001', 'INJET', '000001', 'TORNO1', 'JOAO', 'ANDRE SILVA'."
	oLoadTool:addParameter("bussines_code" , "string" , STR0007 + ". \n " + STR0006, .F.)  //"O código do negócio barter pode conter somente números. Exemplo: '0000001', '000002', '023456'"
	oLoadTool:addParameter("status"        , "string" , STR0041, .F.)  //"O status do fechamento financeiro pode conter as situações: Pendente, Confirmado, Cancelado."
	oLoadTool:addParameter("payment_status", "string" , STR0042, .F.)  //"O status do titulo financeiro pode conter as situações: Aberto, Baixado"
	oLoadTool:addParameter("start_date"    , "string" , STR0008, .F.)  //"Data Inicial para buscar Fechamentos Finaceiros"
	oLoadTool:addParameter("end_date"      , "string" , STR0009, .F.)  //"Data Final para buscar Fechamentos Financeiros"
	 
	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
return jReturn["parameters"]

/*/{Protheus.doc} getExamples
Retorna os exemplos de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, exemplos
/*/
Method getExamples() as Array Class agdDTABarterFechamentoFinaneiroGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addExample(I18N(STR0010, {"customer"}))  //"Quais são os fechamentos finaceiros do cliente '#1[customer]#'?"
	oLoadTool:addExample(I18N(STR0011, {"bussines_code"}))  //"Quais são os fechamentos financeiros para o negócio/barter '#1[bussines_code]#'?"
	oLoadTool:addExample(I18N(STR0012, {"customer"}))  //"O cliente '#1[customer]#' possui fechamentos financeiros?"
	oLoadTool:addExample(I18N(STR0043, {"code"}))  //"Os documentos financeiros do fechamento financeiro '#1[code]# já foram baixados?"
	
	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['examples']

/*/{Protheus.doc} getGlossary
Retorna o glossário de termos da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, glossario
/*/
Method getGlossary() as Array Class agdDTABarterFechamentoFinaneiroGetTool
	oLoadTool := agdDTAToolsBuilder():New()
	
	oLoadTool:addGlossary("customer_code" , STR0014)  //"Código do Cliente"
	oLoadTool:addGlossary("custome_name"  , STR0015) //"Nome do Cliente"
	oLoadTool:addGlossary("bussines_code", STR0016)  //"Código da Negociação"
	oLoadTool:addGlossary("financial_closing_code", STR0017)  //"Código do Fechamento"
	oLoadTool:addGlossary("status", STR0018)  //"Situção do Fechamento"
	oLoadTool:addGlossary("date", STR0019)  //"Data do Fechamento"
	oLoadTool:addGlossary("amount_to_pay", STR0020)  //"Valor dos Títulos à Pagar"
	oLoadTool:addGlossary("amount_to_receive", STR0021)  //"Valor dos Títulos à Receber"
	oLoadTool:addGlossary("financial_closing_list", STR0022)  //"Lista de Fechamentos Financeiros"
	oLoadTool:addGlossary("financial_title_list", STR0023) //"Lista de Títulos do Fechamento Financeiro"
	oLoadTool:addGlossary("financial_title", STR0024)  //"Título Financeiro"
	oLoadTool:addGlossary("title_type", STR0025)  //"Tipo do Título - À Pagar ou À Receber"
	oLoadTool:addGlossary("title_amount", STR0026)  //"Valor do Título"
	oLoadTool:addGlossary("payment_due_date", STR0027)  //"Vencimento do Título Financeiro"
	oLoadTool:addGlossary("title_creation_date", STR0028)  //"Data de Criação do Título Financeiro"
	oLoadTool:addGlossary("status_payment", STR0044 )  //"Status do Pagamento do título"
	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['glossary']


/*/{Protheus.doc} apply
Metodo de Execução pelo DTA para retorno de dados
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param jFilters, json, filter
@return array, dados
/*/
Method apply(jFilters as Json) as Array Class agdDTABarterFechamentoFinaneiroGetTool
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cContext     := ""  as Character
	Local jData        := Nil as Json
	Local cFilter      := ""  as Character 
	Local nX
	Local nY

	If !agdDTAUtilsService():hasParametersByTool({"customer", "bussines_code", "start_date", "end_date"}, jFilters)
		Return agdDTAUtilsService():setToolError(400, I18N(STR0029, {"customer","bussines_code" }))  //"Para realizar a busca de fechamento finaceiros, o parâmetro '#1[PARAM]#' ou '#2[PARAM]#' deve ser informado."
	EndIf

	if jFilters:HasProperty('customer') .and. !Empty(jFilters["customer"])
		listClientes := agdDTAUtilsService():getCodCliente(Upper(jFilters["customer"]))
		if len(listClientes) == 0
			Return agdDTAUtilsService():setToolError(400, I18N(STR0030, {RTrim(jFilters["customer"]) }))  //"O cliente #1[customer]# não foi encontrado na base de dados."
		endif

		if len(listClientes) > 1
			Return agdDTAUtilsService():setToolError(400, I18N(STR0031+listClientes[1]+","+listClientes[2]+STR0032, {RTrim(jFilters["customer"]) }))  //"O cliente #1[customer]# está genérico, pois foram encontrados várias ocorrências do mesmo com código("
		endif	
		
		jFilters["codigoCliente"] := listClientes[1]
	endif	

	if jFilters:HasProperty('bussines_code') .and. !Empty(jFilters["bussines_code"])
		jFilters["codigoNegocio"] := Upper(jFilters["bussines_code"])
	endif

	if jFilters:HasProperty('start_date') .and. !Empty(jFilters["start_date"])
		jFilters["filter"] := "dtFechamento ge '" + jFilters["start_date"] + "'"
	endif

	if jFilters:HasProperty('end_date') .and. !Empty(jFilters["end_date"])
		if jFilters:HasProperty('filter') .and. !Empty(jFilters["filter"])
			cFilter := jFilters["filter"] + " and "  
		endif
		jFilters["filter"] := cFilter + "dtFechamento le '" + jFilters["end_date"] + "'"
	endif

	oRepository := agd.fechamentoFinanceiroRepository.agdFechamentoFinanceiroRepository():New()
	oRepository:find(1, 50, jFilters)
	
	jData  := JsonObject():New()
	jData["financial_closing_list"] := {}
	listaFechamento := oRepository:getResponse()['items']
	
	For nX:= 1 to Len(listaFechamento)

		oItemRepository := agd.tituloFinanceiroRepository.agdTituloFinanceiroRepository():New()
		oItemRepository:find(1, 1000, {'codigo':listaFechamento[nX]['codigo']})
		listaItens := oItemRepository:getResponse()['items']

		jDataLine  := JsonObject():New()
		jDataLine  := {'customer_code': listaFechamento[nX]['codigoCliente'],;
					   'custome_name': listaFechamento[nX]['descricaoCliente'],;
					   'bussines_code': listaFechamento[nX]['codigoNegocio'],;
					   'financial_closing_code': listaFechamento[nX]['codigo'],;
					   'status':X3Combo( "NEJ_STATUS", listaFechamento[nX]['status']),;
					   'date':listaFechamento[nX]['dtFechamento'],;
					   'amount_to_pay':listaFechamento[nX]['valorTotalPagar'],;
					   'amount_to_receive':listaFechamento[nX]['valorTotalReceber'] }
		
		jDataLine['financial_title_list'] := {}			   
		
		For nY:= 1 to Len(listaItens)
			aadd(jDataLine['financial_title_list'], {'financial_title': listaItens[nY]['numero'] + "/" +listaItens[nY]['parcela'],;
													 'title_type':IIF(listaItens[nY]['codigoModo'] == "1", STR0034, STR0033),; //'À RECEBER'
											 		 'title_amount':listaItens[nY]['valorLiquido'],; 
													 'title_creation_date':listaItens[nY]['dataEmissao'],; 
											 	     'payment_due_date':listaItens[nY]['dataVencimento'],;
													 'status_payment':IIF(alltrim(listaItens[nY]['statusTitulo']) == "A", STR0045, STR0046) })
		Next

		aadd(jData["financial_closing_list"], jDataLine)
	Next

	If Len(listaFechamento) > 0
		aAdd(aSuggestions, {"label": STR0035})  //"Quais desses fechamentos não foi conferido?"
		aAdd(aSuggestions, {"label": STR0036})  //"Quanto foi conciliado nos fechamentos finaceiros?"
		aAdd(aSuggestions, {"label": STR0047})  //"Quais títulos estão abertos?"
		cContext := I18N(STR0037, {"financial_closing_list"})  //"Os dados contidos em '#1[ARG_CODE]#' é uma lista dos ultimos 50 fechamentos financeriso conforme filtrado pelo cliente ou número de negócio barter."
		aReturn := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}
	Else
		if !Empty(jFilters["customer"]) .and. !Empty(jFilters["bussines_code"]) 
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0038, {RTrim(jFilters["customer"]), RTrim(jFilters["bussines_code"])}))  //"O cliente #1[customer]# e negócio #1[bussines_code]#  não possui fechamentos financeiros."
		elseif !Empty(jFilters["customer"]) 
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0039, {RTrim(jFilters["customer"])}))  //"O cliente #1[customer]# não possui fechamentos finaceiros."
		else
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0040, {RTrim(jFilters["bussines_code"])}))  //"O negócio #1[CODIGO]# não possui fechamentos finaceiros."
		endif	
	EndIf
	aSuggestions := Nil
	jData        := Nil
Return aReturn
