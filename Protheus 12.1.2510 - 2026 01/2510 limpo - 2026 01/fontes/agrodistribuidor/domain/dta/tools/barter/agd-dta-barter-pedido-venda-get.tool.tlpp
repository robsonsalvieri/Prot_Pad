#include 'tlpp-core.th'
#include 'totvs.ch'
#include 'agd.dta.barter.pedido.venda.get.tool.ch'

#define MAX_LIST_ITEMS 20

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool
Tool do DTA para Pedidos de venda com origem da negociação de barter
@type class
@version 12
@author jc.maldonado
@since 14/01/2026
/*/
class agdDTABarterPedidoVendaGetTool
	static method getSetup()            as json
	static method getParameters()       as json
	static method getRules()            as character
	static method getDescription()      as character
	static method getDescriptionShort() as character
	static method getExamples()         as array
	static method getGlossary()         as array
	static method apply()               as array
endclass

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getSetup
Retorna as configurações da tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return json, setup
/*/
method getSetup() as json class agdDTABarterPedidoVendaGetTool
	local oDTAToolsBuilder            as object
	local cName := 'BPedidosVendaGET' as character
	local jSetup                      as json

	oDTAToolsBuilder := agdDTAToolsBuilder():new()

	oDTAToolsBuilder:setName(cName)
	oDTAToolsBuilder:setDescription(agdDTABarterPedidoVendaGetTool():getDescription())
	oDTAToolsBuilder:setShortDescription(agdDTABarterPedidoVendaGetTool():getDescriptionShort())

	jSetup := oDTAToolsBuilder:build()

	FreeObj(oDTAToolsBuilder)
return jSetup

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getDescription
Retorna a descrição completa da tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return character, descrição completa
/*/
method getDescription() as character  class agdDTABarterPedidoVendaGetTool
return STR0001 //'Fornece um resumo de informações sobre os pedidos de venda gerados pelo negócio/negociação barter. O resumo de dados tem suas informações como valores, situação do pedido, produtos, notas fiscais/documento saída, titulos financeiros.'

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getDescriptionShort
Retorna a descrição curta da tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return character, descrição curta
/*/
method getDescriptionShort() as character class agdDTABarterPedidoVendaGetTool
return STR0002 //'Busca os pedidos de venda gerados pelo negócio/negociação barter.'

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getRules
Retorna as regras da tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return character, regras
/*/
method getRules() as character class agdDTABarterPedidoVendaGetTool
return I18N(STR0003, {'barter_code', 'order_code'})  //"O argumento '#1[ARG_NAME]#' ou '#2[ARG_NAME]#' precisam ser informados, solicite a informação antes de realizar a pesquisa."

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getParameters
Retorna os parâmetros disponíveis para a tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return json, parâmetros
/*/
method getParameters() as json class agdDTABarterPedidoVendaGetTool
	local oDTAToolsBuilder as object
	local jDTATool         as json
	local jParameters      as json

	oDTAToolsBuilder := agdDTAToolsBuilder():new()

	oDTAToolsBuilder:addParameter('barter_code', 'string', STR0004, .F.) //'Código Negócio/negociação barter'
	oDTAToolsBuilder:addParameter('order_code' , 'string', STR0005, .F.) //'Código Pedido de Venda'

	jDTATool    := oDTAToolsBuilder:build()
	jParameters := jDTATool['parameters']

	FreeObj(oDTAToolsBuilder)
	FreeObj(jDTATool)
return jParameters

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getExamples
Retorna exemplos de perguntas para a tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return array, exemplos
/*/
method getExamples() as array class agdDTABarterPedidoVendaGetTool
	local oDTAToolsBuilder as object
	local jDTATool         as json
	local aExamples        as array

	oDTAToolsBuilder := agdDTAToolsBuilder():new()

	oDTAToolsBuilder:addExample(I18N(STR0006, {'barter_code'})) //"Quais os pedidos de venda do negócio/negociação barter '#1[barter_code]#' ?"
	oDTAToolsBuilder:addExample(I18N(STR0007, {'barter_code'})) //"Existe pedido de venda para o negócio/negociação barter '#1[barter_code]#' ?"
	oDTAToolsBuilder:addExample(I18N(STR0008, {'barter_code'})) //"Liste os pedidos de venda ligados ao negócio/negociação barter '#1[barter_code]#' ?"

	oDTAToolsBuilder:addExample(I18N(STR0009, {'order_code'})) //"Quais os itens do pedido de venda '#1[order_code]#' ?"
	oDTAToolsBuilder:addExample(I18N(STR0010, {'order_code'})) //"Quais são as notas fiscais do pedido de venda '#1[order_code]#' ?"
	oDTAToolsBuilder:addExample(I18N(STR0011, {'order_code'})) //"Liste os títulos financeiros do pedido de venda  '#1[order_code]#' ?"

	jDTATool  := oDTAToolsBuilder:build()
	aExamples := jDTATool['examples']

	FreeObj(oDTAToolsBuilder)
return aExamples

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::getGlossary
Retorna o glossário das informações utilizadas pela tool
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@return array, glossário
/*/
method getGlossary() as array class agdDTABarterPedidoVendaGetTool
	local oDTAToolsBuilder as object
	local jDTATool         as json
	local aGlossary        as array

	oDTAToolsBuilder := agdDTAToolsBuilder():new()

	oDTAToolsBuilder:addGlossary('order_list'          , STR0012) //"Lista de pedidos de venda"
	oDTAToolsBuilder:addGlossary('order_code'          , STR0013) //"Código do pedido"
	oDTAToolsBuilder:addGlossary('order_type'          , STR0014) //"Tipo do pedido"
	oDTAToolsBuilder:addGlossary('order_status'        , STR0015) //"Status do pedido"
	oDTAToolsBuilder:addGlossary('order_customer_code' , STR0016) //"Código do cliente do pedido"
	oDTAToolsBuilder:addGlossary('order_customer_name' , STR0017) //"Nome do cliente do pedido"
	oDTAToolsBuilder:addGlossary('order_payment_method', STR0018) //"Condição de pagamento do pedido"
	oDTAToolsBuilder:addGlossary('order_items'         , STR0019) //"Itens do pedido"
	oDTAToolsBuilder:addGlossary('order_sales_invoice' , STR0020) //"Documentos de saída/nota fiscal do pedido"

	oDTAToolsBuilder:addGlossary('order_item_sequence'           , STR0021) //"Item sequencial do pedido"
	oDTAToolsBuilder:addGlossary('order_item_product_code'       , STR0022) //"Código do produto do pedido"
	oDTAToolsBuilder:addGlossary('order_item_description'        , STR0023) //"Descrição do item do pedido"
	oDTAToolsBuilder:addGlossary('order_item_quantity'           , STR0024) //"Quantidade do item do pedido"
	oDTAToolsBuilder:addGlossary('order_item_total_price'        , STR0025) //"Valor total do item do pedido"
	oDTAToolsBuilder:addGlossary('order_item_unit_price'         , STR0026) //"Valor unitário do item do pedido"
	oDTAToolsBuilder:addGlossary('order_item_discount_percentage', STR0027) //"Percentual de desconto do item do pedido"
	oDTAToolsBuilder:addGlossary('order_item_discount_amount'    , STR0028) //"Valor monetário do desconto do item do pedido"

	oDTAToolsBuilder:addGlossary('sales_invoice_number'              , STR0029) //"Número do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_series'              , STR0030) //"Série do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_date'                , STR0031) //"Data do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_gross_total'         , STR0032) //"Valor bruto do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_items'               , STR0033) //"Itens do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_receivable_documents', STR0034) //"Documentos/Títulos de cobrança, boletos, cheques, duplicatas..."

	oDTAToolsBuilder:addGlossary('sales_invoice_item_sequence'    , STR0035) //"Item sequencial do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_item_product_code', STR0036) //"Código do produto do documento de saída"
	oDTAToolsBuilder:addGlossary('sales_invoice_item_quantity'    , STR0037) //"Quantidade do item do documento de saída"

	oDTAToolsBuilder:addGlossary('receivable_document_code'       , STR0038) //"Número do documento de cobrança"
	oDTAToolsBuilder:addGlossary('receivable_document_prefix'     , STR0039) //"Prefixo do documento de cobrança"
	oDTAToolsBuilder:addGlossary('receivable_document_installment', STR0040) //"Número da parcela"
	oDTAToolsBuilder:addGlossary('receivable_document_amount'     , STR0041) //"Valor total da parcela do documento de cobrança"
	oDTAToolsBuilder:addGlossary('receivable_document_due_date'   , STR0042) //"Data de vencimento do documento de cobrança"
	oDTAToolsBuilder:addGlossary('receivable_document_type'       , STR0043) //"Tipo do documento de cobrança"

	jDTATool  := oDTAToolsBuilder:build()
	aGlossary := jDTATool['glossary']

	FWFreeObj(oDTAToolsBuilder)
return aGlossary

/*/{Protheus.doc} agdDTABarterPedidoVendaGetTool::apply
Método utilizado pelo DTA para obter os dados
@type method
@version 12
@author jc.maldonado
@since 14/01/2026
@param jFilters, json, filtros
@return array, dados retornados
/*/
method apply(jFilters as json) as array class agdDTABarterPedidoVendaGetTool
	local lExistBarterCode as logical
	local lExistOrderCode  as logical
	local aReturn          as array

	lExistBarterCode := jFilters:HasProperty('barter_code') .And. ! Empty(jFilters['barter_code'])
	lExistOrderCode  := jFilters:HasProperty('order_code' ) .And. ! Empty(jFilters['order_code'] )

	if ! lExistBarterCode .and. ! lExistOrderCode
		return agdDTAUtilsService():setToolError(400, I18N(STR0044, {'barter_code', 'order_code'})) //"Para realizar a busca de pedidos de venda, o parâmetro '#1[PARAM]#' ou '#2[PARAM]#' deve ser informado."
	endIf

	if lExistBarterCode
		aReturn := getDataByBarterCode(jFilters)
	else //lExistOrderCode
		aReturn := getDataByOrderCode(jFilters)
	endIf
return aReturn

/*/{Protheus.doc} getDataByBarterCode
Retorna os dados de acordo com o código da negociação barter
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param jFilters, json, filtros utilizados
@return array, dados da consulta
/*/
static function getDataByBarterCode(jFilters as json) as array
	local oNegocioRepository as object
	local oNegocioService    as object
	local aPedidosVenda      as array
	local cContext           as character
	local cCodigoPedido      as character
	local jData              as json
	local aSuggestions       as array

	oNegocioRepository := agd.negocioRepository.agdNegocioRepository():new()
	if ! oNegocioRepository:existsByCode(jFilters['barter_code'])
		FWFreeObj(oNegocioRepository)
		return agdDTAUtilsService():setToolError(400, STR0045) //"O negócio/negociação barter não existe."
	endIf
	FWFreeObj(oNegocioRepository)

	oNegocioService := agd.negocioService.agdNegocioService():new()
	if empty(aPedidosVenda := oNegocioService:getPedidosVenda(jFilters['barter_code']))
		FWFreeObj(oNegocioService)
		return agdDTAUtilsService():setToolError(400, STR0046) //"O negócio/negociação barter não possui pedidos de venda."
	endIf
	FWFreeObj(oNegocioService)

	jData := {'order_list': array(0)}
	aEval(aPedidosVenda, {|xIt| aAdd(jData['order_list'], getOrderForDTA(xIt))})

	cContext := I18N(STR0047 /*"Todos os pedidos do negócio/negociação barter com código #1[barter_code]#"*/, {jFilters['barter_code']})

	cCodigoPedido := jData['order_list'][1]['order_code']

	aSuggestions := {;
		{"label": I18N(STR0048 /*"Quais os itens do pedido #1[ORDER_CODE]# ?"*/               , {cCodigoPedido})},;
		{"label": I18N(STR0049 /*"Qual o numero da nota fiscal do pedido #1[ORDER_CODE]# ?"*/ , {cCodigoPedido})},;
		{"label": I18N(STR0050 /*"Quais os títulos financeiros do pedido #1[ORDER_CODE]# ?"*/ , {cCodigoPedido})};
		}
return {.t., {'context': cContext, 'data': jData, 'suggestions': aSuggestions},	200}

/*/{Protheus.doc} getDataByOrderCode
Retorna os dados de acordo com o código do pedido de venda
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param jFilters, json, filtros
@return array, dados da consulta
/*/
static function getDataByOrderCode(jFilters as json) as array
	local oPedidoVendaRepository as object
	local jPedidoVenda           as json
	local jData                  as json
	local cContext               as character
	local aSuggestions           as array

	jFilters['barter_code'] := posicione('NEC', 3, FWxFilial("NEC") + jFilters['order_code'], "NEC->NEC_CODBRT")
	if empty(jFilters['barter_code'])
		return agdDTAUtilsService():setToolError(400, STR0051) //"O pedido de venda informado não faz parte de um negócio barter."
	endif

	oPedidoVendaRepository := agd.pedidoVendaRepository.agdPedidoVendaRepository():New(.T. /*lIncludeOtherFields*/)
	oPedidoVendaRepository:find(1, 1, {'codigo': jFilters['order_code']})
	jPedidoVenda := oPedidoVendaRepository:getResponse()['items'][1]
	FWFreeObj(oPedidoVendaRepository)

	jData := getOrderForDTA(jPedidoVenda, .t. /*lShowFullData*/)

	cContext := I18N(STR0052, {jFilters['order_code'], jFilters['barter_code'], cValToChar(MAX_LIST_ITEMS)}) //"Informações do pedido de venda com código #1[order_code]# gerado pela negociação barter com código #2[BARTER_CODE]#. Todas as listas (itens, notas fiscais, títulos financeiros) estão limitadas a #3[NUMERO_ITENS]# itens cada."

	aSuggestions := {;
		{"label": STR0053 /*"Quais os itens do pedido?"*/              },;
		{"label": STR0054 /*"Qual o número da nota fiscal do pedido?"*/},;
		{"label": STR0055 /*"Quais os títulos financeiros do pedido?"*/};
		}
return {.t., {'context': cContext, 'data': jData, 'suggestions': aSuggestions},	200}

/*/{Protheus.doc} getOrderForDTA
Retorna o pedido de venda formatado para consumo do DTA
Obtem mais informações ao especificar o parametro lShowFullData como .t.
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param jPedidoVenda, json, pedido de venda
@param lShowFullData, logical, exibe os dados completos
@return json, pedido formatado
/*/
static function getOrderForDTA(jPedidoVenda as json, lShowFullData as logical) as json
	local jOrder as json

	default lShowFullData := .F.

	jOrder := {;
		'order_code'          : jPedidoVenda['codigo']                          ,;
		'order_customer_code' : rTrim(jPedidoVenda['codigoCliente'])            ,;
		'order_customer_name' : rTrim(jPedidoVenda['nomeCliente'])              ,;
		'order_type'          : jPedidoVenda['tipo']                            ,;
		'order_status'        : rTrim(jPedidoVenda['statusPedido'])             ,;
		'order_payment_method': rTrim(jPedidoVenda['descricaoCondicaoPagamento']);
		}

	if lShowFullData
		jOrder['order_items']         := getOrderItems(jPedidoVenda['codigo'])
		jOrder['order_sales_invoice'] := getSalesInvoiceFromOrder(jPedidoVenda['codigo'])
	endIf
return jOrder

/*/{Protheus.doc} getOrderItems
Retonar os itens do pedido formatado para o DTA
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param cCodigoPedido, character, código do pedido
@return array, itens do pedido
/*/
static function getOrderItems(cCodigoPedido as character) as array
	local oPedidoVendaItensRepository as object
	local aJsonItens                  as array
	local nX                          as numeric
	local aOrderItens := {}           as array

	oPedidoVendaItensRepository := agd.pedidoVendaItensRepository.agdPedidoVendaItensRepository():new()
	oPedidoVendaItensRepository:find(1, MAX_LIST_ITEMS, {'codigoPedido': cCodigoPedido})
	aJsonItens := oPedidoVendaItensRepository:getResponse()['items']
	FwFreeObj(oPedidoVendaItensRepository)

	for nX := 1 to len(aJsonItens)
		aAdd(aOrderItens,;
			{;
			'order_item_sequence'           : aJsonItens[nX]['item'               ],;
			'order_item_product_code'       : aJsonItens[nX]['codigoProduto'      ],;
			'order_item_description'        : aJsonItens[nX]['descricaoProduto'   ],;
			'order_item_quantity'           : aJsonItens[nX]['quantidadeVenda'    ],;
			'order_item_total_price'        : aJsonItens[nX]['valorTotal'         ],;
			'order_item_unit_price'         : aJsonItens[nX]['precoVenda'         ],;
			'order_item_discount_percentage': aJsonItens[nX]['descontoPorcentagem'],;
			'order_item_discount_amount'    : aJsonItens[nX]['descontoValor'      ];
			};
			)
	next nX
return aOrderItens

/*/{Protheus.doc} getSalesInvoiceFromOrder
Retorna os documentos de saída do pedido
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param cCodigoPedido, character, código do pedido
@return array, documentos de saída
/*/
static function getSalesInvoiceFromOrder(cCodigoPedido as character) as array
	local oRepositoryPedidoVenda     as object
	local aDocumentosSaidaPedido     as array
	local nX                         as numeric
	local jParametersDocumentoSaida  as json
	local oRepositoryDocumentosSaida as object
	local aSalesInvoices             as array
	local aOrderInvoices := {}       as array
	local nY                         as numeric

	oRepositoryPedidoVenda := agd.pedidoVendaRepository.agdPedidoVendaRepository():new()
	aDocumentosSaidaPedido := oRepositoryPedidoVenda:getDocumentosSaida(cCodigoPedido)
	FWFreeObj(oRepositoryPedidoVenda)

	for nX := 1 to len(aDocumentosSaidaPedido)
		jParametersDocumentoSaida  := {'numeroDocumento': aDocumentosSaidaPedido[nX]['numeroDocumento'], 'serieDocumento': aDocumentosSaidaPedido[nX]['serieDocumento']}
		oRepositoryDocumentosSaida := agd.documentoSaidaRepository.agdDocumentoSaidaRepository():New()
		oRepositoryDocumentosSaida:find(1, MAX_LIST_ITEMS, jParametersDocumentoSaida)
		aSalesInvoices := oRepositoryDocumentosSaida:getResponse()['items']

		for nY := 1 to len(aSalesInvoices)
			aAdd(aOrderInvoices,;
				{;
				'sales_invoice_number'              : aSalesInvoices[nY]['numeroDocumento'   ] ,;
				'sales_invoice_series'              : aSalesInvoices[nY]['serieDocumento'    ] ,;
				'sales_invoice_date'                : aSalesInvoices[nY]['dataEmissao'       ] ,;
				'sales_invoice_gross_total'         : aSalesInvoices[nY]['valorTotalBruto'   ] ,;
				'sales_invoice_items'               : getSalesInvoiceItens(aSalesInvoices[nY]) ,;
				'sales_invoice_receivable_documents': getReceivableDocuments(aSalesInvoices[nY]);
				};
				)
		next nY

		FWFreeObj(oRepositoryDocumentosSaida)
	next nY
return aOrderInvoices

/*/{Protheus.doc} getSalesInvoiceItens
Retorna os itens do documento de saída formatados para o DATA
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param jInvoice, json, documento de saída
@return array, itens do documento de saída
/*/
static function getSalesInvoiceItens(jInvoice as json) as array
	local jParameters         as json
	local oRepositoryItens    as json
	local aItensDocumento     as array
	local nX                  as numeric
	local aInvoiceItems := {} as array

	jParameters := {;
		'numeroDocumento': jInvoice['numeroDocumento'],;
		'serieDocumento' : jInvoice['serieDocumento' ],;
		'codigoCliente'  : jInvoice['codigoCliente'  ],;
		'lojaCliente'    : jInvoice['lojaCliente'    ];
		}

	oRepositoryItens := agd.documentoSaidaItensRepository.agdDocumentoSaidaItensRepository():new()
	oRepositoryItens:find(1, MAX_LIST_ITEMS, jParameters)
	aItensDocumento := oRepositoryItens:getResponse()['items']
	FWFreeObj(oRepositoryItens)

	for nX := 1 to len(aItensDocumento)
		aAdd(aInvoiceItems,;
			{;
			'sales_invoice_item_sequence'    : aItensDocumento[nX]['item'         ],;
			'sales_invoice_item_product_code': aItensDocumento[nX]['codigoProduto'],;
			'sales_invoice_item_quantity'    : aItensDocumento[nX]['quantidade'   ] ;
			};
			)
	next nX
return aInvoiceItems

/*/{Protheus.doc} getReceivableDocuments
Retorna os títulos a receber gerados pelo documento de saída formatados para o DTA
@type function
@version 12
@author jc.maldonado
@since 14/01/2026
@param jInvoice, json, documento de saída
@return array, títulos a receber
/*/
static function getReceivableDocuments(jInvoice as json) as array
	local jParameters                as json
	local oTitulosRepository         as object
	local aTitulos                   as array
	local nX                         as numeric
	local aReceivableDocuments := {} as array

	jParameters := {;
		'codigoCliente'  : jInvoice['codigoCliente'  ],;
		'lojaCliente'    : jInvoice['lojaCliente'    ],;
		'numeroDocumento': jInvoice['numeroDocumento'],;
		'prefixo'        : jInvoice['serieDocumento' ];
		}

	oTitulosRepository := agd.tituloReceberRepository.agdTituloReceberRepository():new()
	oTitulosRepository:find(1, MAX_LIST_ITEMS, jParameters)
	aTitulos := oTitulosRepository:getResponse()['items']
	FWFreeObj(oTitulosRepository)

	for nX := 1 to len(aTitulos)
		aAdd(aReceivableDocuments,;
			{;
			'receivable_document_code'       : aTitulos[nX]['numeroDocumento'],;
			'receivable_document_prefix'     : aTitulos[nX]['prefixo'        ],;
			'receivable_document_installment': aTitulos[nX]['parcela'        ],;
			'receivable_document_type'       : aTitulos[nX]['tipo'           ],;
			'receivable_document_amount'     : aTitulos[nX]['valorTotal'     ],;
			'receivable_document_due_date'   : aTitulos[nX]['dataVencimento' ];
			};
			)
	next nX
return aReceivableDocuments
