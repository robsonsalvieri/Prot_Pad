#Include "agd.dta.barter.instrucao.embarque.get.tool.ch"
#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"


/*/{Protheus.doc} agdDTABarterInstrucaoEmbarqueGetTool
Tool do DTA para Instrução de Embarque do Barter
@type class
@version 12
@author jean.schulze
@since 13/11/2025
/*/
Class agdDTABarterInstrucaoEmbarqueGetTool 
 	Static Method getSetup() as Json
	Static Method getParameters() as Json
	Static Method getRules() as Character 
	Static Method getDescription() as Character
	Static Method getDescriptionShort() as Character
	Static Method getExamples() as Array
	Static Method getGlossary() as Array

	Static Method apply(jFilters as Json) as Array
EndClass

/*/{Protheus.doc} getSetup
Dados para configuração da Tool
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@return json, dados de configuracao
/*/
Method getSetup() as Json Class agdDTABarterInstrucaoEmbarqueGetTool
	Local cName         := ""  as Character
	Local oLoadTool     := Nil as Object

	cName             := "BInstrucaoGET"

	oLoadTool := agdDTAToolsBuilder():New()
	oLoadTool:setName(cName)
	oLoadTool:setDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescription())
	oLoadTool:setShortDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescriptionShort()) 


	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
Return jReturn

/*/{Protheus.doc} getDescription
Retorna a descrição completa da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
method getDescription()  as Character  Class agdDTABarterInstrucaoEmbarqueGetTool
return STR0001 //"Fornece um resumo de informações sobre as instruções de embarque de negócios barter. O resumo de dados tem a lista de instruções, com seu cliente, seus produtos, situação e pedidos de venda envolvidos."
/*/{Protheus.doc} getDescriptionShort
Retorna a descrição curta da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getDescriptionShort() as Character Class agdDTABarterInstrucaoEmbarqueGetTool 
return STR0003 //"Busca as instruções de embarque de um negócio barter"

/*/{Protheus.doc} getRules
Retorna as regras de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getRules()  as Character Class agdDTABarterInstrucaoEmbarqueGetTool
return I18N(STR0002, {"customer"})  //"Se o argumento '#1[customer]#' não for informado pelo usuário, solicite a informação antes de realizar a pesquisa."

/*/{Protheus.doc} getParameters
Retorna os parâmetros de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return json, params
/*/
Method getParameters() as Json Class agdDTABarterInstrucaoEmbarqueGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addParameter("customer", "string"  , STR0005 + ". \n " + STR0004, .T.)  //"O código/nome do cliente pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'R001', 'INJET', '000001', 'TORNO1', 'JOAO', 'ANDRE SILVA'."
	oLoadTool:addParameter("bussines_code", "string"  , STR0007 + ". \n " + STR0006, .T.)  //"O código do negócio barter pode conter somente números. Exemplo: '0000001', '000002', '023456'"
	oLoadTool:addParameter("start_date_transmission"  , "string" , STR0008, .F.) //"Filtro da data inicial para busca dos lotes do produto."
	oLoadTool:addParameter("end_date_transmission"    , "string" , STR0009, .F.) //"Filtro da data final para busca dos lotes do produto."

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
return jReturn["parameters"]

/*/{Protheus.doc} getExamples
Retorna os exemplos de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, exemplos
/*/
Method getExamples() as Array Class agdDTABarterInstrucaoEmbarqueGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addExample(I18N(STR0010, {"customer"}))  //"Quais são as instruções de embarque do cliente '#1[customer]#'?"
	oLoadTool:addExample(I18N(STR0011, {"bussines_code"}))  //"Quais são as instruções de embarque para o negócio/barter '#1[bussines_code]#'?"
	oLoadTool:addExample(I18N(STR0012, {"customer"}))  //"O cliente '#1[customer]#' possui instruções de embarque?"
	oLoadTool:addExample(I18N(STR0013, {"bussines_code"}))  //"O negócio barter '#1[bussines_code]#' possui instruções de embarque?"

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['examples']

/*/{Protheus.doc} getGlossary
Retorna o glossário de termos da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, glossario
/*/
Method getGlossary() as Array Class agdDTABarterInstrucaoEmbarqueGetTool
	oLoadTool := agdDTAToolsBuilder():New()
	
	oLoadTool:addGlossary("customer_code" , STR0014)  //"Código do Cliente"
	oLoadTool:addGlossary("custome_name"  , STR0015) //"Nome do Cliente"
	oLoadTool:addGlossary("bussines_code", STR0016)  //"Código da Negociação"
	oLoadTool:addGlossary("boarding_instruction_code", STR0017)  //"Código da Instrução"
	oLoadTool:addGlossary("product_code", STR0018)  //"Código do Comoditie"
	oLoadTool:addGlossary("quantity", STR0019)  //"Quantidade de Commoditie"
	oLoadTool:addGlossary("boarding_instruction_list", STR0020)  //"Lista de Instruções de Embarque"
	oLoadTool:addGlossary("products_list", STR0021)  //"Lista de Insumos da Instrução de Embarque"

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['glossary']


/*/{Protheus.doc} apply
Metodo de Execução pelo DTA para retorno de dados
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param jFilters, json, filter
@return array, dados
/*/
Method apply(jFilters as Json) as Array Class agdDTABarterInstrucaoEmbarqueGetTool
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cContext     := ""  as Character
	Local jData        := Nil as Json
	Local cFilter      := ""  as Character 
	Local nX
	Local nY

	If (!jFilters:HasProperty('customer') .or. Empty(jFilters["customer"])) .and. (!jFilters:HasProperty('bussines_code') .or. Empty(jFilters["bussines_code"])) 
		Return agdDTAUtilsService():setToolError(400, I18N(STR0022, {"customer","bussines_code" }))  //"Para realizar a busca de fechamento finaceiros, o parâmetro '#1[PARAM]#' ou '#2[PARAM]#' deve ser informado."
	EndIf

	if jFilters:HasProperty('customer') .and. !Empty(jFilters["customer"])
		listClientes := agdDTAUtilsService():getCodCliente(Upper(jFilters["customer"]))
		if len(listClientes) == 0
			Return agdDTAUtilsService():setToolError(400, I18N(STR0023, {RTrim(jFilters["customer"]) }))  //"O cliente #1[customer]# não foi encontrado na base de dados."
		endif

		if len(listClientes) > 1
			Return agdDTAUtilsService():setToolError(400, I18N(STR0024+listClientes[1]+","+listClientes[2]+STR0025, {RTrim(jFilters["customer"]) }))  //"O cliente #1[customer]# está genérico, pois foram encontrados várias ocorrências do mesmo com código("
		endif	
		jFilters["codCliente"] := listClientes[1]
	endif	

	if jFilters:HasProperty('bussines_code') .and. !Empty(jFilters["bussines_code"])
		jFilters["codNegocio"] := Upper(jFilters["bussines_code"])
	endif

	if jFilters:HasProperty('start_date_transmission') .and. !Empty(jFilters["start_date_transmission"])
		jFilters["filter"] := "dataEmissao ge '" + jFilters["start_date_transmission"]+"'"
	endif

	if jFilters:HasProperty('end_date_transmission') .and. !Empty(jFilters["end_date_transmission"])
		if jFilters:HasProperty('filter') .and. !Empty(jFilters["filter"])
			cFilter := jFilters["filter"] + " and "  
		endif
		jFilters["filter"] := cFilter + "dataEmissao le '" + jFilters["end_date_transmission"]+"'"
	endif

	/*Vamos buscar os negocios com o codigo de cliente informado*/

	oRepository := agd.instrucaoEmbarqueRepository.agdInstrucaoEmbarqueRepository():New()
	oRepository:find(1, 20, jFilters)
	
	jData  := JsonObject():New()
	jData["boarding_instruction_list"] := {}
	listaIE := oRepository:getResponse()['items']

	For nX:= 1 to Len(listaIE)
		//jFiltersItem['codigoIE'] := listaIE[nX]['codigo']
		oItemRepository := agd.instrucaoEmbarqueItemRepository.agdInstrucaoEmbarqueItemRepository():New()
		oItemRepository:find(1, 20, {'codigoIE':listaIE[nX]['codigo']})
		listaItens := oItemRepository:getResponse()['items']

		jDataLine  := JsonObject():New()
		jDataLine  := {'customer_code': listaIE[nX]['codCliente'],;
					   'custome_name': listaIE[nX]['nomeCliente'],;
					   'bussines_code': listaIE[nX]['codNegocio'],;
					   'boarding_instruction_code': listaIE[nX]['codigo'],;
					   'order':listaIE[nX]['numPedido'],;
					   'order_situation':listaIE[nX]['statusPedido'],;
					   'date_transmission':listaIE[nX]['dataEmissao'],;
					   'harvest':listaIE[nX]['safra'] }
		
		jDataLine['products_list'] := {}			   
		
		For nY:= 1 to Len(listaItens)
			aadd(jDataLine['products_list'], {'product_name': listaItens[nY]['descProd'],;
											  'product_code':listaItens[nY]['codProd'],;
											  'date_delivery':listaItens[nY]['entrega'],; 
											  'quantity':listaItens[nY]['qtdIE']})
		Next

		aadd(jData["boarding_instruction_list"], jDataLine)
	Next

	If Len(listaIE) > 0
		aAdd(aSuggestions, {"label": STR0026})  //"Quais desses instruções de embarque o pedido não foi faturado?"
		aAdd(aSuggestions, {"label": STR0027})  //"Quais dessas instruções tem data de entrega próxima?"
		cContext := I18N(STR0028, {"boarding_instruction_list"})  //"Os dados contidos em '#1[ARG_CODE]#' é uma lista dos ultimos 20 instruções conforme filtrado pelo cliente ou número de negócio barter."
		aReturn := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}
	Else
		if !Empty(jFilters["customer_code"]) .and. !Empty(jFilters["bussines_code"]) 
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0029, {RTrim(jFilters["customer_code"]), RTrim(jFilters["bussines_code"])}))  //"O cliente #1[customer_code]# e negócio #1[bussines_code]#  não possui instruções de embarque."
		elseif !Empty(jFilters["customer_code"]) 
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0030, {RTrim(jFilters["customer_code"])}))  //"O cliente #1[customer_code]# não possui instruções de embarque."
		else
			aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0031, {RTrim(jFilters["bussines_code"])}))  //"O negócio #1[CODIGO]# não possui instruções de embarque."
		endif	
	EndIf
	aSuggestions := Nil
	jData        := Nil
Return aReturn
