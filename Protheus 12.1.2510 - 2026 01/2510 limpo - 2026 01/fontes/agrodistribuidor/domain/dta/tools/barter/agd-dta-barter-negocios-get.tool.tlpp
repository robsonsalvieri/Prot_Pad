#Include "agd.dta.barter.negocios.get.tool.ch"
#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"

/*/{Protheus.doc} agdDTABarterNegociosBarterGetTool
ToolTool do DTA para Negócios do Barter
@type class
@version 12
@author jean.schulze
@since 13/11/2025
/*/
Class agdDTABarterNegociosBarterGetTool
	Static Method getSetup() as Json
	Static Method getParameters() as Json
	Static Method getRules() as Character 
	Static Method getDescription() as Character
	Static Method getDescriptionShort() as Character
	Static Method getExamples() as Array
	Static Method getGlossary() as Array

	Static Method apply(jFilters as Json) as Array
EndClass

/*/{Protheus.doc} getSetup
Dados para configuração da Tool
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@return json, dados de configuracao
/*/
Method getSetup() as Json Class agdDTABarterNegociosBarterGetTool
	Local cName         := ""  as Character
	Local oLoadTool     := Nil as Object

	cName             := "BNegocioGET"

	oLoadTool := agdDTAToolsBuilder():New()
	oLoadTool:setName(cName)
	oLoadTool:setDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescription())
	oLoadTool:setShortDescription(agdDTABarterFechamentoFinaneiroGetTool():getDescriptionShort()) 

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
Return jReturn


/*/{Protheus.doc} getDescription
Retorna a descrição completa da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
method getDescription()  as Character  Class agdDTABarterNegociosBarterGetTool
return  STR0001 //"Fornece um resumo de informações sobre as negociações de um cliente. O resumo de dados tem a lista de negociações, seus valores, situação e produtos envolvidos."

/*/{Protheus.doc} getDescriptionShort
Retorna a descrição curta da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getDescriptionShort() as Character Class agdDTABarterNegociosBarterGetTool 
return STR0003 //"Busca as negociações de um cliente"

/*/{Protheus.doc} getRules
Retorna as regras de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return character, descricao
/*/
Method getRules()  as Character Class agdDTABarterNegociosBarterGetTool
return I18N(STR0002, {"customer"})  //"Se o argumento '#1[ARG_NAME]#' não for informado pelo usuário, solicite a informação antes de realizar a pesquisa."

/*/{Protheus.doc} getParameters
Retorna os parâmetros de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return json, params
/*/
Method getParameters() as Json Class agdDTABarterNegociosBarterGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addParameter("customer", "string"  , STR0004 + ". \n " + STR0005, .T.)  //"Código/Nome do Cliente"
	oLoadTool:addParameter("start_date_transmission"  , "string" , STR0006, .F.)  //"Data Inicial para buscar os Negócios"
	oLoadTool:addParameter("end_date_transmission"    , "string" , STR0007, .F.)  //"Data Final para buscar os Negócios"
	
	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)
return jReturn["parameters"]

/*/{Protheus.doc} getExamples
Retorna os exemplos de uso da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, exemplos
/*/
Method getExamples() as Array Class agdDTABarterNegociosBarterGetTool
	oLoadTool := agdDTAToolsBuilder():New()

	oLoadTool:addExample(I18N(STR0015, {"customer"}))  //"Quais são as negociações do cliente '#1[customer]#'?"
	oLoadTool:addExample(I18N(STR0016, {"customer"}))  //"O cliente '#1[customer]#' possui negociações?"

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['examples']

/*/{Protheus.doc} getGlossary
Retorna o glossário de termos da Tool
@type method
@version 12
@author jean.schulze
@since 12/12/2025
@return array, glossario
/*/
Method getGlossary() as Array Class agdDTABarterNegociosBarterGetTool
	oLoadTool := agdDTAToolsBuilder():New()
	
	oLoadTool:addGlossary("customer_code" , STR0008)  //"Código do Cliente"
	oLoadTool:addGlossary("custome_name"  , STR0009) //"Nome do Cliente"
	oLoadTool:addGlossary("bussines_code", STR0010)  //"Código da Negociação"
	oLoadTool:addGlossary("bussines_situation", STR0011)  //"Situação da Negociação"
	oLoadTool:addGlossary("product_code", STR0012)  //"Código do Comoditie"
	oLoadTool:addGlossary("quantity", STR0013)  //"Quantidade de Commoditie"
	oLoadTool:addGlossary("bussines_list", STR0014)  //"Lista de Negócios"

	jReturn := oLoadTool:build()
	FreeObj(oLoadTool)

return jReturn['glossary']


/*/{Protheus.doc} apply
Metodo de Execução pelo DTA para retorno de dados
@type method
@version 12
@author jean.schulze
@since 13/11/2025
@param jFilters, json, filter
@return array, dados
/*/
Method apply(jFilters as Json) as Array Class agdDTABarterNegociosBarterGetTool
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cContext     := ""  as Character
	Local jData        := Nil as Json
	Local nX

	If (!jFilters:HasProperty('customer') .or. Empty(jFilters["customer"])) .and. (!jFilters:HasProperty('bussines_code') .or. Empty(jFilters["bussines_code"])) 
		Return agdDTAUtilsService():setToolError(400, I18N(STR0017, {"customer","bussines_code" }))  //"Para realizar a busca de fechamento finaceiros, o parâmetro '#1[PARAM]#' ou '#2[PARAM]#' deve ser informado."
	EndIf

	if jFilters:HasProperty('customer') .and. !Empty(jFilters["customer"])
		listClientes := agdDTAUtilsService():getCodCliente(Upper(jFilters["customer"]))
		if len(listClientes) == 0
			Return agdDTAUtilsService():setToolError(400, I18N(STR0018, {RTrim(jFilters["customer"]) }))  //"O cliente #1[customer]# não foi encontrado na base de dados."
		endif

		if len(listClientes) > 1
			Return agdDTAUtilsService():setToolError(400, I18N(STR0020+listClientes[1]+","+listClientes[2]+STR0019, {RTrim(jFilters["customer"]) }))  //", etc). É possível ser mais específico?"
		endif	
		
		jFilters["codigoCliente"] := listClientes[1]
	endif	

	if jFilters:HasProperty('start_date_transmission') .and. !Empty(jFilters["start_date_transmission"])
		jFilters["filter"] := "dataEmissao ge '" + jFilters["start_date_transmission"]+"'"
	endif

	if jFilters:HasProperty('end_date_transmission') .and. !Empty(jFilters["end_date_transmission"])
		if jFilters:HasProperty('filter') .and. !Empty(jFilters["filter"])
			cFilter := jFilters["filter"] + " and "  
		endif
		jFilters["filter"] := cFilter + "dataEmissao le '" + jFilters["end_date_transmission"]+"'"
	endif

	/*Vamos buscar os negocios com o codigo de cliente informado*/	
	oNegocioRepository := agd.negocioRepository.agdNegocioRepository():New()
	oNegocioRepository:find(1, 20, jFilters)

	jData  := JsonObject():New()
	jData["bussines_list"] := {}
	listaNegocios := oNegocioRepository:getResponse()['items']
	
	For nX:= 1 to Len(listaNegocios)
		aadd(jData["bussines_list"], {'customer_code': listaNegocios[nX]['codigoCliente'],;
							          'custome_name': listaNegocios[nX]['descricaoCliente'],;
									  'bussines_code': listaNegocios[nX]['codigo'],;
									  'bussines_situation':X3Combo( "NEA_STATUS", listaNegocios[nX]['status'] ),;
									  'product_code':listaNegocios[nX]['codigoProduto'],;
									  'quantity':listaNegocios[nX]['quantidadeNegociada'] })
	Next

	If Len(listaNegocios) > 0
		aAdd(aSuggestions, {"label": STR0021})  //"Quais desses negócios tem pendência de fechamento?"
		aAdd(aSuggestions, {"label": STR0022})  //"Quais desses negócios já foram faturados?"
		aAdd(aSuggestions, {"label": STR0023})  //"Algum desses negócios está com recebimento atrasado?"
		cContext := I18N(STR0024, {"bussines_list"})  //"Os dados contidos em '#1[ARG_CODE]#' é uma lista dos ultimos 20 negócios do cliente informado."
		aReturn := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}
	Else
		aReturn := agdDTAUtilsService():setToolError(400, I18N(STR0025, {RTrim(jFilters["customer"])})) //"O recurso #1[CODIGO]# não possui recursos alternativos."
	EndIf
	aSuggestions := Nil
	jData        := Nil
Return aReturn
