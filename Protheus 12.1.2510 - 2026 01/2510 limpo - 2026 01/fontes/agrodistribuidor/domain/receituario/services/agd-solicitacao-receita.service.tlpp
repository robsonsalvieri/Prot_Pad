#include "totvs.ch"
#include "tlpp-core.th"
#include "agd.solicitacao.receita.service.ch"

namespace agd.solicitacaoReceitaService
using namespace agd.solicitacaoReceitaRepository
using namespace agd.utilsService


/*/{Protheus.doc} agdSolicitacaoReceitaService
Serviço para gerenciamento de Solicitação de Receita (NET/NEU)
@type class
@version 12
@author lindembergson.pacheco
@since 06/10/2025
/*/
class agdSolicitacaoReceitaService FROM agdUtilsService

	public method New()
	public method gerarSolicitacaoReceita()
	public method listar()
	public method atualizaStatus()
	public method getSolicPedidos() as ARRAY
	public method confirmarSolicitacao()
	public method vincularReceita()

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return variant, this
/*/
method New() class agdSolicitacaoReceitaService
	_Super:New()
return Self


/*/{Protheus.doc} gerarSolicitacaoReceita
Gerar Solicitação de Receita (NET/NEU)
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return logical, true
/*/
method gerarSolicitacaoReceita(cPedido) class agdSolicitacaoReceitaService
	Local cReceitas		:= ""
	Local oRepository	As Object
	Local cMensagem		:= ""

	oRepository := agdSolicitacaoReceitaRepository():New()
	oRepository:gerarSolicitacao(cPedido)
	if !oRepository:isSuccess()
		If ( Upper(FunName()) == "AGDA040" .or. IsInCallStack( "AGDI043M" ) ) .and. !IsBlind()
			AGDHELP(STR0001, oRepository:getMessageError(), oRepository:getSolutionError() ) //#"Atenção!"
		else
			Self:setError(oRepository:getMessageError(), 404, oRepository:getSolutionError())
		endif
	else
		cReceitas := CenArr2Str(oRepository:getResponse(), ", ")
		cMensagem := STRTRAN(STR0002, "*", cReceitas) // #"Receitas " + cReceitas + " geradas com sucesso."
		If ( Upper(FunName()) == "AGDA040" .or. IsInCallStack( "AGDI043M" ) ) .and. !IsBlind()
			FWAlertSuccess(cMensagem )
		else
			Self:setSuccess( cMensagem )
		endif
	endif
	FreeObj(oRepository)

Return nil


/*/{Protheus.doc} listar
Busca as Solicitações de Receita
@type method
@version 12
@author carlos.augusto
@since 04/11/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho
@param oJsonQryParam, object, param
@return variant, this
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdSolicitacaoReceitaService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdSolicitacaoReceitaRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil


/*/{Protheus.doc} atualizaStatus
Atualiza Status da Solicitação
@type Method
@version P12
@author scheronlini.martins
@since 03/12/2025
/*/
method atualizaStatus(cSolic,cStatusSol,cStatusRec) class agdSolicitacaoReceitaService
	Local oRepository As Object
	Local cNewStatus := ""

	//Status Solicitação - 0=Pendente;1=Confirmada;2=Cancelada; 3=Invalida
	//Status Receita - 0=Pendente;1=Emitida;2=Cancelada

	if cStatusSol == "1"/*Confirmada*/ .and. cStatusRec == "1"/*Emitida*/
		cNewStatus := "3"
	elseif cStatusSol == "1"/*Confirmada*/;
			.and. (cStatusRec == "0"/*Pendente*/ .or. cStatusRec == "2"/*Cancelada*/.or. empty(cStatusRec))
		cNewStatus := "2"
	elseif cStatusSol == "0"/*Pendente*/ .and. empty(cStatusRec)
		cNewStatus := "3"
	endif

	oRepository := agdSolicitacaoReceitaRepository():New(FWxFilial('NET')+cSolic)

	if(!Empty(cNewStatus))
		oRepository:atualizaStatus(cNewStatus)

		Self:setFullResponse(oRepository)

		oRepository:DeActivate()
		FreeObj(oRepository)
	endif

return


/*/{Protheus.doc} confirmarSolicitacao
Confirma uma solicitação de receita alterando seu status para "Confirmado"
@type method
@version 12
@author beatriz.dantas
@since 10/12/2025
@param cCodigo, character, Código da solicitação (NET_CODIGO)
@return nil
/*/
method confirmarSolicitacao(cCodigo) class agdSolicitacaoReceitaService
	Local oRepository As Object
	
	oRepository := agdSolicitacaoReceitaRepository():New(FWxFilial('NET')+cCodigo)
	oRepository:confirmarSolicitacao()
	
	If oRepository:isSuccess()
		Self:setSuccess(oRepository:getResponse())
	Else
		Self:setError(oRepository:getMessageError(), 404,oRepository:getSolutionError())
	EndIf
	
	FreeObj(oRepository)
Return nil


/*/{Protheus.doc} vincularReceita
Confirma uma solicitação de receita alterando seu status para "Confirmado"
@type method
@version 12
@author carlos.augusto
@since 23/12/2025
@param cIdSolicitacao, character, Código da solicitação (NET_CODIGO)
@param jPayload, json, receita para vinculo
@return nil
/*/
method vincularReceita(cIdSolicitacao as character, jPayload as json) class agdSolicitacaoReceitaService
	Local oRepository As Object

	oRepository := agdSolicitacaoReceitaRepository():New(cIdSolicitacao)
	oRepository:vincularReceita(jPayload, .T.)
	Self:setFullResponse(oRepository)
	oRepository:DeActivate()
	FreeObj(oRepository)

Return nil
