#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.solicitacaoReceitaItensService
using namespace agd.solicitacaoReceitaItensRepository
using namespace agd.utilsService


/*/{Protheus.doc} agdSolicitacaoReceitaItensService
Serviço para gerenciamento de Solicitação de Receita (NET/NEU)
@type class
@version 12
@author lindembergson.pacheco
@since 06/10/2025
/*/
class agdSolicitacaoReceitaItensService FROM agdUtilsService

	public method New()
	public method listar()
	public method desvinculaReceita()

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return variant, this
/*/
method New() class agdSolicitacaoReceitaItensService
	_Super:New()
return Self


/*/{Protheus.doc} listar
Busca as os itens de Solicitações de Receita
@type method
@version 12
@author carlos.augusto
@since 04/11/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho
@param oJsonQryParam, object, param
@return variant, this
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdSolicitacaoReceitaItensService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdSolicitacaoReceitaItensRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} agdReceituarioService::desvinculaReceita
Apaga o vincula da tabela NEU com a tabela NE6
@type method
@version P12
@author scheronlini.martins
@since 19/12/2025
@param cChaveID, character, ChaveId da tabela NEU
/*/
method desvinculaReceita(cChaveID) class agdSolicitacaoReceitaItensService
	Local oRepository :=agdSolicitacaoReceitaItensRepository():New()
	Local aFieldsSaveComand := {}

	DbSelectArea("NEU")
	NEU->(DBSetOrder(1))
	IF NEU->(DBSeek(cChaveID))
		aadd(aFieldsSaveComand, {"NEU->NEU_CODREC", ""})
		aadd(aFieldsSaveComand, {"NEU->NEU_SEQREC", ""})
		oRepository:updateRepository(aFieldsSaveComand,cChaveID,"1" )
	EndIf

return
