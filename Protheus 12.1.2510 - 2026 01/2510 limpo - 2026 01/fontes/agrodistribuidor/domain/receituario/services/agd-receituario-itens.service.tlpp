#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.receituarioItensService
using namespace agd.receituarioItensRepository
using namespace agd.utilsService

/*/{Protheus.doc} agdreceituarioItensService
Serviço dos Produtos da Receita Agronômica
@type class
@version 12
@author lindembergson.pacheco
@since 06/10/2025
/*/
class agdReceituarioItensService FROM agdUtilsService

	public method New()
	public method listar()
	public method listarItensPorIdReceita()
	public method atualizaQtdDIsponivel()

endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return variant, this
/*/
method New() class agdReceituarioItensService
	_Super:New()
return Self

/*/{Protheus.doc} listar
Busca as os itens deSolicitações de Receita
@type method
@version 12
@author carlos.augusto
@since 04/11/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, query param
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdReceituarioItensService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdReceituarioItensRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} listarItensPorIdReceita
Listar os itens da receita pelo ID da receita(NE7_CODREC)
@type method
@version  P12
@author claudineia.reinert
@since 27/11/2025
@param cIdReceita, character, codigo interno da receita
@param nPage, numeric, numero da paginas
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, parametros de consulta em formato JSON
/*/
method listarItensPorIdReceita(cIdReceita as Character, nPage, nPageSize, oJsonQryParam) class agdReceituarioItensService
	Local oRepository As Object
	Local aFieldsId As array

	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oJsonQryParam['idReceita']        := cIdReceita

	oRepository := agdReceituarioItensRepository():New()

	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil


/*/{Protheus.doc} agdReceituarioService::atualizaQtdDIsponivel
Atualiza a quantidade disponivel da tabela NE7
@type method
@version P12
@author scheronlini.martins
@since 19/12/2025
@param cChaveID, character, ChaveId da tabela NE7
@param nQtdProcessar, numeric, quantidade a ser 
/*/
method atualizaQtdDIsponivel(cChaveID, nQtdProcessar) class agdReceituarioItensService
	Local nQtdAtualizar
	Local oRepository := agdReceituarioItensRepository():New()
	Local aFieldsSaveComand := {}

	DbSelectArea("NE7")
	NE7->(DBSetOrder(1))
	IF NE7->(DBSeek(cChaveID))
		nQtdAtualizar := (NE7->NE7_QTDISP + nQtdProcessar)
		aadd(aFieldsSaveComand, {"NE7->NE7_QTDISP", nQtdAtualizar})
		oRepository:updateRepository(aFieldsSaveComand,cChaveID, "1" )
	EndIf

return
