#include "totvs.ch"
#include "tlpp-core.th"

namespace agd.receituarioService
using namespace agd.receituarioRepository
using namespace agd.utilsService


/*/{Protheus.doc} agdReceituarioService
Serviço para gerenciamento de Solicitação de Receita (NET/NEU)
@type class
@version 12
@author lindembergson.pacheco
@since 06/10/2025
/*/
class agdReceituarioService FROM agdUtilsService

	public method New()
	public method listar()	
	public method listarReceitasDisponiveis()
	public method createReceitaBySolitacao()
	public method insertReceita()
	public method GetReceitasByPedidos()
	public method GetPedidosNF()
	public method cancelarReceita()
	public method vincularNotaReceita()
	public method desvincularNotaReceita()
	
endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author lindembergson.pacheco
@since 06/10/2025
@return variant, this
/*/
method New() class agdReceituarioService
	_Super:New()
return Self

/*/{Protheus.doc} listar
Busca as Solicitações de Receita
@type method
@version 12
@author carlos.augusto
@since 04/11/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, query param
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdReceituarioService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdReceituarioRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} listarReceitasDisponiveis
Listar as receitas disponiveis para reaproveitamento
@type method
@version P12
@author claudineia.reinert
@since 27/11/2025
@param nPage, numeric, numero de paginas
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, parametros de consulta em formato JSON
/*/
method listarReceitasDisponiveis(nPage, nPageSize, oJsonQryParam) class agdReceituarioService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdReceituarioRepository():New()
	oRepository:findReceitasDisponiveis(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil


/*/{Protheus.doc} createReceitaBySolitacao
Gerar Receita Agronômica a ser vinculado na Solicitação de Receita
@type method
@version P12.1.2610
@author Gilson.Venturi
@since 18/11/2025
@return logical, true
/*/
method createReceitaBySolitacao(jPayload as json) class agdReceituarioService
	Local oRepository As Object

	oRepository := agdReceituarioRepository():New()
	oRepository:saveReceitaBySolicitacao(jPayload)
	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
	
return nil

/*/{Protheus.doc}GetPedidosNF
retorna um array de Pedidos relacionados ao recno da Nota Fiscal de saida
@type method
@version p12
@author scheronlini.martins
@since 31/10/2025
@param nRecnoSF2, numeric, recno da Nota Fiscal de saida
@return array, return_array de Pedidos
/*/
Method GetPedidosNF(nRecnoSF2) Class agdReceituarioService
	Local cFil    := ""
	Local cDoc    := ""
	Local cSerie  := ""
	Local aPedidos := {}
	Local cQuery := ""
	Local cAliQry 	:= GetNextAlias()
	Local oStatement 	:= nil

	// Posiciona na nota fiscal informada
	DbSelectArea("SF2")
	DbGoTo(nRecnoSF2)

	If EoF()
		Return {}
	EndIf

	cFil    := SF2->F2_FILIAL
	cDoc    := AllTrim(SF2->F2_DOC)
	cSerie  := AllTrim(SF2->F2_SERIE)

	// Consulta direta na SC5 pelos campos C5_NOTA e C5_SERIE
	cQuery := ""
	cQuery += " SELECT DISTINCT C5_NUM "
	cQuery += " FROM " + RetSqlName("SC5")
	cQuery += " WHERE C5_FILIAL = ? "
	cQuery += "   AND C5_NOTA   = ? "
	cQuery += "   AND C5_SERIE  = ? "
	cQuery += "   AND D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery( cQuery )

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQuery)
	oStatement:SetString(1, cFil)
	oStatement:SetString(2, cDoc)
	oStatement:SetString(3, cSerie)
	cQuery := oStatement:GetFixQuery()
	dbUseArea(.T.,"TOPCONN",TCGenQry(,,cQuery),cAliQry,.T.,.T.)

	While !(cAliQry)->(EoF())
		AAdd(aPedidos, AllTrim((cAliQry)->C5_NUM))
		(cAliQry)->(DbSkip())
	EndDo

	(cAliQry)->(DbCloseArea())

Return aPedidos

/*/{Protheus.doc} agdReceituarioService::GetReceitasByPedidos
Retorna array com o codigo da receita e CPF do engenheiro
@type method
@version p12
@author scheronlini.martins
@since 31/10/2025
@param nRecnoSF2, numeric, recno da Nota Fiscal de saida
@return variant, return_array com o codigo da receita e CPF do engenheiro
/*/
Method GetReceitasByPedidos(nRecnoSF2) Class agdReceituarioService
	Local cAliQry    := GetNextAlias()
	Local cQuery     := ""
	Local oStatement := nil
	Local cListaPed  := ""
	Local aReceitas  := {}
	Local aPedidos   := ::GetPedidosNF(nRecnoSF2)

	// ------------------------------------------------------------------
	// 1. Validação de entrada
	// ------------------------------------------------------------------
	If Empty(aPedidos) .or. Len(aPedidos) == 0
		Return {}
	EndIf

	// ------------------------------------------------------------------
	// 2. Monta lista de pedidos para IN (...)
	// ------------------------------------------------------------------
	cListaPed :=  StrTran(ArrayToStr(aPedidos), ",", "','")

	// ------------------------------------------------------------------
	// 3. Monta query para buscar receituários + CPF do engenheiro
	// ------------------------------------------------------------------
	cQuery := ""
	cQuery += " SELECT DISTINCT "
	cQuery += "   NE6.NE6_NUMREC, "
	cQuery += "   NP8.NP8_CPF "
	cQuery += " FROM " + RetSqlName("NE6") + " NE6 "
	cQuery += " INNER JOIN " + RetSqlName("NET") + " NET "
	cQuery += "   ON NET.NET_FILIAL = ? "
	cQuery += "  AND NET.NET_CODIGO = NE6.NE6_NUMSOL "
	cQuery += "  AND NET.D_E_L_E_T_ = ' ' "
	cQuery += " INNER JOIN " + RetSqlName("NP8") + " NP8 "
	cQuery += "   ON NP8.NP8_FILIAL = NET.NET_FILIAL "
	cQuery += "  AND NP8.NP8_CODIGO = NET.NET_CODENG "
	cQuery += "  AND NP8.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE NE6.NE6_FILIAL = ? "
	cQuery += "   AND NET.NET_NUMPED IN ( ? ) "
	cQuery += "   AND NE6.D_E_L_E_T_ = ' ' "
	cQuery := ChangeQuery(cQuery)

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQuery)
	oStatement:SetString(1, fwxFilial("NET"))
	oStatement:SetString(2, fwxFilial("NE6"))
	oStatement:SetString(3, cListaPed)

	cQuery := oStatement:GetFixQuery()

	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAliQry, .T., .T.)

	// ------------------------------------------------------------------
	// 4. Monta array de retorno com número da receita + CPF do engenheiro
	// ------------------------------------------------------------------
	While !(cAliQry)->(EoF())
		AAdd(aReceitas, { AllTrim((cAliQry)->NE6_NUMREC), AllTrim((cAliQry)->NP8_CPF) })
		(cAliQry)->(DbSkip())
	EndDo

	(cAliQry)->(DbCloseArea())

Return aReceitas

/*/{Protheus.doc} agdReceituarioService::insertReceita
Insere dados da Receita na tabela NES
@type method
@version P12
@author scheronlini.martins
@since 04/11/2025
@param nRecnoSF2, numeric, recno da Nota de saida
@return variant, return_.t.
/*/
Method insertReceita(nRecnoSF2) Class agdReceituarioService
	Local aDadosReceita := Self:GetReceitasByPedidos(nRecnoSF2)
	Local oRepository   := agdReceituarioRepository():New()
	Local oJson         := Nil
	Local aItems        := {}
	Local n             := 1

    oJson   := JsonObject():New()
   
	For n := 1 To Len(aDadosReceita)
		AAdd(aItems, JsonObject():New())
		aItems[n]["tipoVinculo"]   := '1'
		aItems[n]["item"]          := StrZero(n, TamSX3("NES_ITEM")[1])
		aItems[n]["numReceita"]    := aDadosReceita[n][1]
		aItems[n]["cpfEngenheiro"] := aDadosReceita[n][2]
	Next

	oJson["filial"]    := FwXFilial("NES")
	oJson["documento"] := SF2->F2_DOC
	oJson["serie"]     := SF2->F2_SERIE
	oJson["cliente"]   := SF2->F2_CLIENTE
	oJson["loja"]      := SF2->F2_LOJA
    oJson["items"]     := aItems

	oRepository:saveDadosReceitas(oJson)

return .T.

/*/{Protheus.doc} cancelarReceita
Cancelar Receita Agronômica e desvincular na Solicitação de Receita
@type method
@version P12.1.2610
@author Gilson.Venturi
@since 05/12/2025
@param cdIdExterno, character, id unico
@return logical, true
/*/
method cancelarReceita(cdIdExterno) class agdReceituarioService
	Local oRepository As Object

	oRepository := agdReceituarioRepository():New()
	oRepository:cancelarReceita(cdIdExterno)
	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
	
return nil

/*/{Protheus.doc} vincularNotaReceita
Vincular Nota Fiscal ao Receituário
@type method
@version P12.1.2610
@author Gilson.Venturi
@since 26/12/2025
@param cPediOrig, character, pedido de origem
@param cNotaFisc, character, número da nota fiscal
@param cSerie, character, série da nota fiscal
@return variant, this
/*/
method vincularNotaReceita(cPediOrig, cNotaFisc, cSerie) class agdReceituarioService
	Local oModel120        as object

	If TableInDic('NE6')
		DBSelectArea("NE6")
		NE6->(DBSetOrder(4)) //NE6_FILIAL + NE6_PEDORI
		NE6->(DBGoTop())
		NE6->(dbSeek(FWxFilial('NE6') + cPediOrig))
		Do While !NE6->(Eof()) .And. (NE6->(NE6_FILIAL) + NE6_PEDORI) == (FWxFilial("NE6") + cPediOrig)

			// Carrega o Model da rotina AGDA120 - Receita Agronômica
			oModel120 := FwLoadModel("AGDA120")
			oModel120:SetOperation(4) //MODEL_OPERATION_UPDATE
			oModel120:Activate()

			oNE6 := oModel120:GetModel("AGDA120_NE6")
			oNE6:SetValue("NE6_DOC"  , cNotaFisc)
			oNE6:SetValue("NE6_SERIE", cSerie)
			oNE6:SetValue("NE6_STSNF", '2') //2 - Vinculada

			oModel120:VldData()
			oModel120:CommitData()
			oModel120:DeActivate()
			oModel120:Destroy()
			oModel120 := NIL

			NE6->(DBSkip(1))
		Enddo
	Endif

return nil

/*/{Protheus.doc} desvincularNotaReceita
Desvincular Nota Fiscal do Receituário
@type method
@version P12.1.2610
@author Gilson.Venturi
@since 23/12/2025
@param cNotaFisc, character, número da nota fiscal
@param cSerie, character, série da nota fiscal
@return variant, this
/*/
method desvincularNotaReceita(cNotaFisc, cSerie) class agdReceituarioService
	Local oModel120        as object

	If TableInDic('NE6')
		DBSelectArea("NE6")
		NE6->(DBSetOrder(3)) //NE6_FILIAL + NE6_DOC + NE6_SERIE
		NE6->(DBGoTop())
		NE6->(dbSeek(FWxFilial('NE6') + cNotaFisc + cSerie))
		Do While !NE6->(Eof()) .And. NE6->(NE6_FILIAL + NE6_DOC + NE6_SERIE) == (FWxFilial("NE6") + cNotaFisc + cSerie)

			// Carrega o Model da rotina AGDA120 - Receita Agronômica
			oModel120 := FwLoadModel("AGDA120")
			oModel120:SetOperation(4) //MODEL_OPERATION_UPDATE
			oModel120:Activate()

			oNE6 := oModel120:GetModel("AGDA120_NE6")
			oNE6:SetValue("NE6_DOC"  , '')
			oNE6:SetValue("NE6_SERIE", '')
			oNE6:SetValue("NE6_STSNF", '3') //3 - Desvinculada

			oModel120:VldData()
			oModel120:CommitData()
			oModel120:DeActivate()
			oModel120:Destroy()
			oModel120 := NIL

			NE6->(DBSkip(1))
		Enddo
	Endif

return nil
