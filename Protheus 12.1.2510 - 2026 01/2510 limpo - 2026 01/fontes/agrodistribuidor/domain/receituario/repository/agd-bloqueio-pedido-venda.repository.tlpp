#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "agd-bloqueio-pedido-venda.repository.ch"

namespace agd.bloqueioPedidoVendaRepository
using namespace agd.repositoryUtils
using namespace agd.solicitacaoReceitaRepository
using namespace agd.bloqueioFaturamentoService


/*/{Protheus.doc} agdBloqueioPedidoVendaRepository
Repositorio para operacoes na SC9.
Controla liberação e estorno de pedido de venda para faturamento
@type class
@version P12.1.2610
@author carlos.augusto
@since 15/12/2025
/*/
class agdBloqueioPedidoVendaRepository FROM agdRepositoryUtils
	public Method New()
	public Method marcaTodosItensGrupoReceita()

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version P12.1.2610
@author carlos.augusto
@since 15/12/2025
@return variant, instância
/*/
Method New() Class agdBloqueioPedidoVendaRepository
	_Super:New()
Return Self


/*/{Protheus.doc} marcaTodosItensGrupoReceita
Marcação automatica de todos os itens de um grupo.
Executa UPDATE por conta de performance
@type method
@version P12.1.2610
@author carlos.augusto
@since 15/12/2025
@param aItensToMark, array, itens que não foram marcados
@param cMarca, Character, marca do browse
@param cWhere, Character, string com condição SQL
/*/
Method marcaTodosItensGrupoReceita(aItensToMark, cMarca, cWhere) class agdBloqueioPedidoVendaRepository
	Local lRet         := .T.
	Local aAreaNEU     := NEU->(GetArea())
	Local aAreaSC9     := SC9->(GetArea())
	Local nX           := 1
    Local oStatReceita := Nil
    Local oRepSolicRec := agdSolicitacaoReceitaRepository():New()
	Local cReceita     := ""

	For nX := 1 To Len(aItensToMark)
		cReceita := oRepSolicRec:getReceitaPorItemSolicitacao(@oStatReceita, aItensToMark[nX,1],aItensToMark[nX,2] , aItensToMark[nX,3] , aItensToMark[nX,4] )
		If Empty(cReceita)
			//"Validação de itens com receituário agronômico." "O item de Pedido: "" Item: "" Seq Lib: "" não possui receita.""Verifique o item informado para receber a marcação."
			AGDHELP( STR0001, STR0002 + aItensToMark[nX,2] + STR0003 + aItensToMark[nX,3] + STR0004 + aItensToMark[nX,4] + STR0005, STR0006 )
			lRet := .F.
		EndIf
	Next nX

	//Tudo ou nenhum para não gerar inconsistencia
	If lRet
		SC9->(dbSetOrder(1))
		For nX := 1 To Len(aItensToMark)
			If SC9->(dbSeek(xFilial('SC9') + aItensToMark[nX,2] + aItensToMark[nX,3] + aItensToMark[nX,4]))
				If RecLock('SC9',.F.)
					SC9->C9_OK := cMarca
					SC9->(MsUnlock())
				EndIf
			EndIf
		Next nX
	EndIf
	oStatReceita := Nil
	oRepSolicRec := Nil
	RestArea(aAreaNEU)
	RestArea(aAreaSC9)
Return lRet




