#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"
#include "agd.receituario.repository.ch"

#DEFINE CODIGO_MAPA 1
#DEFINE QUANTIDADE_SOLICITADA 2

namespace agd.receituarioRepository
using namespace agd.repositoryUtils
using namespace agd.bloqueioFaturamentoService
using namespace agd.solicitacaoReceitaRepository


/*/{Protheus.doc} agdReceituarioRepository
Repositório de Solicitação de Receita (NET/NEU)
@type class
@version P12.1.2610
@author lindembergson.pacheco
@since 01/10/2025
/*/
class agdReceituarioRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository	As Object

	public Method New()
	public Method getFields()
	public Method getJoins()
	public Method getCodigoSolicitacaoByReceita()
	public Method saveReceitaBySolicitacao()
	public Method findReceitasDisponiveis() 
	public Method saveDadosReceitas()
	public Method getFieldsNESRece()
	public Method getFieldsNESCabec()
	public method cancelarReceita()
	public method isNFeReceita()
	public method isReceitaVinculada()

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version P12.1.2610
@author lindembergson.pacheco
@since 01/10/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdReceituarioRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NE6', self:getFields(), Self:getJoins())
Return Self


/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type function
@version P12.1.2610
@author carlos.augusto
@since 04/11/2025
@return Array, Array com campos do receituario
/*/
Method getFields() Class agdReceituarioRepository
	Local aArrayFields := {}
	AAdd(aArrayFields, {'filial'                  	, 'NE6_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'idReceita'               	, 'NE6_CODREC', 'C', .T.})
	AAdd(aArrayFields, {'idReceitaExterno'        	, 'NE6_CODEXT', 'C', .T.})
	AAdd(aArrayFields, {'numeroReceita'           	, 'NE6_NUMREC', 'C', .T.})

	AAdd(aArrayFields, {'cliente'                 	, 'NE6_CLIENT', 'C', .T.})
	AAdd(aArrayFields, {'loja'                    	, 'NE6_LOJA'  , 'C', .T.})
	AAdd(aArrayFields, {'clienteNome'             	, 'A1_NOME'   , 'C', .T.})
	
	AAdd(aArrayFields, {'dataEmissao'             	, 'NE6_DTEMIS', 'D', .T.})
	AAdd(aArrayFields, {'horaEmissao'             	, 'NE6_HREMIS', 'C', .T.})
	AAdd(aArrayFields, {'urlReceita'              	, 'NE6_URLREC', 'C', .T.})
	AAdd(aArrayFields, {'status'                  	, 'NE6_STATUS', 'C', .T.})

	AAdd(aArrayFields, {'numeroPedido'            	, 'NE6_PEDORI', 'C', .T.})
	AAdd(aArrayFields, {'numeroNotaFiscal'        	, 'NE6_DOC'   , 'C', .T.})
	AAdd(aArrayFields, {'serieNotaFiscal'         	, 'NE6_SERIE' , 'C', .T.})

	AAdd(aArrayFields, {'cultura'                 	, 'NE6_CULTUR', 'C', .T.})
	AAdd(aArrayFields, {'descricaoCultura'        	, 'NP3_DESCRI', 'C', .T.})
	AAdd(aArrayFields, {'engenheiro'               	, 'NP8_CODIGO', 'C', .T.})
	AAdd(aArrayFields, {'nomeEngenheiro'           	, 'NP8_NOME'  , 'C', .T.})
	AAdd(aArrayFields, {'observacao'               	, 'NE6_OBS'   , 'M', .T.})

Return aArrayFields

/*/{Protheus.doc} getJoins
Relacionamentos para a requisicao
@type function
@version 12
@author carlos.augusto
@since 04/11/2025
@return Array, Array com campos do receituario
/*/
Method getJoins() Class agdReceituarioRepository
	Local aArrayJoins  := {}

	AAdd(aArrayJoins, {'LEFT OUTER'		, 'SA1' , Self:getWhereDefault('SA1.A1_COD = NE6.NE6_CLIENT AND SA1.A1_LOJA = NE6.NE6_LOJA'	, 'SA1')})
	AAdd(aArrayJoins, {'LEFT OUTER'		, 'NP3' , Self:getWhereDefault('NP3.NP3_CODIGO = NE6.NE6_CULTUR'							, 'NP3')})
	AAdd(aArrayJoins, {'LEFT OUTER'		, 'NEQ' , Self:getWhereDefault('NEQ.NEQ_NUM = NE6.NE6_PEDORI'								, 'NEQ')})
	AAdd(aArrayJoins, {'LEFT OUTER'		, 'NP8' , Self:getWhereDefault('NP8.NP8_CODIGO = NEQ.NEQ_CODENG'							, 'NP8')})

Return aArrayJoins

/*/{Protheus.doc} saveReceitaBySolicitacao
Gerar Receita Agronômica a ser vinculado na Solicitação de Receita
@type method
@version P12.1.2610
@author Gilson.Venturi
@since 18/11/2025
@param jPayload, json, payload recebido
@return variant, this
/*/
method saveReceitaBySolicitacao(jPayload as json) class agdReceituarioRepository
	Local oBloqFatService  as object
	Local oModel120        as object
	Local oModel040        as object
	Local oNET             as object
	Local oNEU             as object
	Local oNE6             as object
	Local oNE7             as object
	Local oRepoSolic	   as object
	Local aQuantMapaSolic  as array
	Local nTamSeq 		   := TamSX3("NE7_SEQREC")[1]
	Local nI    		   := 1
	Local nJ    		   := 1
	Local nSeq  		   := 1
	Local lRet 			   := .T.
	Local nQtdSaldo		   := 0

	NET->(dbSetOrder(1))
	if jPayLoad:hasProperty('solicitacao') .and. NET->(dbSeek( FWxFilial('NET')+ jPayLoad['solicitacao']))

		// Carrega o Model da rotina AGDA120 - Receita Agronômica
		oModel120 := FwLoadModel("AGDA120")
		oModel120:SetOperation(MODEL_OPERATION_INSERT)
		oModel120:Activate()

		//Atualiza relacionamento na AGDA040 - Solicitação de Receita
		oModel040 := FwLoadModel("AGDA040")
		oModel040:SetOperation(MODEL_OPERATION_UPDATE)
		oModel040:Activate()

		oNE6 := oModel120:GetModel("AGDA120_NE6")
		oNE7 := oModel120:GetModel("AGDA120_NE7")
		oNET := oModel040:GetModel("AGDA040_NET")
		oNEU := oModel040:GetModel("AGDA040_NEU")

		oNE6:SetValue("NE6_FILIAL", FWxFilial('NE6'))
		oNE6:SetValue("NE6_CLIENT", NET->NET_CLIENT)
		oNE6:SetValue("NE6_LOJA",   NET->NET_LOJA)
		oNE6:SetValue("NE6_PEDORI", NET->NET_NUMPED)
		oNE6:SetValue("NE6_CULTUR", NET->NET_CULTRA)
		oNE6:SetValue("NE6_NUMREC", jPayLoad['codigoReceita'])
		oNE6:SetValue("NE6_CODEXT", jPayLoad['idReceitaExterno'])
		oNE6:SetValue("NE6_DTEMIS", Stod(StrTran(jPayLoad['emissao'], "-", "")))
		oNE6:SetValue("NE6_HREMIS", jPayLoad['hora'])
		oNE6:SetValue("NE6_URLREC", jPayLoad['urlReceita'])
		oNE6:SetValue("NE6_STATUS", '1') // '1 - Emitida'
		oNE6:SetValue("NE6_OBS",    jPayLoad['observacao'])
		oNE6:SetValue("NE6_NUMSOL", jPayLoad['solicitacao'])

		oNET:SetValue("NET_STSREC", '1') // '1 - Receita Emitida'

		oRepoSolic := agdSolicitacaoReceitaRepository():New(FWxFilial('NET') + jPayLoad['solicitacao'])
		aQuantMapaSolic := oRepoSolic:quantidadePorMapaNaSolicitacao(jPayLoad['solicitacao'])

		//Cada linha da NE7, possui a quantidade total por mapa
		If !Empty(aQuantMapaSolic)
			For nI := 1 To Len(aQuantMapaSolic)
				if nI > 1
					oNE7:AddLine()
				endif
				nSeq := StrZero(nI, nTamSeq)
				oNE7:SetValue("NE7_SEQREC", nSeq )
				oNE7:SetValue("NE7_MAPA", aQuantMapaSolic[nI][CODIGO_MAPA])
				oNE7:SetValue("NE7_QTDE", aQuantMapaSolic[nI][QUANTIDADE_SOLICITADA])
				nQtdSaldo := aQuantMapaSolic[nI][QUANTIDADE_SOLICITADA]

				// Atualiza NEU com código e sequência da receita gerada/ Recebimento da receita é total
				For nJ := 1 To oNEU:Length()
					oNEU:GoLine(nJ)
					If Alltrim(oNEU:GetValue("NEU_MAPA")) == AllTrim(aQuantMapaSolic[nI][CODIGO_MAPA])
						If Empty(oNEU:GetValue("NEU_CODREC")) .And. Empty(oNEU:GetValue("NEU_SEQREC"))
							oNEU:SetValue("NEU_CODREC", oNE6:GetValue("NE6_CODREC"))
							oNEU:SetValue("NEU_SEQREC", nSeq )
							nQtdSaldo := nQtdSaldo - oNEU:GetValue("NEU_QTDVEN")
						EndIf
					EndIf
				Next nJ
				oNE7:SetValue("NE7_QTDISP", nQtdSaldo)
			Next
		EndIf

		if Self:isSuccess()

			Begin Transaction
				If lRet := oModel120:VldData()
					oModel120:CommitData()
					If lRet .And. Self:isSuccess()
						If lRet := oModel040:VldData()
							oModel040:CommitData()

							// Efetua desbloqueio de faturamento para os itens da receita
							oBloqFatService := agdBloqueioFaturamentoService():New()
							dbSelectArea('NEU')
							NEU->(dbSetOrder(1)) // NEU_FILIAL+NEU_CODIGO
							if NEU->(dbSeek( FWxFilial('NEU')+ jPayLoad['solicitacao']))
								while NEU->(!EoF()) .and. NEU->(NEU_FILIAL + NEU_CODIGO) == FWxFilial('NEU')+ jPayLoad['solicitacao']
									oBloqFatService:gerarBloqueioReceita(NEU->NEU_NUMPED, NEU->NEU_ITEMPE, NEU->NEU_SEQLIB, NEU->NEU_PRODUT)
									NEU->(dbSkip())
								EndDo
							EndIf
							FreeObj(oBloqFatService)
						EndIf
					endif


					If !lRet //Gravar no historico da solicitacao. Issue no backlog DAGRODIST-2397, os erros dos modelos
						DisarmTransaction()
					EndIf
				EndIf 
			End Transaction
		endif

		oModel120:DeActivate()
		oModel120:Destroy()
		FWFreeObj(oModel120)

		oModel040:DeActivate()
		oModel040:Destroy()
		FWFreeObj(oModel040)
	else
		Self:setError(STR0001, 400) //"Registro Não Encontrado"	
	endif
Return nil

/*/{Protheus.doc} findReceitasDisponiveis
consulta os receituarios disponíveis para reaproveitamento
@type method
@version P12 
@author claudineia.reinert
@since 27/11/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonParam, object, query param
/*/
Method findReceitasDisponiveis(nPage, nPageSize, oJsonParam) class agdReceituarioRepository

    Local aArea         As Array
	Local cWhereParam   As Character
	Local cOrder        As Character
	Local jResponseJson as Object
    
	Default oJsonParam := Nil
	Default nPage          := 1
	Default nPageSize      := 10

	If Self:lOk
		aArea       := FwGetArea()
        AAdd(Self:aQueryJoinsRepository,{'INNER', 'NE7', Self:getWhereDefault("NE7.NE7_CODREC = NE6.NE6_CODREC AND NE7.NE7_QTDISP > 0", 'NE7')})
        cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:aFieldsRepository)
        cWhereParam += IIF(!Empty(cWhereParam), ' AND ', '') + "NE6.NE6_STATUS = '1' AND NE6.NE6_DOC = '' AND NE6.NE6_SERIE = '' "
		cOrder      := Self:getOrderQueryParam(oJsonParam, Self:aFieldsRepository)
		
		Self:setFieldsConsulta(Self:aFieldsRepository)
		Self:setPage(nPage)
		Self:setPageSize(nPageSize)
		//Self:SetQuery(Self:getQueryDefault(aArrayJoins))
		Self:SetQuery(Self:getQueryDefault(self:aQueryJoinsRepository) + " GROUP BY #QueryFields# ")
		Self:SetWhere(Self:getWhereDefault(cWhereParam))
		Self:SetUrlFilter(Self:getFilterAdvancedQueryParam(oJsonParam))
		Self:SetOrder( cOrder )

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf

Return Nil

/*/{Protheus.doc} agdReceituarioRepository::saveDadosReceitas
Salva o modelo AGDA070
@type method
@version P12
@author scheronlini.martins
@since 04/11/2025
@param aPayload, array, array com payload
/*/
Method saveDadosReceitas(jPayload) Class agdReceituarioRepository
	Local oModel,  oGrid
	Local nLine
	Local aFieldsNES, aFieldsNESCab, aFields
	Local cError := ""

	// ------------------------------------------------------------------
	// 1. Extrai os blocos de fields
	// ------------------------------------------------------------------
	aFieldsNES    := Self:getFieldsNESRece()
	aFieldsNESCab := Self:getFieldsNESCabec()

	// ------------------------------------------------------------------
	// 2. Carrega o modelo MVC
	// ------------------------------------------------------------------
	oModel := FwLoadModel("AGDA070")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()

	// ------------------------------------------------------------------
	// 3. Preenche o cabeçalho (AGDA070_SF2)
	// ------------------------------------------------------------------
	aFields := Self:jsonToArrayFields(jPayload, aFieldsNESCab)
	Self:setValueModel(oModel:GetModel("AGDA070_SF2"), aFields)

	// ------------------------------------------------------------------
	// 4. Preenche o grid de itens (NES_RECE)
	// ------------------------------------------------------------------

	oGrid := oModel:GetModel("NES_RECE")

	If jPayload:hasProperty("items")
		For nLine := 1 To Len(jPayload['items'])
			If nLine > 1
				oGrid:AddLine()
			EndIf
			aFields := Self:jsonToArrayFields(jPayload['items'][nLine], aFieldsNES)
			Self:setValueModel(oModel:GetModel("NES_RECE"), aFields)
		Next
	EndIf
	
	// ------------------------------------------------------------------
	// 5. Validação e gravação
	// ------------------------------------------------------------------
	If oModel:VldData()
		oModel:CommitData()
	Else
		cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + " - "
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID]) + " - "
		cError += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
		Self:setError(cError, 400)
	EndIf

	// ------------------------------------------------------------------
	// 6. Liberação de objetos
	// ------------------------------------------------------------------
	oModel:DeActivate()
	oModel:Destroy()
	oModel := Nil
	oGrid  := Nil

	FreeObj(oModel)
	FreeObj(oGrid)


Return .T.

/*/{Protheus.doc} agdReceituarioRepository::getFieldsNESRece
Retorna um array com os Campos do Modelo AGDA070 NES_RECE
@type method
@version P12 
@author scheronlini.martins
@since 04/11/2025
@return variant, return_array com os Campos do Modelo AGDA070 NES_RECE
/*/
Method getFieldsNESRece() Class agdReceituarioRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'documento'     , 'NES_DOC'    , 'C', .T.})
	AAdd(aArrayFields, {'serie'         , 'NES_SERIE'  , 'C', .T.})
	AAdd(aArrayFields, {'cliente'       , 'NES_CLIENT' , 'C', .T.})
	AAdd(aArrayFields, {'loja'          , 'NES_LOJA'   , 'C', .T.})
	AAdd(aArrayFields, {'tipoVinculo'   , 'NES_TPVINC' , 'C', .T.})
	AAdd(aArrayFields, {'item'          , 'NES_ITEM'   , 'C', .T.})
	AAdd(aArrayFields, {'numReceita'    , 'NES_NUMREC' , 'C', .T.})
	AAdd(aArrayFields, {'cpfEngenheiro' , 'NES_CGC'    , 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} agdReceituarioRepository::getFieldsNESCabec
Retorna um array com os Campos do Modelo AGDA070 AGDA070_SF2
@type method
@version P12
@author scheronlini.martins
@since 09/12/2025
@return variant, return_array com os Campos do Modelo AGDA070 AGDA070_SF2
/*/
Method getFieldsNESCabec() Class agdReceituarioRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'        , 'NES_FILIAL' , 'C', .T.})
	AAdd(aArrayFields, {'documento'     , 'NES_DOC'    , 'C', .T.})
	AAdd(aArrayFields, {'serie'         , 'NES_SERIE'  , 'C', .T.})
	AAdd(aArrayFields, {'cliente'       , 'NES_CLIENT' , 'C', .T.})
	AAdd(aArrayFields, {'loja'          , 'NES_LOJA'   , 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} getCodigoSolicitacaoByReceita
Retornar o código da solicitação vinculada a receita
@type method
@version P12
@author Gilson.Venturi
@since 05/12/2025
@param codReceita, character, Código da receita
@return Character, código da solicitação vinculada
/*/
Method getCodigoSolicitacaoByReceita(cdReceita) class agdReceituarioRepository
return POSICIONE( "NEU", 5, FWxFilial('NEU') + cdReceita, "NEU_CODIGO" )


/*/{Protheus.doc} isNFeReceita
Verificar se há NF vinculada nareceita
@type method
@version P12
@author Gilson.Venturi
@since 05/12/2025
@param cdIdExterno, character, id unico
@return Logical, .T. se houver vínculo com NF-e, .F. caso contrário
/*/
Method isNFeReceita(cdIdExterno) class agdReceituarioRepository
	cDocto := POSICIONE( "NE6", 2, FWxFilial('NE6') + cdIdExterno, "NE6_DOC" )
return !Empty(cDocto)


/*/{Protheus.doc} isReceitaVinculada
Verificar se receita está vinculada a solicitação válida. Status diferente de invalido
@type method
@version P12
@author Gilson.Venturi
@since 05/12/2025
@param cdIdExterno, character, id unico
@return Logical, .T. se houver vínculo com NF-e, .F. caso contrário
/*/
Method isReceitaVinculada(cdIdExterno) class agdReceituarioRepository
	Local cQuery     As character
	Local oStmt      As object
	Local lRet       := .F.

	cQuery := " SELECT COUNT(*) AS QTDE "
	cQuery += "   FROM " + RetSqlName("NE6") + " NE6 "
	cQuery += "  INNER JOIN " + RetSqlName("NEU") + " NEU "
	cQuery += "     ON NEU.NEU_FILIAL = ? "
	cQuery += "    AND NEU.NEU_CODREC = NE6.NE6_CODREC "
	cQuery += "    AND NEU.D_E_L_E_T_ = '' "
	cQuery += "  INNER JOIN " + RetSqlName("NET") + " NET "
	cQuery += "     ON NET.NET_FILIAL = ? "
	cQuery += "    AND NET.NET_CODIGO = NEU.NEU_CODIGO "
	cQuery += "    AND NET.NET_STSSOL = 1 " //1-Confirmada
	cQuery += "    AND NET.D_E_L_E_T_ = '' "
	cQuery += "  WHERE NE6.NE6_FILIAL = ? "
	cQuery += "    AND NE6.NE6_CODEXT = ? "
	cQuery += "    AND NE6.D_E_L_E_T_ = '' "

	cQuery := ChangeQuery(cQuery)

	// Prepara os parâmetros
	oStmt:= FWPreparedStatement():New()
	oStmt:SetQuery(cQuery)
	oStmt:SetString(1, FWxFilial("NEU"))
	oStmt:SetString(2, FWxFilial("NET"))
	oStmt:SetString(3, FWxFilial("NE6"))
	oStmt:SetString(4, cdIdExterno)

	nQtd := MPSysExecScalar(oStmt:GetFixQuery(), "QTDE")
	if (nQtd > 0)
		lRet := .T.
	else
		lRet := .F.
	endif
return lRet

/*/{Protheus.doc} cancelarReceita
Cancelar Receita Agronômica e desvincular na Solicitação de Receita
@type method
@version P12
@author Gilson.Venturi
@since 05/12/2025
@param cdIdExterno, character, id unico
/*/
Method cancelarReceita(cdIdExterno) class agdReceituarioRepository
	Local oBloqFatService  as object
	Local oModel120        as object
	Local oModel040        as object
	Local oNE6             as object
	Local oNET             as object
	Local oNEU             as object

	Local aAreaNE6 := NE6->(GetArea())
	Local aAreaNET := NET->(GetArea())
	Local aAreaNEU := NEU->(GetArea())

	Local nI      := 1
	Local cCodigo := ''
	Local lTemSC9 := .F.

	if Self:isReceitaVinculada(cdIdExterno)
		if Self:isNFeReceita(cdIdExterno)
			Self:setError(STR0002, 400) //"Não é possível cancelar a receita vinculada a uma NF-e válida"
		else

			dbSelectArea('NE6')
			NE6->(dbSetOrder(2)) //NE6_FILIAL+NE6_CODEXT
			NE6->(dbSeek(FWxFilial('NE6') + cdIdExterno))

			// Carrega o Model da rotina AGDA120 - Receita Agronômica
			oModel120 := FwLoadModel("AGDA120")
			oModel120:SetOperation(MODEL_OPERATION_UPDATE)
			oModel120:Activate()

			oNE6 := oModel120:GetModel("AGDA120_NE6")
			oNE6:SetValue("NE6_STATUS", '2') //2-Cancelada

			oModel120:VldData()
			oModel120:CommitData()
			oModel120:DeActivate()
			oModel120:Destroy()
			oModel120 := NIL

			cCodigo := Self:getCodigoSolicitacaoByReceita(NE6->NE6_CODREC)

			dbSelectArea('NET')
			NET->(dbSetOrder(1))
			NET->(dbSeek( FWxFilial('NET')+ cCodigo))

			// Carrega o Model da rotina AGDA040 - Solicitação de Receita
			oModel040 := FwLoadModel("AGDA040")
			oModel040:SetOperation(MODEL_OPERATION_UPDATE)
			oModel040:Activate()

			oNET := oModel040:GetModel("AGDA040_NET")
			oNEU := oModel040:GetModel("AGDA040_NEU")

			oNET:SetValue("NET_STSREC", '2') // '2 - Receita Cancelada'

			For nI := 1 To oNEU:Length()
				oNEU:GoLine(nI)
				oNEU:SetValue("NEU_CODREC", '')
				oNEU:SetValue("NEU_SEQREC", '' )
			Next
				
			oModel040:VldData()
			oModel040:CommitData()
			oModel040:DeActivate()
			oModel040:Destroy()
			oModel040 := NIL

			// Efetua bloqueio de faturamento para os itens da receita
			oBloqFatService := agdBloqueioFaturamentoService():New()
			dbSelectArea('NEU')
			NEU->(dbSetOrder(1)) // NEU_FILIAL+NEU_CODIGO
			if NEU->(dbSeek( FWxFilial('NEU') + NET->NET_CODIGO))
				while NEU->(!EoF()) .and. NEU->(NEU_FILIAL + NEU_CODIGO) == FWxFilial('NEU') + NET->NET_CODIGO
					lTemSC9 := .T.
				    oBloqFatService:gerarBloqueioReceita(NEU->NEU_NUMPED, NEU->NEU_ITEMPE, NEU->NEU_SEQLIB, NEU->NEU_PRODUT)
					NEU->(dbSkip())
				EndDo
			EndIf
			FreeObj(oBloqFatService)


			// Gerar cópia da Solicitação de Receita
			if (lTemSC9)
				oModel040 := FwLoadModel("AGDA040")
				oModel040:SetOperation(MODEL_OPERATION_INSERT)
				oModel040:Activate(.T.) // Ativa com cópia mantendo os dados atuais
				oNET := oModel040:GetModel("AGDA040_NET")
				oNET:SetValue("NET_STSREC", '0') // '0 - Pendente'
				oModel040:VldData()
				oModel040:CommitData()
				oModel040:DeActivate()
				oModel040:Destroy()
				oModel040 := NIL
			endif

		endif
	endif

	RestArea(aAreaNE6)
	RestArea(aAreaNET)
	RestArea(aAreaNEU)

Return Nil
