#include "totvs.ch"
#include 'tlpp-core.th'

namespace agd.TCODeParaService
using namespace agd.utilsService
using namespace agd.tcoDeParaRepository

/*/{Protheus.doc} agdTCODeParaService
Classe de serviços do TCO de-para
@type class
@version 12
@author Gilson.Venturi
@since 15/10/2025
/*/
class agdTCODeParaService FROM agdUtilsService
	public method new()
	public method findByFeature()
	public method formatInternalIDcliente()        as character
	public method formatInternalIDfornecedor()     as character
	public method formatInternalIDdocEntradaItem() as character
	public method formatInternalIDdocSaidaItem()   as character
	public method formatInternalIdProduto()        as character
	public method formatInternalIdSolReceita()	   as character
	public method formatInternalIdPropriedade()	   as character
	public method formatInternalIdCultura()		   as character
	public method formatInternalIdRespTecnico()	   as character
	public method formatInternalIdDescarte()	   as character

	public method getExternalID()				   as character
	private method setInternalID()				   as character
endClass

/*/{Protheus.doc} new
Construtor
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@return object, Instancia da classe
/*/
method new() class agdTCODeParaService
	_Super:New()
return Self


/*/{Protheus.doc} findByFeature
Buscar Por Id Unico
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@param cCodigo, character, id unico
@return variant, nil
/*/
method findByFeature(cCodigo, jQueryParam, nPage, nPageSize) class agdTCODeParaService
	Local oRepository		As object
	Local oResponseMerge	As object
	Default nPage     := 1
	Default nPageSize := 10

	oRepository := agdTCODeParaRepository():New()
	oRepository:findByFeature(cCodigo, jQueryParam, nPage, nPageSize)

	oResponseMerge := oRepository:getResponse()

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} agdTCODeParaService::formatInternalIDcliente
Formata o codigo do cliente no padrao utilizado pelo EAI
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@param cEmpresa, character, empresa
@param cFil, character, filial
@param cCliente, character, cliente
@param cLoja, character, loja
@return character, valor formatado
/*/
method formatInternalIDcliente(cEmpresa as character, cFil as character, cCliente as character, cLoja as character) as character class agdTCODeParaService
return IntCliExt(cEmpresa, cFil, cCliente, cLoja)[2]

/*/{Protheus.doc} agdTCODeParaService::formatInternalIDfornecedor
Formata o codigo do fornecedor no padrao utilizado pelo EAI
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@param cEmpresa, character, empresa
@param cFil, character, filial
@param cFornecedor, character, fornecedor
@param cLoja, character, loja
@return character, valor formatado
/*/
method formatInternalIDfornecedor(cEmpresa as character, cFil as character, cFornecedor as character, cLoja as character) as character class agdTCODeParaService
return IntForExt(cEmpresa, cFil, cFornecedor, cLoja)[2]

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdProduto
Formata o codigo do produto no padrao utilizado pelo EAI
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@param cEmpresa, character, empresa
@param cFilialSB1, character, filial
@param cCodigoProduto, character, produto
@return character, valor formatado
/*/
method formatInternalIdProduto(cEmpresa as character, cFilialSB1 as character, cCodigoProduto as character) as character class agdTCODeParaService
return IntProExt(cEmpresa, cFilialSB1, cCodigoProduto)[2]

/*/{Protheus.doc} agdTCODeParaService::formatInternalIDdocEntradaItem
Formata a chave do item do documento de entrada no padrao utilizado pelo EAI
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@param cEmpresa, character, codigo empresa
@param cFilialSF1, character, codigo filial
@param cDoc, character, documento
@param cSerie, character, serie
@param cFornecedor, character, fornecedor
@param cLoja, character, loja
@param cCodigoProduto, character, produto
@param cItem, character, item
@return character, valor formatado
/*/
method formatInternalIDdocEntradaItem(cEmpresa as character, cFilialSF1 as character, cDoc as character, cSerie as character, cFornecedor as character, cLoja as character, cCodigoProduto as character, cItem as character) as character class agdTCODeParaService
return IntInvExt(cEmpresa, cFilialSF1, cDoc, cSerie, cFornecedor, cLoja, cCodigoProduto, cItem)[2]

/*/{Protheus.doc} agdTCODeParaService::formatInternalIDdocSaidaItem
Formata a chave do item do documento de saida no padrao utilizado pelo EAI
@type method
@version 12
@author jc.maldonado
@since 17/11/2025
@param cEmpresa, character, codigo empresa
@param cFilialSF2, character, codigo filial
@param cDoc, character, documento
@param cSerie, character, serie
@param cCliente, character, cliente
@param cLoja, character, loja
@param cCodigoProduto, character, produto
@param cItem, character, item
@return character, valor formatado
/*/
method formatInternalIDdocSaidaItem(cEmpresa as character, cFilialSF2 as character, cDoc as character, cSerie as character, cCliente as character, cLoja as character, cCodigoProduto as character, cItem as character) as character class agdTCODeParaService
	default cEmpresa   := cEmpAnt
	default cFilialSF2 := FWxFilial('SF2')

	cItem := PadR(rTrim(cItem), TamSX3("D2_ITEM")[1])
return cEmpresa + '|' + cFilialSF2 + '|' + cDoc + '|' + cSerie + '|' + cCliente + '|' + cLoja + '|' + cCodigoProduto + '|' + cItem

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdSolReceita
Formata a chave da Solicitacao de Receita pelo EAI
@type method
@version 12
@author lindem
@since 25/11/2025
@param cCodigo, character, codigo da solicitacao
@return character, valor formatado
/*/
method formatInternalIdSolReceita(cCodigo as character) as character class agdTCODeParaService
	Local cEmpresa	:= cEmpAnt
	Local cFil 		:= FWxFilial('NET')
return self:setInternalID({cEmpresa, cFil, cCodigo})

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdPropriedade
Formata a chave da Propriedade pelo EAI
@type method
@version 12
@author lindem
@since 25/11/2025
@param cCliente, character, cliente
@param cLoja, character, loja
@return character, valor formatado
/*/
method formatInternalIdPropriedade(cCliente as character, cLoja as character) as character class agdTCODeParaService
	Local cEmpresa	:= cEmpAnt
	Local cFil 		:= FWxFilial('NEP')
return self:setInternalID({cEmpresa, cFil, cCliente, cLoja})

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdCultura
Formata a chave da Cultura pelo EAI
@type method
@version 12
@author lindem
@since 25/11/2025
@param cCodigo, character, codigo da cultura
@return character, valor formatado
/*/
method formatInternalIdCultura(cCodigo as character) as character class agdTCODeParaService
	Local cEmpresa	:= cEmpAnt
	Local cFil 		:= FWxFilial('NP3')
return self:setInternalID({cEmpresa, cFil, cCodigo})

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdRespTecnico
Formata a chave do Responsavel Tecnico pelo EAI
@type method
@version 12
@author lindem
@since 25/11/2025
@param cCodigo, character, codigo do responsavel tecnico
@return character, valor formatado
/*/
method formatInternalIdRespTecnico(cCodigo as character) as character class agdTCODeParaService
	Local cEmpresa	:= cEmpAnt
	Local cFil		:= FWxFilial('NP8')
return self:setInternalID({cEmpresa, cFil, cCodigo})

/*/{Protheus.doc} agdTCODeParaService::formatInternalIdDescarte
Formata a chave do Responsavel Tecnico pelo EAI
@type method
@version 12
@author lindem
@since 25/11/2025
@param cCodigo, character, codigo do responsavel tecnico
@return character, valor formatado
/*/
method formatInternalIdDescarte(cCodigo as character) as character class agdTCODeParaService
	Local cEmpresa	:= cEmpAnt
	Local cFil 		:= FWxFilial('NEO')
return self:setInternalID({cEmpresa, cFil, cCodigo})

/*/{Protheus.doc} agdTCODeParaService::setInternalID
Formata a chave para gravacao de internalId pelo EAI
@type method
@version 12
@author lindembergson.pacheco
@since 19/11/2025
@param aDados, Array, dados para geracao de chave EAI
//dados obrigatorios para geracao de chave 
	cEmpAnt
	cFilAnt
	demais campos
@return character, valor formatado
/*/
method setInternalID(aDados as Array) as character class agdTCODeParaService
	Local cId	:= ""
	AEval( aDados, { |x| cId += x + "|" } )
	cId := SubStr(cId,1, LEN(cId) -1)
return cId

/*/{Protheus.doc} agdTCODeParaService::getExternalID
Recupera External ID pelo EAI
@type method
@version 12
@author lindembergson.pacheco
@since 24/11/2025
@param cCodeInteg, Character, Codigo da Integracao EAI
@param cAlias, Character, Alias da Tabela
@param cField, Character, Campo da Tabela
@param cIdInter, Character, internalId do registro
@return character, valor formatado
/*/
method getExternalID(cCodeInteg, cAlias, cField, cIdInter) as character class agdTCODeParaService
	Local cExternalId := CFGA070Ext(cCodeInteg, cAlias, cField, cIdInter)
return AllTrim(cExternalId)
