#include "totvs.ch"
#include 'tlpp-core.th'
#include 'AGD.TCO.SERVICE.CH'

namespace agd.TCOservice
using namespace agd.utilsService
using namespace agd.tcoRepository
using namespace agd.tcoFeatureComplementosRepository
using namespace agd.restUtils
using namespace agd.tcoDeParaCulturaRepository
using namespace agd.agdTCOComercializacaoTACValidator

/*/{Protheus.doc} agdTCOservice
Classe de serviços do TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdTCOservice FROM agdUtilsService

	public Data nPage     As Integer
	public Data nPageSize As Integer

	public method new()

	public method getFeatures()
	public method getById()
	public method registrarFeature()
	public method getStatus()
	public method getFeaturesDisponiveis()
	public method inativar()
	public method ativar()
	public method getFeatureCadastro()
	public method getFeatureParametros()
	public method parametrosFeature(cCodigoFeature, aParametros)
	public method finalizarFeature()
	public method registrarDePara()
	public method validCadastrosBasicos()
	public method validParametrosAtuais()
	public method getFeatureRotas()
	public method getFeatureAdapter()
	public Method checkIntegrationConnectivity() as logical
	public method registrarFeatureComplemento() as logical
	public method registrarFeatureComplementoAll() as logical
	public method listarFeatureComplemento()
	public method deleteDePara()
	public method importExternalIdsFromFeature()
endClass

/*/{Protheus.doc} new
Construtor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method new() class agdTCOservice
	_Super:New()
return Self

/*/{Protheus.doc} getFeatures
Listar Features do Módulo
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method getFeatures(nPage, nPageSize, oJsonQryParam) class agdTCOservice
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdTCORepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} getById
Busca no repositório TCO as informações completas de uma feature específica identificada pelo código informado.
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, param_feature
/*/
method getById(cFeature) class agdTCOservice
	Local oRepository As Object

	oRepository := agdTCORepository():New()
	oRepository:findById(oRepository:getFilterById(cFeature))

	if oRepository:isSuccess()
		jFeature := oRepository:getResponse()
		jFeature['detalhe'] := oRepository:getDetalheFeature(cFeature)
		Self:setSuccess(jFeature)
	else
		Self:setFullResponse(oRepository)
	endif
	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} registrarFeature
Comando para registrar uma feature
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method registrarFeature(jCmdRegistrar) class agdTCOservice
	Local oRepository As Object

	oRepository := agdTCORepository():New()
	oRepository:save(jCmdRegistrar)
	if oRepository:isSuccess() .and. jCmdRegistrar:hasProperty('codigo')
		::importExternalIdsFromFeature(jCmdRegistrar['codigo'])
	endIf
	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} getStatus
Retorna o status de configuração do TCO
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return character, json com os status
/*/
method getStatus() class agdTCOservice
	local oTCOrepository as object
	local jPayLoad       as json
	Local aCadastros    := {}
	Local aFeatures	    := {}
	Local nX			 as numeric

	oTCOrepository := agdTCORepository():New()

	jPayLoad := JsonObject():New()
	jPayLoad['moduloativo']            := oTCOrepository:isSIGAAGDactive()
	jPayLoad['percentualconfiguracao'] := oTCOrepository:getConfigPercentage()
	jPayLoad['featuresativas']         := oTCOrepository:getTotalActiveFeatures()

	aFeatures := oTCOrepository:getAllClassFeaturesAtivas()

	For nX:= 1 to Len(aFeatures)
		aEval(aFeatures[nX][1]:getCadastrosBasicos(), {|xCadastro| aAdd(aCadastros, xCadastro['rotina'])})
	Next
	
	jPayLoad['cadastrosefetuados'] := len(aCadastros)

	Self:setSuccess(jPayLoad)

	FWFreeObj(oTCOrepository)

return nil


/*/{Protheus.doc} getFeaturesDisponiveis
Lista de Features disponiveis
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@param cCodigoFeature, character, Codigo da Feature para filtro
@return variant, lista de features
/*/
method getFeaturesDisponiveis(cCodigoFeature as character) class agdTCOservice
	local oTCOrepository as object
	local oFeature       as object
	local jResponse      as json
	Local nX             as numeric
	local aItems         := {}

	oTCOrepository := agdTCORepository():New()
	listaFeatures := oTCOrepository:findFeaturesDisponiveis()

	For nX:= 1 to Len(listaFeatures)
		oFeature := &(listaFeatures[nX, 2]+":new()")

		if(Empty(cCodigoFeature) .or. alltrim(upper(cCodigoFeature)) == oFeature:getCodigo() )
			aadd(aItems, JsonObject():New())
			aItems[len(aItems)]['codigo']    := oFeature:getCodigo()
			aItems[len(aItems)]['descricao'] := oFeature:getDescricao()
			aItems[len(aItems)]['release']   := oFeature:getRelease()
			aItems[len(aItems)]['tipo']      := oFeature:getTipo()
		endif

		FWFreeObj(oFeature)
	Next

	jResponse  := JsonObject():New()
	jResponse['hasNext'] := .F.
	jResponse['items']   := aItems

	Self:setSuccess(jResponse)

	FWFreeObj(oTCOrepository)

return nil

/*/{Protheus.doc} inativar
Comando para inativar uma feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, codigo feature
@return variant, retorno
/*/
method inativar(cFeature) class agdTCOservice
	Local oRepository As Object

	oRepository := agdTCORepository():New(xfilial('NE1')+cFeature)

	oRepository:updateRepository({{"NE1_ATIVO", "2"}})

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} ativar
Comando para ativar uma feature
@type method
@version 12
@author jean.schulze
@since 07/10/2025
@param cFeature, character, codigo da feature
@return variant, retorno
/*/
method ativar(cFeature) class agdTCOservice
	Local oRepository As Object

	oRepository := agdTCORepository():New(xfilial('NE1')+cFeature)

	oRepository:updateRepository({{"NE1_ATIVO", "1"}})

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil


/*/{Protheus.doc} getFeatureCadastro
Retorna os cadastros da feature
@type method
@version 12
@author jc.maldonado
@since 08/10/2025
@param cCodigoFeature, character, codigo da feature
/*/
method getFeatureCadastro(cCodigoFeature as character) class agdTCOservice
	local oTCOrepository as object
	local oFeatureClass  as object
	local aCadastros     as array
	local aItems         as array
	local nX             as integer
	local jResponse      as json

	oTCOrepository := agdTCORepository():New()

	oFeatureClass := oTCOrepository:getFeatureClassById(cCodigoFeature)
	if oFeatureClass == nil
		::setError(STR0001, 404) //"Feature inválida"
		FWFreeObj(oTCOrepository)
		return
	endIf

	aCadastros := oFeatureClass:getCadastrosBasicos()
	FWFreeObj(oFeatureClass)

	aItems := {}
	for nX := 1 to Len(aCadastros)
		cCompartilhamento := FwSX2Util():GetSX2Data(aCadastros[nX]['tabela'], {"X2_MODO"}, /*lQuery*/ .T.)[1, 2]

		aadd(aItems, JsonObject():New())
		aItems[nX]['rotina']    := aCadastros[nX]['rotina']
		aItems[nX]['cadastro']  := FwSX2Util():GetX2Name(aCadastros[nX]['tabela'])
		aItems[nX]['filial']    := iIf(cCompartilhamento == "C", STR0002, STR0003) //"Compartilhada", "Exclusiva"
		aItems[nX]['registros'] := oTCOrepository:getTotalRegistrations(aCadastros[nX]['tabela'])
	next nX

	FWFreeArray(aCadastros)

	jResponse            := JsonObject():New()
	jResponse['hasNext'] := .F.
	jResponse['items']   := aItems

	::setSuccess(jResponse)

	FWFreeObj(oTCOrepository)
return

/*/{Protheus.doc} getFeatureParametros
Retorna os parametros da feature
@type method
@version 12
@author jc.maldonado
@since 08/10/2025
@param cCodigoFeature, character, codigo da feature
/*/
method getFeatureParametros(cCodigoFeature as character) class agdTCOservice
	local oTCOrepository    as object
	local oFeatureClass     as object
	local aFeatureParams    as array
	local aParamAllBranches as array
	local oAGDrepository    as object
	local aJsonParam        as array
	local nX                as integer
	local nY                as integer
	local nIt               as integer
	local cDescricao        as character
	local jResponse         as json

	oTCOrepository := agdTCORepository():New()

	oFeatureClass := oTCOrepository:getFeatureClassById(cCodigoFeature)
	if oFeatureClass == nil
		::setError(STR0001, 404) //Feature Invalida
		FWFreeObj(oTCOrepository)
		return
	endIf

	aFeatureParams := oFeatureClass:getParametros()
	FWFreeObj(oFeatureClass)

	aJsonParam := {}
	for nX := 1 to Len(aFeatureParams)
		if ! FWSX6Util():ExistsParam(aFeatureParams[nX]['parametro'])
			loop
		endIf

		GetMV(aFeatureParams[nX]['parametro'])
		cDescricao := rtrim(x6Descric()) + " " + rtrim(X6Desc1()) + " " + rTrim(X6Desc2())

		if empty(SX6->X6_FIL)
			aParamAllBranches := {{STR0002, aFeatureParams[nX]['parametro'], alltrim(x6Conteud())}} //Compartilhada
		else
			aParamAllBranches := oTCOrepository:getParameterForAllBranches(aFeatureParams[nX]['parametro'])
		endIf

		for nY := 1 to len(aParamAllBranches)
			nIt++
			aAdd(aJsonParam, JSonObject():New())
			aJsonParam[nIt]['filial']           := aParamAllBranches[nY, 1]
			aJsonParam[nIt]['codigo']           := aParamAllBranches[nY, 2]
			aJsonParam[nIt]['valoratual']       := aParamAllBranches[nY, 3]
			aJsonParam[nIt]['valorespossiveis'] := aFeatureParams[nX]['options']
			aJsonParam[nIt]['descricao']        := cDescricao
		next nY
	next nX

	jResponse            := JsonObject():New()
	jResponse['hasNext'] := .F.
	jResponse['items']   := aJsonParam

	::setSuccess(jResponse)

	FWFreeObj(oAGDrepository)
	FWFreeObj(oTCOrepository)
return

/*/{Protheus.doc} parametrosFeature
Atualiza os parâmetros configuráveis de uma feature do módulo TCO.
@type method
@version p12 
@author scheronlini.martins
@since 15/10/2025
@param cCodigoFeature, character, codigo da feature
@param aParametros, array, parametros
@return variant, return_json 
/*/
method parametrosFeature(cCodigoFeature, aParametros) class agdTCOService
	Local jResp        := JsonObject():New()
	Local aSucesso     := {}
	Local oRepo        := agdTCORepository():New()
	Local aFeatures    := oRepo:getAllClassFeaturesAtivas()
	Local oFeat        := Nil
	Local aPermitidos  := {}
	Local nI, nF       := 0
	Local cFil         := ""
	Local cParametro   := ""
	Local xValor       := Nil
	Local lEncontrou   := .F.
	Local nPos         := 0
	Local xEsperado    := Nil
	Local cTipoEnviado := ""
	Local cAlias       := ""
	Local cConteudoAnt := ""
	Local xPermitidos

	cAlias       := GetNextAlias()

	If !FWIsAdmin()
		::setError(STR0010,400) //"Acesso negado. Apenas administradores podem alterar parâmetros."
		Return
	EndIf

	For nF := 1 To Len(aFeatures)
		oFeat := aFeatures[nF][1]

		If Upper(AllTrim(oFeat:getCodigo())) == Upper(AllTrim(cCodigoFeature))
			lEncontrou  := .T.
			aPermitidos := oFeat:getParametros()
			Exit
		EndIf
	Next nF

	If !lEncontrou
		::setError(STR0022 + alltrim(cCodigoFeature) + STR0029,400) //"Feature "  //" não foi encontrada."
		Return
	EndIf

	If Empty(aPermitidos)
		::setError(STR0022 + alltrim(cCodigoFeature) + STR0030,400) //"A feature '"  //" não possui parâmetros configuráveis."
		Return
	EndIf

	For nI := 1 To Len(aParametros)

		cFil       := PadR(AllTrim(aParametros[nI]["filial"]), 8)
		cParametro := PadR(AllTrim(aParametros[nI]["parametro"]), 10)
		xValor     := aParametros[nI]["valor"]

		DbSelectArea("SX6")
		DbSetOrder(1)

		If ValType(xValor) == "C"

			// Converte texto ".T." ou ".F." em tipo lógico
			If Upper(AllTrim(xValor)) == ".T."
				xValor := .T.
			ElseIf Upper(AllTrim(xValor)) == ".F."
				xValor := .F.
			EndIf

			cTipoEnviado := ValType(xValor)

			// Se ainda for caractere, tratar aspas e conteúdo anterior
			If ValType(xValor) == "C"
				If DbSeek(cFil + cParametro)
					cConteudoAnt := AllTrim(SX6->X6_CONTEUD)

					// Remove a última aspa caso exista
					If !Empty(cConteudoAnt) .And. (Right(cConteudoAnt,1) == '"' .Or. Right(cConteudoAnt,1) == "'")
						cConteudoAnt := SubStr(cConteudoAnt, 1, Len(cConteudoAnt)-1)
						xValor := cConteudoAnt + AllTrim(CValToChar(xValor)) + SubStr(cConteudoAnt, 1, 1)
					EndIf
				Else
					// Envolve o valor entre aspas caso não exista conteúdo anterior
					xValor := '"' + AllTrim(CValToChar(xValor)) + '"'
				EndIf
			EndIf
		EndIf

		If Empty(IIF(ValType(xValor) == 'L', CValToChar(xValor), xValor)) .Or. Empty(cParametro)
			::setError(cParametro + " - " + STR0011,400) //"Parâmetro ou conteúdo do parâmetro não informado."
			Return
		EndIf
		
		nPos := AScan(aPermitidos, {|x| x['parametro'] == alltrim(cParametro)})

		If nPos == 0
			::setError(cParametro + " - " + STR0031 + alltrim(cCodigoFeature) + ".",400)//"Parâmetro não pertence à configuração da feature "
			return
		Else

			xPermitidos := aPermitidos[nPos]['options']
			xEsperado   := aPermitidos[nPos]['valorEsperado']

			If ValType(xPermitidos) == "A"

				If Len(xPermitidos) > 0

					If Upper(AllTrim(xPermitidos[1]["value"])) == ".T." .or. Upper(AllTrim(xPermitidos[1]["value"])) == ".F."
						cTipoParam := "L"
					else
						cTipoParam := ValType(xPermitidos[1]["value"])
					EndIf

					// Verifica se o tipo do valor enviado bate com o permitido
					If !(  cTipoParam == ValType(xValor) )
						::setError(;
							cParametro + " - " + STR0032 + cParametro + ;// STR0032 = "Tipo de valor diferente do esperado no parâmetro "
						STR0019 + cTipoParam + ;// STR0019 = " deveria ser "
						STR0033 + ValType(xValor) + ").", 400 ) // STR0033 = ", recebido "
						Return
					EndIf
				EndIf
			Else
				If !Empty(xEsperado)
					If !( VALTYPE(xEsperado) $ VALTYPE(xValor))
						::setError(;
							cParametro + " - " + STR0032 + cParametro + ;// STR0032 = "Tipo de valor diferente do esperado no parâmetro "
						STR0019 + VALTYPE(xEsperado) + ; // STR0019 = " deveria ser "
						STR0033 + VALTYPE(xValor) + ")." , 400)// STR0033 = ", recebido "
						Return
					EndIf
				EndIf
			EndIf
			if !fwFilExist(FwArrFilAtu()[1],cfil)
				::setError(cParametro + " - " + STR0036,400) //"Não foi possível localizar a filial informada no cadastro."
				return
			Else

				If DbSeek(cFil + cParametro)

					PutMVFil(cParametro, xValor, iif( Empty(cFil),'*',cFil ))

					AAdd(aSucesso, {cParametro, STR0012}) //"Atualizado com sucesso.

				Else

					IF FWSX6Util():ExistsParam(cParametro)

						FWSX6Util():ReplicateParam( cParametro ,iif( Empty(cFil),'*',{cFil} ))

						PutMVFil(cParametro, xValor, iif( Empty(cFil),'*',cFil ))

						AAdd(aSucesso, {cParametro, STR0035 + alltrim(cfil) + " )"}) //"Parâmetro criado com sucesso (nível filial "
					Else
						::setError(cParametro + " - " + STR0037,400) //"É necessario criar este parametro no configurador"
						return
					EndIf
				EndIf
			EndIf
		EndIf

	Next nI

	jResp["feature"] := alltrim(cCodigoFeature)
	jResp["success"] := .T.
	jResp["updated"] := aSucesso
	jResp["message"] := STR0013 //"Parâmetros atualizados com sucesso."

	::setSuccess(jResp)

Return

/*/{Protheus.doc} finalizarFeature
Finaliza a configuração de uma Feature de Negócio no TCO.
Executa validações: (1) Cadastros Básicos (>=1 registro por rotina) e
(2) Parâmetros Obrigatórios (valor atual não pode ser vazio).
Se ambas aprovarem, atualiza NE1_STATUS para "2" (Finalizado).
@type method
@version 12
@author rodrigo.nsoledade
@since 14/10/2025
@param cCodigoFeature, character, código da feature a finalizar
@return variant, nil
/*/
method finalizarFeature(cCodigoFeature) class agdTCOservice
	local oRepoLookup    as object
	local oRepoUpdate    as object
	local oFeatureClass  as object
	local oJsonReponse   as object

	// Obtem feature (valida existência)
	oRepoLookup   := agdTCORepository():New()
	oFeatureClass := oRepoLookup:getFeatureClassById(cCodigoFeature)
	if oFeatureClass == nil
		Self:setError(STR0001, 404) //"Feature inválida"
		FWFreeObj(oRepoLookup)
		return
	endIf

	// Valida Cadastros Básicos
	if ! Self:validCadastrosBasicos(oRepoLookup, oFeatureClass)
		FWFreeObj(oFeatureClass)
		FWFreeObj(oRepoLookup)
		return
	endIf

	// Valida Parâmetros (valor atual)
	if ! Self:validParametrosAtuais(oRepoLookup, oFeatureClass)
		FWFreeObj(oFeatureClass)
		FWFreeObj(oRepoLookup)
		return
	endIf

	// Atualiza status da Feature para "2" (Finalizado)
	oRepoUpdate := agdTCORepository():New(xFilial("NE1") + cCodigoFeature)
	oRepoUpdate:updateRepository({ {"NE1_STATUS","2"} })

	if oRepoUpdate:isSuccess()
		oJsonReponse := JsonObject():New()
		oJsonReponse['code']    := 200
		oJsonReponse['message'] := STR0015 //"Feature finalizada com sucesso."
		Self:setSuccess(oJsonReponse)
	endIf

	// Liberação
	oRepoUpdate:DeActivate()
	FreeObj(oRepoUpdate)
	FWFreeObj(oFeatureClass)
	FWFreeObj(oRepoLookup)
return .T.

/*/{Protheus.doc} validCadastrosBasicos
Valida os cadastros básicos da feature: cada rotina configurada deve
possuir pelo menos 1 registro (usa getTotalRegistrations).
@type method
@version 12
@author rodrigo.nsoledade
@since 15/10/2025
@param oRepoLookup, object, repositório para consultas
@param oFeatureClass, object, classe/configuração da feature
@return logical, .T. se válido; .F. se inválido (já chama ::setError em caso de erro)
/*/
method validCadastrosBasicos(oRepoLookup, oFeatureClass) class agdTCOservice
	local aCadastros as array
	local nX         as integer
	local nTot       as integer

	aCadastros := oFeatureClass:getCadastrosBasicos()
	for nX := 1 to Len(aCadastros)
		nTot := oRepoLookup:getTotalRegistrations(aCadastros[nX]['tabela'])
		if nTot == 0
			Self:setError(STR0016 + aCadastros[nX]['rotina'], 400) //"Cadastro não possui nenhum registro cadastrado: "
			return .F.
		endIf
	next nX

return .T.

/*/{Protheus.doc} validParametrosAtuais
Valida parâmetros obrigatórios da feature: o "valor atual" não pode ser vazio.
Segue o padrão do getFeatureParametros (coluna 3 = valor atual).
Se SX6 for compartilhado, lê direto; caso contrário, verifica em todas as filiais.
@type method
@version 12
@author rodrigo.nsoledade
@since 15/10/2025
@param oRepoLookup, object, repositório para consultas
@param oFeatureClass, object, classe/configuração da feature
@return logical, .T. se válido; .F. se inválido (já chama ::setError em caso de erro)
/*/
method validParametrosAtuais(oRepoLookup, oFeatureClass) class agdTCOservice
	local aParams            as array
	local aParamAllBranches  as array
	local nX, nY             as integer
	local cPendentes         as character
	local cSep               as character

	cPendentes := ""
	cSep       := ""

	aParams := oFeatureClass:getParametros()

	for nX := 1 to Len(aParams)
		// Ignora parâmetros não existentes
		if ! FWSX6Util():ExistsParam(aParams[nX]['parametro'])
			loop
		endIf

		// Prepara contexto (mesma referência de getFeatureParametros)
		GetMV(aParams[nX]['parametro'])

		if Empty(SX6->X6_FIL)
			// Parametrização compartilhada
			aParamAllBranches := { {"Compartilhada", aParams[nX]['parametro'], AllTrim(x6Conteud())} }
		else
			// Valor por filial
			aParamAllBranches := oRepoLookup:getParameterForAllBranches(aParams[nX]['parametro'])
		endIf

		for nY := 1 to Len(aParamAllBranches)
			if Empty(AllTrim(aParamAllBranches[nY, 3]))
				cPendentes += cSep + aParamAllBranches[nY, 2]
				cSep := ", "
			endIf
		next nY
	next nX

	if ! Empty(cPendentes)
		Self:setError(STR0017 + cPendentes, 400) //"Parâmetros obrigatórios sem configuração: "
		return .F.
	endIf

return .T.


/*/{Protheus.doc} getFeatureRotas
Atualiza os parâmetros configuráveis de uma feature do módulo TCO.
@type method
@version p12 
@author Gilson.Venturi
@since 15/10/2025
@param cCodigoFeature, character, codigo da feature
/*/
method getFeatureRotas(cCodigoFeature as character) class agdTCOservice
	Local oTCOrepository as object
	Local jResponse      as json

	oTCOrepository := agdTCORepository():New()
	jResponse := oTCOrepository:findFeaturesRotas(cCodigoFeature)

	Self:setSuccess(jResponse)
	
	FWFreeObj(oTCOrepository)

return nil

/*/{Protheus.doc} getFeatureAdapter
Atualiza os parâmetros configuráveis de uma feature do módulo TCO.
@type method
@version p12 
@author Gilson.Venturi
@since 15/10/2025
@param cCodigoFeature, character, codigo da feature
/*/
method getFeatureAdapter(cCodigoFeature as character) class agdTCOservice
	Local oTCOrepository as object
	Local jResponse      as json
	Local aItems         := {}

	oTCOrepository := agdTCORepository():New()
	aItems := oTCOrepository:findFeaturesAdapter(cCodigoFeature)

	if !empty(aItems)
		jResponse  := JsonObject():New()
		jResponse['items']   := aItems
		jResponse['hasNext'] := .F.

		Self:setSuccess(jResponse)
	Else
		Self:SetError(STR0044, 400) //"Feature não encontrada"
	Endif

	FWFreeObj(oTCOrepository)

return nil

/*/{Protheus.doc} checkIntegrationConnectivity
Valida as conexões da feature de integracao (rotas EAI - tabela XAI)
@type method
@version 12
@author jc.maldonado
@since 27/10/2025
@param cFeature, character, codigo da feature
@return logical, resultado da verificacao
/*/
method checkIntegrationConnectivity(cFeature as character) as logical class agdTCOService
	Local oTCOrepository as object
	Local oFeatureClass  as object
	Local cResponse      as character
	Local cHeaderGet     as character
	Local cError         as character
	Local aErros         as array
	Local nHttpStatus    as numeric
	Local lValid         as logical

	oTCOrepository := agdTCORepository():New()
	oFeatureClass  := oTCOrepository:getFeatureClassById(cFeature)
	if oFeatureClass == nil;
			.Or. oFeatureClass:getTipo() != '2'; //'2'=Integração;
			.Or. aScan(ClassMethArr(oFeatureClass), {|xIt| xIt[1] == "GETIDENTIFICAOINTEGRACAO"}) = 0;
			.Or. aScan(ClassMethArr(oFeatureClass), {|xIt| xIt[1] == "GETDOCUMENTACAOTDN"}) = 0

		::setError(STR0001, 404) //"Feature inválida"

		FWFreeObj(oTCOrepository)
		FWFreeObj(oFeatureClass)
		return .F.
	endIf

	oRota := oTCOrepository:findFeaturesRotas(cFeature)

	if empty(oRota['url'])
		::setError(STR0041, 404) //"Nenhuma rota encontrada para a feature"

		FWFreeObj(oTCOrepository)
		FWFreeObj(oFeatureClass)
		Return .F.
	endIf

	aErros := {}

	cResponse   := HttpPost(RTrim(oRota['url']), /*cGetParams*/, /*cPostParams*/ , 2/*nTimeOut*/ , /*aHeaderStrings*/, @cHeaderGet)
	nHttpStatus := HTTPGetStatus(@cError)
	if nHttpStatus < 200 .Or. nHttpStatus > 299
		aAdd(aErros, {'code': nHttpStatus, 'message': 'Feature: ' + Rtrim(oRota['feature']) + ', Source: ' + Rtrim(oRota['source']) + ', Erro: ' + cError })
	endIf
	

	if empty(aErros)
		::setSuccess(STR0042) //'Conexão realizada com sucesso!'
		lValid := .T.
	else
		::setError(STR0043, 404, '', , aErros) //'Erro ao conectar na(s) rotas(s) da feature.'
	endIf

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)
return lValid

/*/{Protheus.doc} listarFeatureComplemento
Lista os complementos da feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@param jQueryParam, json, filtros
/*/
method listarFeatureComplemento(jQueryParam) class agdTCOservice
	local oTCOFeatComplementos as object
	Local oRestService         As Object

	oRestService      := agdRestUtils():New()
	oTCOFeatComplementos := agdTCOFeatureComplementosRepository():New()
	oRestService:getPageAndPageSize(@Self:nPage, @Self:nPageSize)

	oTCOFeatComplementos:find(Self:nPage,Self:nPageSize, jQueryParam)

	::setFullResponse(oTCOFeatComplementos)

	oTCOFeatComplementos:DeActivate()
	FWFreeObj(oTCOFeatComplementos)
Return

/*/{Protheus.doc} registrarFeatureComplemento
Registra complemento da feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@param jComplemento, json, informacoes do complemento
@return logical, resultado do registro
/*/
method registrarFeatureComplemento(jComplemento as json) as logical class agdTCOservice
	local oTCOrepository       as object
	local oFeatureClass        as object
	local oTCOFeatComplementos  as object

	oTCOrepository := agdTCORepository():New()
	oFeatureClass  := oTCOrepository:getFeatureClassById(rTrim(jComplemento['codigo']))
	if oFeatureClass == nil
		::setError(STR0001, 404) //"Feature inválida"

		FWFreeObj(oTCOrepository)
		FWFreeObj(oFeatureClass)
		return .F.
	endIf

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)

	oTCOFeatComplementos := agdTCOFeatureComplementosRepository():New()
	oTCOFeatComplementos:save(jComplemento)

	::setFullResponse(oTCOFeatComplementos)

	FWFreeObj(oTCOFeatComplementos)
return .t.


/*/{Protheus.doc} registrarFeatureComplementoAll
Registra complemento da feature lote
@type method
@version 12
@author lindembergson.pacheco
@since 14/11/2025
@param jComplemento, json, informacoes do complemento
@return logical, resultado do registro
/*/
method registrarFeatureComplementoAll(jComplemento as json) as logical class agdTCOservice
	local oTCOrepository       as object
	local oFeatureClass        as object
	local oTCOFeatComplementos  as object

	oTCOrepository := agdTCORepository():New()
	oFeatureClass  := oTCOrepository:getFeatureClassById(rTrim(jComplemento['codigo']))
	if oFeatureClass == nil
		::setError(STR0001, 404) //"Feature inválida"

		FWFreeObj(oTCOrepository)
		FWFreeObj(oFeatureClass)
		return .F.
	endIf

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)

	oTCOFeatComplementos := agdTCOFeatureComplementosRepository():New()
	oTCOFeatComplementos:saveAll(jComplemento)

	::setFullResponse(oTCOFeatComplementos)

	FWFreeObj(oTCOFeatComplementos)
return .t.
/*/{Protheus.doc} registrarDePara
Cria o de-para para as integracoes do TCO
@type method
@version 12
@author jean.schulze
@since 31/10/2025
@return variant, nil
/*/
method registrarDePara(jCmdRegistrar as Json)  class agdTCOservice
	Local aTabelasDePara as array
	Local oRepository 	 as object
	Local oClass 		 as object
	Local oValidator     as object

	oRepository  := agdTCORepository():New()
	oClass       := oRepository:getFeatureClassById(AllTrim(jCmdRegistrar['feature']))
	if valType(oClass) != "U" .and. !empty(oClass:getIdentificaoIntegracao())

		aTabelasDePara := oClass:getTabelasDePara()

		// Verifica se o array de tabelas não está vazio
		If Len(aTabelasDePara) > 0
			// Busca a posição da tabela no array
			nPos := AScan(aTabelasDePara, {|x| x['tabela'] == jCmdRegistrar['tabela']})

			// Se encontrou a tabela, cria o de-para
			If nPos > 0

				if aScan(ClassMethArr(oClass), {|xIt| xIt[1] == "GETVALIDATOR"}) > 0
					oValidator := oClass:getValidator()
					If ValType(oValidator) == "O" .And. aScan(ClassMethArr(oValidator), {|xIt| xIt[1] == "VALIDATEDEPARAINSERT"}) > 0
						If ! oValidator:validateDeParaInsert(aTabelasDePara[nPos]['tabela'], aTabelasDePara[nPos]['campo'], jCmdRegistrar['idInterno'])
							Self:setError(oValidator:getErrorMsg())
							FWFreeObj(oValidator); FWFreeObj(oClass); FWFreeObj(oRepository)
							return
						EndIf
					EndIf
					FWFreeObj(oValidator)
				EndIf

				CFGA070Mnt(oClass:getIdentificaoIntegracao(), aTabelasDePara[nPos]['tabela'], aTabelasDePara[nPos]['campo'], jCmdRegistrar['idExterno'], jCmdRegistrar['idInterno'])
				Self:setSuccess(jCmdRegistrar)
			Else
				Self:setError(STR0046) //"Tabela não encontrada na configuração de De-Para.")
			EndIf
		Else
			Self:setError(STR0045) //"Nenhuma tabela configurada para De-Para."
		EndIf

	else
		Self:setError(STR0001)
	endIf

	FreeObj(oRepository)
	FreeObj(oClass)

return nil

/*/{Protheus.doc} deleteDePara
Deleta o de-para para as integracoes do TCO
@type method
@version 12
@author beatriz.dantas
@since 08/12/2025
@param jCmdDelete, Json, dados para deletar de-para
@return variant, nil
/*/
method deleteDePara(jCmdDelete as Json) class agdTCOservice
	Local aTabelasDePara as array
	Local oRepository    as object
	Local oClass         as object
	Local nPos           as numeric

	oRepository := agdTCORepository():New()
	oClass      := oRepository:getFeatureClassById(AllTrim(jCmdDelete['feature']))
	
	if valType(oClass) != "U" .and. !empty(oClass:getIdentificaoIntegracao())
		
		aTabelasDePara := oClass:getTabelasDePara()
		
		If Len(aTabelasDePara) > 0
			nPos := AScan(aTabelasDePara, {|x| x['tabela'] == jCmdDelete['tabela']})
			
			If nPos > 0
				CFGA070Mnt( oClass:getIdentificaoIntegracao(), aTabelasDePara[nPos]['tabela'], aTabelasDePara[nPos]['campo'], jCmdDelete['idExterno'], jCmdDelete['idInterno'], .T.) 	
				Self:setSuccess(jCmdDelete)
			Else
				Self:setError(STR0046) //"Tabela não encontrada na configuração de De-Para."
			EndIf
		EndIf
		
	else
		Self:setError(STR0001) //"Feature inválida"
	endIf
	
	FreeObj(oRepository)
	FreeObj(oClass)
	
return nil

/*/{Protheus.doc} importExternalIdsFromFeature
Importa os External IDs de De-Para da Feature se não forem encontrados na tabela NE2 (Complementos)
@type method
@version 12
@author jc.maldonado
@since 17/12/2025
@param cFeature, character, codigo da feature
/*/
method importExternalIdsFromFeature(cFeature as character) class agdTCOservice
	local oTCOrepository      as object
	local oFeatureClass       as object
	local nIndexNE2           as integer
	local aAreaNE2            as character
	local cTipoComplemento    as character
	local cFilialNE2          as character
	local oComplementoFeature as object
	local aContextos          as array
	local jExternalIds        as json
	local jComplemento        as json
	local nX                  as numeric
	local nY                  as numeric
	local cContexto           as character
	local cChave              as character

	nIndexNE2      := retOrder('NE2', 'NE2_FILIAL+NE2_CODIGO+NE2_TIPO+NE2_CONTEX+NE2_CHAVE')
	oTCOrepository := agdTCORepository():New()
	oFeatureClass  := oTCOrepository:getFeatureClassById(cFeature)

	if nIndexNE2 = 1;
			.Or. ! MethIsMemberOf(oFeatureClass, "getAllExternalIds")
		FWFreeObj(oTCOrepository)
		FWFreeObj(oFeatureClass)
		return
	endIf

	cFeature			:= PadR(rTrim(cFeature), TamSX3("NE2_CODIGO")[1])
	cTipoComplemento    := PadR('2'/*De-para*/ , TamSX3("NE2_TIPO"  )[1])
	jExternalIds        := oFeatureClass:getAllExternalIds()
	aContextos          := jExternalIds:getNames()
	cFilialNE2          := FWxFilial("NE2")
	oComplementoFeature := agdTCOFeatureComplementosRepository():new()

	dbSelectArea("NE2")
	aAreaNE2 := NE2->(FWGetArea())
	NE2->(DBSetOrder(nIndexNE2))

	for nX := 1 to Len(aContextos)
		for nY := 1 to Len(jExternalIds[aContextos[nX]])
			if jExternalIds[aContextos[nX]][nY]:hasProperty('chave')
				cContexto := PadR(rTrim(aContextos[nX])                    , TamSX3("NE2_CONTEX")[1])
				cChave    := PadR(jExternalIds[aContextos[nX]][nY]['chave'], TamSX3("NE2_CHAVE" )[1])

				if ! NE2->(DBSeek(cFilialNE2 + cFeature + cTipoComplemento + cContexto + cChave))
					jComplemento             := JsonObject():New()
					jComplemento['codigo'] 	 := cFeature
					jComplemento['tipo'] 	 := cTipoComplemento
					jComplemento['contexto'] := cContexto
					jComplemento['chave'] 	 := jExternalIds[aContextos[nX]][nY]['chave']
					jComplemento['valor'] 	 := jExternalIds[aContextos[nX]][nY]['valor']

					oComplementoFeature:save(jComplemento)
				endIf
			endIf
		next nY
	next nX

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)
	FWFreeObj(oComplementoFeature)

	FWRestArea(aAreaNE2)
return
