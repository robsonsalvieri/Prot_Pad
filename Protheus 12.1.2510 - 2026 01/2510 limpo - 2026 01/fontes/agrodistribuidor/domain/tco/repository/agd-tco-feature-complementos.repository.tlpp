#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'
#include 'agd.tco.feature.complementos.repository.ch'

#define TABLE_ALIAS "NE2"

namespace agd.tcoFeatureComplementosRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdTCOFeatureComplementosRepository
Classe responsavel pelos complementos da Feature
@type class
@version 12
@author jc.maldonado
@since 03/11/2025
/*/
class agdTCOFeatureComplementosRepository from agdRepositoryUtils
	public data cChaveIDRepository as character

	public method new()
	public method getFields()         as array
	public method save()              as logical
	public method saveAll()              as logical

	private method getNextItemNumber() as character
endclass

/*/{Protheus.doc} agdTCOFeatureComplementosRepository::new
Constructor
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@param pcChaveIDRepository, character, chave
@return object, instancia da classe
/*/
method new(pcChaveIDRepository as character) class agdTCOFeatureComplementosRepository
	::cChaveIDRepository := pcChaveIDRepository
	_Super:New(TABLE_ALIAS, ::getFields())
return self

/*/{Protheus.doc} agdTCOFeatureComplementosRepository::getFields
Retorna os campos do repositorio
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@return array, {{campoJson, campoProtheus, tipoCampo, lShowField}, ...}
/*/
method getFields() as array class agdTCOFeatureComplementosRepository
	local aFields as array

	aFields := {;
		{'filial'  , 'NE2_FILIAL', 'C', .f.},;
		{'codigo'  , 'NE2_CODIGO', 'C', .f.},;
		{'tipo'    , 'NE2_TIPO'  , 'C', .f.},;
		{'contexto', 'NE2_CONTEX', 'C', .f.},;
		{'item'    , 'NE2_ITEM'  , 'C', .t.},;
		{'chave'   , 'NE2_CHAVE' , 'C', .t.},;
		{'valor'   , 'NE2_VALOR' , 'M', .t.}}
return aFields

/*/{Protheus.doc} agdTCOFeatureComplementosRepository::save
Registra um novo complemento da Feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@param jComplemento, json, campos do complemento
@return logical, resultado do registro
/*/
method save(jComplemento as json) as logical class agdTCOFeatureComplementosRepository
	local oModel  as object
	local lResult as logical

	oModel := FwLoadModel("AGDA100")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	if oModel:Activate()

		jComplemento['item'] := ::getNextItemNumber(jComplemento['codigo'], jComplemento['tipo'], jComplemento['contexto'])
		::setValueModel(oModel:GetModel("AGDA100_NE2"), ::jsonToArrayFields(jComplemento, ::getFields()))

		if oModel:VldData() .and. oModel:CommitData()
			::setSuccess(STR0001) //"Registro incluído com sucesso."
			lResult := .t.
		else
			::setErrorByErrorMessageModel(oModel, 404)
		endIf

		oModel:deActivate()
	endIf

	oModel:destroy()
	FWFreeObj(oModel)
return lResult


/*/{Protheus.doc} agdTCOFeatureComplementosRepository::save
Registra um novo complemento da Feature
@type method
@version 12
@author lindembergson.pacheco
@since 17/11/2025
@param jComplemento, json, campos do complemento
@return logical, resultado do registro
/*/
method saveAll(jComplemento as json) as logical class agdTCOFeatureComplementosRepository

	local nI		as integer
	local lResult 	as logical

	if jComplemento:hasProperty("items")
		if ValType(jComplemento["items"]) == "A"
			For nI := 1 To Len(jComplemento["items"])
				jComplemento["items"][nI]['codigo'] 	:= alltrim(jComplemento['codigo'])
				jComplemento["items"][nI]['tipo'] 		:= alltrim(jComplemento['tipo'])
				jComplemento["items"][nI]['contexto'] 	:= alltrim(jComplemento['contexto'])
				lResult := ::save(jComplemento["items"][nI])
			next
		endif
	else
		::setErrorField(403)
	endif

return lResult

/*/{Protheus.doc} agdTCOFeatureComplementosRepository::getNextItemNumber
Retorna o próximo codigo do item do complemento da Feature
@type method
@version 12
@author jc.maldonado
@since 03/11/2025
@param cCodigo, character, codigo da Feature
@param cTipo, character, '1'=Interno;'2'=De-Para
@param cContexto, character, contexto
@return character, cNewiD
/*/
method getNextItemNumber(cCodigo as character, cTipo as character, cContexto as character) as character class agdTCOFeatureComplementosRepository
	local cDefaultItem as character
	local aBindQry     as array
	local cQuery       as character
	local cAlias       as character
	local cNextId      as character

	cDefaultItem := StrZero(0, FWSX3Util():GetFieldStruct("NE2_ITEM")[3/*nTamanho*/])
	aBindQry     := {}

	cQuery := "SELECT"
	cQuery += "	COALESCE(MAX(NE2_ITEM), '" + cDefaultItem + "') AS ITEM"
	cQuery += " FROM"
	cQuery += "	" + retSQLname("NE2")
	cQuery += " WHERE"
	cQuery += "	NE2_FILIAL = ?"     ; aAdd(aBindQry, FWxFilial("NE2"))
	cQuery += "	AND NE2_CODIGO = ?" ; aAdd(aBindQry, cCodigo         )
	cQuery += "	AND NE2_TIPO = ?"   ; aAdd(aBindQry, cTipo           )
	cQuery += "	AND NE2_CONTEX = ?" ; aAdd(aBindQry, cContexto       )
	cQuery += "	AND D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery(cQuery)

	cAlias  := MPSysOpenQuery(cQuery,,,,aBindQry)

	cNextId := Soma1((cAlias)->ITEM)

	(cAlias)->(DBCloseArea())
return cNextId
