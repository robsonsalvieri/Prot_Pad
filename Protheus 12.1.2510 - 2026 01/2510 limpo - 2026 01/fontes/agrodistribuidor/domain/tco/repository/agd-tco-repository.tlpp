#INCLUDE 'totvs.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'fwmvcdef.ch'
#include 'AGD.TCO.REPOSITORY.CH'

namespace agd.TCORepository
using namespace agd.repositoryUtils

/*/{Protheus.doc} agdTCORepository
Repositorio do TCO
@type class
@version 12
@author jc.maldonado
@since 23/09/2025
/*/
class agdTCORepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public method new()

	public Method save()
	public Method updateRepository()
	public Method getDetalheFeature()       as Json
	public Method findFeaturesDisponiveis() as array
	public Method isRepositoryChange()
	public method isSIGAAGDactive()        as logical
	public Method getPayloadFields()       as array
	public Method getFilterById()          as Array	
	public method getConfigPercentage()    as double
	public method getTotalActiveFeatures() as integer
	public method getTotalRegistrations()  as integer
	public method findActiveFeatures()     as array
	public Method getFeatureClassById()    as object
	public method getFeatureStatus() 	   as character
	public Method getAllClassFeaturesAtivas() as array
	public method updateFeatureStatus()    as logical
	public method findFeaturesRotas()      as Json
	public method findFeaturesAdapter()    as Array
	public method isFeatureActive()        as logical

	private method getFields()             as array

	
endClass

/*/{Protheus.doc} New
Construtor
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return object, Instancia da classe
/*/
method New(pcChaveIDRepository as character) class agdTCORepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NE1', self:getFields())
return Self

/*/{Protheus.doc} save
Faz a inclusao do fechamento financeiro
@type method
@version 12
@author jc.maldonado
@since 03/06/2025
@param jPayload, json, payload recebido
/*/
method save(jPayload as json, lUpdate as logical) class agdTCORepository
	Local oModel           as object
	Local aFieldsCabecalho := Self:getFields()
	
	Default lUpdate := .F.

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA090
		oModel := FwLoadModel("AGDA090")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()
	
		if Self:isSuccess()

			// Carregamento do cabeçalho
			aFields := Self:jsonToArrayFields(jPayload, aFieldsCabecalho)
			Self:setValueModel(oModel:GetModel("AGDA090_NE1"), aFields)
			    
			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDA090_NE1", "NE1_CODIGO"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif			

		endif

		oModel:DeActivate()
		oModel:Destroy()
		FWFreeObj(oModel)
	endif
Return

/*/{Protheus.doc} updateRepository
Atualiza Repositório
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdTCORepository
	Local nField		as Numeric
	Local aArea		:= GetArea()

	if Self:existsById(Self:cChaveIDRepository)

		RecLock("NE1",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NE1->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NE1->(MsUnlock())

		Self:setSuccess(Self:cChaveIDRepository)

	endif

	RestArea(aArea)

return nil

/*/{Protheus.doc} getDetalheFeature
Obtem retorno do Json de Detalhe da Feature
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method getDetalheFeature(cFeature) as Json Class agdTCORepository
	Local aArea		 := GetArea()
	Local jDetalhe   := JsonObject():New()
	
	if Self:existsById(xFilial('NE1')+cFeature)

		if !Empty(NE1->NE1_MSGOBS)
			if ValType( jDetalhe:FromJson(NE1->NE1_MSGOBS)) != "U"
				jDetalhe :=  JsonObject():New()
			endif
		endif	

	endif

	RestArea(aArea)

return jDetalhe

/*/{Protheus.doc} findFeaturesDisponiveis
Retorna a Lista das Features(DTOs) Disponiveis no AgroDistribuidor
@type method
@version 12
@author jean.schulze
@since 26/09/2025
@return variant, retorno
/*/
Method findFeaturesDisponiveis() Class agdTCORepository
	Local aFeatures := {}

	AAdd(aFeatures, {"BARTER"                        , "agdTCOBarterDTO()"                       })
	AAdd(aFeatures, {"ORIGINACAO_PROTHEUS"           , "agdTCOOriginacaoProtheusDTO()"           })
	AAdd(aFeatures, {"RECEITUARIO"                   , "agdTCOReceituarioDTO()"                  })
	AAdd(aFeatures, {"INTEGRACAO_COMERCIALIZACAO_TAC", "agdTCOComercializacaoTACDTO()"           })
	AAdd(aFeatures, {"DTA"                           , "agdTCODTADTO()"                          })
	AAdd(aFeatures, {"INTEGRACAO_RECEITUARIO"        , "agdTCOSolicitacaoExternaReceituarioDTO()"})

Return aFeatures

/*/{Protheus.doc} getFeatureClassById
Retorna uma instancia da classe da feature a partir do id.
@type method
@version 12
@author jc.maldonado
@since 03/10/2025
@param cId, character, Id da feature
@return object, Instancia da classe
/*/
Method getFeatureClassById(cId as character) as object class agdTCORepository
	local nPos      as integer
	local aFeatures as array
	local oClass    as object

	nPos := aScan(aFeatures := ::findFeaturesDisponiveis(), {|xIt| xIt[1] == cId})
	if nPos > 0
		oClass := &(aFeatures[nPos, 2] + ":new()")
	endIf
Return oClass

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, lista de campos
/*/
Method getFields() Class agdTCORepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'    , 'NE1_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'    , 'NE1_CODIGO', 'C', .T.})
	AAdd(aArrayFields, {'status'    , 'NE1_STATUS', 'C', .T.})
	AAdd(aArrayFields, {'tipo'      , 'NE1_TIPO',   'C', .T.})
	AAdd(aArrayFields, {'ativo'     , 'NE1_ATIVO',  'C', .T.})
	AAdd(aArrayFields, {'data'      , 'NE1_DATA',   'D', .T.})
	AAdd(aArrayFields, {'hora'      , 'NE1_HORA',   'C', .T.})
	AAdd(aArrayFields, {'usuario'   , 'NE1_NOMUSU', 'C', .T.})
	AAdd(aArrayFields, {'detalhe'   , 'NE1_MSGOBS', 'C', .F.})
	AAdd(aArrayFields, {'descricao' , 'NE1_DESC'  , 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigo, character, codigo Negocio
@param cVersao, character, versao negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigo) as Array Class agdTCORepository
    Local aFilterId := {}

	AAdd(aFilterId, {'NE1_CODIGO', cCodigo}) 

return aFilterId

/*/{Protheus.doc} isRepositoryChange
Verifica se modelo permite edicao
@type method
@version 12
@author carlos.augusto
@since 25/07/2025
@return logical, modelo permite não edicao
/*/
Method isRepositoryChange(cChaveIdFechamentoFinanceiro as character) Class agdTCORepository
	
	If Self:existsById(Self:cChaveIDRepository)
		Self:buildModelCheck("AGDA090")
	Endif

return nil

/*/{Protheus.doc} getPayloadFields
Retorna todos os campos disponiveis para o payload
@type method
@version 12
@author jean.schulze
@since 11/08/2025
@return array, array de campos
/*/
Method getPayloadFields() as Array Class agdTCORepository
	Local aFields:= {}
	
	AEval(Self:getFields(),{|field| AAdd(aFields, field)})

Return aFields

/*/{Protheus.doc} isSIGAAGDactive
Retorna se o modulo sigaagd está ativo
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return logical, .T. ativo, .F. inativo
/*/
method isSIGAAGDactive() as logical class agdTCORepository 
return SUPERGETMV("MV_SIGAAGD", .f., .f.)

/*/{Protheus.doc} getConfigPercentage
Retorna a porcentagem de features configuradas
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return numeric, Porcentagem configurada
/*/
method getConfigPercentage() as double class agdTCORepository 
	local cSQL            as character
    local cAlias          as character
    local aBindParams     as array
    local nPercent        as double
    local nActiveFeatures as numeric

    aBindParams := {}
	cSQL := "SELECT COUNT(NE1_CODIGO) AS TOTAL"
	cSQL += " FROM " + retSqlName("NE1")
	cSQL += " WHERE NE1_FILIAL = ? " ; aAdd(aBindParams, FWxFilial("NE1"))
	cSQL += " AND NE1_ATIVO = ?"     ; aAdd(aBindParams, '1') //'1'=ativo
	cSQL += " AND NE1_STATUS = ?"    ; aAdd(aBindParams, '2') //'2'=finalizado
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := changeQuery(cSQL)
    
	cAlias := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, aBindParams), cAlias, .F., .T.)
	nConfiguredFeatures := (cAlias)->TOTAL
    (cAlias)->(DBcloseArea())

    nActiveFeatures := ::getTotalActiveFeatures()

    nPercent := Round((nConfiguredFeatures / nActiveFeatures) * 100, 2)
return nPercent

/*/{Protheus.doc} getTotalActiveFeatures
Retorna o total de features ativas
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@return numeric, Total features ativas
/*/
method getTotalActiveFeatures() as integer class agdTCORepository 
	local cSQL        as character
	local aBindParams as array
	local cAlias      as character
	local nTotal      as integer

	aBindParams := {}

	cSQL := "SELECT COUNT(NE1_CODIGO) AS TOTAL"
	cSQL += " FROM " + retSqlName("NE1")
	cSQL += " WHERE NE1_FILIAL = ? " ; aAdd(aBindParams, FWxFilial("NE1"))
	cSQL += " AND NE1_ATIVO = ? "    ; aAdd(aBindParams, '1') //'1'=ativo
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := changeQuery(cSQL)

	cAlias := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, aBindParams), cAlias, .F., .T.)

	nTotal := (cAlias)->TOTAL

	(cAlias)->(DBcloseArea())
return nTotal

/*/{Protheus.doc} getTotalRegistrations
Retorna o total de registros cadastrados para o alias informado
@type method
@version 12
@author jc.maldonado
@since 23/09/2025
@param cAliasTab, character, alias da tabela
@return numeric, Total registros cadastrados
/*/
method getTotalRegistrations(cAliasTab as character) as integer Class agdTCORepository 
	local cSQL      as character
	local cNewAlias as character
	local nTotal    as integer

	default cAliasTab := ""

	if !empty(cAliasTab) .And. FWAliasInDic(cAliasTab)
		cSQL := "SELECT COUNT(*) AS TOTAL"
		cSQL += " FROM " + retSqlName(cAliasTab)
		cSQL += " WHERE D_E_L_E_T_ != '*'"
		cSQL := changeQuery(cSQL)

		cNewAlias := GetNextAlias()
		dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, {}), cNewAlias, .F., .T.)

		nTotal := (cNewAlias)->TOTAL

		(cNewAlias)->(DBcloseArea())
	endif
return nTotal

/*/{Protheus.doc} findActiveFeatures
Retorna a lista de Features ativas na tabela NE1
@type method
@version 12
@author scheronlini
@since 08/10/2025
@return array, Lista de códigos de features ativas
/*/
method findActiveFeatures() class agdTCORepository
	Local aRet    := {}
	Local cSQL    := ""
	Local aParams := {}
	Local cAlias  := GetNextAlias()

	cSQL := "SELECT NE1_CODIGO, NE1_FILIAL FROM " + RetSqlName("NE1")
	cSQL += " WHERE NE1_ATIVO = ? " ; AAdd(aParams, "1")
	cSQL += " AND D_E_L_E_T_ = ' '"
	cSQL := ChangeQuery(cSQL)

	dbUseArea(.T., "TOPCONN", TcGenQry2(,,cSQL,aParams), cAlias, .F., .T.)
	dbSelectArea(cAlias)
	dbGoTop()

	While !(cAlias)->(EoF())
		AAdd(aRet, {AllTrim((cAlias)->NE1_CODIGO), (cAlias)->NE1_FILIAL})
		(cAlias)->(DbSkip())
	EndDo

	(cAlias)->(DbCloseArea())

Return aRet

/*/{Protheus.doc} getFeatureStatus
Retorna o status atual de uma feature específica
@type method
@version 12
@author scheronlini
@since 08/10/2025
@param cCodigo, character, código da feature
@return character, status atual da feature
/*/
method getFeatureStatus(cCodigo) class agdTCORepository
	Local cSQL    := ""
	Local aParams := {}
	Local cAlias  := GetNextAlias()
	Local cStatus := ""

	cSQL := "SELECT NE1_STATUS FROM " + RetSqlName("NE1") + " WHERE NE1_FILIAL = ? " ; AAdd(aParams, FWxFilial("NE1"))
	cSQL += " AND NE1_CODIGO = ? "    ; AAdd(aParams, cCodigo)
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := ChangeQuery(cSQL)

	dbUseArea(.T., "TOPCONN", TcGenQry2(,,cSQL,aParams), cAlias, .F., .T.)
	dbSelectArea(cAlias)
	dbGoTop()

	If !(cAlias)->(EoF())
		cStatus := AllTrim((cAlias)->NE1_STATUS)
	EndIf

	(cAlias)->(DbCloseArea())

Return cStatus


/*/{Protheus.doc} updateFeatureStatus
Atualiza o status e a data de finalização da Feature na NE1
@type method
@version 12
@author scheronlini
@since 08/10/2025
@param cCodigo, character, código da feature
@param cStatus, character, novo status (ex: '3')
@param dData, date, data de finalização
@return logical, .T. se atualizou, .F. caso contrário
/*/
method updateFeatureStatus(cCodigo , cStatus , dData, cHora ) class agdTCORepository
	Local lOk := .F.

	dbSelectArea("NE1")
	If dbSeek(xFilial("NE1") + cCodigo)
		RecLock("NE1", .F.)
		NE1->NE1_STATUS := cStatus
		If !Empty(dData)
			NE1->NE1_DTFINA := dData
		EndIf
			If !Empty(cHora)
			NE1->NE1_HRFINA := cHora
		EndIf
		NE1->(MsUnLock())
		lOk := .T.
	EndIf

Return lOk

/*/{Protheus.doc} findFeaturesRotas
Retorna a rotas da features
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@return array, array das rotas
/*/
Method findFeaturesRotas(cCodFeature) Class agdTCORepository
	Local cQuery           As character
	Local oStmt            As object
	Local jValue           As Json
	Local oFeatureClass    As object
	Local cCodProduto      As character
	Local cAliasXB9  := GetNextAlias()

	oTCOrepository  := agdTCORepository():New()
	oFeatureClass   := oTCOrepository:getFeatureClassById(cCodFeature)
	cCodProduto     := oFeatureClass:getIdentificaoIntegracao()

	jValue := JsonObject():New()
	jValue["identificador"] := cCodFeature
	jValue["feature"] := cCodProduto
	jValue["source"] := oFeatureClass:getDescricao()
	jValue["url"] := ""
	jValue['tdn'] := oFeatureClass:getDocumentacaoTDN()

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)

	cQuery := " SELECT XB9_PROD, XB9_SOURCE, XAI_URL"
	cQuery += "  FROM XB9"
	cQuery += " INNER JOIN XAI ON XAI_APPID = XB9_APPID"
	cQuery += "   AND XAI.D_E_L_E_T_ = ''"
	cQuery += " WHERE XB9.XB9_FILIAL = ?"
	cQuery += "   AND XB9.XB9_PROD   = ?"
	cQuery += "   AND XB9.D_E_L_E_T_ = ''"
	cQuery := ChangeQuery(cQuery)
	
	/*Garante que existe a tabela*/
	Try
		// Prepara os parâmetros
		oStmt:= FWPreparedStatement():New()
		oStmt:SetQuery(cQuery)
		oStmt:SetString(1, xFilial("XB9"))
		oStmt:SetString(2, cCodProduto)
		cQuery := oStmt:GetFixQuery()

		// Executa a query e percorre os resultado
		MPSysOpenQuery(cQuery, cAliasXB9)

		If !(cAliasXB9)->(EoF())
			jValue["url"] := (cAliasXB9)->XAI_URL
		endIf

		(cAliasXB9)->(DbCloseArea())

	Catch oError
		Self:setError(oError:Description, 401)
	EndTry

Return jValue

/*/{Protheus.doc} findByFeature
Retorna os adapters da features
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@param cCodFeature, character, codigo da feature
@return variant, nil
/*/
Method findFeaturesAdapter(cCodFeature) class agdTCORepository
	local oTCOrepository as object
	local oFeatureClass  as object
	local aCadastros     as array
	local aItems         as array
	local nX             as integer
	local cDetalhe       as character

	oTCOrepository := agdTCORepository():New()
	oFeatureClass  := oTCOrepository:getFeatureClassById(cCodFeature)
	aCadastros     := oFeatureClass:getAdapter()

	aItems := {}
	for nX := 1 to Len(aCadastros)
		aAdapter := FwAdapterInfo(aCadastros[nX]['rotina'], aCadastros[nX]['mensagem'])

		if empty(aAdapter) 
			cDetalhe := STR0001 //Adapter não está cadastrado
		elseif aAdapter[3] != .t. .and. aAdapter[4] != .t. // ADAPTER_INFO_SEND - ADAPTER_INFO_RECEIVE
			cDetalhe := STR0002 //Não está configurado para envio ou recebimento
		Endif

		aadd(aItems, JsonObject():New())
		aItems[nX]['rotina']  := aCadastros[nX]['rotina']
		aItems[nX]['config']  := iif(cDetalhe=="",STR0003,STR0004)
		aItems[nX]['detalhe'] := cDetalhe

	next nX

	FWFreeArray(aCadastros)
	FWFreeObj(oTCOrepository)
return aItems


/*/{Protheus.doc} getAllClassFeaturesAtivas
Retorna as features ativas instanciando os respectivos DTOs.
@type method
@version 12
@author jean.schulze
@since 30/10/2025
@return array, lista de DTOs das features
/*/
method getAllClassFeaturesAtivas() as array class agdTCORepository
	Local aFeatures := {}
	Local aCodigos  := Self:findActiveFeatures()
	Local aDisponiveis := Self:findFeaturesDisponiveis()
	Local nI, nJ
	Local cCodigo, cClasse

	// Percorre as features ativas na NE1
	For nI := 1 To Len(aCodigos)
		cCodigo := aCodigos[nI][1]

		// Busca na lista de disponíveis para saber qual classe instanciar
		For nJ := 1 To Len(aDisponiveis)
			If Upper(AllTrim(cCodigo)) == Upper(AllTrim(aDisponiveis[nJ][1]))
				cClasse := aDisponiveis[nJ][2]
				AAdd(aFeatures, { &(cClasse + ":New()"), aCodigos[nI][2] })
				Exit
			EndIf
		Next nJ
	Next nI

Return aFeatures

/*/{Protheus.doc} isFeatureActive
description
@type method
@version 12
@author jean.schulze
@since 12/22/2025
@param cFeature, character, param_description
@return logical, return_description
/*/
method isFeatureActive(cFeature as character) as logical class agdTCORepository
	Local lAtivo := .F.

	dbSelectArea("NE1")
	If dbSeek(xFilial("NE1") + cFeature)
		If NE1->NE1_ATIVO == '1'
			lAtivo := .T.
		EndIf
	EndIf

Return lAtivo
