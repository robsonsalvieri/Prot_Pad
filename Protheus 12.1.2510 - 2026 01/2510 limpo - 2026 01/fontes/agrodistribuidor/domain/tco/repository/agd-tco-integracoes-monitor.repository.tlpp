#include "tlpp-core.th"
#include "totvs.ch"

namespace agd.integracoesMonitorRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdIntegracoesMonitorRepository
Repository do Monitor de Integração.
Responsável por consultas performáticas na tabela XX3 (schedule), aplicando filtros e paginação.
@type class
@version 12
@author rodrigo.nsoledade
@since 24/10/2025
/*/
class agdIntegracoesMonitorRepository from agdRepositoryUtils
    public Data cChaveIDRepository As Object //chave unica para updates

    public method New()
    public method findIntegracoes()
    private method getFields() as Array

endclass

/*/{Protheus.doc} New
Construtor do Repository.
@type method
@author rodrigo.nsoledade
@since 24/10/2025
/*/
Method New(pcChaveIDRepository as character) Class agdIntegracoesMonitorRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('XX3', self:getFields())
Return Self

/*/{Protheus.doc} find
Listagem Padrão do Repositório
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@return variant, nil
/*/
Method findIntegracoes(nPage, nPageSize, oJsonParam) Class agdIntegracoesMonitorRepository
	Local aArea         As Array
	Local cWhereParam   As Character
	Local cWhrDefault   As Character
	Local cOrder        As Character
	Local jResponseJson as Object

	Default oJsonParam := Nil
	Default nPage          := 1
	Default nPageSize      := 10

	If Self:lOk
		aArea       := FwGetArea()
		cOrder      := Self:getOrderQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhrDefault := " XX3.XX3_FILIAL = '"+ cFILANT +"' AND D_E_L_E_T_ = ' '"
		if !Empty(cWhereParam)
			cWhrDefault += " AND ("+cWhereParam+")"
		Endif

		Self:setFieldsConsulta(Self:aFieldsRepository)
		Self:setPage(nPage)
		Self:setPageSize(nPageSize)
		Self:SetQuery(Self:getQueryDefault(Self:aQueryJoinsRepository, Self:cQueryOptionalRepository))
		Self:SetWhere(cWhrDefault) //Tabela XX3 nao esta no SX3
		Self:SetUrlFilter(Self:getFilterAdvancedQueryParam(oJsonParam))
		Self:SetOrder( cOrder )
		Self:SetUseSpaces(.T.)

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf
return nil

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositório da tabela XX3 (Schedule de Integrações)

@type method
@version 12
@author 
Rodrigo N. Soledade
@since 27/10/2025
@return array, lista de campos
/*/
method getFields() class agdIntegracoesMonitorRepository
	Local aFields := {}

	AAdd(aFields, {"filial"        , "XX3_FILIAL" , "C", .T.})
	AAdd(aFields, {"idInterno"     , "XX3_CODIGO" , "C", .T.})
	AAdd(aFields, {"uuid"          , "XX3_UUID"   , "C", .T.})
	AAdd(aFields, {"funcaoCod"     , "XX3_FUNCOD" , "C", .T.})
	AAdd(aFields, {"funcaoDesc"    , "XX3_FUNDES" , "C", .T.})
	AAdd(aFields, {"trData"        , "XX3_TRDATA" , "D", .T.})
	AAdd(aFields, {"trHora"        , "XX3_TRHORA" , "C", .T.})
	AAdd(aFields, {"prData"        , "XX3_PRDATA" , "D", .T.})
	AAdd(aFields, {"prHora"        , "XX3_PRHORA" , "C", .T.})
	AAdd(aFields, {"tentativa"     , "XX3_TENTAT" , "N", .T.})
	AAdd(aFields, {"tnData"        , "XX3_TNDATA" , "D", .T.})
	AAdd(aFields, {"tnHora"        , "XX3_TNHORA" , "C", .T.})
	AAdd(aFields, {"trans"         , "XX3_TRANS"  , "M", .T.})
	AAdd(aFields, {"trans2"        , "XX3_TRANS2" , "M", .T.})
	AAdd(aFields, {"tipoDoc"       , "XX3_TPDOC"  , "C", .T.})
	AAdd(aFields, {"tipoTrans"     , "XX3_TPTRAN" , "C", .T.})
	AAdd(aFields, {"status"        , "XX3_STATUS" , "C", .T.})
	AAdd(aFields, {"tmData"        , "XX3_TMDATA" , "D", .T.})
	AAdd(aFields, {"tmHora"        , "XX3_TMHORA" , "C", .T.})
	AAdd(aFields, {"recpData"      , "XX3_RECPDT" , "D", .T.})
	AAdd(aFields, {"recpHora"      , "XX3_RECPHR" , "C", .T.})
	AAdd(aFields, {"respData"      , "XX3_RESPDT" , "D", .T.})
	AAdd(aFields, {"respHora"      , "XX3_RESPHR" , "C", .T.})
	AAdd(aFields, {"msgType"       , "XX3_MSGTYP" , "C", .T.})
	AAdd(aFields, {"chanel"        , "XX3_CHANEL" , "C", .T.})
	AAdd(aFields, {"referencia"    , "XX3_REFER"  , "C", .T.})
	AAdd(aFields, {"url"           , "XX3_URL"    , "C", .T.})
	AAdd(aFields, {"webService"    , "XX3_WBSER"  , "C", .T.})
	AAdd(aFields, {"retorno"       , "XX3_RETURN" , "M", .T.})
	AAdd(aFields, {"pRoute"        , "XX3_PROUTE" , "C", .T.})
	AAdd(aFields, {"sRoute"        , "XX3_SROUTE" , "C", .T.})
	AAdd(aFields, {"orUuid"        , "XX3_ORUUID" , "C", .T.})
	AAdd(aFields, {"producao"      , "XX3_PRODUC" , "C", .T.})
	AAdd(aFields, {"source"        , "XX3_SOURCE" , "C", .T.})
	AAdd(aFields, {"versao"        , "XX3_VERSAO" , "C", .T.})
	AAdd(aFields, {"evento"        , "XX3_EVENT"  , "C", .T.})
	AAdd(aFields, {"uuidLote"      , "XX3_UUIDLT" , "C", .T.})
	AAdd(aFields, {"tipoLote"      , "XX3_TIPOLT" , "C", .T.})

Return aFields
