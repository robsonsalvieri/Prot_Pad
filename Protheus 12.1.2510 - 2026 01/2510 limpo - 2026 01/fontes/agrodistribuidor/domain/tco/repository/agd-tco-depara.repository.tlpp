#INCLUDE 'totvs.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'fwmvcdef.ch'

namespace agd.TCODeParaRepository
using namespace agd.repositoryUtils
using namespace agd.tcoRepository

/*/{Protheus.doc} agdTCODeParaRepository
Repositorio do TCO de-para
@type class
@version 12
@author Gilson.Venturi
@since 15/10/2025
/*/
class agdTCODeParaRepository FROM agdRepositoryUtils

	public method new()
	public method findByFeature()

	private method getFields()		as array
	
endClass

/*/{Protheus.doc} New
Construtor
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@return object, Instancia da classe
/*/
method New() class agdTCODeParaRepository
	_Super:New('XXF', self:getFields())
return Self

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@return variant, lista de campos
/*/
Method getFields() Class agdTCODeParaRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'tabela'     , 'XXF_TABLE'  , 'C', .T.})
	AAdd(aArrayFields, {'alias'      , 'XXF_ALIAS'  , 'C', .T.})
	AAdd(aArrayFields, {'campo'      , 'XXF_FIELD'  , 'C', .T.})
	AAdd(aArrayFields, {'mensagem'   , 'XXF_UNMESS' , 'C', .T.})
	AAdd(aArrayFields, {'valexterno' , 'XXF_EXTVAL' , 'C', .T.})
	AAdd(aArrayFields, {'descexterno', 'NE2_VALOR'  , 'M', .T.})
	AAdd(aArrayFields, {'valinterno' , 'XXF_INTVAL' , 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} findByFeature
Busca pelo codigo da Feature
@type method
@version 12
@author Gilson.Venturi
@since 15/10/2025
@param cCodFeature, character, codigo da feature
@return variant, nil
/*/
Method findByFeature(cCodFeature as character, jQueryParam as json, nPage as numeric, nPageSize as numeric) class agdTCODeParaRepository
	Local jResponseJson    As json
	Local oFeatureClass    As object
	Local cCodProduto      As character
	
	Default nPage     := 1
	Default nPageSize := 10

	oTCOrepository  := agdTCORepository():New()
	oFeatureClass   := oTCOrepository:getFeatureClassById(cCodFeature)
	cCodProduto     := oFeatureClass:getIdentificaoIntegracao()
	cOrder          := Self:getOrderQueryParam(jQueryParam, self:getFields())
	cWhereParam     := Self:getWhereQueryParam(jQueryParam, self:getFields())

	AAdd(Self:aQueryJoinsRepository,{"LEFT", "NE2", Self:getWhereDefault("NE2.NE2_CHAVE = XXF.XXF_EXTVAL AND NE2.D_E_L_E_T_ = '' ", "NE2")})

	FWFreeObj(oTCOrepository)
	FWFreeObj(oFeatureClass)

	Self:setFieldsConsulta(self:getFields())

	Self:setPage(nPage)
	Self:setPageSize(nPageSize)
	Self:SetQuery(Self:getQueryDefault(self:aQueryJoinsRepository))
	Self:SetWhere("XXF.XXF_REFER = '"+cCodProduto+"' AND XXF.D_E_L_E_T_ = ' '" + IIF(!EMPTY(cWhereParam), ' AND ', '') + cWhereParam )
	Self:SetUseSpaces(.T.)
	Self:SetUrlFilter(Self:getFilterAdvancedQueryParam(jQueryParam))
	Self:SetOrder( cOrder )

	If Self:Execute()
		Self:FillGetResponse()
		if Self:lOk
			jResponseJson := Self:convertToJson(Self:getJSONResponse())
			Self:setSuccess(jResponseJson)
		Endif
	EndIf

	FreeObj(jResponseJson)

Return Nil
