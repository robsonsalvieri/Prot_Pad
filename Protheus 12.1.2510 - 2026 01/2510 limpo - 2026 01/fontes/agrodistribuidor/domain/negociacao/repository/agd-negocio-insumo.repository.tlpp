#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'

namespace agd.negocioInsumoRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdNegocioInsumoRepository
Repositório de Insumos de Barter - NEB
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/
class agdNegocioInsumoRepository FROM agdRepositoryUtils

	public Method New()
	public Method findByCodigoNegocio()
	public Method findById(cCodigoNegocio, pcInsumo)
	public Method getFields()
	
	private Method getQueryJoin() as Array
	private Method getQueryWithTotal()

endclass

/*/{Protheus.doc} New
Constructor do Repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instancia
/*/
Method New() Class agdNegocioInsumoRepository
	_Super:New('NEB',Self:getFields(.T.),Self:getQueryJoin(),Self:getQueryWithTotal())	
Return Self

/*/{Protheus.doc} findByCodigoNegocio
Busca por um Código de Negócio Barter
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param cCodigo, character, codigo da negociação barter
@param cVersao, character, versão da negociação barter
@return variant, nil
/*/
Method findByCodigoNegocio(cCodigo as character, cVersao as character, nPage as numeric, nPageSize as numeric, lAllFields as logical) class agdNegocioInsumoRepository
	Local aArea             As Array
	Local cWhereParam       As Character
	Local jResponseJson     as json
	Local jResponseItemJson as json
	
	Default nPage           := 1
	Default nPageSize       := 10
	Default lAllFields      := .F.

	If !Empty(cCodigo)
		If Self:lOk
			aArea := FwGetArea()
			aQueryRequest := {}

			cWhereParam := "NEB.NEB_CODBRT = '"+cCodigo+"' "
			cWhereParam += " AND NEB.NEB_VERSAO = '"+cVersao+"' "

			aFields := self:getFields(lAllFields)

			Self:setFieldsConsulta(aFields)

			Self:setPage(nPage)
			Self:setPageSize(nPageSize)
			Self:SetQuery(Self:getQueryDefault(Self:aQueryJoinsRepository, Self:cQueryOptionalRepository))
			Self:SetWhere(Self:getWhereDefault(cWhereParam))
			Self:SetStyleReturn("listInsumos")

			If Self:Execute()
				Self:FillGetResponse()
				if Self:lOk
					jResponseJson := Self:convertToJson(Self:getJSONResponse())
					Self:setSuccess(jResponseJson['listInsumos'])
				Endif
			EndIf

			FwRestArea(aArea)
			FwFreeArray(aArea)
			FreeObj(jResponseJson)
			FreeObj(jResponseItemJson)
		EndIf
	Else
		Self:setDefaultError(401)
	EndIf
Return Nil

/*/{Protheus.doc} findById
Busca por ID
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param pcCodigoNegocio, character, codigo da negociação(NEB_CODBRT/NEA_CODIGO)
@param pcVersao, character, versao da negociação(NEB_VERSAO)
@param pcInsumo, character, item do insumo da negociação(NEB_ITEM)
@return variant, nil
/*/
Method findById(pcCodigoNegocio, pcVersao, pcInsumo) class agdNegocioInsumoRepository
	Local aArea             As Array
	Local cWhereParam       As Character
	Local nI 				as Numeric
	Local jResponseJson     as json
	Local jResponseItemJson as json

	If !Empty(pcCodigoNegocio)
		If Self:lOk
			aArea := FwGetArea()
			aQueryRequest := {}

			cWhereParam := "NEB.NEB_CODBRT = '" + pcCodigoNegocio + "' AND NEB.NEB_ITEM = '" + pcInsumo + "' "
			cWhereParam += " AND NEB.NEB_VERSAO = '" + pcVersao + "' "

			aFields := self:getFields()

			For nI := 1 to len(aFields)
				Self:AddMapFields(aFields[nI][1], aFields[nI][2] , aFields[nI][4], aFields[nI][4], {aFields[nI][2] , aFields[nI][3], TamSX3(aFields[nI][2])[1], 0})
			Next

			Self:setPage(1)
			Self:setPageSize(1)
			Self:SetQuery(Self:getQueryDefault())
			Self:SetWhere(Self:getWhereDefault(cWhereParam))
			Self:SetStyleReturn("item")

			if Self:Execute()
				Self:FillGetResponse()

				if Self:lOk
					jResponseJson := Self:convertToJson(Self:getJSONResponse())
					jResponseItemJson := jResponseJson['item']
					if Self:validateGetByUnico(jResponseJson)
						Self:setSuccess(jResponseItemJson[1])
					EndIf
				Endif

			endif

			FwRestArea(aArea)
			FwFreeArray(aArea)
			FreeObj(jResponseJson)
			FreeObj(jResponseItemJson)
		EndIf
	Else
		Self:setDefaultError(401)
	EndIf
Return Nil

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositorio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return array, lista de campos
/*/
Method getFields(lFieldsAdd) Class agdNegocioInsumoRepository
	Local aArrayFields := {}
	Default lFieldsAdd := .f. 

	AAdd(aArrayFields, {'filial'            , 'NEB_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'            , 'NEB_CODBRT', 'C', .T.})
	AAdd(aArrayFields, {'versao'            , 'NEB_VERSAO', 'C', .T.})
	AAdd(aArrayFields, {'item'              , 'NEB_ITEM'  , 'C', .T.})
	AAdd(aArrayFields, {'codigoCliente'     , 'NEB_CLIENT', 'C', .T.})
	AAdd(aArrayFields, {'loja'              , 'NEB_LOJENT', 'C', .T.})
	AAdd(aArrayFields, {'codigoProduto'     , 'NEB_CODPRO', 'C', .T.})
	AAdd(aArrayFields, {'unidadeMedida'     , 'NEB_UM'    , 'C', .T.})
	AAdd(aArrayFields, {'precoCusto'        , 'NEB_PRCCUS', 'N', .T.})
	AAdd(aArrayFields, {'precoVenda'        , 'NEB_PRCTAB', 'N', .T.})
	AAdd(aArrayFields, {'quantidade'        , 'NEB_QTDVEN', 'N', .T.})
	AAdd(aArrayFields, {'frete'             , 'NEB_FRETE' , 'N', .T.})	
	AAdd(aArrayFields, {'percentualAjuste'  , 'NEB_PDESAC', 'N', .T.})
	AAdd(aArrayFields, {'valorAjuste'       , 'NEB_VDESAC', 'N', .T.})	
	AAdd(aArrayFields, {'precoFinal'        , 'NEB_PRCUNI', 'N', .T.})	
	AAdd(aArrayFields, {'valorTotal'        , 'NEB_VALOR' , 'N', .T.})
	AAdd(aArrayFields, {'armazem'           , 'NEB_LOCAL' , 'C', .T.})
	AAdd(aArrayFields, {'operacaoFiscal'    , 'NEB_OPER'  , 'C', .T.})
	AAdd(aArrayFields, {'codigoTes'         , 'NEB_TES'   , 'C', .T.})
	AAdd(aArrayFields, {'dataEntrega'       , 'NEB_ENTREG', 'D', .T.})
	AAdd(aArrayFields, {'lote'              , 'NEB_LOTCTL', 'C', .T.})
	AAdd(aArrayFields, {'paridade'          , 'NEB_PARID' , 'N', .T.})
	AAdd(aArrayFields, {'precoCustoOM'      , 'NEB_PCUSM2', 'N', .T.})
	AAdd(aArrayFields, {'precoVendaOM'      , 'NEB_PTABM2', 'N', .T.})
	AAdd(aArrayFields, {'precoFinalOM'      , 'NEB_PUNIM2', 'N', .T.})
	AAdd(aArrayFields, {'valorTotalOM'      , 'NEB_VALM2' , 'N', .T.})
	AAdd(aArrayFields, {'percentualAjusteOM'   , 'NEB_PDAM2' , 'N', .T.})
	AAdd(aArrayFields, {'freteOM'           , 'NEB_FRETM2', 'N', .T.})
	AAdd(aArrayFields, {'valorAjusteOM'     , 'NEB_VDAM2' , 'N', .T.})

	If lFieldsAdd
		AAdd(aArrayFields, {'nomeCliente'            , 'A1_NOME'    , 'C', .T.})
		AAdd(aArrayFields, {'descricaoProduto'       , 'B1_DESC'    , 'C', .T.})
		AAdd(aArrayFields, {'descricaoArmazem'       , 'NNR_DESCRI' , 'C', .T.})
		AAdd(aArrayFields, {'descricaoOperacaoFiscal', 'X5_DESCRI'  , 'C', .T.})
		AAdd(aArrayFields, {'descricaoTes'           , 'F4_TEXTO'   , 'C', .T.})
		AAdd(aArrayFields, {'qtdVendaPerdida'        , 'TOTAL_VP'   , 'N', .T.})
		AAdd(aArrayFields, {'qtdInstrucaoEmbarque'   , 'TOTAL_IE'   , 'N', .T.})
		AAdd(aArrayFields, {'saldoNeg'               , 'SLD_NEGOCIO', 'N', .T.})
	EndIf

Return aArrayFields

/*/{Protheus.doc} getQueryJoin
Retorna Array com os parametros usados no relacionamento entre tabelas
@type method
@version P12
@author rodrigo.nsoledade
@since 07/05/2025
@return character, query sql para consulta
/*/
Method getQueryJoin() as Array class agdNegocioInsumoRepository

	Local aArrayJoin    As Array
	Local cJoinSB1 := "SB1.D_E_L_E_T_ = ' ' AND SB1.B1_FILIAL = '" + FWxFilial("SB1") + "' AND SB1.B1_COD = NEB.NEB_CODPRO"
	Local cJoinSA1 := "SA1.D_E_L_E_T_ = ' ' AND SA1.A1_FILIAL = '" + FWxFilial("SA1") + "' AND SA1.A1_COD = NEB.NEB_CLIENT AND SA1.A1_LOJA = NEB.NEB_LOJENT"

	aArrayJoin := {}
	AAdd(aArrayJoin, {"LEFT", "SB1", cJoinSB1})
	AAdd(aArrayJoin, {"LEFT", "SA1", cJoinSA1})
	AAdd(aArrayJoin, {"LEFT", "SF4", ::getWhereDefault("SF4.F4_CODIGO = NEB.NEB_TES", "SF4")})
	AAdd(aArrayJoin, {"LEFT", "SX5", ::getWhereDefault("SX5.X5_TABELA = 'DJ' AND SX5.X5_CHAVE = NEB.NEB_OPER", "SX5")})
	AAdd(aArrayJoin, {"LEFT", "NNR", ::getWhereDefault("NNR.NNR_CODIGO = NEB.NEB_LOCAL", "NNR")})

Return aArrayJoin

/*/{Protheus.doc} getQueryWithTotal
Query com consulta e calculo usando sub query
@type method
@version 12
@author rodrigo.nsoledade
@since 07/05/2025
@return variant, query
/*/
Method getQueryWithTotal() Class agdNegocioInsumoRepository
	Local cQuery    as Character
	Local cVendPerd as Character

	cVendPerd := "(SELECT CASE WHEN COUNT(NEL_QUANT) > 0 THEN SUM(NEL_QUANT) ELSE 0 END FROM "+ RetSqlName("NEL") +" NEL WHERE " + Self:getWhereDefault('NEL_CODBRT = NEB_CODBRT AND NEL_ITEBRT = NEB_ITEM', 'NEL') + ")"
	cInsEmbar := "(SELECT CASE WHEN COUNT(NED_QTDIEB) > 0 THEN SUM(NED_QTDIEB - NED_QTDDEV) ELSE 0 END FROM "+ RetSqlName("NED") +" NED WHERE " + Self:getWhereDefault('NED_CODBRT = NEB_CODBRT AND NED_ITEBRT = NEB_ITEM', 'NED') + ")"

	cQuery := "SELECT *, "+cVendPerd+" AS TOTAL_VP, "+cInsEmbar+" as TOTAL_IE, (NEB_QTDVEN - "+cVendPerd+" - "+cInsEmbar+" ) as SLD_NEGOCIO FROM " + RetSqlName('NEB') 
	
Return cQuery
