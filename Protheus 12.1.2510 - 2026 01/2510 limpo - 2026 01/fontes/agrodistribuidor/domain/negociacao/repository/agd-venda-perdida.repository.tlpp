#INCLUDE "totvs.ch"
#INCLUDE 'tlpp-core.th'
#INCLUDE "FWMVCDEF.CH"

namespace agd.VendaPerdidaRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/


/*/{Protheus.doc} agdVendaPerdidaRepository
Repositório de Negociações Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/
class agdVendaPerdidaRepository FROM agdRepositoryUtils

	public Method New()

	public Method save()
	public Method sumByNegocioInsumo() as numeric
	public Method getFilterById()      as Array
	public Method getPayloadFields()   as Array

	private Method getFields()

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
Method New() Class agdVendaPerdidaRepository
	_Super:New('NEL', self:getFields())
Return Self

/*/{Protheus.doc} sumByNegocioInsumo
Soma de venda perdida do insumo
@type method
@version 12
@author jean.schulze
@since 04/02/2025
@param pcCodigoNegocio, variant, codigo do negocio
@param pcInsumo, variant, codigo do insumo
@return numeric, quantidade de venda perdida
/*/
method sumByNegocioInsumo(pcCodigoNegocio, pcInsumo) as numeric class agdVendaPerdidaRepository
	Local totalVPInsumo := 0

	dbSelectArea('NEL')
	dbSetOrder(1)
	NEL->(DBGoTop())
	NEL->(dbSeek(xFilial('NEL')+pcCodigoNegocio+pcInsumo))

	While NEL->(!EoF()) .and. (xFilial('NEL')+pcCodigoNegocio+pcInsumo == NEL->NEL_FILIAL+NEL->NEL_CODBRT+NEL->NEL_ITEBRT)
		totalVPInsumo += NEL->NEL_QUANT
		NEL->(DBSkip(1))
	end

Return totalVPInsumo

/*/{Protheus.doc} save
Salva um novo negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando
@return variant, nil
/*/
Method save(jCmdRegistrar)  Class agdVendaPerdidaRepository
	Local aNELFields    as Array
	Local nField		as Numeric
	Local oModel 		as object

	oModel := FwLoadModel("AGDA050")
	oModel:SetOperation(MODEL_OPERATION_INSERT)
	oModel:Activate()

	aNELFields := self:jsonToArrayFields(jCmdRegistrar, self:getFields())

	For nField := 1 to len(aNELFields)
		if  oModel:HasField("AGDA050_NEL", aNELFields[nField][1] )
			oModel:SetValue("AGDA050_NEL", aNELFields[nField][1], aNELFields[nField][2])
		endif
	Next

	If oModel:VldData();
			.And. oModel:CommitData()
		Self:setSuccess(oModel:GetValue("AGDA050_NEL", "NEL_ITEM"))
	Else
		::setErrorByErrorMessageModel(oModel, 400)
	EndIf

	oModel:DeActivate()
	oModel:Destroy()
	oModel := NIL
	FreeObj(oModel)
Return nil

/*/{Protheus.doc} getFilterById
Obtem o filtro para getById
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigoNegocio, character, codigo do negocio
@param cItemInsumo, character, item do negocio
@param cItemSeq, character, sequencia do negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigoNegocio, cItemInsumo, cItemSeq) as Array Class agdVendaPerdidaRepository
	Local aFilterId := {}

	AAdd(aFilterId, {'NEL_CODBRT', cCodigoNegocio})
	AAdd(aFilterId, {'NEL_ITEBRT', cItemInsumo})
	AAdd(aFilterId, {'NEL_ITEM',   cItemSeq})

return aFilterId

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, lista de campos
/*/
Method getFields() Class agdVendaPerdidaRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'           , 'NEL_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigoNegocio'    , 'NEL_CODBRT', 'C', .T.})
	AAdd(aArrayFields, {'itemInsumo'       , 'NEL_ITEBRT', 'C', .T.})
	AAdd(aArrayFields, {'item'             , 'NEL_ITEM'  , 'C', .T.})
	AAdd(aArrayFields, {'produto'          , 'NEL_CODPRO', 'C', .T.})
	AAdd(aArrayFields, {'motivo'           , 'NEL_MOTIVO', 'M', .T.})
	AAdd(aArrayFields, {'quantidade'       , 'NEL_QUANT' , 'N', .T.})
	AAdd(aArrayFields, {'usuario'          , 'NEL_NOMUSU', 'C', .T.})
	AAdd(aArrayFields, {'data'             , 'NEL_DATA'  , 'D', .T.})
	AAdd(aArrayFields, {'hora'             , 'NEL_HORA'  , 'C', .T.})

Return aArrayFields

/*/{Protheus.doc} agdVendaPerdidaRepository::getPayloadFields
Obtem os campos a serem carregados no payload
@type method
@version 12
@author jc.maldonado
@since 13/08/2025
@return array, lista de campos
/*/
Method getPayloadFields() as array Class agdVendaPerdidaRepository
	Local aFields := {} as array

	aeval(::getFields(), {|xIt| AAdd(aFields, xIt)})
Return aFields
