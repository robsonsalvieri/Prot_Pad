#include 'agd.negocio.service.ch'
#include "totvs.ch"
#include 'tlpp-core.th'

#DEFINE STEXP_PENDENTE "1"
#DEFINE STEXP_PARCIAL "2"
#DEFINE STEXP_FINALIZADO "3"
#DEFINE MOEDA_REAL 1

namespace agd.negocioService
using namespace agd.negocioRepository /*shortCurt de funcao*/
using namespace agd.negocioStatusService
using namespace agd.negocioInsumoRepository
using namespace agd.instrucaoEmbarqueItemRepository
using namespace agd.utilsService
using namespace agd.tituloFinanceiroService
using namespace agd.negocioTotalizadorService
using namespace agd.negocioDadosContratosService
using namespace agd.negocioImpostosService
using namespace agd.dadosContratosNegocioRepository
using namespace agd.impostosRetidosNegocioRepository
using namespace agd.instrucaoEmbarqueRepository
using namespace agd.pedidoVendaRepository

/*/{Protheus.doc} agdNegocioService
Servico de Negócios Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/ 
class agdNegocioService FROM agdUtilsService

	public method new()

	public method listar()
	public method buscarPorId()
	public method registrar()
	public method deletar()
	public method buscarVersaoAtualValida() as Character
	public method validarSeTemVersionamento() as Logical
	public method buscarIdUnicoNegocio() as Character
	public method gerarVersionamento()
	public method buscarStatusRecebimento() as Character
	public method getTotalMovimentoNEI() as Numeric
	public method getTotalEntradaContratoNJR() as Numeric
	public method getTotalParidadeNEE() as Numeric
	public method getTotalParidadeConvertidaNEE() as Numeric
	public method getStExpedBarter() as Character
	public method getPrecoCustoProdut() as Array
	public method getTotalizador()
	public method setNegociacao()
	public method getStatusNegocio()
	public method getJSONFromModel()
	public method getTotValorNegociado() as Numeric
	public method getTotFinNegoc() as Numeric
	public method getPercFinanc() as Character
	public method getTaxaMoeda() as Numeric
	public method getPedidosVenda() as array
	public method verificaAlteracaoModelo()
	public method verificaVincularContrato()
	public method convertePrecoMoedaPadraoRelacaoTroca() as Numeric
	public method convertePrecoOutraMoedaRelacaoTroca() as Numeric
endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
method new() class agdNegocioService
	_Super:New()
return Self

/*/{Protheus.doc} listar
Listar Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdNegocioService
	Local oNegocioRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oNegocioRepository := agdNegocioRepository():New()
	oNegocioRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oNegocioRepository)

	oNegocioRepository:DeActivate()
	FreeObj(oNegocioRepository)
Return nil


/*/{Protheus.doc} buscarPorId
Buscar Por Id Unico
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param cCodigo, character, id unico
@return variant, nil
/*/
method buscarPorId(cCodigo, cVersao) class agdNegocioService
	Local oNegocioRepository 							As object
	Local oNegocioInsumosRepository 					As object
	Local oDadosContratosNegocioRepository 				As object
	Local oImpostosRetidosNegocioRepository	As object
	Local oResponseMerge    							As object
	Local oJsonReturn        							As object
	Local oNegocioTotalizadorService 					As object
	Local oImpostosService           					As Object
	
	oNegocioRepository := agdNegocioRepository():New()
	oNegocioInsumosRepository := agdNegocioInsumoRepository():New()
	oDadosContratosNegocioRepository := agdDadosContratosNegocioRepository():New("")
	oImpostosRetidosNegocioRepository := agdImpostosRetidosNegocioRepository():New("")

	oNegocioRepository:findById(oNegocioRepository:getFilterById(cCodigo, cVersao))

	oResponseMerge := oNegocioRepository:getResponse()

	Self:setFullResponse(oNegocioRepository)

	if oNegocioRepository:isSuccess()
		oJsonReturn  := JsonObject():new()
		oJsonReturn['codigo'] := cCodigo
		oJsonReturn['versao'] := cVersao
		oNegocioInsumosRepository:find(1, 9999,oJsonReturn)
		oResponseMerge['listInsumos'] := oNegocioInsumosRepository:getResponse()['items']

		oDadosContratosNegocioRepository:find(1, 9999,oJsonReturn)
		oResponseMerge['listLocaisContratos'] := oDadosContratosNegocioRepository:getResponse()['items']
		
		oImpostosRetidosNegocioRepository:find(1, 9999,oJsonReturn)
		oResponseMerge['listImpostosRetidos'] := oImpostosRetidosNegocioRepository:getResponse()['items']

		oResponseMerge['listImpostosRetidos'] := {}
		if Self:buscarVersaoAtualValida(cCodigo) == cVersao
			oImpostosService := agdNegocioImpostosService():New()
			if oImpostosService:consultarImpostos(cCodigo)
				oResponseMerge['listImpostosRetidos'] := aClone(oImpostosService:getResponse()['items'])
			endif
			FWFreeObj(oImpostosService)
		endif

		oNegocioTotalizadorService := agdNegocioTotalizadorService():New(oResponseMerge)
		oResponseMerge['totais']   := JsonObject():New()
		oResponseMerge['totais']:fromJson(oNegocioTotalizadorService:getTotais():toJson())
		FWFreeObj(oNegocioTotalizadorService)

		Self:setSuccess(oResponseMerge)
	endif

	oNegocioRepository:DeActivate()
	oNegocioInsumosRepository:DeActivate()
	FreeObj(oNegocioRepository)
	FreeObj(oNegocioInsumosRepository)
return nil

/*/{Protheus.doc} registrar
Comando para registrar um Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param jCmdRegistrar, json, comando de payload
@return variant, nil
/*/
method registrar(jCmdRegistrar) class agdNegocioService
	Local oNegocioRepository As Object

	oNegocioRepository := agdNegocioRepository():New()

	oNegocioRepository:save(jCmdRegistrar)

	Self:setFullResponse(oNegocioRepository)

	oNegocioRepository:DeActivate()
	FreeObj(oNegocioRepository)
return nil


/*/{Protheus.doc} Deletar
deletar negociacao
@type method
@version 12
@author carlos.augusto
@since 20/05/2025
@param cIdNegocioBarter, character, chave negociacao NEA
@return variant, nil
/*/
method deletar(cIdNegocioBarter) class agdNegocioService
	Local oRepository As Object

	oRepository := agdNegocioRepository():New(cIdNegocioBarter)
	oRepository:deleteRepository()
	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
return nil


/*/{Protheus.doc} agdNegocioService::buscarVersaoAtualValida
Busca a versão atual valida da negociação, a qual não esteja versionada.
@type method
@version P12 
@author claudineia.reinert
@since 13/02/2025
@param pcCodigo, character, Código da negociação
@return character, codigo da versão
/*/
method buscarVersaoAtualValida(pcCodigo as Character) as Character class agdNegocioService
	Local cVersao as character
	Local oNegocioRepository As Object

	oNegocioRepository := agdNegocioRepository():New()

	cVersao := oNegocioRepository:findLastVersion(pcCodigo)

	FreeObj(oNegocioRepository)
return cVersao

/*/{Protheus.doc} agdNegocioService::validarSeTemVersionamento
Valida de negociação tem versionamento gerado.
@type method
@version P12
@author claudineia.reinert
@since 13/02/2025
@param pcCodigo, character, Código da negociação
@return logical, .t. ou .F.
/*/
method validarSeTemVersionamento(pcCodigo as Character) as Logical class agdNegocioService
	Local lRet as Logical
	Local oNegocioRepository As Object

	oNegocioRepository := agdNegocioRepository():New()

	lRet := oNegocioRepository:hasVersionamento(pcCodigo)

	FreeObj(oNegocioRepository)
return lRet

/*/{Protheus.doc} agdNegocioService::buscarIdUnicoNegocio
Busca o valor da chave unica(ID) da negociação
@type method
@version P12
@author claudineia.reinert
@since 21/02/2025
@param pcCodigo, character, codigo da negociação
@param pcVersao, character, versão da negociação, parametro opcional
@return character, valor chave unica da negociação
/*/
method buscarIdUnicoNegocio(pcCodigo as Character, pcVersao as Character) as Character class agdNegocioService
	Local cRet as Character
	Local oNegocioRepository As Object

	Default pcVersao := ""

	oNegocioRepository := agdNegocioRepository():New()

	If Empty(pcVersao)
		pcVersao := Self:buscarVersaoAtualValida(pcCodigo)
	EndIf

	lRet := oNegocioRepository:isRegistroUnico(xFilial("NEA")+pcCodigo+pcVersao)
	If lRet
		cRet := oNegocioRepository:cChaveIDRepository
	Else
		cRet := ""
	EndIf

	FreeObj(oNegocioRepository)
return cRet

/*/{Protheus.doc} versionar
Realiza o versionamento da negociação. Necessario estar posicionado sobre o registro da tabela NEA.
A negociação selecionada passará para o status versionado e será gerado uma copia desta negociação com uma nova versão. 
Esta nova versão da negociação ficará em aberto para edição e aprovações. 
@type method
@version P12
@author claudineia.reinert
@since 14/02/2025
@param lUsaTela, logical, se é via tela de negociação, default = .T.
/*/
method gerarVersionamento(pcIdNegocioBarter as character, lUsaTela as Logical, cMsgHist as character) class agdNegocioService
	Local oNegocioRepository 	As Object
	Local oNegocioSatusService 	As Object
	Local cVersao				As Character
	Local cTempStatus			As Character

	Default lUsaTela := .T.
	Default cMsgHist := ""

	//versiona o registro atual
	oNegocioSatusService := agdNegocioStatusService():new(pcIdNegocioBarter)
	cTempStatus := oNegocioSatusService:getStatusAtual()
	oNegocioSatusService:versionar(lUsaTela, cMsgHist)

	If oNegocioSatusService:isSuccess()
		oNegocioRepository := agdNegocioRepository():New(pcIdNegocioBarter)
		If oNegocioRepository:isRegistroValido()
			//gera uma copia do registro setando uma nova versão
			oNegocioRepository:gerarNovaVersao(cTempStatus)
			Self:setFullResponse(oNegocioRepository)
			if self:isSuccess()
				DBSelectArea("NEA")
				NEA->(dbSetOrder(1))
				If NEA->(dbSeek(pcIdNegocioBarter))
					cVersao:= self:buscarVersaoAtualValida(NEA->NEA_CODIGO)
					If NEA->(dbSeek(NEA->(NEA_FILIAL + NEA_CODIGO + cVersao)))
						oNegocioSatusService := agdNegocioStatusService():new(NEA->(NEA_FILIAL + NEA_CODIGO + cVersao))
						oNegocioSatusService:informarInstrucaoEmbarque()
					endif
				endif
			Else
				Self:setError(STR0001,404) //#"Registro não encontrado."
			EndIf
		Else
			Self:setError(STR0002, 404)
		EndIf
	Else
		Self:setFullResponse(oNegocioSatusService)
	EndIf

	FreeObj(oNegocioRepository)
	FreeObj(oNegocioSatusService)

return nil

/*/{Protheus.doc} buscarStatusRecebimento
Retorna o status de recebimento (1=Pendente, 2=Parcial, 3=Finalizado)
@type method
@version P12
@author rodrigo.nsoledade
@since 25/03/2025
@param cIdNegocioBarter, character, id da negociação barter
@return character, valores (1, 2 ou 3)
/*/ 
method buscarStatusRecebimento(cIdNegocioBarter as character, lRetPerc as logical) as character class agdNegocioService
	Local cUMPreco      as character
	Local cCodProduto   as character
	Local nQtdNegoc     as numeric
	Local nQtdRecebida  as numeric
	Local nPercent      as numeric
	Local xRetorno      := Nil
	Default lRetPerc := .F.

	// Status Default: 1=Pendente
	xRetorno := IIF(lRetPerc,"0","1")

	// Busca dados do cabeçalho da negociação
	dbSelectArea("NEA")
	NEA->(dbSetOrder(1)) // NEA_FILIAL+NEA_CODIGO+NEA_VERSAO
	If NEA->(dbSeek(cIdNegocioBarter))

		cUMPreco     := NEA->NEA_UMPRC
		cCodProduto  := NEA->NEA_CODPRO

		// total negociado
		nQtdNegoc := NEA->NEA_QTCONV

		// Quantidades movimentadas Contrato Compra
		nQtdRecebida := Self:getTotalMovimentoNEI(NEA->NEA_CODIGO)

		If lRetPerc
			If nQtdNegoc > 0
				nPercent := (nQtdRecebida / nQtdNegoc) * 100
				xRetorno := Min(Round(nPercent,0), 100)
			EndIf
		Else
			// Determina o status com base na quantidade recebida
			If nQtdRecebida > 0
				xRetorno := "2" // Parcial
			EndIf

			If nQtdRecebida >= nQtdNegoc .and. nQtdNegoc > 0
				xRetorno := "3" // Finalizado
			EndIf
		EndIf
	EndIf

Return xRetorno

/*/{Protheus.doc} getTotalMovimentoNEI
Soma a quantidade de entradas (tipo 1) nos movimentos da NEI relacionados à negociação
@type method
@version P12
@author rodrigo.nsoledade
@since 25/03/2025
@param cCodNegocio, character, codigo da negociação
@return numeric, quantidade do contrato
/*/
method getTotalMovimentoNEI(cCodNegocio as character) as numeric class agdNegocioService
	Local cQueryNEI as character
	Local oStatement as object

	cQueryNEI := " SELECT SUM(NEI.NEI_QUANT) AS QTDE "
	cQueryNEI += " FROM " + RetSQLName("NEF") + " NEF "
	cQueryNEI += " JOIN  " + RetSQLName("NEI") + " NEI "
	cQueryNEI += " ON NEI.NEI_FILIAL = NEF.NEF_FILIAL "
	cQueryNEI += " AND NEI.NEI_IDCTR = NEF.NEF_IDCTR AND NEI.D_E_L_E_T_ = '' "
	cQueryNEI += " WHERE " + RetSqlCond("NEF")
	cQueryNEI += "     AND NEF.NEF_CODBRT = ? "
	cQueryNEI += "     AND NEI.NEI_TIPO = '1' " // Entrada
	cQueryNEI := ChangeQuery(cQueryNEI)

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQueryNEI)
	oStatement:SetString(1, cCodNegocio)
	cQueryNEI := oStatement:GetFixQuery()

return MpSysExecScalar(cQueryNEI, "QTDE")

/*/{Protheus.doc} getTotalEntradaContratoNJR
Soma a quantidade física recebida (QTEFIS) dos contratos relacionados à negociação
@type method
@version P12
@author rodrigo.nsoledade
@since 25/03/2025
@param cCodNegocio, character, código da negociação
@return numeric, quantidade fisica
/*/
method getTotalEntradaContratoNJR(cCodNegocio as character)  as numeric class agdNegocioService
	Local cQueryNJR as character
	Local oStatement as object

	cQueryNJR := " SELECT SUM(NJR_QTEFIS) AS QTDE " //NJR_UM1PRO
	cQueryNJR += " FROM " + RetSQLName("NEF") + " NEF "
	cQueryNJR += " JOIN " + RetSQLName("NJR") + " NJR "
	cQueryNJR += "   ON NJR.NJR_FILIAL = NEF.NEF_FILIAL "
	cQueryNJR += "  AND NJR.NJR_CODCTR = NEF.NEF_CTREXT "
	cQueryNJR += " WHERE " + RetSqlCond("NEF")
	cQueryNJR += "   AND NEF.NEF_CODBRT = ? "
	cQueryNJR += "   AND NJR.D_E_L_E_T_ = '' "
	cQueryNJR += " GROUP BY NJR_UM1PRO "
	cQueryNJR := ChangeQuery(cQueryNJR)

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQueryNJR)
	oStatement:SetString(1, cCodNegocio)
	cQueryNJR := oStatement:GetFixQuery()

return MpSysExecScalar(cQueryNJR, "QTDE")

/*/{Protheus.doc} getTotalParidadeNEE
Retorna a soma da quantidade negociada nos contratos da negociação na unidade de preço da negociação(NEE_QTDSAC) 
@type method
@version P12
@author rodrigo.nsoledade
@since 28/03/2025
@param cCodNegocio, character, código da negociação
@param cCodVersao, character, código da versão
@param nTipoUM, numeric, tipo de unidade 1 = NEE_QTDSAC (unidade de preço), 2 = NEE_QTCONV (unidade de produto), padrão será 1
@return numeric, quantidade total de paridade nos contratos(NEE_QTDSAC)
/*/
method getTotalParidadeNEE(cCodNegocio as character, cCodVersao as character) as numeric class agdNegocioService
	Local cQueryNEE   as character
	Local oStatement  as object
	
	cQueryNEE := " SELECT SUM(NEE_QTDSAC) AS QTDE "
	cQueryNEE += " FROM " + RetSQLName("NEE") + " NEE "
	cQueryNEE += " WHERE " + RetSqlCond("NEE")
	cQueryNEE += "   AND NEE.NEE_CODBRT = ? "
	cQueryNEE += "   AND NEE.NEE_VERSAO = ? "
	cQueryNEE := ChangeQuery(cQueryNEE)

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQueryNEE)
	oStatement:SetString(1, cCodNegocio)
	oStatement:SetString(2, cCodVersao)
	cQueryNEE := oStatement:GetFixQuery()

return MpSysExecScalar(cQueryNEE, "QTDE")

/*/{Protheus.doc} getTotalParidadeConvertidaNEE
Retorna a soma da quantidade negociada nos contratos da negociação na unidade de produto(NEE_QTCONV) 
@type method
@version P12
@author claudineia.reinert
@since 02/10/2025
@param cCodNegocio, character, código da negociação
@param cCodVersao, character, código da versão
@return numeric, quantidade total de paridade nos contratos(NEE_QTCONV)
/*/
method getTotalParidadeConvertidaNEE(cCodNegocio as character, cCodVersao as character) as numeric class agdNegocioService
	Local cQueryNEE   as character
	Local oStatement  as object
	
	cQueryNEE := " SELECT SUM(NEE_QTCONV) AS QTDE "
	cQueryNEE += " FROM " + RetSQLName("NEE") + " NEE "
	cQueryNEE += " WHERE " + RetSqlCond("NEE")
	cQueryNEE += "   AND NEE.NEE_CODBRT = ? "
	cQueryNEE += "   AND NEE.NEE_VERSAO = ? "
	cQueryNEE := ChangeQuery(cQueryNEE)

	oStatement := FWPreparedStatement():New()
	oStatement:SetQuery(cQueryNEE)
	oStatement:SetString(1, cCodNegocio)
	oStatement:SetString(2, cCodVersao)
	cQueryNEE := oStatement:GetFixQuery()

return MpSysExecScalar(cQueryNEE, "QTDE")

/*/{Protheus.doc} getStExpedBarter
Retornar o status de expedição da negociação
@type method
@version  P12
@author Gilson.Venturi
@since 31/03/2025
@param cIdNegocioBarter, character, id da negociação barter
@return character, return cStatus
/*/
method getStExpedBarter(cIdNegocioBarter as character, lRetPerc as logical) as Character class agdNegocioService
	Local xRetorno		:= Nil
	Local nX      		:= 0
	Local nTotNotas		:= 0
	Local nCancelada	:= 0
	Local nAutorizado	:= 0
	Local nQtdVenda		:= 0
	Local nQtdInstruida	:= 0
	Local nPercent		:= 0
	Local oRepository	:= agdInstrucaoEmbarqueItemRepository():New()
	Default lRetPerc := .F.

	// Busca dados do cabeçalho da negociação
	dbSelectArea("NEA")
	NEA->(dbSetOrder(1)) // NEA_FILIAL+NEA_CODIGO+NEA_VERSAO
	NEA->(dbSeek(cIdNegocioBarter))

	aNotasFiscais := oRepository:findAllNotasFiscaisBarter(NEA->NEA_CODIGO, NEA->NEA_VERSAO)

	For nX:= 1 to Len(aNotasFiscais)
		nQtdVenda		:= aNotasFiscais[nX]['TOTINSTR']
		nQtdInstruida	+= aNotasFiscais[nX]['NED_QTDIEB'] - aNotasFiscais[nX]['NED_QTDDEV']
		nTotNotas		+= 1

		if !aNotasFiscais[nX]['F3_DTCANC'] = ' ' .or. aNotasFiscais[nX]['F3_CODRSEF'] == '101' .or. aNotasFiscais[nX]['F3_CODRSEF'] == '102' // Cancelado ou inutilizado
			nCancelada += 1
		elseif aNotasFiscais[nX]['F3_CODRSEF'] = ' ' .or. aNotasFiscais[nX]['F3_CODRSEF'] == '101' //Autorizado
			nAutorizado += 1
		endif
	Next

	If lRetPerc
		If nQtdVenda > 0
			nPercent := (nQtdInstruida / nQtdVenda) * 100
			xRetorno := Min(Round(nPercent,0), 100)
		Else
			xRetorno := 0
		EndIf
	Else
		if nTotNotas == nCancelada
			xRetorno := STEXP_PENDENTE
		elseif nQtdVenda == nQtdInstruida .and. nTotNotas == nAutorizado
			xRetorno := STEXP_FINALIZADO
		else
			xRetorno := STEXP_PARCIAL
		endif
	EndIf

return xRetorno

/*/{Protheus.doc} getPrecoCustoProdut
Retornar preco de custo do produto/Local
@type method
@version  P12
@author Gilson.Venturi
@since 05/05/2025
@param cCodProd , character, código do produto
@param cCodLocal, character, código do armazem (Local)
@return Array, Custo Produto
/*/
method getPrecoCustoProdut(cCodProd as character, cCodLocal as character) as Array class agdNegocioService
	// Opções: 1-Custo Medio, 2-Custo Unit FIFO, 3-Custo Reposição, 4-Custo Standard

	Local aCustoPrd	:= {0,0,0,0,0}
	Local cTpCusto	:= SUPERGETMV("MV_AGD0001", .F., '1') // 1-Custo Médio
	Local cAlias	:= GetArea()

	Default cCodLocal := ''

	if cTpCusto == '4' //Custo Standard
		dbSelectArea("SB1")
		SB1->(DbSetOrder(1))
		if SB1->(MsSeek(xFilial("SB1") + cCodProd))

			aCustoPrd[Val(SB1->B1_MCUSTD)] := SB1->B1_CUSTD

			if Val(SB1->B1_MCUSTD) > 1
				aCustoPrd[1] := xMoeda(SB1->B1_CUSTD,Val(SB1->B1_MCUSTD),1,SB1->B1_UCALSTD)
			endif
		endif
	else
		DbSelectArea("SB2")
		SB2->(DbSetOrder(1))
		if SB2->(MsSeek(xFilial("SB2")+cCodProd+cCodLocal))

			if cTpCusto == '1'     //1-Custo Medio
				aCustoPrd := {SB2->B2_CM1,SB2->B2_CM2,SB2->B2_CM3,SB2->B2_CM4,SB2->B2_CM5}
			elseif cTpCusto == '2' //2-Custo Unit FIFO
				aCustoPrd := {SB2->B2_CMFF1,SB2->B2_CMFF2,SB2->B2_CMFF3,SB2->B2_CMFF4,SB2->B2_CMFF5}
			elseif cTpCusto == '3' //3-Custo Reposição
				aCustoPrd := {SB2->B2_CMRP1,SB2->B2_CMRP2,SB2->B2_CMRP3,SB2->B2_CMRP4,SB2->B2_CMRP5}
			endif
		endif

	endif

	RestArea(cAlias)

return aCustoPrd

/*/{Protheus.doc} agdNegocioService::setNegociacao
Método para Alterar a Negociação
@type method
@version P12  
@author scheronlini.martins
@since 29/05/2025
@param aFieldsSaveComand, array, param_Array conm campos e Dados
/*/
method setNegociacao(jCmdRegistrar, cChaveIDRepository as Character) class agdNegocioService
	Local oNegocioRepository As Object

	oNegocioRepository := agdNegocioRepository():New(cChaveIDRepository)

	oNegocioRepository:save(jCmdRegistrar, .T.)

	Self:setFullResponse(oNegocioRepository)

	oNegocioRepository:DeActivate()
	FreeObj(oNegocioRepository)

return nil

/*/{Protheus.doc} Status da Negociação Recebimento/Expedição
Buscar por Codigo, Versao
@type method
@version 12
@author lindembergson.pacheco
@since 05/06/2025
@param cCodigo, character, cVersao, character
@return variant, nil
/*/
method getStatusNegocio(cCodigo, cVersao) class agdNegocioService

	Local oResponseMerge	:= JsonObject():New()
	Local cStatusExp		:= ''
	Local cStatusRec		:= ''

	//Proteção do campo
	If NEA->(ColumnPos('NEA_STSREC')) > 0 .And. NEA->(ColumnPos('NEA_STSEXP')) > 0
		dbSelectArea("NEA")
		NEA->(dbSetOrder(1)) // NEA_FILIAL+NEA_CODIGO+NEA_VERSAO
		If NEA->(dbSeek(FWxFilial("NEA") + cCodigo + cVersao))
			cStatusRec := (X3Combo( "NEA_STSREC", NEA->NEA_STSREC ))
			cStatusExp := (X3Combo( "NEA_STSEXP", NEA->NEA_STSEXP ))
		EndIf
	else
		cStatusExp := self:getStExpedBarter(FWxFilial("NEA") + cCodigo + cVersao)
		cStatusRec := self:buscarStatusRecebimento(FWxFilial("NEA") + cCodigo + cVersao)

		cStatusRec := (X3Combo( "NEA_STSREC", cStatusRec ))
		cStatusExp := (X3Combo( "NEA_STSEXP", cStatusExp ))
	EndIf

	oResponseMerge['statusRecebimento'] 	:= UPPER(FwNoAccent(cStatusRec))
	oResponseMerge['statusExpedicao']		:= UPPER(FwNoAccent(cStatusExp))
	Self:setSuccess(oResponseMerge)

	FreeObj(oResponseMerge)

return nil

/*/{Protheus.doc} getJSONFromModel
Retornar jSon a partir de um modelo
@type method
@version P12  
@author lindembergson.pacheco
@since 17/06/2025
@return jSon, modelo de dados
/*/
method getJSONFromModel(oModel as Object, nLinhaDelNEB as Numeric) class agdNegocioService
	Local oNegocioRepository As Object
	Local nX            := 0
	Local nY            := 0
	Local oNEA          := oModel:getModel("AGDA010_NEA")
	Local oNEB          := oModel:getModel("AGDA010_NEB")
	Local oNEE          := oModel:getModel("AGDA010_NEE")
	Local oNEN          := oModel:getModel("AGDA010_NEN")
	Local aNEAFields    := oNEA:OFORMMODELSTRUCT:getFields()
	Local aNEBFields    := oNEB:OFORMMODELSTRUCT:getFields()
	Local aNEEFields    := oNEE:OFORMMODELSTRUCT:getFields()
	Local aNENFields    := oNEN:OFORMMODELSTRUCT:getFields()
	Local aJNEA         := {}
	Local aJNEB         := {}
	Local aJNEE         := {}
	Local aJNEN         := {}
	Local aJson         := {}
	Local oJson         := JsonObject():New()
	Local oItem         := JsonObject():New()
	Local aLinhas		:= FWSaveRows()

	Default nLinhaDelNEB := 0

	oNegocioRepository := agdNegocioRepository():New()
	aJNEA := oNegocioRepository:getFields()
	aJNEB := oNegocioRepository:getFieldsRelationship("listInsumos")
	aJNEE := oNegocioRepository:getFieldsRelationship("listLocaisContratos")
	aJNEN := oNegocioRepository:getFieldsRelationship("listImpostosRetidos")

	//Montando JSON para NEA
	For nX := 1 to Len(aNEAFields)
		Aeval( aJNEA,;
			{ |x|  IF(alltrim(x[2]) == alltrim(aNEAFields[nX, 3]), oJson[x[1]] := FWFldGet(aNEAFields[nX, 3]) ,  ) })
	Next nX

	aJson := {}

	//Montando JSON para NEB
	For nX := 1 to oNEB:Length()
		oNEB:GoLine( nX )
		If !oNEB:IsDeleted()
			IF !Empty(oNEB:GetValue("NEB_CODPRO"))
				oItem	:= JsonObject():New()
				For nY := 1 to Len(aNEBFields)
					If nLinhaDelNEB != nX
						Aeval( aJNEB,;
							{ |x|  IF(alltrim(x[2]) == alltrim(aNEBFields[nY, 3]), oItem[x[1]] := FWFldGet(aNEBFields[nY, 3]) ,  ) })
					EndIf
				Next
				aAdd(aJson, oItem)
			ENDIF
		endif
	Next

	oJson['listInsumos'] := aJson

	aJson := {}

	//Montando JSON para NEE
	For nX := 1 to oNEE:Length()
		oNEE:GoLine( nX )
		If !oNEE:IsDeleted()
			IF !Empty(oNEE:GetValue("NEE_CDPROP"))
				oItem	:= JsonObject():New()
				For nY := 1 to Len(aNEEFields)
					Aeval( aJNEE,;
						{ |x|  IF(alltrim(x[2]) == alltrim(aNEEFields[nY, 3]), oItem[x[1]] := FWFldGet(aNEEFields[nY, 3]) ,  ) })
				Next
				aAdd(aJson, oItem)
			ENDIF
		endif
	Next

	oJson['listLocaisContratos'] := aJson

	aJson := {}

	//Montando JSON para NEN
	For nX := 1 to oNEN:Length()
		oNEN:GoLine( nX )
		If !oNEN:IsDeleted()
			IF !Empty(oNEN:GetValue("NEN_IDTRIB"))
				oItem	:= JsonObject():New()
				For nY := 1 to Len(aNENFields)
					Aeval( aJNEN,;
						{ |x|  IF(alltrim(x[2]) == alltrim(aNENFields[nY, 3]), oItem[x[1]] := FWFldGet(aNENFields[nY, 3]) ,  ) })
						
				Next
				aAdd(aJson, oItem)
			ENDIF
		endif
	Next

	oJson['listImpostosRetidos'] := aJson

	FreeObj(oNegocioRepository)
	FWRestRows(aLinhas)
return oJson

/*/{Protheus.doc} getTotalizador
Realiza a listagem dos totalizadores de expedição, recebimento e financeiro
@type method
@version 12
@since 11/06/2025
@author rodrigo.nsoledade
@param cCodBarter, character, código da negociação
@param cVersao, character, versão da negociação
@return Object, JSON com totalizadores
/*/
method getTotalizador(cCodBarter as character) class agdNegocioService
	Local oJsonReturn  as object
	Local cTotFatur    as character
	Local cTotReceb    as character
	Local cTotFinan    as character
	Local cVersao      as character

	oJsonReturn := JsonObject():New()

	// Busca versão atual valida
	cVersao:= self:buscarVersaoAtualValida(cCodBarter)

	// Recebimento (em percentual)
	cTotReceb := Self:buscarStatusRecebimento(FWxFilial("NEA") + cCodBarter + cVersao, .T.)

	// Expedição (em percentual)
	cTotFatur := Self:getStExpedBarter(FWxFilial("NEA") + cCodBarter + cVersao, .T.)

	// Financeiro (em percentual)
	cTotFinan := Self:getPercFinanc(cCodBarter, cVersao)

	// Monta o objeto JSON
	oJsonReturn['faturamento'] := cTotFatur
	oJsonReturn['recebimento'] := cTotReceb
	oJsonReturn['financeiro']  := cTotFinan

	Self:setSuccess(oJsonReturn)

Return

/*/{Protheus.doc} getTotValorNegociado
Retorna o total de valor de insumos e frete negociado (NEB_VALOR + NEB_FRETE) da negociação
@type method
@version P12
@author rodrigo.soledade
@since 18/06/2025
@param cCodNegocio, character, código da negociação
@param cVersao, character, versão da negociação
@return numeric, total de valor negociado (insumos + frete)
/*/
method getTotValorNegociado(cCodNegocio as character, cVersao as character) as numeric class agdNegocioService
	Local nTotal     := 0
	Local cChave     := xFilial("NEB") + cCodNegocio + cVersao

	dbSelectArea("NEB")
	NEB->(dbSetOrder(1))

	If NEB->(dbSeek(cChave))
		While !NEB->(Eof()) .And. xFilial("NEB") + NEB->NEB_CODBRT + NEB->NEB_VERSAO == cChave
			nTotal += NEB->NEB_VALOR
			NEB->(dbSkip())
		EndDo
	EndIf

Return nTotal

/*/{Protheus.doc} getTotFinNegoc
Retorna o total financeiro de todos os fechamentos vinculados à negociação (venda)
@type method
@version 12
@author rodrigo.nsoledade
@since 19/06/2025
@param cCodNegocio, character, código da negociação (NEJ_CODBRT)
@return numeric, soma dos valores financeiros dos fechamentos
/*/
method getTotFinNegoc(cCodNegocio as character) as numeric class agdNegocioService
	Local cQuery       as character
	Local oStmt        as object
	Local cAliasNEJ    as character
	Local cCodFech     as character
	Local nTotalFinal  as numeric
	Local oTituloServ  as object

	nTotalFinal := 0
	cAliasNEJ := GetNextAlias()
	oTituloServ := agdTituloFinanceiroService():New()

	// Monta a query
	cQuery := "SELECT NEJ_CODIGO FROM " + RetSQLName("NEJ") + " NEJ "
	cQuery += "WHERE " + RetSqlCond("NEJ")
	cQuery += "AND NEJ.NEJ_CODBRT = ?"
	cQuery := ChangeQuery(cQuery)

	// Prepara os parâmetros
	oStmt := FWPreparedStatement():New()
	oStmt:SetQuery(cQuery)
	oStmt:SetString(1, cCodNegocio)
	cQuery := oStmt:GetFixQuery()

	// Executa a query e percorre os resultados
	MPSysOpenQuery(cQuery, cAliasNEJ)

	While !(cAliasNEJ)->(Eof())
		cCodFech := (cAliasNEJ)->NEJ_CODIGO
		nTotalFinal += oTituloServ:getTotTituloFinanceiro(cCodFech, "2") // Módulo 2 = Venda
		(cAliasNEJ)->(DbSkip())
	EndDo

	(cAliasNEJ)->(DbCloseArea())

Return nTotalFinal

/*/{Protheus.doc} getPercFinanc
Calcula o percentual financeiro da negociação com base no valor total dos insumos/frete e no total financeiro dos fechamentos.
@type method
@version 12
@since 18/06/2025
@author rodrigo.nsoledade
@param cCodBarter, character, código da negociação
@param cVersao, character, versão da negociação
@return character, percentual em string (0 a 100)
*/
method getPercFinanc(cCodBarter as character, cVersao as character) as character class agdNegocioService
    Local nTotInsumo := 0 as numeric
    Local nTotFecVen := 0 as numeric
    Local nPercFin   := 0 as numeric

    // Total de insumos e frete da negociação
    nTotInsumo := Self:getTotValorNegociado(cCodBarter, cVersao)
    // Total financeiro de todos os fechamentos de venda
    nTotFecVen := Self:getTotFinNegoc(cCodBarter)

    If nTotInsumo > 0 .And. nTotFecVen > 0
        nPercFin := (nTotFecVen / nTotInsumo) * 100
        nPercFin := Min(Round(nPercFin, 0), 100)
    EndIf

Return cValToChar(nPercFin)

/*/{Protheus.doc} verificaAlteracaoModelo
Checa se o modelo da negociação permite edição
@type method
@version 12
@since 25/07/2025
@author carlos.augusto
@param cCodBarter, character, código da negociação
*/
method verificaAlteracaoModelo(cIdNegocioBarter) class agdNegocioService
	Local oRepository As Object

	oRepository := agdNegocioRepository():New(cIdNegocioBarter) 
	oRepository:isRepositoryChange()
	
	Self:setFullResponse(oRepository)
	
	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} agdNegocioService::getTaxaMoeda
Obtem o valor da taxa da moeda
@type method
@version 12
@author jc.maldonado
@since 20/08/2025
@param nMoeda, numeric, codigo da moeda
@param dData, date, data da cotacao
@return numeric, valor da taxa
/*/
method getTaxaMoeda(nMoeda as numeric, dData as date) as numeric class agdNegocioService
	local nTxMoeda as numeric

	if nMoeda > 1 .and. AGDX010(nMoeda, .F. /*lHelp*/)
		nTxMoeda := RecMoeda(dData, nMoeda)
		::setSuccess()
	else
		::setError(STR0003, 400) //"Moeda inválida."
	endif
return nTxMoeda



/*/{Protheus.doc} verificaAlteracaoModelo
Verifica a negociacao pode receber vinculo de contratos
@type method
@version 12
@since 26/08/2025
@author carlos.augusto
@param cCodBarter, character, código da negociação
*/
method verificaVincularContrato(cIdNegocioBarter) class agdNegocioService
	Local oRepository As Object

	oRepository := agdNegocioRepository():New(cIdNegocioBarter) 
	oRepository:canBindContract()
	
	Self:setFullResponse(oRepository)
	
	oRepository:DeActivate()
	FreeObj(oRepository)
return nil

/*/{Protheus.doc} agdNegocioService::converterValorMoedaRelacaoTroca
Converte o valor sempre para R$ REAIS independente da moeda de origem, utilizando a taxa (Cotação) da moeda de relação de troca
desde que o valor seja maior que zero e a moeda de relacao de troca não seja '1'=REAL
@type method
@version 12
@author jc.maldonado
@since 30/12/2025
@param nValor, numeric, valor a ser convertido
@param nMoedaRelacaoTroca, numeric, moeda da relação de troca
@param nTaxaMoedaRelacaoTroca, numeric, taxa da moeda de relação de troca
@return numeric, valor convertido
/*/
method convertePrecoMoedaPadraoRelacaoTroca(nValor as numeric, nMoedaRelacaoTroca as integer, nTaxaMoedaRelacaoTroca as numeric) as numeric class agdNegocioService
	local nMoedaOrigem                    as numeric
	local nTaxaMoedaOrigem                as numeric
	local nMoedaDestino     := MOEDA_REAL as numeric
	local nTaxaMoedaDestino := 1          as numeric
	local nDecimais                       as integer

	if nValor <= 0 .or. nMoedaRelacaoTroca == MOEDA_REAL
		return nValor
	endIf
	
	nMoedaOrigem     := nMoedaRelacaoTroca
	nTaxaMoedaOrigem := nTaxaMoedaRelacaoTroca
	nDecimais        := FWSX3Util():GetFieldStruct("NEB_PRCTAB")[4]
return xMoeda(nValor, nMoedaOrigem, nMoedaDestino, /*dDataTXmoedaDestino*/, nDecimais, nTaxaMoedaOrigem, nTaxaMoedaDestino)

/*/{Protheus.doc} agdNegocioService::convertePrecoOutraMoedaRelacaoTroca
Converte o valor de R$ REAIS para outra moeda da relação de troca, desde que a moeda da relação de troca não seja '1'=REAL e o valor maior que 0
@type method
@version 12
@author jc.maldonado
@since 02/01/2026
@param nValor, numeric, valor a ser convertido
@param nMoedaRelacaoTroca, numeric, moeda da relação de troca
@param nTaxaMoedaRelacaoTroca, numeric, taxa da moeda de relação de troca
@return numeric, valor convertido
/*/
method convertePrecoOutraMoedaRelacaoTroca(nValor as numeric, nMoedaRelacaoTroca as integer, nTaxaMoedaRelacaoTroca as numeric) as numeric class agdNegocioService
	local nMoedaOrigem     := MOEDA_REAL as numeric
	local nTaxaMoedaOrigem := 1          as numeric
	local nMoedaDestino                  as numeric
	local nTaxaMoedaDestino              as numeric
	local nDecimais                      as integer

	if nValor <= 0 .or. nMoedaRelacaoTroca == MOEDA_REAL
		return nValor
	endIf

	nMoedaDestino     := nMoedaRelacaoTroca
	nTaxaMoedaDestino := nTaxaMoedaRelacaoTroca 
	nDecimais         := FWSX3Util():GetFieldStruct("NEB_PRCTAB")[4]
return xMoeda(nValor, nMoedaOrigem, nMoedaDestino, /*dDataTXmoedaDestino*/, nDecimais, nTaxaMoedaOrigem, nTaxaMoedaDestino)

/*/{Protheus.doc} agdNegocioService::getPedidosVenda
Retorna os pedidos de venda do negócio barter
@type method
@version 12
@author jc.maldonado
@since 15/01/2026
@param cCodigoNegocio, character, código do negócio barter
@param nPage, numeric, numero da paginação
@param nPageSize, numeric, numero de itens por paginação
@return array, lista de pedidos
/*/
method getPedidosVenda(cCodigoNegocio as character, nPage as numeric, nPageSize as numeric) as array class agdNegocioService
	local jInstrucaoParameters         as json
	local oRepositoryInstrucaoEmbarque as object
	local aInstrucoesEmbarque          as array
	local oPedidoVendaRepository       as object
	local ajSonPedidos := {}           as array
	local nX                           as numeric

	default nPage     := 1
	default nPageSize := 999

	jInstrucaoParameters := {'codNegocio': cCodigoNegocio, 'filter': "numPedido ne ' '"}
	
	oRepositoryInstrucaoEmbarque := agdInstrucaoEmbarqueRepository():New()
	oRepositoryInstrucaoEmbarque:find(nPage, nPageSize, jInstrucaoParameters)
	aInstrucoesEmbarque := oRepositoryInstrucaoEmbarque:getResponse()['items']
	FWFreeObj(oRepositoryInstrucaoEmbarque)

	if ! empty(aInstrucoesEmbarque)
		for nX := 1 to len(aInstrucoesEmbarque)
			oPedidoVendaRepository := agdPedidoVendaRepository():New(.T. /*lIncludeOtherFields*/)
			oPedidoVendaRepository:find(1, 1, {'codigo': aInstrucoesEmbarque[nX]['numPedido']})
			aAdd(ajSonPedidos, oPedidoVendaRepository:getResponse()['items'][1])
			FWFreeObj(oPedidoVendaRepository)
		next nX
	endif
return ajSonPedidos
