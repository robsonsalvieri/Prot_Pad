#include 'totvs.ch'
#include 'tlpp-core.th'

namespace agd.rateioNegociacaoService

using namespace agd.utilsService
using namespace agd.rateioNegociacaoRepository

/*/{Protheus.doc} agdRateioNegociacaoService
Servico de Rateio de Entregas da Negociacao
@type class
@version 12
@author jc.maldonado
@since 26/05/2025
/*/
class agdRateioNegociacaoService from agdUtilsService

	public method new()
	public method listar()
	public method registrar()
	public method updateRateioEntregas()
	public method deleteRateioEntregas()
	public method convertEntregasPayload()

endclass

/*/{Protheus.doc} agdRateioNegociacaoService::new
Construtor
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@return object, self
/*/
method new() class agdRateioNegociacaoService
	_Super:New()
return Self

/*/{Protheus.doc} agdRateioNegociacaoService::listar
Lista os Rateios de Entrega das negociações
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@param nPage, numeric, numero da pagina
@param nPageSize, numeric, tamanho da pagina
@param jQueryParam, json, parametro jQuery
/*/
method listar(nPage as integer, nPageSize as integer, jQueryParam as json) class agdRateioNegociacaoService
	local oRateioEntregaRepo as object

	default nPage     := 1
	default nPageSize := 10

	oRateioEntregaRepo := agdRateioNegociacaoRepository():New()
	oRateioEntregaRepo:find(nPage, nPageSize, jQueryParam)

	::setFullResponse(oRateioEntregaRepo)

	oRateioEntregaRepo:DeActivate()
	FreeObj(oRateioEntregaRepo)
return

/*/{Protheus.doc} agdRateioNegociacaoService::registrar
Registra Rateio de Entrega da Negociação
@type method
@version 12
@author jc.maldonado
@since 5/26/2025
@param jPayload, json, payload
/*/
method registrar(jPayload as json) class agdRateioNegociacaoService
	local oRateioEntregaRepo as object

	oRateioEntregaRepo := agdRateioNegociacaoRepository():New()
	oRateioEntregaRepo:save(jPayload)

	::setFullResponse(oRateioEntregaRepo)

	oRateioEntregaRepo:DeActivate()
	FreeObj(oRateioEntregaRepo)
return

/*/{Protheus.doc} agdRateioNegociacaoService::updateRateioEntregas
Atualiza a quantidade do Rateio
@type method
@version P12
@author scheronlini.martins
@since 02/07/2025
@param jPayload, json, Json com Payload
/*/
method updateRateioEntregas(jPayload as json) class agdRateioNegociacaoService
	local oRateioEntregaRepo as object

	oRateioEntregaRepo := agdRateioNegociacaoRepository():New()
	oRateioEntregaRepo:updateRateio(jPayload, .T.)

	::setFullResponse(oRateioEntregaRepo)

	oRateioEntregaRepo:DeActivate()
	FreeObj(oRateioEntregaRepo)
return

/*/{Protheus.doc} agdRateioNegociacaoService::deleteRateioEntregas
Deleta o rateio
@type method
@version P12
@author scheronlini.martins
@since 02/07/2025
@param jPayload, json, payload
/*/
method deleteRateioEntregas(jPayload as json) class agdRateioNegociacaoService
	local oRateioEntregaRepo as object

	oRateioEntregaRepo := agdRateioNegociacaoRepository():New()

	oRateioEntregaRepo:deleteRateiosByInsumo(jPayLoad[oRateioEntregaRepo:getJsonFieldName('NEM_CODBRT')],jPayLoad[oRateioEntregaRepo:getJsonFieldName('NEM_ITEM')])

	::setFullResponse(oRateioEntregaRepo)

	oRateioEntregaRepo:DeActivate()
	FreeObj(oRateioEntregaRepo)
return

/*/{Protheus.doc} agdRateioNegociacaoService::convertEntregasPayload
Converte dados para payload
@type method
@version P12
@author scheronlini.martins
@since 13/01/2026
@param oPayload, object, payload em json
@param cIdNegocio, character, id do negocio
@param cIdItem, character, id do Item
@param cIdInsumo, character, id do Insumo
@return json, return_payload
/*/
METHOD convertEntregasPayload(oPayload, cIdNegocio, cIdItem) Class agdRateioNegociacaoService

	if oPayload <> nil
		oPayload["codigoNegocio"] := cIdNegocio
		oPayload["item"] := cIdItem

	else
		oPayload := JsonObject():New()
		oPayload["codigoNegocio"] := cIdNegocio
		oPayload["item"] := cIdItem
	endif

Return oPayload
