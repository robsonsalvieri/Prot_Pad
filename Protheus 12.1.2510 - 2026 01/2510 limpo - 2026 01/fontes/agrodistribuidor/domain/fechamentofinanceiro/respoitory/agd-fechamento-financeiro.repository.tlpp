#INCLUDE 'totvs.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'fwmvcdef.ch'
#INCLUDE 'agd.fechamento.financeiro.repository.ch'

namespace agd.fechamentoFinanceiroRepository
using namespace agd.repositoryUtils /*shortCurt de funcao*/

/*/{Protheus.doc} agdFechamentoFinanceiroRepository
Repositório de Negociações Barter
@type class
@version 12
@author jean.schulze
@since 14/01/2025
/*/
class agdFechamentoFinanceiroRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public Method New()

	public Method updateRepository()
	public Method getFilterById() as Array
	public Method save()
	public Method delete()
	public Method isRepositoryChange()
	public Method getPayloadFields()    	  as array

	private Method getFields()
	private Method getFieldsRelationship()    as array
	private Method getJoins()
	private Method getQueryWithTotal() as Character
	private Method getMaxSequenceItem()  as Character

endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, instância
/*/
Method New(pcChaveIDRepository as character) Class agdFechamentoFinanceiroRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New('NEJ', self:getFields(), Self:getJoins(), Self:getQueryWithTotal())
Return Self

/*/{Protheus.doc} save
Faz a inclusao do fechamento financeiro
@type method
@version 12
@author jc.maldonado
@since 03/06/2025
@param jPayload, json, payload recebido
/*/
method save(jPayload as json, lUpdate as logical) class agdFechamentoFinanceiroRepository
	Local oModel           as object
	Local nX 			   as Numeric
	Local aFieldsCabecalho := Self:getFields()
	Local aFiedsTitulo     := Self:getFieldsRelationship()
	Local cSeqTitulos      := STRZERO(0,TAMSX3("NEK_ITEM")[1])
	
	Default lUpdate := .F.

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA060
		oModel := FwLoadModel("AGDA060")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()

		cSeqTitulos := Self:getMaxSequenceItem(oModel:GetValue("AGDA060_NEJ", "NEJ_CODIGO"))
	
		if Self:isSuccess()

			// Carregamento do cabeçalho
			aFields := Self:jsonToArrayFields(jPayload, aFieldsCabecalho)
			Self:setValueModel(oModel:GetModel("AGDA060_NEJ"), aFields)

			oGridCompras  := oModel:GetModel("COMPR_NEK")		   
		    oGridCompras:DelAllLine()	  

			if lUpdate .and. Len(jPayload["titulosCompra"]) > 0
				oGridCompras:AddLine()
			endif
			
			For nX := 1 To Len(jPayload["titulosCompra"])
				if Self:isSuccess()
					if nX > 1
						oGridCompras:AddLine()
					endif	
					
					//ajuste de campos operacionais
					cSeqTitulos := Soma1(cSeqTitulos)
					oGridCompras:SetValue('CHECK', .T.)	
					oGridCompras:SetValue('NEK_ITEM', cSeqTitulos)	
					
					//informar demais campos
					aFields := Self:jsonToArrayFields(jPayload["titulosCompra"][nX], aFiedsTitulo) 
					Self:setValueModel(oGridCompras, aFields)
					
					
				endif
			Next

			oGridVendas	  := oModel:GetModel("VENDA_NEK")
			oGridVendas:DelAllLine()
			
			if lUpdate .and. Len(jPayload["titulosVenda"]) > 0
				oGridVendas:AddLine()
			endif

			For nX := 1 To Len(jPayload["titulosVenda"])
				if Self:isSuccess()
					if nX > 1
						oGridVendas:AddLine()
					endif	
					
					//ajuste de campos operacionais
					cSeqTitulos := Soma1(cSeqTitulos)
					oGridVendas:SetValue('CHECK', .T.)
					oGridVendas:SetValue('NEK_ITEM', cSeqTitulos)	
										
					aFields := Self:jsonToArrayFields(jPayload["titulosVenda"][nX], aFiedsTitulo)
					Self:setValueModel(oGridVendas, aFields)
					
				endif
			Next

			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDA060_NEJ", "NEJ_CODIGO"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif			

		endif

		oModel:DeActivate()
		oModel:Destroy()
		FWFreeObj(oModel)
	endif
Return

/*/{Protheus.doc} updateRepository
Atualiza Repositório
@type method
@version 12
@author jean.schulze
@since 16/01/2025
@param aFieldsSaveComand, array, lista de campos
@return variant, nil
/*/
method updateRepository(aFieldsSaveComand as Array) Class agdFechamentoFinanceiroRepository
	Local nField		as Numeric
	Local aArea		:= GetArea()

	dbSelectArea('NEJ')
	NEJ->(dbSetOrder(1))
	NEJ->(dbSeek(Self:cChaveIDRepository))

	If Self:cChaveIDRepository  == NEJ->(NEJ_FILIAL+NEJ_CODIGO)

		RecLock("NEJ",.f.)
		For nField := 1 to len(aFieldsSaveComand)
			NEJ->( &(aFieldsSaveComand[nField][1]) ) :=  aFieldsSaveComand[nField][2]
		Next
		NEJ->(MsUnlock())

		Self:setSuccess(Self:cChaveIDRepository)

	endif

	RestArea(aArea)

return nil

/*/{Protheus.doc} agdFechamentoFinanceiroRepository::delete
Exclui fechamento financeiro
@type method
@version 12
@author jc.maldonado
@since 21/05/2025
/*/
method delete() class agdFechamentoFinanceiroRepository
	local aArea  as array
	local oModel as object
	local cError as character

	dbSelectArea('NEJ')
	aArea := NEJ->(FWGetArea())

	NEJ->(dbSetOrder(1))
	if NEJ->(dbSeek(::cChaveIDRepository));
			.and. ::cChaveIDRepository == NEJ->NEJ_FILIAL + NEJ->NEJ_CODIGO

		oModel := FwLoadModel("AGDA060")
		oModel:SetOperation(MODEL_OPERATION_DELETE)

		if oModel:Activate();
				.and. oModel:VldData();
				.and. oModel:CommitData()
			::setSuccess(::cChaveIDRepository)
		else
			cError := cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]);
				+ ' - ' + cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]);
				+ ' - ' + cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
			::setError(cError, 400)
		endIf

		oModel:DeActivate()
		oModel:Destroy()
		oModel := nil
		FWFreeObj(oModel)
	else
		::setError(STR0001, 400) // "Registro não encontrado."
	endif

	FWRestArea(aArea)
	FWFreeArray(aArea)
return

/*/{Protheus.doc} getFields
Retorna a lista de campos do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return variant, lista de campos
/*/
Method getFields() Class agdFechamentoFinanceiroRepository
	Local aArrayFields := {}

	AAdd(aArrayFields, {'filial'                 , 'NEJ_FILIAL', 'C', .T.})
	AAdd(aArrayFields, {'codigo'                 , 'NEJ_CODIGO', 'C', .T.})
	AAdd(aArrayFields, {'codigoNegocio'          , 'NEJ_CODBRT', 'C', .T.})
	AAdd(aArrayFields, {'codigoVendedor'         , 'NEJ_CODVEN', 'C', .T.})
	AAdd(aArrayFields, {'descricaoVendendor'     , 'A3_NOME',    'C', .T.})
	AAdd(aArrayFields, {'codigoCliente'          , 'NEJ_CODCLI', 'C', .T.})
	AAdd(aArrayFields, {'codigoClienteLoja'      , 'NEJ_LOJA',   'C', .T.})
	AAdd(aArrayFields, {'descricaoCliente'       , 'A1_NOME',    'C', .T.})
	AAdd(aArrayFields, {'status'                 , 'NEJ_STATUS', 'C', .T.})
	AAdd(aArrayFields, {'dtFechamento'           , 'NEJ_DATFEC', 'D', .T.})

	AAdd(aArrayFields, {'valorTotalPagar'        , 'VLR_PAGAR',     'N', .T.})
	AAdd(aArrayFields, {'valorTotalReceber'      , 'VLR_RECEBER',   'N', .T.})
	AAdd(aArrayFields, {'titulosPagar'           , 'ITENS_PAGAR',   'N', .T.})
	AAdd(aArrayFields, {'titulosReceber'         , 'ITENS_RECEBER', 'N', .T.})
	AAdd(aArrayFields, {'valorDiferenca'         , 'VLR_DIFERENCA', 'N', .T.})
	

Return aArrayFields

/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param cCodigo, character, codigo Negocio
@param cVersao, character, versao negocio
@return array, lista de filtro
/*/
Method getFilterById(cCodigo) as Array Class agdFechamentoFinanceiroRepository
    Local aFilterId := {}

	AAdd(aFilterId, {'NEJ_CODIGO', cCodigo}) 

return aFilterId


/*/{Protheus.doc} getJoins
Retorna os joins das consultas
@type method
@version 12
@author jean.schulze
@since 07/05/2025
@return variant, array de dados
/*/
Method getJoins() Class agdFechamentoFinanceiroRepository
	Local aArrayJoins  := {}
	
	AAdd(aArrayJoins, {'LEFT OUTER', 'SA1' , Self:getWhereDefault('SA1.A1_COD = NEJ.NEJ_CODCLI AND SA1.A1_LOJA = NEJ_LOJA', 'SA1')})
	AAdd(aArrayJoins, {'LEFT OUTER', 'SA3' , Self:getWhereDefault('SA3.A3_COD = NEJ.NEJ_CODVEN', 'SA3')})

Return aArrayJoins

/*/{Protheus.doc} getQueryWithTotal
Select com totalizadores
@type method
@version 12
@author jean.schulze
@since 14/05/2025
@return Character, query
/*/
Method getQueryWithTotal() as Character Class agdFechamentoFinanceiroRepository
	Local cQuery    as Character
	
	cQuery := "SELECT NEJ.*, NEKP.VLR_PAGAR, NEKP.ITENS_PAGAR, NEKR.VLR_RECEBER, NEKR.ITENS_RECEBER, (NEKP.VLR_PAGAR - NEKR.VLR_RECEBER) AS VLR_DIFERENCA  FROM " + RetSqlName('NEJ')  + " NEJ "
	cQuery += "LEFT JOIN (SELECT NEK_FILIAL, NEK_CODFEC,  SUM(NEK_LIQUID) as VLR_PAGAR, COUNT(*) as ITENS_PAGAR FROM " + RetSqlName('NEK')  + " NEK  where "+ Self:getWhereDefault("NEK_CODMOD = '1'", "NEK")+" GROUP BY NEK_FILIAL, NEK_CODFEC ) as NEKP ON NEKP.NEK_CODFEC = NEJ_CODIGO "
	cQuery += "LEFT JOIN (SELECT NEK_FILIAL, NEK_CODFEC,  SUM(NEK_LIQUID) as VLR_RECEBER, COUNT(*) as ITENS_RECEBER FROM " + RetSqlName('NEK')  + " NEK where "+ Self:getWhereDefault("NEK_CODMOD = '2'", "NEK")+" GROUP BY NEK_FILIAL, NEK_CODFEC ) as NEKR ON NEKR.NEK_CODFEC = NEJ_CODIGO "

Return cQuery

/*/{Protheus.doc} agdFechamentoFinanceiroRepository::getFieldsRelationship
Obtem a lista de campos das Notas Fiscais Compra/Venda (tabela NEK) utilizado no model AGDA060 
@type method
@version 12
@author jc.maldonado
@since 28/05/2025
@return array, {{campoJson, campoProtheus, tipoCampo, lJsonField|lFixed}, ...}
/*/
method getFieldsRelationship() as array class agdFechamentoFinanceiroRepository
	local aFields   as array
	

	aFields := {{'filial'          , 'NEK_FILIAL', 'C', .T.},;
				{'numeroFechamento', 'NEK_CODFEC', 'C', .T.},;
				{'sequencia'       , 'NEK_ITEM'  , 'C', .T.},;
				{'codigoNegocio'   , 'NEK_CODBRT', 'C', .T.},;
				{'origem'          , 'NEK_CODMOD', 'C', .T.},;
				{'codigoNota'      , 'NEK_CODNF' , 'C', .T.},;
				{'serieNota'       , 'NEK_SERNF' , 'C', .T.},;
				{'emissao'         , 'NEK_DTEMIS', 'D', .T.},;
				{'vencimento'      , 'NEK_DTVENC', 'D', .T.},;
				{'codigoFornecedor', 'NEK_CODFOR', 'C', .T.},;
				{'lojaFornecedor'  , 'NEK_LOJA'  , 'C', .T.},;
				{'nomeFornecedor'  , 'NEK_NOME'  , 'C', .T.},;
				{'valorTitulo'     , 'NEK_VALOR' , 'N', .T.},;
				{'valorRetencao'   , 'NEK_RETENC', 'N', .T.},;
				{'valorLiquido'    , 'NEK_LIQUID', 'N', .T.},;
				{'filialTitulo'    , 'NEK_FILTIT', 'C', .T.},;
				{'prefixoTitulo'   , 'NEK_PREFIX', 'C', .T.},;
				{'codigoTitulo'    , 'NEK_CODTIT', 'C', .T.},;
				{'parcelaTitulo'   , 'NEK_PARCEL', 'C', .T.},;
				{'tipoTitulo'      , 'NEK_TIPO'  , 'C', .T.},;
				{'serieTitulo'     , 'NEK_SERTIT', 'C', .T.}}
return aFields


/*/{Protheus.doc} isRepositoryChange
Verifica se modelo permite edicao
@type method
@version 12
@author carlos.augusto
@since 25/07/2025
@return logical, modelo permite não edicao
/*/
Method isRepositoryChange(cChaveIdFechamentoFinanceiro as character) Class agdFechamentoFinanceiroRepository
	
	If Self:existsById(Self:cChaveIDRepository)
		Self:buildModelCheck("AGDA060")
	Endif

return nil

/*/{Protheus.doc} getPayloadFields
Retorna todos os campos disponiveis para o payload
@type method
@version 12
@author jean.schulze
@since 11/08/2025
@return array, array de campos
/*/
Method getPayloadFields() as Array Class agdFechamentoFinanceiroRepository
	Local aFields:= {}
	
	AEval(Self:getFields(),{|field| AAdd(aFields, field)})
	AEval(Self:getFieldsRelationship(),{|field| AAdd(aFields, field)})

Return aFields

/*/{Protheus.doc} agdFechamentoFinanceiroRepository::getMaxSequenceItem
Retorna a sequencia maxima da NEK_ITEM gravado no banco de dados
@type method
@version  P12.1.2610
@author claudineia.reinert
@since 31/10/2025
@param cCodFechamento, character, codigo do fechamento financeiro
@return character, retorna a sequencia maxima da NEK_ITEM para o fechamento financeiro informado
/*/
Method getMaxSequenceItem(cCodFechamento as character) as Character Class agdFechamentoFinanceiroRepository
	Local cSeqTitulos      := STRZERO(0,TAMSX3("NEK_ITEM")[1])
	Local cQuery    as Character
	Local oStatem   as Object

	cQuery := "SELECT MAX(NEK_ITEM) AS NEK_ITEM "
	cQuery += " FROM "+ retSqlName('NEK') +" NEK "
	cQuery += " WHERE NEK_FILIAL = ? "
	cQuery += " AND NEK_CODFEC = ?  "
	cQuery += " AND NEK.D_E_L_E_T_ = '' "
	cQuery := ChangeQuery( cQuery )

	oStatem := FWPreparedStatement():New()
	oStatem:SetQuery(cQuery)
	oStatem:SetString(1, FWxFilial('NEK'))
	oStatem:SetString(2, cCodFechamento)
	cQuery := oStatem:GetFixQuery()

	cSeqTitulos := MPSysExecScalar(cQuery,"NEK_ITEM")
	cSeqTitulos := STRZERO((GetDToVal(cSeqTitulos)),TAMSX3("NEK_ITEM")[1])

Return cSeqTitulos

