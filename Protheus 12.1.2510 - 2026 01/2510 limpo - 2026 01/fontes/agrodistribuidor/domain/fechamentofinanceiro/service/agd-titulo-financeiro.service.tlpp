#Include "agd.titulo.financeiro.service.ch"
#include "totvs.ch"
#include 'tlpp-core.th'


namespace agd.tituloFinanceiroService
using namespace agd.utilsService
using namespace agd.tituloFinanceiroRepository /*Funcoes de Repositorio*/


/*/{Protheus.doc} agdTituloFinanceiroService
Serviço para tratamento dos titulos financeiros do Fechamento Barter
@type class
@version 12
@author jean.schulze
@since 17/03/2025
/*/
class agdTituloFinanceiroService FROM agdUtilsService

	public method new()

	public method listar()
	public method listarTitulos()
	public method getSituacaoTituloAReceberBarter() as character
	public method getSituacaoTituloAPagarBarter() as character
	public method getTotTituloFinanceiro() as numeric

	private method isFechamentoConfirmado() as logical
endclass

/*/{Protheus.doc} new
Constructor
@type method
@version 12
@author jean.schulze
@since 02/01/2025
@param pcCodigoNegocio, character, Codigo Negocio Barter
@return variant, instância
/*/   
method new() class agdTituloFinanceiroService
	_Super:New()
return Self

/*/{Protheus.doc} listar
Listar Negócio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@param oJsonPathParam, object, path param
@return variant, nil
/*/
method listar(nPage, nPageSize, oJsonQryParam) class agdTituloFinanceiroService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10
	Default oJsonQryParam := nil

	oRepository := agdTituloFinanceiroRepository():New()
	oRepository:find(nPage, nPageSize, oJsonQryParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)
Return nil

/*/{Protheus.doc} listarTitulos
Listagem dos títulos a pagar e receber disponíveis para fechamento financeiro
@type method
@version 12
@author Gilson.Venturi
@since 22/05/2025
@param nPage, numeric, numero pagina
@param nPageSize, numeric, tamanho da pagina
@param oJsonQryParam, object, params json
@return variant, nil
/*/
method listarTitulos(nPage, nPageSize, oJsonParam) class agdTituloFinanceiroService
	Local oRepository As Object
	Default nPage     := 1
	Default nPageSize := 10

	oRepository := agdTituloFinanceiroRepository():New()
	oRepository:findByTitulosFinanceiroDisponiveisFechamento(nPage, nPageSize, oJsonParam)

	Self:setFullResponse(oRepository)

	oRepository:DeActivate()
	FreeObj(oRepository)	
return nil

/*/{Protheus.doc} getSituacaoTituloAReceberBarter
Retorno da situacao do titulo em relacao ao processo de barter a receber
@type method
@version 12
@author jean.schulze
@since 17/03/2025
@param pcFilial, variant, filial E1
@param pcPrefix, variant, prefixo E1
@param pcTitulo, variant, num E1
@param pcParcel, variant, parcela E1
@param pcTipo, character, tipo E1
@return character, Situação do titulo
/*/
method getSituacaoTituloAReceberBarter(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo) as character class agdTituloFinanceiroService
	Local cSituacao as character
	Local oRepository := agdTituloFinanceiroRepository():New()

	oRepository:findByTituloAReceberInFechamento(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo)
	if oRepository:isSuccess()
		if len(oRepository:getResponse()['items']) > 0
			if Self:isFechamentoConfirmado(oRepository:getResponse()['items'])
				cSituacao := STR0001 //"Título Conferido (TC)"
			else
				cSituacao := STR0002 //"Título Não Conferido (TNC)"
			endif		
		else
			if oRepository:existByBarterTituloFinanceiroAReceber(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo)
				cSituacao := STR0002 //"Título Não Conferido (TNC)"
			endif
		endif		
	endif
	
	FreeObj(oRepository)
return cSituacao

/*/{Protheus.doc} getSituacaoTituloAPagarBarter
Retorno da situacao do titulo em relacao ao processo de barter para titulos a pagar
@type method
@version 12
@author jean.schulze
@since 17/03/2025
@param pcFilial, variant, filial E2
@param pcPrefix, variant, prefixo E2
@param pcTitulo, variant, num E2
@param pcParcel, variant, parcela E2
@param pcTipo, variant, tipo E2
@param pcFornec, variant, fornecedor E2
@param pcLoja, variant, loja E2
@return character,  Situação do titulo
/*/
method getSituacaoTituloAPagarBarter(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcFornec, pcLoja) as character class agdTituloFinanceiroService
	Local cSituacao as character
	Local oRepository := agdTituloFinanceiroRepository():New()

	oRepository:findByTituloAPagarInFechamento(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcFornec, pcLoja)
	if oRepository:isSuccess()
		if len(oRepository:getResponse()['items']) > 0
			if Self:isFechamentoConfirmado(oRepository:getResponse()['items'])
				cSituacao := STR0001 //"Título Conferido (TC)"
			else
				cSituacao := STR0002 //"Título Não Conferido (TNC)"
			endif	
		else
			if oRepository:existByBarterTituloFinanceiroAPagar(pcFilial, pcPrefix, pcTitulo, pcParcel, pcTipo, pcFornec, pcLoja)
				cSituacao := STR0002 //"Título Não Conferido (TNC)"
			endif
		endif		
	endif

	FreeObj(oRepository)
return cSituacao

/*/{Protheus.doc} isFechamentoConfirmado
Verifica se um fechamento está confirmado
@type method
@version 12
@author jean.schulze
@since 20/03/2025
@param aItensResponse, array, lsita de respostas
@return logical, se está confirmado
/*/
method isFechamentoConfirmado(aItensResponse as array) as logical class agdTituloFinanceiroService
	Local lRet := .f.
	Local nX   := 1

	For nX:= 1 to Len(aItensResponse)
		if Posicione("NEJ", 1, aItensResponse[nX]['filial']+aItensResponse[nX]['codigo'], "NEJ_STATUS") == "2"
			lRet := .t.
		end
	Next
	
return lRet

/*/{Protheus.doc} getTotTituloFinanceiro
Calcula o total financeiro dos títulos vinculados ao fechamento
@type method
@version 12
@since 18/06/2025
@author rodrigo.nsoledade
@param cCodFec, character, Código do fechamento financeiro (NEK_CODFEC)
@param cCodMod, character, Código do módulo (1=Compra, 2=Venda)
@return numeric, Valor total dos títulos financeiros
/*/
method getTotTituloFinanceiro(cCodFec as character, cCodMod as character) as numeric class agdTituloFinanceiroService
    Local nTotal := 0

    DbSelectArea("NEK")
    NEK->(DbSetOrder(2)) // NEK_FILIAL+NEK_CODFEC+NEK_CODMOD

    If NEK->(DbSeek(xFilial("NEK") + cCodFec + cCodMod))
        While !NEK->(Eof()) .and. NEK->NEK_FILIAL == xFilial("NEK") .and. NEK->NEK_CODFEC == cCodFec .and. NEK->NEK_CODMOD == cCodMod
            // Garante que títulos do tipo NDF ou NCC estejam com valor negativo
            If NEK->NEK_TIPO $ "NDF|NCC" .and. NEK->NEK_LIQUID > 0
                nTotal += (NEK->NEK_LIQUID * -1)
            Else
                nTotal += NEK->NEK_LIQUID
            EndIf
            NEK->(DbSkip())
        EndDo
    EndIf

Return nTotal
