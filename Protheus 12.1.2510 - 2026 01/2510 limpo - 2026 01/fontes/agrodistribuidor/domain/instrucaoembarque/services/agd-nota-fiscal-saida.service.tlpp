#include "totvs.ch"
#include 'tlpp-core.th'
//#INCLUDE "agd.instrucao.embarque.pedido.service.ch"

namespace agd.notaFiscalSaidaService
using namespace agd.utilsService
using namespace agd.instrucaoEmbarqueItemRepository
using namespace agd.negocioStatusService
using namespace agd.negocioService

/*/{Protheus.doc} agdNotaFiscalSaidaService
Classe de Servico para o Gerenciamento de Pedidos da Instrucao de Embaque
@type class
@version 12
@author jean.schulze
@since 26/12/2024
/*/
class agdNotaFiscalSaidaService FROM agdUtilsService
	public Data cIdItemNotaFiscal         As Character
	public Data cIdInstrucaoEmbarqueItem  As Character

	public method new()

	public method isNotaFiscalAgroDistribuidor() as Logical

	public method vincularNotaFiscal()
	public method desvincularNotaFiscal()
	public method atualizarQuantidadeDevolvida()

	private method setInstrucaoEmbarque()
	private method getDadosNotaFiscal() as json

endclass

/*/{Protheus.doc} new
Constructor da Classe de Serviço
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@param pcIdInstrucaoEmbarque, character, Id da Instrucao de Embarque
@return object, Retorna a instancia da Classe de Servico
/*/
method new(pcIdItemNotaFiscal as Character) class agdNotaFiscalSaidaService
	Self:cIdItemNotaFiscal := pcIdItemNotaFiscal
	Self:setInstrucaoEmbarque()
	_Super:new()
return Self

/*/{Protheus.doc} isNotaFiscalAgroDistribuidor
Verifica se Existe Nota Fiscal Associada a IE
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return logical, Existe Pedido
/*/
method isNotaFiscalAgroDistribuidor() as Logical class agdNotaFiscalSaidaService
return !Empty(Self:cIdInstrucaoEmbarqueItem)

/*/{Protheus.doc} vincularNotaFiscal
Vincula uma nota fiscal de saída
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@return variant, nil
/*/
method vincularNotaFiscal() class agdNotaFiscalSaidaService
	Local aFieldsUpdate := {}

	if Self:isNotaFiscalAgroDistribuidor()
		jDadosNF :=  Self:getDadosNotaFiscal()

		aAdd(aFieldsUpdate, {'NED_DOC',    jDadosNF:GetJsonText('D2_DOC')})
		aAdd(aFieldsUpdate, {'NED_SERIE',  jDadosNF:GetJsonText('D2_SERIE')})
		aAdd(aFieldsUpdate, {'NED_CLIENT', jDadosNF:GetJsonText('D2_CLIENTE')})
		aAdd(aFieldsUpdate, {'NED_LOJA',   jDadosNF:GetJsonText('D2_LOJA')})
		aAdd(aFieldsUpdate, {'NED_ITEMNF', jDadosNF:GetJsonText('D2_ITEM')})

		oRepository := agdInstrucaoEmbarqueItemRepository():New(Self:cIdInstrucaoEmbarqueItem)
		oRepository:updateRepository(aFieldsUpdate)

		Self:setFullResponse(oRepository)

		oRepository:DeActivate()

		//Proteção do campo
		If NEA->(ColumnPos('NEA_STSEXP')) > 0
			dbSelectArea('NED')
			NED->(dbSetOrder(1))
			If NED->(dbSeek(Self:cIdInstrucaoEmbarqueItem))
				oNegocioService	:= agdNegocioService():New()
				cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NED->NED_CODBRT)

				oNegocioSatusService := agdNegocioStatusService():new(FWxFilial("NEA") + NED->(NED_CODBRT) + cVersaoNegocio)
				oNegocioSatusService:atualizarStExpedNegocio()
				oNegocioSatusService:concluirNegocio()
				FreeObj(oNegocioSatusService)
				FreeObj(oNegocioService)
			Endif
		Endif
		
	endif
return nil

/*/{Protheus.doc} desvincularNotaFiscal
Desvincula uma Nota Fiscal de Saída
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@return variant, nil
/*/
method desvincularNotaFiscal() class agdNotaFiscalSaidaService
	Local aFieldsUpdate := {}

	if Self:isNotaFiscalAgroDistribuidor()
		aAdd(aFieldsUpdate, {'NED_DOC',    ""})
		aAdd(aFieldsUpdate, {'NED_SERIE',  ""})
		aAdd(aFieldsUpdate, {'NED_CLIENT', ""})
		aAdd(aFieldsUpdate, {'NED_LOJA',   ""})
		aAdd(aFieldsUpdate, {'NED_ITEMNF', ""})

		oRepository := agdInstrucaoEmbarqueItemRepository():New(Self:cIdInstrucaoEmbarqueItem)
		oRepository:updateRepository(aFieldsUpdate)

		Self:setFullResponse(oRepository)

		oRepository:DeActivate()

		//Proteção do campo
		If NEA->(ColumnPos('NEA_STSEXP')) > 0
			dbSelectArea('NED')
			NED->(dbSetOrder(1))
			If NED->(dbSeek(Self:cIdInstrucaoEmbarqueItem))
				oNegocioService	:= agdNegocioService():New()
				cVersaoNegocio	:= oNegocioService:buscarVersaoAtualValida(NED->NED_CODBRT)
				FreeObj(oNegocioService)

				oNegocioSatusService := agdNegocioStatusService():new(FWxFilial("NEA") + NED->(NED_CODBRT) + cVersaoNegocio)
				oNegocioSatusService:atualizarStExpedNegocio()
				oNegocioSatusService:concluirNegocio()
				FreeObj(oNegocioSatusService)
			Endif
		Endif
	Endif

return nil

/*/{Protheus.doc} atualizarQuantidadeDevolvida
Função para atualizar quantidade devolvida
@type method
@version 12
@author Gilson.Venturi
@since 25/02/2025
@return variant, nil
/*/
method atualizarQuantidadeDevolvida(pnQuantidade as Numeric) class agdNotaFiscalSaidaService
	Local aFieldsUpdate := {}

	if Self:isNotaFiscalAgroDistribuidor()
		aAdd(aFieldsUpdate, {'NED_QTDDEV', pnQuantidade})

		oRepository := agdInstrucaoEmbarqueItemRepository():New(Self:cIdInstrucaoEmbarqueItem)
		oRepository:updateRepository(aFieldsUpdate)

		Self:setFullResponse(oRepository)

		oRepository:DeActivate()

	endif

return nil

/*/{Protheus.doc} setInstrucaoEmbarque
Preenche o Id do item da IE que vai ser alterado
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@return variant, nil
/*/
method setInstrucaoEmbarque() class agdNotaFiscalSaidaService
	Local oRepository := agdInstrucaoEmbarqueItemRepository():New()

	if Empty(oRepository:findByNotaFiscal(Self:cIdItemNotaFiscal))  //busca pelo id da NF
		jDadosNF :=  Self:getDadosNotaFiscal()
		if !Empty(oRepository:findByPedidoItem(jDadosNF:GetJsonText("D2_PEDIDO"), jDadosNF:GetJsonText("D2_ITEMPV")))
			Self:cIdInstrucaoEmbarqueItem := oRepository:getResponse()
		endif
	else
		Self:cIdInstrucaoEmbarqueItem := oRepository:getResponse()
	endif

	oRepository:DeActivate()
return nil
/*/{Protheus.doc} getDadosNotaFiscal
Obtem os dados do item da nota fiscal
@type method
@version 12
@author jean.schulze
@since 22/01/2025
@return object, dados NF
/*/
method getDadosNotaFiscal() as json class agdNotaFiscalSaidaService
	Local oDados := JsonObject():New()
	Local aArea	 := GetArea()

	dbSelectArea('SD2')
	SD2->(dbSetOrder(3)) //chave primaria
	SD2->(msSeek(Self:cIdItemNotaFiscal))

	if Self:cIdItemNotaFiscal  == SD2->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA+D2_COD+D2_ITEM)
          /*Somente os campos necessários*/
		oDados["D2_DOC"]     := SD2->D2_DOC
		oDados["D2_SERIE"]   := SD2->D2_SERIE
		oDados["D2_CLIENTE"] := SD2->D2_CLIENTE
		oDados["D2_LOJA"]    := SD2->D2_LOJA
		oDados["D2_ITEM"]    := SD2->D2_ITEM
		oDados["D2_PEDIDO"]  := SD2->D2_PEDIDO
		oDados["D2_ITEMPV"]  := SD2->D2_ITEMPV
	endif

	RestArea(aArea)

return oDados
