#INCLUDE "totvs.ch"
#INCLUDE "tlpp-core.th"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "agd.instrucao.embarque.repository.ch"

namespace agd.instrucaoEmbarqueRepository
using namespace agd.repositoryUtils
using namespace agd.instrucaoEmbarqueItemRepository

/*/{Protheus.doc} agdInstrucaoEmbarqueRepository
Repositório de Instruções de Embarque - NEC
@type class
@version 12
@author rodrigo.nsoledade
@since 09/05/2025
/*/
class agdInstrucaoEmbarqueRepository FROM agdRepositoryUtils
	public Data cChaveIDRepository As Object //chave unica para updates

	public method New()
	public method save()
	public method findNotasFiscais()
	public method deleteRepository()
	public method isRepositoryChange()
	public method getPayloadFields() as Array
	public Method getFilterById()    as Array

	private method getFields() as Array
	private method getQueryJoin() as Array
	private method getQueryAdapt() as Character
	private method getFieldsNotasFiscais() as Array
	private method getQueryNotasFiscais() as Character
endclass

/*/{Protheus.doc} New
Constructor
@type method
@version 12
@author jean.schulze
@since 15/05/2025
@return variant, this
/*/
method New(pcChaveIDRepository as character) class agdInstrucaoEmbarqueRepository
	Self:cChaveIDRepository := pcChaveIDRepository
	_Super:New("NEC",Self:getFields(.T.),,Self:getQueryAdapt())
return Self

/*/{Protheus.doc} findNotasFiscais
Retorna lista paginada de notas fiscais de uma instrucao de embarque
@type method
@version 12
@author jean.schulze
@since 15/05/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@return variant, nil
/*/
Method findNotasFiscais(nPage, nPageSize, oJsonParam) class agdInstrucaoEmbarqueRepository
	Local aArea  		As Array
	Local jResponseJson As Json
	Local cWhereParam   as Character
	Local cOrder        as Character

	Default nPage          := 1
	Default nPageSize      := 10


	If Self:lOk
		aArea  := FwGetArea()
		cOrder := Self:getOrderQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhereParam += IIF(!Empty(cWhereParam), ' AND ', '') + "NEC.NEC_NUMPED <> ''"


		Self:setFieldsConsulta(Self:getFieldsNotasFiscais())
		Self:setPage(nPage)
		Self:setPageSize(nPageSize)
		Self:SetQuery(Self:getQueryDefault(pcFrom=Self:getQueryNotasFiscais()))
		Self:SetWhere(Self:getWhereDefault(cWhereParam))

		if Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		Endif

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf
Return Nil

/*/{Protheus.doc} getFields
Retorna a Lista de Campos do Repositorio
@type method
@version 12
@author rodrigo.nsoledade
@since 09/05/2025
@return array, lista de campos
/*/
method getFields(lFieldsAdd) class agdInstrucaoEmbarqueRepository
	Local aFields := {}
	Default lFieldsAdd := .f.

	AAdd(aFields, {"filial"        	, "NEC_FILIAL" , "C", .T.})
	AAdd(aFields, {"codigo"        	, "NEC_CODIGO" , "C", .T.})
	AAdd(aFields, {"codNegocio"    	, "NEC_CODBRT" , "C", .T.})
	AAdd(aFields, {"codCliente"    	, "NEC_CODCLI" , "C", .T.})
	AAdd(aFields, {"lojaCliente"   	, "NEC_LOJCLI" , "C", .T.})
	AAdd(aFields, {"safra"         	, "NEC_CODSAF" , "C", .T.})
	AAdd(aFields, {"numPedido"     	, "NEC_NUMPED" , "C", .T.})
	AAdd(aFields, {"dataEmissao"   	, "NEC_EMISSA" , "D", .T.})
	AAdd(aFields, {"serieNF"       	, "NEC_SERNF"  , "C", .T.})
	AAdd(aFields, {"transportadora"	, "NEC_TRANSP"  , "C", .T.})
	AAdd(aFields, {"veiculo"       	, "NEC_VEICUL"  , "C", .T.})

	If lFieldsAdd
		AAdd(aFields, {"nomeCliente"  , "A1_NOME"     , "C", .T.})
		AAdd(aFields, {"statusPedido" , "STATUSPED"   , "C", .T.})
	EndIf

Return aFields

/*/{Protheus.doc} getFieldsNotasFiscais
Lista de Campos da Consulta de Notas Fiscais
@type method
@version 12
@author jean.schulze
@since 15/05/2025
@return variant, lista de campos
/*/
method getFieldsNotasFiscais() class agdInstrucaoEmbarqueRepository
	Local aFields := {}

	AAdd(aFields, {"filial"              , "NEC_FILIAL" , "C", .T.})
	AAdd(aFields, {"codigo"              , "NEC_CODIGO" , "C", .T.})
	AAdd(aFields, {"codNegocio"          , "NEC_CODBRT" , "C", .T.})
	AAdd(aFields, {"numPedido"           , "NEC_NUMPED" , "C", .T.})
	AAdd(aFields, {"codCliente"          , "NEC_CODCLI" , "C", .T.})
	AAdd(aFields, {"codClienteLoja"      , "NEC_LOJCLI" , "C", .T.})
	AAdd(aFields, {"documento"           , "DOCUMENTO"  , "C", .T.})
	AAdd(aFields, {"serie"               , "SERIE"      , "C", .T.})
	AAdd(aFields, {"dataEmissaoDoc"      , "DT_EMISSAO" , "D", .T.})
	AAdd(aFields, {"itemNota"            , "ITEM"       , "C", .T.})
	AAdd(aFields, {"quantidade"          , "QUANTIDADE" , "N", .T.})
	AAdd(aFields, {"valorUnitario"       , "VALOR"      , "N", .T.})
	AAdd(aFields, {"valorTotal"          , "VLR_TOTAL"  , "N", .T.})
	AAdd(aFields, {"codigoProduto"       , "PRODUTO"    , "C", .T.})
	AAdd(aFields, {"quantidadeDevolvida" , "QTD_DEVOL"  , "N", .T.})
	AAdd(aFields, {"tipoNota"            , "TIPO"       , "C", .T.})

Return aFields


/*/{Protheus.doc} getQueryAdapt
Query com adpatacoes para campos especiais, como StatusPedido da SC5
@type method
@version 12
@author rodrigo.nsoledade
@since 12/05/2025
@return character, subquery SQL
/*/
Method getQueryAdapt() as Character class agdInstrucaoEmbarqueRepository
	Local cQuery   as character
	Local cStatPed as character

	cStatPed := " CASE "
	cStatPed += "	WHEN NEC_NUMPED = '' THEN 'NAO_GERADO' "
	cStatPed += "	WHEN SC5.C5_NOTA <> '' AND SF2.F2_FIMP IN ('T','S') THEN 'FATURADO' "
	cStatPed += "	WHEN SC5.C5_NOTA <> '' AND SF2.F2_FIMP NOT IN ('T','S') THEN 'FATURADO_SEM_NFE' "
	cStatPed += "	WHEN SC5.C5_LIBEROK <> '' AND SC5.C5_NOTA = '' AND SC5.C5_BLQ = '' THEN 'PEDIDO_LIBERADO' "
	cStatPed += "	WHEN SC5.C5_LIBEROK = '' AND SC5.C5_NOTA = '' AND SC5.C5_BLQ = '' THEN 'PEDIDO_ABERTO' "
	cStatPed += "	WHEN SC5.C5_BLQ = '1' THEN 'PEDIDO_BLOQUEADO_REGRA' "
	cStatPed += "	WHEN SC5.C5_BLQ = '2' THEN 'PEDIDO_BLOQUEADO_VERBA' "
	cStatPed += "	ELSE 'NAO_GERADO' "
	cStatPed += " END AS STATUSPED "

	cQuery := "SELECT NEC.*, SA1.A1_NOME, " + cStatPed + " FROM " + RetSqlName("NEC") + " NEC "
	cQuery += " LEFT JOIN "+ RetSqlName("SA1") +" SA1 ON SA1.D_E_L_E_T_ = ' ' AND A1_FILIAL = '"+ FWxFilial("SA1") +"' AND A1_COD = NEC.NEC_CODCLI AND A1_LOJA = NEC.NEC_LOJCLI "
	cQuery += " LEFT JOIN "+ RetSqlName("SC5") +" SC5 ON SC5.D_E_L_E_T_ = ' ' AND C5_FILIAL = '"+ FWxFilial("SC5")+ "' AND C5_NUM = NEC_NUMPED "
	cQuery += " LEFT JOIN "+ RetSqlName("SF2") +" SF2 ON SF2.D_E_L_E_T_ = ' ' AND F2_FILIAL = '"+ FWxFilial("SF2")+ "' AND F2_DOC = C5_NOTA AND F2_SERIE = C5_SERIE "

Return cQuery

/*/{Protheus.doc} getQueryNotasFiscais
Query para retornar notas fiscais de uma instrucao de embarque
@type method
@version 12
@author jean.schulze
@since 15/05/2025
@return character, query
/*/
Method getQueryNotasFiscais() as Character class agdInstrucaoEmbarqueRepository
	Local cQuery   as character

	cQuery := "SELECT NEC.*, "
	cQuery += "D2_DOC AS DOCUMENTO, "
	cQuery += "D2_SERIE AS SERIE, "
	cQuery += "D2_EMISSAO AS DT_EMISSAO, "
	cQuery += "D2_ITEM AS ITEM, "
	cQuery += "D2_QUANT AS QUANTIDADE, "
	cQuery += "D2_PRCVEN AS VALOR, "
	cQuery += "D2_TOTAL AS VLR_TOTAL, "
	cQuery += "D2_COD AS PRODUTO, "
	cQuery += "D2_QTDEDEV AS QTD_DEVOL, "
	cQuery += "D2_TIPO AS TIPO "
	cQuery += "FROM " + RetSqlName( "NEC" ) + " NEC "
	cQuery += "INNER JOIN " + RetSqlName( "SD2" ) + " SD2 ON " + Self:getWhereDefault("NEC.NEC_NUMPED  = SD2.D2_PEDIDO", "SD2")
	cQuery += "WHERE NEC_NUMPED <> ''"

	cQuery += " UNION ALL "

	cQuery += "SELECT NEC.*, "
	cQuery += "D1_DOC AS DOCUMENTO, "
	cQuery += "D1_SERIE AS SERIE, "
	cQuery += "D1_EMISSAO AS DT_EMISSAO, "
	cQuery += "D1_ITEM  AS ITEM, "
	cQuery += "D1_QUANT AS QUANTIDADE, "
	cQuery += "D1_VUNIT AS VALOR, "
	cQuery += "D1_TOTAL AS VLR_TOTAL, "
	cQuery += "D1_COD AS PRODUTO, "
	cQuery += "0 AS QTD_DEVOL, "
	cQuery += "'D' AS TIPO	"
	cQuery += "FROM " + RetSqlName( "NEC" ) + " NEC "
	cQuery += "INNER JOIN " + RetSqlName( "SD2" ) + " SD2 ON " + Self:getWhereDefault("NEC.NEC_NUMPED  = SD2.D2_PEDIDO", "SD2")
	cQuery += "INNER JOIN " + RetSqlName( "SD1" ) + " SD1 ON " + Self:getWhereDefault("SD1.D1_TIPO = 'D' AND SD2.D2_DOC = SD1.D1_NFORI AND SD2.D2_SERIE	= SD1.D1_SERIORI AND SD2.D2_ITEM = SD1.D1_ITEMORI", "SD1")
	cQuery += "WHERE NEC_NUMPED <> ''"

Return cQuery

/*/{Protheus.doc} save
Salva os dados da Instrução de Embarque (NEC e NED) a partir de um JSON
@type method
@version 1.0
@author rodrigo.nsoledade
@since 21/05/2025
@param jCmdRegistrar, object, JSON com os dados da instrução
@return nil
/*/
Method save(jCmdRegistrar, lUpdate as logical) Class agdInstrucaoEmbarqueRepository
	Local oModel        as object
	Local oRepoItem     as object
	Local oGridNED      as object
	Local nI            as numeric
	Local aFieldsNEC    as array
	Local aFieldsNED    as array
	Local aItFields     as array
	Local cItemNegoc    as character
	Local nPosItemNegoc as numeric
	Local lFound        as logical

	Default lUpdate := .f.

	// Valida JSON
	If !jCmdRegistrar:hasProperty("instrucao") .Or. !jCmdRegistrar:hasProperty("itens") .Or. !ValType(jCmdRegistrar["itens"]) == "A"
		Self:setDefaultError(403)
		Return nil
	EndIf

	if !lUpdate .or. (lUpdate .and. Self:existsById(Self:cChaveIDRepository))

		// Carrega o Model da rotina AGDA020
		oModel := FwLoadModel("AGDA020")
		oModel:SetOperation(IIF(lUpdate, MODEL_OPERATION_UPDATE, MODEL_OPERATION_INSERT))
		oModel:Activate()

		if Self:isSuccess()

			// Carregamento do cabeçalho NEC
			aFieldsNEC := Self:jsonToArrayFields(jCmdRegistrar["instrucao"], self:getFields())
			Self:setValueModel(oModel:GetModel("AGDA020_NEC"), aFieldsNEC)


			// Carregamento dos Itens da NED
			oRepoItem := agdInstrucaoEmbarqueItemRepository():New()
			aFieldsNED := oRepoItem:getFields()
			oGridNED := oModel:GetModel("AGDA020_NED")

			oGridNED:DelAllLine() //Deleta todas as linhas
			if !lUpdate
				oGridNED:ClearData() //limpa as linhas da tela
			endif

			For nI := 1 To Len(jCmdRegistrar["itens"])
				if Self:isSuccess()
					// Validação: campo "itemnegoc" obrigatório (NED_ITEBRT)
					nPosItemNegoc := aScan(aFieldsNED, {|x| x[2] == "NED_ITEBRT" })
					cItemNegoc    := aFieldsNED[nPosItemNegoc][1]

					If !jCmdRegistrar["itens"][nI]:hasProperty(cItemNegoc)
						Self:setErrorField(403,cItemNegoc)
						Return nil
					EndIf
					if lUpdate
						lFound := oGridNED:SeekLine({{"NED_ITEM",  StrZero( nI, TamSX3("NED_ITEM")[1])}}, .T., .T.)
						If lFound
							oGridNED:UnDeleteLine()
						ElseIF Len(jCmdRegistrar["itens"]) > oGridNED:Length() .and. !lFound
							oGridNED:AddLine()
							oGridNED:SetValue("NED_ITEM"  	, StrZero( oGridNED:Length(), TamSX3("NED_ITEM")[1]) )
						EndIf
					else
						if nI > 1
							oGridNED:AddLine()
						endif
						oGridNED:SetValue("NED_ITEM"  	, StrZero( oGridNED:Length(), TamSX3("NED_ITEM")[1]) )
					endif
					// Carrega campos editáveis da grid
					aItFields := Self:jsonToArrayFields(jCmdRegistrar["itens"][nI], aFieldsNED)
					Self:setValueModel(oGridNED, aItFields)
				endif
			Next

			if Self:isSuccess()
				// Validação e commit
				If oModel:VldData()
					oModel:CommitData()
					Self:setSuccess(oModel:GetValue("AGDA020_NEC", "NEC_CODIGO"))
				Else
					Self:setErrorByErrorMessageModel(oModel, 404)
				EndIf
			Endif

		endif

		oModel:DeActivate()
		oModel:Destroy()

	endif

	FreeObj(oModel)
	FreeObj(oRepoItem)

Return nil

/*/{Protheus.doc} deleteRepository
Deleta a instrucao de embarque com o ID informado
@type method
@version 12
@author carlos.augusto
@since 21/05/2025
@return variant, nil
/*/
method deleteRepository(cIdInstrucaoEmbarque) Class agdInstrucaoEmbarqueRepository
	Local oModel    := nil

	If Self:existsById(cIdInstrucaoEmbarque)
		oModel := FWLoadModel("AGDA020")
		oModel:SetOperation(MODEL_OPERATION_DELETE)
		If oModel:Activate() .And. oModel:VldData() .And. oModel:CommitData()
			Self:setSuccess(STR0002)//"Instrução de embarque excluída com sucesso."
		Else
			Self:setErrorByErrorMessageModel(oModel, 400)
		Endif
		oModel:DeActivate()
		oModel:Destroy()
		oModel := NIL
		FreeObj(oModel)
	Endif

Return

/*/{Protheus.doc} isRepositoryChange
Verifica se o repostitório permite edicao conforme modelo de dados
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@return variant, nil
/*/
method isRepositoryChange() Class agdInstrucaoEmbarqueRepository

	If Self:existsById(Self:cChaveIDRepository)
		Self:buildModelCheck("AGDA020", MODEL_OPERATION_UPDATE)
	Endif

Return nil

Method getPayloadFields() as Array Class agdInstrucaoEmbarqueRepository
	Local aFields:= {}
	oRepoItem := agdInstrucaoEmbarqueItemRepository():New()

	AEval(Self:getFields(),{|field| AAdd(aFields, field)})
	AEval(oRepoItem:getFields(),{|field| AAdd(aFields, field)})

Return aFields

/*/{Protheus.doc} getFilterById
Obtem Filtro para Get By ID
@type method
@version 12
@author lindembergson.pacheco
@since 08/09/2025
@param cCodigo, character, codigo instrucao de embarque
@return array, lista de filtro
/*/
Method getFilterById(cCodigo) as Array Class agdInstrucaoEmbarqueRepository
	Local aFilterId := {}

	AAdd(aFilterId, {'NEC_CODIGO', cCodigo})

return aFilterId
