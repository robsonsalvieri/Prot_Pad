#include "totvs.ch"
#include 'tlpp-core.th'
#include "FWMVCDEF.CH"
#include "agd.utils.service.ch"

namespace agd.utilsService

/*/{Protheus.doc} agdUtilsService
Classe Utilitaria para Padronizar o retorno de Servicos do AgroDistribuidor
@type class
@version 12
@author jean.schulze
@since 26/12/2024
/*/
class agdUtilsService
	public Data lOk                    As Logical
	public Data nCodeError             As Numeric
	public Data cMessage               As Character
	public Data cSolution               As Character
	public Data cDetailedMessage       As Character
	public DAta aDetails               As Array
	public Data oResponse              As object

	public method new()


	public method getResponse()     as object
	public method getMessageError() as Character
	public method getSolutionError() as Character
	public method getCodeError()    as Numeric
	public method getDetailedMessage() as Character
	public method getDetailsError() as array
	public method isSuccess()       as Logical
	public method isSIGAAGDactive() as Logical

	protected method setError()
	protected method setErrorByErrorMessageModel()
	protected method setSuccess()
	protected method setFullResponse()

endclass

/*/{Protheus.doc} new
Constructor da Classe
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return variant, Retorna a propria instancia
/*/
method new() class agdUtilsService
	Self:oResponse := nil
	Self:lOk := .F.
	Self:nCodeError := 0
	Self:cMessage := ""
	Self:cSolution := ""
	Self:cDetailedMessage := ""
	Self:aDetails := {}
return Self

/*/{Protheus.doc} getResponse
Retorna o objeto de Resposta da classe de Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return object, Objeto de Resposta
/*/
method getResponse() as object class agdUtilsService
return Self:oResponse

/*/{Protheus.doc} getMessageError
Retorna a Mensagem de Erro do Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return character, Mensagem de erro
/*/
method getMessageError() as Character class agdUtilsService
return Self:cMessage

/*/{Protheus.doc} agdUtilsService::getSolutionError
Retorna a Mensagem de Solução do Erro do Servico
@type method
@version P12
@author claudineia.reinert
@since 26/03/2025
@return character, Mensagem de solução do erro
/*/
method getSolutionError() as Character class agdUtilsService
return Self:cSolution

/*/{Protheus.doc} getCodeError
Retorna o Codigo de Erro Numerico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return numeric, Codigo de Erro
/*/
method getCodeError() as Numeric class agdUtilsService
return Self:nCodeError

/*/{Protheus.doc} agdUtilsService::getDetailedMessage
Retorna a mensagem detalhada
@type method
@version 12
@author jc.maldonado
@since 27/10/2025
@return character, mensagem detalhada
/*/
method getDetailedMessage() as Character class agdUtilsService
return Self:cDetailedMessage

/*/{Protheus.doc} agdUtilsService::getDetails
Retorna os detalhes
@type method
@version 12
@author jc.maldonado
@since 05/11/2025
@return array, array com detalhes
/*/
method getDetailsError() as Array class agdUtilsService
return Self:aDetails


/*/{Protheus.doc} isSuccess
Retorna se a classe está com operacoes com sucesso
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return logical, True ou False da Operacao
/*/
method isSuccess() as Logical class agdUtilsService
return Self:lOk


/*/{Protheus.doc} setError
Informar erros na classe de Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@param pcErrorMessage, character, String de Erro
@param pnErrorCode, numeric, Numero do erro
@return variant, nenhum
/*/
method setError(pcErrorMessage as Character, pnErrorCode as Numeric, pcSolutionMessage as Character, pcDetailedMessage as Character, aDetails as array) class agdUtilsService
	Default pcErrorMessage := ""
	Default pcSolutionMessage := ""
	Default pnErrorCode := 0
	Default pcDetailedMessage := ""
	Default aDetails := {}
	Self:oResponse := nil
	Self:lOk := .F.
	Self:nCodeError := pnErrorCode
	Self:cMessage := pcErrorMessage
	Self:cSolution := pcSolutionMessage
	Self:cDetailedMessage := pcDetailedMessage
	Self:aDetails := aDetails
return nil


/*/{Protheus.doc} setErrorByErrorMessageModel
Envia os erros do modelo para o repository
MODEL_MSGERR_IDFIELDERR 4 - Id do campo de erro
MODEL_MSGERR_ID         5 - Campo Titulo da Janela
MODEL_MSGERR_MESSAGE    6 - Campo Problema
MODEL_MSGERR_SOLUCTION  7 - Campo Solução
@type method
@version 12
@author carlos.augusto
@since 22/05/2025
@param oModel, object, modelo para obter os erros
@param pnErrorCode, numeric, Numero do erro
@return variant, nenhum
/*/
method setErrorByErrorMessageModel(oModel as Object, pnErrorCode as Numeric) class agdUtilsService
	Default pnErrorCode := 0
   
	Self:nCodeError := pnErrorCode

	Self:cMessage := ""
	If !Empty(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR])
		Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
	EndIf
	Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	   + ' - '
	Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])  
	If !Empty(AllTrim(StrTran(StrTran(oModel:GetErrorMessage()[MODEL_MSGERR_SOLUCTION], Chr(13), ""), Chr(10), ""))) //A posicao 7 retorna \r\n (nao vazio)
		Self:cMessage += ' - ' + cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_SOLUCTION])
	Endif

	Self:oResponse := nil
	Self:lOk := .F.
return nil

/*/{Protheus.doc} setSuccess
Informar o sucesso na operacao da Classe de Serviço
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@param poResponse, object, Objeto de Resposta
@return variant, nenhum
/*/
method setSuccess(poResponse) class agdUtilsService
	Default poResponse := nil
	Self:oResponse := poResponse
	Self:lOk := .T.
	Self:nCodeError := 0
	Self:cMessage := ""
	Self:cSolution := ""
	Self:cDetailedMessage := ""
return nil

/*/{Protheus.doc} setFullResponse
Repassa o Retorno de outra classe que herda o Agro Utils
@type method
@version 12
@author jean.schulze
@since 13/01/2025
@param poAgroUtilsService, variant, Classe que herda o Agro Utils
@return variant, nil
/*/
method setFullResponse(poAgroUtilsService) class agdUtilsService
	Self:oResponse := poAgroUtilsService:getResponse()
	Self:lOk := poAgroUtilsService:isSuccess()
	Self:nCodeError :=  poAgroUtilsService:getCodeError()
	Self:cMessage :=  poAgroUtilsService:getMessageError()
	Self:cSolution :=  poAgroUtilsService:getSolutionError()
	Self:cDetailedMessage := poAgroUtilsService:getDetailedMessage()
	Self:aDetails := poAgroUtilsService:getDetailsError()
return nil

/*/{Protheus.doc} agdUtilsService::isSIGAAGDactive
Verifica se o parametro MV_SIGAAGD está ativo.
Quando desativado, define mensagem de erro.
@type method
@version 12
@author jc.maldonado
@since 24/09/2025
@return logical, resultado da verificacao
/*/
method isSIGAAGDactive() class agdUtilsService as logical
	if ! SUPERGETMV("MV_SIGAAGD", .f., .f.)
		::setError(STR0001) //"Parâmetro MV_SIGAAGD desativado. Impossível continuar."
		return .f.
	endIf
return .t.
