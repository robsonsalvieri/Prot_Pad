#include 'totvs.ch'
#include 'tlpp-core.th'
#include 'fwmvcdef.ch'

namespace agd.lookupRepository

using namespace agd.repositoryUtils

/*/{Protheus.doc} agdLookupRepository
Repositorio de Lookup
@type class
@version 12
@author lindembergson.pacheco
@since 20/05/2025
/*/
class agdLookupRepository from agdRepositoryUtils
	public method new()

	private method getFields()           as array
	private method getFieldsFromIndex()  as array
	private method getStructFromFields() as array
endclass

/*/{Protheus.doc} agdLookupRepository::New
Construtor
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return object, self
/*/
method new(cAlias, cFields) class agdLookupRepository
	_Super:new(cAlias, self:getFields(cAlias, cFields))
return self

/*/{Protheus.doc} agdLookupRepository::getFields
Retorna os campos que serao utilizados no AddMapFields da classe FWAdapterBaseV2
@type method
@version 12
@author lindembergson.pacheco
@since 20/05/2025
@return array, {{cCampoJson, cCampoTabela, cTipoCampo, lJsonField|lFixed}, ...}
/*/
method getFields(cAlias as character, cFields as character) as array class agdLookupRepository
	local aFieldNames          as array
	local aFields        := {} as array
	local aFirstPosition       as array

	if !empty(cFields)
		aFieldNames := strToKArr(upper(alltrim(cFields)), ',')
		aFields     := ::getStructFromFields(cAlias, aFieldNames)
	endif

	if empty(aFields)
		aFieldNames := ::getFieldsFromIndex(cAlias)
		aFields     := ::getStructFromFields(cAlias, aFieldNames)
	endif

	//se possivel, move o campo filial para manter o campo de busca por id na primeira posicao
	if len(aFields) > 1 .and. "_FILIAL" $ aFields[1, 2]
		aFirstPosition := aFields[1]
		aFields[1]     := aFields[2]
		aFields[2]     := aFirstPosition
	endif
return aFields

/*/{Protheus.doc} agdLookupRepository::getFieldsFromIndex
Retorna os nomes dos campos a partir do indice da tabela.
A prioridade é:
1. Campos da chave única (X2_UNICO).
2. Campos do índice primário (ordem 1 da tabela SIX).
@type method
@version 12
@author jc.maldonado
@since 21/07/2025
@return array, {"TAB_CAMPO1", "TAB_CAMPO2", ...}
/*/
method getFieldsFromIndex(cAlias as character) as array class agdLookupRepository
	local aFields as array

	aFields := strToKArr(alltrim(FwSX2Util():GetSX2data(cAlias, {"X2_UNICO"})[1][2]), "+")

	if empty(aFields)
		aFields := FWSIXUtil():GetAliasIndexes(cAlias)[1]
	endif
return aFields

/*/{Protheus.doc} agdLookupRepository::getStructFromFields
Retorna os campos estruturados para o AddMapFields a partir da tabela e dos campos informados.
@type method
@version 12
@author jc.maldonado
@since 22/07/2025
@param cAlias, character, alias da tabela
@param aFields, array, campos para serem estruturados
@return array, campos estruturados
/*/
method getStructFromFields(cAlias as character, aFields as array) as array class agdLookupRepository
	local aStructFields       as array
	local nX                  as numeric
	local nPos                as numeric
	local aValidFields  := {} as array

	aStructFields := FWSX3Util():GetListFieldsStruct(cAlias, .F./*lVirtual*/)
	if !empty(aStructFields)
		for nX := 1 To Len(aFields)
			if (nPos := aScan(aStructFields, {|xIt| xIt[1] == aFields[nX]})) > 0
				aAdd(aValidFields,;
					{;
					lower(aStructFields[nPos, 1]),;
					aStructFields[nPos, 1],;
					aStructFields[nPos, 2],;
					.T. })
			endIf
		next nX
	endif

	FwFreeArray(aStructFields)
return aValidFields
