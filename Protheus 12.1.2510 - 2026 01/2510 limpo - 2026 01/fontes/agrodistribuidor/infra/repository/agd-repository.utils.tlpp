#include "totvs.ch"
#include 'tlpp-core.th'
#INCLUDE 'agd.repository.utils.ch'
#INCLUDE "FWMVCDEF.CH"

namespace agd.repositoryUtils
using namespace tlpp.regex

/*/{Protheus.doc} agdRepositoryUtils
Classe Utilitaria para Padronizar o retorno de Repositorios do AgroDistribuidor
@type class
@version 12
@author jean.schulze
@since 26/12/2024
/*/
class agdRepositoryUtils FROM FWAdapterBaseV2
	public Data lOk                    As Logical
	public Data nCodeError             As Numeric
	public Data cMessage               As Character
	public Data cDetailedMessage       As Character
	public Data aDetails               As Array
	public Data cSolution              As Character
	public Data oResponse

	public Data cTableRepository          As Character //Tabela do repositório
	public Data aFieldsRepository         As Array     //Lista de Campos da tabela do Repositorio
	public Data aQueryJoinsRepository     As Object    //Lista de Joins da tabela do Repositorio
	public Data cQueryOptionalRepository  As Object    //Query Opcional da tabela do Repositorio

	public method new()

	public method find()
	public method findById()
	public method existsById() 		 as Logical

	public method getResponse()
	public method getMessageError()   as Character
	public method getDetailedMessage() as Character
	public method getSolutionError()  as Character
	public method getCodeError()      as Numeric
	public method getDetailsError()   as Array
	public method isSuccess()         as Logical
	public method isTableAGD()        as Logical
	public Method jsonToArrayFields() as Array
	public Method getParameterForAllBranches() as Array


	
	protected method setError()
	protected method setErrorByErrorMessageModel()
	protected method setSuccess()
	protected Method validateGetByUnico() as Logical
	protected Method convertToJson()      as object
	protected Method freeQueryToArray()   as Array
	protected Method getFieldByJson() 	  as Character
	protected Method getJsonFieldName()   as character
	protected Method getQueryDefault()    as character
	protected Method getWhereDefault()    as character
	protected Method setFieldsConsulta()
	protected Method setDefaultError()
	protected Method setErrorField()
	protected Method getOrderQueryParam() as character
	protected Method getWhereQueryParam() as character
	protected Method filterFields()       as array
	protected Method getFilterAdvancedQueryParam() as Array
	protected method setValueModel()
	protected Method buildModelCheck() 
	protected Method updateRepository()

endclass

/*/{Protheus.doc} new
Constructor da Classe
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return variant, Retorna a propria instancia
/*/
method new(pcTableRepository, paFields, paJoins, pcQueryOptional) class agdRepositoryUtils
	Default paFields        := {}  //Lista de Campos da tabela do Repositorio
	Default paJoins         := {}  //Lista de Joins da tabela do Repositorio
	Default pcQueryOptional := ''  //Query Opcional da tabela do Repositorio

	Self:oResponse := nil
	Self:lOk := .F.
	Self:nCodeError := 0
	Self:cMessage := ""
	Self:cSolution := ""
	
	Self:cTableRepository         := pcTableRepository
	Self:aFieldsRepository        := paFields
	Self:aQueryJoinsRepository    := paJoins
	Self:cQueryOptionalRepository := pcQueryOptional

	_Super:New("GET", .T.)

	::setIsCaseSensitive(.T.)
	::setUseSpaces(.T.)
return Self

/*/{Protheus.doc} getResponse
Retorna o objeto de Resposta da classe de Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return object, Objeto de Resposta
/*/
method getResponse() class agdRepositoryUtils
return Self:oResponse

/*/{Protheus.doc} getMessageError
Retorna a Mensagem de Erro do Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return character, Mensagem de erro
/*/
method getMessageError() as Character class agdRepositoryUtils
return Self:cMessage

/*/{Protheus.doc} agdRepositoryUtils::getDetailedMessage
Retorna a Mensagem Detalhada
@type method
@version 12
@author jc.maldonado
@since 04/11/2025
@return character, Mensagem Detalhada
/*/
method getDetailedMessage() as Character class agdRepositoryUtils
return Self:cDetailedMessage

/*/{Protheus.doc} agdRepositoryUtils::getDetailedMessage
Retorna o array com detalhes
@type method
@version 12
@author jc.maldonado
@since 04/11/2025
@return character, array com detalhes
/*/
method getDetailsError() as Array class agdRepositoryUtils
return Self:aDetails

/*/{Protheus.doc} getSolutionError
Retorna a Mensagem de Solução do Erro do Servico
@type method
@version P12 
@author claudineia.reinert
@since 26/03/2025
@return character, Mensagem de solução do erro
/*/
method getSolutionError() as Character class agdRepositoryUtils
return Self:cSolution

/*/{Protheus.doc} getCodeError
Retorna o Codigo de Erro Numerico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return numeric, Codigo de Erro
/*/
method getCodeError() as Numeric class agdRepositoryUtils
return Self:nCodeError

/*/{Protheus.doc} isSuccess
Retorna se a classe está com operacoes com sucesso
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@return logical, True ou False da Operacao
/*/
method isSuccess() as Logical class agdRepositoryUtils
return Self:lOk

/*/{Protheus.doc} setError
Informar erros na classe de Servico
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@param pcErrorMessage, character, String de Erro
@param pnErrorCode, numeric, Numero do erro
@return variant, nenhum
/*/
method setError(pcErrorMessage as Character, pnErrorCode as Numeric, pcSolutionMessage as Character) class agdRepositoryUtils
	Default pcErrorMessage := ""
	Default pcSolutionMessage := ""
	Default pnErrorCode := 0
	Self:oResponse := nil
	Self:lOk := .F.
	Self:nCodeError := pnErrorCode
	Self:cMessage := pcErrorMessage
	Self:cSolution := pcSolutionMessage
return nil


/*/{Protheus.doc} setErrorByErrorMessageModel
Envia os erros do modelo para o repository
MODEL_MSGERR_IDFIELDERR 4 - Id do campo de erro
MODEL_MSGERR_ID         5 - Campo Titulo da Janela
MODEL_MSGERR_MESSAGE    6 - Campo Problema
MODEL_MSGERR_SOLUCTION  7 - Campo Solução
@type method
@version 12
@author carlos.augusto
@since 22/05/2025
@param oModel, object, modelo para obter os erros
@param pnErrorCode, numeric, Numero do erro
@return variant, nenhum
/*/
method setErrorByErrorMessageModel(oModel as Object, pnErrorCode as Numeric) class agdRepositoryUtils
	Default pnErrorCode := 0

	Self:nCodeError := pnErrorCode

	Self:cMessage := ""
	If !Empty(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR])
		Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_IDFIELDERR]) + ' - '
	EndIf
	Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_ID ]) 	   + ' - '
	Self:cMessage += cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_MESSAGE])
	If !Empty(AllTrim(StrTran(StrTran(oModel:GetErrorMessage()[MODEL_MSGERR_SOLUCTION], Chr(13), ""), Chr(10), ""))) //A posicao 7 retorna \r\n (nao vazio)
		Self:cMessage += ' - ' + cValToChar(oModel:GetErrorMessage()[MODEL_MSGERR_SOLUCTION])
	Endif

	Self:oResponse := nil
	Self:lOk := .F.
return nil


/*/{Protheus.doc} setSuccess
Informar o sucesso na operacao da Classe de Serviço
@type method
@version 12
@author jean.schulze
@since 26/12/2024
@param poResponse, object, Objeto de Resposta
@return variant, nenhum
/*/
method setSuccess(poResponse) class agdRepositoryUtils
	Default poResponse := nil
	Self:oResponse := poResponse
	Self:lOk := .T.
	Self:nCodeError := 0
	Self:cMessage := ""
	Self:cSolution := ""
return nil
/*/{Protheus.doc} validateGetByUnico
Valida se a chave informada foi a unica encontrada na base de dados.
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@param oJsonResponse, object, Json Response
@return logical, valido/invalido
/*/
Method validateGetByUnico(oJsonResponse) as logical Class agdRepositoryUtils
	Local lValid := .f.

	If Empty(oJsonResponse['item'])
		Self:setError(STR0001, 401) //"Registro Não Encontrado"
	Elseif oJsonResponse['hasNext'] == .t.
		Self:setError(STR0002, 404) //"Existe outro registro com a mesma identificação"
	else
		lValid := .t.
	EndIf
Return lValid

/*/{Protheus.doc} convertToJson
Converter String em Json Object
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@param cJsonParam, character, Json String
@return object, Json Object
/*/
Method convertToJson(cJsonParam) as object Class agdRepositoryUtils
	Local oJsonObj as object
	oJsonObj := JSonObject():New()
	oJsonObj:fromJson(cJsonParam)
Return oJsonObj


/*/{Protheus.doc} find
Listagem Padrão do Repositório
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param nPage, numeric, pagina
@param nPageSize, numeric, tamanho pagina
@param oJsonQryParam, object, query param
@return variant, nil
/*/
Method find(nPage, nPageSize, oJsonParam) Class agdRepositoryUtils
	Local aArea         As Array
	Local cWhereParam   As Character
	Local cOrder        As Character
	Local jResponseJson as Object

	Default oJsonParam := Nil
	Default nPage          := 1
	Default nPageSize      := 10

	If Self:lOk
		aArea       := FwGetArea()
		cOrder      := Self:getOrderQueryParam(oJsonParam, Self:aFieldsRepository)
		cWhereParam := Self:getWhereQueryParam(oJsonParam, Self:aFieldsRepository)

		Self:setFieldsConsulta(Self:aFieldsRepository)
		Self:setPage(nPage)
		Self:setPageSize(nPageSize)
		Self:SetQuery(Self:getQueryDefault(Self:aQueryJoinsRepository, Self:cQueryOptionalRepository))
		Self:SetWhere(Self:getWhereDefault(cWhereParam))
		Self:SetUrlFilter(Self:getFilterAdvancedQueryParam(oJsonParam))
		Self:SetOrder( cOrder )

		If Self:Execute()
			Self:FillGetResponse()
			if Self:lOk
				jResponseJson := Self:convertToJson(Self:getJSONResponse())
				Self:setSuccess(jResponseJson)
			Endif
		EndIf

		FwRestArea(aArea)
		FwFreeArray(aArea)
		FreeObj(jResponseJson)
	EndIf
return nil

/*/{Protheus.doc} findById
Retorna registro unico conforme chave informada
@type method
@version 12
@author jean.schulze
@since 09/05/2025
@param aFieldsId, array, campos de pesquisa
@return variant, nil
/*/
Method findById(aFieldsId as Array) class agdRepositoryUtils
	Local aArea				As Array
	Local nX				As Numeric
	Local jResponseJson		As object
	Local cWhereParam		:= ""

	For nX:= 1 to Len(aFieldsId)
		If TamSX3(aFieldsId[nX][1])[3] == "C" .Or. TamSX3(aFieldsId[nX][1])[3] == "D"
			cWhereParam +=	IIF((nX > 1)," AND ", " ") + aFieldsId[nX][1] + " = '" + aFieldsId[nX][2] + "'"
		Else
			cWhereParam +=	IIF((nX > 1)," AND ", " ") + aFieldsId[nX][1] + " = " + aFieldsId[nX][2]
		EndIf
	Next

	If !Empty(cWhereParam)
		If Self:lOk
			aArea := FwGetArea()

			Self:setFieldsConsulta(Self:aFieldsRepository)
			Self:setPage(1)
			Self:setPageSize(1)
			Self:SetQuery(Self:getQueryDefault(Self:aQueryJoinsRepository, Self:cQueryOptionalRepository))
			Self:SetWhere(Self:getWhereDefault(cWhereParam))
			Self:SetStyleReturn("item")
			
			If Self:Execute()
				Self:FillGetResponse()
				if Self:lOk
					jResponseJson := Self:convertToJson(Self:getJSONResponse())
					if Self:validateGetByUnico(jResponseJson)
						Self:setSuccess(jResponseJson['item'][1])
					EndIf
				endif
			EndIf

			FwRestArea(aArea)
			FwFreeArray(aArea)
			FreeObj(jResponseJson)
		EndIf
	else
		Self:setDefaultError(401)
	EndIf
Return Nil


/*/{Protheus.doc} jsonToArrayFields
Criar array de comando de salvar com json
@type method
@version 12
@author jean.schulze
@since 06/01/2025
@param pjComandFields, json, comando com campos externos e valor
@param paFields, array, lista de campos internos
@return array, array de comando com campos internos e valor
/*/
Method jsonToArrayFields(pjComandFields as Json, paFields as Array, lApplyParseX3 as Logical) as Array Class agdRepositoryUtils
	Local nField		        as numeric
	Local aFieldStructure 	:= {} as array
	Local aJsonNames 		:= {} as array
	Local nPos 				:= 0
	Default lApplyParseX3 	:= .F.

	For nField := 1 to len(paFields)
		aJsonNames := pjComandFields:getNames()
		nPos := aScan(aJsonNames, {|x| UPPER(x) == UPPER(paFields[nField][1])})
		
		if nPos > 0
			cValueProp := pjComandFields:GetJsonText(aJsonNames[nPos])

			if cValueProp == 'null'
				cValueProp := ""
			endif

			if paFields[nField][3] == 'N'
				cValueProp := Val(cValueProp)
			elseif paFields[nField][3] == 'D'
				cValueProp := Fw8601ToDate(cValueProp)
			endif

			If lApplyParseX3 .And. paFields[nField][3] == 'C'
				cValueProp := Padr(cValueProp, TamSX3(paFields[nField][2])[1])
			EndIf

			AAdd(aFieldStructure, {paFields[nField][2], cValueProp})
		endIf
	Next
return aFieldStructure

/*/{Protheus.doc} getQueryDefault
Gera uma Query Default para entidade do repositorio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@return character, query
/*/
Method getQueryDefault(paInnerJoin as Array, pcFrom as character) as Character Class agdRepositoryUtils
	Local cQuery As Character
	Local nX     As Numeric
	Default paInnerJoin   := {}
	Default pcFrom        := ""

	cQuery := " SELECT #QueryFields# "

	if !Empty(pcFrom)
		cQuery += " FROM ("+pcFrom+") " +Self:cTableRepository+" "
	else
		cQuery += " FROM " + RetSqlName(Self:cTableRepository) + " "+Self:cTableRepository+" "
	endif

	For nX:= 1 to Len(paInnerJoin)
		cQuery += " "+ paInnerJoin[nX][1] +" JOIN " + RetSqlName(paInnerJoin[nX][2]) + " "+paInnerJoin[nX][2]+" ON " + paInnerJoin[nX][3]
	Next

	cQuery += " WHERE #QueryWhere# "

return cQuery

/*/{Protheus.doc} getWhereDefault
Gera um where default para o repositorio
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param cWhereParam, character, where adicional
@return character, wehrer query
/*/
Method getWhereDefault(cWhereParam, cTable) as Character Class agdRepositoryUtils
	Local cWhere As Character
	Default cWhereParam := ""
	Default cTable      := Self:cTableRepository

	if Len(TamSX3(cTable+"_FILIAL")) > 0
		cWhere := " "+cTable+"."+cTable+"_FILIAL = '"+ FWxFilial(cTable) +"'"
	else //tabelas sem a primeira letra
		cWhere := " "+cTable+"."+SubStr(cTable,2,2)+"_FILIAL = '"+ FWxFilial(cTable) +"'"
	endif

	cWhere += " AND "+cTable+".D_E_L_E_T_ = ' '"
	if !Empty(cWhereParam)
		cWhere += " AND ("+cWhereParam+")"
	Endif
Return cWhere

/*/{Protheus.doc} setFieldsConsulta
Informa os campos da consulta conforme um getFields
@type method
@version 12
@author jean.schulze
@since 06/05/2025
@param aFields, array, Self:getFields()
@return variant, nil
/*/
Method setFieldsConsulta(aFields) Class agdRepositoryUtils
	Local nI as Numeric

	For nI := 1 to len(aFields)

		//verifica se é um campo existente no dicionario
		if Len(TamSX3(aFields[nI][2])) > 0
			aStruct :={aFields[nI][2] , aFields[nI][3], TamSX3(aFields[nI][2])[1] , TamSX3(aFields[nI][2])[2]}
		elseif aFields[nI][3]  = "N"
			aStruct := {aFields[nI][2] , aFields[nI][3], 25 , 2}
		else
			aStruct := {aFields[nI][2] , aFields[nI][3], 60 , 0}			
		endif

		Self:AddMapFields(aFields[nI][1], aFields[nI][2] , aFields[nI][4], aFields[nI][4], aStruct)

	Next
return nil

/*/{Protheus.doc} setDefaultError
Informa erros padrões do repositório
@type method
@version 12
@author jean.schulze
@since 14/01/2025
@param nCode, numeric, codigo do erro
@return variant, nil
/*/
Method setDefaultError(nCode as Numeric) Class agdRepositoryUtils

	If nCode == 401
		Self:setError(STR0001, 401) //registro nao encontrado
	Elseif nCode == 403
		Self:setError(STR0003, 403) //"O payload não está no padrão esperado."
	EndIf
Return nil

/*/{Protheus.doc} setErrorField
Define uma mensagem de erro padronizada baseada no código e no campo ausente no JSON.
@type method
@version 1.0
@since 23/05/2025
@author rodrigo.nsoledade
@param nCode, numeric, Código do erro HTTP (401 ou 403)
@param cField, character, Nome do campo ausente ou inválido no payload
@return nil
/*/
Method setErrorField(nCode as Numeric, cField as Character) Class agdRepositoryUtils

	If nCode == 403
		If !Empty(cField)
			Self:setError(STR0003 + space(1) + STR0005 + cField, 403) //"O payload não está no padrão esperado."#"Campo obrigatório não encontrado: "
		Else
			Self:setError(STR0003, 403) //"O payload não está no padrão esperado."
		EndIf
	EndIf

Return nil

/*/{Protheus.doc} getOrderQueryParam
Obtem a ordem conforme o json de queryParam
@type method
@version 12
@author jean.schulze
@since 03/02/2025
@param oJsonQuery, object, json de opções de query
@param aFields, array, lista de campos
@return character, query order by
/*/
method getOrderQueryParam(oJsonQuery, aFields) as Character Class agdRepositoryUtils
	Local cOrder   := ""
	Local cFields  as Character
	Local cField   as Character
	Local cDirecao as Character
	Local aOrdens  as Array
	Local nParam := 0

	If ValType(oJsonQuery) == "J" .and. oJsonQuery:hasProperty("order") .and. oJsonQuery:GetJsonText("order") != 'null'

		cFields   := alltrim(oJsonQuery:GetJsonText("order"))

		aOrdens := StrTokArr2(cFields, ",")

		for nParam := 1 to len(aOrdens)
			cField := aOrdens[nParam]

			cDirecao := "ASC"

			if SubString(cField, 1, 1) == "-"
				cField   := SubStr(cField, 2)
				cDirecao := "DESC"
			endif

			cFieldQuery := Self:getFieldByJson(cField, aFields)

			if !Empty(cFieldQuery)
				cOrder += IIF(!Empty(cOrder), " , ", " ")
				cOrder += cFieldQuery + " " + cDirecao
			endif

		next nParam

	EndIf

return cOrder

/*/{Protheus.doc} getFilterAdvancedQueryParam
Obtem o filtro avançado da request 
@type method
@version 12
@author jean.schulze
@since 10/06/2025
@param oJsonQuery, object, query param
@return array, filtro avançado
/*/
method getFilterAdvancedQueryParam(oJsonQuery) as Array Class agdRepositoryUtils
	Local aFilterAdvaced := {}

	If ValType(oJsonQuery) == "J" .and. oJsonQuery:hasProperty("filter") .and. oJsonQuery:GetJsonText("filter") != 'null'
		aFilterAdvaced := {{"FILTER", oJsonQuery:GetJsonText("filter")}}
	EndIf


return aFilterAdvaced

/*/{Protheus.doc} getWhereQueryParam
Obtem os filtros da query(WHERE) conforme o json de queryParam
@type method
@version 12
@author jean.schulze
@since 04/02/2025
@param oJsonQuery, object, lista de parametros da query
@param aFields, array, lista de campos
@return character, cWhere
/*/
method getWhereQueryParam(oJsonQuery, aFields) as Character Class agdRepositoryUtils
	Local cWhere := ""
	Local nParam := 0
	Local aParamsQuery := {}

	If oJsonQuery != nil .and. ValType( oJsonQuery) == "J"
		aParamsQuery := oJsonQuery:GetNames()
	EndIf

	for nParam := 1 to len(aParamsQuery)

		cFieldQuery := Self:getFieldByJson(aParamsQuery[nParam], aFields)

		if !Empty(cFieldQuery)

			cWhere += IIF(!Empty(cWhere), " AND ", " ")
			cWhere += cFieldQuery
			cValueFiltro := alltrim(oJsonQuery:GetJsonText(aParamsQuery[nParam]))

			if LEN(TamSX3(cFieldQuery)) > 0 .and. TamSX3(cFieldQuery)[3] != "C"
				cWhere += " = " + cValueFiltro
			else
				cWhere += " = '" + cValueFiltro + "'"
			endif

		endif

	next nParam

return cWhere

/*/{Protheus.doc} freeQueryToArray
Executa Query e retorna em um array
@type method
@version 12
@author jean.schulze
@since 14/03/2025
@param cQuery, character, Query completa para execucao
@param aFields, array, lista de campos
@return array, retorno da query com os devidos campos
/*/
Method freeQueryToArray(pcQuery as Character, paFields as Array) as Array Class agdRepositoryUtils
	Local oQry     as Object
	Local nx       as numeric
	Local aReturn  := {}
	Local cQuery 	:= ""

	cQuery := ChangeQuery(pcQuery)

	oQry := FWPreparedStatement():New( )

	oQry:SetQuery(cQuery)

	cQuery := oQry:GetFixQuery()
	cAlias := MpSysOpenQuery(cQuery)

	While (cAlias)->(!EOF())

		AAdd(aReturn, JsonObject():new())
		nPos := Len(aReturn)

		For nX:= 1 to Len(paFields)
			aReturn[nPos][paFields[nX]] := (cAlias)->(&(paFields[nX]))
		Next

		(cAlias)->(DBSkip())

	EndDo

	(cAlias)->(DBCloseArea())

Return aReturn

/*/{Protheus.doc} getFieldByJson
Obtem o campo de dicionário através do campo json correposdente
@type method
@version 12
@author jean.schulze
@since 07/05/2025
@param pcFieldSearch, variant, atributo do json
@param aFields, array, lita de campos do repositorio
@return character, campo correspondente no dicionário
/*/
Method getFieldByJson(pcFieldSearch, aFields) as Character Class agdRepositoryUtils
	nPosicaoField := aScan(aFields, {|x| Upper(x[1]) == Upper(pcFieldSearch)})
	if nPosicaoField > 0
		return aFields[nPosicaoField][2]
	endif
return ""

/*/{Protheus.doc} agdRepositoryUtils::getJsonFieldName
Obtem o nome do campo json a partir do nome do campo da tabela do Protheus
@type method
@version 12
@author jc.maldonado
@since 26/05/2025
@param cProtheusName, character, nome campo tabela protheus
@return character, nome campo json
/*/
Method getJsonFieldName(cProtheusName as character) as character class agdRepositoryUtils
	local cJsonName as character

	if (nPos := aScan(::aFieldsRepository, {|xIt| upper(xIt[2]) == upper(cProtheusName)})) > 0
		cJsonName := ::aFieldsRepository[nPos, 1]
	endif
return cJsonName

/*/{Protheus.doc} buildModelCheck
Realiza a abertura do model e trata erros de abertura
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@param pcModel, character, nome do modelo
@param nOption, numeric, opção a ser realizada
/*/
method buildModelCheck(pcModel as Character, nOption as integer)  Class agdRepositoryUtils
	local oModel as object

	default nOption := MODEL_OPERATION_UPDATE

	oModel := FwLoadModel(pcModel)
	oModel:SetOperation(nOption)
	oModel:Activate()

	if oModel:HasErrorMessage()
		Self:setErrorByErrorMessageModel(oModel, 403)
	else
		Self:setSuccess()	
	endif

	oModel:DeActivate()
	oModel:Destroy()
	
Return nil

/*/{Protheus.doc} setValueModel
Realiza a aplicação de valores a um modelo de dados
@type method
@version 12
@author jean.schulze
@since 04/08/2025
@param oModel, object, Modelo/Submodelo que receberá os valores
@param aFields, array, Lista de Campos
@return variant, nil
/*/
Method setValueModel(oModel as object, aFields as array) class agdRepositoryUtils
	Local nField as numeric

	/*coloca em ordem o array para seguir a sequencia de tratamento de campos do dicionario*/
	aSort(aFields,,, {|x, y| Val(GetSx3Cache(x[1],'X3_ORDEM')) < Val(GetSx3Cache(y[1],'X3_ORDEM')) })

	For nField := 1 to len(aFields)
		if oModel:CanSetValue(aFields[nField][1])
			if !oModel:SetValue(aFields[nField][1], aFields[nField][2])
				Self:setErrorByErrorMessageModel(oModel:getModel(), 400)
				return nil
			endif
		endif	
	Next nField

	if Self:isSuccess()
		/*Verifica se todos os campos foram preenchidos corretamente*/
		For nField := 1 to len(aFields)
			xModelValue := oModel:GetValue(aFields[nField][1])
			xFieldValue := aFields[nField][2]

			if ValType(oModel:GetValue(aFields[nField][1])) == "C"
				xModelValue := AllTrim(xModelValue)
				xFieldValue := AllTrim(xFieldValue)		
			endif

			if xModelValue != xFieldValue
				cError := STR0007 + aFields[nField][1] + '. ' //'Diferença de apropriação de valores no modelo de dados para o campo: '
				cError += STR0008 + cValToChar(xFieldValue) + STR0009 + cValToChar(xModelValue) //'Valor informado/configurado: ' //' / Valor no modelo: '
				Self:setError(cError, 403)
				return nil
			endif

		Next nField
	endif

return nil

/*/{Protheus.doc} isTableAGD
Validas tabelas usadas no modulo AGD
@type method
@version 12
@author lindembergson.pacheco
@since 03/06/2025
@return logical, True ou False
/*/
method isTableAGD(cAlias) as Logical class agdRepositoryUtils
	Local cTabelasPermitidas := "SA1,SA2,SA3,SA4,SAH,SB8,SE4,SED,SB1,SF4,SM4,DA0,DA3,NJU,NNR,NP3,NE0,NEA,NEB,NEC,NED,NEE,NEF,NEG,NEH,NEI,NEJ,NEK,NEL,NEM,NEN,SB5,NEX,NP8,NEO,NE2" 
	if cAlias $ cTabelasPermitidas
		Self:lOk := .T.
	else
		Self:lOk := .F.
	endif
return Self:lOk

/*/{Protheus.doc} agdRepositoryUtils::filterFields
Filtra os campos de acordo com o filtro e indice informados.
@type method
@version 12
@author jc.maldonado
@since 03/06/2025
@param aFields, array, array com os campos a serem filtrados
@param nIndex, numeric, 1=jsonField; 2=ProtheusField
@param aFilter, array, campos para serem usados no filtro
@return array, array com os campos filtrados
/*/
method filterFields(aFields as array, nIndex as integer, aFilter as array) as array class agdRepositoryUtils
	local aFiltered := {} as array
	local nX              as integer
	local nPos            as integer

	for nX := 1 to len(aFilter)
		if (nPos := aScan(aFields, {|xFld| xFld[nIndex] == aFilter[nX]})) > 0
			aAdd(aFiltered, aFields[nPos])
		endif
	next nX
return aFiltered

/*/{Protheus.doc} existsById
Verifica se o registro existe para a entidade do repositorio
@type method
@version 12
@author jean.schulze
@since 14/08/2025
@param cId, character, identificador do dbseek
@return logical, existe/inexistente
/*/
method existsById(cId) as Logical Class agdRepositoryUtils
	Local lRet := .T.

	&(Self:cTableRepository + '->(dbSetOrder(1))')
	If !&(Self:cTableRepository + '->(dbSeek(cId))')
		lRet := .F.
		Self:setError(STR0001, 400) //"Registro Não Encontrado"
	EndIf

Return lRet

/*/{Protheus.doc} agdRepositoryUtils::getSX6Param
Retorna os valores do parametro (SX6) de todas as filiais.
@type method
@version 12
@author jc.maldonado
@since 08/10/2025
@param cParam, character, codigo do parametro
@return array, {{filial, codigo, conteudo}}
/*/
method getParameterForAllBranches(cParam as character) as array class agdRepositoryUtils
	local cSQL        as character
	local aBindParams as array
	local cNewAlias   as character
	local aParams     as array

	aBindParams := {}
	cSQL := "SELECT X6_FIL, X6_VAR, X6_CONTEUD"
	cSQL += " FROM " + retSqlName("SX6")
	cSQL += " WHERE X6_VAR = ?" ; aAdd(aBindParams, cParam)
	cSQL += " AND D_E_L_E_T_ != '*'"
	cSQL := changeQuery(cSQL)

	cNewAlias := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry2(,, cSQL, aBindParams), cNewAlias, .F., .T.)

	aParams := {}
	While (cNewAlias)->(!EOF())
		aAdd(aParams, {(cNewAlias)->X6_FIL, (cNewAlias)->X6_VAR, alltrim((cNewAlias)->X6_CONTEUD)})
		(cNewAlias)->(DBSkip())
	EndDo

	(cNewAlias)->(DBcloseArea())
return aParams

/*/{Protheus.doc} agdNegocioRepository::updateRepository
Faz update na tabela
@type method
@version P12
@author scheronlini.martins
@since 19/12/2025
@param aFieldsSaveComand, array, array com campos e valores a serem salvos
/*/
method updateRepository(aFieldsSaveComand, cChaveID,cOrderTable) class agdRepositoryUtils
	Local nField		as Numeric
	Local aArea		:= GetArea()
	lOCAL cTable := Self:cTableRepository

	dbSelectArea(cTable )
	&(cTable + "->(dbSetOrder(" + cOrderTable + "))")
	if &(cTable +'->(dbSeek("' + cChaveID +  '"))')

		RecLock(cTable ,.f.)
		For nField := 1 to len(aFieldsSaveComand)
			&(aFieldsSaveComand[nField][1]) :=  aFieldsSaveComand[nField][2]
		Next
		&(cTable  + "->(MsUnlock())")

		Self:setSuccess(cChaveID)

	endif

	RestArea(aArea)

return nil
