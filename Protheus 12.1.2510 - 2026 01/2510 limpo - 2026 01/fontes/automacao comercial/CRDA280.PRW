#INCLUDE "CRDA280.CH"
#INCLUDE 'FWMVCDEF.CH'
#INCLUDE "PROTHEUS.CH"

Static lMvLjPdvPa	:= FindFunction("LjxBGetPaf") .AND. LjxBGetPaf()[2]	// Indica se eh pdv 
Static aCarFid   	:= {} 													// Numero do Cartao Fidelidade do Cliente/Data Validade/Status.

/*


Ŀ
Funo	 CRDA280    Autor  Leandro Nogueira	     Data  15/12/10 
Ĵ
Descrio Cadastro de cartao fidelidade								  
Ĵ
Sintaxe	 ExpL1:=CRDA280()				 							  
Ĵ
Parametros 		 													  
Ĵ
Retorno   NIL					   									  
Ĵ
Uso		 SIGACRD												      
ٱ

*/ 
Function CRDA280()
Local oBrowse

If !GetRpoRelease ("R5")
	Return NIL
EndIf

oBrowse := FWMBrowse():New()
oBrowse:SetAlias('MBO')
oBrowse:SetDescription(STR0001) // "Cadastro de cartao Fidelidade"
oBrowse:Activate()  		

Return NIL

/*


Ŀ
Funo	 MenuDef	 Autor  Leandro Nogueira	     Data  15/12/10 
Ĵ
Descrio MenuDef - MVC												  
Ĵ
Sintaxe	 ExpA1:=MenuDef()				 							  
Ĵ
Parametros 		 													  
Ĵ
Retorno   ExpA1 = aRotina											  
Ĵ
Uso		 SIGACRD												      
ٱ

*/

Static Function MenuDef()
Local aRotina 	:= {}


ADD OPTION aRotina TITLE STR0002 	ACTION 'PesqBrw'         	OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0003 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0004 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0005 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0006 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 5 ACCESS 0 //"Excluir"
ADD OPTION aRotina TITLE STR0007 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 8 ACCESS 0 //"Imprimir"
ADD OPTION aRotina TITLE STR0008 	ACTION 'VIEWDEF.CRDA280' 	OPERATION 9 ACCESS 0 //"Copiar"
ADD OPTION aRotina TITLE STR0030	ACTION 'CA280CONSO'  		OPERATION 10 ACCESS 0 //"Consolidar"
ADD OPTION aRotina TITLE STR0031	ACTION 'CA280TRFIL'       OPERATION 11 ACCESS 0 // "Trocar Filial"

Return aRotina



/*


Ŀ
Funo	 ModelDef   Autor  Leandro Nogueira	     Data  15/12/10 
Ĵ
Descrio MVC - Camada de modelo de dados						 	  
Ĵ
Sintaxe	 ExpO1:=ModelDef()			 								  
Ĵ
Parametros 		 													  
Ĵ
Retorno   ExpO1 = oModel											  
Ĵ
Uso		 SIGACRD												      
ٱ

*/
Static Function ModelDef()  

Local oStruMBO 	:= FWFormStruct( 1, 'MBO') 	// Estrutura do Modelo de Dados
Local oModel	:= MPFormModel ():New('CRDA280MODEL',,{||.T.},{|oModel| CA280COM(oModel)})

//
//Definicoes do modelo de dados  
//  
oModel:AddFields( 'MBOMASTER',   , oStruMBO)
oModel:SetDescription(STR0009) 							//"Cadastro de carto fidelidade"
oModel:GetModel( 'MBOMASTER' ):SetDescription(STR0010)	//"Dados do cartao" 

Return oModel


/*


Ŀ
Funo	 ViewDef    Autor  Leandro Nogueira	     Data  15/12/10 
Ĵ
Descrio MVC - Camada de visualizao de dados					 	  
Ĵ
Sintaxe	 ExpO1:=ModelDef()			 								  
Ĵ
Parametros 		 													  
Ĵ
Retorno   ExpO1 = oModel											  
Ĵ
Uso		 SIGACRD												      
ٱ

*/
Static Function ViewDef()

Local oModel   := FWLoadModel( 'CRDA280' )  	// Modelo de Dados baseado no ModelDef do fonte informado
Local oStruMBO := FWFormStruct( 2, 'MBO' )		// Objeto com a estrutura da View
Local oView    := FWFormView():New()			// Objeto que tem como funcionalidade instanciar os objetos graficos do modelo de dados

//
//Definicoes do view  
//

oView:SetModel( oModel )
oView:AddField( 'VIEW_MBO', oStruMBO, 'MBOMASTER' )
oView:CreateHorizontalBox( 'TELA' , 100 )
oView:SetOwnerView( 'VIEW_MBO', 'TELA' )

Return oView       


/*


Ŀ
Funo	 Ca280Exec  Autor  Leandro Nogueira	     Data  22/12/10 
Ĵ
Descrio Executa a funcao desejada de acordo com o ambiente onde a    
			 e feita a requisicao.										   				  
			 Se a requisicao de execucao for do Front Loja ou do          				  
			 SigaLoja PDV Off-Line,a funcao sera executada via WebService 				  
			 													           				  
			 Se a requisicao de execucao for do Sigaloja OnLine           				  
			 a funcao sera executada local.							   				  
Ĵ
Sintaxe	 Expl1:=Ca280Exec(cExp1,cExp2,dExp3,nExp4;					  
			 					cExp5,cExp6,cExp7,nExp8,lExp9)			  
Ĵ
ParametroscExp1 - Nome da funcao que ser executada					  
			 cExp2 - Numero do cartao fidelidade						  
			 dExp3 - Data de validade da recarga						  
			 nExp4 - Valor da recarga									  
			 cExp5 - Numero do documento								  
			 cExp6 - Serie												  
			 cExp7 - Loja												  
			 cExp8 - Tipo do mmovimento 								  
			 nExp9 - Valor que sera abatido do saldo do cartao (venda)	  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280Exec (cFunc,		cNumCar, 	dDtValid,	nValor,	;
					cDoc,		cSerie,		cLoja,		cTipo ,	;	
					nSaldoAbat, cCodCli)

Local lRet	:= .F.						//Retorno
Local nX 	:=  0           			//Cotador
Local oSvc 	                			//Objeto do WebService
Local nSaldo:= 0						//Saldo do cartao fidelidade
Local cMoeda:= SuperGetMv("MV_SIMB1")	// Simbolo da moeda principal

Default cFunc		:= ""
Default cNumCar		:= "" 
Default dDtValid	:= "" 
Default nValor		:= 0 
Default cDoc		:= "" 
Default cSerie		:= "" 
Default cLoja		:= "" 
Default cTipo		:= ""
Default cCodCli     := ""
Default nSaldoAbat 	:= 0 


//
//Execucao via WebService para           
//Front Loja ou Sigaloja(PDV Off-Line)                              
//
If  (nModulo == 23) .OR. (nModulo ==12 .AND. lMvLjPdvPa)
	
	If !Ca280CkWs () 
		MsgStop(STR0023)//"Web Service para consulta do carto fidelidade est indisponvel !"		
		Return lRet 
	EndIf
	
	
	oSvc      := WSLjCarFid():New()
	iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oSvc),Nil) //Monta o Header de Autenticao do Web Service
	oSvc:_URL := "http://"+Alltrim(LJGetStation("WSSRV"))+"/LjCarFid.apw"    

	Do Case	
	
	//Validar Cartao Fidelidade		
	Case Upper(cFunc) $ "CA280CART"		
		oSvc:ValCarFid(cNumCar,cEmpAnt,cFilAnt) 		
		lRet:=oSvc:LVALCARFIDRESULT	 		
		If ValType(lRet) <> "U"
			If !lRet
				MsgStop (STR0022)//"Carto Invlido."
			EndIf
		Else
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		EndIf
	//Incluir saldo no cartao fiidelidade
	Case Upper(cFunc) $ "CA280ISLD"	 

		oSvc:IncSaldFid(cNumCar,	dDtValid,	nValor,	cDoc,;
						cSerie,		cLoja,		cEmpAnt,cFilAnt)
						
		lRet:=oSvc:lINCSALDFIDRESULT						
		
		If ValType(lRet) == "U"
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		EndIf
	//Consultar saldo do cartao fiidelidade
	Case Upper(cFunc) $ "CA280CSLD"	 			
		oSvc:ConSaldFid(cNumCar,nValor,cEmpAnt,cFilAnt)
		lRet:=oSvc:lCONSALDFIDRESULT
		If ValType(lRet) <> "U"		
			If !lRet	
				oSvc:CalSaldFid (cNumCar,cEmpAnt,cFilAnt)
				
				If ValType(oSvc:nCALSALDFIDRESULT) <> "U"
					MsgStop (STR0014 +  AllTrim(cMoeda) + " " + AllTrim(TRANSFORM(oSvc:nCALSALDFIDRESULT,'@E 99,999,999.99'))  + STR0015 +  AllTrim(cNumCar) + STR0016,STR0017)//"O Saldo de " # " do carto fidelidade " # "  insuficiente para completar esta operao !"#"Saldo Insuficiente"  														
				Else
					//Ŀ
					//Realiza o tratamento de erro na comunicacao com o Web Service
					//
					cSvcError := GetWSCError()
					If Left(cSvcError,9) == "WSCERR048"
							cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
							cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
							Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
					EndIf				
				EndIf
			Endif
		Else
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		Endif
	
	//Atualizar saldo do cartao fiidelidade
	Case Upper(cFunc) $ "CA280ASLD"	 
		oSvc:AtuSaldFid(cNumCar,nSaldoAbat,	cDoc,cSerie,;
						cLoja,cEmpAnt,cFilAnt)
		lRet:=oSvc:lATUSALDFIDRESULT
											
		If ValType(lRet) == "U"
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		EndIf
	//Estornando saldo do cartao fiidelidade
	Case Upper(cFunc) $ "CA280ESLD"	 
		oSvc:EstSaldFid(cNumCar,cDoc,cSerie,cLoja,;
						cTipo,cEmpAnt,cFilAnt)
		lRet:=oSvc:lESTSALDFIDRESULT									

		If ValType(lRet) == "U"
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		EndIf
	//Consultando Cartao Fidelidade do Cliente
	Case Upper(cFunc) $ "CA280CONCL"	 
		oSvc:ConCartFid(cCodCli,cLoja,cEmpAnt,cFilAnt)
		If ValType(oSvc:oWSCONCARTFIDRESULT:oWSWSFIDITENS) <> "U"
			If Len(oSvc:oWSCONCARTFIDRESULT:oWSWSFIDITENS)> 0	
				aAdd( aCarFid, {	oSvc:oWSCONCARTFIDRESULT:oWSWSFIDITENS[1]:cCartaoFid  ,;
									oSvc:oWSCONCARTFIDRESULT:oWSWSFIDITENS[1]:dDataValFid ,;
									oSvc:oWSCONCARTFIDRESULT:oWSWSFIDITENS[1]:cStatusFid})
	        	lRet := .T.
	        EndIf
		Else
			//Ŀ
			//Realiza o tratamento de erro na comunicacao com o Web Service
			//
			cSvcError := GetWSCError()
			If Left(cSvcError,9) == "WSCERR048"
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					Conout("Err WS :" + cSoapFDescr + " -> " + cSoapFCode)
			EndIf
			lRet := .F.
		EndIf
	OtherWise
		lRet:= .F.
		MsgStop(STR0020)//"Funo invlida para validacao de carto fidelidade"	
	EndCase	                    
		
//
//Execucao local para   
//Sigaloja On Line      
//
Else
	
	Do Case
	
	//Validar Cartao Fidelidade
	Case Upper(cFunc) $ "CA280CART"
		lRet := Ca280Cart (cNumCar)   
		If !lRet
			MsgStop (STR0022)//"Carto Invlido."
		EndIf
	//Incluir saldo no cartao fiidelidade
	Case Upper(cFunc) $ "CA280ISLD"	 
		lRet := Ca280ISld (cNumCar,	dDtValid,	nValor,	cDoc,;
							cSerie,	cLoja)		
			
	//Consultar saldo do cartao fiidelidade
	Case Upper(cFunc) $ "CA280CSLD"	 
		lRet := Ca280CSld(cNumCar,nValor,@nSaldo)
		If !lRet				
			MsgStop (STR0014 +  AllTrim(cMoeda) + " " + AllTrim(TRANSFORM(nSaldo,'@E 99,999,999.99'))  + STR0015 +  AllTrim(cNumCar) + STR0016,STR0017)//"O Saldo de "#" do carto fidelidade "#"  insuficiente para completar esta operao !"#"Saldo Insuficiente"
		Endif		
		
	//Atualizar saldo do cartao fiidelidade
	Case Upper(cFunc) $ "CA280ASLD"	 
		lRet := Ca280ASld(cNumCar,nSaldoAbat,cDoc,cSerie,;
							cLoja)	 								 
							
	//Estornar saldo do cartao fiidelidade                                       
	Case Upper(cFunc) $ "CA280ESLD"	 
		lRet := Ca280ESld(cNumCar,cDoc,cSerie,cLoja,;
							cTipo)

	//Consultando Cartao Fidelidade do Cliente
	Case Upper(cFunc) $ "CA280CONCL"	 
		aCarFid := CA280CONCL(cCodCli,cLoja)
		If Len(aCarFid)> 0
			lRet := .T.
		EndIf	 									
	OtherWise
		lRet:= .F.
		MsgStop(STR0020)//"Funo invlida para validacao de carto fidelidade"	
	EndCase
	

EndIf


Return lRet

/*


Ŀ
Funo	 Ca280Cart  Autor  Leandro Nogueira	     Data  15/12/10 
Ĵ
Descrio Realiza as validacoes do cartao fidelidade selecionado       
			 na tela de recarga (inclusao de saldo).					   		  
Ĵ
Sintaxe	 Expl1:=Ca280Cart(cExp1)									  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280Cart (cNumCar) 
Local lRet		:= .T.     	//Retorno
Local dDataCar 	:= ""      	//Data de validade do cartao
Local cStatus		:= ""		//Status do carto fidelidade
Local aArea		:= GetArea() //Armazena o alias corrente

Default cNumCar := "" 

//Ŀ
//Codigo de barras do cartao informado deve existir na tabela MBO.
//
DbSelectArea("MBO")
DbSetOrder(1)
If !DbSeek(xFilial("MBO")+cNumCar)	
	lRet := .F.
Else
	dDataCar:= MBO->MBO_DTVAL 
	cStatus := MBO->MBO_ATIVO			
Endif

//Ŀ
//A data de validade do carto escolhido para recarga deve 
//ser igual ou superior a data corrente do sistema;        
//
If lRet .AND. dDataCar < dDataBase	
	lRet := .F.
EndIf          
              

//Ŀ
//O carto escolhido para recarga de saldo estar "Ativo" 
//
If lRet .AND. cStatus == "2"	
	lRet := .F.
Endif

RestArea(aArea)
                   
Return lRet

/*


Ŀ
Funo	 Ca280Movim Autor  Leandro Nogueira	     Data  14/12/10 
Ĵ
Descrio Incluir movimento de cartao fidelidade (MBN)				  
Ĵ
Sintaxe	 Expl1:=Ca280Movim(cExp1,cExp2,nExp3,cExp4,cExp5,			  
			 					cExp6,cExp7)							  
Ĵ
ParametroscNumCar - Numero do cartao fidelidade						  
			 cItem - Numero do item de saldo do cartao fidelidade.(MBP)  
			 nValor - Valor do saldo inserido no carto.				  
			 cDoc - Numero do documento								  
			 cSerie - Serie												  
			 cLoja - Codigo da loja									  
			 cTipo - Tipo do movimento (1-Entrada;2-Saida;Estorno Venda; 
			 		  Estorno Recarga) 									  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280Movim (cNumCar,	cItem,	nValor,	cDoc,;
						cSerie,	cLoja,	cTipo) 
					
Local aArea	  	:= GetArea()					// Armazena area atual (alias, order e recno)
Local lRet 		:= .T.                         	//Retorno
Local cSeq		:= Space(TamSx3("MBN_SEQ")[1])	//Item 
Local nSaldoFut := 0							//Novo valor do saldo aps o estorno 	

Default cNumCar	:= ""
Default cItem	:= ""
Default	nValor	:= 0 
Default	cDoc	:= ""
Default	cSerie	:= ""
Default	cLoja	:= ""
Default	cTipo	:= ""

Do Case
//Ŀ
//Tipo do movimento do cartao fidelidade  
//
Case cTipo == "3"                                                        
	//Ŀ
	//3 - ESTORNO DE VENDA PAGA COM CARTAO FIDELIDADE,                      
	//devolve o valor para o registro de saldo correspondente na tabela MBP.
	//

	//
	//Atualizar tipo do registro da tabela MBN   
	//	
	RecLock("MBN",.F.)	
	REPLACE MBN_TIPMOT 	WITH cTipo 		
	MBN->(MsUnlock())
	MBN->(dbCloseArea())
	
	DbSelectArea("MBP")	
	DbSetOrder(3)	
	If DbSeek (xFilial("MBP")+cNumCar+cItem)
		
		//Somar ao valor do saldo atual o valor do movimento estornado.
		nSaldoFut:= MBP->MBP_SALDO + nValor		
		
		RecLock("MBP",.F.)		
		REPLACE MBP_SALDO  WITH nSaldoFut		
		MBP->(MsUnlock())
	EndIf	
	MBP->(dbCloseArea())

Case cTipo == "4"
	//Ŀ
	//4 - EXCLUSAO DE RECARGA DE CARTAO FIDELIDADE, 
	//Exclui o registro de movimento (MBN) e de saldo (MBP) correspondentes 
	//
	
	DbSelectArea("MBN")	
	DbSetOrder(2)
	
	If DbSeek (xFilial("MBN")+cNumCar+cItem)		
		While MBN->(!Eof()) .AND. MBN->MBN_NUMCAR == cNumCar .AND. MBN->MBN_ITSALD == cItem
			RecLock( "MBN", .F. )
			MBN->(DbDelete())
			MBN->(MsUnLock())						
			MBN->(DbSkip())		
	
			DbSelectArea("MBP")	
			DbSetOrder(3)
			If DbSeek (xFilial("MBP")+cNumCar+cItem)
				While MBP->(!Eof()) .AND. MBP->MBP_NUMCAR == cNumCar .AND. MBP->MBP_ITEM == cItem			
					RecLock( "MBP", .F. )
					MBP->(DbDelete())
					MBP->(MsUnLock())						
					MBP->(DbSkip())		
				End
			Endif						
		End
	EndIf

OtherWise 

	//
	//Obter proximo sequencial do movimento   
	//
	DbSelectArea("MBN")
	DbSetOrder(1)
	
	If DbSeek(xFilial("MBN")+cNumCar)
		While AllTrim(MBN->MBN_NUMCAR) == AllTrim(cNumCar)
			cSeq := MBN->MBN_SEQ		                                        
			MBN->(dbSkip())		
		End
	EndIf                     
	MBN->(dbCloseArea())
	cSeq := SomaIt( cSeq )
		
	//
	//Gravar novo registro de saldo na tabela MBN                                                        "
	//
	DbSelectArea("MBN")
	RecLock("MBN",.T.)
	
	REPLACE MBN_FILIAL  WITH xFilial("MBN")
	REPLACE MBN_NUMCAR  WITH cNumCar
	REPLACE MBN_ITSALD  WITH cItem
	REPLACE MBN_DATA  	WITH dDatabase
	REPLACE MBN_DOC 	WITH cDoc
	REPLACE MBN_LOJA	WITH cLoja
	SerieNfId("MBN",1,"MBN_SERIE",dDataBase,LjEspecieNF(),cSerie)
	REPLACE MBN_TIPMOT 	WITH cTipo 
	REPLACE MBN_SEQ 	WITH cSeq
	REPLACE MBN_VALOR 	WITH nValor
	
	MBN->(MsUnlock())
	MBN->(dbCloseArea())
					
EndCase

RestArea (aArea) //Restaura area anterior

Return lRet

/*


Ŀ
Funo	 Ca280ISld  Autor  Leandro Nogueira	     Data  13/12/10 
Ĵ
Descrio Incluir registro de saldo de cartao fidelidade (MBP)		  
Ĵ
Sintaxe	 Expl1:=Ca280ISld(cExp1,dExp2,nExp3,cExp4,	    			  
			 					cExp5,cExp6)		 					  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
			 dExp2 - Data de validade do saldo							  
			 nExp3 - Valor do saldo inserido no carto.				  
			 cExp4 - Numero do documento								  
			 cExp5 - Serie												  
			 cExp6 - Codigo da loja									  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/


Function Ca280ISld (cNumCar,	dDtValid,	nValor,	cDoc,;
					cSerie,		cLoja) 

Local aArea 		:= GetArea() 							// Armazena o alias corrente
Local lRet			:= .F.                             	// Retorno
Local cItem		:= Space(TamSx3("MBP_ITEM")[1])	// Item 

Default cNumCar	:= ""
Default dDtValid:= ""
Default	nValor	:= 0  
Default	cDoc	:= ""
Default	cSerie	:= ""
Default	cLoja	:= ""


//
//Obter proximo sequencial de item de sado
//
DbSelectArea("MBP")
DbOrderNickname("MBPIND3")

If DbSeek(xFilial("MBP")+cNumCar)
	While MBP->MBP_NUMCAR == cNumCar
		cItem := MBP->MBP_ITEM		                                        
		MBP->(dbSkip())		
	End	
EndIf                     

cItem := SomaIt( cItem )
	
//
//Gravar novo registro de saldo na tabela MBP
//
RecLock("MBP",.T.)

REPLACE MBP_FILIAL  WITH xFilial("MBP")
REPLACE MBP_NUMCAR  WITH cNumCar
REPLACE MBP_ITEM  	WITH cItem
REPLACE MBP_DATA  	WITH dDatabase
REPLACE MBP_DTVAL  	WITH dDtValid
REPLACE MBP_VALOR  	WITH nValor 
REPLACE MBP_SALDO  	WITH nValor
   
MBP->(MsUnlock())

lRet:= Ca280Movim (cNumCar,	cItem,	nValor,cDoc,;
					cSerie,	cLoja,	"1")

RestArea(aArea)

Return lRet

/*


Ŀ
Funo	 Ca280CSld  Autor  Leandro Nogueira	     Data  20/12/10 
Ĵ
Descrio Consulta se o cartao fidelidade tem saldo suficiente para    
			 ser usado como forma de pagto da venda					   		  
Ĵ
Sintaxe	 Expl1:=Ca280CSld(cExp1,nExp2)								  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
			 nExp2 - Valor pago com cartao fidelidade 					  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280CSld (cNumCar,nValPagto,nSaldo)
Local lRet 		:= .T.						// Retorno
Local nX		:=0                        	// Contador
Local cMoeda	:= SuperGetMv("MV_SIMB1")	// Simbolo da moeda principal

Default cNumCar 	:= ""
Default nValPagto 	:= 0 
Default nSaldo 		:= 0 					// Saldo disponivel do Cartao Fidelidade

nSaldo :=  Ca280Calc(cNumCar)

If nSaldo < nValPagto 
	lRet := .F.
Endif 

Return lRet 

/*


Ŀ
Funo	 Ca280ASld  Autor  Leandro Nogueira	     Data  21/12/10 
Ĵ
Descrio Atualiza o saldo do cartao fidelidade (abatimento) apos o    
			 o mesmo ter sido utilizado como forma de pagamento na venda. 		  
Ĵ
Sintaxe	 Expl1:=Ca280ASld(cExp1,nExp2,cExp3,cExp4,cExp5)			  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
			 nExp2 - Valor que sera abatido do saldo					  
			 cExp3 - Numro do documento								  
			 cExp4 - Serie												  
			 cExp5 - Codigo da loja									  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280ASld (cNumCar,nSaldoAbat,	cDoc,cSerie,;
					cLoja)                    
					
Local nSaldoAnt		:= 0 			//Saldo anterior da recarga 
Local nSaltoFut 	:= 0			//Saldo Futuro
Local nSaldoRest 	:= nSaldoAbat	//Saldo restante a ser abatido do proximo registro de saldo
Local nValorMov     := 0			//Valor do movimento que ser gravado na tabela MBN
Local aSaldosFid	:=	{}			//Array que armazenara os saldos vlidos para abatimento do cartao fidelidade informado
Local n				:= 0			//Contador    
Local lRet			:= .T.         	//Retorno

Default cNumCar		:= ""
Default nSaldoAbat  := 0
Default cDoc        := "" 
Default cSerie     	:= "" 
Default cLoja		:= "" 

//Ŀ
//Estrutura do array aSaldosFid 
//                              
//1- Numero do cartao fidelidade
//2- Numero do item de saldo    
//3- Valor do saldo             
//
DbSelectArea("MBP")
DbOrderNickname("MBPIND2")

If DbSeek(xFilial("MBP")+cNumCar)
	While !MBP->(Eof()) .AND. AllTrim(MBP->MBP_NUMCAR) == AllTrim(cNumCar) .AND. MBP->MBP_DTVAL >= dDataBase		
		If  (MBP->MBP_SALDO > 0 , Aadd(aSaldosFid,{MBP->MBP_NUMCAR,MBP->MBP_ITEM,MBP->MBP_SALDO}),) 
		MBP->(DbSkip())	                           
	End                
EndIf
MBP->(dbCloseArea())

For n:= 1 to Len(aSaldosFid)
	If nSaldoRest > 0
		nSaldoAnt:= aSaldosFid [n][3]			
		nSaltoFut:= aSaldosFid [n][3]
	 
		If nSaldoAnt < nSaldoRest
			nSaldoRest 	:= nSaldoAbat - nSaldoAnt
			nSaltoFut	:= nSaldoAnt -  nSaldoAbat + nSaldoRest
			nValorMov	:= nSaldoAbat - nSaldoRest		
		Else
			//Ŀ
			//Saldo Futuro = Saldo Anterior - Saldo Abatido
			//			
			nSaltoFut 	:= nSaldoAnt -  nSaldoRest
			nValorMov	:= nSaldoRest
			nSaldoRest	:= 0 
		Endif 		
		//Ŀ
		//Atualizar saldo na tabela MBP
		//	
		DbSelectArea("MBP")
		DbSetOrder(3)
		If DbSeek(xFilial("MBP")+aSaldosFid [n][1]+aSaldosFid [n][2])
			RecLock("MBP",.F.)
			REPLACE MBP_SALDO WITH nSaltoFut			
			MsUnlock()
			MBP->(dbCloseArea())					
			
			//Inserir movimento na tabela MBN
			Ca280Movim(cNumCar,	aSaldosFid [n][2],nValorMov,cDoc,;
						cSerie,	cLoja,	"2")
		EndIf
	EndIf 	
		
Next n	
	
lRet:= .T.
			
Return lRet

/*


Ŀ
Funo	 Ca280ESld  Autor  Leandro Nogueira	     Data  27/12/10 
Ĵ
Descrio Estorna o movimento e atualiza o saldo do cartao fidelidade  
			 quando a venda for cancelada.								   		  
Ĵ
Sintaxe	 Expl1:=Ca280ESld(cExp1,cExp2,cExp3,cExp4,cExp5)			  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
			 cExp2 - Numero do documento								  
			 cExp3 - Serie												  
			 cExp4 - Loja												  
			 cExp5 - Tipo do movimento									  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280ESld (cNumCar,cDoc,cSerie,cLoja,;
					cTipo)                    
					
Local aArea	  		:= GetArea()	//Armazena area atual (alias, order e recno)
Local lRet			:= .F.         	//Retorno
Local nRecNo		:= 	0			//Numero do RECNO anterior

Default cNumCar		:= ""
Default cDoc		:= ""
Default cSerie		:= ""
Default cLoja		:= ""
Default cTipo		:= ""

DbSelectArea("MBN")
DbSetOrder(3)

If DbSeek (xFilial("MBN")+cNumCar+cDoc+cSerie)
	While (MBN->MBN_NUMCAR == cNumCar .AND. MBN->MBN_DOC == cDoc .AND. MBN->MBN_SERIE == cSerie)
		nRecNo:= MBN->(recno())
		If MBN->MBN_TIPMOT <> "3" .AND. MBN->MBN_TIPMOT <> "4"		  
	    	lRet:= Ca280Movim(cNumCar,	MBN->MBN_ITSALD	,	MBN->MBN_VALOR,		cDoc,;
	    						cSerie,	cLoja,				cTipo)					   		    
	  	EndIf
	  	MBN->(DbGoto(nRecNo))
	    MBN->(DbSkip())
    End
EndIf

RestArea(aArea)
Return lRet



/*


Ŀ
Funo	 Ca280Calc  Autor  Leandro Nogueira	     Data  20/12/10 
Ĵ
Descrio Calcula o saldo disponivel do cartao fidelidade 			   
Ĵ
Sintaxe	 Expl1:=Ca280Calc(cExp1)									  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280Calc(cNumCar)
Local aArea 	:= GetArea()	//Armazena o alias corrente 
Local nSaldo	:= 0       	//Saldo disponivel para o cartao

Default cNumCar	:= ""      //Numero do Cartao

DbSelectArea("MBP")
DbOrderNickname("MBPIND2")


//Ŀ
//Consultar saldo do cartao de fidelidade na tabela MBP,
//considerando que a data de validade do saldo deve ser 
//maior ou igual a data corrente do sistema.            
//A soma do valor de saldo dos registros encontrados    
//sera o valor de saldo disponivel para utilizacao      
//como forma de pagamento da venda.                     
//                                                      
//

If DbSeek(xFilial("MBP")+cNumCar)
	While !MBP->(Eof()) .AND. AllTrim(MBP->MBP_NUMCAR) == AllTrim(cNumCar)		
		If MBP->MBP_DTVAL >= dDataBase
			nSaldo+=MBP->MBP_SALDO			
		EndIf
		MBP->(DbSkip())			   		
	End
Endif

RestArea(aArea)

Return nSaldo


/*


Ŀ
Funo	 Ca280CkWs  Autor  Leandro Nogueira	     Data  10/01/11 
Ĵ
Descrio Verifica disponibilidade do WebService LJCARFID 			   
Ĵ
Sintaxe	 Expl1:=Ca280CkWs()										  
Ĵ
Parametros															  
Ĵ
Retorno   lExp1 - lRet												  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function Ca280CkWs()
Static lRet := .F. 	//Retorno
Static oSvc			//Objeto do WebService

oSvc      := WSLjCarFid():New()
iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oSvc),Nil) //Monta o Header de Autenticao do Web Service
oSvc:_URL := "http://"+Alltrim(LJGetStation("WSSRV"))+"/LjCarFid.apw"

If oSvc:Conecta("")
	lRet := .T.
Endif

oSvc := NIL

Return lRet


/*


Ŀ
Funo	  CA280COM  Autor  Ricardo Lovrenovic     Data  25/02/11 
Ĵ
Descrio Verifica dados SA1 e Insere Cod. Cart. Fidelidade no Cliente 
Ĵ
Sintaxe	 Expl1 := CA280COM (oModel)								  
Ĵ
Retorno   True 														  
Ĵ
Uso		 CRDA280COM									              
ٱ

*/
Function CA280COM (oModel)
Local nOperation 	:= 0 // Numero da Operao.

Default oModel := Nil


If ValType(oModel) <> "U"
	nOperation 	:= oModel:GetOperation() 
EndIf

//Ŀ
//Realiza Gravao na Tabela SA1, pegando o cdigo do       
//Cliente Digitado na Tela de Incluso de Cartes Fidelidade
//
If FWFormCommit( oModel ) 	
	DbSelectArea("SA1")
	DbSetOrder(1)
	If DbSeek(xFilial("SA1") + oModel:GetValue( 'MBOMASTER', 'MBO_CODCLI'))
		RecLock("SA1",.F.)
		Replace A1_CODFID with oModel:GetValue( 'MBOMASTER', 'MBO_CODBAR')
    	SA1->(MsUnlock())
    EndIf
EndIf

Return (.T.)

/*


Ŀ
Funo	 CA280CONCL Autor  Ricardo Lovrenovic     Data  03/03/11 
Ĵ
Descrio Consulta o Numero do Cartao Fidelidade do Cliente			   
Ĵ
Sintaxe	 Expl1:=CA280CONCL(cExp1,cExp2)    						  
Ĵ
ParametroscExp1 - Numero do cartao fidelidade						  
			 cExp2	- Codigo da Loja				  					  
Ĵ
Retorno   cExp3 - Codigo Fidelidade 								  
Ĵ
Uso		 LOJA701/FRTA271										      
ٱ

*/
Function CA280CONCL(cCodCli,cLoja)

Local aArea      	:= GetArea() // Grava o estado das Tabelas
Local cCarFid		:= ""        // Codigo Cartao Fidelidade
Local cStatus       := ""        // Status Ativo ou Inativo do Cartao
Local dDataCar      := ""        // Data de Validade do Cartao
Local aCliCarFid    := {}        // Array com infos do Cartao

Default cCodCli	:= ""
Default cLoja   := ""

DbSelectArea("SA1")
DbSetOrder(1)
If DbSeek(xFilial("SA1")+cCodCli+cLoja)
	cCarFid := SA1->A1_CODFID
	If !Empty(cCarFid)     
		DbSelectArea("MBO")
		DbSetOrder(1)
		If DbSeek(xFilial("MBO")+cCarFid)	
			dDataCar:= MBO->MBO_DTVAL 
			cStatus := MBO->MBO_ATIVO			
		Endif
		//Ŀ
		//Adiciona informacoes pesquisadas sobre o Cartao 
		//na Tabela MBO de cadastro dos Cartoes fidelidade
		//
		AAdd(aCliCarFid, {cCarFid,dDataCar,cStatus} )
	EndIf                                                                        
	
Endif

RestArea( aArea )	

Return aCliCarFid

/*


ͻ
Programa  CA280GetAr Autor 	Vendas & Crm      Data   10/03/11   
͹
Desc.     Transporta o valor da variavel aCarFid.				      
͹
Parametros Nil                                                        
͹
Uso             	                                                  
ͼ


*/
Function CA280GetAr()
Return aCarFid


/*


ͻ
Programa  | CA280Conso  Autor 	  Ricardo.Bueno     Data   14/03/2011 
͹
Desc.      Exibe tela dos Cartoes selecionados para consolidacao       	
͹
Parametros aCarts: Array com Cartoes a serem exibidos para Consolidar   
           em um novo Cartao e excluir os antigos    			        
͹
Retorno    lRet		   									              	
͹
Uso        CRDA280                                                     	


*/
Function CA280CONSO()

Local aCarts    := {}                                                       // Cartoes
Local aArea		:= GetArea()												// Array de Retorno 
Local cCartFid	:= ""                                                      // Cdigo Carto Fidelidade
Local cCliente	:= ""                                                      // Codigo do cliente
Local cLoja		:= ""                                                      // Codigo da Loja
Local cFilTab   := ""                                                      // Filial
Local dValiCart := ""                                                      // Validade do Carto
Local cHistCons := ""                                                      // Historico da Consolidao do Cartao
Local nValor	:=  0                                                      // Valores
Local nVSaldo	:=  0                                                      // Saldos
Local nX		:=  0                                                      // Contador
Local nVlrSaldo :=  0                                                      // Valor Saldo Manipulado
Local nOper     :=  0                                                      // Retorno de Numero da Operaao
Local lOkNo		:= .F.                                                     // Ok/No
Local lRet		:= .F.                                                     // Retorno
Local lIncSaNeg := .T.                                                     // Incluir Saldo Negativo
Local cPicture 	:= PesqPict("SLR","LR_VLRITEM") 							// Picture

//Ŀ
//Realiza Varredura na Tabela MBO, para analisar todos os
//Cartes Fidelidade que esto Cadastrados e apenas      
//adicionar no Array os que possuem Saldo, positivo.     
//
DbSelectArea("MBO")
While !Eof("MBO")
	cFilTab   := MBO->MBO_FILIAL
	cNumCar   := MBO->MBO_CODBAR
	nVSaldo   := TRANSFORM(CA280CALC(cNumCar), cPicture)
	cCliente  := MBO->MBO_CODCLI
	cLoja	  := MBO->MBO_LOJA
	dValiCart := MBO->MBO_DTVAL
	//Ŀ
	//Tabelas [MBP] e [MBO] inserindo as informacoes no Array aCarts dos                  
	//Cartoes Fidelidade para exibir e criar opcoes de Selecionar e 					   
	//consolidar os Cartoes.															   
	//
	IF	!EMPTY(nVSaldo) 
		AAdd(aCarts, {lOkNo,cNumCar,cCliente,cLoja,dValiCart,nVSaldo,cFilTab} )
	EndIf 
	//
	MBO->(DbSelectArea("MBO"))
	MBO->(DbSkip())
End                                                                        

//Ŀ
// CA280TELA													    
// Funo que monta o GRID a partir do array passado e exibe       
// os cartoes cadastrados na tabela MBO para que sejam selecionados
//
aRetorno := CA280TELA(aCarts)

If (Len(aRetorno) > 1) 
	For nX := 1 To Len(aRetorno)
		nVlrSaldo := nVlrSaldo +Val(StrTran(StrTran(aRetorno[nX][1][6],".",""),",","."))
	Next nX
    //Ŀ
	//Realiza a abertura da Tela de Inclusao de Cartao Fidelidade
	//
    nOper := FWExecView(STR0033,'CRDA280', MODEL_OPERATION_INSERT, , { || .T.},,30)

    If (nOper == 0)
 		//Ŀ
		//Inclusao do Saldo total dos Cartoes selecionados no Cartao
		//que acabou de ser cadastrado e registrado na [MBO]        
		//
		DbSelectArea("MBO")
		cHistCons := (STR0034+MBO_CODBAR)
		lIncSaldo := Ca280Exec ( "Ca280ISld",MBO_CODBAR, MBO->MBO_DTVAL,nVlrSaldo,	;
										 Nil, 		Nil,   MBO->MBO_LOJA,     Nil ,	;	
				   	    				 Nil, 		Nil)
        If lIncSaldo
			For nX := 1 To Len(aRetorno)
				cCartDel := aRetorno[nX][1][2]
				DbSelectArea("MBO")
				DbSetOrder(1)
				If DbSeek(xFilial("MBO")+cCartDel)	
  					nVSaldo   := CA280CALC(MBO_CODBAR)
                   	lIncSaNeg := Ca280Exec ( "Ca280ISld",MBO_CODBAR, MBO->MBO_DTVAL,	nVSaldo*(-1),	;
										Nil, 		Nil, MBO->MBO_LOJA,  Nil ,	;	
				   	  					Nil, 		Nil)
				    If lIncSaNeg
				    	RecLock("MBO",.F.)
						Replace MBO_ATIVO  WITH "2"
    					Replace MBO_COHIST WITH cHistCons
    					MBO->(MsUnlock())
				        lRet := .T.
				  	EndIf
				EndIf
			Next nX
        EndIf
    Else
    	MsgAlert(STR0041) // Novo Cartao Nao Cadastrado, Reiniciar o Processo de Consolidacao! //verificar! 															
    EndIf
Else
	MsgAlert(STR0040) // Selecionar mais de um Cartao para realizar a Consolidacao! //verificar! 															
EndIf    	

If lRet
	MsgAlert(STR0039) // Processo de Consolidacao Concluido com Sucesso! //verificar! 															
EndIf

RestArea (aArea)
Return lRet

/*


ͻ
Programa  | CA280TRFIL  Autor 	  Ricardo.Bueno     Data   31/03/2011 
͹
Desc.      Funcao troca filial de Cartao Selecionado                	
͹
Parametros aCarts: Array com Cartoes a serem exibidos para Consolidar   
           em um novo Cartao e excluir os antigos    			        
͹
Retorno    Nil		  									              	
͹
Uso        CRDA280                                                     	


*/

Function CA280TRFIL()

Local aArea	  	:= GetArea()					// Armazena area atual (alias, order e recno)
Local oFil 	    := Nil          				// Objeto
Local oWiCndEtq	:= Nil							// Objeto para mostrar codigo da filial.
Local aRetCarts := {}							// Array de Retorno dos Cartoes Selecionados
Local aCarts    := {} 							// Array dos Cartoes Selecionados no Grid
Local cFil   	:= Space(FWGETTAMFILIAL)		// Filial que foi selecionada na tela.
Local cNumCar	:= ""           				// Numero do Carto
Local cCliente	:= ""           				// Cliente
Local cLoja		:= ""           				// Loja
Local dValiCart	:= ""           				// Validade do Carto
Local cFilTab	:= ""           				// Filial
Local nXt 		:= 0            				// Contador
Local nVSaldo	:= 0            				// Valor do Saldo
Local lOkNo		:= .F.          				// Ok/No
Local cPicture 	:= PesqPict("SLR","LR_VLRITEM")// Picture 

//Ŀ
//Verifica.                                             
//Caso CAMODOEXCL for True, significa que as tabelas MBO
//MBP e MBN esto em modo exclusivo e podem receber o   
//registro de filiais do sistema.                       
//
If CaModoExcl()
	DbSelectArea("MBO")
	DbSetOrder(1)
	While !Eof("MBO")
		//Ŀ
		//Realiza Varredura na Tabela MBO, para analisar todos os
		//Cartes Fidelidade que esto Cadastrados               
		//
		cFilTab   := MBO->MBO_FILIAL
		cNumCar   := MBO->MBO_CODBAR
 		nVSaldo   := TRANSFORM(CA280CALC(cNumCar), cPicture)
		cCliente  := MBO->MBO_CODCLI
		cLoja	  := MBO->MBO_LOJA
		dValiCart := MBO->MBO_DTVAL
		//Ŀ
		//Tabelas [MBP] e [MBO] inserindo as informacoes no Array aCarts dos                  
		//Cartoes Fidelidade para exibir e criar opcoes de Selecionar e 					   
		//consolidar os Cartoes.															   
		//
		AAdd(aCarts, {lOkNo,cNumCar,cCliente,cLoja,dValiCart,nVSaldo,cFilTab} )
		MBO->(DbSelectArea("MBO"))
		MBO->(DbSkip())
	End
	
	If Len(aCarts) > 0
		//
		//Exibe.                                                  
		//Tela com os Cartes Fidelidade Cadastrados na Tabela MBO
		//so exibidos para que o usurio Selecione os que sero  
		//manipulados                                             
		//
		aRetCarts := CA280TELA(aCarts)
	EndIf
	
	If Len(aRetCarts) > 0
		//Ŀ
		// Monta tela de consulta das Filiais 
		//
		DEFINE MSDIALOG oWindEtq FROM 57,160 TO 200,200 TITLE STR0036 PIXEL OF GetWndDefault()
		
		@ 04, 05 TO 28, 56 LABEL STR0029 OF oWindEtq PIXEL
		@ 13, 10 MSGET oFil VAR cFil F3 ("XM0") OF oWindEtq PIXEL Valid ExistCpo("SM0",cEmpAnt+cFil)
		DEFINE SBUTTON FROM 050,005 TYPE 1 ACTION (oWindEtq:End()) ENABLE OF oWindEtq 						//"Ok"
		DEFINE SBUTTON FROM 050,035 TYPE 2 ACTION (oWiCndEtq:End()) ENABLE OF oWindEtq //"Cancelar"
		
		ACTIVATE MSDIALOG oWindEtq CENTER
		
		//Ŀ
		//Caso o usurio tenha escolhido uma Filial, continua
		//a rotina de mudana de Filiais.                    
		//
		IF !EMPTY(cFil)
			If Len(aRetCarts) > 0
				For nXt := 1 To Len(aRetCarts)
					//
					//Tabela de Cadastro de Cartoes.
					//
					DbSelectArea("MBO")
					DbSetOrder(1)
					If DbSeek(xFilial("MBO")+aRetCarts[nXt][1][2])
						RecLock("MBO",.F.)
						Replace MBO_FILIAL  WITH cFil
						MBO->(MsUnlock())
						//Ŀ
						//Tabela de Movimentao de Cartoes
						//
						DbSelectArea("MBN")
						DbSetOrder(1)
						If DbSeek(xFilial("MBN")+aRetCarts[nXt][1][2])
							While MBN->MBN_NUMCAR == (aRetCarts[nXt][1][2])
								RecLock("MBN",.F.)
								Replace MBN_FILIAL  WITH cFil
								MBN->(MsUnlock())
								MBN->(dbSkip())		
							End
						EndIf                     
						//Ŀ
						//Tabela de Saldo de Cartoes		
						//
						DbSelectArea("MBP")
						DbSetOrder(1)
						If DbSeek(xFilial("MBP")+aRetCarts[nXt][1][2])
							While MBP->MBP_NUMCAR == (aRetCarts[nXt][1][2])
								RecLock("MBP",.F.)
								Replace MBP_FILIAL  WITH cFil
								MBP->(MsUnlock())
								MBP->(dbSkip())		
							End
						EndIf                     
					EndIf
				Next nXt
			EndIf
		EndIf
	EndIf
Else
	MsgAlert(STR0037,STR0038) //Funcionalidade somente ativa em modo exclusivo das tabelas MBO, MBP e MBN.															
EndIf

RestArea (aArea)
Return Nil

/*


ͻ
Programa  | CA280RETGR  Autor 	  Ricardo.Bueno     Data   22/03/2011 
͹
Desc.      Separa os itens selecionados como True e retorna Array     	
͹
Parametros aCarts: Array com Cartoes a serem exibidos  			        
͹
Retorno    aCodProd    									              	
͹
Uso        CRDA280                                                     	


*/
Function CA280RETGR(aSelGrid)
Local aCodProd 	:= {}            // Array de produtos do retorno
Local nCont    	:= 1             // Contador
                                                 
Default aSelGrid  := {}			 // Array com dados trazidos para manipular.
                                                                                                    
//Ŀ
//Rotina que verifica os itens do Array                       
//Caso o item seja True, significa que foi selecionado no Grid
//Separa os itens selecionados como True e retorna Array.     
//
For nCont := 1 To Len(aSelGrid)
	If aSelGrid[nCont][1] == .T.
		AADD(aCodProd,{aSelGrid[nCont]})
	EndIf
Next nCont

Return aCodProd

/*


ͻ
Programa  | CA280TELA   Autor 	  Ricardo.Bueno     Data   01/04/2011 
͹
Desc.      Exibe GRID para selecionar os Itens							
͹
Parametros  aArray :=													
			 	cTipo  :=			        								
͹
Retorno    ARRAY    									              	
͹
Uso        CRDA280                                                     	


*/
Function CA280TELA(aArray)

Local oDlg      := Nil 																// Objeto de Tela. 
Local olstResul := Nil																// Objeto de Tela. 
Local obtnInser := Nil 																// Objeto de Tela. 
Local oOK 		:= LoadBitmap(GetResources(),"LBOK") 								// Botoes de selecao 
Local oNO 		:= LoadBitmap(GetResources(),"LBNO") 								// Botoes de selecao
Local aHeaders 	:= {"  ",STR0024,STR0025,STR0026,STR0027,STR0028,STR0029}   		// Labels do cabecalho do grid.
Local aTamHead 	:= {10,52,52,52,52,52,52}                                          // Tamanho das linhas do cabealho.
Local aRetorno  := {}																// Array de retorno com os itens selecionados.

Default aArray	:= {}                                                               // Array enviado com os itens para exibiao na tela.

If Len(aArray)> 0
	DEFINE MSDIALOG oDlg TITLE STR0033 FROM 80,100 TO 500,830 PIXEL
	//Ŀ
	// Cria Grid de Resultado 
	//
	olstResul := TCBrowse():New( 10,20,330,180, ,aHeaders,aTamHead,;
	oDlg,,,,,{||},,,,,,,.F.,,.T.,,.F.,,,)
	olstResul:SetArray(aArray)
	olstResul:bLine := {||{ If(aArray[olstResul:nAt,01],oOk,oNo),aArray[olstResul:nAt,02],aArray[olstResul:nAt,03],(aArray[olstResul:nAt,04]),(aArray[olstResul:nAt,05]),(aArray[olstResul:nAt,06]),(aArray[olstResul:nAt,07])} }
	olstResul:bLDblClick := {|| aArray[olstResul:nAt][1] := !aArray[olstResul:nAt][1],olstResul:DrawSelect(),olstResul:Refresh() }
	obtnInser  := TButton():New( 195,315,STR0035,oDlg,{|| aRetorno := CA280RETGR(aArray), oDlg:End() },; //"OK"
	37,12,,,.F.,.T.,.F.,,.F.,,,.F. )
    
	ACTIVATE MSDIALOG oDlg CENTERED
Else
	MsgAlert(STR0040) // alerta o usuario														
EndIf
obtnInser  := Nil
olstResul  := Nil
oDlg       := Nil

Return aRetorno

/*


ͻ
Programa  | CaModoExcl  Autor 	  Ricardo.Bueno     Data   05/04/2011 
͹
Desc.     	Verifica o Modo Exclusivo das Tabelas para realizar o 		
			 	controle de Cartoes por Filiais						        
͹
Parametros Nil 			        										
͹
Retorno    lRet											              	
͹
Uso        CRDA280                                                     	


*/
Function CaModoExcl()
Local lRet 	:= .F.
Local aArea	:= GetArea()

SX2->(DbSetOrder(1))
If SX2->(DbSeek("MBO"))
   lRet  := ( FWModeAccess("MBO",3) == "E")
EndIf

If lRet .AND. SX2->(DbSeek("MBP")) 
   lRet  := ( FWModeAccess("MBP",3) == "E")
EndIf

If lRet .AND. SX2->(DbSeek("MBN"))
   lRet  := ( FWModeAccess("MBN",3) == "E")
EndIf

RestArea (aArea) 
Return lRet
