#include "totvs.ch"
#Include "FONT.CH"
#Include "COLORS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "LOJC070.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Lojc070  ³ Autor ³ Fernando Machima      ³ Data ³ 13/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consulta do saldo do Caixa                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Lojc070()     											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ BOPS ³  Motivo da Alteracao 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Fernando	M. ³13/04/00³xxxxxx³ Desenvolvimento inicial                  ³±±
±±³Fernando	M. ³19/08/01³xxxxxx³ Impressao                                ³±±
±±³J. Aurelio  ³04/03/02³13856 ³ Tratamento do MV_CRNEG para considerar   ³±±
±±³            ³        ³      ³ Devolucao na impressao(REX - Argentina)  ³±±
±±³Sergio F.   ³14/08/02³001478³ Ajuste na Query                          ³±±
±±³Fernando	   |13/12/06|9.12  |Bops 115315 Alterado a array aRotina  para³±±
±±³        	   ³        |      ³ criação do menu funcional                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJC070()

// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa 	 ³
//³ ----------- Elementos contidos por dimens„o ------------	 ³
//³ 1. Nome a aparecer no cabe‡alho 							 ³
//³ 2. Nome da Rotina associada									 ³
//³ 3. Usado pela rotina										 ³
//³ 4. Tipo de Transa‡„o a ser efetuada							 ³
//³	 1 - Pesquisa e Posiciona em uma Cota‡„o					 ³
//³	 4 - Analisa e/ou encerra uma cota‡„o						 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aRotina := MenuDef()

PRIVATE lInverte := .F.
PRIVATE nIndice	:= SE1->(Indexord())

PRIVATE cCadastro := STR0002  //"Consulta Caixa"

dbSelectArea("SE1")
If !lAutomato
	mBrowse( 6, 1,22,75,"SE1",,"!E1_PORTADO")
EndIf
dbSelectArea("SE1")
dbSetOrder(1)	// devolve ordem principal

Return 


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    |MenuDef	³ Autor ³ Fernando Amorim       ³ Data ³13/12/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de definição do aRotina                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina   retorna a array com lista de aRotina             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef() 

Local aRotina:= { 		{STR0001, "Lj070Con", 0 , 4 , , .T.}}  //"Consultar"
							
							
Return(ARotina)



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ ValPort  ³ Autor ³ Fernando Machima      ³ Data ³ 07/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o portador digitado                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := ValPort(ExpC1)       							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Retorna .T.= true ou .F.= false					  ³±±
±±³			 ³ ExpC1 = Portador											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValPort(cPort)
LOCAL lRetorna := .T.

If !Empty(cPort)
	dbSelectArea("SA6")
	If !(MsSeek(xFilial()+cPort))
		Help(" ",1,"E1_PORTADO")
		lRetorna := .F.
	EndIf
	dbSelectArea("SE1")
EndIf
Return lRetorna


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ ValPorAg ³ Autor ³ Fernando Machima      ³ Data ³ 07/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o Portador com agencia        					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := ValPorAg(ExpC1,ExpC2)                			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Retorna .T.= true ou .F.= false					  ³±±
±±³			 ³ ExpC1 = Codigo do portador 								  ³±±
±±³			 ³ ExpC2 = Agencia											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValPorAg(cPort,cAgen)
LOCAL lRetorna := .T.

If !Empty(cPort) .And. !Empty(cAgen)
	dbSelectArea("SA6")
	If !(MsSeek(cFilial+cPort+cAgen))
		Help(" ",1,"E1_PORTADO")
		lRetorna := .F.
	EndIf
	dbSelectArea("SE1")
EndIf
Return lRetorna


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ValPorCont³ Autor ³ Fernando Machima      ³ Data ³ 07/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o Portador com agencia e conta   			          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := ValPorCont(ExpC1,ExpC2,Exp3)             		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Retorna .T.= true ou .F.= false					  ³±±
±±³			 ³ ExpC1 = Codigo do portador 								  ³±±
±±³			 ³ ExpC2 = Agencia											  ³±±
±±³			 ³ ExpC3 = Conta											  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValPorCont(cPort,cAgen,cConta)
LOCAL lRetorna := .T.

If !Empty(cPort) .And. !Empty(cAgen) .And. !Empty(cConta)
	dbSelectArea("SA6")
	If !(MsSeek(cFilial+cPort+cAgen+cConta))
		Help(" ",1,"E1_CONTA")
		lRetorna := .F.
	EndIf
EndIf
dbSelectArea("SE1")
Return lRetorna


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³CheckMoeda³ Autor ³ Fernando Machima      ³ Data ³ 07/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Disponibiliza opcao de todas as moedas ou somente moeda 1   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ CheckMoeda(ExpC1)   										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function CheckMoeda(cAgencia,oCheckM,lMoeda1,nMoedaOrigem)
//Se o portador origem trabalha con a moeda 1, habilita o check box
//Moeda do portador origem
//  Moeda 1
//     - Todas as monedas ativo   -> lMoeda1 := .F.
//     - Todas as monedas inativo -> lMoeda1 := .T.
//  Moeda > 1 -> nao se trabalha com a variavel lMoeda1. Assume o valor inicial .F.
//Obs: Todas as caixas que trabalham con a moneda 1 tem AGENCIA = "."

If !(Empty(cAgencia))
	If AllTrim(cAgencia) == "."
		oCheckM:Enable()
		lCheckM:= .T.
		oCheckM:Refresh()
		IIf (nMoedaOrigem == 1,lMoeda1 := .F.,.T.)
	Else
		oCheckM:Disable()
		lCheckM:= .F.
		oCheckM:Refresh()
		IIf (nMoedaOrigem == 1,lMoeda1 := .T.,.T.)
	End
EndIF

Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³CheckForma³ Autor ³ Fernando Machima      ³ Data ³ 13/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Marca/Desmarca opcao de formas de pagamento                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ CheckForma(ExpC1)   										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function CheckForma(oCheckF,lCheckF,cForma)

If !(Empty(cForma))
	oCheckF:Disable()
	lCheckF:= .F.
Else
	oCheckF:Enable()
	lCheckF:= .T.
EndIf
oCheckF:Refresh()

Return .T.


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ValForma  ³ Autor ³ Fernando Machima      ³ Data ³ 13/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida se a forma de pagto. existe no SX5(tab. 24)         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ValForma(ExpC1,ExpC2)   									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Forma de pagto                                     ³±±
±±³			 ³ ExpC2 = Flag da opcao "Todas as Formas de Pagto." 		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValForma(cPagto)

LOCAL lRet:=.T.

If !(Empty(cPagto))
	If !(SX5->(MsSeek(xFilial()+"24"+cPagto)))
		Help(" ",1,"E1_TIPO")
		lRet:=.F.
	End
EndIf

Return lRet


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ValidFPgto³ Autor ³ Fernando Machima      ³ Data ³ 18/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o preenchimento da forma de pagto                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ValidFPgto(ExpC1,ExpC2)   								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Forma de pagto                                     ³±±
±±³			 ³ ExpC2 = Flag da opcao "Todas as Formas de Pagto." 		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ValidFPgto(cPagto,lCheckF)

LOCAL lRet:=.T.

If !lCheckF .And. Empty(cPagto)
	MsgStop(STR0011)  //"Selecione uma forma de pagto!"
	lRet := .F.
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³SetMoeda  ³ Autor ³ Fernando Machima      ³ Data ³ 13/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recupera a moeda do Caixa                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ SetMoeda(ExpC1,ExpC2,ExpC3,ExpC4)   						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Portador                                           ³±±
±±³			 ³ ExpC2 = Agencia                                  		  ³±±
±±³			 ³ ExpC3 = Conta                                    		  ³±±
±±³			 ³ ExpC4 = Moeda                                       		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function SetMoeda(cPort,cAgencia,cConta,nMoedaOri)

If !Empty(cPort) .And. !Empty(cAgencia) .And. !Empty(cConta)
	dbSelectArea("SA6")
	If (MsSeek(cFilial+cPort+cAgencia+cConta))
		nMoedaOri := Max(SA6->A6_MOEDA,1)
	EndIf
EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj070Con  ³ Autor ³Fernando Machima       ³ Data ³ 13/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consulta de saldo dos Caixas                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj070Con() 	                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Lojc070 	    											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data	³ BOPS ³  Motivo da Alteracao 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Geronimo A. ³18/10/05³086834³ Tratamento de diferenca do tipo de dado  ³±±
±±³            ³        ³      ³ entre EL_MOEDA(Char) e E1_MOEDA(Numerico)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Norbert W.  ³20/07/07³112669³ Tratamento para criar arquivo temporario ³±±
±±³            ³        ³      ³ quando for AS/400.                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj070Con()

Local oDlg												//Objeto para montar a tela
Local nOpca			:= 0 								//Opcao escolhida
Local cForma		:= Space(TamSx3("E1_TIPO")[1])    	//Forma de pagamento
Local aStruTRB		:= {}								//Estrutura do arquivo temporario
Local ni    		:= 1								//Contador		

Local oValTit											//Mostra o valor do titulo 
Local oQuant  											//Mostra a quantidade
LOCAL nSavRec     										//Armazena o numero do registro corrente do SE1
Local nDecs
Local nDecimais											//Numero de casas decimais	
Local aMoeda		:= {}								//Array para armazenar as moedas
Local nX  			:= 1								//Contador
Local oCbx
Local cMoeda
Local oCheckM 
Local oCheckF
Local lCheckF 		:= .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se .T. -> Portador origem trabalha com moeda 1 e esta ativada a opcao "Todas as Moedas".³
//³Neste caso, selecionamos somente os titulos a receber em moeda 1                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lMoeda1		:= .F.
Local nMoedaOrigem  := 1
Local nMoedaTmp 	:= 1
Local oDataMovto
Local nTot
Local nL 			:= 1
Local cIndexKey 	:= ""
Local lQuery 		:= .F.
Local lVersao		:= .F.
Local oTempTable	:= Nil 								//Objeto tabela temporaria

// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

Private cMarca   	:= GetMark()
Private nQuant 		:= 0
Private aTotais 	:= {}  								//Totalizador dos valores por moeda
Private aRegistros	:= {}
Private nOutros   	:= 0
Private nTipos	 	:= 0
Private nSaldo		:= 0
Private aTotTipos	:= {}  								//Totalizador dos valores por tipo de documento(Dinheiro, Cheque, etc.)
Private nMoeda 		:= CriaVar("SE1->E1_MOEDA")
Private dDataMovto	:= dDatabase
Private cPortador	:= CriaVar("SE1->E1_PORTADO")
Private cAgencia	:= CriaVar("SE1->E1_AGEDEP")
Private cConta 		:= CriaVar("SE1->E1_CONTA")
Private lCheckM

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Esse array tem o intuito de "peneirar" o aTotTipos, subtraindo               ³
//³dos valores e ainda nao registrando os movimentos que possuem                ³
//³tipo com o simbolo "-".  REX - Argentina / Jose Aurelio - 12/03/02           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aOtherTipos := {} //Totalizador dos valores por tipo de documento(Dinheiro, Cheque, etc.) - Para Localizacoes

/*
 * Controle de versionamento de recibos de cobrança (Argentina)
 */
If cPaisLoc $ "ARG"
	DbSelectArea("SEL")
	If SEL->(FieldPos("EL_VERSAO")) > 0 .AND. FindFunction("F841VERSA")
		lVersao := .T.
	EndIf
EndIf

nIndice := SE1->(IndexOrd())

nMoeda := 1
nTot   := MoedFin()
If cPaisLoc == "BRA"
	For nX	:=	1	To nTot
		If(!(Empty(GetMv("MV_MOEDA"+AllTrim(Str(nX))))))
			AAdd(aMoeda,GetMv("MV_MOEDA"+AllTrim(Str(nX))))
			AAdd(aTotais,{nX,0.00,{}})
		Else
			nX := nTot+1
		Endif
	Next nX
Else
	For nX	:=	1	To nTot
		If(!(Empty(GetMv("MV_MOEDA"+AllTrim(Str(nX))))))
			AAdd(aMoeda,GetMv("MV_MOEDA"+AllTrim(Str(nX))))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Memoria do Array aTotais...                                        ³
			//³ aTotais[n][1] := Numero da Moeda                                   ³
			//³ aTotais[n][2] := Valor total por Moeda                             ³
			//³ aTotais[n][3] := {Tipo Titulo, Valor } Array detalhado por Titulo  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AAdd(aTotais,{nX,0.00,{}})
		Else
			nX := nTot+1
		Endif
	Next nX
EndIf

dbSelectArea( "SE1" )
nSavRec:=RecNo()

DEFINE MSDIALOG oDlg FROM	15,6 TO 230,470 TITLE STR0002 PIXEL  
@	0,  1 TO  85, 228  OF oDlg PIXEL //"Consulta Caixa"

@ 11, 5 SAY	 STR0004 SIZE 55, 7 OF oDlg PIXEL  //"Caixa"
@  9, 40 MSGET cPortador F3 "SA6"    Valid	ValPort(cPortador)  .And.;
CheckMoeda(cAgencia,@oCheckM,@lMoeda1,nMoedaOrigem);
SIZE 25,10 OF oDlg PIXEL

@ 11, 80 SAY	STR0005	SIZE 26, 7 OF oDlg PIXEL  //"Agencia"
@  9, 110 MSGET cAgencia   Valid	ValPorAg(cPortador,cAgencia) .And.;
CheckMoeda(cAgencia,@oCheckM,@lMoeda1,nMoedaOrigem);
SIZE 25,10 OF oDlg PIXEL

@ 11,150 SAY	STR0006	SIZE 26, 7 OF oDlg PIXEL  //"Conta "
@  9,175 MSGET  cConta	Valid ValPorCont(cPortador,cAgencia,cConta);
SIZE 45,10 OF oDlg PIXEL

@ 30, 5 SAY	 STR0007 	SIZE	65,  7	 OF oDlg PIXEL  //"Moeda de Conversao"
@ 28,60 COMBOBOX oCbx VAR cMoeda ITEMS aMoeda   ON CHANGE (nMoeda:=oCbx:nAt) SIZE 50,50 OF oDlg PIXEL

@ 30 ,130 CHECKBOX oCheckM VAR lCheckM PROMPT STR0008 OF oDlg;  //"Todas as moedas  "
SIZE 60,08 ON CHANGE (lMoeda1:=!lCheckM) PIXEL

@ 45,5 SAY	STR0009	SIZE 75, 7 OF oDlg PIXEL  //"Forma de Pagto."
@ 45,60 MSGET  cForma	F3 "24" Valid ValForma(cForma) .And.;
CheckForma(@oCheckF,@lCheckF,cForma);
SIZE 25,10 OF oDlg PIXEL

@  45, 130 CHECKBOX oCheckF VAR lCheckF PROMPT STR0010 OF oDlg;  //"Todas as formas de pagto."
SIZE 85,08 PIXEL

@ 65,5 SAY	STR0027	SIZE 75, 7 OF oDlg PIXEL  //"Data do movimento   "
@ 65,60  MSGET oDataMovto  VAR dDataMovto SIZE 42, 10 OF oDlg PIXEL RIGHT

DEFINE SBUTTON FROM 90, 90 ;
TYPE 1 ;
ACTION ( nOpca:=1,oDlg:End());
ENABLE OF oDlg

DEFINE SBUTTON FROM 90, 130;
TYPE 2;
ACTION (nOpca:=0,oDlg:End());
ENABLE OF oDlg

If !lAutomato
	ACTIVATE MSDIALOG oDlg CENTERED
Else
	nOpcA := 1
Endif

If nOpcA == 0
	Return(.T.)
ElseIf nOpcA == 1
	If !ValPorCont(cPortador,cAgencia,cConta)
		Return
	EndIf
	
	If Empty(cForma) .And. !lCheckF
		MsgStop(STR0011)  //"Selecione uma forma de pagto!"
		Return
	EndIf
	
	If !ValidFPgto(cForma,lCheckF)
		Return
	EndIf
	
	SetMoeda(cPortador,cAgencia,cConta,@nMoedaOrigem)
	
	nDecimais := MsDecimais(nMoeda)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria‡Æo da estrutura de TRB com base em SE1.                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SE1")
	DbSetOrder(4)
	
	aStruTRB := {}
	AADD(aStruTRB,{"E1_FILIAL" ,"C",TamSX3("E1_FILIAL")[1] ,TamSX3("E1_FILIAL")[2]})
	AADD(aStruTRB,{"E1_PREFIXO","C",TamSX3("E1_PREFIXO")[1],TamSX3("E1_PREFIXO")[2]})
	AADD(aStruTRB,{"E1_NUM"    ,"C",TamSX3("E1_NUM")[1]    ,TamSX3("E1_NUM")[2]})
	AADD(aStruTRB,{"E1_PARCELA","C",TamSX3("E1_PARCELA")[1],TamSX3("E1_PARCELA")[2]})
	AADD(aStruTRB,{"E1_TIPO"   ,"C",TamSX3("E1_TIPO")[1]   ,TamSX3("E1_TIPO")[2]})
	AADD(aStruTRB,{"E1_CLIENTE","C",TamSX3("E1_CLIENTE")[1],TamSX3("E1_CLIENTE")[2]})
	AADD(aStruTRB,{"E1_LOJA"   ,"C",TamSX3("E1_LOJA")[1]   ,TamSX3("E1_LOJA")[2]})
	AADD(aStruTRB,{"E1_NOMCLI" ,"C",TamSX3("E1_NOMCLI")[1] ,TamSX3("E1_NOMCLI")[2]})
	AADD(aStruTRB,{"E1_EMISSAO","D",TamSX3("E1_EMISSAO")[1],TamSX3("E1_EMISSAO")[2]})
	AADD(aStruTRB,{"E1_VENCTO" ,"D",TamSX3("E1_VENCTO")[1] ,TamSX3("E1_VENCTO")[2]})
	AADD(aStruTRB,{"E1_VALOR"  ,"N",TamSX3("E1_VALOR")[1]  ,TamSX3("E1_VALOR")[2]})
	AADD(aStruTRB,{"E1_MOEDA"  ,"N",TamSX3("E1_MOEDA")[1]  ,TamSX3("E1_MOEDA")[2]})
	AADD(aStruTRB,{"E1_VLCRUZ" ,"N",TamSX3("E1_VLCRUZ")[1] ,TamSX3("E1_VLCRUZ")[2]})
	AADD(aStruTRB,{"E1_PORTADO","C",TamSX3("E1_PORTADO")[1],TamSX3("E1_PORTADO")[2]})
	AADD(aStruTRB,{"E1_AGEDEP" ,"C",TamSX3("E1_AGEDEP")[1] ,TamSX3("E1_AGEDEP")[2]})
	AADD(aStruTRB,{"E1_NUMNOTA","C",TamSX3("E1_NUMNOTA")[1],TamSX3("E1_NUMNOTA")[2]})
	AADD(aStruTRB,{"E1_RECIBO" ,"C",TamSX3("E1_RECIBO")[1] ,TamSX3("E1_RECIBO")[2]})
	AADD(aStruTRB,{"E1_BCOCHQ" ,"C",TamSX3("E1_BCOCHQ")[1] ,TamSX3("E1_BCOCHQ")[2]})
	AADD(aStruTRB,{"E1_OK"     ,"C",TamSX3("E1_OK")[1]     ,TamSX3("E1_OK")[2]})
	
	lQuery := .F.
	#IFDEF TOP
		If TcSrvType() != "AS/400"
			lQuery := .T.
			
			cIndexKey := "E1_FILIAL+E1_PORTADO+E1_NOMCLI+E1_PREFIXO+E1_NUM+E1_TIPO"
			//Cria tabela temporaria
			oTempTable := LjCrTmpTbl("TRB", aStruTRB, {cIndexKey})
			
			cQuery := "SELECT "
			cQuery += "E1_FILIAL    AS E1_FILIAL,"
			cQuery += "E1_PREFIXO   AS E1_PREFIXO,"
			cQuery += "E1_NUM       AS E1_NUM,"
			cQuery += "E1_PARCELA   AS E1_PARCELA,"
			cQuery += "E1_TIPO      AS E1_TIPO,"
			cQuery += "E1_CLIENTE   AS E1_CLIENTE,"
			cQuery += "E1_LOJA      AS E1_LOJA,"
			cQuery += "E1_NOMCLI    AS E1_NOMCLI,"
			cQuery += "E1_EMISSAO   AS E1_EMISSAO,"
			cQuery += "E1_VENCTO    AS E1_VENCTO,"
			cQuery += "E1_VALOR     AS E1_VALOR," 
			If lVersao
		 		cQuery += "'00' AS E1_VERSAO,"
			EndIf
			cQuery += "E1_MOEDA     AS E1_MOEDA,"
			cQuery += "E1_VLCRUZ    AS E1_VLCRUZ,"
			cQuery += "E1_PORTADO   AS E1_PORTADO,"
			cQuery += "E1_AGEDEP    AS E1_AGEDEP,"
			cQuery += "E1_NUMNOTA   AS E1_NUMNOTA,"
			cQuery += "E1_RECIBO    AS E1_RECIBO,"
			cQuery += "E1_BCOCHQ    AS E1_BCOCHQ,"
			cQuery += "E1_OK        AS E1_OK"
			cQuery += "  FROM "
			cQuery += RetSqlName("SE1") +" SE1 "
			cQuery += " WHERE E1_FILIAL = '" +xFilial("SE1")+"'"
			cQuery += "  AND E1_EMISSAO = '" + DTOS(dDataMovto) + "'"
			cQuery += "  AND E1_PORTADO = '" +cPortador+"'"
			cQuery += "  AND E1_TIPO NOT IN ('RA ','"+MV_CRNEG+"')"
			cQuery += "  AND E1_SITUACA = '0' "
			If ! Empty(cForma) .And. !lCheckF
				cQuery += "AND  E1_TIPO = '"+cForma+ "' "
			EndIf
			If (lMoeda1) .Or. (nMoedaOrigem > 1)
				cQuery += "   AND E1_MOEDA = "+Str(nMoedaOrigem,1) 	//cQuery += "   AND E1_MOEDA = '"+Str(nMoedaOrigem,1)+"'"
			EndIf
			cQuery += "   AND D_E_L_E_T_ <> '*' "
			
			If cPaisLoc == "ARG"
				cQuery += "UNION "
				cQuery += "SELECT "
				cQuery += "EL_FILIAL    AS E1_FILIAL,"
				cQuery += "EL_PREFIXO   AS E1_PREFIXO,"
				cQuery += "EL_NUMERO    AS E1_NUM,"
				cQuery += "EL_PARCELA   AS E1_PARCELA,"
				cQuery += "EL_TIPO      AS E1_TIPO,"
				cQuery += "EL_CLIENTE   AS E1_CLIENTE,"
				cQuery += "EL_LOJA      AS E1_LOJA,"
				cQuery += "A1_NOME      AS E1_NOMCLI,"
				cQuery += "EL_EMISSAO   AS E1_EMISSAO,"
				cQuery += "EL_DTVCTO    AS E1_VENCTO,"
				cQuery += "EL_VALOR     AS E1_VALOR,"
				If lVersao
					cQuery += "EL_VERSAO AS E1_VERSAO,"
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Converto o campo EL_MOEDA para numerico para nao conflitar com o E1_MOEDA    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If 	UPPER(TcGetDb()) $ "DB2/MYSQL"
					cQuery += "INTEGER(EL_MOEDA) AS E1_MOEDA, "
				ElseIf 	UPPER(TcGetDb()) $ "ORACLE_INFORMIX"
					cQuery += "TO_NUMBER(EL_MOEDA,99) AS E1_MOEDA,"
				ElseIf "MSSQL"$Upper(TcGetDb())
					cQuery += "	Convert( integer, EL_MOEDA )  AS E1_MOEDA,"
				Else
					cQuery += "EL_MOEDA AS E1_MOEDA,"
				Endif
				cQuery += "EL_VLMOED1   AS E1_VLCRUZ,"
				cQuery += "EL_BANCO     AS E1_PORTADO,"
				cQuery += "EL_AGENCIA   AS E1_AGEDEP,"
				cQuery += "'         '  AS E1_NUMNOTA,"
				cQuery += "EL_RECIBO    AS E1_RECIBO,"
				CQuery += "EL_BCOCHQ    AS E1_BCOCHQ,"
				cQuery += "'  '         AS E1_OK"
				cQuery += "  FROM "
				cQuery += RetSqlName("SEL") +" SEL, "
				cQuery += RetSqlName("SA1") +" SA1 "
				cQuery += "  WHERE SEL.EL_FILIAL = '" +xFilial("SEL")+"'"
				cQuery += "  AND SEL.EL_EMISSAO = '" + DTOS(dDataMovto) + "'"
				cQuery += "  AND SEL.EL_BANCO = '" +cPortador+"'"
				cQuery += "  AND SEL.EL_TIPO NOT IN ('RA ','CC ','CH ','PG ')"
				If (lMoeda1) .Or. (nMoedaOrigem > 1)
					cQuery += " AND SEL.EL_MOEDA = '"+Str(nMoedaOrigem,1)+"'"
				EndIf
				cQuery += " AND SEL.EL_CLIENTE = SA1.A1_COD "
				cQuery += " AND SEL.EL_LOJA = SA1.A1_LOJA "
				cQuery += " AND SEL.D_E_L_E_T_ <> '*' "
			EndIf
			cQuery += " ORDER BY "+ SqlOrder(cIndexKey)
			//MemoWrite("C:\LOJC070.SQL",cQuery)
			Processa( {|lEnd| SqlToTrb(cQuery, aStruTRB, "TRB")},,STR0045 )
			
			DbSelectArea("TRB")
			DbGoTop()
			While !Eof()
				If AllTrim(E1_TIPO) == "EF" .AND. Iif(lVersao,(TRB->E1_VERSAO == F841VERSA(TRB->E1_RECIBO)),.T.)
					RecLock("TRB",.F.)
					Replace E1_TIPO	With GetMV("MV_SIMB"+StrZero(TRB->E1_MOEDA,1))
					MsUnLock()
				EndIf
				DbSkip()
			End
		Else
			AADD(aStruTRB,{"RECSE1","N",10,0})
		
			cIndexKey := "E1_FILIAL+E1_PORTADO+E1_NOMCLI+E1_PREFIXO+E1_NUM+E1_TIPO"

			//Cria tabela temporaria
			oTempTable := LjCrTmpTbl("TRB", aStruTRB, {cIndexKey})

			DbSelectArea("SE1")
			DbSetOrder(4)    // Chave (Portador+Nome cliente)
			MsSeek(xFilial("SE1")+cPortador,.T.)
		EndIf
	#ELSE
		
		AADD(aStruTRB,{"RECSE1","N",10,0})
		
		cIndexKey := "E1_FILIAL+E1_PORTADO+E1_NOMCLI+E1_PREFIXO+E1_NUM+E1_TIPO"

		//Cria tabela temporaria
		oTempTable := LjCrTmpTbl("TRB", aStruTRB, {cIndexKey})		
		
		DbSelectArea("SE1")
		DbSetOrder(4)    // Chave (Portador+Nome cliente)
		MsSeek(xFilial("SE1")+cPortador,.T.)
		
	#ENDIF
	
	If !lQuery
		While ! Eof() .and. SE1->E1_FILIAL == xFilial("SE1");
			.and. SE1->E1_PORTADO == cPortador
			
			If SE1->E1_SITUACA != "0"
				DbSkip()
				Loop
			EndIf
			
			If SE1->E1_PORTADO <> cPortador
				DbSkip()
				Loop
			EndIf
			
			If DTOS(SE1->E1_EMISSAO) <> DTOS(dDataMovto)
				DbSkip()
				Loop
			EndIf
			
			If SE1->E1_TIPO $ MV_CRNEG .and. cPaisloc == "BRA"
				dbSkip()
				Loop
			EndIf
			
			If !Empty(cForma) .And. SE1->E1_TIPO != cForma
				DbSkip()
				Loop
			EndIf
			
			If (lMoeda1) .Or. (nMoedaOrigem > 1)
				If SE1->E1_MOEDA != nMoedaOrigem
					dbSkip()
					Loop
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gravar campos de TRB.                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE1->E1_SITUACA == "0"
				DbSelectArea("TRB")
				RecLock("TRB",.T.)
				Replace E1_FILIAL    With xFilial("SE1")
				Replace E1_PREFIXO   With SE1->E1_PREFIXO
				Replace E1_NUM       With SE1->E1_NUM
				Replace E1_PARCELA   With SE1->E1_PARCELA
				Replace E1_TIPO      With SE1->E1_TIPO
				Replace E1_CLIENTE   With SE1->E1_CLIENTE
				Replace E1_LOJA      With SE1->E1_LOJA
				Replace E1_NOMCLI    With SE1->E1_NOMCLI
				Replace E1_EMISSAO   With SE1->E1_EMISSAO
				Replace E1_VENCTO    With SE1->E1_VENCTO
				Replace E1_VALOR     With SE1->E1_VALOR
				Replace E1_MOEDA     With SE1->E1_MOEDA
				Replace E1_VLCRUZ    With SE1->E1_VLCRUZ
				Replace E1_PORTADO   With SE1->E1_PORTADO
				Replace E1_AGEDEP    With SE1->E1_AGEDEP
				Replace E1_NUMNOTA   With SE1->E1_NUMNOTA
				Replace E1_BCOCHQ    With SE1->E1_BCOCHQ
				Replace E1_OK 		 With Space(02)
				MsUnLock()
			EndIf
			DbSelectArea("SE1")
			DbSkip()
		End
		
		If cPaisLoc == "ARG"
			DbSelectArea("SEL")
			DbSetOrder(4)
			MsSeek(xFilial("SEL")+DTOS(dDataMovto),.T.)
			
			While ! Eof() .and. EL_FILIAL == xFilial("SEL");
				.and. DTOS(EL_DTDIGIT) == DTOS(dDataMovto) .AND. Iif(lVersao,(TRB->EL_VERSAO == F841VERSA(TRB->E1_RECIBO)),.T.)
				
				If SEL->EL_BANCO <> cPortador
					DbSkip()
					Loop
				EndIf
				
				If DTOS(SEL->EL_EMISSAO) <> DTOS(dDataMovto)
					DbSkip()
					Loop
				EndIf
				
				SA1->(DbSetOrder(1))
				SA1->(MsSeek(xFilial("SA1")+SEL->EL_CLIENTE+SEL->EL_LOJA))
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Gravar campos de TRB.                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("TRB")
				RecLock("TRB",.T.)
				Replace E1_FILIAL    With xFilial("SE1")
				Replace E1_PREFIXO   With SEL->EL_PREFIXO
				Replace E1_NUM       With SEL->EL_NUMERO
				Replace E1_PARCELA   With SEL->EL_PARCELA
				Replace E1_TIPO      With SEL->EL_TIPO
				Replace E1_CLIENTE   With SEL->EL_CLIENTE
				Replace E1_LOJA      With SEL->EL_LOJA
				Replace E1_NOMCLI    With SA1->A1_NOME
				Replace E1_EMISSAO   With SEL->EL_EMISSAO
				Replace E1_VENCTO    With SEL->EL_DTVCTO
				Replace E1_VALOR     With SEL->EL_VALOR
				Replace E1_MOEDA     With Val(SEL->EL_MOEDA)
				Replace E1_VLCRUZ    With SEL->EL_VLMOED1
				Replace E1_PORTADO   With SEL->EL_BANCO
				Replace E1_AGEDEP    With SEL->EL_AGENCIA
				Replace E1_RECIBO    With SEL->EL_RECIBO
				Replace E1_BCOCHQ    With SEL->EL_BCOCHQ
				Replace E1_OK 		 With Space(02)
				If AllTrim(SEL->EL_TIPO) == "EF"
					Replace E1_TIPO		With GetMV("MV_SIMB"+StrZero(Val(SEL->EL_MOEDA),1))
				Else
					Replace E1_TIPO     With SEL->EL_TIPO
				EndIf
				MsUnLock()
				
				DbSelectArea("SEL")
				DbSkip()
			End
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a existencia de registros no TRB.                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TRB")
	dbGotop()
	If BOF() .And. EOF()
		Help(" ",1,"RECNO")

		If( ValType(oTempTable) == "O")
		  oTempTable:Delete()
		  FreeObj(oTempTable)
		  oTempTable := Nil
		EndIf
		
		dbSelectArea("SE1")
		dbSetOrder(1)
		Return .T.
	EndIf
	
	DbSelectArea("TRB")
	DbGoTop()
	While !TRB->(Eof())
		If !Empty(TRB->E1_NUM)
			Aadd(	aRegistros													,;	//01 Registros
					{	TRB->E1_NUM												,;	//02 Numero do documento 	
						TRB->E1_TIPO											,;	//03 Tipo de documento 
						TRB->E1_EMISSAO											,;	//04 Data de Emissao 
						TRB->E1_VENCTO											,;	//05 Vencimento 
						TRB->E1_VALOR											,;	//06 Valor 
						IIf(cPaisLoc=="ARG",TRB->E1_RECIBO,TRB->E1_NUMNOTA)	,;	//07 Numero da nota 
						TRB->E1_BCOCHQ											,;	//08 Bco cheque
						TRB->E1_NOMCLI											,;	//09 Nome do cliente
						TRB->E1_PORTADO											,;	//10 Portador
						TRB->E1_AGEDEP											,;	//11 AGEDEP
						TRB->E1_MOEDA})                                        		//12 Moeda
			nQuant++
		EndIf
		
		nMoedaTmp := Max(TRB->E1_MOEDA,1)
		nDecs     := MsDecimais(nMoedaTmp)
		nPosTip	 :=	Ascan(aTotTipos,{ |x| Alltrim(x[1])==Alltrim(TRB->E1_TIPO) })
		If nPosTip == 0
			AAdd(	aTotTipos												,;	//Total por tipos
					{	TRB->E1_TIPO										,;	//01 Tipo de documento
						If(TRB->E1_MOEDA>1,TRB->E1_VALOR,TRB->E1_VLCRUZ)	,;	//02 Valor 	
						TRB->E1_MOEDA})                                  		//03 Moeda
			nPosTip := Len(aTotTipos)
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Quando no mesmo tipo de documento existir 2 moedas diferentes separo-as aqui criando novo elemento na array ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aTotTipos[nPosTip][3] <> TRB->E1_MOEDA
				AAdd(aTotTipos,{TRB->E1_TIPO,If(TRB->E1_MOEDA>1,TRB->E1_VALOR,TRB->E1_VLCRUZ),TRB->E1_MOEDA})
			Else
				aTotTipos[nPosTip][2] += If(TRB->E1_MOEDA>1,TRB->E1_VALOR,TRB->E1_VLCRUZ)
				aTotTipos[nPosTip][3] := TRB->E1_MOEDA
			Endif
		EndIf
		If cPaisLoc == "BRA"
			nSaldo += Round(xMoeda(TRB->E1_VALOR,nMoedaTmp,nMoeda,TRB->E1_EMISSAO,nDecimais+1),nDecimais)
			aTotais[nMoedaTmp][2] += Round(TRB->E1_VALOR,nDecs)
		EndIf
		TRB->(DbSkip())
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso o pais nao for Brasil e os tipos possuirem o simbolo "-",³
	//³sera subtraido do total o valor corrente. REX - Argentina     ³
	//³Jose Aurelio - 12/03/02                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nL := 1 To Len(aTotais)
		nTotTipoRec := 0.00
		nTotTipoDev := 0.00
		aDetalhes  := {}
		nMoedaTot  := 0
		For nI := 1 To Len(aTotTipos)
			If aTotTipos[nI][3] == aTotais[nL][1]
				nMoedaTot  := aTotTipos[nI][3]
				If Subs(aTotTipos[nI][1],1,3) $ MV_CRNEG .or. AT("-",aTotTipos[nI][1]) > 0
					nTotTipoDev += aTotTipos[nI][2]
				Else
					nTotTipoRec += aTotTipos[nI][2]
				EndIf
				AADD(aDetalhes,{aTotTipos[nI][1],aTotTipos[nI][2]})
			EndIf
		Next nI
		If nMoedaTot > 0
			If aTotais[nL][1] == nMoedaTot
				aTotais[nL][2] := (nTotTipoRec - nTotTipoDev)
				aTotais[nL][3] := AClone(aDetalhes)
			EndIf
		EndIf
	Next nL
	
	If cPaisLoc == "ARG"
		BuscaRet()
	EndIf
	nTipos	:=	Len(aTotTipos)
	nOutros	:=	0
	If  nTipos > 0
		For nX:=	1 To nTipos
			If nX == 4 .And. nTipos > 4
				nOutros	+=	aTotTipos[nX][2]
			Else
				aTotTipos[nX][1]	:= Substr(Tabela("24",aTotTipos[nX][1],.F.),1,15)
			Endif
		Next
		If nOutros > 0
			aSize(aTotTipos,3)
			aadd(aTotTipos,{STR0038,nOutros})  //Outros
		Endif
	Endif
	dbSelectArea("SE1")
	
	MontaTela("TRB",oValTit,oQuant,nDecimais)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura os indices 													  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If( ValType(oTempTable) == "O")
  oTempTable:Delete()
  FreeObj(oTempTable)
  oTempTable := Nil
EndIf

dbSelectArea("SE1")
dbSetOrder(nIndice)
Return(.T.)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³MontaTela ³ Autor ³ Fernando Machima      ³ Data ³ 13/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Montagem da tela para Consulta de saldo dos Caixas         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Lojc070													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MontaTela(cAlias,oValTit,oQuant,nDecimais)

Local oDlg1, nI, nL, nPosTip
Local cVarQReg	:= "  ", cVarT := "  ", cVarD := "  "
Local oQualReg, oLbxTotal, oLbxDetalhe
Local aLbxTotal := {}
Local aLbxDetalhe := {}

// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

nSaldo := 0.00

For nI := 1 To Len(aTotais)
	AADD(aLbxTotal,{aTotais[nI][1],aTotais[nI][2]})
	nSaldo += aTotais[nI][2]
Next nI
If Len(aLbxTotal) == 0
	AADD(aLbxTotal,{1,0.00})
EndIf
For nI := 1 To Len(aTotais)
	For nL := 1 To Len(aTotais[nI][3])
		If aTotais[nI][3][nL][2] > 0
			nPosTip	 :=	Ascan(aLbxDetalhe,{ |x| Alltrim(x[1])==Alltrim(aTotais[nI][3][nL][1]) })
			If nPosTip == 0
				AADD(aLbxDetalhe,{aTotais[nI][3][nL][1],aTotais[nI][3][nL][2]})
			Else
				aLbxDetalhe[nPosTip][2] += aTotais[nI][3][nL][2]
			Endif
		EndIf
	Next nL
Next nI
If Len(aLbxDetalhe) == 0
	AADD(aLbxDetalhe,{1,0.00})
EndIf

DbSelectArea(cAlias)

DEFINE MSDIALOG  oDlg1 From 9,5 To 32,100 OF oMainWnd

@  0.2,.8  SAY STR0039+Space(65)+STR0040+Space(37)+STR0041
@  1.4,.8  SAY STR0013  //"Valor Total : "
@  1.4, 6  MSGET oSaldo VAR nSaldo Picture PesqPict("SE1","E1_VALOR",,nMoeda) SIZE 60,10 WHEN .F.
@  3  ,.8  SAY STR0014  //"Quantidade : "
@  3  , 6  MSGET oQuant VAR nQuant Picture PesqPictQt("L2_QUANT",7) SIZE 50,10 WHEN .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exibe ListBox de Totais por Moedas... 							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 1.1,15  LISTBOX oLbxTotal VAR cVarT Fields HEADER STR0042,STR0043 SIZE 100,45;
ON DBLCLICK (aLbxDetalhe:=LC070Refresh(aLbxTotal[oLbxTotal:nAT,1],oLbxDetalhe,aLbxDetalhe),oLbxTotal:Refresh()) NOSCROLL  //"Total" ### "Moeda "
oLbxTotal:SetArray(aLbxTotal)
oLbxTotal:bLine := { || {aLbxTotal[oLbxTotal:nAT,1],Transform(aLbxTotal[oLbxTotal:nAT,2],PesqPict("SL1","L1_VLRTOT",,aLbxTotal[oLbxTotal:nAT,1]))}}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exibe ListBox de Detalhes por Tipo de Documento...			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 1.1,28 LISTBOX oLbxDetalhe VAR cVarD Fields HEADER STR0044,STR0043 SIZE 100,45 NOSCROLL //"Tipo" ### "Valor"
oLbxDetalhe:SetArray(aLbxDetalhe)
oLbxDetalhe:bLine := { || {aLbxDetalhe[oLbxDetalhe:nAT,1],Transform(aLbxDetalhe[oLbxDetalhe:nAT,2],PesqPict("SL1","L1_VLRTOT",,2))}}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exibe ListBox dos Titulos...                      			  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 5.2,.8 LISTBOX oQualReg VAR cVarQReg Fields HEADER STR0019,STR0009,STR0020,STR0021,STR0022,;  //"Numero" ### "Forma de Pagto." ### "Data Emissao " ### "Data Vencto. " ### "Valor"
IIf(cPaisLoc=="ARG",STR0046,STR0023),STR0024,STR0025,STR0026,STR0005,STR0018;  //"Num. Cheque" ### "Bco. Cheque" ### "Nome Cliente  " ### "Portador" ### "Agencia" ### "Moeda "
SIZE 367,95 NOSCROLL
oQualReg:SetArray(aRegistros)
oQualReg:bLine := { || {aRegistros[oQualReg:nAT,1],aRegistros[oQualReg:nAT,2],aRegistros[oQualReg:nAT,3],;
aRegistros[oQualReg:nAT,4],;
Transform(aRegistros[oQualReg:nAT,5],PesqPict("SE1","E1_VALOR",,aRegistros[oQualReg:nAT,11])),;
aRegistros[oQualReg:nAT,6],aRegistros[oQualReg:nAT,7],aRegistros[oQualReg:nAT,8],;
aRegistros[oQualReg:nAT,9],aRegistros[oQualReg:nAT,10],aRegistros[oQualReg:nAT,11]}}

DEFINE SBUTTON FROM  7.2 ,335 TYPE 1  ENABLE OF oDlg1 ACTION ( oDlg1:End() )
DEFINE SBUTTON FROM 32.1 ,335 TYPE 6  ENABLE OF oDlg1 ACTION ( LJCR070(),oDlg1:End() )

If !lAutomato
	ACTIVATE MSDIALOG oDlg1
Else
	LJCR070() // chamando a impressão
EndIf
Return


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ LJCR070  ³ Autor ³ Fernando Machima      ³ Data ³ 19/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime a Consulta de saldo dos Caixas                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Lojc070													  			     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function LJCR070()

LOCAL cString := "SE1"
LOCAL cDesc1 := STR0028  //"Este relatorio mostra os titulos a receber de um determinado caixa"
LOCAL cDesc2 := ""
LOCAL cDesc3 := ""
LOCAL wnrel
LOCAL Tamanho:="G"

Private aReturn :={ STR0029, 1,STR0030, 1, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE NomeProg:="LOJC070"
PRIVATE nLastKey := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
li       := 80
m_pag    := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos cabecalhos                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE titulo := STR0031  //"CONSULTA DE CAIXA"
PRIVATE cabec1 := STR0033  //"Numero     Parcela  Tipo  Cod. Cliente  Loja  Nome Cliente                 Dt. Emissao  Dt. Vencto.     Valor     Moeda  Valor Moeda 1"
PRIVATE cabec2 := ""
Private CbTxt  := Space(10)
Private cbCont := 0

If cPaisLoc == "ARG"
	cabec1 += Space(05)+STR0046 //"Nr. Recibo"
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel := "LOJC070"            //Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,"",@titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho)

If nLastKey == 27
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	Return
Endif

RptStatus({|lEnd| Ljr070Cons(@lEnd,wnRel,cString)},titulo)

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LJR070Cons³ Autor ³ Fernando Machima     ³ Data ³ 19/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime a Consulta de saldo dos Caixas                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LJR070Cons                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ LOJR070                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ljr070Cons(lEnd,WnRel,cString)
LOCAL tamanho := "G"
LOCAL nDecs, nDecs1

// Campo verificador se está sendo chamado pelo Robo
Local lAutomato	:= If(Type("lAutomatoX")<>"L",.F.,lAutomatoX)

Private nTotVlrRec := 0
Private nTotVlrDev := 0
Private nSubTotDev := 0
Private nSubTotRec := 0
Private nTotVlCruz := 0 

dbSelectarea("TRB")
dbGoTop()
SetRegua(RecCount())   // Total de elementos da regua

While !Eof()
	
	IF lEnd
		@PROW()+1,001 PSAY STR0032  //"CANCELADO PELO OPERADOR"
		Exit
	End
	
	IF li > 55
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho)
	End
	
	IncRegua()
	
	nDecs   :=   MsDecimais(TRB->E1_MOEDA)
	nDecs1  :=   MsDecimais(1)
	
	li++
	@li, 002 PSAY TRB->E1_NUM
	@li, 019 PSAY TRB->E1_PARCELA
	@li, 027 PSAY TRB->E1_TIPO
	@li, 035 PSAY TRB->E1_CLIENTE
	@li, 046 PSAY TRB->E1_LOJA
	@li, 051 PSAY TRB->E1_NOMCLI
	@li, 084 PSAY TRB->E1_EMISSAO
	@li, 098 PSAY TRB->E1_VENCTO
	@li, 105 PSAY TRB->E1_VALOR              Picture TM(SE1->E1_VALOR,17,nDecs)
	@li, 127 PSAY TRB->E1_MOEDA
	@li, 130 PSAY TRB->E1_VLCRUZ             Picture TM(SE1->E1_VALOR,17,nDecs1)
	
	If cPaisloc == "BRA"
		If Subs(TRB->E1_TIPO,1,3) $ MV_CRNEG
			nTotVlCruz -= TRB->E1_VLCRUZ
		Else
			nTotVlCruz += TRB->E1_VLCRUZ
		EndIf
	Else
		@li, 152 PSAY TRB->E1_RECIBO
		If Subs(TRB->E1_TIPO,1,3) $ MV_CRNEG .or. AT("-",TRB->E1_TIPO) > 0
			nTotVlrRec += TRB->E1_VLCRUZ
			nSubTotRec += TRB->E1_VALOR
		Else
			nTotVlrDev += TRB->E1_VLCRUZ
			nSubTotDev += TRB->E1_VALOR
		EndIf
	Endif
	dbSkip()
End

IF li != 80
	IF li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho)
	EndIF
	Ljr070Tot()
Endif

If !lAutomato
	Set Device To Screen
	Set Filter To
	
	If aReturn[5] == 1
		Set Printer To
		dbCommit()
		Ourspool(wnrel)
	End
	MS_FLUSH()
Endif

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³LJR070TOT ³ Autor ³ Fernando Machima      ³ Data ³ 19/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime os totais da Consulta de saldo dos Caixas          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LOJC070                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ljr070Tot()
Local nI, nC, nY
LOCAL nTotMoeda := Len(aTotais)
LOCAL cMoeda    := ""
LOCAL nDecs     := MsDecimais(nMoeda)
LOCAL nPosRet   := 0
LOCAL tamanho := "G"

If cPaisLoc == "BRA"
	nPosRet    :=  Ascan(aTotTipos,{ |x| Alltrim(x[1])=="RET" })
	li := li + 3
	@li,002 PSAY STR0034 //"T O T A L   G E R A L ----> "
	@li,132 PSAY nTotVlCruz + Iif(nPosRet > 0, xMoeda(aTotTipos[nPosRet][2],nMoeda,1,dDataMovto), 0)    Picture TM(SE1->E1_VALOR,17,nDecs)
	li := li + 2
	If nTipos > 0  //Imprime os totais por tipo de titulo
		@li,002 PSAY STR0035+Str(nMoeda,1)+" : " //"TOTAL POR TIPO DE TITULO - MOEDA "
		li++
		@li,010 PSAY aTotTipos[1][1]
		@li,025 PSAY aTotTipos[1][2] Picture TM(SE1->E1_VALOR,17,nDecs)
		If nTipos > 1
			li++
			@li,010 PSAY aTotTipos[2][1]
			@li,025 PSAY aTotTipos[2][2] Picture TM(SE1->E1_VALOR,17,nDecs)
			If nTipos > 2
				li++
				@li,010 PSAY aTotTipos[3][1]
				@li,025 PSAY aTotTipos[3][2] Picture TM(SE1->E1_VALOR,17,nDecs)
				If nTipos > 3
					li++
					@li,010 PSAY aTotTipos[4][1]
					@li,025 PSAY aTotTipos[4][2] Picture TM(SE1->E1_VALOR,17,nDecs)
				Endif
			Endif
		Endif
	Endif
	li := li + 2
	If nTotMoeda > 0  //Imprime os totais por moeda
		@li,002 PSAY STR0036 //"TOTAL POR MOEDA : "
		For nY := 1 to nTotMoeda
			If aTotais[nY][1] > 0
				cMoeda := Alltrim(Str(aTotais[nY][1]))+" - " + AllTrim(GetMV("MV_MOEDA"+Str(nY,1))) + " : "
				@li,010 PSAY cMoeda
				@li,025 PSAY TransForm(aTotais[nY][2],PesqPict("SE1","E1_VALOR"))
				li++
			EndIf
		Next nY
	EndIf
Else
	nPosRet    :=  Ascan(aTotTipos,{ |x| Alltrim(x[1])=="RET" })
	
	li := li + 2
	@li,002 PSAY STR0037 //"TOTAL DE RETENCOES : "
	@li,132 PSAY Iif(nPosRet > 0, xMoeda(aTotTipos[nPosRet][2],nMoeda,1,dDataMovto), 0)     Picture TM(SEL->EL_VALOR,17,nDecs)
	
	li := li + 3
	@li,002 PSAY STR0034 //"T O T A L   G E R A L ----> "
	@li,107 PSAY ABS(nSubTotRec-nSubTotDev)       Picture TM(SE1->E1_VALOR,17,nDecs)
	@li,132 PSAY ABS(nTotVlrRec-nTotVlrDev) + IIf(nPosRet > 0, xMoeda(aTotTipos[nPosRet][2],nMoeda,1,dDataMovto), 0)    Picture TM(SE1->E1_VALOR,17,nDecs)
	li := 80
	
	IF li > 55
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho)
		li := 8
	EndIf
	
	If Len(aTotais) > 0
		For nI := 1 To Len(aTotais)
			@ li,002 PSAY STR0036    //+ AllTrim(GetMV("MV_MOEDA"+Str(nI,1)))
			@ li,050 PSAY aTotais[nI][2]		   Picture TM(SE1->E1_VALOR,17,2)
			li++
			For nC := 1 To Len(aTotais[nI][3])
				If aTotais[nI][3][nC][2] > 0
					@li,010 PSAY aTotais[nI][3][nC][1]
					@li,050 PSAY aTotais[nI][3][nC][2]	Picture TM(SE1->E1_VALOR,17,2)
					li++
				EndIf
			Next nCZ
		Next nI
	EndIf
	
	li++
	If Len(aTotais) > 0
		For nI := 1 To Len(aTotais)
			@ li,002 PSAY STR0036 + AllTrim(GetMV("MV_MOEDA"+Str(nI,1)))
			@ li,050 PSAY aTotais[nI][2]		   Picture TM(SE1->E1_VALOR,17,MsDecimais(aTotais[nI][1]))
			li++
		Next nI
	EndIf
EndIf
roda(cbcont,cbtxt)
Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³BUSCARET  ³ Autor ³ Fernando Machima      ³ Data ³ 19/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Busca as retencoes no arq. de recibo(Loc. Argentina)       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LOJC070                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function BuscaRet()

LOCAL nValRet   := 0
LOCAL nMoedaRec := 1
LOCAL nDecs

DbSelectArea("SEL")
DbSetOrder(4)
If MsSeek(xFilial("SE1")+DTOS(dDataMovto))
	While !Eof() .And. EL_DTDIGIT == dDataMovto
		//Se a opcao Todas as Moedas nao estiver assinalada deve buscar os registros em SEL considerando a agencia e a conta,
		//para trazer os dados de um caixa em uma unica moeda
		If EL_TIPODOC $ "RG|RB|RI|RS" .And. EL_BANCO == cPortador .And. IIf(!lCheckM,EL_AGENCIA == cAgencia .And. EL_CONTA == cConta,.T.)
			nMoedaRec := Max(Val(EL_MOEDA),1)
			nDecs     := MsDecimais(nMoedaRec)
			nValRet   += xMoeda(EL_VALOR,nMoedaRec,nMoeda,EL_DTDIGIT)
			aTotais[nMoedaRec][2] += Round(EL_VALOR,nDecs)
		EndIf
		DbSkip()
	End
	Aadd(aTotTipos,{"RET",nValRet,nMoedaRec})
EndIf
Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LC070Refresh ³ Autor ³ Lucas             ³ Data ³ 22/04/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua o Refresh no Objeto ListBox oLbxDetalhe.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ oLbxDetalhe:=LC070Refresh(ExpO)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LC070Refresh(nMoeda,oLbxDetalhe,aLbxDetalhe)
Local nI, nL
aLbxDetalhe := {}
For nI := 1 To Len(aTotais)
	For nL := 1 To Len(aTotais[nI][3])
		If aTotais[nI][3][nL][2] > 0
			nPosTip	 :=	Ascan(aLbxDetalhe,{ |x| Alltrim(x[1])==Alltrim(aTotais[nI][3][nL][1]) })
			If nPosTip == 0
				AADD(aLbxDetalhe,{aTotais[nI][3][nL][1],aTotais[nI][3][nL][2]})
			Else
				aLbxDetalhe[nPosTip][2] += aTotais[nI][3][nL][2]
			Endif
		EndIf
	Next nL
Next nI
If Len(aLbxDetalhe) == 0
	AADD(aLbxDetalhe,{"",0.00})
EndIf
If Len(aLbxDetalhe) > 0
	oLbxDetalhe:SetArray(aLbxDetalhe)
	oLbxDetalhe:bLine := { || {aLbxDetalhe[oLbxDetalhe:nAT,1],Transform(aLbxDetalhe[oLbxDetalhe:nAT,2],PesqPict("SL1","L1_VLRTOT",,2))}}
EndIf
Return( aLbxDetalhe )