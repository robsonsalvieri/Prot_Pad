#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

Class LjCallApiAutoPreenchimento

    Public Method New()             as Object
    Public Method BuscaCEPViaCEP( ) as variant
    Public Method BuscaCNPJCarol()  as variant
    Public Method GetResposta()     as Json
    Public Method FreeVar()         as Variant
    Public Method BuscaCEPTAS()     as variant

    Private Method TrataComplemento()   as Character
    Private Method SetRetError()        as Variant

    Private Data cCEP           as Character
    Private Data cCNPJ          as Character
    Private Data cApiCEP        as Character
    Private Data oResponse      as Json
    Private Data oJsonRetCEP    as Json
EndClass

/*/{Protheus.doc} New
Construtor da classe
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method New() As Object Class LjCallApiAutoPreenchimento
Self:cCNPJ      := ""
Self:cCEP       := ""
Self:cApiCEP    := ""
Self:oJsonRetCEP:= JsonObject():New()
Self:oResponse  := JsonObject():New()

Return Self

/*/{Protheus.doc} BuscaCEPViaCEP
Faz a busca do CEP informado e retorna os dados do endereço
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method BuscaCEPViaCEP(cCEP as Character) as variant class LjCallApiAutoPreenchimento

Local oRest     := FWRest():New(Self:cApiCEP)   as Object
Local cResult   := ""                           as Character
Local cMsgError := ""                           as Character
Local lError    := .F.                          as Logical
Local nTimeOutRest := 30                        as Numeric

Self:cApiCEP    := "https://viacep.com.br"
oRest           := FWRest():New(Self:cApiCEP) 
self:cCEP       := cCEP

oRest:SetTimeOut(nTimeOutRest)
oRest:setPath("/ws/" + self:cCEP + "/json/")

If oRest:Get()
    cResult := oRest:GetResult()
    Self:oJsonRetCEP:FromJson(cResult)

    If Self:oJsonRetCEP:GetJsonObject('erro') <> "true"

        Self:oResponse['Endereco'] := {}
        Aadd(Self:oResponse['Endereco'], JsonObject():New())

        Self:oResponse['Endereco'][1]['cep'] := ""
        If Self:oJsonRetCEP:hasproperty('cep') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('cep'))
            Self:oResponse['Endereco'][1]['cep'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('cep') )
        EndIf

        Self:oResponse['Endereco'][1]['logradouro'] := ""
        If Self:oJsonRetCEP:hasproperty('logradouro') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('logradouro'))
            Self:oResponse['Endereco'][1]['logradouro'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('logradouro') )
        EndIf

        Self:oResponse['Endereco'][1]['complemento'] := ""
        If Self:oJsonRetCEP:hasproperty('complemento') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('complemento'))
            Self:oResponse['Endereco'][1]['complemento'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('complemento') )
        EndIf

        Self:oResponse['Endereco'][1]['bairro'] := ""
        If Self:oJsonRetCEP:hasproperty('bairro') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('bairro'))
            Self:oResponse['Endereco'][1]['bairro'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('bairro') )
        EndIf

        Self:oResponse['Endereco'][1]['cidade'] := ""
        If Self:oJsonRetCEP:hasproperty('cidade') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('localidade'))
            Self:oResponse['Endereco'][1]['cidade'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('localidade') )
        EndIf

        Self:oResponse['Endereco'][1]['uf'] := ""
        If Self:oJsonRetCEP:hasproperty('uf') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('uf'))
            Self:oResponse['Endereco'][1]['uf'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('uf') )
        EndIf

        Self:oResponse['Endereco'][1]['ibge'] := ""
        If Self:oJsonRetCEP:hasproperty('ibge') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('ibge'))
            Self:oResponse['Endereco'][1]['ibge'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('ibge') )
        EndIf

        Self:oResponse['Endereco'][1]['gia'] := ""
        If Self:oJsonRetCEP:hasproperty('gia') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('gia'))
            Self:oResponse['Endereco'][1]['gia'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('gia') )
        EndIf

        Self:oResponse['Endereco'][1]['ddd'] := ""
        If Self:oJsonRetCEP:hasproperty('ddd') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('ddd'))
            Self:oResponse['Endereco'][1]['ddd'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('ddd') )
        EndIf

        Self:oResponse['Endereco'][1]['siafi'] := ""
        If Self:oJsonRetCEP:hasproperty('ddd') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('siafi'))
            Self:oResponse['Endereco'][1]['siafi'] := DecodeUTF8( Self:oJsonRetCEP:GetJsonObject('siafi') )
        EndIf

        FreeObj(Self:oJsonRetCEP)
        Self:oJsonRetCEP := Nil
    Else 
        lError := .T.
    EndIf

Else
    lError := .T.
EndIf

If lError
    cMsgError := "Erro ao consultar CEP: " + oRest:GetLastError()
    Self:SetRetError( cMsgError )      
EndIf

Return

/*/{Protheus.doc} BuscaCEPTAS
Faz a busca do CEP pela API do TAS
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method BuscaCEPTAS(cCEP as Character) as variant class LjCallApiAutoPreenchimento
Local cAPITAS   := "SOACEPService"  as Character

self:cCEP := cCEP

Self:oJsonRetCEP := BlindTASExec("SA1", cAPITAS, {self:cCEP})

If Self:oJsonRetCEP["error"] == nil

    Self:oResponse['Endereco'] := {}
    Aadd(Self:oResponse['Endereco'], JsonObject():New())

    If Self:oJsonRetCEP:hasproperty('CEP') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('CEP'))
        Self:oResponse['Endereco'][1]['cep'] := Self:oJsonRetCEP:GetJsonObject('CEP')
    EndIf

    If Self:oJsonRetCEP:hasproperty('Endereço') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('Endereço'))
        Self:oResponse['Endereco'][1]['logradouro'] := Self:oJsonRetCEP:GetJsonObject('Endereço')
    EndIf

    If Self:oJsonRetCEP:hasproperty('Bairro') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('Bairro'))
        Self:oResponse['Endereco'][1]['bairro'] := Self:oJsonRetCEP:GetJsonObject('Bairro')
    EndIf

    If Self:oJsonRetCEP:hasproperty('Cidade') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('Cidade'))
        Self:oResponse['Endereco'][1]['cidade'] := Self:oJsonRetCEP:GetJsonObject('Cidade')
    EndIf

    If Self:oJsonRetCEP:hasproperty('Estado') .AND. !Empty(Self:oJsonRetCEP:GetJsonObject('Estado'))
        Self:oResponse['Endereco'][1]['uf'] := Self:oJsonRetCEP:GetJsonObject('Estado')
    EndIf

    Self:oResponse['Endereco'][1]['complemento'] := ""
    Self:oResponse['Endereco'][1]['ibge'] :=""
    Self:oResponse['Endereco'][1]['gia'] := ""
    Self:oResponse['Endereco'][1]['ddd'] := ""
    Self:oResponse['Endereco'][1]['siafi'] := ""

    FreeObj(Self:oJsonRetCEP)
    Self:oJsonRetCEP := Nil
Else
    LjGrvLog( "LjCallApiAutoPreenchimento",  "ValidRetCep | Erro na execução do TAS via função", Self:oJsonRetCEP["error"])
    Self:SetRetError( "Erro na execução do TAS via função" + Self:oJsonRetCEP["error"])   
EndIf

Return

/*/{Protheus.doc} BuscaCNPJCarol
Faz a busca do CNPJ informado e retorna os dados da empresa
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method BuscaCNPJCarol(cCNPJ as Character) as variant class LjCallApiAutoPreenchimento
Local aRetApiForCli     := {}                   as Array
Local oRest             := JsonObject():New()   as Json
Local oJsonRetApiForCli := Nil                  as Json
Local cUFBrasil         := 'AC|AL|AM|AP|BA|CE|DF|ES|GO|MA|MG|MS|MT|PA|PB|PE|PI|PR|RJ|RN|RO|RR|RS|SC|SE|SP|TO' as character
Local lError            := .F.                  as Logical
Local cMsgError         := ""                           as Character
Local cCodErro          := ""                           as Character

self:cCNPJ := cCNPJ

aRetApiForCli := APIForCli(Self:cCNPJ) // Consulta CNPJ na API da Carol

oRest   := JsonObject():New()

If aRetApiForCli[1]
    oRest:FromJson(aRetApiForCli[2])
    Iif( Len(oRest["hits"]) > 0, oJsonRetApiForCli := oRest["hits"][1]:GetJsonObject("mdmGoldenFieldAndValues"), lError := .T.)
EndiF

If ValType(oJsonRetApiForCli) == "U"
    lError := .T.
    cCodErro := "001"
    cMsgError := "Dados não encontrados - Informe os dados do cadastro manualmente."
ElseIf lError .Or. ValType(oJsonRetApiForCli) == "U" .OR. !(aRetApiForCli[1]) .OR. Upper( AlLTrim(oJsonRetApiForCli["mdmsituationdescription"]) ) <> "ATIVA"
    lError := .T.
    If Upper( AlLTrim(oJsonRetApiForCli["mdmsituationdescription"]) ) == "BAIXADA"
        cMsgError := "CNPJ está com status Baixado"
    ElseIf Upper( AlLTrim(oJsonRetApiForCli["mdmsituationdescription"]) ) == "INAPTA"
        cMsgError := "CNPJ está com status Inapto"
    EndIF    
Else
    
    /* Empresa */
    Self:oResponse['Empresa'] := {}
    Aadd(Self:oResponse['Empresa'], JsonObject():New())

    Self:oResponse['Empresa'][1]['cnpj']            := self:cCNPJ
    Self:oResponse['Empresa'][1]['nome']            := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli:hasproperty('mdmname')    , oJsonRetApiForCli["mdmname"], "")) )
    Self:oResponse['Empresa'][1]['nomeFantasia']    := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli:hasproperty('mdmdba')     , oJsonRetApiForCli["mdmdba"] , "")) )
    Self:oResponse['Empresa'][1]['cnae']            := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli:hasproperty('cnaebr')     , oJsonRetApiForCli["cnaebr"] , "")) )
    Self:oResponse['Empresa'][1]['tipoPessoa']      := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli:hasproperty('mdmtaxid')   , Iif(Len(AllTrim(oJsonRetApiForCli["mdmtaxid"])) == 11, "F", "J"), "J")) )

    /* Endereco */
    Self:oResponse['Endereco'] := {}
    Aadd(Self:oResponse['Endereco'], JsonObject():New())

    Self:oResponse['Endereco'][1]['cep']        := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmzipcode') , oJsonRetApiForCli["mdmaddress"][1]["mdmzipcode"] , "")) )
    Self:oResponse['Endereco'][1]['endereco']   := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmaddress1'), oJsonRetApiForCli["mdmaddress"][1]["mdmaddress1"] , "")) )
    
    cComplemento := Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmaddress2') , Capital(oJsonRetApiForCli["mdmaddress"][1]["mdmaddress2"]), "")
    Self:oResponse['Endereco'][1]['complemento']   := Iif(!Empty(cComplemento), EncodeUTF8( Alltrim(Self:TrataComplemento(cComplemento)) ), "" )

    Self:oResponse['Endereco'][1]['bairro']     := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmaddress3')   , oJsonRetApiForCli["mdmaddress"][1]["mdmaddress3"]   , "")) )
    Self:oResponse['Endereco'][1]['municipio']  := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmcity')       , oJsonRetApiForCli["mdmaddress"][1]["mdmcity"]       , "")) )
    Self:oResponse['Endereco'][1]['estado']     := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmstate')      , oJsonRetApiForCli["mdmaddress"][1]["mdmstate"]      , "")) )
    Self:oResponse['Endereco'][1]['pais']       := EncodeUTF8( Alltrim(Iif( oJsonRetApiForCli["mdmaddress"][1]:hasproperty('mdmstate')      , Iif(AllTrim(oJsonRetApiForCli["mdmaddress"][1]["mdmstate"]) $ cUFBrasil, "105", ""), "")) )
    
    /* Telefone */
    Self:oResponse['Telefone'] := {}
    Aadd(Self:oResponse['Telefone'], JsonObject():New())

    Self:oResponse['Telefone'][1]['telefone']  := EncodeUTF8("")
    Self:oResponse['Telefone'][1]['ddd']       := EncodeUTF8("")
    Self:oResponse['Telefone'][1]['ddi']       := EncodeUTF8("")

    If oJsonRetApiForCli["mdmphone"][2]:hasproperty('mdmphonenumber') .And. !Empty(oJsonRetApiForCli["mdmphone"][2]["mdmphonenumber"])            
        
        If FindFunction("RemDddTel")
            aTelefone := RemDddTel( oJsonRetApiForCli["mdmphone"][2]["mdmphonenumber"] ) // Formata telefone
            
            If Len(aTelefone) == 3 
                Self:oResponse['Telefone'][1]['telefone']  := aTelefone[1]
                Self:oResponse['Telefone'][1]['ddd']       := aTelefone[2]
                Self:oResponse['Telefone'][1]['ddi']       := aTelefone[3]
            EndIf

        EndIF

    EndIf
    
EndIf

If lError
    Self:SetRetError( cMsgError, cCodErro )   
EndIf

Return 

/*/{Protheus.doc} SetRetError
Seta o retorno caso ocorra um erro na busca
@author joao.marcos
@type  Method
@since 11/08/2025
@version 1.0
@param cMsgError, character, Mensagem de erro a ser retornada
@param cCodError, character, Código do erro a ser retornado 
/*/
Method SetRetError(cMsgError as character, cCodError as character) As Variant Class LjCallApiAutoPreenchimento 

Self:oResponse['Error'] := {}
Aadd(Self:oResponse['Error'], JsonObject():New())
Self:oResponse['Error'][1]['Codigo']    := AllTrim( cCodError )
Self:oResponse['Error'][1]['Mensagem']  := AllTrim( cMsgError )

Return

//-----------------------------------------------------------------
/*/{Protheus.doc} TrataComplemento
Trata o complemento recebido pela API da Carol, tirando espaÃ§os
e separando os tipos de complemento com traÃ§o (-).
Ex: 
- Antes : "CONJ  ABC                 BLOCO A                   COND  EXEMPLO"
- Depois: "CONJ ABC - BLOCO A - COND EXEMPLO"
@param cComplemento - Complemento para tratamento
@return cComplemento - Complemento ajustado
@author Jorge Martins
@since  03/10/2023
/*/
//-----------------------------------------------------------------
Method TrataComplemento(cComplemento as character) As character Class LjCallApiAutoPreenchimento 

If !Empty(cComplemento)

    While (At("  ", cComplemento)) > 0 .OR. (At(" - - ", cComplemento)) > 0 .OR. (At("   ", cComplemento)) > 0
        cComplemento := StrTran(cComplemento, "  ", " ")
        cComplemento := StrTran(cComplemento, " - - ", " - ")
        cComplemento := StrTran(cComplemento, "   ", " - ")
    End

EndIf

Return cComplemento

/*/{Protheus.doc} GetResposta
Fornece o objeto de resposta da busca
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method GetResposta() As Json Class LjCallApiAutoPreenchimento
Return Self:oResponse

/*/{Protheus.doc} FreeVar
Destroi vaiaveis/objetos de instancia
@type  Method
@author joao.marcos
@since 11/08/2025
@version 1.0
/*/
Method FreeVar() As Variant Class LjCallApiAutoPreenchimento
FreeObj(Self:oResponse)
Self:oResponse := Nil
Return
