#INCLUDE "MSOBJECT.CH" 

User Function LOJA0012 ; Return  // "dummy" function - Internal Use

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบClasse    ณLJALog 			  บAutor  ณVendas Clientes     บ Data ณ  14/11/07   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณClasse abstrata de log.										  	    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        		    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Class LJALog From LJAAbstrataArquivos
	
	Data nTamLog									//Determina o tamanho do log
	
	Method ALog()									//Metodo construtor
	Method DataHora()    							//Metodo que ira retornar data e hora.
   	Method VerTamArq()								//Metodo que ira verificar o tamanho do arquivo
   	Method Gravar(cTexto)							//Responsavel em gravar o arquivo de log
   	
EndClass

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณALog      บAutor  ณVendas Clientes     บ Data ณ  14/11/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConstrutor da classe LJALog.          		              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ														      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method ALog() Class LJALog

	Local cTamLog := ""								//Determina o tamanho do log
	//Executa o metodo construtor da classe pai.
	::AbstArq()
 	
	cTamLog := SuperGetMV('MV_LJTMLOG',, "0") 
	
	If Val(cTamLog) == 0
		//Configura o log em 100Kb
		::nTamLog := 100 * 1000
	ElseIf Val(cTamLog) > 1000
		//Configura o log em 1Mb
		::nTamLog := 1000 * 1000	
	Else
		::nTamLog := Val(cTamLog) * 1000		
	EndIf
	
Return Self

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณDataHora  บAutor  ณVendas Clientes     บ Data ณ  14/11/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em retorna a data e hora no formato 			  บฑฑ
ฑฑบ			 ณdd/mm/yyyy hh:mm:ss								  		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ															  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณString												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method DataHora() Class LJALog
	
	Local cRetorno := ""					//Variavel de retorno do metodo
	
	cRetorno =  StrZero(Day(Date()),2) + "/" + StrZero(Month(Date()),2) + "/" + Str(Year(Date()),4)
	cRetorno += " " + SubStr(Time(),1,2) + ":" + SubStr(Time(),4,2) + ":" + SubStr(Time(),7,2)
	
Return cRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณVerTamArq บAutor  ณVendas Clientes     บ Data ณ  14/11/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em verificar o tamanho do arquivo e renomea-lo  บฑฑ
ฑฑบ			 ณse ultrapassar 1MB.								  		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ															  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method VerTamArq() Class LJALog
	
	Local oDiretorio 	:= Nil						//Objeto do tipo Diretorio
	Local cNome			:= ""						//Nome do arquivo a ser renomeado
	
	//Instancia o objeto Diretorio 
	oDiretorio := LJCDiretorio():Diretorio(::cPath)
	
	//Verifica se o diretorio existe
	If oDiretorio:Existe()                
		//Le os arquivos do diretorio		
		oDiretorio:Ler()
		//Verifica se o arquivo existe           
		If oDiretorio:Arquivos():Contains(Upper(::cNomeArq))
			//Verifica se o arquivo esta com mais de 1MB
			If oDiretorio:Arquivos():ElementKey(Upper(::cNomeArq)):nTamanho >= ::nTamLog
				
				//Verifica se o path esta vindo com barra no final
				If Substr(::cPath, Len(::cPath), 1) != ::Arquivo():cBarra
					::cPath	:= ::cPath + ::Arquivo():cBarra
				EndIf
				

				oDiretorio := LJCDiretorio():Diretorio(::cPath + "bkp")
				
				//Cria o diretorio de BKP se nใo existir
				If !oDiretorio:Existe()
					oDiretorio:Criar()
				EndIf
				//Monta o nome do arquivo no formato
				//Nome + DDMMYYYY + HHMMSS + "." + extensao
				//Exemplo: LJTEF14112007120510.LOG
				cNome := ::cPath + "bkp" + ::Arquivo():cBarra + Substr(::cNomeArq, 1, AT(".", ::cNomeArq) - 1)
				cNome += StrZero(Day(Date()),2) + StrZero(Month(Date()),2) + Str(Year(Date()),4) + SubStr(Time(),1,2) + SubStr(Time(),4,2) + SubStr(Time(),7,2)
				cNome += Substr(::cNomeArq, RAT(".", ::cNomeArq))
							
				//Renomeia o arquivo
				::Arquivo():Renomear(cNome)        
							
			EndIf

		EndIf 

	EndIf    	
	oDiretorio:Destroy()
	oDiretorio := FreeObj(oDiretorio)

	   	
Return Nil
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบMetodo    ณGravar    บAutor  ณVendas Clientes     บ Data ณ  04/03/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณResponsavel em gravar o arquivo de log.			  		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 (1 - cTexto) - Texto a ser gravado.					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณLogico												      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Gravar(cTexto) Class LJALog
	
	Local lRetorno := .T.						//Variavel de retorno do metodo
	
	//Verifica se precisa renomear o arquivo
	::VerTamArq()
	
	//Adiciona data e hora no texto.
	cTexto = ::DataHora() + " - " + cTexto
		
	//Chama o metodo da classe pai para gravar o arquivo
	If lRetorno
		lRetorno := _Super:Gravar(cTexto)
	EndIf
			
Return lRetorno
