#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "LJSIMULADESC.CH"

/*/{Protheus.doc} LJSimulaDesc
Fluxo de simulacao e aplicacao de desconto no total da venda
@type       Function
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  oPanVA3, painel da venda assistida
/*/
Function LJSimulaDesc( oPanVA3 as object)
Local oDlgSimulaDesc := Nil as Object
Local nPosCodProd    := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2]

If Len(aCols) > 0 .And. !Empty(aCols[1][nPosCodProd])

   oDlgSimulaDesc := LjSimuladorDesconto():New(LJ7T_Total(2), aCols, LJ7T_DescP(2), LJ7T_DescV(2), RetImpVend())
   
   If oDlgSimulaDesc:lAbreTela
      If LJ7T_DescP(2) > 0
         oDlgSimulaDesc:ZeraDescontoTotal()
      EndIf

      // Exibe a tela
      oDlgSimulaDesc:Executar(oPanVA3)
   EndIf

   FreeObj(oDlgSimulaDesc)
   oDlgSimulaDesc := Nil

Else
   MsgInfo(STR0001,STR0002) //"Não foi informado nenhum produto na venda!" # "Venda Assistida"
EndIf

Return



/*/{Protheus.doc} LjResumoDesconto
Tela para apresentar um resumo dos descontos que foram aplicados na venda
@type       Function
@author     Bruno Almeida
@since      10/12/2025
@version    v1.0
/*/
Function LjResumoDesconto()

Local oResumo     := Nil
Local nPosCodProd := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2]

If Len(aCols) > 0 .And. !Empty(aCols[1][nPosCodProd])
   oResumo := LjSimuladorDesconto():New(Nil, aCols)
   oResumo:showResumo()

   FwFreeObj(oResumo)
Else
   MsgInfo(STR0001,STR0002) //"Não foi informado nenhum produto na venda!" # "Venda Assistida"
EndIf

Return Nil

/*/{Protheus.doc} RetImpVend
Retorna o valor do imposto da venda IPI e ICMS ST
@type       Static Function
@author     joao.marcos
@since      12/12/2025
@version    v1.0
/*/
Static Function RetImpVend()
Local nVlrImpVda := 0 as Numeric

If MaFisFound("NF")
    nVlrImpVda := MaFisRet(,"NF_VALIPI") + MaFisRet(,"NF_VALSOL")
EndIf

Return nVlrImpVda
