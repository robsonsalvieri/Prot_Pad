#include "PROTHEUS.CH"
#include "tlpp-core.th"
#INCLUDE "AUTODEF.CH"
#Include "LjSimuladorDesconto.ch"

/*/{Protheus.doc} LjSimuladorDesconto
Classe responsável por simular e aplicar descontos no total da venda.
@type       Class
@author     joao.marcos
@since      26/11/2025
@version    v1.0
/*/

Class LjSimuladorDesconto

    Public Data oFontBoldP          as Object
    Public Data oFontBoldM          as Object
    Public Data oFontBoldG          as Object

    Public Data nTotOriginal        as Numeric      // Total original da venda
    Public Data nValorImpostoVenda  as Numeric      // Valor do imposto da venda
    Public Data nTotSimulado        as Numeric      // Total simulado após desconto
    Public Data nPercDescOrig       as Numeric      // Percentual original do desconto
    Public Data nValorDescOrig      as Numeric      // Valor original do desconto
 
    Public Data nTipoDesc           as Numeric      // Tipo desconto | V - Valor | P - Percentual
    Public data aItensVenda         as Array        //Array de itens da venda

    Public Data lAbreTela           as Logical      // Indica se a tela deve ser aberta

    Public Data cNomeCliente        as Character    // Nome do cliente da venda
    Public Data cTelCliente         as Character    // Telefone do cliente da venda

    Private data   lMotivoDesc      as Logical      // Motivo do desconto Ativo/Desativo
    Public data    cCodMotivo       as Character    // Cadigo Motivo do desconto
    Public data    cDescMotivo      as Character    // Descricao do Motivo do desconto
    Public data    aMaxDescCaixa    as Array        // Percentual máximo permitido para o Operador de Caixa sem aprovação
    Public data    aMaxDescSuper    as Array        // Percentual máximo permitido para o Superior/Gerente

    Public Data nPercDesc           as Numeric      // Percentual do desconto
    Public Data nVlrDesc            as Numeric      // Valor do desconto
    Public Data nVlrImposto         as Numeric      // Valor do imposto incidente no desconto

    Public Data oDlgTelaSimulDesc   as Object
    Private Data oGetPerc           as object
    Private Data oGetVlrDesc        as object
    Private Data oGetTotFinal       as object
    Public Data cTamanhoTela        as Character     // Tamanho da tela P - Pequena | G - Grande

    Public Data oDlgTelaAprovacaoSuper  as Object  // Tela de aprovação do superior

    Private Data lLimiteExcedido        as Logical  // Indica se o limite de desconto foi excedido

    Public Data lAprovado               as Logical  // Flag de aprovação
    Public Data  lAplicar               as Logical  // Informa se o desconto será aplicado
    Public Data lImpostos               as Logical  // Indica que avenda tem impostos como IPI e ICMS-ST
    Public Data lAprovadoSuper          as Logical      // Indica se o desconto foi aprovado pelo superior

    Public Data nPerDescItens           as Numeric  // Percentual de desconto aplicado nos itens
    Public Data nRetrai                 as Numeric  // Indica se a string deve ser retraída

    Private Data aItensResumo           as Array        // Contem os itens que possuem desconto para serem apresentados no resumo de desconto
    Private Data aTotalDesc             as Array        // Contem o desconto no total da venda
    Public  Data nTotalGeral            as Numeric      // Contem o total geral de descontos na venda (Venda + Itens)
    Public  Data cTipoPermissaoDesconto as Character    // Verifica se o usuario tem permissão para aplicar desconto (Vermelho, Amarelo ou Verde na tabela SLF)
    Public  Data cTextoPermissao        as Character    // Texto que sera exibido para o usuario verificando se tem ou não permissão para desconto
    
    Public Method New()                     as Object
    Public Method TelaSimulacaoDesconto()   as Variant
    Public Method CalcDesconto()            as Variant
    Public Method AlertaLimite()            as Variant
    Private Method LimitesDeDesconto()      as Array
    Private Method GetLimitesSLF()          as array
    Public Method Executar()                as Variant
    Private Method Impostos()               as Variant
    Public Method ZeraDescontoTotal()       as Variant
    Public Method DescriMotDesc()           as Variant
    Public Method ConFirmaDesc()            as Logical
    Private Method ValidaConfirmacao()      as Logical
    Private Method RetItensVenda()          as Array
    Private Method DescontoItens()          as Variant
    Private Method RetQuantEstoque()        as Numeric
    Public Method AprovacaoSuper()          as Variant
    Public Method TelaAprovacaoSuperior()   as Variant
    Public Method LimpaSenhaSuper()         as Variant
    Public Method ValidaAprovSuper()        as Logical
    Public Method showResumo()              as Variant
    Public Method formataItens()            as Variant
    Public Method getVendedor()             as Character
    Private Method ValidaValor()            as Logical
    Private Method PermissaoDesconto()      as Variant

EndClass

/*/{Protheus.doc} New
Construtor da classe
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  nTotOrig, Total original da venda
        aItens, itens da venda aCols
        nPerDescTotal percenteal de desconto no total
        nValorDescTotal, valor do desconto no total
        nVlrImpVda, valor do imposto da venda
/*/
Method New(nTotOrig as numeric, aItens as array, nPerDescTotal as Numeric, nValorDescTotal as Numeric, nVlrImpVda as Numeric) as Object Class LjSimuladorDesconto

    ::nTotOriginal  := nTotOrig
    ::nValorImpostoVenda := nVlrImpVda
    ::nTotSimulado  := ::nTotOriginal
    ::nPercDescOrig := nPerDescTotal
    ::nValorDescOrig := nValorDescTotal
    ::aItensVenda   := Iif(aItens == NIL, {}, aClone(aItens) )

    ::cNomeCliente  := ""
    ::cTelCliente   := ""

    ::lAbreTela     := .T.

    ::nPercDesc     := 0
    ::nVlrDesc      := 0
    ::nVlrImposto   := 0
    ::nTipoDesc     := 0
    ::nPerDescItens := ::DescontoItens()
    ::nRetrai       := 0

    ::oFontBoldP    := TFont():New(,,-12,,.T.)
    ::oFontBoldM    := TFont():New(,,-14,,.T.)
    ::oFontBoldG    := TFont():New(,,-16,,.T.)

    ::oDlgTelaSimulDesc := Nil
    ::cTamanhoTela      := Iif(MsAdvSize()[5] <= 1350, "P", "G")

    ::lLimiteExcedido   := .F.

    ::lMotivoDesc   := Lj7Mv_Desc()  // Usa motivo de desconto
    ::cCodMotivo    := Space(TamSx3("MDT_CODIGO")[1])
    ::cDescMotivo   := Space(TamSx3("MDT_DESCRI")[1])
   
    ::aMaxDescCaixa := {}
    ::aMaxDescSuper := {}

    ::lAprovado     := .F.
    ::lAplicar      := .F.
    ::lImpostos     := .F.
    ::lAprovadoSuper  := .F.

    ::aItensResumo := {}
    ::aTotalDesc   := {}

    ::cTipoPermissaoDesconto := ""    

Return Self

/*/{Protheus.doc} Executar
Executa o fluxo de Simulacao e aplicacao do desconto
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  oPanVA3, painel da venda assistida
/*/
Method Executar(oPanVA3) Class LjSimuladorDesconto
Local lDescontoAplicado := .F. as Logical

    ::Impostos()
    ::aMaxDescCaixa := ::LimitesDeDesconto(1) // {Percentual Maximo (SLF), Valor Maximo (SLF), Maximo valor de desconto na venda}
    ::aMaxDescSuper := ::LimitesDeDesconto(2) // {Percentual Maximo (SLF), Valor Maximo (SLF), Maximo valor de desconto na venda}

    Lj7SetKeys(.F.)
    ::TelaSimulacaoDesconto()
    ::oDlgTelaSimulDesc:Activate(, , , .T., , , {||})
   
    If ::lAplicar .AND. Lj7VldServ() .AND. Iif(::lAprovadoSuper, .T., LJ7_ValDesc(::nTipoDesc, ::nPercDesc, ::nVlrDesc) )
        If ::nTipoDesc == 2  // Valor 
            lDescontoAplicado := LJ7VldDesc(oPanVA3,,::nVlrDesc,.T.,, .F.,, .T.)
        Else  // Percentual
            lDescontoAplicado := LJ7VldDesc(oPanVA3, ::nPercDesc,,.T.,, .F.,, .T.)
        EndIf

        Iif(!lDescontoAplicado, Help(" ", 1, "HELP", STR0001, STR0002, 1, ,,,,,,, {STR0003}), ) // "Simulação de Desconto" # "Motivo do desconto não informado." # "Informe o código do motivo de desconto."

    EndIf

    If !::lAplicar
        LJ7VldDesc(,,::nValorDescOrig,.T.,, .F.)
    EndIf
    Lj7SetKeys(.T.)

Return NIL

/*/{Protheus.doc} TelaSimulacaoDesconto
Gera a tela de Simulacao de desconto
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
/*/
Method TelaSimulacaoDesconto() Class LjSimuladorDesconto

Local oBrowse           := Nil                                  as Object
Local lDimPixels        := .T.                                  as Logical
Local cTitulo           := STR0004                              as Character // "Desconto no Total"
Local oTGetDesc         := Nil                                  as Object
Local nTGetDesc         := 0                                    as Numeric
Local oSay01            := Nil                                  as Object
Local oSay02            := Nil                                  as Object

Local oPanelResumoFin   := Nil                                  as Object
Local oSayResumoFin01   := Nil                                  as Object
Local oSayResumoFin02   := Nil                                  as Object
Local oSayVlrSemDesc01  := Nil                                  as Object
Local oSayVlrDesc01     := Nil                                  as Object
Local oSayVlrComDesc01  := Nil                                  as Object
Local oSayLimiteDesc    := Nil                                  as Object

Local nClrBackPn1       := RGB(245,245,245)                     as Numeric
Local nClrBackYllw      := RGB(254,252,232)                     as Numeric
Local oButtonFechar     := Nil                                  as Object
Local oButtonAplicar    := Nil                                  as Object
Local oPanelLimite      := Nil                                  as Object
Local oButtonValor      := Nil                                  as Object
Local oButtonPercentual := Nil                                  as Object
Local oSayAlertLimite   := Nil                                  as Object
Local oPanelYellow      := Nil                                  as Object
Local oButtonAprovacao  := Nil                                  as Object

Local oSayMtDesc1       := Nil                                  as Object
Local oSayMtDesc2       := Nil                                  as Object
Local oTGetCodMtDesc    := Nil                                  as Object
Local oTGetDescMtDesc   := Nil                                  as Object
  
Local aBrowse       := ::RetItensVenda(1)

Local nJanAltu        := 650    as Numeric
Local nJanLarg        := 1200   as Numeric 

Local nColResumoFin   := 290    as Numeric
Local nLargResumoFin  := 305    as Numeric
Local nColResSemDesc  := 250    as Numeric
Local nColResVlDesc   := 250    as Numeric
Local nColResConDesc  := 250    as Numeric

Local nLargBrowse     := 585    as Numeric
Local nAltBroese      := 120    as Numeric

Local nLinBoxLimite   := 250    as Numeric
Local nAltBoxLimite   := 285    as Numeric
Local nLargBoxLimite  := 595    as Numeric

Local nAltPanelLimite := 25     as Numeric

Local nLinSeparadorFim := 295   as Numeric

Local nColBtVoltar    := 505    as Numeric
Local nColBtAplicDesc := 550    as Numeric
Local nLinBtVoltar    := 300    as Numeric
Local nLinBtAplicDesc := 300    as Numeric

If ::cTamanhoTela == "P"
    nJanAltu        := 455
    nJanLarg        := 1000

    nColResumoFin   := 220
    nLargResumoFin  := 270
    nColResSemDesc  := 220
    nColResVlDesc   := 220
    nColResConDesc  := 220

    nLargBrowse     := 480
    nAltBroese      := 60
    
    nLinBoxLimite   := 185
    nAltBoxLimite   := 210
    nLargBoxLimite  := 315

    nAltPanelLimite := 15

    nLinSeparadorFim := 230

    nLinBtVoltar    := 205
    nColBtVoltar    := 400
    nLinBtAplicDesc := 205
    nColBtAplicDesc := 445   
EndIf

If Len(aBrowse) > 0

    ::permissaoDesconto()
    ::oDlgTelaSimulDesc := TDialog():New(0, 0, nJanAltu, nJanLarg, cTitulo, , , , , , /*CorFundo*/, , , lDimPixels)

    oSay01 := TSay():New(20,10,{|| STR0005},::oDlgTelaSimulDesc,, ::oFontBoldG,,,,.T./*10*/,,/*BackGrd*/,90,10) // "Desconto manual"
    oSay02 := TSay():New(35,10,{|| STR0006},::oDlgTelaSimulDesc,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,300,10) // "Informe o valor e o tipo do desconto que deseja aplicar"

    oTGetDesc           := TGet():New( 50,10,{|u| if( PCount() > 0, nTGetDesc := u, nTGetDesc ) },::oDlgTelaSimulDesc,130,15,X3Picture("LR_VALDESC"),{||.T.},,/*10*/,,.F.,,.T.,,.F.,{||.T.},,,/*20*/,.F.,.F.,,"nTGetDesc",,,, )
    oButtonValor        := TButton():New( 50, 145, STR0007 , ::oDlgTelaSimulDesc,{|| ::CalcDesconto("V",nTGetDesc) ,::AlertaLimite(@oPanelYellow, @oSayAlertLimite, @oButtonAplicar, @oButtonAprovacao) },25,15,,,.F.,.T.,.F.,,.F.,,,.F.) // "R$"
    oButtonPercentual   := TButton():New( 50, 175, "%"     , ::oDlgTelaSimulDesc,{|| ::CalcDesconto("P",nTGetDesc) ,::AlertaLimite(@oPanelYellow, @oSayAlertLimite, @oButtonAplicar, @oButtonAprovacao) },25,15,,,.F.,.T.,.F.,,.F.,,,.F.) // "%"
    
    If ::lMotivoDesc  // Motivo do Desconto
        oSayMtDesc1 := TSay():New(75,10,{|| STR0008},::oDlgTelaSimulDesc,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,200,10) // "Motivo do Desconto"
        oTGetCodMtDesc := TGet():New( 85,10,{|u| if( PCount() > 0, ::cCodMotivo := u, ::cCodMotivo ) },::oDlgTelaSimulDesc,20,15,/*Picture*/,{|| ::DescriMotDesc(), oTGetDescMtDesc:Refresh(), ::oDlgTelaSimulDesc:Refresh()},,/*10*/,,.F.,,.T.,,.F.,{||.T.},,,/*20*/,.F.,.F.,,"cCodMotivo",,,, )
        oTGetCodMtDesc:cF3 := "MDT"

        oSayMtDesc2 := TSay():New(75,70,{|| STR0009},::oDlgTelaSimulDesc,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,300,10) // "Descrição:"
        oTGetDescMtDesc := TGet():New( 85,70,{|u| if( PCount() > 0, ::cDescMotivo := u, ::cDescMotivo ) },::oDlgTelaSimulDesc,130,15,/*Picture*/,{||.T./*Valid*/},,/*10*/,,.F.,,.T.,,.F.,{||.F.},,,/*20*/,.F.,.F.,,"cDescMotivo",,,, )
    EndIf

    oPanelResumoFin := tPanel():New(10,nColResumoFin,,::oDlgTelaSimulDesc,/*Font*/,.T.,,,nClrBackPn1, nLargResumoFin,100) 
    oSayResumoFin01 := TSay():New(10,10,{|| STR0010},oPanelResumoFin,, ::oFontBoldG,,,,.T./*10*/,,/*BackGrd*/,90,10) // "Resumo Financeiro"
    oSayResumoFin02 := TSay():New(25,10,{|| STR0011},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,230,10) // "Valores que serão praticados na venda após aplicação do desconto, considerando os impostos."
    oSayResumoFin01:SetCSS("QWidget { font-weight: bold ;}")
    oSayResumoFin02:SetCSS("QWidget { font-weight: bold ;}")

    oSayVlrSemDesc01 := TSay():New(40,10,{|| STR0012 + Iif(::lImpostos .AND. ::cTamanhoTela == "G", STR0029,"") + Iif(::cTamanhoTela == "P",Space(109-::nRetrai),Iif(::lImpostos,Space(82-::nRetrai),Space(127-::nRetrai))) + STR0007 + " " + Alltrim(Transform(::nTotOriginal, X3Picture("LQ_VALBRUT"))) },oPanelResumoFin,,/*font*/,,,,.T./*10*/,,/*BackGrd*/,350,10) // "Sem desconto" # " (Mercadorias + Impostos)"
    oSayVlrSemDesc01:SetCSS("QWidget { font-weight: bold ;}")

    oSayVlrDesc01 := TSay():New(50,10,{|| STR0013 + Iif(::cTamanhoTela == "P",Space(101-::nRetrai),Space(120-::nRetrai)) + "-" + STR0007 + " " + Alltrim(Transform(::nVlrDesc,X3Picture("LR_VALDESC"))) + " (" + AllTrim(Str(::nPercDesc)) + "%)" },oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,300,10) // "Valor do desconto"
    oSayVlrDesc01:SetCSS("QWidget { color: #980909; font-weight: bold ;}")

    @ 60,10 TO 61,297 PIXEL OF oPanelResumoFin

    oSayVlrComDesc01 := TSay():New(65,10,{|| STR0014 + Iif(::cTamanhoTela == "P",Space(108-::nRetrai),Space(127-::nRetrai)) + "R$ " + Alltrim(Transform(::nTotSimulado, X3Picture("LQ_VALBRUT")))},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,300,10) // "Com desconto"
    oSayVlrComDesc01:SetCSS("QWidget { font-weight: bold ;}")

    @ 75,10 TO 61,297 PIXEL OF oPanelResumoFin
  
    oPanelYellow := tPanel():New(80,10,/*""*/,oPanelResumoFin,::oFontBoldP,.T.,,,nClrBackYllw,285,15) // 585
    oPanelYellow:SetCSS("QWidget { border-width: 2px ; }")
    oSayAlertLimite := TSay():New(05,10,{|| ::cTextoPermissao },oPanelYellow,,/*oFont*/,,,,.T./*10*/,,/*BackGrd*/,230,10)
    oSayAlertLimite:SetCSS("QWidget { color: #8F560E; }")

    oBrowse := TCBrowse():New( 120 , 10, nLargBrowse, nAltBroese,,  {AllTrim(RetTitle("LR_ITEM")), AllTrim(RetTitle("LR_PRODUTO")), STR0016, AllTrim(RetTitle("LR_QUANT")), STR0017, STR0068, AllTrim(RetTitle("LR_UM")), STR0018, STR0019},; // "Item", "Produto", "Descrição", "Quantidade", "Preço Unit.", "Vlr. Item", "Unidade", "Desconto em R$", "Desconto em %"
                                                    {50,65,35,55,55,55,45,60}, ::oDlgTelaSimulDesc,,,,,{||},,,,,,,.F.,/*21*/,.T.,,.F.,,, )
    oBrowse:SetArray(aBrowse)
    oBrowse:bLine := {|| {  aBrowse[oBrowse:nAt][1]                                                     ,; // 01 - Item
                            aBrowse[oBrowse:nAt][2]                                                     ,; // 02 - Cod Produto
                            aBrowse[oBrowse:nAt][3]                                                     ,; // 03 - Descricao
                            AllTrim( Transform(aBrowse[oBrowse:nAt][4]  , X3Picture("LR_QUANT"  ) ) )   ,; // 04 - Quantidade
                            AllTrim( Transform(aBrowse[oBrowse:nAt][5]  , X3Picture("LR_VRUNIT" ) ) )   ,; // 05 - Preço Unitario
                            AllTrim( Transform(aBrowse[oBrowse:nAt][6]  , X3Picture("LR_VLRITEM") ) )   ,; // 06 - Vlr. Item
                            aBrowse[oBrowse:nAt][7]                                                     ,; // 07 - Unidade      
                            AllTrim( Transform(aBrowse[oBrowse:nAt][8]  , X3Picture("LR_VALDESC") ) )   ,; // 08 - Desconto em R$
                            AllTrim( Transform(aBrowse[oBrowse:nAt][9]  , X3Picture("LR_DESC"   ) ) )   ,; // 08 - Desconto em %
                        }}

    oBrowse:lHScroll := .T.
    oBrowse:lJustific := .T.

    @ nLinBoxLimite, 10 TO nAltBoxLimite, nLargBoxLimite PIXEL OF ::oDlgTelaSimulDesc
    oPanelLimite := tPanel():New(nLinBoxLimite+5,20,,::oDlgTelaSimulDesc,/*Font*/,.T.,,nClrBackYllw,nClrBackPn1, 282, nAltPanelLimite, .T.,.T.) // 585

    oSayLimiteDesc := TSay():New(Iif(::cTamanhoTela=="G",10,5),10,{|| STR0020 + Alltrim(Transform(IIF(::cTipoPermissaoDesconto == "S", ::aMaxDescCaixa[3], 0), X3Picture("LQ_DESCONT")))  + " (" + AllTrim(Str(IIF(::cTipoPermissaoDesconto == "S", ::aMaxDescCaixa[1], 0))) + " %)"},oPanelLimite,,::oFontBoldM,,,,.T./*10*/,,,200,10) // "Limite de desconto sem aprovação: R$ "

    @ nLinSeparadorFim := 295,00 TO nLinSeparadorFim+1,600 PIXEL OF ::oDlgTelaSimulDesc

    oButtonFechar  := TButton():New( nLinBtVoltar, nColBtVoltar, STR0021, ::oDlgTelaSimulDesc,{|| ::oDlgTelaSimulDesc:End() },40,015,,,.F.,.T.,.F.,,.F.,,,.F.) // "Voltar"
    oButtonFechar:cToolTip := STR0034 // "Clique para voltar sem aplicar o desconto."
    oButtonAplicar := TButton():New( nLinBtAplicDesc, nColBtAplicDesc, STR0022, ::oDlgTelaSimulDesc,{|| Iif(::ConFirmaDesc(nTGetDesc),::oDlgTelaSimulDesc:End(),) },45,15,,,.F.,.T.,.F.,,.F.,,,.F.) // "Aplicar desconto"
    oButtonAplicar:SetCSS("QWidget { color: #ffffffff; background-color: #00659A; }")
    oButtonAplicar:cToolTip := STR0035 // "Clique para aplicar o desconto na venda."

    oButtonAprovacao := TButton():New( nLinBtAplicDesc, nColBtAplicDesc, STR0037, ::oDlgTelaSimulDesc,{|| Iif(::ConFirmaDesc(nTGetDesc) .And. ::AprovacaoSuper(nTGetDesc),,) },45,15,,,.F.,.T.,.F.,,.F.,,,.F.) // "Aprovação"
    oButtonAprovacao:SetCSS("QWidget { color: #ffffffff; background-color: #00659A; }")
    oButtonAprovacao:cToolTip := STR0036 // "Clique para solicitar a aprovação do superior."

    ::AlertaLimite(@oPanelYellow, @oSayAlertLimite, @oButtonAplicar, @oButtonAprovacao )

EndIf

Return NIL

/*/{Protheus.doc} LimitesDeDesconto
Carrega os limites de desconto que o operador consegue aplicar na venda
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  nOp, opcao | 1 - Caixa | 2 - Superior/Gerente
/*/
Method LimitesDeDesconto(nOp as Numeric) as Array Class LjSimuladorDesconto
Local aLimitesDesc          := {}                                                       as Array
Local aUsers	            := {}                                                       as Array
Local cCodCaixa             := GetAdvFVal("SA6","A6_COD",xFilial('SA6') + cUserName,2)  as Character
Local cCodSuper             := ""                       		                        as Character // Codigos do Caixa Superior
Local aUsuarios             := LjRetSup(1,"",@aUsers)                                   as Array
Local aLimitesDescontoSLF   := {}                                                       as Array // aLimitesDescCaixa[1] Percentual, aLimitesDescCaixa[1] Valor
Local nVlrMaxDescVenda      := 0                                                        as Numeric // Valor maximo de desconto na venda
Local nPercMaxDescVenda     := 0                                                        as Numeric // Percentual maximo de desconto na venda

If Len(aUsuarios) > 0
    cCodSuper := aUsuarios[1]
EndIf

If nOp == 1 // Caixa
    aLimitesDescontoSLF := ::GetLimitesSLF(cCodCaixa)        
ElseIf nOp == 2 .and. !Empty(cCodSuper) // Superior
    aLimitesDescontoSLF := ::GetLimitesSLF(cCodSuper)    
EndIf

If Len(aLimitesDescontoSLF) > 0

    nVlrMaxDescVenda := Round( (::nTotOriginal * aLimitesDescontoSLF[1]) / 100 , 2)

    If nVlrMaxDescVenda > aLimitesDescontoSLF[2]
        nVlrMaxDescVenda := aLimitesDescontoSLF[2]

        nPercMaxDescVenda := Round( (nVlrMaxDescVenda / ::nTotOriginal) * 100, 2)

        aLimitesDesc := {   nPercMaxDescVenda,;
                            aLimitesDescontoSLF[2],;
                            nVlrMaxDescVenda}        
    Else
        aLimitesDesc := {   aLimitesDescontoSLF[1],; // Percentual Maximo
                            aLimitesDescontoSLF[2],; // Valor Maximo
                            nVlrMaxDescVenda      } // Maximo valor de desconto na venda
    EndIf
EndIf

Return aLimitesDesc

/*/{Protheus.doc} CalcDesconto
Calcula o valor do desconto a ser aplicado
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  cTipo, tipo do desconto | V - Valor | P - Porcentagel
        nValorTela, Valor digitado na tela
/*/
Method CalcDesconto(cTipo as character, nValorTela as numeric) as Variant Class LjSimuladorDesconto
Local aVlrSimul     := {}   as array
Local nValor        := nValorTela
Local lRet          := .F.

If nValorTela > 0 .and. ::ValidaValor(cTipo, nValorTela)

    Do Case         

        Case cTipo == "P" // Percentual

            ::nTipoDesc := 1

            LJ7VldDesc( , nValor, Nil, .T., .F. , .T., @aVlrSimul)

            ::nPercDesc := aVlrSimul[1]
            ::nVlrDesc := aVlrSimul[2]
            ::nTotSimulado := aVlrSimul[3]
            ::nVlrImposto := aVlrSimul[4]
        Case cTipo == "V" // Valor

            ::nTipoDesc := 2

            LJ7VldDesc( /*oPanVA3*/, Nil, nValor, .T., .F., .T., @aVlrSimul)

            ::nPercDesc := aVlrSimul[1]
            ::nVlrDesc := aVlrSimul[2]
            ::nTotSimulado := aVlrSimul[3]
            ::nVlrImposto := aVlrSimul[4]
        
    EndCase
    
    If ::cTipoPermissaoDesconto == "S"

        lRet := LJSenhaSup( ""                            , 11	       , ""  , AllTrim(SuperGetMV("MV_LJFORMD", .F., "1")),;
                            ::nPercDesc + ::nPerDescItens , ::nVlrDesc , Nil , .T.		                                  ,;
                            Nil   		                  , .T.	       , Nil , Nil                                        ,;
                            "1"                           , .T.        )

        If lRet
            ::lLimiteExcedido := .F.
        Else
            ::lLimiteExcedido := .T.
        EndIf

    EndIf

    If nValorTela > 999 .OR. (::nTotOriginal > 99000 .AND. nValorTela > 99 )
        ::nRetrai := 35
        ::oDlgTelaSimulDesc:Refresh()
    EndIf

EndIf

Return

/*/{Protheus.doc} ValidaValor
Valida o valor digitado na tela de simulacao de desconto
@type       Method
@author     joao.marcos
@since      12/12/2025
@version    v1.0
@param  cTipo, tipo do desconto | V - Valor | P - Porcentagel
        nValorTela, Valor digitado na tela
/*/
Method ValidaValor(cTipo as Character, nValorTela as Numeric) as Logical Class LjSimuladorDesconto
Local lRet := .T. as Logical
Local nValorVendaSemImposto := (::nTotOriginal - ::nValorImpostoVenda) as Numeric

Do Case
    Case cTipo == "V" .AND. nValorTela >= nValorVendaSemImposto
        Help(" ", 1, "HELP", STR0001, STR0066,1,,,,,,, {STR0067 + Alltrim(Transform(nValorVendaSemImposto, X3Picture("LQ_VALBRUT")))}) // "Simulação de Desconto" # "O valor do desconto não pode ser igual ou maior que o valor das mercadorias sem os impostos" # "Informe um valor menor que R$ "
        lRet := .F.
    Case cTipo == "P" .AND. nValorTela > 99
        Help(" ", 1, "HELP", STR0001, STR0030,1,,,,,,, {STR0031}) // "Simulação de Desconto" # "Percentual de desconto não pode ser maior que 99%" # "Informe um valor menor ou igual a 99%."
        lRet := .F.
EndCase

Return lRet

/*/{Protheus.doc} GetLimitesSLF
Retorna os limites de desconto confugurados na tabela SLF
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  cCodCaixa, codigo do caixa ou superior
/*/
Method GetLimitesSLF(cCodCaixa as character) as array Class LjSimuladorDesconto
Local aLimitesSLF := {0,0}                    as Array

aLimitesSLF[1] := GetAdvFVal("SLF","LF_TOTDESP",xFilial('SLF') + cCodCaixa,1)
aLimitesSLF[2] := GetAdvFVal("SLF","LF_TOTDESV",xFilial('SLF') + cCodCaixa,1)

Return aLimitesSLF

/*/{Protheus.doc} AlertaLimite
Dispara alerta de limite de deconto excedido
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
@param  oPanelYellow, painel amarelo de alerta
        oSayAlertLimite, label de alerta
        oButtonAplicar, botao aplicar desconto
        oButtonAprovacao, botao aprovacao do superior'
/*/
Method AlertaLimite( oPanelYellow as object, oSayAlertLimite as object, oButtonAplicar as object, oButtonAprovacao as object) as Variant Class LjSimuladorDesconto

If ::cTipoPermissaoDesconto == "S"
    If !::lLimiteExcedido
        oPanelYellow:lVisibleControl        := .F.
        oSayAlertLimite:lVisibleControl     := .F.
        oButtonAprovacao:lVisibleControl    := .F.
        oButtonAplicar:lVisibleControl      := .T.
    ElseIf ::lLimiteExcedido
        oPanelYellow:lVisibleControl        := .T.
        oSayAlertLimite:lVisibleControl     := .T.
        oButtonAprovacao:lVisibleControl    := .T.
        oButtonAplicar:lVisibleControl      := .F.
    EndIf
ElseIf ::cTipoPermissaoDesconto == "X"
    oPanelYellow:lVisibleControl        := .T.
    oSayAlertLimite:lVisibleControl     := .T.
    oButtonAprovacao:lVisibleControl    := .T.
    oButtonAplicar:lVisibleControl      := .F.
Else
    oPanelYellow:lVisibleControl        := .T.
    oSayAlertLimite:lVisibleControl     := .T.
    oButtonAprovacao:lVisibleControl    := .F.
    oButtonAplicar:lVisibleControl      := .T.
EndIf

oPanelYellow:Refresh()   
oSayAlertLimite:Refresh()
oButtonAplicar:Refresh()
oButtonAprovacao:Refresh()

Return

/*/{Protheus.doc} DescriMotDesc
Retorna Descricao do motivo do desconto
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
/*/
Method DescriMotDesc() as Variant Class LjSimuladorDesconto
Local aAreaMDT := MDT->(GetArea())

DbSelectArea('MDT')
DbSetOrder(1)

If !DbSeek(xFilial("MDT") + ::cCodMotivo)
    Help(" ", 1, "HELP", STR0001, STR0023,1,,,,,,, {STR0024}) // "Simulação de Desconto" # "Este código de Motivo de Desconto não existe." # "Informe um código existente no Cadastro de Motivo de Desconto."
    ::cCodMotivo := Space(TamSx3("MDT_CODIGO")[1])
Else 
    ::cDescMotivo := MDT->MDT_DESCRI
EndIf 

RestArea(aAreaMDT)

Return

/*/{Protheus.doc} ConFirmaDesc
Confirma aplicacao do deconto
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
/*/
Method ConFirmaDesc(nValor as numeric) as Logical Class LjSimuladorDesconto
Local lRet := .T.

If !::ValidaConfirmacao(nValor)
    lRet := .F.
Else 
    ::lAplicar := .T.
EndIf

Return lRet

/*/{Protheus.doc} ValidaConfirmacao
Valida confirmacao do Desconto
@type       Method
@author     joao.marcos
@since      26/11/2025
@version    v1.0
/*/
Method ValidaConfirmacao(nValor as numeric) as Logical Class LjSimuladorDesconto
Local lRet := .T.   as Logical

If ::lMotivoDesc .AND. Empty(::cCodMotivo) .AND. nValor > 0
    Help(" ", 1, "HELP", STR0001, STR0027, 1, ,,,,,, {STR0028}) // "Simulação de Desconto" # "Motivo do desconto não informado." # "Informe o código do motivo de desconto."
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} ZeraDescontoTotal
Zera desconto no total
@type       Method
@author     joao.marcos
@since      28/11/2025
@version    v1.0
/*/
Method ZeraDescontoTotal() as Variant Class LjSimuladorDesconto
    LJ7VldDesc(, 0,,.T.,, .F.)
    ::nTotOriginal  := LJ7T_Total(2)
    ::nTotSimulado  := ::nTotOriginal
Return

/*/{Protheus.doc} Impostos
Verifica se a venda pssui Impostos, IPI, ICMS-ST
@type       Method
@author     joao.marcos
@since      28/11/2025
@version    v1.0
/*/
Method Impostos() as Variant Class LjSimuladorDesconto
Local nX        := 0                        as Numeric

For nX := 1 To Len(::aItensVenda)
    If !::aItensVenda[nX][Len(::aItensVenda[nX])] .AND. MaFisRet(nX,"IT_VALSOL") > 0 .OR. MaFisRet(nX,"IT_VALIPI") > 0
        ::lImpostos := .T. 
        Exit
    EndIf
Next nX

Return

/*/{Protheus.doc} RetItensVenda
Retorna os itens da venda para exibição na tela de Simulação de Desconto ou Aprovação do Superior/Gerente
@type       Method
@author     joao.marcos
@since      09/11/2025
@version    v1.0
@param  nOp, opcao | 1 - Itens para tela de Simulação de Desconto | 2 - Itens para tela de Aprovação do Superior/Gerente
/*/
Method RetItensVenda(nOp as Numeric) as Array Class LjSimuladorDesconto
Local aNewItensVenda    := {}   as Array
Local nX                := 0    as Numeric

Local nPosItem      := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_ITEM"})][2]		// Posicao do item
Local nPosProd		:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2]	// Posicao da codigo do produto
Local nDescrProd	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_DESCRI"})][2]		// Posicao da codigo do produto
Local nPosQtda  	:= aPosCpo[Ascan(aPosCpo,{|x| Alltrim(Upper(x[1])) == "LR_QUANT"})][2]		// Posicao da quantidade
Local nPosVrUnit	:= aPosCpo[Ascan(aPosCpo,{|x| Alltrim(Upper(x[1])) == "LR_VRUNIT"})][2]		// Posicao do Valor unitario
Local nPosVlItem	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VLRITEM"})][2]	// Posicao do Valor Total do Item
Local nPosUnidade	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_UM"})][2]		    // Posicao do Unidade
Local nPosValDesc	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VALDESC"})][2]	// Posicao do valor de desconto
Local nPosPercDesc	:= aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_DESC"})][2]		// Posicao do Percentual de desconto

If nOp == 1 // Itens para tela de Simulação de Desconto
    For nX := 1 To Len(::aItensVenda)
        If !::aItensVenda[nX][Len(::aItensVenda[nX])]
            Aadd(aNewItensVenda,{::aItensVenda[nX][nPosItem]   , ::aItensVenda[nX][nPosProd]   , ::aItensVenda[nX][nDescrProd],;
                                ::aItensVenda[nX][nPosQtda]   , ::aItensVenda[nX][nPosVrUnit] , ::aItensVenda[nX][nPosVlItem],;
                                ::aItensVenda[nX][nPosUnidade], ::aItensVenda[nX][nPosValDesc], ::aItensVenda[nX][nPosPercDesc]})
        EndIf
    Next
ElseIf nOp == 2 // Itens para tela de Aprovação do Superior/Gerente
    For nX := 1 To Len(::aItensVenda)
        If !::aItensVenda[nX][Len(::aItensVenda[nX])]
            Aadd(aNewItensVenda,{::aItensVenda[nX][nDescrProd]  , ::aItensVenda[nX][nPosQtda]   , ::aItensVenda[nX][nPosVrUnit],;
                                ::aItensVenda[nX][nPosPercDesc], ::aItensVenda[nX][nPosValDesc], ::aItensVenda[nX][nPosVlItem],;
                                ::RetQuantEstoque(::aItensVenda[nX][nPosProd])})
        EndIf
    Next
EndIf

Return aNewItensVenda

/*/{Protheus.doc} DescontoItens
Calcula o percentual de desconto aplicado nos itens da venda
@type       Method
@author     joao.marcos
@since      09/11/2025
@version    v1.0
/*/
Method DescontoItens() as Variant Class LjSimuladorDesconto
Local nX            := 0                            as Numeric
Local nPosPerDes    := GdFieldPos("LR_DESC")        as Numeric  // Posicao do percentual de desconto
Local nPosVlrDes    := GdFieldPos("LR_VALDESC")     as Numeric  // Posicao do valor do desconto
Local nPosVlrItem   := GdFieldPos("LR_VLRITEM")     as Numeric // Posicao do valor do item
Local nVlItOrig     := 0                            as Numeric  // Valor original dos itens
Local nVlDesItem    := 0                            as Numeric  // Valor total de desconto dos itens
Local nPosProd	    := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2] as Numeric	// Posicao da codigo do produto
Local lItensValidos := .F.                         as Logical   // Flag para verificar se existem itens validos na venda

If Len( ::aItensVenda ) > 0 .AND. ( nPosPerDes > 0 .AND. nPosVlrDes > 0 )
		
    For nX := 1 To Len(::aItensVenda)
        If Empty(::aItensVenda[nX][nPosProd])
            ::lAbreTela := .F.
            Help(" ", 1, "HELP", STR0001, STR0071, 1, ,,,,,,{STR0072}) // "Simulação de Desconto" # "Venda possui linha de itens vazia." # "Não é possível aplicar desconto em vendas com itens vazios."
            LjGrvLog(,"LjSimuladorDesconto | Venda possui linha de itens vazia, a tela de Simulacao de Desconto nao sera aberta", ::lAbreTela)
        ElseIf !::aItensVenda[nX][Len(aCols[nX])]
            nVlItOrig  += ::aItensVenda[nX][nPosVlrItem]
            nVlDesItem += ::aItensVenda[nX][nPosVlrDes]
            lItensValidos := .T.
        EndIf
    Next nX
 
    ::nPerDescItens := 100 - ((nVlItOrig / (nVlItOrig + nVlDesItem)) * 100) 

    If !lItensValidos
        ::lAbreTela := .F.
        MsgInfo(STR0073, STR0001) // "Venda não possui itens válidos para aplicar desconto." # "Simulação de Desconto"
    EndIf

EndIf

Return

/*/{Protheus.doc} AprovacaoSuper
Aciona a etapa de aprovação do desconto pelo Superior/Gerente
@type       Method
@author     joao.marcos
@since      08/11/2025
@version    v1.0
/*/
Method AprovacaoSuper(nValor as numeric) as Variant Class LjSimuladorDesconto

If nValor > 0
    ::oDlgTelaSimulDesc:Hide()

    ::TelaAprovacaoSuperior()
    ::oDlgTelaAprovacaoSuper:Activate()

    If ::lAprovadoSuper
        ::lAplicar := .T.
        ::oDlgTelaSimulDesc:End()  
        Iif(ValType(::oDlgTelaAprovacaoSuper) == "O", ::oDlgTelaAprovacaoSuper:End(), )
    Else
        ::lAplicar := .F.
        Iif(ValType(::oDlgTelaAprovacaoSuper) == "O", ::oDlgTelaAprovacaoSuper:End(), )
        ::oDlgTelaSimulDesc:Show()
    EndIf
Else
    ::oDlgTelaSimulDesc:End()
EndIf

Return

/*/{Protheus.doc} TelaAprovacaoSuperior
Tela de aprovação do desconto pelo Superior/Gerente
@type       Method
@author     joao.marcos
@since      08/11/2025
@version    v1.0
/*/
Method TelaAprovacaoSuperior() as Variant Class LjSimuladorDesconto
Local oFontP            := TFont():New(,,-11,,.F.)       as Object
Local oFontBoldG        := TFont():New(,,-16,,.T.)      as Object
Local oBrowse           := Nil                          as Object
Local nJanAltu          := 370                          as Numeric
Local nJanLarg          := 1100                         as Numeric
Local lDimPixels        := .T.                          as Logical
Local cTitulo           := STR0038                      as Character //"Regra de Desconto aplicada nos Itens"

Local oSayResumoFin01   := Nil                                  as Object
Local oSayResumoFin02   := Nil                                  as Object    
Local oSayVlrTotal01    := Nil                                  as Object
Local oSayMotivo01      := Nil                                  as Object
Local oSayMotivo02      := Nil                                  as Object
Local oSayVlrComDesc01  := Nil                                  as Object
Local oSayVlrDesc01     := Nil                                  as Object
Local oSayVlrSemDesc01  := Nil                                  as Object
Local oSayVlrSemDesc02  := Nil                                  as Object
Local oSayLimiteDesc    := Nil                                  as Object

Local oPanelResumoFin   := Nil                                  as Object
Local nClrBackPn1       := RGB(245,245,245)                     as Numeric
Local oButtonFechar     := Nil                                  as Object

Local aBrowse := ::RetItensVenda(2)

Local oSayDadosCli01   := Nil                                  as Object
Local oSayDadosCli02   := Nil                                  as Object
Local oSayDadosCli03   := Nil                                  as Object

Local oButtonAporvarSuper  := Nil                              as Object

Local cDDDCli   := Alltrim(GetAdvFVal("SA1","A1_DDD" ,xFilial("SA1") + M->LQ_CLIENTE )) as Character

::cNomeCliente  := Alltrim(GetAdvFVal("SA1","A1_NOME",xFilial("SA1") + M->LQ_CLIENTE ))
::cTelCliente   := Iif(!Empty(cDDDCli), "(" + cDDDCli + ") ", "") + Alltrim(GetAdvFVal("SA1","A1_TEL" ,xFilial("SA1") + M->LQ_CLIENTE ))

If Len(aBrowse) > 0
    ::oDlgTelaAprovacaoSuper := TDialog():New(0, 0, nJanAltu, nJanLarg, cTitulo, , , , , , /*BackGrd*/, , , lDimPixels)

    oPanelResumoFin:= tPanel():New(10,10,,::oDlgTelaAprovacaoSuper,/*Font*/,.T.,,,nClrBackPn1, 260/*L*/,140/*A*/)
    oSayResumoFin01 := TSay():New(10,10,{|| STR0010},oPanelResumoFin,, ::oFontBoldG,,,,.T./*10*/,,/*BackGrd*/,90,10) // "Resumo Financeiro"
    oSayResumoFin01:SetCSS("QWidget { font-weight: bold ;}")
    oSayResumoFin02 := TSay():New(25,10,{|| STR0011},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,200,10) // "Valores que serão praticados na venda após aplicação do desconto, considerando os impostos."    

    oSayVlrSemDesc01 := TSay():New(40,10,{|| STR0012 + Iif(::lImpostos," (" + STR0039 + ")","")},oPanelResumoFin,,/*font*/,,,,.T./*10*/,,/*BackGrd*/,200,10) // "Sem desconto" + "Mercadorias + Impostos"
    oSayVlrSemDesc01:SetCSS("QWidget { font-weight: bold ;}")
    oSayVlrSemDesc02 := TSay():New(40,194, {|| STR0007 + " " + Alltrim(Transform(::nTotOriginal, X3Picture("LQ_VALBRUT")))},oPanelResumoFin,,/*::Font*/,,,,.T./*10*/,,/*BackGrd*/,200,10) // "R$ "    
    oSayVlrSemDesc02:SetCSS("QWidget { font-weight: bold ;}")

    oSayVlrDesc01 := TSay():New(50,10,{|| STR0013 + Space(88) + "-" + STR0007 + " " + Alltrim(Transform(::nVlrDesc + ::nVlrImposto,X3Picture("LR_VALDESC"))) + " (" + AllTrim(Str(::nPercDesc)) + "%)" },oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,250,10) // "Valor do desconto"
    oSayVlrDesc01:SetCSS("QWidget { color: #980909; font-weight: bold ;}")

    @ 60,10 TO 61,250 PIXEL OF oPanelResumoFin

    oSayVlrComDesc01 := TSay():New(65,10,{|| STR0014 + Space(95) + STR0007 + " " + Alltrim(Transform(::nTotSimulado, X3Picture("LQ_VALBRUT")))},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,300,10) // "R$"
    oSayVlrComDesc01:SetCSS("QWidget { font-weight: bold ;}")

    @ 60,10 TO 61,250 PIXEL OF oPanelResumoFin

    oSayVlrTotal01 := TSay():New(80,10,{|| STR0040 + Space(98) + STR0007 + " " + Alltrim(Transform(::nTotSimulado, X3Picture("LQ_VALBRUT")))},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,250,10) // "Total a pagar" # "R$"
    oSayVlrTotal01:SetCSS("QWidget { font-weight: bold ;}")

    // Motivo do Desconto
    oSayMotivo01 := TSay():New(100,10,{|| STR0041},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,200,10) // "Motivo"
    oSayMotivo01:SetCSS("QWidget { font-weight: bold ;}")
    oSayMotivo02 := TSay():New(110,10,{|| ::cDescMotivo},oPanelResumoFin,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,200,10)    

    @ 123, 10 TO 135, 200 PIXEL OF oPanelResumoFin
    oSayLimiteDesc := TSay():New(126,15,{|| STR0065 + Alltrim(Transform(IIF(Len(::aMaxDescSuper) > 0, ::aMaxDescSuper[3], 0), X3Picture("LQ_DESCONT")))  + " (" + AllTrim(Str(IIF(Len(::aMaxDescSuper) > 0, ::aMaxDescSuper[1], 0))) + " %)"},oPanelResumoFin,,/*::oFontBoldP*/,,,,.T./*10*/,,,200,10) // "Limite de desconto para Supervisor: "
    oSayLimiteDesc:SetCSS("QWidget { font-weight: bold ;}")

    @ 10,275 TO 110,545 PIXEL OF ::oDlgTelaAprovacaoSuper

    oSayItens := TSay():New(20, 280,{|| STR0042},::oDlgTelaAprovacaoSuper,,oFontBoldG,,,,.T./*10*/,,/*BackGrd*/,50,10) // "Itens"

    oBrowse := TCBrowse():New(35, 280, 260/*L*/, 70/*A*/,,  {AllTrim(RetTitle("LR_PRODUTO")), AllTrim(RetTitle("LR_QUANT")), STR0043, STR0044, STR0045, STR0046, STR0047},; // "Produto", "Quantidade", "Preço Unit.", "Desc. (%)", "Desc. R$", "Valor a pagar", "Qtd. Estoque"
                                                    {35,28,30,30,30,45,30}, ::oDlgTelaAprovacaoSuper,,/*10*/,,,{||},,oFontP,,,,,.F.,/*21*/,.T.,,.F.,,, )
    oBrowse:SetArray(aBrowse)
    oBrowse:bLine := {|| {  aBrowse[oBrowse:nAt][1]                                                 ,; // 01 - Descricao
                            AllTrim( Transform(aBrowse[oBrowse:nAt][2], X3Picture("LR_QUANT")   ) ) ,; // 02 - Quantidade
                            AllTrim( Transform(aBrowse[oBrowse:nAt][3], X3Picture("LR_VRUNIT")  ) ) ,; // 03 - Preço Unitario
                            AllTrim( Transform(aBrowse[oBrowse:nAt][4], X3Picture("LR_DESC")    ) ) ,; // 04 - Desconto em %  
                            AllTrim( Transform(aBrowse[oBrowse:nAt][5], X3Picture("LR_VALDESC") ) ) ,; // 04 - Desconto em R$                          
                            AllTrim( Transform(aBrowse[oBrowse:nAt][6], X3Picture("LR_VLRITEM") ) ) ,; // 05 - Valor do Item
                            AllTrim( Transform(aBrowse[oBrowse:nAt][7], X3Picture("B2_QATU")    ) ) ,; // 06 - Qtd. Estoque
                        }}

    oBrowse:lHScroll    := .T.
    oBrowse:lJustific   := .T.

    @ 115,275 TO 150,545 PIXEL OF ::oDlgTelaAprovacaoSuper

    oSayDadosCli01 := TSay():New(125,290,{||STR0048},::oDlgTelaAprovacaoSuper,,oFontBoldG,,,,.T./*10*/,,/*BackGrd*/,100,10) // "Dados do cliente"
    oSayDadosCli01:SetCSS("QWidget { font-weight: bold ;}")
    oSayDadosCli02 := TSay():New(135,290,{|| SubStr(::cNomeCliente,1,60) },::oDlgTelaAprovacaoSuper,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,150,10)
    oSayDadosCli03 := TSay():New(135,470,{|| ::cTelCliente  },::oDlgTelaAprovacaoSuper,,/*Font*/,,,,.T./*10*/,,/*BackGrd*/,50,10)

    @ 160,00 TO 161,555 PIXEL OF ::oDlgTelaAprovacaoSuper

    oButtonFechar := TButton():New( 165, 460, STR0021, ::oDlgTelaAprovacaoSuper, {|| ::lAprovadoSuper := .F., ::LimpaSenhaSuper(), ::oDlgTelaAprovacaoSuper:End() },40,015,,,.F.,.T.,.F.,,.F.,,,.F.) // "Voltar"
    oButtonFechar:SetCSS("QWidget { color: #ffffffff; background-color: #00659A; }")
    oButtonFechar:cToolTip := STR0049 // "Clique para voltar para a tela de Simulação de Desconto"

    oButtonAporvarSuper := TButton():New( 165, 505, STR0064, ::oDlgTelaAprovacaoSuper,{|| ::ValidaAprovSuper() },40,15,,,.F./*10*/,.T.,.F.,,.F.,{||.T.},,.F.) // "Aprovar"
    oButtonAporvarSuper:SetCSS("QWidget { color: #ffffffff; background-color: #099842; }")
    oButtonAporvarSuper:cToolTip := STR0050 // "Clique para aprovar o desconto."

EndIf

Return

/*/{Protheus.doc} RetQuantEstoque
Retorna a quantidade em estoque do produto
@type       Method
@author     joao.marcos
@since      09/11/2025
@version    v1.0
@param  cCodProd, codigo do produto
/*/
Method RetQuantEstoque(cCodProd as Character) as Numeric Class LjSimuladorDesconto
Local nEstoque := 0 as Numeric
Local aAreaSB2 := SB2->(GetArea())

SB2->(dbSeek(xFilial("SB2") + cCodProd))
nEstoque := SaldoSb2()

RestArea(aAreaSB2)

Return nEstoque

/*/{Protheus.doc} LimpaSenhaSuper
Limpa a senha do Superior/Gerente após a aprovação do desconto
@type       Method
@author     joao.marcos
@since      11/11/2025
@version    v1.0
/*/
Method LimpaSenhaSuper() as Variant Class LjSimuladorDesconto
    LjLimpStat()
Return

/*/{Protheus.doc} ValidaAprovSuper
Valida se o Superior/Gerente pode aprovar o desconto
@type       Method
@author     joao.marcos
@since      11/11/2025
@version    v1.0
/*/
Method ValidaAprovSuper() as Logical Class LjSimuladorDesconto
Local lRet := .F. as Logical // Retorno da validação

lRet := LJ7_ValDesc(::nTipoDesc, ::nPercDesc, ::nVlrDesc, .T.)

Iif( !lRet, Help(" ", 1, "HELP", STR0001, STR0063, 1, ,,,,,, {""}), ) // "Valor do desconto não permitido para o caixa atual."

::lAprovadoSuper := lRet

If lRet
    ::oDlgTelaAprovacaoSuper:End()
EndIf

Return lRet

/*/{Protheus.doc} showResumo
Resumo dos descontos aplicados na venda
@type       Method
@author     Bruno Almeida
@since      09/12/2025
@version    v1.0
/*/
Method showResumo() as Variant Class LjSimuladorDesconto

    Local oTelaResumo     := Nil
    Local oSayTitulo      := Nil
    Local oSayDescItem    := Nil
    Local oSayDescTotal   := Nil
    Local oSayTotalGeral  := Nil
    Local oSayValorTotal  := Nil
    Local oGroupDescItem  := Nil
    Local oGroupDescTotal := Nil
    Local oGroupLinha     := Nil
    Local oBrowseItens    := Nil
    Local oBrowseTotal    := Nil
    Local oButtonFechar   := Nil
    Local aBrowseItens    := {} 
    Local aBrowseTotal    := {} 

    ::FormataItens()

    If !Empty(::aItensResumo[1][1]) .or. !Empty(::aTotalDesc[1][1])

        aBrowseItens := ::aItensResumo
        aBrowseTotal := ::aTotalDesc

        oTelaResumo := TDialog():New(0, 0, 455, 1000, STR0053, , , , , , /*CorFundo*/, , , .T.) //"Descontos aplicados"
        oSayTitulo  := TSay():New(10,10,{|| STR0054},oTelaResumo,,TFont():New(,,-13,,.F.),,,,.T./*10*/,,/*BackGrd*/,200,10) // "Veja todos os descontos aplicados neste atendimento."

        //-- Grupo 1 Inicio - Desconto no item
        oGroupDescItem := TGroup():New(25,10,95,491,'',oTelaResumo,,,.T.)
        oSayDescItem   := TSay():New(29,16,{|| STR0055},oGroupDescItem,,::oFontBoldM,,,,.T./*10*/,,/*BackGrd*/,200,10) //"Desconto no item"

        oBrowseItens   := TCBrowse():New( 40, 16, 469, 49,, {AllTrim(RetTitle("LR_ITEM")), AllTrim(RetTitle("LR_PRODUTO")), STR0016, AllTrim(RetTitle("LR_QUANT")), STR0017, STR0068, STR0056, STR0057},; //"Descrição" # "Desconto" # "Vendedor"
                                                            {20,30,110,35,35,35,35,60}, oGroupDescItem,,,,,{||},,,,,,,.F.,/*21*/,.T.,,.F.,,, )

        oBrowseItens:SetArray(aBrowseItens)
        oBrowseItens:bLine := {|| {  aBrowseItens[oBrowseItens:nAt][1]                                                ,; // 01 - Item
                                aBrowseItens[oBrowseItens:nAt][2]                                                     ,; // 02 - Cod Produto
                                aBrowseItens[oBrowseItens:nAt][3]                                                     ,; // 03 - Descricao
                                AllTrim( Transform(aBrowseItens[oBrowseItens:nAt][4]  , X3Picture("LR_QUANT"  ) ) )   ,; // 04 - Quantidade
                                AllTrim( Transform(aBrowseItens[oBrowseItens:nAt][5]  , X3Picture("LR_VRUNIT" ) ) )   ,; // 05 - Preço Unitario
                                AllTrim( Transform(aBrowseItens[oBrowseItens:nAt][6]  , X3Picture("LR_VLRITEM") ) )   ,; // 06 - Vlr. Item
                                AllTrim( Transform(aBrowseItens[oBrowseItens:nAt][7]  , X3Picture("LR_VALDESC") ) )   ,; // 07 - Desconto em R$
                                AllTrim( aBrowseItens[oBrowseItens:nAt][8] )                                          ,; // 08 - Vendedor
                            }}

        oBrowseItens:lHScroll  := .T.
        oBrowseItens:lJustific := .T.
        //-- Grupo 1 Fim - Desconto no item

        //---------------------------------------------------------------------------------------------

        //-- Grupo 2 Inicio - Desconto no total
        oGroupDescTotal := TGroup():New(105,10,160,491,'',oTelaResumo,,,.T.)
        oSayDescTotal   := TSay():New(109,16,{|| STR0004},oGroupDescTotal,,::oFontBoldM,,,,.T.,,,200,10) // "Desconto no total"

        oBrowseTotal    := TCBrowse():New( 121, 16, 469, 35,, {STR0016, STR0056, STR0057},; //"Descrição" # "Desconto" # "Vendedor"
                                                            {50,65,35}, oGroupDescTotal,,,,,{||},,,,,,,.F.,/*21*/,.T.,,.F.,,, )

        oBrowseTotal:SetArray(aBrowseTotal)
        oBrowseTotal:bLine := {|| {  aBrowseTotal[oBrowseTotal:nAt][1]                                                ,; // 01 - Desconto no total
                                AllTrim( Transform(aBrowseTotal[oBrowseTotal:nAt][2]  , X3Picture("LQ_DESCONT") ) )   ,; // 02 - Desconto
                                aBrowseTotal[oBrowseTotal:nAt][3]                                                     ,; // 03 - Vendedor
                            }}

        oBrowseTotal:lHScroll  := .F.
        oBrowseTotal:lJustific := .F.
        //-- Grupo 2 Fim - Desconto no total

        //---------------------------------------------------------------------------------------------

        oSayTotalGeral := TSay():New(172,380,{|| STR0058},oTelaResumo,,::oFontBoldM,,,,.T.,,,100,10) //"Total de descontos"
        oSayValorTotal := TSay():New(172,450,{|| STR0007 + " " + AllTrim( Transform(::nTotalGeral, X3Picture("LR_VLRITEM") ) )},oTelaResumo,,TFont():New(,,-14,,.F.),,,,.T.,,,100,10) // "R$"

        oGroupLinha    := TGroup():New(190,10,191,491,'',oTelaResumo,,,.T.)

        oButtonFechar  := TButton():New( 196, 448, STR0059, oTelaResumo, {|| oTelaResumo:End() },45,20,,,.F.,.T.,.F.,,.F.,,,.F.) // "Fechar"
        oButtonFechar:SetCSS("QWidget { color: #00659A; background-color: #ffffffff; }")

        oTelaResumo:Activate(, , , .T., , , {||})

        FWFreeObj(oTelaResumo)
        FWFreeObj(oSayTitulo)
        FWFreeObj(oSayDescItem)
        FWFreeObj(oSayDescTotal)
        FWFreeObj(oSayTotalGeral)
        FWFreeObj(oSayValorTotal)
        FWFreeObj(oGroupDescItem)
        FWFreeObj(oGroupDescTotal)
        FWFreeObj(oGroupLinha)
        FWFreeObj(oBrowseItens)
        FWFreeObj(oBrowseTotal)
        FWFreeObj(oButtonFechar)
    Else
        MsgInfo(STR0060,STR0061)  //"Não foi informado desconto na venda!" # "Venda Assistida"
    EndIf

    FwFreeArray(aBrowseItens)
    FwFreeArray(aBrowseTotal)

Return Nil

/*/{Protheus.doc} formataItens
Formata os itens para serem apresentados na tela de resumo.
@type       Method
@author     Bruno Almeida
@since      09/12/2025
@version    v1.0
/*/
Method formataItens() as Variant Class LjSimuladorDesconto

    Local nI           := 0 as Numeric    
    Local nPosItem     := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_ITEM"})][2]
    Local nPosProd     := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_PRODUTO"})][2]
    Local nDescrProd   := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_DESCRI"})][2]
    Local nPosQtda     := aPosCpo[Ascan(aPosCpo,{|x| Alltrim(Upper(x[1])) == "LR_QUANT"})][2]
    Local nPosVrUnit   := aPosCpo[Ascan(aPosCpo,{|x| Alltrim(Upper(x[1])) == "LR_VRUNIT"})][2]
    Local nPosVlItem   := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VLRITEM"})][2]
    Local nPosValDesc  := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VALDESC"})][2]
    Local nPosVendedor := 0
    Local lLrVendedor  := Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VEND"}) > 0
    Local cVendedor    := ""
    Local nDescVenda   := Lj7T_DescV(2)

    ::nTotalGeral := 0

    If lLrVendedor
        nPosVendedor := aPosCpo[Ascan(aPosCpo,{|x| AllTrim(Upper(x[1])) == "LR_VEND"})][2]
    EndIf

    For nI := 1 To Len(::aItensVenda)

        If ::aItensVenda[nI][nPosValDesc] > 0 .AND. !::aItensVenda[nI][Len(::aItensVenda[nI])]

            If nPosVendedor > 0
                cVendedor := IIF(!Empty(::aItensVenda[nI][nPosVendedor]), ::aItensVenda[nI][nPosVendedor], M->LQ_VEND)
            Else
                cVendedor := M->LQ_VEND
            EndIf

            ::nTotalGeral += ::aItensVenda[nI][nPosValDesc]

            aAdd(::aItensResumo,{::aItensVenda[nI][nPosItem]   ,; // 1 - Item
                                ::aItensVenda[nI][nPosProd]    ,; // 2 - Codigo do produto
                                ::aItensVenda[nI][nDescrProd]  ,; // 3 - Descricao do produto
                                ::aItensVenda[nI][nPosQtda]    ,; // 4 - Quantidade
                                ::aItensVenda[nI][nPosVrUnit]  ,; // 5 - Valor unitario
                                ::aItensVenda[nI][nPosVlItem]  ,; // 6 - Valor do item
                                ::aItensVenda[nI][nPosValDesc] ,; // 7 - Valor do desconto
                                ::getVendedor(cVendedor)       ,; // 8 - Vendedor
                                })
        EndIf
    Next nI

    If nDescVenda > 0
        aAdd(::aTotalDesc,{STR0062, nDescVenda, ::getVendedor(M->LQ_VEND) }) //"Desconto no total"
        ::nTotalGeral += nDescVenda
    EndIf


    If Len(::aItensResumo) == 0
        aAdd(::aItensResumo,{" " ,; // 1 - Item
                            " " ,; // 2 - Codigo do produto
                            " " ,; // 3 - Descricao do produto
                            0   ,; // 4 - Quantidade
                            0   ,; // 5 - Valor unitario
                            0   ,; // 6 - Valor do item
                            0   ,; // 7 - Valor do desconto
                            " " ,; // 8 - Vendedor
                            })
    EndIf

    If Len(::aTotalDesc) == 0
        aAdd(::aTotalDesc,{" ", 0, " " })
    EndIf

Return Nil

/*/{Protheus.doc} getVendedor
Retorna o nome do vendedor
@type       Method
@author     Bruno Almeida
@since      09/12/2025
@version    v1.0
@param  cCodigo, codigo do vendedor
/*/
Method getVendedor(cCodigo as Character) as Character Class LjSimuladorDesconto

Local cVendedor as character

cVendedor := Posicione("SA3", 1, xFilial("SA3") + padR(cCodigo, TamSx3("A3_COD")[1]), "A3_NOME")

Return cVendedor


/*/{Protheus.doc} permissaoDesconto
Verifica a configuração sobre a permissão de desconto do usuario caixa.
@type       Method
@author     Bruno Almeida
@since      16/12/2025
@version    v1.0
/*/
Method permissaoDesconto() as Variant Class LjSimuladorDesconto

Local cCodCaixa  := GetAdvFVal("SA6","A6_COD",xFilial('SA6') + cUserName,2)    as Character
Local cPermissao := GetAdvFVal("SLF","LF_ACESSO",xFilial('SLF') + cCodCaixa,1) as Character

If SubStr(cPermissao, 11, 1) == "S"
    ::cTipoPermissaoDesconto := "S"
    ::cTextoPermissao        := STR0015 //"Atenção! Limite de desconto excedido, necessário aprovação do superior."
ElseIf SubStr(cPermissao, 11, 1) == "X"
    ::cTipoPermissaoDesconto := "X"
    ::cTextoPermissao        := STR0069 //"Atenção! Desconto na venda permitido somente com a aprovação do superior."
Else
    ::cTipoPermissaoDesconto := "N"
    ::cTextoPermissao        := STR0070 //"Atenção! Usuário não tem permissão para conceder desconto na venda."
EndIf

Return Nil
