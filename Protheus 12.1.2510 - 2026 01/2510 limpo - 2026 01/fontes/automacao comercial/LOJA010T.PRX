#INCLUDE "PROTHEUS.CH" 
#INCLUDE "LOJA010T.CH"
#INCLUDE "AUTODEF.CH"

// ------------------------------------------------
// Status de transmissao dos cartoes do TEF DIRECAO  

	#DEFINE DTEF_PENDENTE		  "PENDENTE"
	#DEFINE DTEF_CONFIRMADA		  "CONFIRMADA"
	#DEFINE DTEF_CANCELADA		  "CANCELADA"
	#DEFINE DTEF_DESFEITA		  "DESFEITA"
	#DEFINE DTEF_NAO_TRANSMITIDA  "NAO_TRANSMITIDA"
	#DEFINE _FORMATEF			  "CC;CD" 				//Formas de pagamento que utilizam operação TEF para validação	

	/* Array aCartoes
	aCartoes[1]  -> Numero de parcelas do cartão.
	aCartoes[2]  -> Valor da transacao do cartao
	aCartoes[3]  -> Tipo de cartao CC/CD/009 - "Recarga de Celular"
	aCartoes[4]  -> Indica se o cartão processou OK
	aCartoes[5]	 -> Numero da transacao (nSeqArq)
	aCartoes[6]	 -> Nome da operadora
	aCartoes[7]	 -> Numero do DOC
	aCartoes[8]	 -> Status da transacao (PENDENTE | CONFIRMADA | CANCELADA | NAO_TRANSMITIDA)
	aCartoes[9]	 -> nI do aTefDisc utilizado
	aCartoes[10] -> Data da operacao
	aCartoes[11] -> Hora da operacao
	aCartoes[12] -> Campo 027 - Finalizacao 
	aCartoes[13] -> Autorizacao da transacao TEf 
	aCartoes[14] -> Id do Parcelamento L4_FORMAID	
	*/

	/* Array aTefDados - validar e adicionar para ter todos os campos documentados
	aTefDados[1] -> Indica que fez operacap TEF
	aTefDados[2] -> data no formato DDMMAAAA
	aTefDados[3] -> hora no formato HHMMSS
	aTefDados[4] -> vem preenchido mas necessita identificar o que é
	aTefDados[5] -> Autorização
	aTefDados[6] -> documento de cancelamento
	aTefDados[7] -> hora do cancelamento
	aTefDados[8] -> Rede
	aTefDados[9] -> Numero do Host
	aTefDados[10]-> L1_TIPCART (segundo abaixo - no fonte)
	aTefDados[11]-> Rede
	aTefDados[12]-> data do cancelamento
	aTefDados[13]-> numero do cartão - não é mais gravado
	aTefDados[14]-> Array de 14 posicoes
	aTefDados[15]-> (identificar)
	aTefDados[16]-> (identificar)
	aTefDados[17]-> (identificar)
	aTefDados[18]-> Banda do Cartao
	aTefDados[19]-> Parcelas
	aTefDados[20]-> Tipo : CC|CD	
	*/
	
// ---------------------------------------------------

// Matriz com as infomacoes para processamento do cancelamento TEF

#DEFINE __FORMPG	1
#DEFINE __INSTITU	2
#DEFINE __DOCTEF	3
#DEFINE __EMISNF	4

#DEFINE __TIPCART	5                                        
#DEFINE __CARTAO	6
#DEFINE __VLRDEBI	7
#DEFINE __DATATEF	8

#DEFINE __INFO 1
#DEFINE __STOP 2

#DEFINE __INFOCARD "INFOCARD;SOFTWAY"
#DEFINE __TECBAN   "TECBAN HOST HOST"
#DEFINE __CCS	   "C.C.S."

#DEFINE _PICTURE	13

#DEFINE TEF_NAOUTILIZA          "1"
#DEFINE TEF_SEMCLIENT_DEDICADO  "2"
#DEFINE TEF_COMCLIENT_DEDICADO  "3"
#DEFINE TEF_DISCADO             "4"
#DEFINE TEF_LOTE                "5"
#DEFINE LOG_DIR	            	"\AUTOCOM\LOG"+cEmpAnt+StrTran(Alltrim(cFilAnt)," ","")+"\"
#DEFINE TEF_DESFAZIMENTO		"12|20|21|22|24|25|26|28|30|32|38|50"	//Transacoes que necessitam de desfazimento
#DEFINE TEF_IMPTEFC				LJGetStation("IMPTEFC")					//Verifica se o cliente imprime consulta de cheques

/*Este Log é um recurso a ser habilitado pelo departamento de desenvolvimento para averigua-
ção de possíveis problemas de transações TEF.
NÃO ESTÁ HABILITADO NA VERSÃO 609 */
#DEFINE LOG_TEF LjLogTef()		// Log do TEF

// Variaveis de transacoes habilitadas
STATIC ltTCredit
STATIC ltCpParCD
STATIC ltCpPreCD
STATIC ltCpForCD
STATIC ltCpCD
STATIC ltCancCD
STATIC ltCancCC
STATIC ctTrans

// Variaveis configuradas pelo Sitef

STATIC ntLimDebito
STATIC ntParcCart
STATIC ntTaxaServ
STATIC ntTimeOut
STATIC ntNuParDeb
STATIC ntIntrParc
STATIC ltPrParVist
STATIC ltMesFech
STATIC ltNotaProm

// Variaveis de controle de transacao

STATIC ntCtrlTran  	:= 0
STATIC ntId        	:= 0
STATIC lCdc        	:= .F.
STATIC ntSeqTerm
STATIC cMens
STATIC aTicket
STATIC aPromis
STATIC _oDlg
STATIC oDlgTef

// Uso comum TEF Dedicado e TEF Discado

STATIC nParCred
STATIC nParDeb
STATIC nParDin
STATIC nParCheq
STATIC nDin
STATIC nCartao
STATIC nCheq
STATIC nDebito
STATIC dDtParDeb
STATIC nMilhas
STATIC nParMilh

// TEF Discado

STATIC aConf
STATIC cImpress
STATIC c030
STATIC c006
STATIC c007
STATIC nTipTEF
STATIC cSaida   	:= "I"
STATIC cBANESETril	:= ""								// No BANESECARD eh necessario capturar a Trilha 2 do Cartao

// Variaveis para rotinas CCS

STATIC ctDados
STATIC ctEntDoc
STATIC ctCodNet
STATIC ctNsu
STATIC ctDat_Pag
STATIC ctCarCCS
STATIC lCcs 		:= .F.
STATIC lInfoCard 	:= .F.

// Parametros do TEF

STATIC cDirT_Tx
STATIC cDirT_Rx

// Transacao corrente

STATIC cPTerm
STATIC cOpera
STATIC cTipVenCon
STATIC lDiscado    		:= (cTipTef == TEF_DISCADO)         		// TEF Discado
STATIC lCliente			:= (cTipTef == TEF_COMCLIENT_DEDICADO)		// TEF Dedicado com Client
STATIC lCPFInfo 		:= .F.  									// Se .T. faz Consulta EMS/CCS
STATIC nITmp			:=  0                                      // Numero da Transação corrente

// Variáveis para a versão 3.0 do SITEF
STATIC lVer300		:= .F.
STATIC aBlocoResp[3][14]
STATIC ctCVV2		:= ""
STATIC ctCashBack	:= ""
STATIC ctRedeDest	:= ""
STATIC ctADM		:= ""
STATIC ctGarantia	:= ""
STATIC ctCodAut		:= ""
STATIC lAceite 		:= .F.
STATIC lCancFile 	:= .F.
STATIC cDrvTEFDsc	:= "C" //Drive Padrão do TEF discado, existe um ponto de entrada para que possa ser alterado					
STATIC cPayGoExec	:= IIf(File(cDrvTEFDsc + ':\Arquivos de programas\SETIS\Pay&Go Cliente\PayGo Cliente GerPadr Socket.exe'),'PayGo Cliente GerPadr Socket.exe' , 'PGCliW32.exe')
STATIC aTefDisc		:= Nil  
STATIC lHomTEF		:= ExistFunc("LJHOMTEF") .AND. LJMSSM0(SM0->M0_CGC)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³LojA010T()³ Autor ³ Sergio Silveira       ³ Data ³ 09.06.97 ³±±
±±³		     ³ 	        ³	    ³ Luiz Vergari          ³      ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Vendas - Transferencia Eletronica de Fundos ( TEF )		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Loja010T( ExpC1, ExpC2, ExpA1 )                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 													  ³±±		
±±³		     ³	"V" - Venda												  ³±±
±±³		     ³	"C" - Consulta											  ³±±
±±³		     ³ 	"G" - Garantia					                          ³±±
±±³		     ³  "E" - Exclusao ( cancelamento )					          ³±±
±±³		     ³	"A" - Abertura					  						  ³±±
±±³		     ³  "X" - Cancelamento Automatico					 		  ³±±
±±³		     ³  "F" - Confirmacao					                      ³±±
±±³		     ³  "L" - Leitura da trilha2 do cartao                        ³±±
±±³		     ³					  										  ³±±
±±³		     ³ ExpC2 (Tipo de venda) 					                  ³±±
±±³		     ³	"C"   Tipo Consulta 					  				  ³±±
±±³		     ³	"N" 					 								  ³±±
±±³		     ³									 						  ³±±
±±³		     ³ ExpA3 (Array com variaveis da garantia de cheques ou 	  ³±±
±±³		     ³ 		  consulta de cheques)							      ³±±
±±³		     ³ ExpL4  ()                                                  ³±±
±±³		     ³ ExpL5  ()                                                  ³±±
±±³		     ³ ExpC6  (Forma de Pagamento utilizada para o Recebimento)   ³±±
±±³		     ³ ExpL7  Controla se ha transacao TEF pendente			      ³±±
±±³		     ³ ExpL8  Controla se nao confirmou o inicio da transacao TEF ³±±
±±³          | ExpL9  Indica se eh tratamento para recebimento de titulo  ³±±
±±³          | ExpA10 Array temporaria utilizada para impressao TEF.      ³±±
±±³          | ExpL11 Indica se foi chamado pelo processo de baixa(LOJA701³±±
±±³          | ExpL12 Indica se existe a Administradora cadastrada no SAE ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³ SIGALOJA 										          ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/                                   

/*/ TRANSACOES DISPONIVEIS ATUALMENTE NO SITEF                                                                           		
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³DEC  	HEX TRANSACAO                                                                                                   	³
³1		01	Abertura de terminal.                                                                                       	³
³2		02	Fechamento do terminal.                                                                                     	³
³10		0A	Consulta de Pendências.                                                                                     	³
³18		12	Transação de pré-autorização.                                                                               	³
³21		15	Consulta de bins                                                                                            	³
³22		16	Transação de Consulta AVS.                                                                                  	³
³32		20	Compra com cartão de débito	magnético.                                                                      	³
³34		22	Compra com cartão de débito pré-datado.                                                                     	³
³35		23	Cancelamento de compra com cartão	de débito.                                                              	³
³36		24	Compra com cartão de débito parcelada, primeira parcela à vista.                                            	³
³38		26	Compra com cartão de débito	parcelada, sem entrada.                                                         	³
³39		27	Consulta CDC                                                                                                	³
³40		28	Compra CDC                                                                                                     	³
³54		36	Cancelamento compra com cartão de crédito magnético.                                                           	³
³55		37	Cancelamento compra com cartão de crédito não magnético (digitado).                                            	³
³56		38	Compra com cartão de crédito magnético ou não magnético (com ou sem juros quando parcelada).                   	³
³63		3F	Consulta com cartão de Crédito Parcelado                                                                       	³
³112	70	Consulta Cheque Papel, Telecheque Garantido, Garantia de Cheque Papel e Cancelamento de Garantia Cheque Papel. 	³
³114	72	Consulta de cgc/cpf ao Serasa ou ACSP.                                                                         	³
³116	74	Consulta/Garantia do Telecheque.                                                                               	³
³119	77	Cancelamento de garantia e desconto garantido de cheques.                                                      	³
³120	78	Transação de Consulta ACSP.                                                                                    	³
³240	F0	Reimpressão (*)                                                                                                	³
³240	F0	Transação Finninvest (*)                                                                                       	³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
/*/
Function Loja010T(  cOper   , cVenCon	  	, aDados		, lConsulta   	,;
                   lImprime , cFPagReceb 	, lTemTEFPend	, lAbandonaTEF	,;
                   lTitulo	, aTefTmp		, lBaixa		, lSemRede		,;
                   cOrcamen	, lRelGer		, cMsgCupom 	, nVlrTotal		,;
                   cId800	, oWsTrn		, aDadosTrn		, lRecCel		,;
				   lTemTEFOk, lConfNF, aParcTef )

Local	lRet		:= .F.
Local   xRetorno	:= .F.
Local   oDlg
Local   nOp
Local   nParc
Local   lConsCdc	:= .F.
Local   nPos		:= 0                                                 	
Local 	nX			:= 0	
Local   nY          := 0
Local   lSintVisu	:= If( Type("lVisuSint")<>"U", lVisuSint , .F.)	//Tratamento para a nova identificação do cartão no Multi-TEF
Local   cMV_TEFPEND := SuperGetMV("MV_TEFPEND",,"0")         // Define o tratamento a ser realizado quando uma ou mais transacoes TEF ficam pendentes
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay 	:= !Empty(LjGetStation("DISPLAY"))
Local cArqTefPend 	:= GetClientDir() + "TEFPEND.TMP"	// Arquivo de transacoes TEF Pendentes

DEFAULT lImprime     	:= .T.
DEFAULT cFPagReceb   	:= ""
DEFAULT lTemTEFPend		:= .F. 
DEFAULT lAbandonaTEF 	:= .F.
DEFAULT lTitulo      	:= .F.
DEFAULT aTefTmp      	:= {}
DEFAULT lBaixa       	:= .F. 
DEFAULT lRelGer      	:= .F.   
DEFAULT cMsgCupom 	 	:= ""	// Mensagem do fechamento do cupom fiscal
DEFAULT nVlrTotal	 	:= 0	// Valor total da venda
DEFAULT cId800		 	:= ""	// Id transacao 800 
DEFAULT oWsTrn		 	:= Nil // Objeto Ws de Recarga /CB  
DEFAULT aDadosTrn	 	:= {}	// Dados da Transacao
DEFAULT lRecCel			:= .F.	// Transacao de Pagamento em cartão de uma recarga de celular
DEFAULT lTemTEFOk		:= .F.	// indica que existem transacoes confirmadas e elas serao reaproveitadas
DEFAULT lConfNF			:= .F.  //confirmação Nota Fiscal - venda Direta
DEFAULT aParcTef		:= {}   // Parcelas TEF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³TEF DEDICADO SEM CLIENT:                                                                                                                   ³
//³Utiliza o Agente IPDV e efetua as gravações nos diretórios \TEFTX\ e \TEFRX\ para efetuar a troca de dados                                 ³
//³                                                                                                                                           ³
//³TEF DEDICADO COM CLIENT:                                                                                                                   ³
//³Utiliza o Programa Client para interdace de solicitação de dados e os diretórios \CLIENT\REQ\ e \CLIENT\RESP\ para efetuar a troca de dados³
//³                                                                                                                                           ³
//³TEF DISCADO:                                                                                                                               ³
//³Utiliza os agentes das bandeiras e utiliza o diretório \TEF_DIAL\REQ e \TEF_DIAL\RESP para troca de informações                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cDirT_Tx 			:= LjGetStation("DIRTTX")
cDirT_Rx 			:= LjGetStation("DIRTRX")

If SuperGetMV("MV_TEFVERS") <> "03.00"
	Aviso("Atenção", "Esta versão do SITEF ("+AllTrim(SuperGetMV("MV_TEFVERS"))+") não é suportada e poderá causar inconsistências em suas operações de crédito e/ou débito. Corrija o parâmetro MV_TEFVERS para '03.00'. ", {"Ok"},,"Versão Não Suportada")
Endif

lVer300	    		:= (cTipTEF == TEF_SEMCLIENT_DEDICADO .AND. SuperGetMV("MV_TEFVERS") == "03.00")
lDiscado    		:= (cTipTef == TEF_DISCADO)         		// TEF Discado
lCliente			:= (cTipTef == TEF_COMCLIENT_DEDICADO)

If aTefDisc = NIL
	aTefDisc			:= L010LoadTef()
EndIf  

Private nMod	    := 3
Private nOpc 		:= 2
Private nTotal 		:= 0
Private cWork       := ""

If !Type("nMoedaCor") == "N"
	Private nMoedaCor := 1
Endif

If !Type("aTefDados") == "A"
	Private aTefDados := {}
Endif

If !Valtype(cVenCon) == "C"
	cVenCon := "V"
Endif

/* Se a operacao for de venda apaga o arquivo */
If cOper == "V" .AND. File(cArqTefPend) .AND. (Len(aTefDados)==0 .OR. nModulo==23)    
	FErase(cArqTefPend)
EndIf

cTipVenCon := cVenCon  

// Variaveis padrao para a montagem da mensagem (conforme tipo)
Private ctValor		:= ""
Private ctTrilha_2  
Private ctSenha 	:= ""
Private ctData		:= ""
Private ctMod_Entr
Private ctDocument_ctData
Private ctDad_Parc
Private ctPlano
Private ctTaxa
Private ctCartao
Private ctDat_Venc
Private ctCod_Autor
Private ctCCorrent
Private ctTipo_Pes
Private ctCgc_Cpf
Private ctBanco
Private ctAgencia
Private ctNum_Cheq
Private ctDt_ChQ_Ser
Private ctBco_Agenc
Private ctDt_Cheq_TelD
Private ctTipOp
Private ctTipPar
Private ctJur
Private ctParc_Val
Private ctTipVen
Private ctEntrada
Private ctTipCons 	:= "1"
Private ctDad_Chq
Private ctQCheque  	:= "1"
Private ctRg 		:= space(15)
Private ctOrcam
Private ctCompens 	:="018"
Private ctHoraVend 	:= ""
Private ctDat_Vend 	:= ""
Private ctRede_H	:= ctFuncao := ""
Private cDados   	:= space(80)		// Dados complementares da forma de pagamento para o SL4
Private ctCEP    	:= space(9)
Private ctEnd    	:= space(50)
Private ctNumero 	:= space(10)
Private ctCompl  	:= space(2)

// Diretorios de TX e RX
Private ctSucess
Private cSiTVers 	:= SuperGetMV( "MV_TEFVERS" )
Private nPTerm
Private cPIdentTerm
Private nOffSet
Private ntOffSet := 0 	// Este campo indica em que parte do campo de dados se encontra o número do cartão para o qual está sendo feita a operação de TEF. O seu conteúdo é binário e considera a primeira posição do campo de dados como 0.
Private dtDataAgen
Private nParcelas:=0
Private cModCart	:= " "
Private cInst		:= ""
Private cEmpresa  	:= SuperGetMV("MV_EMPTEF")

// Transações para serem executadas pelo cartao fininvest
Private lFininvest  := .F.
Private nConsRot    := 0

Private lTefMult	:= SuperGetMV("MV_TEFMULT",NIL,.F.)

Private cRegFim    := "0"

Private aCartCan	:= {}
// a Modalidade de Multplas transacoes TEF, so esta valida para a interface
// Venda Assistida ( LOJA701) e Abertura do terminal( cOper=A) e no cancelamento (cOper=X ou E)
If !LjVldMult(cOper, aDados)
   Return(.F.)
Endif

//Versao sitef mono-empresa colocar 00000000 na empresa
//Versao sitef multi-empresa colocar StrZero(Val(cEmpAnt),8) na empresa
ctSucess := "00"
If Empty(cEmpresa)
	cEmpresa:= StrZero(Val(cEmpAnt),8)
Endif
nTime := 1

If ! ValType( cOper ) == "C"
	cOper := "V"
Endif

If ValType(aDados) == "A" .AND. ValType(aDados[1]) == "A"
   lFininvest := ( Ascan(aDados,{|X| Upper(Alltrim(Subs(X[4],7)))=='FININVEST'}))<>0
Endif

If ValType(aDados) == "A" .AND. ValType(aDados[1]) == "A"
   lCcs := ( Ascan(aDados,{|X| At('C.C.S.',Upper(Alltrim(Subs(X[4],7))))<>0})<>0)
Endif

Private lAltera := .T.
// Campos para a Confirmacao de Transacao
Private ctDt_H ,ctNum_H,ctRede_H
cOpera := cOper
// Contemplar a opcao de reimpressao visa "RV"

If cOpera $ "SVCEXARQBD" .OR. cVenCon == "R" .OR. cVenCon == "RV"
	If !lTemTEFOk
		aTefDados := {{"","","","","","","","","","","","","",{},"","","","","",""}}
	EndIf
	lCdc := .F.
Endif

nPTerm 		:= LjGetStation("TERMTEF")

ltPrParVist := ltMesFech := ltNotaProm := .T.
cPIdentTerm := "MS" + StrZero( nPTerm, 6 )
cPTerm		:= StrZero( nPTerm, 3 )
aMens		:= { { "V", "Venda Tef" 			},; // 4 pernas - Enviar mensagens de conf e nao confirmacao
				 { "C", "Consulta Tef" 			},; // 4 pernas - Enviar mensagens de conf e nao confirmacao
				 { "G", "Garantia Tef" 			},; // 4 pernas - Enviar mensagens de conf e nao confirmacao
				 { "E", "Cancelamento TEF" 		},; // 4 pernas - Enviar mensagens de conf e nao confirmacao
				 { "A", "Abertura TEF" 			},; // 2 pernas
				 { "X", "Cancelamento TEF" 		},; // 4 pernas - Enviar mensagens de conf e nao confirmacao
			 	 { "F", "Confirma/Cancela" 		},; //   	
				 { "I", "Impressão cupom TEF" 	},; //
				 { "P", "Transações C.C.S." 	},; //
				 { "S", "Setup TEF" 			},; //
				 { "Q", "Operações Fininvest" 	},; // 4 pernas - Enviar mensagens de conf e nao confirmacao 
				 { "R", "Pré autorização"		},;  // 4 pernas - Enviar mensagens de conf e nao confirmacao 	
				 { "B", "Correspondente Bancario"},; // 4 pernas - Enviar mensagens de conf e nao confirmacao 
				 { "D", "Recarga Celular"		}}  // 4 pernas - Enviar mensagens de conf e nao confirmacao 	

// Se o TEF é Discado
IF lDiscado .OR. lCliente
	if !lVer300  
		Return L010VerGrP(aDados, cMsgCupom	, nVlrTotal	, cId800	,;
						  oWsTrn, @aDadosTrn, lRecCel	, lTemTEFOk	, ;
						  lConfNF, lTitulo, lBaixa, aParcTef )
	Endif
Endif
nReg     	:= aScan( aMens, { |x| x[1] == cOpera } )
cMens       := aMens[ nReg, 2 ]
ntSeqTerm	:= 1
If ExistBlock("LJTEFPARC")
	LjGrvLog(Nil, "Ponto de Entrada LJTEFPARC - Parametro :", aDados)
	aDados	:=	ExecBlock("LJTEFPARC",.F.,.F.,aDados)
	LjGrvLog(Nil, "Ponto de Entrada LJTEFPARC - Retorno :", aDados)	
Endif

If Alltrim(cOpera) $ "V|C|I"
	nTotal := 0
	If cOpera == "V"
		nParCred  := 0
		nParMilh  := 0
		nParDeb   := 0
		// Inicializacao dos totalizadores de valor para CCS
		nParDin   := 0
		nParcheq  := 0
		nDin      := 0
		nCartao   := 0
		nCheq     := 0
		nDebito   := 0
		nMilhas   := 0
		nEntrada  := 0
		dDtParDeb := CTOD("")
		If lTefMult
			aTefMult  := {}
			For nParc := 1 To Len(aDados)
				cMoeda := AllTrim(aDados[nParc][3])
				If ("MH" $ cMoeda).AND.Len(aDados)>1 
				   Loop
				Endif
				If cMoeda==AllTrim(MVCHEQUE)
                    // Banco+Agencia+Conta
					cAdm   := AllTrim(aDados[nParc][5])+AllTrim(aDados[nParc][6])+AllTrim(aDados[nParc][7])
                Else
    				// ID Cartão+Administradora
					If lSintVisu
						cAdm   := Alltrim(aDados[nParc][6])+Space(4-Len(AllTrim(aDados[nParc][6])))+' - '+AllTrim(Subs(aDados[nParc][4],7))
					Else
					// Dígitos do Cartão+Administradora
						cAdm   := StrZero(Val(aDados[nParc][6]),4)+' - '+AllTrim(Subs(aDados[nParc][4],7))
					Endif						
                Endif
                // Possibilidade de venda para cartoes diferentes com a mesma bandeira
    	        // nPos  := Ascan(aTefMult,{|X| X[1]==cMoeda.AND.Alltrim(Upper(X[2]))==Alltrim(Upper(cAdm)).AND.X[6][1]<>aDados[nParc][1]})
    	        nPos   := Ascan(aTefMult,{|X| X[1]==cMoeda .AND. Alltrim(Upper(X[2]))==Alltrim(Upper(cAdm))})
	            If nPos=0
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³aTefMult[1]Moeda,                    ³
					//³        [2]Administradora,           ³
					//³        [3]Entrada,                  ³
					//³        [4]Total da Transação,       ³
					//³        [5]Número parcelas}          ³
					//³        [6][1]Data PrimeiraParcela   ³
					//³        [6][2]Data Ultima Parcela    ³
					//³        [6][3]Intervalo entre parcela³
					//³        [7]Resposta da Transação     ³
					//³        [8]Resultado da Transação    ³
					//³        [9]Cconfirmacao da Transacao ³
					//³        [10]Numero da Parcel no Apgtos³
					//³        [11]Texto de Promissoria     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	               aadd(aTefMult,{cMoeda,cAdm,0,0,0,{aDados[nParc][1],ctod(''),0},{},.F.,.F.,{},{}})
	               nPos:=Len(aTefMult)
	            Endif
                // Entrada
				If aDados[nParc][1] == dDataBase
					nEntrada += aDados[nParc][2]
					aTefMult[nPos][3]:=aDados[nParc][2]
				Endif
				//Total da Transacao
				aTefMult[nPos][4]+=aDados[nParc][2]
				// Numero de parcelas
				aTefMult[nPos][5]+=1
	            //Inserindo o numero da parcela correspondente ao Apgtos
                If cMoeda==AllTrim(MVCHEQUE)
					aadd(aTefMult[nPos][10],StrZero(aDados[nParc][8],2))
	            Else
					aadd(aTefMult[nPos][10],StrZero(aDados[nParc][5],2))
				Endif	
                // Data da primeira parcela
				If aTefMult[nPos][6][1] > aDados[nParc][1]
				   aTefMult[nPos][6][1]:=aDados[nParc][1]
				Endif
                // Data da ultima parcela
				If aTefMult[nPos][6][2] < aDados[nParc][1]
				   aTefMult[nPos][6][2]:=aDados[nParc][1]
				Endif
                // Intervalo entre as parcelas
				If aTefMult[nPos][5]=2
				   aTefMult[nPos][6][3]:=aTefMult[nPos][6][2]-aTefMult[nPos][6][1]
				Endif
				If ("$" $ cMoeda)
					nDin+=aDados[nParc][2]
                Endif
				nTotal += aDados[nParc][2]
			Next nParc
		Else
			For nParc := 1 To Len(aParcTef)
				cMoeda := AllTrim(aParcTef[nParc][3])
				If aParcTef[nParc][1] == dDataBase
					nEntrada += aParcTef[nParc][2]
				Endif
				If ("$" $ cMoeda)
					nDin+=aParcTef[nParc][2]
				ElseIf ("MH" $ cMoeda)
					nMilhas+=aParcTef[nParc][2]
					nParMilh++
				ElseIf ("CC" $ cMoeda)
					nCartao+=aParcTef[nParc][2]
					nParCred++
				ElseIf ("CH" $ cMoeda)
					nCheq+=aParcTef[nParc][2]
					nParCheq++
				ElseIf ("CD" $ cMoeda)
					nDebito+=aParcTef[nParc][2]
					nParDeb++
					if Empty(dDtParDeb) .OR. dDtParDeb < aParcTef[nParc][1]
						dDtParDeb := aParcTef[nParc][1]
					Endif
				Endif
				nTotal += aParcTef[nParc][2]
			Next nParc
		Endif
		If (nCartao > 0) .AND. (cOpera == "V")
			cTipVenCon:= "CT"

		ElseIf (nCheq > 0) .AND. (cOpera == "V") .AND. SuperGetMV("MV_INTEEMS")
			cTipVenCon:= "CH"

		Else
			If (nDebito > 0) .AND. (cOpera == "V")
				If (nParDeb > 1)
					lCdc := ('CDC' $ Upper(Alltrim(SE4->E4_DESCRI)))
					If lCdc
						nOp  := 1
						DEFINE MSDIALOG oDlg TITLE "Operação CDC"  From 13,21 to 18,50  of GetWndDefault()
						@ 10,005 BUTTON "Consulta"  SIZE 50,15 OF oDlg PIXEL ACTION ( nOp:=1,oDlg:End() )
						@ 10,060 BUTTON "Venda"   	SIZE 50,15 OF oDlg PIXEL ACTION ( nOp:=2,oDlg:End() )
						ACTIVATE MSDIALOG oDlg CENTERED
						// A variável lConsCDC era usada somente nas transações de consulta
						// Agora a mesma passou a ser usada na Venda, caso a transação, seja uma
						// transação de consulta CDC
						nConsRot    := nOp
						lConsCdc    := (nOp=1)
						lConsCdcCmp := .T.
					Endif
					cTipVenCon:= "DA"      // Parcelado
				Else
					If (nEntrada > 0)
						cTipVenCon:= "DV"   // A Vista
					Else
						cTipVenCon:= "DR"   // Pre-Datada
					Endif
				Endif
			Endif
		Endif
	Endif
	ctValor	  := AllTrim( Str( nTotal * 100, 14 ) )
ElseIf (cOpera == Nil)
	ctValor	  := Str(0,12,2)
	cOpera	  := "V"
Elseif cOpera == "P"  // Rotina de Pagamento da CCS / INFOCARD
	aTefDados := { {	"", "", "", "", ;
						"", "", "", "", ;
						"", "", "", "", ;
						"", {}, "", "", ;
						"", "", "", ""} }
Endif
ctMod_Entr  := "0"
ctTrilha_2	:= ""
ctCartao 	:= Space( 19 )
ctDat_Venc	:= Space( 4 )
ctTaxa		:= ""

While .T.
	
	// Se Sitef nao aberto, apaga todos os arquivos ( Tx e Rx ) desse PDV
	If (cOper <> "F") .AND. (cOper <> "L")
		If !ltTefAberto
		aEval( Directory( cDirT_Tx + "*." + cPTerm ),;
		{ |aFile| FErase( cDirT_Tx + aFile[ 1 ] ) } )
		aEval( Directory( cDirT_Rx + "*." + cPTerm ),;
		{ |aFile| FErase( cDirT_Rx + aFile[ 1 ] ) } )
			ntTimeOut       := 20
			If !L010TefAbre()
				xRetorno := .F.
				Exit
			Endif
			
		Endif
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³HOMOLOGACAO: Verificar quais as operacoes que exigem a confirmacao³
	//³ou seja quais transacoes possuem as quatro pernas, para as que    ³
	//³não possuirem deveremos mandar o desfazimento                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If At(cOpera,"F#I")=0 .AND. IIf(cMV_TEFPEND $ "1|2" .AND. lTemTEFPend,.F.,!L010ConfTrn())
//		MsgInfo("Não foi possivél efetivar transação TEF pendente")
		xRetorno := .F.
		Exit
	Endif

	If (cOpera == "A")
		xRetorno := .T.
		Exit
	Endif
	
	If (cOpera == "L")
		xRetorno := If( L010LeCartao(), ctTrilha_2, "" )
		Exit
	Endif
	
	If (cOpera == "V")
		If (ValType( cTipVenCon ) <> "C")
			nMod:=3
			DEFINE MSDIALOG oDlg TITLE "Modalidade do cartão"  From 13,21 to 18,64  of GetWndDefault()
			@ 10,005 BUTTON "Crédito"  SIZE 50,15 OF oDlg PIXEL ACTION ( nMod:=2,oDlg:End() )
			@ 10,060 BUTTON "Débito"   SIZE 50,15 OF oDlg PIXEL ACTION ( nMod:=1,oDlg:End() )
			@ 10,115 BUTTON "Abandona" SIZE 50,15 OF oDlg PIXEL ACTION ( nMod:=3,oDlg:End() )
			ACTIVATE MSDIALOG oDlg CENTERED
			
			// Verifica se e cartao de credito ou cartao de debito
			
			If nCartao > 0
				nMod:= 2
			Else
				If nDebito > 0
					nMod:= 1
				Endif
			Endif
			
			If nMod == 1
				nTipVen:=4
				DEFINE MSDIALOG oDlg TITLE "Modalidade do cartão"  From 13,21 to 18,80  of GetWndDefault()
				@ 10,005 BUTTON "À Vista"		SIZE 40,15 OF oDlg PIXEL ACTION ( nTipVen:=1,oDlg:End() )
				@ 10,050 BUTTON "Pré-Datada"	SIZE 40,15 OF oDlg PIXEL ACTION ( nTipVen:=2,oDlg:End() )
				@ 10,095 BUTTON "Parcelada"		SIZE 40,15 OF oDlg PIXEL ACTION ( nTipVen:=3,oDlg:End() )
				@ 10,140 BUTTON "CDC"          	SIZE 40,15 OF oDlg PIXEL ACTION ( nTipVen:=4,oDlg:End() )
				@ 10,185 BUTTON "Abandona"   	SIZE 40,15 OF oDlg PIXEL ACTION ( nTipVen:=5,oDlg:End() )
				ACTIVATE MSDIALOG oDlg CENTERED
				
				// Chama scripts de venda conforme escolha
				
				Do Case
					Case nTipVen == 1
						xRetorno := L010AVista( )
					Case nTipVen == 2
						xRetorno := L010PreDat( )
					Case (nTipVen == 3 .OR. nTipVen == 4) .AND. nParDeb > 1
						If nTipVen == 4
							lCdc:= .T.
						Endif
						
						xRetorno := L010Parcel( .F. , @lConsulta)
					OtherWise
						lRet := .F.
				EndCase
			ElseIf nMod == 2
				xRetorno := L010Credito(,aDados, @lSemRede, @cOrcamen)
			Endif
		Else
			If !Lojaok("Deseja prosseguir com a operação TEF ?") //"Confirma Venda pelo TEF ?"
				HELP("",1,"LJABANDTEF")
				nOp := 0
				xretorno := .F.
				lAbandonaTEF := .T.
				Exit
			Else
				nOp := 1
			Endif
			If nOp == 1
				// Valida a existencia dos campos na tabela SL4, Necesario para Multiplas transacoes TEF
				If !Lj010TSL4()
				    nOp 	 := 0
					xRetorno := .F.
					exit
				Endif
	            For nX := 1 to If( !lTefMult, 1, Len(aTefMult) )
				   If lTefMult
                        // Inicializa todas as variaveis private
						L010TCVar()
	                    lFininvest := At('FININVEST',Upper(Alltrim(aTefMult[nX][2])))<>0
                   		If aTefMult[nX][1]=='CC'
							nCartao   :=aTefMult[nX][4]
							nParCred  :=aTefMult[nX][5]
    	               		cTipVenCon:='CT'
                   		ElseIf aTefMult[nX][1]=='MH' // Pagamento com milhas
							nMilhas   :=aTefMult[nX][4]
							nParMilh  :=aTefMult[nX][5]
    	               		cTipVenCon:='CT'
		                ElseIf aTefMult[nX][1]=='CH'
							nCheq     :=aTefMult[nX][4]
    	               		cTipVenCon:='CH'
    	           		ElseIf aTefMult[nX][1]=='CD'
							nDebito   :=aTefMult[nX][4] 	 // Valor da Transacao
							nParDeb   :=aTefMult[nX][5]  	 // Numero de parcelas
							dDtParDeb :=aTefMult[nX][6][2]  // Data da Ultima parcela
    	           		    If aTefMult[nX][5]>1
    	               		   cTipVenCon:="DA"      // Parcelado
			   				   lCdc := ('CDC'$Upper(Alltrim(SE4->E4_DESCRI)))
							   If lCdc
									nOp  :=Aviso( "Operação CDC", "Tipo da operação", {"&Consulta", "&Venda"},, "Selecione" )
									// A variável lConsCDC era usada somente nas transações de consulta
									// Agora a mesma passou a ser usada na Venda, caso a transação, seja uma
									// transação de consulta CDC
									nConsRot    := nOp
									lConsCdc    := (nOp=1)
									lConsCdcCmp := .T.
								Endif
    	           		    ElseIf aTefMult[nX][5]=1 .AND. aTefMult[nX][6][1]==dDataBase
			           			cTipVenCon:= "DV"   // A Vista
    	          			ElseIf aTefMult[nX][5]=1 .AND. aTefMult[nX][6][1]<>dDataBase
							    cTipVenCon:= "DR"   // Pre-Datada
							Endif    
				   		Else
				   		    Loop
				   		Endif
				   Else
				   		If Upper(Alltrim(aDados[nX, 3])) == "CH"
				   			cTipVenCon := "CH"
				   		Endif
                   Endif
    			   While .T.
	    			   Do Case

					    	Case cPaisLoc == "MEX"
								 //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								 //³Efetua a Transacao TEF dos Cartoes de Credito e Debitos³
								 //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                                 If cTipVenCon$"DV|DA|CT"
									xRetorno :=L010Mex(cOper,nX,aDados)
					    	     Else
					    	       MsgInfo(STR0029) //"Transacao Invalida!"
					    	       Exit
					    	     Endif
					    	Case cTipVenCon == "DV"
						    	xRetorno 	:= L010AVista(nX)
							Case cTipVenCon == "DA"
								xRetorno 	:= L010Parcel(lConsCdc,@lConsulta,nX)
							Case cTipVenCon == "DR"
								xRetorno 	:= L010PreDat(nX)
							Case cTipVenCon == "CT"
								xRetorno 	:= L010Credito(nX, aDados, @lSemRede, @cOrcamen)
							Case cTipVenCon == "CH" .AND. SuperGetMV("MV_INTEEMS") .AND. SuperGetMV("MV_TEFMILH",NIL,.F.)
   								lCPFInfo   	:= .T.
								xRetorno 	:= L010Cheque(nX,aDados,@cOrcamen)
							Case cTipVenCon == "CH" .AND. At("S",LjGetStation("TEFCONS")) <> 0
   								lCPFInfo   	:= .T.        
								xRetorno 	:= L010Servicos("C",.F.,@lConsulta,nX)
							OtherWise
								Exit
					   EndCase
					   If lTefMult
				    		If !xRetorno //.AND.!lConsCDC
				    			If !lSemRede
					    	 		If MsgYesNo( "Deseja efetuar novamente a transação TEF?")
							        	Loop
	    	          				EndIf
						        Endif   
	    	        			If !lTemTEFPend
		    	                	lAbandonaTEF := .T.
		        	      		Endif
	                	        Return( xRetorno )
	            			Endif   
			              aTefMult[nX][8]	:= xRetorno
			              lTemTEFPend		:= aTefMult[nX][8]
					   Endif	   
					   Exit
				   End
				Next nX
			Endif
		Endif

		If At("S",LjGetStation("TEFCONS"))<>0 .AND. nCheq>0 .AND. !lCPFInfo
			For nY := 1 To Len(aDados)
				If aDados[nY][03] == "CH"
					xRetorno := L010Servicos("C",.F.,@lConsulta,nY)
				Endif
			Next nY
		Endif
		
	ElseIf cOpera == "F"
		lConf := If( cTipVenCon == "S", .T., .F. )
		//HOMOLOGACAO: Adicionado um novo parametro para sabermos se devemos enviar o desfazimento ao inves de Nao Confirmacao
		xRetorno := L010Confirma( lConf , , cTipVenCon )
		If !Empty(LOG_TEF)
			LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "1) Confirmação com lConf = " + Iif(lConf,".T.",".F." ) )
		Endif						
		
	ElseIf cOpera == "I"
		cTipVenCon := If(Empty(cTipVenCon),"V",cTipVenCon)
		
		If cTipVenCon == "R" .OR. cTipVenCon == "RV"
			// ReImpressao cupom TEF
			xRetorno:=L010Reimp(aTicket,aPromis,@lConsulta,cTipVenCon)
		Else
  			If lUsaDisplay
   				DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do cupom TEF " )         
   				DisplayEnv(StatDisplay(), "2C" + " ")         
			Endif	
			// ### "Aguarde ... imprimindo o cupom TEF "
			LjMsgRun(STR0065,,{ || xRetorno := L010TefImpr(	aTicket		, aPromis		, .T.   	,@lConsulta ,;
															cFPagReceb	, lTemTEFPend	, @lRelGer 	)})
															
			If !Empty(LOG_TEF)
				LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "Impressão do Cupom TEF " + Iif(xRetorno,"Ok","Problema") )
			Endif						
			
		Endif
		
	Elseif cOpera == "P" // Rotinas da CCS / InfoCard
		Do case
			case cTipVenCon == "PG" .OR. cTipVenCon == "PC"
				lInfoCard:= (cTipVenCon == "PC")
				xRetorno := L010CCSPG(aDados)						// Efetua o pagamento de titulos com cartao CSS ou INFOCARD
			case cTipVenCon == "CA" .OR. cTipVenCon == "CC"			
				lInfoCard:= (cTipVenCon == "CC")
				xRetorno := L010CCSCA(aDados)						// Efetua o cancelamento de pagamentos feitos com o CSS ou INFOCARD
			case cTipVenCon == "CO" .OR. cTipVenCon == "CS" 		
				xRetorno := L010CCSCO(@aDados,lImprime,cTipVenCon)  // Efetua a consulta dos dados do cartao CSS
			case cTipVenCon == "AM" .OR. (cTipVenCon == "AO" .AND. !lCCS .AND. SuperGetMV("MV_TEFMILH",NIL,.F.))
				xRetorno := L010CCSAM(aDados)						// Aquisicao de Milhas com outras forma de pagamento
			case cTipVenCon == "EM" .AND. SuperGetMV("MV_TEFMILH",NIL,.F.)
				xRetorno := L010CCSEM()								// Estorno de Aquisicao de Milhas com outras forma de pagamento
			case cTipVenCon == "AO" .AND. lCCS
			    lCCS:=.F.											// Ultimo passo da transação com Milhas, inicia a varivel para a proxima trn.
		Endcase
	ElseIf (cOpera == "R") .AND. lVer300
		L010PreAutoriz(cVenCon)
	ElseIf (cOpera == "Q") .AND. lVer300
		L010Finin(cVenCon)
	ElseIf (cOpera == "E" .OR. cOpera == "X") .AND. lVer300 .AND. lTefMult
        xRetorno := L010CTefMult(	cOpera, lTemTEFPend, lAbandonaTef, lTitulo, ;
        							@aTefTmp, lBaixa ) //Multiplos cancelamento para a rotina de devolucao ("X")
	Else
		xRetorno := L010Servicos(cOpera,lImprime,@lConsulta)
	Endif
	Exit
End
Return xRetorno

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010Credito  ³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a venda com cartao de credito						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010Credito( [ <ExpA1> ] ) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ [ <ExpA1> ] - Elemento 1 -> Taxa de Juros da Admin ou Esta.³±±
±±³			 ³ 			   - Elemento 2 -> Indica se Consiste Dig do Cart.³±±
±±³			 ³ 		   	   - Elemento 3 -> Grava ou nao o Num do Cart Visa³±±
±±³			 ³ [ <ExpL1> ] - Retorna se existe a Administradora cadastrada³±±
±±³			 ³				 no arquivo SAE.							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Venda com sucesso ou nao						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³03/10/01  ³ XXXXX³MMancio Se o parâmetro: MV_INTEEMS, estiver preenchi-³±±
±±³          ³      ³        do com True. O Sistema, também, solicitará  a³±±
±±³          ³      ³        digitação do CPF.                            ³±±
±±³----------³------³--------³--------------------------------------------³±±
±±³12/11/01  ³ XXXXX³MMancio ³ Acrescentado o Cartão SOFTWAY, similar ao  ³±±
±±³          ³      ³        ³ Cartão Infocard.                           ³±±
±±³----------³------³--------³--------------------------------------------³±±
±±³10/04/07  ³121570³Danilo  ³ Qdo o campo E4_JURCART estiver vazio, rea- ³±±
±±³          ³      ³Calil   ³ liza a pergunta "Quem ira cobrar juros...?"³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Static Function L010Credito(nX, aDados, lSemRede	, cOrcamen)

Local lRet				:= .T.
Local nTipCar			:=  4										//Tipo de cartão utilizado 1-Mag, 2-NaoMag, 3-CPF, 4-Abandona
Local nPlano			:=  1
Local oDlg
Local nOpc1
Local cTrans
Local nOpd
Local aRet
Local lContinua	 		:= .T.
Local lLerPin			:= .F.
Local nTentar			:= 0
Local nFormaDeb    		:= 0										//Número de parcelas do cartao

Local oVlrCap,nVlrCap 	:= 0
Local oCodAUT,cCodAut 	:= Space(9)
Local oNsu,cNsu 		:= Space(9)
Local oData,cData		:= Space(5)
Local octCartao
Local octDat_Venc
Local cNumCupFis 		:= '0000000'
Local nJurTef       	:= 2
Local lCartaoMH     
Local cMsgTit   		:= "Cartão "
Local lSintVisu			:= If(SL4->(ColumnPos("L4_FORMAID"))>0 .AND. Type("lVisuSint")<>"U",lVisuSint,.F.)	//Tratamento para a nova identificação do cartão no Multi-TEF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

If lTefMult .AND. nX<>Nil
   If lSintVisu	
   	   //Nova mensagem com ID do Cartão		
	   cMsgTit   += AllTrim(Subst(aTefMult[nX][2],1,4))
   Else
   	   //Mensagem com os 4 Ultimos Dígitos do Cartão
	   cMsgTit   += AllTrim(Subst(aTefMult[nX][2],8))+If(Val(aTefMult[nX][2])>0," Final "+Subst(aTefMult[nX][2],1,4),"")
   Endif
Endif

// Verifica se o cartao utilizado e o InfoCard
dbSelectArea("SAE")
dbSetOrder(1)
If lTefMult .AND. nX <> Nil
	dbSeek(xFilial("SAE") + Alltrim(Left(aDados[nX][4], At("-", aDados[nX][4])-1)) , .T. )
Else
	dbSeek(xFilial("SAE") + Alltrim(Left(aDados[1][4], At("-", aDados[1][4])-1)) , .T. )
EndIf
If (Upper(AllTrim(SAE->AE_DESC)) $ __INFOCARD )
	lInfoCard:= .T.
	cTrans    := "F0"
	ctDat_Venc:= Space( 7 )
Else
	lInfoCard:= .F.
	ctDat_Venc:= Space( 4 )
Endif
If Type("aCartaoMH") == "U"
	aCartaoMH :={}
Endif

ctCartao 			:= Space( 19 )
ctPlano				:= "1"
ctTrilha_2			:= ""
ctOrcam             := cOrcamen
ctData              := StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+;
Str(Year(dDatabase),4)
ctHoraVend          := SubStr(Time(),1,2) + SubStr(Time(),4,2) + SubStr(Time(),7,2)
ctValor := AllTrim( StrTran( Str( Round( nCartao, 2 ),12,2 ), ".", "" ) )

If lTefMult
	nEntrada  := aTefMult[nX][3] 												// aTefMult[nX][3] -> Valor de Entrada
	nCartao   := aTefMult[nX][4] 												// aTefMult[nX][4] -> Valor da operacao
	nParCred  := aTefMult[nX][5] 												// aTefMult[nX][5] -> Numero de Parcelas
	lCCs      := At('C.C.S.',Upper(Alltrim(aTefMult[nX][2])))<>0 				// aTefMult[nX][2] -> Administradora
	nFormaDeb := If(aTefMult[nX][5]>1,2,If(aTefMult[nX][6][2]=dDataBase,0,1)) 	// aTefMult[nX][5] -> Numero de Parcelas // aTefMult[nX][6][2] -> Data da Ultima Parcelas
Else
	If Type("aParcTEF") == "A"
		nFormaDeb := If(Len(aParcTEF)>1,2,If(aParcTEF[1][1]=dDataBase,0,1))
	Endif
	nEntrada  := nTotal
Endif	
// Se deve solicitar a leitura do cartao, se usa o que foi definido no inicio do 
// rotina quando usa MV_TEFMILH = .T.
lCartaoMH     := Empty(aCartaoMH).OR.( Len(aCartaoMH)>0.AND.aCartaoMH[1]==4).OR.!lCCS

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Na condição negociaa tem que selecionar quem ira cobrar os juros do financiamento a Administradora ou a Loja        |
//| Caso contrario a condição de pagamento cadastrada define quem ira cobrar                                            |
//| "A"dministradora (1 parcela c/Juros) "E"mpresa (n parcelas s/Juros) "P"ro Rata                                      |
//| Se for à Vista o Juros é pela Loja...                                                                               |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nFormaDeb == 2										// Cartão parcelado
	If Upper(FunName()) == "FRTA010" .AND. cItemCond = "CN"
		//"Juros "+"Quem irá cobrar juros pelo financiamento?"+"&Adm"+"&Loja"+"Selecione"
		nJurTef		:= Aviso( STR0091 + Alltrim(cMsgTit) , STR0092 , { STR0093 , STR0094 },, STR0095 )
		ctTipPar 	:= If( nJurTEF == 1, "3", "4" )
	ElseIf Alltrim(SL1->L1_CONDPG) == "CN"
		//"Juros "+"Quem irá cobrar juros pelo financiamento?"+"&Adm"+"&Loja"+"Selecione"
		nJurTef		:= Aviso( STR0091 + Alltrim(cMsgTit) , STR0092 , { STR0093 , STR0094 },, STR0095 )
		ctTipPar 	:= If( nJurTEF == 1, "3", "4" )
	Else
		If !Empty(SE4->E4_JURCART)
			ctTipPar 	:= If( SE4->E4_JURCART == "1", "3", "4" )
			ctTipPar 	:= If( SE4->E4_JURCART == "3", "5", ctTipPar )
		Else
			//"Juros "+"Quem irá cobrar juros pelo financiamento?"+"&Adm"+"&Loja"+"Selecione"
			nJurTef		:= Aviso( STR0091 + Alltrim(cMsgTit) , STR0092 , { STR0093 , STR0094 },, STR0095 )
			ctTipPar 	:= If( nJurTEF == 1, "3", "4" )				
		EndIf
	Endif	
Else
	ctTipPar := "4"
Endif

ctJur 	   := "0"
ctParc_Val := "0"

If !ltTCredit
	Aviso( "Atenção", "Compra com cartão de crédito", {"&Ok"},, "Transação não está habilitada." )
	//MsgStop("Esta transação não está habilitada.","Compra com cartão de crédito")
	lRet := .F.
Else
	If Empty( LjGetStation("PINPAD") )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Caso a execução seja feita através do CALL CENTER o tipo do cartão será não magnético  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTipCar := 2
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se aCartaoMh, não estiver vazioo significa que o cliente     ³
		//³trabalha com Milhas e indentifica os cliente no inicio       ³
		//³da operação de venda.                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCartaoMH
			If SuperGetMV("MV_INTEEMS")
				DEFINE MSDIALOG oDlg TITLE cMsgTit FROM 13,21 TO 18,78 OF GetWNDDefault()
				@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
				@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
				@ 10,115 BUTTON "CPF"	    	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
				@ 10,170 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
			Else
				DEFINE MSDIALOG oDlg TITLE cMsgTit FROM 13,21 TO 18,64 OF GetWNDDefault()
				@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
				@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
				@ 10,115 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
			Endif
			ACTIVATE MSDIALOG oDlg CENTERED
		Endif
	Endif
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se aCartaoMh, não estiver vazio  significa que o cliente     ³
//³trabalha com Milhas e indentifica os cliente no inicio       ³
//³da operação de venda.                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCartaoMH)>0 .AND. aCartaoMH[1]<>4 .AND. lCCS
  nTipCar  := aCartaoMH[1]
  ctCartao := aCartaoMH[2]
  nOpd     := 1
Endif         

cModCart   := If( nTipCar == 1, "M", "N" )
ctTipOp	   := Str( nTipCar, 1 )

// Se vai fazer a venda manual
If (nTipCar == 4)
	HELP("",1,"LJABANDTEF")
	lRet:= .F.
Endif

While ltTCredit .AND. nTipCar <> 4
	// Calcula o numero de parcelas
	nOpc1 := 3
	nPlano:= nParCred
	If lInfoCard
		If (nPlano == 1)
			ctPlano:= ""
		Else
			ctPlano := SE4->E4_PLANO
		Endif
	Else
		If (nPlano > 1)
			ctPlano := Alltrim( Str( nPlano, 2 ) )
		Endif
	Endif
	If (nTipCar == 1)		
		// Se tipo de cartao = magnetico, fazer a leitura do cartao
		If lInfoCard
			ctFuncao  := "10"
			ctTipOp   := "1"
			ctDat_Vend:= ""
		Else
			If lVer300
				cTrans	 := "38"
			Else
				cTrans	 := "30"
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se aCartaoMh, não estiver vazioo significa que o cliente     ³
		//³trabalha com Milhas e indentifica os cliente no inicio       ³
		//³da operação de venda.                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCartaoMH
			nOpd := If( L010LeCartao("C",nX), 1, 3 )
		Endif	
	ElseIf (nTipCar == 2)
		// Se tipo de cartao = nao magnetico, coletar o numero
		If lInfoCard
			ctFuncao  := "10"
			ctTipOp   := "2"
			ctDat_Vend:= ""
		Else
			If lVer300
				cTrans    := "38"
			Else
				cTrans    := "32"
			Endif
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se aCartaoMh, não estiver vazioo significa que o cliente     ³
		//³trabalha com Milhas e indentifica os cliente no inicio       ³
		//³da operação de venda.                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCartaoMH
			nOpd := 3
			If lUsaDisplay
	 			DisplayEnv(StatDisplay(), "1C" + Upper("Dados do Cartao") )           
	 			DisplayEnv(StatDisplay(), "2E" + "Informe o no do cartao:" )
			Endif				
			DEFINE MSDIALOG oDlg TITLE cMsgTit FROM 13,21 TO 21,60 OF GetWNDDefault()
			@	.074,.1 TO 4,19
			@ 06,12 SAY		"Informe o número do cartão:"	SIZE 130,10 OF oDlg PIXEL
			@ 06,88 MSGET	octCartao VAR ctCartao			SIZE 055,10 OF oDLG PIXEL PICTURE "@!" VALID ! EMPTY( ctCartao )
				octCartao:bGotFocus  := {|| If (lUsaDisplay, DisplayEnv(StatDisplay(), "2E" + "Informe o no do cartao:" ), )}     
				octCartao:bLostFocus := {|| If (lUsaDisplay, DisplayEnv(StatDisplay(), "2E" + "Informe o no do cartao:" + ctCartao), )} 

			@ 18,12 SAY		If(lInfoCard,"Informe a data do cartão:","Informe a validade:") SIZE 130, 10 OF oDlg PIXEL
 			@ 18,88 MSGET	octDat_Venc VAR ctDat_Venc	SIZE 024,10 OF oDlg PIXEL PICTURE If(lInfoCard,"@R 99/9999","@R 99/99") VALID L010ValidDt( ctDat_Venc )
 				octDat_Venc:bGotFocus  := {|| If (lUsaDisplay, DisplayEnv(StatDisplay(), "2E" + (	If(lInfoCard,"Informe a data do cartao:","Informe a validade:")) ), )} 
  				octDat_Venc:bLostFocus := {|| If (lUsaDisplay, DisplayEnv(StatDisplay(), "2E" + (	If(lInfoCard,"Informe a data do cartao:","Informe a validade:")) + ctDat_Venc), )} 
					
			DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
			DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=3,oDlg:End() ) ENABLE OF oDlg
			ACTIVATE MSDIALOG oDlg CENTERED
			ctCartao := AllTrim( ctCartao )
			If (nOpD == 3)
				lRet:= .F.
				Exit
			Endif
		Endif	
	ElseIf (nTipCar == 3)
		// Obtém o número do Cartão, a partir do CPF
		If SuperGetMV("MV_INTEEMS")
			If lInfoCard
				ctFuncao  := "10"
				ctTipOp   := "2"
				ctDat_Vend:= ""
			Else
				If lVer300
					cTrans    := "38"
				Else
					cTrans    := "32"
				Endif
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se aCartaoMh, não estiver vazioo significa que o cliente     ³
			//³trabalha com Milhas e indentifica os cliente no inicio       ³
			//³da operação de venda.                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lCartaoMH
				ctCartao   := LjConsCPF()

				If Alltrim(ctCartao)=="*"
             		Return(.F.)
				ElseIf Empty( ctCartao )
					MsgInfo("CPF inválido")
				    nOpD :=3
				    Loop
				Endif
			Endif	
		    ctTipOp    := '2'
		    ctTipPar   := '4'
		    ctDat_Venc := StrZero(Month(dDatabase)+1,2) + SubStr( StrZero( YEAR(dDatabase) ,4) ,3,2)
            ctParc_Val := "0"
			nOpd       := 1
			lRet       := .T.
			lCPFInfo   := .T.
		Endif
	Endif
	// Teclado cancel no Pinpad
	If (nOpD == 3)
		Return (.F.)
	Endif
	If !lInfoCard
		lContinua := L010BinCar( "0" , @lLerPin,,nX)  // Consulta ao Bin do Cartao
	Endif
	If lContinua
		// Se Venda Parcelada, Utilizar Transacao 38
		If (nParCred > 1)
			If lInfoCard
				ctFuncao:= "11"
			Else
				cTrans  := "38"
			Endif
		Endif
		
		While (nTentar < 4)
			nTentar ++
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Preencher o parametro MV_TEFCRIP com TESTE apenas para trabalhar com o Simulador do Sitef         ³
			//³Existem alguns cartoes de credito com senha (VISA - Bradesco), mas o simulador nao distingue      ³
			//³qual tem senha e qual nao optamos por nunca pedir senha para CC no ambiente de testes, pois estava³
			//³gerando erro na DLL do Pinpad.                                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lLerPin 	//.AND. ! SuperGetMV("MV_TEFCRIP") == "TESTE"
	  			If lUsaDisplay
   		 			DisplayEnv(StatDisplay(), "1C" + "Digite a senha do cartao magnetico" )         
   		  			DisplayEnv(StatDisplay(), "2C" + " ")         
		 		Endif	
				lRet := L010LePinPad()
				If !lRet
					Exit
				Endif
			Endif
			If (nOpD == 1)
				If lVer300.AND.('CAPT PRE-AUTOR'$SE4->E4_DESCRI)
					// Solicita o preenchimento dos dados do Estorno.
					nVlrCap:=nCartao
					DEFINE MSDIALOG oDlg TITLE "Captura de pré-autorização" FROM 13,21 to 24,60  of GetWNDDefault()
					@	6,12 SAY "Valor da Captura"  SIZE 130, 10 OF oDlg PIXEL
					@	6,88 MSGET oVlrCap VAR nVlrCap SIZE  55, 10 OF oDLG PIXEL	Picture "@E 999,999.99" WHEN .F.
					
					@ 18,12 SAY "Código de Autorização  " SIZE 130, 10 OF oDlg PIXEL
					@ 18,88 MSGET oCodAUT VAR cCodAut SIZE  18, 10 OF oDlg PIXEL Picture "999999" Valid !Empty(cCodAut)
					
					@ 30,12 SAY "Código de NSU" SIZE 130, 10 OF oDlg PIXEL
					@ 30,88 MSGET oNSU    VAR cNSU    SIZE  18, 10 OF oDlg PIXEL Picture "999999" Valid !Empty(cNSU)
					
					@ 42,12 SAY "Data (DD/MM).:"  SIZE 130, 10 OF oDlg PIXEL
					@ 42,88 MSGET oData VAR cData SIZE  55, 10 OF oDLG PIXEL	Picture "@R 99/99" Valid !Empty( cData )
					DEFINE SBUTTON FROM 60,  115 TYPE 1 PIXEL ACTION (oDlg:End() ) ENABLE OF oDlg
					ACTIVATE MSDIALOG oDlg CENTERED
					
					ctCodAut := AllTrim(cCodAut)
					ctNsu    := AllTrim(cNsu)
					ctData   := Subs(cData,1,4)+Str(Year(dDatabase),4)
					ctValor  := L010ValStr( nVlrCap )
					cTrans   := "12"
					ctTipOp  := "3"
				Endif
				If lCCS .AND. SuperGetMV("MV_TEFMILH",NIL,.F.)
                    IFPegCupom( nHdlECF, @cNumCupFis)       // Pegar o Numero do Cupom da Impressora
                    ctJur       := StrZero(SL1->L1_JUROS*100,7,0)
        		    cSaida		:= 'S' 						// Saida da consulta
                    ctDat_Venc  := StrZero(Month(dDatabase),2) + SubStr( StrZero( YEAR(dDatabase) ,4) ,3,2)
					cTrans	    := '50'						// Codigo da transacao
                    //Tipo de venda Milhagem 
					aRet        := L010VLRENTR("CC",aDados)
					If aRet[1]='9'
                       Return(.F.)
					Endif
                    ctTipOp  := "1"  						// a vista
		            If aRet[5]  > 1
                       If Val(ctJur)>0
                          ctTipOp  := "3" 					//Parcelado c/ Juros
                       Else
                          ctTipOp  := "2"					//Parcelado s/ Juros
                       Endif
                    Endif   
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Pagamento total da venda com milhas³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                    If nMilhas>0 .AND. nParMilh == 1
						ctValor := AllTrim( StrTran( Str( Round( nMilhas, 2 ),12,2 ), ".", "" ) )
				        aTrans  :={"50"           ,;
				        {ctRedeDest       		  ,;
				        ctADM            		  ,;
				        '1'               		  ,;  				// Valor fixo
				        '36'             		  ,;  				// Codigo da rede
				        '02'              		  ,;  				// SubFuncao - Aquisicao/Trcoa de milhas com outras formas de pag.
				        StrZero(Val(ctCartao),16),;  				// Numero do Cartão ou Trilha 2
				        StrZero(Val(ctValor),12,0),; 				// Valor da Venda
				        ctDat_Venc}}                  				// Vencimento do cartão, para digitado

                        // Montagem do dados adicionais
						ctDados	:= '0000'			               // * Versão da Mensagem. Identifica o mapeamento
						ctDados	+= StrZero(Val(cNumCupFis)+1,7)    // Numero do cupom fiscal emitido pelo terminal
						ctDados += Str(Year(dDatabase),4,0)+StrZero(Month(dDatabase),2,0)+StrZero(Day(dDatabase),2,0) // Data do cupom fiscal 
						ctDados += StrTran(Time(),":","")		   // Hora do cupom fiscal 
						ctDados += StrZero(0,6,0) 				   // Matricula do operador PDV
						ctDados += StrZero(0,30,0)				   // Nome do funcionario do PDV
						ctDados	+= StrZero(Val(ctValor),12,0)      // * Valor das milhas utilizadas como forma de pagamento
						ctDados	+= StrZero(0,12,0)                 // * Valor de desconto
						ctDados	+= '11'						       // Forma de pagamento - 11 - Milhas
                    Else
				        aTrans := { "50",;
		        					{ctRedeDest,;
				        			ctADM,;
				        			'1',;										// Valor fixo
				        			'36',;										// Codigo da rede
				        			'01',;										// SubFuncao - Venda/troca de milhas 
				        			ctTipOp,;									// Tipo da venda 1-A Vista/2-Parc s/ Juros/ 3-Parc. c/ Juros
				        			StrZero(Val(ctCartao),16),;					// Numero do Cartão ou Trilha 2
				        			StrZero(Val(ctValor),12,0),;				// Valor da Venda
				        			If(nParCred=1,'01',StrZero(nParCred,2)),;	// Numero de parcela, 00 para venda a vista
				        			ctDat_Venc }}								// Vencimento do cartão, para digitado

                       // Montagem do dados adicionais
						ctDados	:= '0000'			                // * Versão da Mensagem. Identifica o mapeamento
	                    ctDados	+= StrZero(Val(ctJur),7,0)	        // * Percentual de juros mensal do financiamento
						ctDados	+= StrZero(aRet[3]*100,12,0)     	// Valor da entrada
						ctDados	+= StrZero(Val(cNumCupFis)+1,7)    	// Numero do cupom fiscal emitido pelo terminal
						ctDados += Str(Year(dDatabase),4,0)+StrZero(Month(dDatabase),2,0)+StrZero(Day(dDatabase),2,0) // Data do cupom fiscal 
						ctDados += StrTran(Time(),":","")			// Hora do cupom fiscal 
						ctDados += StrZero(0,6,0) 					// Matricula do operador PDV
						ctDados += StrZero(0,30,0)					// Nome do funcionario do PDV
						ctDados	+= StrZero(aRet[4]*100,12,0)      	// * Valor das milhas utilizadas como forma de pagamento
						ctDados	+= '1'								// Indicador da venda  '1'-Venda normal '2'-Venda forcada 
						ctDados += StrZero(0,6,0) 					// Matricula do funcionario que forcou a venda
						ctDados += StrZero(0,30,0)					// Nome do funcionario que forcou a venda
						ctDados	+= StrZero(aRet[2]*100,12,0)     	// Valor à vista
						ctDados	+= StrZero(0,12,0)              	// * Valor com desconto, sobre o valor a vista
						ctDados	+= '0'								// Opção de parcelamento / 0 - DEFAULT /1,2 e 3 não usar
						ctDados	+= '00000000'						// Data de vencimento da primeira parcela
						ctDados	+= '000'							// Numero do local do contrato a ser registrado no CCS
						ctDados	+= '0000000'						// Numero do contrato a ser registrado no CCS
						ctDados	+= '0'						        // DV fechando LOCAL e CONTRATO  no CCS
					Endif
					
					If ExistBlock("LJTEFDADO")
						LjGrvLog(Nil, "Ponto de Entrada LJTEFDADO - Parametro :", {ctDados})	
						ctDados := ExecBlock("LJTEFDADO",.F.,.F.,{ctDados})
						LjGrvLog(Nil, "Ponto de Entrada LJTEFDADO - Retorno :", {ctDados})	
					Endif   
					
					AAdd(aTrans[2],ctDados)
			    	aRet := L010MonEnvResp(cTrans,"C",,aTrans,nX)
			    Else	
					aRet := L010MonEnvResp(AllTrim(cTrans),"C",,,nX)
			    Endif	

				// Se tudo Ok, e a Instituicao esta cadastrada no SAE vai para a funcao de impressao / confirmacao
				
				If (aRet[ 1 ] == 1)
					If L010InstVal( cInst, @lSemRede )
						lRet    := .T.
						lAceite := .T.
					Else
						// Nao pode emitir um cancelamento pois, foi emitido uma transacao de debito do carto do celiente,
						// efetuando um cancelamento, o depto de " suspeita" ira investigar....verificar outro ponto para analise
						HELP( " ", 1, "SEMINSTITU" )
						// ### "Administradora: " ### ". Efetue o cadastro da Administradora no arquivo SAE e salve esta venda como Orçamento."
						MsgInfo(STR0088 + TRIM(cInst) + STR0089)
						lRet    := .F.

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Envia o desfazimento da transação.³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						LOJA010T("F", "D")
					Endif
				Else
					lRet:= .F.
				Endif
				
				If Substr(aRet[ 2 ],2,2) == "55" .AND. !lVer300
					// Se senha incorreta, repete leitura ate tentativas = 3
					If (nTentar == 3)
						Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
						lRet := .F.
						Exit
					Endif
				Else
					If !aRet[ 5 ]
						Exit
					Endif
				Endif
			Endif
		End
	Else
		lRet:= .F.
	Endif
	Exit
End

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010AVista   ³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a venda com cartao de debito a vista				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010AVista()						    		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum												      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Venda realizada com sucesso ou nao			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 											      ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010AVista(nX )

Local lRet		 := .T.
Local lExcedeu   := .F.
Local lLeu		 := .F.
Local cTrans	 := "20"
Local nTent 	 :=  1
Local nTipCar
Local cCodRet
Local aRetorno
Local lLerPin	 := .T.
Local lContinua  := .T.
Local oDlg,oPre
Local cItem
Local nOp
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

If lVer300
	ctTipPar := "4"
Endif


ctTrilha_2	  := ""

ntOffSet  := 2
cModCart  := "M"
ctValor	 := AllTrim( StrTran( Str( Round( nDebito, 2 ),12,2 ), ".", "" ) )
// Faz a leitura do cartao magnetico
If !ltCpCD
	Aviso( "Atenção", "Compra com cartão de débito", {"&Ok"},, "Transação não está habilitada." )
	lRet:=.F.
Else
	// Faz a leitura do Cartao e da Senha
	lLeu := L010LeCartao(If(nDebito>0,"D","C"),nX)
Endif
If lLeu
	lContinua := L010BinCar( "1", @lLerPin,,nX )  // Consulta ao Bin do Cartao
	If !lContinua
		lRet:= .F.
	Endif
Else
	lRet      := .F.
	lContinua := .F.
Endif
While ltCpCD .AND. lLeu .AND. lContinua
	If lLerPin
		If lUsaDisplay
  			DisplayEnv(StatDisplay(), "1C" + "Digite a senha do cartao magnetico" )         
   		  	DisplayEnv(StatDisplay(), "2C" + " ")         
		Endif	
		lRet := L010LePinPad()
		If !lRet
			Exit
		Endif
	Endif
	If lFininvest
        cItem := space(11)
    	nOp   := 0
	    DEFINE MSDIALOG oDlg TITLE "Fininvest Item" FROM 13,10 TO 19,38 OF GetWndDefault()
	    @ 7,07 SAY "Codigo do item" SIZE 45,10 OF oDlg PIXEL
     	@ 7,67 MSGET oPre VAR cItem SIZE 35,10 OF oDLG PIXEL PICTURE '@!'
	    DEFINE SBUTTON FROM 30,45 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
	    DEFINE SBUTTON FROM 30,75 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
	    ACTIVATE MSDIALOG oDlg CENTER
        If nOp =2
           Return .F.
        Endif   
        ctRede_H   := '9'
        ctFuncao   := '1'
        ctOrcam    := Alltrim(cItem)
        ctTipOp    := '1'
        ctDat_Venc := ''
        ctParc_Val := ''
        ctPlano    := ''
    	aRetorno := L010MonEnvResp( "F0","D",,{"F0",{ctRedeDest, ctADM , ctRede_H,"10",ctFuncao, ctTipOp, ctCartao, ctValor, ctDat_Venc,ctOrcam,'2' ,ctParc_Val,ctPlano, ctSenha}},nX)
    Else	
    	aRetorno := L010MonEnvResp( AllTrim(cTrans),"D",,,nX )
    Endif	
	cCodRet	:= aRetorno[ 2 ]

	Do Case
		Case aRetorno[ 1 ] == 1
			
			// Se resposta = ok, imprimir e confirmar
			lRet := .T.
			lAceite := .T.
			
		Case aRetorno[ 1 ] == 2
			
			// Se resposta nao ok, tratar o erro
			lRet:= .F.
			If !lVer300
				If L010Confirma(.F.)
				   MsgInfo("Transação TEF não efetuada!")
					If !Empty(LOG_TEF)
						LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "2) Confirmação com .F." )
					Endif						
				Endif
			Endif
			
			Do Case
				Case (SubStr(cCodRet,2,2) == "55")  .AND. !lVer300
					// Se senha incorreta, repete leitura ate tentativas = 3
					If (nTent == 3)
						Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
						Exit
					Endif
					nTent++
					Loop
					
				Case (cCodRet $ "051/065/079") .OR. lExcedeu
					// Se saldo insuficiente, pede autorizacao para venda for‡ada
					If !ltCpForCD
						nTipCar:=1
						Aviso( "Atenção", "Compra com cartão de débito forçada", {"&Ok"},, "Transação não está habilitada." )
						lRet := .F.
					Else
						If (nEntrada > 0) .AND. (cInst == __TECBAN )
							cTrans  := "21"
							lLerPin := .F.
							If (( nEntrada - nDinheiro - nCheques ) <= ntLimDebito)
								Loop
							Else
								If Lj010Autor("TEF-FORC")
									Loop
								Else
									Exit
								Endif
							Endif
						Endif
					Endif
			EndCase
			
		Case (aRetorno[ 1 ] == 3)
			lRet:= .F.
			If aRetorno[ 5 ]
				Loop
			Endif
	EndCase
	
	Exit
End

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³L010PreDat() ³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a venda com cartao de debito pre-datada             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := L010PreDat()								      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum												      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna   ³ ExpL1 -> Venda realizada com sucesso ou nao                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³ LOJA010T 										          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010PreDat(nX)

Local lRet		 := .T.
Local oDlg
Local lLeu		 := .T.
Local cTrans	 := "22"
Local nTent 	 := 1
Local nOp
Local cCodRet
Local lContinua  := .T.
Local lLerPin	 := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

// nLimCDeb -> Valor limite de venda com cartao de debito

ctValor	 := AllTrim( StrTran( Str( Round( nDebito, 2 ),12,2 ), ".", "" ) )

If !ltCpPreCD
	Aviso( "Atenção", "Compra com cartão de débito pré datado", {"&Ok"},, "Transação não está habilitada." )
	lRet := .F.
Else
	lLeu := L010LeCartao(If(nDebito>0,"D","C"),nX)
Endif

If lLeu
	lContinua := L010BinCar( "1" ,@lLerPin,,nX)  // Consulta ao Bin do Cartao
	
	If !lContinua
		lRet:= .F.
	Endif
Else
	lRet:= .F.
Endif

While ltCpPreCd .AND. lLeu .AND. lContinua
	If lLerPin
		If lUsaDisplay
  			DisplayEnv(StatDisplay(), "1C" + "Digite a senha do cartao magnetico" )         
   			DisplayEnv(StatDisplay(), "2C" + " ")         
		Endif	
		lRet := L010LePinPad()
		If !lRet
			Exit
		Endif
	Endif
	If lVer300
		ctData:= Dtos( If(lTefMult,aTefMult[nX][6][1],aParcTef[1][1]) )
	Else
		ctData:= Right( Dtos( If(lTefMult,aTefMult[nX][6][1],aParcTef[1][1]) ), 4 )
	Endif
	
	If lFininvest
        cCiclos := space(2)
    	nOp   := 0

	    DEFINE MSDIALOG oDlg TITLE "Fininvest Ciclos" FROM 13,10 TO 19,38 OF GetWndDefault()
	    @ 7,07 SAY "Numero de Ciclos" SIZE 45,10 OF oDlg PIXEL
     	@ 7,67 MSGET oPre VAR cCiclos SIZE 35,10 OF oDLG PIXEL PICTURE '@!'
	    DEFINE SBUTTON FROM 30,45 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
	    DEFINE SBUTTON FROM 30,75 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
	    ACTIVATE MSDIALOG oDlg CENTER
        If nOp =2
           Return .F.
        Endif   
        ctRede_H   := '9'
        ctFuncao   := '1'
        ctTipOp    := '2'
        ctOrcam    := '1' 
        ctParc_Val := StrZero(Val(cCiclos),2)
        ctPlano    := ''
    	aRetorno := L010MonEnvResp( "F0","D",,{"F0",{ctRedeDest, ctADM , ctRede_H,"10",ctFuncao, ctTipOp, ctCartao, ctValor, ctDat_Venc,ctOrcam,'2' ,ctParc_Val,ctPlano, ctSenha}},nX)
    Else	
    	// Se o valor da venda superar limite estabelecido
    	aRetorno := L010MonEnvResp( AllTrim(cTrans),"D",,,nX )
    Endif	
	cCodRet	:= aRetorno[2]
	
	Do Case
		Case aRetorno[ 1 ] == 1
			
			// Se tudo Ok, imprime e confirma
			
			lRet := .T.
			lAceite := .T.
		Case aRetorno[ 1 ] == 2
			// Se nao Ok, trata o codigo de retorno
			lRet:= .F.
			lAceite:=.T.
			If Substr(cCodRet,2,2) == "55" .AND.  !lVer300
				// Se senha incorreta, repete leitura ate tentativas = 3
				If nTent == 3
					nOpd := 1
					Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
					lRet := .F.
					Exit
				Endif
				nTent++
				Loop
			Endif
		Case aRetorno[ 1 ] == 3
			lRet := .F.
			If aRetorno[ 5 ]
				Loop
			Endif
			Exit
		Case aRetorno[ 1 ] == 4   // Não Assume garantia...
			lAceite := .T.
			lRet    := .F.
			If L010Confirma( .F.,"XXXX.XXX")
			   MsgInfo("Transação TEF não efetuada!")
				If !Empty(LOG_TEF)
					LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "3) Confirmação com .F." )
				Endif						
			Endif
			Exit
	EndCase
	Exit
End

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³L010Parcel() ³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a venda com cartao de debito parcelada              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010Parcel( )                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Se e' cartao de debito por contigencia (Financiado - CDC)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Venda realizada com sucesso ou nao			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 											      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Parcel(lConsCdc,lConsulta,nX)

Local lRet		  	:= .T.
Local lExcedeu    	:= .F.
Local lLeu		  	:= .T.
Local oDlg
Local cSep		  	:=  Chr( 4 )		//Caracter EOT para o Sitef
Local nTent 	  	:=  1
Local nPlano
Local nValor
Local lLerPin	  	:= .T.
Local lContinua   	:= .T.
Local nOp			
Local cDataVenc   	:= ""
Local nLoop
Local nVist
Local nParcela  
Local dData1    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))


DEFAULT nx := 0

nParcela   			:= If(lTefMult .AND. nX<>0 ,aTefMult[nX][5],Len(aParcTef))
dData1     			:= dDataBase

If Len(aParcTef)>0
	dData1     		:=If(lTefMult.AND.nX<>0,aTefMult[nX][6][1],aParcTef[1][1])
Endif	

ctTrilha_2 := ""
cModCart   := "M"

If !lConsCDC
	ctValor	 	:= AllTrim( StrTran( Str( Round( nDebito, 2 ),12,2 ), ".", "" ) )
	ctEntrada 	:= AllTrim( StrTran( Str( Round( nEntrada, 2 ),12,2 ), ".", "" ) )
	nVist 	  	:= If( nEntrada > 0, 1, 2 )
Endif

If !ltCpParCD
	nTipCar := 1
	Aviso( "Atenção", "Compra com cartão de débito parcelada", {"&Ok"},, "Transação não está habilitada." )
	lRet := .F.
Else
	lLeu := L010LeCartao(If(nDebito>0,"D","C"),nX)
Endif

If lLeu
	//HOMOLOGACAO: A consulta CDC também deve verificar os retornos da consulta Bin
	lContinua := L010BinCar("1",@lLerPin,Iif(lConsCdc,.T.,.T.),nX)  // Consulta ao Bin do Cartao //L010BinCar( "1" ,@lLerPin,!lConsCdc,nX)  // Consulta ao Bin do Cartao
	If !lContinua
		lRet:= .F.
	Endif
Else
	lRet:= .F.
Endif

cTrans := If( nVist == 1, "24", "26" )
If lVer300
	// Intercepta, transação de Pagamento CDC para o usuário decidir se é uma
	// Venda ou apenas uma consulta (Na consulta não é impresso cupom !)
	If lCDC .AND. lContinua
		// Verifica se  Primeira parcela nao esta superior ao limite de data estipulado pelo SITEF
		// ou se nao e maior de 365 dias
		If ( dData1>CToD( Subs(aBlocoResp[1][11],1,2)+'/'+ Subs(aBlocoResp[1][11],3,2)+'/'+Subs(aBlocoResp[1][11],7,2)) ).OR.( (dData1-dDataBase)>365 )
			MsgInfo('Excede Prazo')
			Return( .F. )
		Endif
	Endif
Endif

If lCDC .AND. lContinua
	If !lConsCDC
		ctDat_Venc := dData1
		ctDat_Venc := StrZero(Day(ctDat_Venc),2)+StrZero(Month(ctDat_Venc),2)+;
		Substr(Str(Year(ctDat_Venc),4),3,2)
		cTrans := "28"
	Else	 //Somente Consulta CDC
		nValor	:= nDebito
		nOp:= 2
		ctValor	  := AllTrim( StrTran( Str( Round( nValor   , 2 ),12,2 ), ".", "" ) )
		ctEntrada  := AllTrim( StrTran( Str( Round( nEntrada , 2 ),12,2 ), ".", "" ) )
		
		ctDat_Venc := dData1
		ctDat_Venc := StrZero(Day(ctDat_Venc),2)+StrZero(Month(ctDat_Venc),2)+;
		Substr(Str(Year(ctDat_Venc),4),3,2)
		cTrans := "27"
	Endif
Endif

While ltCpParCD .AND. lLeu .AND. lContinua
	If lLerPin
		If lUsaDisplay
   		 	DisplayEnv(StatDisplay(), "1C" + "Digite a senha do cartao magnetico" )         
   		  	DisplayEnv(StatDisplay(), "2C" + " ")         
		Endif	
		lRet := L010LePinPad()
		If !lRet
			Exit
		Endif
	Endif
	
	nOp := 3
	
	//lIgual := .T.
	If !lConsCDC
		// Verificacao para descobrir se as parcelas sao iguais ou diferentes
		nDifDias := If(lTefMult,aTefMult[nX][6][3],aParcTef[2][1]- dData1) 
		nOp := 2
	Else
		nOp := 0  // Consulta CDC
	Endif
	
	// nOp -> 1 - Parc. iguais, assume param. do Sitef ( nao usado )
	// nOp -> 2 - Parcelas iguais, parametros informados pelo Sigaloja.
	// nOp -> 3 - Parcelas diferentes, valores e datas informados pelo Sigaloja.
	If nOp == 1
		// Se parcelas iguais Sigaloja assumira os dados configurados pelo Sitef
		ctDad_Parc := "1"
		nOpc2      := 3
		DEFINE MSDIALOG oDlg TITLE "Parcelas de mesmo valor" FROM 13,21 TO 22,64 OF GetWndDefault()
		@ 10,45 SAY "Número de parcelas: "+ Str( ntNuParDeb, 2 ) SIZE 100,10 OF oDlg PIXEL
		@ 20,45 SAY "Intervalo entre parcelas: " + Str( ntIntrParc, 3 ) SIZE 100,10 OF oDLG PIXEL
		@ 30,45 SAY "Primeira parcela à vista ? " + If( ltPrParVist,"Sim", "Não" ) SIZE 100,10 OF oDLG PIXEL
		@ 45,30 BUTTON "Confirma" SIZE 50, 15 OF oDlg PIXEL 	ACTION ( nOpc2:=1,oDlg:End() )
		@ 45,90 BUTTON "Abandona" SIZE 50, 15 OF oDlg PIXEL 	ACTION ( nOpc2:=2,oDlg:End() )
		ACTIVATE MSDIALOG oDlg CENTERED
		
		If nOpc2 <> 1
			Exit
		Endif
		
		nPlano := Val( ctPlano )
		
		nParcel	 := Int( ( ( Val( ctValor ) / 100 ) / nPlano ) * 100 )  / 100
		nDif		 := Val( ctValor ) - nParcel * nPlano
		
		dDataPar  := dDatabase - ntIntrParc
		
		aParcTef := {}
		
		For nLoop := 1 to nPlano
			dDataPar += ntIntrParc
			nParc 	:= If( nLoop == 1, nParcel + nDif, nParcel )
			
			aAdd( aParcTef, { dDataPar, nParc } )
		Next
		
	ElseIf nOp == 2
		// Condicao especial para TECBAN, parcelas iguais
		cIntervalo := If( nDifDias > 9, Str( nDifDias, 2 ), Str( nDifDias, 1 ) )
		ctDad_Parc := "2" + cSep + cIntervalo + cSep + ;
		AllTrim( Str( nParDeb, 2 ) ) + cSep + If( SuperGetMV( "MV_MESFECH" ) == "S","1","0" )

	ElseIf nOp == 3
		// Se parcelas diferentes, utiliza datas e vencimentos do aParcelas
		ctPlano    := Alltrim( Str( nParDeb, 2 ) )
		ctDad_Parc := "3" + cSep + ctPlano + cSep
		For nLoop := 1 To Len(aParcTef)
			If (AllTrim(aParcTef[nLoop][3]) == "CD")
				cDataVenc  := Dtos(aParcTef[nLoop][1])
				ctDad_Parc += StrZero( aParcTef[nLoop][2] * 100, 12 ) + Right( cDataVenc, 2 ) ;
				+ SubStr( cDataVenc, 5, 2 ) + Left(  cDataVenc, 4 )
			Else
				ctEntrada:= ""    // Zera a entrada do cartao pois a entrada esta sendo feita com outra forma.
			Endif
		Next nLoop
	Endif
	
	If (AllTrim(cTrans) == "28")
		ctDad_Parc := StrZero(nParDeb,2)
	ElseIf (AllTrim(cTrans) == "27")
		ctDad_Parc := StrZero(nParDeb,2)
	Endif

	If lFininvest
        nPlano:= 0
    	nOp   := 0

	    DEFINE MSDIALOG oDlg TITLE "Fininvest Plano" FROM 13,10 TO 19,38 OF GetWndDefault()
	    @ 7,07 SAY "Numero" SIZE 45,10 OF oDlg PIXEL
     	@ 7,61 MSGET oPre VAR nPlano SIZE 35,10 OF oDLG PIXEL PICTURE '9999999999'
	    DEFINE SBUTTON FROM 30,45 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
	    DEFINE SBUTTON FROM 30,75 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
	    ACTIVATE MSDIALOG oDlg CENTER
        If nOp =2
           Return .F.
        Endif   
        ctRede_H   := '9'
        ctFuncao   := '1'
        If dData1==dDataBase
            ctTipOp    := '3' // Parcelado a vista
        Else
            ctTipOp    := '4'// Parcelado pre-datado
        Endif    
        ctOrcam    := '1' 
        ctParc_Val := StrZero(nParDeb,2)  // ??
        ctPlano    := Alltrim(Str(nPlano))
    	aRetorno   := L010MonEnvResp( "F0","D",,{"F0",{ctRedeDest, ctADM, ctRede_H,"10",ctFuncao, ctTipOp, ctCartao, ctValor, ctDat_Venc,ctOrcam,'2' ,ctParc_Val,ctPlano, ctSenha}},nX)
    Else	
    	aRetorno := L010MonEnvResp( AllTrim(cTrans),"D",,,nX )
    Endif	
	cCodRet	:= aRetorno[ 2 ]
	
	Do Case
		Case aRetorno[ 1 ] == 1
			// Se tudo Ok, e a Instituicao esta cadastrada no SAE vai p/ a funcao de impressao / confirmacao
			If lCdc
				If L010InstVal( cInst, @lSemRede )
					lRet := .T.
					lAceite := .T.
				Else
					HELP( " ", 1, "SEMINSTITU" )
					// ### "Administradora: " ### ". Efetue o cadastro da Administradora no arquivo SAE e salve esta venda como Orçamento."
					MsgInfo(STR0088 + TRIM(cInst) + STR0089)
					If L010Confirma( .F. )
						MsgInfo("Transação TEF não efetuada!")
						If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "4) Confirmação com .F." )
						Endif						
					Endif
					lRet := .F.
				Endif
			Else
				lRet := .T.
				lAceite := .T.
			Endif
		Case aRetorno[ 1 ] == 2
			// Se resposta nao ok, trata resposta recebida
			lRet:= .F.
			Do Case
				Case Substr(cCodRet,2,2) == "55" .AND. !lVer300
					// Se senha incorreta, repete leitura ate tentativas = 3
					If nTent == 3
						Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
						lRet := .F.
						Exit
					Endif
					nTent++
					Loop
				Case cCodRet $ "051/065/079" .OR. lExcedeu
					// Se saldo insuficiente, pede autorizacao para venda for‡ada
					L010Confirma( .T. )
					If !Empty(LOG_TEF)
						LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "5) Confirmação com .T." )
					Endif						
					If nEntrada > 0 .AND. cInst == __TECBAN
						lLerPin := .F.
						cTrans  := "25"
						If ( nEntrada - nDinheiro - nCheques ) <= ntLimDebito
							Loop
						Else
							If Lj010Autor("TEF-FORC")
								cTrans := "25"
								Loop
							Else
								Exit
							Endif
						Endif
					Endif
			EndCase
		Case aRetorno[ 1 ] == 3
			// Se TimeOut, verifica se deve tentar novamente
			lRet:= .F.
			If aRetorno[ 5 ]
				Loop
			Endif
			Exit
	EndCase
	If (AllTrim(cTrans) == "27") .AND. (lRet)
		If lVer300
			If lConsCdc
				lConsulta :=Consulta_CDC( aTicket )
			Endif
		Else
			cTipVenCon := "S"
			If lUsaDisplay
   		 		DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do cupom"  )         
   		  		DisplayEnv(StatDisplay(), "2C" + " ")         
		 	Endif	
			// ### "Aguarde ... imprimindo o cupom TEF "
			ljMsgRun(STR0065,, { || L010TefImpr( aTicket, aPromis, .T.,,,, @lRelGer)})
		Endif
		lConsTef := .F.
		lRet	 := .F.
	Endif
	Exit
End

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010MonEnvResp ³ Autor ³ Sergio / Luiz   ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a montagem da Arquivo de solicitacao, envia o mesmo ³±±
±±³			 ³ , recebe a resposta, e devolve array.				      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := L010MonEnvResp( ExpN1 ) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 -> tipo de solicitacao								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ Array com os seguintes elementos:						  ³±±
±±³			 ³ 1 - nResult  -> Resultado da operacao					  ³±±
±±³			 ³ 			  1 -> Operacao Ok								  ³±±
±±³			 ³ 			  2 -> Operacao nao Ok							  ³±±
±±³			 ³ 			  3 -> Time-Out									  ³±±
±±³			 ³ 2 - nCod 	 -> Codigo de resposta do SiTef				  ³±±
±±³			 ³ 3 - aTicket  -> Ticket format. pronto p/ exib.			  ³±±
±±³			 ³ 4 - aPromis  -> Promissoria format. pronto p/ exib.		  ³±±
±±³			 ³ 5 - lRetry	 -> Tenta novamente							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA 	 ³ BOPS ³Prograd.³ALTERACAO								      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 22.11.01 ³ XXXXX³MMancio ³Foi acrescentado o parâmetro: "cModalidade" ³±±
±±³          ³      ³        ³para atender o Cartão com Chip              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010MonEnvResp(	cTipSol, cModalidade, lVisa, aTrans, ;
								nX )
Local nAt
Local nSecIni
Local nResult		:=  2
Local nI, nLp1
Local cDisp 		:= ""
Local cRBuffer 		:= ""
Local cBuffer		:= ""
Local cGran 		:= ""
Local cMult 		:= ""
Local cZerBin		:= Chr( 0 )
Local lOk			:= .T.
Local lSucess		:= .F.
Local lRetry		:= .F.
Local lTimeOut 		:= .T.
Local aTick 		:= { }
Local aProm 		:= { }
Local aLHeader		:= { }
Local cImpArq		:= ""
Local cResposta		:= ""
Local cStringTxt	:= ""
Local cCampoBkp		:= ""  // Variavel auxiliar utilizada no tratamento do Texto de Impressão (Bloco I)
Local nHandle
Local lMsg			:= .F. 
Local aTransAtu
Local lDesfaz		:= .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

DEFAULT nX 			:= 0
DEFAULT cModalidade	:= "C"

lVisa				:= If(lVisa == Nil,.F.,.T.)

// Armazena o valor da transacao para enviar no header da confirmacao

ctTrans  := cTipSol

// Monta o campo de dados conforme o tipo de transacao
If aTrans == Nil
   aVarNec := L010StrVar(AllTrim(cTipSol),aTrans)
Else
   aVarNec := aClone(aTrans[2])
Endif   

ntOffSet := -1
If ValType( aVarNec ) == "A"
	aEval( aVarNec, { |cElemen| cBuffer += cElemen + cZerBin } )
	ntOffSet := -1
	if len(alltrim(ctTrilha_2)) > 0
		For nAt := 1 to (Len(cBuffer)-len(ctTrilha_2))+1
			if ctTrilha_2 == substr(cBuffer,nAt,len(ctTrilha_2))
				ntOffSet := nAt -1
			Endif
		Next nAt
	Endif
	If ntOffSet == -1 .AND. len(alltrim(ctCartao)) > 0
		For nAt := 1 to (Len(cBuffer)-len(ctCartao))+1
			if ctCartao == substr(cBuffer,nAt,len(ctCartao))
				ntOffSet := nAt -1
			Endif
		Next nAt
	Endif
Endif

if (ntOffSet < 0) .OR. (cTipSol $ "01;40;41;70;72;73;74;78;80;81;82;83")
	ntOffSet := 0
Endif

nTam := Len( cBuffer )
// Incrementa o numero da transacao
ntId++

// Monta o Header e concatena com o campo de dados
cHeader := L010CHeader(cSiTVers,"P",AllTrim(cTipSol),cPIdentTerm,ntSeqTerm,ntId,ntCtrlTran,ntOffSet,cEmpresa,nTam)
cMessage := cHeader + cBuffer

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ HOMOLOGACAO: Controle para transação de desfazimento                                         ³
//| Pos1 - Tipo da transacao                             								         |
//| Pos2 - 9 primeiros dígitos do cartão						 								 |
//| Pos3 - Rede de destino                                       								 |
//| Pos4 - Posicao do numero do cartão                           								 |
//| Pos5 - Numero seq. gerado para controlar o sitef             								 |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TEF_aTrans(cTipSol,Subs(ctCartao,1,9),NIL,NIL,Alltrim(Str(ntId,10)))

// Codifica a mensagem se o parametro de criptografia estiver habilitado
// Apenas os dois primeiros bytes ( tamanho ) nao sao codificados
If SuperGetMV( "MV_TEFCRIP" ) == "S"
	cTam := Left( cMessage, 2 )
	cCod := Right( cMessage, Len( cMessage ) - 2 )
	nTamX := Len( cCod )
	cCod := CodSitef( cCod )
	cMessage := cTam + cCod
Endif

While .T.
	// Cria o arquivo de transmissao e tenta fazer o rename
	nHandle := FCreate( cDirT_Tx + "TEMP." + cPTerm )
	If (nHandle == -1)
		FClose( nHandle )
		nAction := L010ErroRede( 1 )
		If (nAction == 1)
			Exit
		ElseIf (nAction == 2)
			lRetry := .T.
		Endif
		Loop
	Else
		FWrite( nHandle, cMessage )
		FClose( nHandle )
		// Tentativas de rename do arquivo de transmissao
		While .T.
			If lUsaDisplay
   		 		DisplayEnv(StatDisplay(), "1C" + "Tentando renomear arquivo de transmissao"  )         
   		  		DisplayEnv(StatDisplay(), "2C" + " ")         
		 	Endif	
			        // ### "Tentando renomear arquivo de transmissão" ### " - Em comunicação"
			MsgRun( STR0066,cMens + STR0067 ,{ || lSucess := L010RenTry()})
			If lSucess
				Exit
			Endif
			nAction := L010ErroRede( 2 )
			If nAction == 1
				Exit
			ElseIf (nAction == 2)
				lRetry := .T.
			Endif
			Loop
		End
		Exit
	Endif
End

aTransAtu := TEF_aTrans()
TEF_aTrans(aTransAtu[1], aTransAtu[2], aBlocoResp[1][1], Alltrim(Str((Len(Alltrim(aBlocoResp[1][1]))+2),10)), aTransAtu[5]) //Monto o offset desta transacao +2 seriam os dois chr(0)

If lSucess .AND. ((cTipSol$TEF_DESFAZIMENTO) .OR. (cTipSol=="F0" .AND. lInfoCard))
	//O layout do desfazimento não funciona para o cheque garantido TECBAN VERIFICAR
	//.OR. (cTipSol=="70" .AND. ctFuncao$"27"))
	nHandle := fCreate("TMPTRN." + cPTerm)
	fWrite(nHandle,"00=D"+";01="+Alltrim(aTransAtu[1])+";02="+Alltrim(aTransAtu[2])+";03="+Alltrim(aTransAtu[3])+";04="+Alltrim(aTransAtu[4])+";05="+Alltrim(Str(ntId,10))+";")
	fClose(nHandle)
	__CopyFile("TMPTRN." + cPTerm,"NCNTRN"+Strzero(nX,2)+"." + cPTerm)
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(1) " + "00=D"+";01="+Alltrim(aTransAtu[1])+";02="+Alltrim(aTransAtu[2])+";03="+Alltrim(aTransAtu[3])+";04="+Alltrim(aTransAtu[4])+";05="+Alltrim(Str(ntId,10))+";" )
	Endif						
Endif

While .T.
	If lSucess
		nSecIni := Seconds()
		// Verifica a existencia do arquivo de resposta do Sitef
		If lUsaDisplay
			DisplayEnv(StatDisplay(), "1C" + "Aguardando resposta da rede TEF" )
			DisplayEnv(StatDisplay(), "2C" + " ")
		Endif

		// ### "Aguardando resposta da rede TEF" ### " - Em comunicação"
		MsgRun( STR0068 ,cMens + STR0067 ,{ || lTimeOut := L010WaitWin( cDirT_Rx + "PDV." + cPTerm )})
		nResult := 2

		// Abre o arquivo de resposta
		If !lTimeOut
			
			nHandle := -1
			For nI  := 1 To 25
				nHandle	:= FOpen( cDirT_Rx + "PDV." + cPTerm )
				If Empty(Ferror())
					lDesfaz := .T.
					Exit
				Endif
			Next nI
			
			IF nHandle >= 0
				nSize 	:= FSeek( nHandle, 0, 2 )
				FSeek( nHandle, 0 )
				cRBuffer := Space( nSize )
				nRead 	:= FRead( nHandle, @cRBuffer, nSize )
				FClose( nHandle )

				// Faz a leitura do Header
				// Decodif. a mensagem se o parametro de criptografia estiver habilitada
				// Apenas os dois primeiros bytes ( tamanho ) nao sao decodificados
				If SuperGetMV( "MV_TEFCRIP" ) == "S"
					cTam	:= Left( cRBuffer, 2 )
					cDec	:= Right( cRBuffer, Len( cRBuffer ) - 2 )
					nTamX := Len( cDec )
					cDec	:= DecSitef( cDec )
					cRBuffer := cTam + cDec
				Endif
				cHeader	:= SubStr( cRBuffer, 1, 32 )
				aLHeader	:= L010LHeader( cHeader )
				If aLHeader[ 3 ] == ctSucess .AND. ntId == aLHeader[ 6 ]
					nResult := 1
				ElseIf ntId <> aLHeader[ 6 ]
					nAction := L010ErroRede( 7 )
					If nAction == 1
						lDesfaz := .T.
						Exit
					Endif
				Endif

				// Verifica se retorno == "00" ( Sucesso ) ou se transacao == 72 ( Consulta Associacao Comercial )
				// e retorno == 89 e componentes estao na string
				If ntId == aLHeader[ 6 ]
					If aLHeader[ 3 ] == ctSucess
						nResult := 1
					Endif
					If  AllTrim(cTipSol) $ "72;78" .AND. (aLHeader[ 3 ] == "FF" .OR. aLHeader[ 3 ] $ "88;89") .AND. (At("PAS",cRBuffer)<>0) .AND. (At("PRE",cRBuffer)<>0) .AND. (At("DIA",cRBuffer)<>0)
						nResult := 1
					Endif
				Else
					L010ErroRede( 7 )
				Endif

				// Iguala ao controle de transacao enviado pelo Sitef
				ntCtrlTran := aLHeader[ 6 ]
				// Interpreta o campo de dados e separa por tipo
				If nSize <> nRead
					lOk := .F.
				Else
					If (AllTrim(cTipSol) == "15")
						If lVer300
							cResposta := Substr(cRBuffer,42,Len(cRBuffer) - 42)
						Else
							cResposta := Substr(cRBuffer,35,Len(cRBuffer) - 35)
						Endif
					Endif
					For nLp1  := 33 to Len( cRBuffer )
						cTipo := SubStr( cRBuffer, nLp1 + 1, 1 )
						nTam  := Asc( SubStr( cRBuffer, nLp1, 1 ) )
						cCampo := SubStr( cRBuffer, nLp1 + 2, nTam - 1 )
						Do Case
							Case (cTipo == "D")
								cDisp := cCampo
								MsgImpressao( cDisp )
							Case (cTipo == "I")
								// Retirar o Chr(0) do cupom tef, Quando Existir
								If At(Chr(0),cCampo) <> 0
									cCampoBkp := ""
									For nI := 1 To Len(cCampo)
										If SubStr(cCampo,nI,1) <> Chr(0)
											cCampoBkp += SubStr(cCampo,nI,1)
										Endif
									Next
									cCampo := cCampoBkp
								Endif
								AAdd( aTick,  cCampo )
								cImpArq += cCampo+Chr(10)
							Case (cTipo == "M")
								cMult := cCampo
							Case (cTipo == "P")
								AAdd( aProm,  cCampo )
							Case (cTipo == "H")
								L010LeArmaze( cCampo, AllTrim(cTipSol), @aTick, cDisp )
								// Caso a Venda solicite. Deve verificar se a operação é
								// Com ou Sem Garantia...
								If SubStr( cCampo,  1,	2 )=="SC"
									If "ASSUME GARANTIA" $ cDisp 
										If MsgYesNo(cDisp)        // SIM
											If !Lj010Autor(cDisp) // Senha do Administrador
												nResult	:= 4
												lRetry	:= .F.
												lDesfaz := .F.
											Else
												nResult	:=	1
												lDesfaz := .T.
												Exit
											Endif
										Else     				// NAO - Desiste
											nResult	:= 4
											lRetry	:= .F.   
											lDesfaz := .F.
										Endif
									Endif
									If !lRetry .AND. nResult=4 .AND. lVer300
										Exit
									Endif
								Endif
								
							case (cTipo == "S")
								If ! Empty(Val(Substr(cCampo,1,3)))
									aAdd( aTick, "CONSULT. ANTERIORES: " + Substr(cCampo,1,3) )
								Endif
								If ! Empty(Val(Substr(cCampo,4,12)))
									aAdd( aTick, "VALOR CONSULT. ANT.: " + Str(Val(Substr(cCampo,4,12))/100) )
								Endif
								If ! Empty(Val(Substr(cCampo,16,3)))
									aAdd( aTick, "CONSULTAS NO DIA...: " + Substr(cCampo,16,3) )
								Endif
								If ! Empty(Val(Substr(cCampo,19,12)))
									aAdd( aTick, "VALOR CONSULT. DIA.: " + Str(Val(Substr(cCampo,19,12))/100) )
								Endif
								If ! Empty(Val(Substr(cCampo,31,3)))
									aAdd( aTick, "CONSULTAS CHEQ. PRE: " + Substr(cCampo,31,3) )
								Endif
								If ! Empty(Val(Substr(cCampo,34,12)))
									aAdd( aTick, "VALOR CONS. CHQ.PRE: " + Str(Val(Substr(cCampo,34,12))/100) )
								Endif
								
							Case (cTipo == "C")
								L010LeConfig( cCampo )
								
							Case (cTipo == "K")
								cGran := cCampo
								
							Case (cTipo == "/")
								AAdd( aProm, Chr( 196 ) )
								
							Case (cTipo ==  "1")
								L010Bloco1( cCampo,  cModalidade )
								
							Case (cTipo ==  "2")
								L010Bloco2( cCampo,  cModalidade )
								
							Case (cTipo == "3")
								cWork:= cCampo
								
							Case (cTipo == "A")
								cCampo:= cCampo
								
							Case (cTipo == "X")
								nLp1 +=5
								cCampo:=SubStr( cRBuffer, nLp1,Len(cRBuffer))
								If At('@', cCampo)>0
									While At('@', cCampo)>0
										aadd(aTick,Subs(cCampo,1,At('@', cCampo)-1))
										cCampo:=Subs(cCampo,At('@', cCampo)+1,Len(cCampo))
									End
								Else
									aadd(aTick,Subs(cCampo,1,Len(cCampo)))
								Endif
								//cCampo:=space(len(aTicket))
								cCampo:=space(1)
								Aeval(aTick, {|cLinha| cCampo+=cLinha})
							OtherWise
								If SuperGetMV("MV_INTEEMS") .AND. cSaida="S"
									AAdd( aTick,  cCampo )
								Endif
						EndCase
						nLp1 += Len( cCampo ) + 1
					Next nLp1
					
					cMult := AllTrim( StrTran( cMult, "@", "," ) )
					c030:=cDisp
					If (!( Upper( cDisp )$"TERMINAL ABERTO#TERMINAL FECHADO#NADA CONSTA#OK#APROVADA#ASSUME GARANTIA") .AND. !Empty(cDisp)) .OR. (!Empty(cMult))
						If lUsaDisplay
							DisplayEnv(StatDisplay(), "1C"+ Upper("Mensagem do Sitef"))
							DisplayEnv(StatDisplay(), "2C"+ cDisp)
						End
						MsgInfo(cDisp+cMult,cMens + " - Mensagem do Sitef")
						lMsg:=.T.
						lDesfaz := .F.
						Exit
					Endif
				Endif
			Else
				If (L010ErroRede( 5 ) == 1)
					aTefDados := { {	"", "", "", "", ;
										"", "", "", "", ;
										"", "", "", "", ;
										"", {}, "", "", ;
										"", "", "", ""} }
					lDesfaz := .T.
					Exit
				Endif
				Loop
			Endif
		Else
			If (L010ErroRede( 5 ) == 1)
				aTefDados := {{	"", "", "", "", ;
								"", "", "", "", ;
								"", "", "", "", ;
								"", {}, "", "", ;
								"", "", "", "" }}
				//HOMOLOGACAO: Enviar o Desfazimento pois perdeu a comunicação com o Sitef
				If ((cTipSol$TEF_DESFAZIMENTO) .OR. (cTipSol=="F0" .AND. lInfoCard))	
    				//O layout do desfazimento não funciona para o cheque garantido TECBAN VERIFICAR
					//.OR. (cTipSol=="70" .AND. ctFuncao$"27"))
					TEF_lEnvDF(.T.)
				Else
					TEF_lEnvDF(.F.)
				Endif					
				lDesfaz := .T.
				Exit
			Endif
			Loop
		Endif
	Endif
	Exit
End       

//HOMOLOGACAO: Preciso controlar em que momento devo enviar o desfazimento ou apagar o arquivo
If !lDesfaz
	If File("NCNTRN"+StrZero(nX,2)+"."+cPTerm)
		Ferase("NCNTRN"+StrZero(nX,2)+"."+cPTerm)
	Endif                             
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(2) " + "Apagou o arquivo ncn" )
	Endif						
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Apaga o arquivo lido ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cDirT_Rx + "PDV." + cPTerm )
	While FErase( cDirT_Rx + "PDV." + cPTerm )=-1
	   Sleep(2)
	End
Endif	

//aTefDados[1][11] == Rede, 04 Visa. Na Rede visa não ha necessidade do 
// arquivo de reimpressao, usa-se a funcao L010Reimp.
If !Empty(cImpArq) .AND. !lVisa
	While .T.
		If lTefMult .AND. nX>1 .AND. File("RX_PRN." + cPTerm)
			For nI := 1 To 25
				nHandle := FOpen("RX_PRN." + cPTerm,2)
				If Empty(Ferror())
					exit
				Endif
			Next nI
			If nHandle >= 0
				FSeek(nHandle,0,2)
			Endif
		Else
			nHandle := FCreate("RX_PRN." + cPTerm)
        Endif
		If (nHandle == -1)
			nAction := L010ErroRede( 1 )
			If (nAction == 1)
				MsgInfo( "Não será possível a reimpressão !!!" )
			Else
				Loop
			Endif	
		Else
			If SuperGetMV( "MV_TEFCRIP" ) == "S"
				cImpArq := CodSitef( cImpArq )
			Endif
			FWrite( nHandle, cImpArq )
			FClose( nHandle )
		Endif
		Exit
	End
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Alimenta os vetores estaticos de ticket e promissoria ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cStringTxt := Str( nResult, 1 ) + If( lTimeOut, "T", " " ) + If( lRetry, "T", " " ) + Str( If( lTimeOut, 0, Val(aLHeader[ 3 ]) ), 3 ) + cDisp

MemoWrit( "RETURN." + xNumCaixa(), cStringTxt )

aTicket := aTick
aTefDados[1][14] := aTicket
aPromis := AClone(aProm)

If lTefMult .AND. nX <> 0

   // O array ATEFDADOS, contem as informacoes necessarias para impressao do cupom e 
   // tb para confirmacao ou cancelamento do cupom
   aTefMult[nX][11]:=aClone(aProm)
   aTefMult[nX][7] :=aClone(aTefDados)
   If nResult == 1
  	  aTefMult[nX][8]:=.T.
   Endif   

Endif

// cTipoSol=15 -> Consulta BIN
// cTipoSol=01 -> Abertura de terminal

If !lMsg .AND. nResult==1 .AND. At(cTipSol,"01/15/")=0 .AND. !Empty(cDisp+cMult)
   MsgInfo(cDisp+cMult,cMens + " - Mensagem do Sitef")
Endif   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³HOMOLOGACAO: O controle de gravacao de arquivos precisou ser  ³
//³alterado devido aos desligamentos no meio da operacao         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//O codigo das transacoes de Reimpressao VISA e InfoCard eh o mesmo "F0"
//A Reimpressao VISA nao precisa confirmacao
If nResult == 1 .AND. ((cTipSol$TEF_DESFAZIMENTO) .OR. ;
	(cTipSol=="F0" .AND. lInfoCard) .OR. ;
    (cTipSol=="70" .AND. ctFuncao$"27"))
	nHandle := fCreate("TMPTRN." + cPTerm)
	fWrite(nHandle,"00=N"+";02="+aTefDados[1][2]+";09="+aTefDados[1][9]+";11="+aTefDados[1][11]+";")
	fClose(nHandle)
	__CopyFile("TMPTRN." + cPTerm,"NCNTRN"+Strzero(nX,2)+"." + cPTerm)
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(3) " + "00=N"+";02="+aTefDados[1][2]+";09="+aTefDados[1][9]+";11="+aTefDados[1][11]+";" )
	Endif						
Endif   
Return( { nResult, If( Len( aLHeader ) > 0, aLHeader[ 3 ], "FF" ), aTick, aProm, lRetry, cMult, cGran, cResposta } )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010BinCar ³ Autor ³Aline Correa do Vale ³ Data ³ 14/07/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consulta ao BIN do cartao     							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ cTipVen 1-Cartao de Debito 0-Cartao de Credito			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> .T. ou .F.										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010BinCar(cTipVen, lLerPin, lApresenta,nX)

Local oDlg
Local cRede
Local cResposta
Local lRet			:=.F.
Local nMod			:=1
Local cVerSenha		:=""
Local cDigCtrl 		:=Space(4)
Local cData 		:=ctDat_Venc
Local nTentativas	:=0
Local cMsgDados		:="Cartão "
Local lSaiCart		:=.T.
Local lSintVisu		:= If(Type("lVisuSint")<>"U",lVisuSint,.F.)	//Tratamento para a nova identificação do cartão no Multi-TEF

If lTefMult .AND. nX<>nil
   If cOpera=="E"
	   cMsgDados += AllTrim(Subst(aTefMult[nX][2],7))+" Doc.: "+AllTrim(Str(Val(aTefMult[nX][7][3])))
   Else	   
	   If lSintVisu
	   	   //Nova mensagem com ID do Cartão		
		   cMsgDados += AllTrim(Subst(aTefMult[nX][2],1,4))
       Else	
	   	   //Mensagem com os 4 Ultimos Dígitos do Cartão
	       cMsgDados += AllTrim(Subst(aTefMult[nX][2],8))+If(Val(aTefMult[nX][2])>0," Final "+Subst(aTefMult[nX][2],1,4),"")
	   Endif		       
   Endif	  
Endif

lApresenta		:= If(lApresenta==Nil,.T.,lApresenta)
ctTipVen 		:= cTipVen	//0-Cartao de Credito  1-Cartao de Debito
ctDat_Venc		:= If(Empty(ctDat_Venc),"",ctDat_Venc)
cData 			:= If(Empty(cData),"",cData)

// Faz a Leitura de BIN uma unica vez...
aRetorno  := L010MonEnvResp( "15",IF(cTipVen="1","D","C"))

While .T.
	If (aRetorno[ 1 ] <> 1)
		lRet := .F.
		Exit
	Endif
    
    If lVer300
	   If !L010IsTrHabilit(nX)
		  lRet := .F.
		  Exit
	   Endif
    Endif
	
	cResposta := aRetorno[ 8 ]
	cRede 	 := SubStr(cResposta,1,2)
	cVerSenha := SubStr(cResposta,8,1)
	
	//0 - Nao Validar   1 - Validar 4 ultimos digitos do cartao
	If (Substr(cResposta,3,1) == "1")
		// Digita os bytes de controle para verificacao do cartao
		nOpd := 3
		DEFINE MSDIALOG oDlg TITLE cMsgDados FROM 13,21 TO 19,55 OF GetWNDDefault()
		@	.074,.2 TO 2,16.5
		@ 9,007 SAY "Informe os 4 últimos dígitos do cartão:" SIZE 100, 10 OF oDlg PIXEL
		@ 7,101 MSGET cDigCtrl SIZE 16,10 OF oDlg PIXEL PICTURE "@!" VALID !Empty( cDigCtrl )
		DEFINE SBUTTON FROM 33,075 TYPE 1 PIXEL ACTION (lSaiCart:=.F.,oDlg:End() ) ENABLE OF oDlg
		DEFINE SBUTTON FROM 33,105 TYPE 2 PIXEL ACTION (lSaiCart:=.T.,oDlg:End() ) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg CENTERED

		//Saio da tela de digitação sem validar		
		If lSaiCart			
			lRet:=.F.
			Exit
		Else
			lRet:=.T.
			nMod:=1
			
			If (cDigCtrl <> Substr(cResposta,4,4))
				lRet := .F.
	    		nMod:=Aviso( "Atenção","Cartão invalido", {"&Abandona", "&Novamente"},,"Erro - Validação")
				If lVer300
					nTentativas++
					If nTentativas >= 3
						lRet := .F.
						Exit
					Endif
				Endif
			Else
				lRet := .T.
			Endif
			If (nMod == 1)
				Exit
			Endif
			Loop
		Endif
	Else
		lSaiCart := .F.
		lRet	 := .T.
		Exit
	Endif
End

// Se não fechou a tela de digitação efetua as validações
If !lSaiCart
	If lVer300 .AND. lRet
		If !L010Bin(lApresenta,nX)
			Return(.F.)
		Endif
	Endif
	ctDat_Venc := cData
	If (cVerSenha == "1") .OR. (cVerSenha == "2")  //Solicitar senha obrigatoriamente
		lLerPin := .T.
	Else
		lLerPin := .F.
	Endif
Endif
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010TefAbre  ³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Realiza a abertura do PDV junto ao TEF 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010TefAbre()									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> .T. ou .F.						 			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ Funcao utilizada tambem pelo LOJAXFUN.PRX 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010TefAbre()

Local lRet 		:= .F.
Local aRetorno
Local nMod
Local aDesfaz	:= {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Para o Mexico nao existe transacao de abertura          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc=="MEX"
   lRet			:=oAutocom:lAtivo
   ltTefAberto 	:=lRet
   Return lRet
Endif   
//Esta função verifica se este número de terminal pode se logar na rede TEF, verifica se não existe 
//algum outro terminal já logado com o mesmo número, impedindo que os arquivos sejam sobrepostos
If !L010TermTef()
	Return(.F.)
Endif

// Valida a existencia dos campos na tabela SL4, Necesario para Multiplas transacoes TEF
Lj010TSL4()

//HOMOLOGACAO: Os desfazimentos devem ser enviados ao Sitef Antes da Abertura de Terminal
aDesFaz := L010TefDesf()
If aDesFaz[1] .AND. !L010ConfTrn()   //Encontrei um arquivo para enviar o desfazimento
	lRet := .F.
	Aviso( "Atenção","Não foi possível efetuar o cancelamento das pendências devido a falha de comunicação com o Sitef."+;
	"Reestabeleça a comunicação antes de efetuar novas operações TEF!",{"&Abandona"},,"Erro - Comunicação")
Else
	While .T.
		ltTCredit  := .T.
		ltCpParCD  := .T.
		ltCpPreCD  := .T.
		ltCpForCD  := .T.
		ltCpCD	   := .T.
		ltCancCD   := .T.
		ltCancCC   := .T.
		aRetorno 	:= L010MonEnvResp( "01" )
		ltTefAberto :=If( aRetorno[ 1 ] == 1, .T., .F. )
		If aRetorno[ 1 ] <> 1 .AND. cModulo != 'TMK'
			nMod:=Aviso( "Atenção","Não foi possível abrir o PDV junto a rede TEF" , {"&Abandona", "&Novamente"},,"Erro - Abertura")
			If (nMod == 1)
				Exit
			Endif
			Loop
		Else
			lRet := .T.
			Exit
		Endif
	End
Endif

Return  lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010TefImpr  ³ Autor ³ Sergio / Luiz      ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime o ticket, promissoria, verifica e envia mensagem   ³±±
±±³			 ³ de confirmacao ou nao confirmacao e notifica operador 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010TefImpr( ExpA1, [ ExpA2 ], [ ExpC1 ] )		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpA1 -> Ticket formatado / ExpA2 -> Promissoria formatad  ³±±
±±³			 ³ ExpC1 -> String Garantia de cheque                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Venda realizada com sucesso                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T                                                   ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010TefImpr( aTicket    ,aPromis     ,lConfTef  ,lConfirma  ,;
                             cFPagReceb ,lTemTEFPend , lRelGer  ,lRecCel	,;
                             lCancTEF2	,nOperacao	, lConfNF)
Local nOpt
Local nLoop
Local cTickForm
Local lPronta
Local nLaco
Local nI
Local nFor
Local nY
Local cImpTEF		:= LJGetStation("IMPTEF")
Local nCopias		:= Max(LjGetStation("TEFVIAS"),1)
Local cFPgto    	:= ""
Local xValorTef 	:= ""
Local nRet			:= 1
Local aTickTmp  	:= {}
Local cTotalizNFis	:= "01"										// Se nao existir o SIGALOJA.INI mantem sempre o totalizador "01" como DEFAULT porque temos um legado que sempre utilizou desta forma
Local cMV_TEFPEND   := SuperGetMV("MV_TEFPEND",,"0")         	// Define o tratamento a ser realizado quando uma ou mais transacoes TEF ficam pendentes
Local lContinua		:= .T.										// Define se continua o While da impressao do TEF
Local lRet			:= .F.										// Controle de Retorno do Gerenciador Padrao
Local nParc			:= 0
Local nBandeira     := L010GetGPAtivo()					        // Controla a administradora que foi efetuada a transacao TEF baseado no aTefDisc
Local cValorCNF     := "0"                                      // Valor do Comprovante não fiscal
Local cTempImp		:= ""										// Texto de impressao parcial (3 em 3 linhas)
Local nVlMultCart	:= 0										// Variavel que guardara os valores dos cartoes em caso de venda diferente de Retira
Local nPosEntrega   := Iif(nModulo == 12 .AND. Type("aPosCpo") <> "U", aPosCpo[Ascan(aPosCpo,{|x| Alltrim(Upper(x[1])) == "LR_ENTREGA"})][2], 0)
Local nSaltoLn		:= SuperGetMV("MV_FTTEFLI",,1)							// Linha pula entre comprovante		
Local aAreaSL2 		:= SL2->(GetArea())							// WorkArea da tabela SL2
Local aAreaSL4 		:= SL4->(GetArea())							// WorkArea da tabela SL4 
Local lFtvdVer12	:= LjFTVD() 		//Verifica se é Release 11.7 - Compatibilização Venda Direta x Venda Assisitida
Local lIsAllTef		:= .F.

DEFAULT cFPagReceb 	:= ""
DEFAULT lTemTEFPend := .F.
DEFAULT lRelGer     := .F.  
DEFAULT lRecCel		:= .F. //Impressão de comprovante de recarga de celular
DEFAULT lConfirma 	:= .F.
DEFAULT lCancTEF2 	:= .F.
DEFAULT nOperacao	:= 0
Default lConfNF		:= .F.

If EmpTy(aTicket) .AND. Empty(aPromis) .AND. IIf(cMV_TEFPEND $ "1|2",!lTemTEFPend,.T.)
	Return (.T.)
Endif

If lFtvdVer12
	lIsAllTef	:= L010IsDirecao(nBandeira) .OR. L010IsPayGo(nBandeira) .OR. L010IsAuttar(nBandeira)
	cTickForm	:= ""
	
	For nFor := 1 To Len(aTicket)
		cTempImp	:= RTrim(aTicket[nFor]) + IIF(lIsAllTef, "", Chr(10))
		cTempImp	:= LjRmvCRLF(cTempImp) 
		If !Empty(cTempImp)
			cTickForm	+= cTempImp + CHR(10)
		EndIf
	Next nFor
	
	nRet := INFTexto(cTickForm)
	If nRet <> 0
		LjGrvLog(Nil, "Impressao do comprovante não fiscal sem sucesso")
		COnout("Impressao do comprovante não fiscal sem sucesso")
	ElseIf lConfNF
		L010NCNCNF(.F.)			
		If !lCancTEF2 //Que não seja cancelamento
			LJRenDTEF()
		EndIf	
	EndIf
	
	If nRet == 0
		If nSaltoLn > 0
			INFTexto(Replic(Chr(10),nSaltoLn)) //Salta linha extra
		EndIf
		
		INFCutPpr()
	EndIf
		
	Return (.T.)
Endif	
						
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se manda ou nao a confirma‡ao ( DEFAULT == .T. ) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lConfTef := If( Valtype(lConfTef) == "L", lConfTef , .T. )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define qual porta e modelo utilizar para imprimir cupom TEF  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cImpTEF == "N" .OR. (nModulo == 13 .OR. nModulo == 73)
	IF lConfTef
		While !Empty(c030) .AND. ! LjMsg(Alltrim(c030),"Resultado",__INFO)
		End
		L010Confirma( .T. )
		If !Empty(LOG_TEF)
			LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "6) Confirmação com .T." )
		Endif						
	Endif
	Return (.T.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona ao array do ticket o numero de copias de cupons cofigurado ³
//³ Adiciona ao array do ticket a(s) notas(s) promissoria(s)			³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Este laco e porque nao se pode imprimir promissoria nas duas vias do cupom tef

xValorTef := 0

// Para o Mexico continua mesma logica
If cPaisLoc="MEX"
	For nY:= 1 To If(!lTefMult .OR. !lConfTef , 1 , Len(aTefMult) )
	    If lTefMult .AND. lConfTef .AND. Len(aTefMult[nY][7])>0
			xValorTef += aTefMult[nY][4]
			aTicket   := aClone(aTefMult[nY][7][1][14])
			aPromis   := aClone(aTefMult[nY][11])
		Endif	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Para o MEXico a quantidade de ticket e controlada pela funcao ³
		//³IFTxtNFis                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    aEval( aTicket, { |x| aAdd( aTickTmp, x ) } )
	Next nY
	aTicket 	:= Aclone(aTickTmp)	
Endif

lPronta 	:= .T.
xValorTef 	:= Str(xValorTef,14,2)
cValorCNF   := xValorTef

If ValType( aTicket ) == "A"
	IF cImpTEF == "F"
		If  (cOpera == "B" .AND. !lRelGer) .OR. ;
			(cOpera == "I" .AND. cTipVenCon == "V") .OR. ;
		   (cOpera == "P" .AND. (cTipVenCon == "S" .OR. cTipVenCon == "PG")) 
			If ValType(nParCred) <> "N"
				nParCred := 0
			Endif          
            
			// Forma usada para diferenciar recebimentos nao fiscais
			// Ex.: Venda do tipo entrega com pagamento em R$ e CC
			// O primeiro comprovante fiscal eh o comprovante da venda toda
			// O segundo comprovante nao fiscal sera o comprovante do cartao de credito
			// Usando esta forma de pagamento os totalizadores continuaram somando no total da impressora 
			// Porem facilitara a subtracao do total da venda menos a forma informada
			// Com isso a conclusao eh que com esta operacao os numerarios do da caixa ficara facilitado na conferencia
									                                         
			// Modulo SIGALOJA						
			If nModulo == 12 .AND. nPosEntrega > 0
	            For nI = 1 To Len(aCols)
					// Verifica a existencia de itens diferentes de Retira
					If !Empty(aCols[nI][nPosEntrega]) .AND. aCols[nI][nPosEntrega] <> "2"
					    For nY = 1 to Len(aPgtos)
							If Alltrim(aPgtos[nY][3]) $ "CC/CD"
								// Soma as formas em cartao
								nVlMultCart += aPgtos[nY][2]
							Endif
					    Next nY 
					    Exit								
					Endif            
	            Next nI	            
			ElseIf nModulo == 23 // Modulo FrontLoja
				// Verifica a existencia de item diferente de Retira(SL2->L2_ENTREGA<>Branco e SL2->L2_ENTREGA <> "2")			
				// No Front considera a concomitancia e que os registros das tabelas ja estejam gravados
				DbSelectArea("SL2")
				SL2->(DbSetOrder(1))
				DbSeek(xFilial("SL2")+SL1->L1_NUM)
				While !SL2->(Eof()) .AND. (SL2->L2_FILIAL+SL2->L2_NUM == xFilial("SL2")+SL1->L1_NUM)
					If !Empty(SL2->L2_ENTREGA) .AND. SL2->L2_ENTREGA <> "2"
						DbSelectArea("SL4")
						SL4->(DbSetOrder(1))
						DbSeek(xFilial("SL4")+SL1->L1_NUM)
						While !SL4->(Eof()) .AND. (SL4->L4_FILIAL+SL4->L4_NUM == xFilial("SL4")+SL1->L1_NUM)
				            // Verifica pagamentos efetuados com cartao				
							If !Empty(SL4->L4_FORMA) .AND. Alltrim(SL4->L4_FORMA) $ "CC/CD"
								// Soma as formas em cartao
								nVlMultCart += SL4->L4_VALOR
							Endif 
					        SL4->(DbSkip())
						Enddo             
                        Exit
                    Endif				
			        SL2->(DbSkip())
				Enddo			
				RestArea(aAreaSL2)												
				RestArea(aAreaSL4)								
			Endif	            
			
            // Este valor sera passado para o Meio de Pagamento que estiver configurado no SIGALOJA.INI
            // Sera passada a variavel nVlMultCart para chave [Condicao de Pagamento] do conteudo FORMATEFPED = NOME DO MEIO DE PAGAMENTO						
			If nVlMultCart > 0			
				cFPgto := GetPvProfString("condicao de pagamento","FORMATEFPED", "", GetClientDir()+"SIGALOJA.INI")			    			
			Endif	
													
			If nParCred > 0
				If Empty(cFPgto)
					cFPgto    := RetDescrFP( "CC" )
				Endif	
				xValorTef := StrZero(nCartao*100,14)
				cValorCNF := Iif(nVlMultCart==0, Str(nCartao,14,2), Str(nVlMultCart,14,2))
			Else
				If ValType(nParDeb) <> "N"
					nParDeb := 0
				Endif
				If nParDeb > 0  
					If Empty(cFPgto)				
						cFPgto    := RetDescrFP( "CD" )
					Endif	
					xValorTef := StrZero(nDebito*100,14)
					cValorCNF := Iif(nVlMultCart==0, Str(nDebito,14,2), Str(nVlMultCart,14,2))					
				Else //CCS
					If ValType(nParDin) <> "N"
						nParDin := 0
					Endif
					If nParDin > 0
						cFPgto    := RetDescrFP( "R$" )
						xValorTef := Str(nDin,14,2)
					Else
						If ValType(nParCheq) <> "N"
							nParCheq := 0
						Endif
						If nParCheq > 0
							cFPgto    := RetDescrFP( "CH" )
							xValorTef := Str(nCheq,14,2)
						Endif
					Endif
					cValorCNF := xValorTef
				Endif
			Endif
		Endif

		If (cOpera == "I" .AND. cTipVenCon == "V") .OR. ( cOpera == "P" )  .OR. ( cOpera $ "BD" .AND. !lRelGer )
			nOpt := 1
			If (At("S",LjGetStation("TEFCONS"))<>0 .OR. cTipVenCon == "R") .AND. Empty(cFPgto)
   	            nCheq     := If(nCheq==NIL,1,nCheq)
       	        cFPgto    := RetDescrFP( "CH" )
				xValorTef := Str(nCheq,14,2)
				cValorCNF := xValorTef
            EndIf
		Else
			nOpt := 2
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Define qual sera o totalizdor utilizado para a impressao do comprovante TEF.          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRecCel
			cTotalizNFis := GetPvProfString("recarga celular","Totalizador", "01", GetClientDir()+"SIGALOJA.INI") 	    				
		ElseIf !cOpera $ "B" 		
			cTotalizNFis := GetPvProfString("Comprovante TEF", "Totalizador", "01", GetClientDir()+"SIGALOJA.INI") 
	    Else 
	    	cTotalizNFis:= GetPvProfString("Correspondente Bancario","Totalizador", "01", GetClientDir()+"SIGALOJA.INI") 
	    EndIf
	
		While lContinua
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Quando eh utilizado o LOJA010T para Recebimento (Front Loja e Venda Assistida),       ³
			//³a forma de pagamento utilizada em todos os Recebimentos Nao-Fiscais DEVERA ser a mesma³
			//³utilizada no comprovante de TEF.                                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(cFPagReceb)
				cFPgto := cFPagReceb
			Endif                        
					
			Do Case
				//  IMPRESSAO DE CUPOM VINCULADO (nOpt == 1)
				Case nOpt == 1  

					/* ABERTURA DO CUPOM VINCULADO */
					If !Empty(LOG_TEF)
						LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 1 - Abr Imp.' )
					Endif

					LjGrvLog( SL1->L1_NUM , 'TEF 1 - Abr Imp.' )
										       
					If cOpera == "I"
						If Empty(cFPgto)
							cFPgto := RetDescrFP( SL1->L1_FORMPG)
						Endif	
						If Val(cValorCNF) == 0
							cValorCNF := Alltrim(Str(SL1->L1_VLRTOT,14))
						Endif		
                    Endif
                    
					nRet := IFAbrCNFis( nHdlECF, AllTrim(cFPgto), cValorCNF, cTotalizNFis )
					If L010AskImp(@nOpt, nRet, .T.) .OR. (nOpt == 2 .And. !LjSenSupTEF())
						If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
						EndIf

						LjGrvLog( SL1->L1_NUM , "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )

						nOpt := 2
						Loop
					Else
						If nOpt == 2
 							lContinua := .F.
 						EndIf
 					EndIf 
					             					         					
					/* IMPRESSAO DO TEXTO NO CUPOM VINCULADO */
					If lContinua
						nOpt := 1  
						cTickForm := ""

						LjGrvLog(Nil, "L010TefImpr - Inicio da impressão do texto no cupom vinculado")	

						For nFor := 1 To Len(aTicket)						
							cTempImp	:= RTrim(aTicket[nFor]) + IIF(lIsAllTef, "", Chr(10))
							cTempImp	:= LjRmvCRLF(cTempImp) 
							If !Empty(cTempImp)
								cTickForm	+= cTempImp + CHR(10)
							EndIf
						Next nFor

						LjGrvLog(Nil, "L010TefImpr - Antes da chamada IFTxtNFis")	

						nRet := IFTxtNFis(nHdlECF, cTickForm, 1)
						If nRet <> 0
							LjGrvLog(Nil, "Problemas na impressão do comprovante")
						EndIf
					
						If L010AskImp(@nOpt,nRet, .T.) .OR. (nOpt == 2 .And. !LjSenSupTEF())
							If !Empty(LOG_TEF)
								LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
							EndIf

							LjGrvLog( SL1->L1_NUM , "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
							
							nOpt := 2
							Loop
						Else
							If nOpt == 2
								lContinua := .F.
							EndIf
							If !Empty(LOG_TEF)
	    	 					LjWriteLog( LOG_TEF + SL1->L1_NUM + ".TXT", "TEF 2 - Apos Imp" + StrZero(nRet,2) )
							Endif

							LjGrvLog( SL1->L1_NUM , "TEF 2 - Apos Imp" + StrZero(nRet,2) )
						Endif
						         
						
						/* FECHAMENTO DO CUPOM VINCULADO */
						If lContinua
							nOpt := 1
							Sleep(2)
							nRet := IFFchCNFis( nHdlECF )

						   /*Apaga o arquivos se for diferente de BANESCARD */
							If nITmp <> 5 .AND. nRet == 0
								L010TCLear()
							EndIf	
							
 							If L010AskImp(@nOpt,nRet, .T.) .OR. (nOpt == 2 .And. !LjSenSupTEF())
								If !Empty(LOG_TEF)
									LjWriteLog( LOG_TEF + SL1->L1_NUM + ".TXT", "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
								EndIf

								LjGrvLog( SL1->L1_NUM , "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
								
								nOpt := 2
								Loop
							Else
								If nOpt == 2
									lContinua := .F.
								EndIf
							Endif
						EndIf
					Endif
					
				//  IMPRESSAO DE RELATORIO GERENCIAL (nOpt == 2 - Reimpressao do cupom)
				Case nOpt == 2
					cTickForm := ""
					
					For nFor := 1 To Len(aTicket)
	                	cTickForm += RTrim(aTicket[nFor]) + IIF(L010IsDirecao(nBandeira) .OR. L010IsPayGo(nBandeira) .OR. L010IsAuttar(nBandeira), "", Chr(10))
    				Next nFor
					
					nOpt := 1
					nRet := IFRelGer(nHdlECF, cTickForm, 1)
					If nRet == 0
					  	lRelGer := .T.
					EndIf
					
					If L010AskImp(@nOpt,nRet, .T.) .OR. (nOpt==2 .And. !LjSenSupTEF())
						If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
						EndIf

						LjGrvLog( SL1->L1_NUM , "Tentou realizar a reimpressão do cupom TEF. Usuario: " + SubStr(cUsuario, 7, 15) )
						nOpt := 2
						Loop
					Endif
			EndCase              

			If nRet == 0 .And. nOpt == 1
				L010NCNCNF(.F.)			
				If !lCancTEF2 .And. nOperacao <> 2 //Que não seja cancelamento e diferente da operação adm (nI == 2)
					LJRenDTEF()
				Else
					If nOperacao == 2
						aTicket := {} //Zero para evitar conteudo duplicado apos imprimir as operações adm
					EndIf
				EndIf
			Endif	
			
			lContinua := .F.
			If nOpt == 1 .AND. nRet <> 0 //somente executa a funcao de fechamento de Cupom Nao-Fiscal quando ele nao estiver fechado
	        	nRet := IFFchCNFis( nHdlECF ) 
	  		EndIf
		End

		lPronta := (nRet == 0)
		
		If lInfoCard .AND. cTipVenCon == "PG"
			MsgInfo("Posicione o Documento e Tecle < Enter > Para Autenticar...","Autentica‡„o")
			nRet := IFAutentic( nHdlECF, '1', xValorTef, '' )
		Endif
		
	Else
		If "COM" $ Upper(LjGetStation("PORTICP"))	// verifica se a impressora de cupom eh via serial
			
			cTickForm := ""
			For nFor := 1 to Len( aTicket )
				cTickForm += rtrim(aTicket[ nFor ]) + Chr(10)
			Next
			If ! empty(cTickForm)
				nRet := ImpCupImp( nHdlCupom, cTickForm )
			Endif

		ElseIf Lj010InitPrint("C")
			
			For nLoop := 1 to nCopias
				For nLaco := 1 to Len (aTicket)
					@ Prow()+1,000 Psay aTicket[nLaco]
				Next
				If ! nLoop == nCopias
					Sleep(5)
				Endif
			Next nLoop
			
			Lj010EndPrint("C")
			lPronta := .T.        
		Else
			lPronta := .F.
		Endif
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se Ok, envia mensagem de confirmacao para o Sitef ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(LOG_TEF)
	LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "lConfTef  = " + Iif(lConfTef,".T.",".F." ) )
Endif						

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificando se o Gerenciador Padrão está átivo    |
//³ Se não estiver, ativa                             |                                                                           o
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lDiscado .AND. !L010IsDirecao(nBandeira) .AND. !L010IsPayGo(nBandeira) .AND. !L010IsAuttar(nBandeira)
	If SuperGetMV("MV_LJATVGP",.F.,.T.)
		LjMsgRun(STR0102 + aTefDisc[nBandeira][1] + STR0103, STR0087, { || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq++,10)), "999-999 = " + cRegFim},nBandeira)}) //"Verificando se o gerenciador padrão " " do TEF discado está ativo." "Aguarde"
		If !lRet
			// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
			If !SuperGetMV("MV_LJTSC")
				// Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
				Aviso( STR0104 , STR0109 ,{"Ok"} ) // ###"TEF Discado" ###"Gerenciador padrão não está ativo e será ativado automaticamente"
				WinExec(cDrvTEFDsc+aTefDisc[nI][4],3)
				Sleep(5)
			EndIf
		EndIf
	Else
		For nParc := 1 To 50
			LjMsgRun(STR0102 + aTefDisc[nBandeira][1] + STR0103, STR0087, { || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nBandeira)}) //"Verificando se o gerenciador padrão " " do TEF discado está ativo." "Aguarde"
			If !lRet
				// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
				If !SuperGetMV("MV_LJTSC")
					// Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
					Alert(STR0108) //"O Gerenciador padrao não esta Ativo - Favor Ativar"
					Sleep(5)
					Loop
				EndIf
			EndIf
			Exit
		Next nParc	
	EndIf
EndIf	  

If lConfTef  .AND. !lTefMult
	If L010IsDirecao(nBandeira)
		lConfirma := LJProcDTEF(!lPronta)
		If lPronta 
			LJ010DelNsuTxt()
		EndIf                  		
	Else                       
		If !L010IsPayGo(nBandeira) .AND. !L010IsAuttar(nBandeira) 	
			lConfirma := L010Confirma(lPronta)
		Else
			lConfirma := .T.	
		Endif	
	EndIf
	
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "7) Confirmação com lPronta = " + Iif(lPronta,".T.",".F." ) )
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "lConfirma = " + Iif(lConfirma,".T.",".F." ) )
	Endif						

    If !lPronta .AND. lConfirma
		MsgInfo("Transação não efetuada, favor reter o cupom")
    Endif

	aTicket:={}
Else
	//HOMOLOGACAO: Apagamos o arquivo após a impressao pois algumas transações não exigem confirmacões
	If lPronta 
		For nY:= 1 To If(lTefMult .AND. Type("aTefMult")=="A" .AND. Len(aTefMult)>0 , Len(aTefMult) , 1 )
			If File("NCNTRN"+StrZero(nY,2)+"."+cPTerm)
				fErase("NCNTRN"+StrZero(nY,2)+"."+cPTerm)
				If !Empty(LOG_TEF)
					LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(4) " + "Apagou o arquivo" )
				Endif						
 			Endif
		Next nY
	Endif
Endif

Return lPronta

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010Confirma³ Autor ³ Sergio / Luiz 	    ³ Data ³ 10/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Envia uma mensagem de confirmacao ou nao confirmacao para  ³±±
±±³			 ³ o SiTef													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ L010Confirma( ExpL1 )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpL1 -> Confirma ou nao.								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010Confirma(lConfirma,cArq,cDesfaz)
Local nAt
Local nI
Local nO
Local nHandle
Local lSucess
Local nSecIni
Local cTipo		:= If( lConfirma , "C" , "N" )	// Tipo de transação a ser efetuada
Local cBuffer	:= ""							// Contem os dados da transacao	
Local aVarCfr	:= {}
Local lTimeOut	:= .T.							// Verifica se a resposta excedeu o time-out do PDV
Local lRet		:= .T.							// Retorno lógico da função para a chamada principal
Local nTentar	:= 0							// Controle de tentativas para se apagar o arquivo de resposta do SiteF
Local aTransAtu	:= TEF_aTrans()					// Carrega os dados guardados para efetuarmos a transacao de desfazimento
Local lFezDesf	:= .T.
Local nTot		:= 0							// Inicia com valor DEFAULT = 0 Contador auxiliar
Local aDesfaz	:= {}							// 1-Indica se encontrou o ncn do desfazimento 2-Indica o número de controle do ncn
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

DEFAULT cArq	:= ""
DEFAULT cDesfaz := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida se a variavel aTefMult foi declarada para evitar Alias Does not Exist³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("aTefMult") == "U"
	Private aTefMult:= {}						// Declara como private para garantir a integridade
Endif

If (!lTefMult .OR. !Empty(cArq)) 
	nTot :=  1 
Else
	nTot := Len(aTefMult) 
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Para Mexico- Nao existe a "perna" de confirmacao da Transacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc=="MEX" 
   Return .T.
Endif
//HOMOLOGACAO: Controlar as operacoes de desfazimento
If cDesfaz == "D"
   cTipo 	:= "2"       
   lFezDesf	:= .F.
Endif

For nI:= 1 To nTot

    If lTefMult .AND. Empty(cArq)
		//HOMOLOGACAO: Se mandei o desfazimento uma vez e não obitve sucesso não prosseguir com a operação
		If cTipo == "2" .AND. !lRet
           Exit
		Endif
        //So enviar NAO CONFIRMACAO nas transacoes efetuadas com sucesso
        If (!lConfirma .AND. !aTefMult[nI][8]) .OR. ;
           Len(aTefMult[nI][7])==0
			Loop
        Endif
		cBuffer	 := ""
		aVarCfr  := {}
		lTimeOut := .T.
		lRet     := .T.
		aTefDados:=aClone(aTefMult[nI][7])
	Endif

	//Criando os arquivos de transacoes confirmadas para controle de queda do sistema, as transações de desfazimento não precisam pois o arquivo já está gravado
	If lFezDesf .AND. ;
		Empty(cArq) .AND. ;
		lVer300 .AND. ;
		cTipTEF $ TEF_SEMCLIENT_DEDICADO+";"+TEF_COMCLIENT_DEDICADO .AND. ;
		Val(aTefDados[1][9])>0

		nHandle := fCreate("TMPTRN." + cPTerm) //Gravacao dos dados da Ultima confirmacao TEF para que seja possivel sua retransmissao caso nao consiga finalizar a transacao
		fWrite(nHandle,"00="+If(lConfirma,"S","N")+";02="+aTefDados[1][2]+";09="+aTefDados[1][9]+";11="+aTefDados[1][11]+";")
		fClose(nHandle)
		__CopyFile("TMPTRN." + cPTerm,"NCNTRN"+Strzero(nI,2)+"." + cPTerm)
		If !Empty(LOG_TEF)
			LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 4 - Grava arq. conf ' + "00="+If(lConfirma,"S","N")+";02="+aTefDados[1][2]+";09="+aTefDados[1][9]+";11="+aTefDados[1][11]+";" )
		Endif
	Else
		If !Empty(LOG_TEF)
			LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 4A- Identificacao Trn'+ "00="+If(lConfirma,"S","N")+";02="+aTefDados[1][2]+";09="+aTefDados[1][9]+";11="+aTefDados[1][11]+";" )
		Endif
	Endif
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se lRet=.F., significa que a tentativa de confirmacao da transacao³
	//³falhou, entao efetua somente a gravacao do arquivo de confirmacao ³
	//³que sera utilizado na proxima trn TEF, pela funcao L010ConfTrn    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lRet
	    Loop
	Endif

	If lVer300
		If ctDt_h=Nil .OR. ctRede_h=Nil .OR. ctNum_h=Nil .OR. lTefMult
			ctDt_h   := Space(4)
			ctRede_h := Space(2)
			ctNum_h  := Space(6)
			If (aTefDados <> Nil) .AND. Len(aTEFDados)<>0
				ctDt_h   := aTefDados[1][2]
				ctRede_h := aTefDados[1][11]
				ctNum_h  := aTefDados[1][9]
			Endif
		Endif
	Else
		ctDt_h   := Space(4)
		ctRede_h := Space(2)
		ctNum_h  := Space(6)
		If (aTefDados <> Nil) .AND. Len(aTEFDados)<>0
			ctDt_h   := aTefDados[1][2]
			ctRede_h := aTefDados[1][11]
			ctNum_h  := aTefDados[1][9]
		Endif
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o campo de dados conforme o tipo de transacao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lVer300
		//HOMOLOGACAO: Transação de Desfazimento é montada neste momento
		If cTipo=="2" .AND. !lFezDesf
			aVarCfr := {aTransAtu[3]+chr(0)+chr(0)+aTransAtu[2]+chr(0)}
		Else	
			aVarCfr := L010StrVar( "82" )
		End
	Else
		aVarCfr := L010StrVar( "80" )
	Endif

	If (ValType( aVarCfr ) <> "C")
		aEval( aVarCfr, { |cElemen| cBuffer += cElemen } )
		ntOffSet := -1
		if len(alltrim(ctTrilha_2)) > 0
			For nAt := 1 to (Len(cBuffer)-len(ctTrilha_2))+1
				if ctTrilha_2 == substr(cBuffer,nAt,len(ctTrilha_2))
					ntOffSet := nAt -1
				Endif
			Next nAt
		Endif
		If ntOffSet == -1 .AND. len(alltrim(ctCartao)) > 0
			For nAt := 1 to (Len(cBuffer)-len(ctCartao))+1
				if ctCartao == substr(cBuffer,nAt,len(ctCartao))
					ntOffSet := nAt -1
				Endif
			Next nAt
		Endif
	Endif

	If ntOffSet < 0
		ntOffSet := 0
	Endif

	//HOMOLOGACAO: Transação de desfazimento
    If cTipo=="2" .AND. !lFezDesf
		ctTrans	 := aTransAtu[1]							//Recupera a transacao utilizada anteriormente
 	    ntOffSet := Val(aTransAtu[4])			            //Recupero o offset calculado desta transacao, contei o codigo da rede mais 2 posicoes
		ntId 	 := Val(aTransAtu[5]) + 1					//Recupero o último o NID da transacao do desfazimento e somo + 1	
	Endif	

	nTam := Len(cBuffer)
	cHeader := L010CHeader( cSiTVers, cTipo, ctTrans, cPIdentTerm , ntSeqTerm, ntId, ntCtrlTran, ntOffSet, cEmpresa, nTam )
	cMessage := cHeader + cBuffer
	
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "Arq.Confirmação " + cMessage )
	Endif						

	If SuperGetMV( "MV_TEFCRIP" ) == "S"
		cTam := Left( cMessage, 2 )
		cCod := Right( cMessage, Len( cMessage ) - 2 )
		nTamX := Len( cCod )
		cCod := CodSitef( cCod )
		cMessage := cTam + cCod
	Endif
	
	
	While .T.
		
		nHandle	:= FCreate( cDirT_Tx + "TEMP." + cPTerm )
		
		FWrite( nHandle, cMessage )
		FClose( nHandle )
		
		// Tentativas de rename do arquivo de transmissao
		If lUsaDisplay
			DisplayEnv(StatDisplay(), "1C" + "Tentando renomear arquivo de transmissao"   )
			DisplayEnv(StatDisplay(), "2C" + " ")
		Endif
		
		// ### "Tentando renomear arquivo de transmissão" ### " - Em comunicação"
		MsgRun( STR0066 ,cMens + STR0067 ,{ || lSucess := L010RenTry()})

		If !Empty(LOG_TEF)
			LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 4B- Renomeou arquivo '+If(lSucess,"S",'N - DOS ERROR '+Str( FError(), 2 )))
		Endif
		
		If !lSucess
			nAction := L010ErroRede( 2 )
			If (nAction == 1)
				lRet := .F.
				Exit
			ElseIf (nAction == 2)
				Loop
			Endif
			
		ElseIf lVer300 .AND. cTipo=="2" .AND. !lFezDesf			//HOMOLOGACAO: Apagar o arquivo no momento do desfazimento
			nSecIni := Seconds()
			// Verifica a existencia do arquivo de resposta do Sitef
			If lUsaDisplay
				DisplayEnv(StatDisplay(), "1C" + STR0068 + " - " + STR0069 ) // ### "Aguardando resposta da rede TEF" ### Cancelando Transação
				DisplayEnv(StatDisplay(), "2C" + " ")
			Endif
			// ### 
			MsgRun( STR0068 + " - " + STR0069 ,cMens + STR0067 ,{ || lTimeOut := L010WaitWin( cDirT_Rx + "PDV." + cPTerm )})
			If !lTimeOut .AND. File( cDirT_Rx + "PDV." + cPTerm )
				While FErase( cDirT_Rx + "PDV." + cPTerm )= -1
		           Sleep(2)
				End
				If !Empty(LOG_TEF)
					LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 5D- Eliminando Arquivo')
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Quando controlamos os desfazimentos das múltiplas transações precisamos verificar        ³
				//³qual é o ncn do desfazimento para apagá-lo deixando somente  as confirmações ou não conf.³
				//³a serem enviadas                                                                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aDesFaz := L010TefDesf()
				If aDesFaz[1] .AND. aDesFaz[2] > 0
				   If File("NCNTRN"+StrZero(aDesFaz[2],2)+"."+cPTerm)
					  fErase("NCNTRN"+StrZero(aDesFaz[2],2)+"."+cPTerm)
	 				  	If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(5) " + "Apagou o arquivo" )
						Endif						
				   Endif
				Endif
				lRet := .T.
			Else
				lRet := .F.
			Endif
			//HOMOLOGACAO: Ja enviei o desfazimento uma vez agora o tratamento passa a ser pelo arquivo
			lFezDesf := .T.
			TEF_lEnvDF(.F.)

		Elseif lVer300 .AND. lAceite .AND. cTipo <> "2"	//Neste caso devo tratar a resposta pois são transações de 4 pernas
			If !Empty(LOG_TEF)
				LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 5 - Enviou conf. e aguarda resposta' )
			Endif
			nSecIni := Seconds()
			// Verifica a existencia do arquivo de resposta do Sitef
			If lUsaDisplay
				DisplayEnv(StatDisplay(), "1C" + "Aguardando resposta da rede TEF"  )
				DisplayEnv(StatDisplay(), "2C" + " ")
			Endif
			// "Aguardando resposta da rede TEF" ###  - Em comunicação
			MsgRun( STR0068 ,cMens + STR0067 ,{ || lTimeOut := L010WaitWin( cDirT_Rx + "PDV." + cPTerm )})
			lRet := .F.
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Abre o arquivo de resposta ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(LOG_TEF)
				LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 5A- TEF Respondeu?   '+If(!lTimeOut,"S","N"))
			Endif
			If !lTimeOut
				nHandle := -1
				If File(cDirT_Rx + "PDV." + cPTerm )
					For nO:= 1 to 5
						nHandle	:= FOpen( cDirT_Rx + "PDV." + cPTerm )
						If nHandle<>-1
							lRet := .F.
							Exit
						Endif
						Sleep(2)
					Next nO
				Endif
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Faz a leitura do Header ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ    
				If nHandle >= 0
					nSize 	 := FSeek( nHandle, 0, 2 )
					FSeek( nHandle, 0 )
					cRBuffer := Space( nSize )
					nRead 	 := FRead( nHandle, @cRBuffer, nSize )
					FClose( nHandle )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Decodif. a mensagem se o parametro de criptografia estiver habil.³
					//³ Apenas os dois primeiros bytes ( tamanho ) nao sao decodificados ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If SuperGetMV( "MV_TEFCRIP" ) == "S"
						cTam	:= Left( cRBuffer, 2 )
						cDec	:= Right( cRBuffer, Len( cRBuffer ) - 2 )
						nTamX := Len( cDec )
						cDec	:= DecSitef( cDec )
						cRBuffer := cTam + cDec
					Endif
					cHeader	:= SubStr( cRBuffer, 1, 32 )
					aLHeader	:= L010LHeader( cHeader )
					If (aLHeader[ 3 ] $ "130;131") .AND. (ntId == aLHeader[ 6 ])
						If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 5B- Resposta OK')
						Endif
						lRet := .T.
					ElseIf (ntId <> aLHeader[ 6 ])
						If !Empty(LOG_TEF)
							LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 5C- Erro de resposta - DOS ERROR '+Str( FError(),2))
						Endif
						If L010ErroRede( 8 ) == 1
							lRet := .F.
							Exit
						Else
							Loop
						Endif
					Endif
				Endif
			Endif
		Endif
		Exit
	End
	
	If lVer300 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Para transações somente de duas pernas, não preciso efetuar                                                    ³
		//³a leitura do arq. de resposta porém precisamos apagá-lo para que não tenhamos problemas nas próximas transações³
		//|não podemos colocar mensagem devido ao processo de homologação												  |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTentar := 0
		While nTentar < 4
			If File( cDirT_Rx + "PDV." + cPTerm ) .AND. FErase( cDirT_Rx + "PDV." + cPTerm ) == 0
				Exit
			Endif
			Sleep(2)
			nTentar ++
		End

        //Caso receba a resposta da confirmacao elimina o arquivo para nao tentar confirmar na proxima transacao
		If lTefMult .AND. Empty(cArq) .AND. cTipo<>"2"
			aTefMult[nI][9] := lRet
		Endif

        //Apaga o arquivo criado para reimpressão do cupom Tef, pois a transação não foi confirmada
		If !lConfirma .AND. cTipo<>"2"
			fErase("RX_PRN." + cPTerm)
		Endif

        //Apago os arquivos lidos de não conf. , conf. ou desfazimento
		If lRet  
		   If !Empty(cArq)										
			   If File(cArq)
				  fErase(cArq)
			   Endif
		   Else
			   If cTipo<>"2" .AND. File("NCNTRN"+StrZero(nI,2)+"."+cPTerm)
				  fErase("NCNTRN"+StrZero(nI,2)+"."+cPTerm)               
					If !Empty(LOG_TEF)
						LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "NCN(6) " + "Apagou o arquivo" )
					Endif						
			   Endif
		   Endif	   
		Endif
		
	Endif
Next nI

If !Empty(LOG_TEF) .AND. cTipo<>"2"
	LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF 6 - Confirmou a Transacao ? '+If(lRet,'S','N') )
Endif

lAceite := .F.
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010Dec2Hex()³ Autor ³ Sergio / Luiz 	    ³ Data ³ 12/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Converte um numero decimal ate' 255 para hexadecimal        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := L010Dec2Hex( ExpN1 ) 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 -> valor 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC1 -> String de 2 bytes 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Dec2Hex(nVal)
Local cString := "0123456789ABCDEF"
Return(Substr(cString,Int(nVal/16)+1,1)+Substr(cString,nVal-(Int(nVal/16)*16)+1,1))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010Hex2Dec()³ Autor ³ Sergio / Luiz 	    ³ Data ³ 12/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte um numero Hexadecimal para decimal ate' 65535     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN1 := L010Hex2Dec( ExpC1 ) 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 -> String a converter ( ate 4 bytes )				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpN1 -> Numero decimal 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Hex2Dec(cVal)

Local cString:="0123456789ABCDEF", nVal:=0

If Len(cVal) < 4
	cVal:= Replicate("0", 4 - Len(cVal) ) + cVal
Endif
nVal := ( At( Left( cVal, 1 )   , cString ) - 1  ) * 4096
nVal += ( At( Substr( cVal, 2, 1 ), cString ) - 1  ) * 256
nVal += ( At( Substr( cVal, 3, 1 ), cString ) - 1  ) * 16
nVal += ( At( Substr( cVal, 4, 1 ), cString ) - 1  )
Return( nVal )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010CHeader()³ Autor ³ Sergio Silveira    ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria uma String contendo o Header da mensagem pronto p/	  ³±±
±±³			 ³ gravacao 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := L010DscRsp( ExpC2, ExpC3, ExpN1, ExpC4, ExpN2,	  ³±±
±±³			 ³ ExpN3, ExpN4, ExpN5, ExpN6 )								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC2 -> Versao no formato "01.00"                         ³±±
±±³			 ³ ExpC3 -> Tipo de mensagem "P", "C" ou "N"                  ³±±
±±³			 ³ ExpN1 -> Numero da transacao								  ³±±
±±³			 ³ ExpC4 -> Identifica‡ao do terminal ( 6 Bytes )	 	  	  ³±±
±±³			 ³ ExpN2 -> Numero logico do terminal						  ³±±
±±³			 ³ ExpN3 -> Numero de identificacao da mensagem 			  ³±±
±±³			 ³ ExpN4 -> Numero de controle da Transacao					  ³±±
±±³			 ³ ExpN5 -> Deslocamento em bytes do numero do cartao 		  ³±±
±±³			 ³ ExpN6 -> Tamanho do campo de dados						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC1 -> String											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CHeader( cVers		, cTipo	, cTrans	, cIdentTer	, ;
							 nSeqTerm	, nId	, nCtrlTran	, nOffSet	, ;
							 cEmpresa	, nTamDados )

Local lOk	  := .T.
Local cString := ""
Local aCampos := {}

aCampos := Array( 11 )

aFill( aCampos, " " )

aCampos[1]  := L010Dec2WChr( nTamDados + 30 )
aCampos[6]  := "000"
aCampos[8]  := Chr(0) + Chr(0)
aCampos[10] := cEmpresa
aCampos[11] := L010Dec2WChr( nTamDados )
aCampos[2]  := Chr( L010Hex2Dec( Right( cVers, 2 ) ) ) + ;
Chr( L010Hex2Dec(	Left( cVers, 2 ) ) )

Do Case
	Case (cTipo == "P")
		aCampos[3] := Chr( 1 )
	Case (cTipo == "C")
		If lVer300
			aCampos[3] := Chr( 130 ) 	// "82" CONFIRMACAO
		else
			aCampos[3] := Chr( 128 )
		Endif
	Case (cTipo == "N")
		if lVer300
			aCampos[3] := Chr( 131 ) 	// "83" NAO CONFIRMACAO
		else
			aCampos[3] := Chr( 129 )
		Endif
	Case (cTipo == "2") .AND. lVer300
			aCampos[3] := Chr( 2 ) 		// HOMOLOGACAO: Dezfazimento
	Otherwise
		lOk := .F.
EndCase

aCampos[4]	:= Chr( L010Hex2Dec( AllTrim( cTrans ) ) ) // Transacao em Hexa
aCampos[5]	:= cIdentTer

lOk := If( Len( aCampos[5] ) <> 8, .F., lOk )

aCampos[ 7] := Chr( nId ) // nId -> char em hexa
aCampos[ 9] := L010Dec2WChr( nOffSet )

aEval( aCampos, { |x| cString += x } )
Return( cString )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010Dec2WChr ³ Autor ³ Luiz Vergari		³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte um numero decimal ( ate 65535 ) em um campo tipo  ³±±
±±³			 ³ word ASCII												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := L010Dec2WCHR( ExpN1 )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 -> Valor a converter 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC1 -> Campo tipo word ASCII ( 2 Bytes )				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Dec2WChr( nVal )

Local cHexFirst:="00"
Local cHexSecond:=""
Local nTmp:=0
Local cRet
If nVal > 255
	nTmp := Int( nVal / 256 )
	cHexFirst := L010Dec2Hex( nTmp )
	nTmp := nTmp * 256
Endif

nVal := nVal - nTmp
nTmp := Int( nVal / 16 )
cHexSecond :=	Right( L010Dec2Hex( nTmp ) ,1 )
nTmp := nTmp * 16
nVal := nVal - nTmp
cHexSecond +=	Right( L010Dec2Hex( nVal ) ,1)

cRet := Chr( L010Hex2Dec(cHexSecond) ) + Chr( L010Hex2Dec(cHexFirst) )

Return( cRet )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010LHeader()³ Autor ³ Sergio Silveira    ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Le a String do Header e devolve um array com os conteudos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³ ExpC1 -> String do Header ( 24 Bytes ) 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpA1 -> Array com a seguinte estrutura:					  ³±±
±±³			 ³ 															  ³±±
±±³			 ³ 1 -> N - Tamanho total da mensagem						  ³±±
±±³			 ³ 2 -> C - Versao no formato "01.00"                         ³±±
±±³			 ³ 3 -> N - Codigo da resposta								  ³±±
±±³			 ³ 4 -> N - Numero logico do terminal						  ³±±
±±³			 ³ 5 -> N - Numero de identificacao da mensagem 			  ³±±
±±³			 ³ 6 -> N - Numero de controle da Transacao					  ³±±
±±³			 ³ 7 -> N - OffSet do cartao								  ³±±
±±³			 ³ 8 -> N - Tamanho do campo de dados						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010LHeader( cBuffer )

Local cTamanho
Local cVers
Local cCodResp
Local cNumTerm
Local cNumLogTerm
Local cNumIdMess
Local cNumCtrlTran
Local cOffSet
Local cTamCamDad
Local cEmpresa
Local nTamanho
Local nNumLogTerm
Local nNumIdMess
Local nNumCtrlTran
Local nOffSet
Local nTamCamDad
Local nEmpresa

cTamanho 	 := SubStr( cBuffer,  1, 2 )
cVers 		 := SubStr( cBuffer,  3, 2 )
cCodResp 	 := SubStr( cBuffer,  5, 1 )
cCodTrn		 := SubStr( cBuffer,  6, 1 )
cNumTerm 	 := SubStr( cBuffer,  7, 8 )
cNumLogTerm  := SubStr( cBuffer, 12, 3 )
cNumCtrlTran := SubStr( cBuffer, 18, 1 )
cNumIdMess	 := SubStr( cBuffer, 19, 2 )
cOffSet		 := SubStr( cBuffer, 21, 2 )
cEmpresa     := SubStr( cBuffer, 23, 8 )
cTamCamDad	 := SubStr( cBuffer, 31, 2 )
if Asc(cCodResP) < 100
	cCodResp 	 := StrZero( Asc( cCodResp ), 2 )
else
	cCodResp 	 := StrZero( Asc( cCodResp ), 3 )
Endif
nTamanho 	 := L010WChr2Dec( cTamanho )
cVers 		 := L010Dec2Hex( Asc( Right( cVers, 1 ) ) ) + "." + ;
L010Dec2Hex( Asc(  Left( cVers, 1 ) ) )
nNumLogTerm  := Val( cNumLogTerm )
nNumIdMess	 := Asc( cNumIdMess )
nNumCtrlTran := L010WChr2Dec( cNumCtrlTran )
nOffSet		 := L010WChr2Dec( cOffSet		 )
nTamCamDad	 := L010WChr2Dec( cTamCamDad	 )
nEmpresa	:= L010WChr2Dec( cEmpresa )

Return({nTamanho, cVers, cCodResp, nNumLogTerm, nNumIdMess, nNumCtrlTran,;
nOffSet, nTamCamDad })

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010WChr2Dec ³ Autor ³ Luiz Vergari	    ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte um campo tipo word ASCII em decimal 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN1 := L010WChr2Dec( ExpC1 )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 -> Campo word ASCII								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpN1 -> Numero decimal 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010WChr2Dec(cVal)
Local cResult := ""
Local nCt 	  := 0
For nCt:=Len(cVal) To 1 Step - 1
	cResult += L010Dec2Hex( Asc( Substr(cVal,nCt,1) ) )
Next
Return( L010Hex2Dec( cResult ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³L010StrVar³ Autor ³ Sergio Silveira	    ³ Data ³ 13/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna String de variaveis conforme transacao			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := L010StrVar( ExpN1 )								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> Codigo da transacao								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC1 -> String contendo as variaveis necess. p/ transacao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010StrVar( cTrans,aTrans )
Local aRelation
Local nReg
Local cString
Local cTime   := Time()
Local cMsg530 := Space(12)

If AllTrim(cTrans) <> "01"
	If lVer300
		L010RedeDest()
		// PE para inclusao do codigo do cliente no campo ADM da transação Sitef (Válido somente para a versão desenvolvimento da BrasilTelecom)
		If AllTrim(cTrans) $ "20|22|24|26|38"
			If ExistBlock("LJTEFMSG")
				LjGrvLog(Nil, "Ponto de Entrada LJTEFMSG ")	
				cMsg530 := Subs(ExecBlock("LJTEFMSG",.F.,.F.),1,12)
				LjGrvLog(Nil, "Ponto de Entrada LJTEFMSG - Retorno :", cMsg530)	
				
				ctAdm := "ADM:"+DToS(dDataBase)+";"+Substr(cTime,1,2)+Substr(cTime,4,2)+Substr(cTime,7,2)+";;"+cMsg530+";;"
			Endif
		Else
			ctAdm := ""
		Endif
        If aTrans==Nil
    		If lInfoCard
				// Transacao InfoCard
    			aTrans:={ctRedeDest,ctADM,"6",ctFuncao,ctTipOp,ctCartao,ctValor,Iif(ctFuncao$"3031",ctOrcam,ctPlano),Iif(ctFuncao$"3031",ctDat_Vend,ctOrcam),ctData,ctHoraVend+Iif(ctFuncao$"3031","",Iif(Empty(ctDat_Venc),"",Chr(0)+Alltrim(ctDat_Venc))) }
    		Else
				// Transacao de Reimpressao VISA
    			aTrans:={ctRede_H,ctADM,"26","4",ctNsu,ctDat_Pag}
    		Endif
		Endif
		aRelation := { ;
		{ "10", { ctRedeDest, ctADM, ctValor,  ctCartao, ctSenha, ctTaxa, ctCVV2 } },;
		{ "11", { ctRedeDest, ctADM, ctCartao, ctValor , ctDat_Venc, ctTaxa, ctCVV2 } },;
		{ "12", If(ctTipOp="0",{ ctRedeDest, ctADM, ctTipOp ,ctCartao, ctDat_Venc, ctValor ,ctSenha, ctTaxa,ctCVV2 }, { ctRedeDest, ctADM, ctTipOp,ctCartao, ctDat_Venc, ctValor ,ctSenha, ctData,AllTrim(ctCodAut),ctNSU, ctCVV2  })},;
		{ "15", { ctRedeDest, ctADM, ctTipVen, ctCartao  , ctDat_Venc } },;
		{ "16", { ctRedeDest, ctADM, ctCartao , ctDat_Venc,ctCEP,ctEnd,ctNumero,ctCompl,ctCgc_Cpf } },;
		{ "20", { ctRedeDest, ctADM, ctValor,  ctCartao, ctSenha, ctTaxa, ctCVV2, ctCashBack } },;
		{ "21", { ctRedeDest, ctADM, ctValor,  ctCartao, ctSenha } },;
		{ "22", { ctRedeDest, ctADM, ctValor,  ctData    , ctCartao, ctSenha, ctMod_Entr,ctTaxa, ctCVV2, ctGarantia } },;
		{ "23", { ctRedeDest, ctADM, ctCartao, ctValor , ctDocument_Data, ctCVV2  } },;
		{ "24", { ctRedeDest, ctADM, ctValor,  ctCartao, ctSenha, ctDad_Parc } },;
		{ "25", { ctValor,  ctTrilha_2, ctSenha, ctDad_Parc } },;
		{ "26", { ctRedeDest, ctADM, ctValor,  ctCartao, ctSenha, ctDad_Parc } },;
		{ "27", { ctRedeDest, ctADM, ctValor,  ctCartao , ctSenha, ctEntrada , ctDad_Parc, ctDat_Venc, ctTaxa, ctCVV2 } },;
		{ "28", { ctRedeDest, ctADM, ctValor,  ctCartao , ctSenha, ctEntrada , ctDad_Parc, ctDat_Venc, ctTaxa, ctCVV2 } },;
		{ "30", { ctValor,  ctCartao, ctPlano , ctTaxa, ctSenha } },;
		{ "31", { ctValor,  ctTrilha_2, ctPlano, ctTaxa } },;
		{ "32", { ctCartao, ctValor   , ctDat_Venc , ctPlano, ctTaxa } },;
		{ "33", { ctCartao, ctValor   , ctDat_Venc , ctPlano, ctTaxa } },;
		{ "34", { ctValor,  ctTrilha_2, ctCod_Autor, ctPlano, ctTaxa } },;
		{ "35", { ctCartao, ctValor   , ctDat_Venc , ctCod_Autor, ctPlano, ctTaxa } },;
		{ "36", { ctRedeDest, ctADM, ctCartao, ctValor, ctDocument_Data, ctCVV2 } },;
		{ "37", { ctRedeDest, ctADM, ctCartao, ctValor, ctDocument_Data, ctCVV2 } },;
		{ "38", { ctRedeDest, ctADM, ctTipOp,  ctTipPar , ctCartao, ctValor, ctPlano, ctTaxa, ctDat_Venc, ctJur  , ctParc_Val, ctSenha, ctCVV2 } },;
		{ "40", { ctRedeDest, ctADM, ctCodNet, ctParc_Val, ctEntDoc, ctDados } },;  //CCS
		{ "41", { ctRedeDest, ctADM, ctCodNet, ctParc_Val, ctEntDoc, ctDados, ctNsu, ctDat_Pag } },;
		{ "50", { ctRedeDest, ctADM, '1', ctCodNet, ctFuncao, ctTipOp, ctEntrada, cSaida, ctDados } },; // { ctRedeDest, ctADM, '1', ctCodNet, ctFuncao, ctDados} },;
		{ "60", If(Left(SuperGetMV("MV_TEFEMS"),1)="5",{ ctRedeDest, ctADM,ctCodNet, ctEntDoc, cSaida, ctDados },{ ctCodNet, ctEntDoc, ctDados }) },;
		{ "3F", { ctTipOp,  ctValor, ctCartao, ctPlano, ctDat_Venc, ctSenha, ctTaxa } },;
		{ "70", If(ctFuncao="7",{ ctRedeDest, ctADM, "1" , ctFuncao , "2" , ctDad_Chq , ctNum_H , ctDt_H } , { ctRedeDest, ctADM,"1",ctFuncao, ctValor, ctTipo_Pes, ctCgc_Cpf, ctQCheque, ctDt_ChQ_Ser,ctValor,"2", ctDad_Chq }) },;
		{ "72", { ctRedeDest, ctADM,ctValor,  ctTipo_Pes, ctCgc_Cpf, ctBanco, ctAgencia,ctNum_Cheq, ctDt_ChQ_Ser, ctQCheque, "0" } } ,;
		{ "73", { ctValor,  ctBco_Agenc, ctNum_Cheq, ctTipo_Pes, ctCgc_Cpf } },;
		{ "74", { ctRedeDest, ctADM,ctValor,  ctBco_Agenc, ctNum_Cheq, ctTipo_Pes, ctCgc_Cpf, ctDt_Cheq_TelD } } ,;
		{ "78", { ctRedeDest, ctADM,"13", ctTipCons, ctTipo_Pes, ctCgc_Cpf, ctValor, ctRg, ctBanco, ctAgencia, ctNum_Cheq, ctDt_Chq_Ser, ctQCheque, "1"} } ,;
		{ "80", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "81", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "82", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "83", { ctDt_H,   ctNum_H, ctRede_H } }}
		If cTrans=='F0'
		   aadd(aRelation,{cTrans,aTrans})
		Endif   
		nReg := AScan( aRelation, { |X| X[ 1 ] == AllTrim(cTrans) } )
		cString := If( Empty( nReg ), "*", aRelation[ nReg, 2 ] )
	Else
		aRelation := {{"10", { ctValor,  ctCartao, ctSenha, ctTaxa } },;
		{ "11", { ctCartao, ctValor , ctDat_Venc, ctTaxa } },;
		{ "15", { ctTipVen, ctCartao  , ctDat_Venc } },;
		{ "20", { ctValor,  ctCartao, ctSenha, ctTaxa } },;
		{ "21", { ctValor,  ctCartao, ctSenha } },;
		{ "22", { ctValor,  ctData    , ctCartao, ctSenha, ctMod_Entr,ctTaxa } },;
		{ "23", { ctCartao, ctValor , ctDocument_Data  } },;
		{ "24", { ctValor,  ctCartao, ctSenha, ctDad_Parc } },;
		{ "25", { ctValor,  ctTrilha_2, ctSenha, ctDad_Parc } },;
		{ "26", { ctValor,  ctCartao, ctSenha, ctDad_Parc } },;
		{ "27", { ctValor,  ctCartao , ctSenha, ctEntrada , ctDad_Parc, ctDat_Venc, ctTaxa } },;
		{ "28", { ctValor,  ctCartao , ctSenha, ctEntrada , ctDad_Parc, ctDat_Venc, ctTaxa } },;
		{ "30", { ctValor,  ctCartao, ctPlano , ctTaxa, ctSenha } },;
		{ "31", { ctValor,  ctTrilha_2, ctPlano, ctTaxa } },;
		{ "32", { ctCartao, ctValor   , ctDat_Venc , ctPlano, ctTaxa } },;
		{ "33", { ctCartao, ctValor   , ctDat_Venc , ctPlano, ctTaxa } },;
		{ "34", { ctValor,  ctTrilha_2, ctCod_Autor, ctPlano, ctTaxa } },;
		{ "35", { ctCartao, ctValor   , ctDat_Venc , ctCod_Autor, ctPlano, ctTaxa } },;
		{ "36", { ctCartao, ctValor, ctDocument_Data } },;
		{ "37", { ctCartao, ctValor, ctDocument_Data } },;
		{ "38", { ctTipOp,  ctTipPar , ctCartao, ctValor, ctPlano, ctTaxa, ctDat_Venc, ctJur  , ctParc_Val, ctSenha } },;
		{ "40", { ctCodNet, ctParc_Val, ctEntDoc, ctDados } },;  //CCS
		{ "41", { ctCodNet, ctParc_Val, ctEntDoc, ctDados, ctNsu, ctDat_Pag } },;
		{ "60", If(Left(SuperGetMV("MV_TEFEMS"),1)="5",{ ctCodNet, ctEntDoc, "I", ctDados },{ ctCodNet, ctEntDoc, ctDados }) },;
		{ "3F", { ctTipOp,  ctValor, ctCartao, ctPlano, ctDat_Venc, ctSenha, ctTaxa } },;
		{ "70", { "0", "1", ctValor, ctTipo_Pes, ctCgc_Cpf, ctQCheque, ctDt_ChQ_Ser,ctValor,"2", ctDad_Chq } },;
		{ "72", { ctValor,  ctTipo_Pes, ctCgc_Cpf, ctBanco, ctAgencia,ctNum_Cheq, ctDt_ChQ_Ser, ctQCheque, "0" } } ,;
		{ "73", { ctValor,  ctBco_Agenc, ctNum_Cheq, ctTipo_Pes, ctCgc_Cpf } },;
		{ "74", { ctValor,  ctBco_Agenc, ctNum_Cheq, ctTipo_Pes, ctCgc_Cpf, ctDt_Cheq_TelD } } ,;
		{ "78", { "13", ctTipCons, ctTipo_Pes, ctCgc_Cpf, ctValor, ctRg, ctBanco, ctAgencia, ctNum_Cheq, ctDt_Chq_Ser, ctQCheque, "1"} } ,;
		{ "80", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "81", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "82", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "83", { ctDt_H,   ctNum_H, ctRede_H } } ,;
		{ "F0", { "6",ctFuncao,ctTipOp, ctCartao, ctValor,If (ctFuncao $ "3031", ctOrcam, ctPlano),	If (ctFuncao $ "3031", ctDat_Vend, ctOrcam), ctData, ctHoraVend + If (ctFuncao $ "3031", "",  Chr(0) + ctDat_Venc) } } }
		nReg := AScan( aRelation, { |X| X[ 1 ] == AllTrim(cTrans) } )
		cString := If( Empty( nReg ), "*", aRelation[ nReg, 2 ] )
	Endif
Else
	cString := "*"
Endif

Return( cString )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010LePinPad ³ Autor ³ Sergio Silveira    ³ Data ³ 16/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Realiza a leitura da senha do PIN PAD					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010LePinPad
Local nRet		:= 0
Local lRet		:= .T.

DEFAULT cMsg 	:= ""

ctSenha := Space(150)

If !Empty(LjGetStation("PINPAD"))
	MsgRun("Digite a senha do cartão magnético","Senha",{|| nRet:=PinPadLeS(nHdlPinPad,IF(At(Chr(4),ctCartao)=0,ctCartao,SubStr(ctCartao,1,At(Chr(4),ctCartao) - 1)),"DIGITE A SENHA",cWork,@ctSenha) } )
Else
	// "O PinPad selecionado não é compatível com o sistema !" ### "Equipamento Incompativel"
	MsgInfo( STR0070, STR0071)
Endif
If nRet=1
	If AllTrim(cTSenha)="#09"
		// "Finalizado pelo Operador"
		MsgInfo( STR0072 )
	Endif
	lRet:=.F.
Endif

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010LeConfig ³ Autor ³ Sergio Silveira    ³ Data ³ 16/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Interpreta a configura‡ao enviada pelo SiTef e carrega	  ³±±
±±³			 ³ variaveis Private ou Static de paramentros				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ L010LeConfig( ExpC1 )									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1   -> String de configura‡ao						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010LeConfig( cBuffer )

Local cRes1
Local cTrxHabil
Local cRes2
Local cRes3
Local cLimDebito
Local cDataAgen
Local cParcCart
Local cTaxaServ
Local cRes4
Local cRes5
Local cTimeOut
Local cNuParDeb
Local cIntrParc
Local cPrParVist
Local cMesFech
Local cTrx
Local cBits
Local cNotaProm
Local nLoop

cRes1 	  	:= SubStr( cBuffer,  1,	8 )
cTrxHabil  	:= SubStr( cBuffer,  9,	4 )
cRes2 	  	:= SubStr( cBuffer, 13, 16 )
cRes3 	  	:= SubStr( cBuffer, 29, 16 )
cLimDebito 	:= SubStr( cBuffer, 45, 16 )
cDataAgen  	:= SubStr( cBuffer, 61,	4 )
cParcCart  	:= SubStr( cBuffer, 65,	2 )
cTaxaServ  	:= SubStr( cBuffer, 67,	2 )
cRes4 	  	:= SubStr( cBuffer, 69,	2 )
cRes5 	  	:= SubStr( cBuffer, 71,	2 )
cTimeOut   	:= SubStr( cBuffer, 73,	2 )
cNuParDeb  	:= SubStr( cBuffer, 75,	2 )
cIntrParc  	:= SubStr( cBuffer, 77,	2 )
cPrParVist 	:= SubStr( cBuffer, 79,	1 )
cMesFech   	:= SubStr( cBuffer, 80,	1 )
cNotaProm  	:= SubStr( cBuffer, 81,	1 )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Le os 4 bytes de transacoes habilitadas, converte para binario  ³
//³ e carrega as variaveis Static de configuracao						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nLoop := 1 to 4
	cTrx	:= SubStr( cTrxHabil, nLoop, 1 )
	cBits := L010AscBase( cTrx, 2 )
	
	Do Case
		Case (nLoop == 1)
			ltTCredit := If( Right( cBits, 1 ) == "1", .T., .F. )
			
		Case (nLoop == 2)
			ltCpParCD := If( SubStr( cBits, 3, 1 ) == "1", .T., .F. )
			ltCpPreCD := If( SubStr( cBits, 6, 1 ) == "1", .T., .F. )
			ltCpForCD := If( SubStr( cBits, 7, 1 ) == "1", .T., .F. )
			ltCpCD	 := If( SubStr( cBits, 8, 1 ) == "1", .T., .F. )
			
		Case (nLoop == 4)
			ltCancCD  := If( SubStr( cBits, 4, 1 ) == "1", .T., .F. )
			ltCancCC  := If( SubStr( cBits, 8, 1 ) == "1", .T., .F. )
	EndCase
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega variaveis private de configuracao						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

ntLimDebito := Val( cLimDebito ) / 100
ntParcCart	:= L010WChr2Dec( cParcCart )
ntTaxaServ	:= L010WChr2Dec( cTaxaServ )
ntTimeOut	:= L010WChr2Dec( cTimeOut	) * 1.3
ntNuParDeb	:= L010WChr2Dec( cNuParDeb )
ntIntrParc	:= L010WChr2Dec( cIntrParc )
ltPrParVist := ( cPrParVist	== Chr(1) )
ltMesFech	:= ( cMesFech	== Chr(1) )
ltNotaProm	:= ( cNotaProm 	== Chr(1) )
dtDataAgen	:= CToD( 	Right( cDataAgen, 2 ) + "/" + Left( cDataAgen, 2 ) + "/" ;
						+ Right( Str( Year( dDataBase ), 4 ), 2 ) , "ddmmyy" )

If (dtDataAgen < dDatabase)
	dtDataAgen := CToD( Right( cDataAgen, 2 ) + "/" + Left( cDataAgen, 2 )+ "/";
						+ Right( Str( Year( dDataBase ) + 1, 4 ), 2 ) , "ddmmyy" )
Endif

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010LeArmaze ³ Autor ³ Sergio Silveira	³ Data ³ 23/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Interpreta o campo armazenar ( H ) enviado pelo SiTef 	  ³±±
±±³			 ³ e grava no SL1 se venda ou cancelamento efetuados		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := L010LeArmaze( ExpC1 )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1   -> String do campo 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010LeArmaze( cBuffer, cTipSol, aTick, cDisp )

Local cCodResp
Local cInstitu
Local cAliasOld 	:= Alias()
Local cCodAdm 	 	:= Space(3)
Local cRede     	:= Space(2)

cCodResp   	:= SubStr( cBuffer,  1,	2  )
cDataTef   	:= SubStr( cBuffer,  3,	2  ) + SubStr( cBuffer, 5, 2 )
cHoraTef   	:= SubStr( cBuffer,  7,	6  )
cDocTef	  	:= SubStr( cBuffer, 13,	9  )
cCodEst	  	:= SubStr( cBuffer, 22, 15 )
cAutoriz   	:= SubStr( cBuffer, 37,	6  )
cDocCanc   	:= SubStr( cBuffer, 43,	9  )
cDatCanc   	:= SubStr( cBuffer, 52,	4  )
cHorCanc   	:= SubStr( cBuffer, 56,	6  )
cInstitu   	:= SubStr( cBuffer, 62, 16 )
cInst 	  	:= cInstitu
cNsuTef	  	:= SubStr( cBuffer, 78,	6  )
cRede	    := SubStr( cBuffer, 84,  2 )

If lVer300
	ctDt_h   := cDataTef
	ctRede_h := cRede
	ctNum_h  := cNsuTef
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Complemento das informa‡äes da forma de pagamento para ³
//³o L4_OBS                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SAE")
dbSetOrder(1)
DbSeek(xFilial("SAE"))
While !Eof()
	If (Upper(AllTrim(AE_DESC)) == Upper(AllTrim(cInst)))
		cCodAdm := AE_COD
		Exit
	Endif
	dbSkip()
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³NÆo armazena o numero do cartao       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cDados := cCodAdm + "-" + cInstitu + SPACE(19) + "01/" + LEFT(ctDat_Venc,2) + "/" + RIGHT(ctDat_Venc,2)

dbSelectArea(cAliasOld)

/* Se resposta == 00 ( sucesso ), e operacao venda ou cancelamento, 
 armazena dados no SL1						 					 */
If cOpera $ "V/E/X/P"
	aTefDados[1][1] 	:= "S"
	aTefDados[1][2] 	:= cDataTef
	aTefDados[1][3] 	:= cHoraTef
	aTefDados[1][4] 	:= cDocTef
	aTefDados[1][5] 	:= cAutoriz
	aTefDados[1][6] 	:= cDocCanc
	aTefDados[1][7] 	:= cHorCanc
	aTefDados[1][8] 	:= cInstitu
	aTefDados[1][9] 	:= cNsuTef
	aTefDados[1][10] 	:= cModCart
	aTefDados[1][11] 	:= cRede
	aTefDados[1][12] 	:= cDatCanc
	If cModCart == "M"
		aTefDados[1][13] := Left(ctTrilha_2,At("=",ctTrilha_2)-1)
	Else
		aTefDados[1][13] := ctCartao
	Endif
	If Len(aTefDados[1]) > 15
		If ctTipPar == "3"
			aTefDados[1][16]     := "1"  //Juros da Administradora
		Else 
			aTefDados[1][16]     := "0"	//Juros do Estabelecimento
		Endif     
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Nesse momento e calculada a Quantidade de Parcelas.		  ³
		//³Eh feita a validacao das variaveis nParCred e nParDeb, pois³
		//³caso a transação TEF tenha sido feita antes da melhoria da ³
		//³quantidade de parcelas, ocorreria Error.log no cancelamento³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nParCred <> Nil .AND. nParDeb <> Nil
			aTefDados[1][17]        := StrZero(nParCred+nParDeb,2)  //Quantidade de parcelas
		Else
			aTefDados[1][17]        := "00"
		Endif
	Endif   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apagar o arquivo de reimpressão caso ocorra alguma falha de transação e ³
	//³caso nao seja Visa                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cCodResp <> "00" .AND. cRede<>'04'	
		FErase("RX_PRN." + cPTerm)
    Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³HOMOLOGACAO: Nao precisamos mais imprimir, pois a própria Trn ³
//³ja monta sozinha                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ElseIf AllTrim( cTipSol ) $ "72/74/78"

	If cCodResp == "00" .OR. ( AllTrim(cTipSol) $ "72;78" .AND. cCodResp $ "88.89" )
		
		Do Case
			Case AllTrim(cTipSol) == "72"
				aAdd( aTick, "CUPOM - SERASA ou ACSP" )
			Case AllTrim(cTipSol) == "74"
				aAdd( aTick, "CUPOM - CONSULTA TELECHEQUE" )
			Case AllTrim(cTipSol) == "70"
				aAdd( aTick, "CUPOM - CHEQUE TECBAN" )
			Case AllTrim(cTipSol) == "78"
				aAdd( aTick, "CUPOM - TELECHEQUE + SCPC" )
		EndCase
		
		aAdd( aTick, "-----------------------" )
		aAdd( aTick, "STATUS : " + cDisp )
		aAdd( aTick, "" )
		aAdd( aTick, "DATA...............: " + Right( cDataTef, 2 ) + "/" + ;
		Left( cDataTef, 2 ) )
		aAdd( aTick, "HORA...............: " + Left( cHoraTef, 2 ) + ":"  + ;
		SubStr( cHoraTef, 3, 2 ) + ":" + Right( cHoraTef, 2 ) )
		aAdd( aTick, "DOCUMENTO..........: " + cDocTef  )
		If ! Val(cAutoriz) == 0
			aAdd( aTick, "AUTORIZACAO........: " + cAutoriz )
		Endif
		
	Endif
Endif

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010AscBase  ³ Autor ³ Luiz Vergari		³ Data ³ 16/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte caractere ASCII para qualquer base				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC2 := L010AscBase( ExpC1, ExpN1 )					      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1   -> Caractere  /  ExpN1 -> Base 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC2   -> Retorno										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010AscBase( cLetra, nDiv )

Local xResult
Local nResto
Local cRet
Local nDividen
Local bHexa := { |x| Asc( Val( x ) + 55 ) }

If (cLetra == Nil) .OR. (nDiv = Nil)
	Return( "-1" )
Endif

nDividen := Asc( cLetra )

If (nDividen == 0)
	If (nDiv == 2)
		Return( "00000000" )
	Endif
	Return("0")
Endif

If (nDividen < 1) .OR. (nDividen > 255)
	Return( "-1" )
Endif

If (nDiv < 2) .OR. (nDiv > 32)
	Return( "-1" )
Endif

xResult := nDiv
cRet	  := Space(1)
nResto  := Space(1)

While (xResult >= nDiv)
	xResult	 := nDividen / nDiv
	nResto	 := Str( Int( mod( nDividen, nDiv ) ) )
	If (nDiv > 9)
		nResto := Eval( bHexa, nResto )
	Endif
	
	cRet		 := AllTrim( nResto ) + AllTrim( cRet )
	nDividen  := Int( xResult )
End

xResult := Str( Int( xResult ) )

If (nDiv > 9)
	xResult := Eval( bHexa, xResult )
Endif

cRet := AllTrim( xResult ) + AllTrim( cRet )

If (nDiv == 2)
	cRet := Replicate( "0", 8 - Len( cRet ) ) + cRet
Endif

Return( cRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010ValidDt()³ Autor ³ Sergio Silveira    ³ Data ³ 18/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a data de validade do cartao de credito			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010ValidDt( ExpC1 ) 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 -> Data no formato MMAA 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Data valida ou nao								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010ValidDt( cDataVal )

Local lRetorno := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida apenas o mes, pois nao ‚ permitido validar o ano  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If (Val( Left( cDataVal, 2 ) ) > 0) .AND. (Val( Left( cDataVal, 2 ) ) < 13)
	lRetorno := .T.
Else
	Aviso( "Atenção", "Data inválida", {"&Ok"})
Endif

Return( lRetorno )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010ErroRede³ Autor ³ Sergio Silveira    ³ Data ³ 20/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Avisa operador que ocorreu erro na rede					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN2 := L010ErroRede( ExpN1 )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 -> Tipo de erro									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpN2 -> 1 - Abandona, 2 - Tenta novamente				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010ErroRede( nErrType )

Local nRet := 1
Local nMod := 2
Local aMess
Local oDlg

//"Foi ultrapassado o tempo máximo de espera para uma resposta !",;

aMess := {	"Não foi possível criar o arquivo de transmissão !"				, ;
		  	"Não foi possível renomear o arquivo de transmissão!"			, ;
		  	"Não foi possível criar o arquivo de confirmação!"				, ;
		  	"Não foi possível renomear o arquivo de confirmação!"			, ;
		  	"Erro de Comunicação Servidor Sitef"							, ;
		  	"A versão do Sitef não é compat¡vel com a versão do Sigaloja !"	, ;
		  	"Ocorreu algum problema com o controle de transação !"			, ;
		  	"O aceite não foi recebido corretamente !"						, ;
		  	"Não foi possível abrir o arquivo de resposta!"	}

Do Case

	Case (nErrType < 5)
		cMess2 := "Ocorreu algum problema com a rede Local. - Dos Error :" + Str( FError(), 2 )
		
	Case (nErrType == 6)
		nMod := 3
		cMess2 := "Utilize a versão " + cSiTVers + " do Sitef."
		
	Case (nErrType == 7)
		nMod := 3
		cMess2 := "A venda está cancelada."
		
	OtherWise
		cMess2 := "Ocorreu algum problema com a rede Local. nErrType="+AllTrim(Str(nErrType))
EndCase

DEFINE MSDIALOG oDlg TITLE "Erro - Rede"  From 13,21 to 20,64  of GetWndDefault()

@ 10, 14 SAY aMess[ nErrType ]	SIZE 160, 10 OF oDlg PIXEL
@ 20, 14 SAY cMess2				SIZE 150, 10 OF oDlg PIXEL

@ 35, 35  BUTTON "Abandona"         SIZE 50, 15 OF oDlg PIXEL ACTION ( nMod:=1,oDlg:End() )

If (nMod <> 3.AND. nErrType <>5)
	@ 35, 90  BUTTON "Tenta Novamente"  SIZE 50, 15 OF oDlg PIXEL ACTION ( nMod:=2,oDlg:End() )
Endif

ACTIVATE MSDIALOG oDlg CENTERED


nRet := If( nMod == 1, nMod, 2 )
If nErrType =5 .AND. nMod=2 .AND. lVer300
	ntTimeOut	:= 45
Endif

Return( nRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010LeCartao³ Autor ³ Sergio Silveira    ³ Data ³ 16/06/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Realiza a leitura do cartao magnetico (Equip. via teclado) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno	 ³ Nenhum ( alimenta a variavel private da trilha_2 ) 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 										   	      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010LeCartao(cModalidade,nX,cOpera)
Local lRet 		 	:= .F.
Local cTrilha_1  	:= ""
Local nRet       	:= 0
Local cMsg       	:= " Passe o cartão magnético pelo leitor"
Local cMsgTit    	:= "Cartão "
Local lSintVisu		:= If(Type("lVisuSint")<>"U",lVisuSint,.F.)	//Tratamento para a nova identificação do cartão no Multi-TEF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

DEFAULT nX 			:= 0
DEFAULT cModalidade	:= "C"
DEFAULT cOpera 		:= ""

If lTefMult .AND. nX>0
	If cOpera=="E"
		cMsgTit += AllTrim(Subst(aTefMult[nX][2],7))+" Doc.: "+AllTrim(aTefMult[nX][7][3])
	Else
	    If lSintVisu
	   	   //Nova mensagem com ID do Cartão		
	 		cMsgTit += AllTrim(Subst(aTefMult[nX][2],1,4))
		Else
	   	   //Mensagem com os 4 Ultimos Dígitos do Cartão
	 		cMsgTit += AllTrim(Subst(aTefMult[nX][2],8))+If(Val(aTefMult[nX][2])>0," Final "+Subst(aTefMult[nX][2],1,4),"")
		Endif	 		
	Endif
Endif

ctTrilha_2 	:= space(150) // Nao remover
nCol 		:= -12

If !Empty(LJGetStation("CARTMAG"))
	If At("CHIP",Upper(LJGetStation("CARTMAG")))>0
		cMsg := "Insira ou passe o cartão magnético pelo leitor"
	Endif
	lRet := .T.
	While .T.
		ctTrilha_2 := Space( 150 )
		If lUsaDisplay
			DisplayEnv(StatDisplay(), "1C" + "Insira ou passe" )
			DisplayEnv(StatDisplay(), "2C" + " o cartao magnetico pelo leitor")
		Endif
		MsgRun(cMsg,cMsgTit,{|| nRet:=PinPadLeC(nHdlPinPad,cModalidade,@ctTrilha_2) } )
		If nRet=1
			If AllTrim(ctTrilha_2)="#09"
				MsgInfo("Finalizado pelo Operador")
			Endif
			lRet       := .F.
			ctTrilha_2 := ''
		Endif
		Exit
	End
Else
	MsgInfo("O leitor de cartao selecionado nao e' compativel com o sistema !","Equip. incompativel")
Endif
If (Len(cTrilha_1) > 0)
	ctTrilha_2 := ctTrilha_2+chr(4)+cTrilha_1
Endif
ctCartao := ctTrilha_2
Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010Servicos ³ Autor ³ Luiz Vergari 	    ³ Data ³ 02/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Servi‡os diversos ( consultas, garant. cheque, etc... )	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010Servicos( ExpC1, ExpA1, ExpN1 ) 		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ cOpera           					                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA 	 ³ BOPS ³Prograd.³ALTERACAO								      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 29.10.01 ³ 10982³MMancio ³Caso a forma de pagamento, no "SL1" seja di-³±±
±±³          ³      ³        ³ferente de "CC", "CD" ou "CH".  Será efetua-³±±
±±³          ³      ³        ³do uma varredura no "SL4"   para identificar³±±
±±³          ³      ³        ³qual é a primeira parcela "válida".         ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010Servicos( cOpera, lImprime , lConsulta , nX )

Local nValor		:= Val(ctValor)/100 , oDlg , nAlt
Local nValorBak		:= 0
Local nTipCon     	:= 0
Local cTipoPessoa 	:= If(Upper(SA1->A1_TIPO)=="F",SA1->A1_TIPO,"J")
Local cCGC_CPF 		:= SA1->A1_CGC
Local nIndice, nI 	:= 0
Local cBco          := Space(4)	 
Local cAgencia      := Space(4)
Local cNumCheque    := Space(7)	 
Local cConta		:= Space(10)
Local cContr		:= Space(9)
Local dData			:= dDataBase
Local aTip			:= {}
Local cSep 			:= Chr( 4 )			//Caracter EOT para o Sitef
Local lLeu			:= .T.
Local lRet			:= .F.
Local lCancela      := .F.
Local lConsCDC 		:= .F.
Local nOpc1 		:= 0
Local cTrans        := ""
Local aRetorno		:= {}
Local lConfOk		:= .T.
Local _oDlg			:= NIL
Local cCaption    	:= ""
Local cData       	:= ""
Local aCmc7       	:= {}
Local cFormPg     	:= ""
Local aLeTef		:= {}
Local cNum		  	:= CRIAVAR("L1_NUM",.F.)
Local cBotoesCons
Local L1			:= 02
Local C1			:= 28
Local L2			:= 05
Local C2			:= 49
Local L3			:= 05
Local nTipConAnt	:= 0
Local nParcela  
Local dData1        := dDataBase
Local nPos			:= 0
Local lSaiCanc		:= .T.					// HOMOLOGACAO: Variavel utilizada no controle de saida da tela de cancelamento
Local lEstTecBan	:= .F.					// HOMOLOGACAO: Indica se devemos efetuar o cancelamento da Garantia de Cheques
Local cTitulo	    := ""
Local nLin		    := 0	
Local nTamEF_BAN := TamSx3("EF_BANCO")[1]   // Tamanho do campo EF_BANCO
Local nTamEF_AGE := TamSx3("EF_AGENCIA")[1] // Tamanho do campo EF_AGENCIA
Local nTamEF_CON := TamSx3("EF_CONTA")[1]   // Tamanho do campo EF_CONTA
Local nTAMEF_NUM := TamSx3("EF_NUM")[1]     // Tamanho do campo EF_NUM
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

DEFAULT nX 			:= 0
DEFAULT lImprime 	:= .T.

If Type("aParcTef") == "U"
   aParcTef := {}
Endif   

nParcela   	:= Len(aParcTef)
dData1 		:= Iif(nParcela>0,aParcTef[1][1],dDataBase)

If lTefMult .AND. nX <> 0
	nParcela   	:= aTefMult[nX][5]
	dData1		:= aTefMult[nX][6][1]
	If nParcela > 0 .AND. aTefMult[nx][1] == "CH"
		If Upper(FunName()) == "FRTA010"
			cBco       	:= Alltrim(aParcTef[nX][5])
			cAgencia   	:= Alltrim(aParcTef[nX][6])
			cConta		:= Alltrim(aParcTef[nX][7])
			cNumCheque 	:= Alltrim(aParcTef[nX][4])
			dData1		:= aParcTef[nX][1]
			nValor		:= aParcTef[nX][2]
		ElseIf Upper(FunName()) == "LOJA701"
			cBco       	:= Space( nTamEF_BAN )
			cAgencia   	:= Space( nTamEF_AGE )
			cConta		:= Space( nTamEF_CON )
			cNumCheque 	:= Space( nTAMEF_NUM )
			dData1		:= aTefMult[nX][6][1]
			nValor		:= aTefMult[nX][4]
		Endif
	Endif
Else
	// Só pego os dados do aReceb se não for multi-transação.
	If nX > 0 .AND. Type("aReceb") == "A" .AND. Len(aReceb)>0
		dData1     := aReceb[nX][01]
		nValor     := aReceb[nX][02]
		cBco       := aReceb[nX][04]
		cNumCheque := aReceb[nX][05]
		cAgencia   := aReceb[nX][06]
		cConta     := aReceb[nX][07]
	Endif		
Endif

ctRg := Space(15)

If ExistBlock("LJLETEF")
	aLeTef := ExecBlock("LJLETEF",.F.,.F.)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Caso a execução seja feita por qualquer outro modulo diferente de CALL CENTER  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !(nModulo == 13) .AND. !(nModulo == 73)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Le as informacoes do SIGALOJA - SL1³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aLeTef := {	SL1->L1_FORMPG	, ;
					SL1->L1_INSTITU	, ;
					SL1->L1_DOCTEF	, ;
					SL1->L1_EMISNF	, ;
					SL1->L1_TIPCART	, ;
					SL1->L1_CARTAO	, ;
					SL1->L1_VLRDEBI	, ;
					Alltrim(SL1->L1_DATATEF)}
		cNum := SL1->L1_NUM
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Le as informacoes do CALL CENTER - SUA  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aLeTef := {	SUA->UA_FORMPG	, ;
					SUA->UA_INSTITU	, ;
					SUA->UA_DOCTEF	, ;
					SUA->UA_EMISNF	, ;
					SUA->UA_TIPCART	, ;
					SUA->UA_CARTAO	, ;
					""				, ;
					Alltrim(SUA->UA_DATATEF)}
		cNum := SUA->UA_NUM
	Endif
Endif

ctTrilha_2 := ""

If cOpera == "X" .OR. cOpera == "E"
	cAgencia := Space( 4 )
Endif

cFormPg := aLeTef[__FORMPG]

If !(cFormPg $ 'CH/CD/CC')
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Caso a primeira parcela seja diferente de uma transação TEF               ³
	//³ Posiciona no primeiro registro do orcamento e localiza                    ³
	//³ a primeira transação TEF. Para validar o processo de cancelamento         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SL4")
	DbSetorder(1)
	DbSeek( xFilial("SL4") + cNum )
	If Found()
		While SL4->L4_FILIAL == xFilial("SL4") .AND. SL4->L4_NUM == cNum .AND. !Eof()
			If Upper(ALLTRIM(SL4->L4_FORMA)) $ 'CH/CD/CC'
				cFormPg := ALLTRIM(SL4->L4_FORMA)
				Exit
			Endif
			DbSkip()
		End
	Endif
Endif

While .T.
	
	Do Case
		Case cOpera == "C"
			cCaption:= "Consultas Tef"
			
		Case cOpera == "G"
			cCaption:= "Garantia"
			
		Case cOpera == "E" .OR. cOpera == "X"
			cCaption:= "Cancelamento Venda"
			
	EndCase

	If cOpera == "C"

		cBotoesCons := LjGetStation("TEFCONS")
		nTipCon := 0
		For nI  := 1 to Len(AllTrim(cBotoesCons))
			L2+=If(Subs(cBotoesCons,nI,1)=='S',3.3,0)
		Next nI

		If Substr(cBotoesCons,3,1)="S" .AND. (Type("lConsulta") <> "U" .OR. !lConsulta)
			L2 += 3.3
		Endif
		If Substr(cBotoesCons,5,1)="S" .AND. nParcela<2
			L2 -= 3.3
		Endif
		
		If Type("lConsulta") <> "U" .OR. !lConsulta
			L2 -= 1.1
		Else
			L2 -= 0.5
		Endif

		DEFINE MSDIALOG oDlg TITLE "Consultas"  From L1,C1 To L2,C2  of GetWNDDefault()
		If Substr(cBotoesCons,3,1)="S"
			@ L3,03 BUTTON "Cheque Papel - TECBAN"     		SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=3 ,oDlg:End() )
			L3+=20
			If Type("lConsulta") <> "U" .OR. !lConsulta 
				@ L3,03 BUTTON "Cheque Garantido - TECBAN" 	SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=9 ,oDlg:End() )
				L3+=20
			Endif				
			@ L3,03  BUTTON "TeleCheque - TECBAN" 			SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=10,oDlg:End() )
			L3+=20
		Endif
		If Substr(cBotoesCons,1,1)="S"
			@ L3,03  BUTTON "SERASA ou ACSP"          		SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=1,oDlg:End() )
			L3+=20
		Endif
		If Substr(cBotoesCons,4,1)="S"
			@ L3,03  BUTTON "TeleCheque+SCPC - ACSP "  		SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=4,oDlg:End() )
			L3+=20
		Endif
		If Substr(cBotoesCons,2,1)="S"
			@ L3,03  BUTTON "TeleCheque - TELEDATA"  		SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=2,oDlg:End() )
			L3+=20
		Endif
		If Substr(cBotoesCons,5,1)="S" .AND. nParcela>1
			@ L3,03  BUTTON "Parcelamento CDC"           	SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=5,oDlg:End() )
			L3+=20
		Endif
		If Substr(cBotoesCons,6,1)="S"
			@ L3,03  BUTTON "AVS - REDECARD"      			SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=6,oDlg:End() )
			L3+=20
		Endif
		@ L3,03  BUTTON "Sair"           					SIZE 80,17 OF oDlg PIXEL ACTION ( nTipCon:=0,oDlg:End() )
		ACTIVATE MSDIALOG oDlg CENTERED
	Else
		nTipCon := If(cOpera=="G",4,7)
	Endif
	If nTipCon == 0
		lRet := .F.
		Exit
	Else
		//HOMOLOGACAO: Consultas TecBan transação 70 deve ser diferenciada pelos tipos
		If nTipCon==9
		   nTipConAnt:=9	//Garantia Cheque
		   nTipCon	 :=3		
		ElseIf nTipCon==10
		   nTipConAnt:=10   //Telecheque Garantido
		   nTipCon	 :=3		
		Else
			nTipConAnt:=0   //Consulta de Cheque		
		Endif
		//Consultas CDC
		If nTipCon == 5
			lCDC     := .T.
			lConsCDC := .T.
			lRet	 := L010Parcel( lConsCDC, @lConsulta )  // Consulta CDC - Visualiza a venda parcelada no cartão de débito
			Exit
		Endif
		// Consulta AVS
		If nTipCon == 6 									
			lRet	 := L010ConsAVS()
			Exit
		Endif
	Endif
	nLin := 2

	If nTipCon < 5
		//Se usa o cmc7 faz a leitura do dados do cheque
		If lUsaCmc7
			While .T.
				aCmc7 := LjLeCmc7(1)
				If Len(aCmc7) > 3
					cBco	    := aCmc7[1]
					cNumCheque	:= aCmc7[2]
					cAgencia  	:= aCmc7[3]
					cConta	 	:= aCmc7[4]
					ctCompens 	:= aCmc7[5]
					Exit
				Else
					If !MsgRetryCancel("Erro ao tentar ler o cheque , Tentar novamente ou cancelar e informar manualmente?","Atenção")
						Exit
					Endif
				Endif
			End
		Endif

		If nTipCon == 3 .OR. nTipCon == 4 
			nAlt := 34
		Else
			nAlt := 33
		Endif
		lCancela := .T.
	
		DEFINE MSDIALOG _oDlg TITLE cCaption  From 13,11 to nAlt,50	of GetWndDefault()
		@ nLin+=12,10	SAY "Valor:"                               			 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET nValor 		       							 SIZE 45 ,10 OF _oDLG PIXEL Picture "@E 9,999,999.99"
		@ nLin+=12,10	SAY "Tipo de Pessoa:"                      			 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET cTipoPessoa Valid cTipoPessoa $ "FJ" 			 SIZE 10 ,10 OF _oDLG PIXEL Picture "@!"
		@ nLin+=12,10	SAY "CNPJ/CPF:"                            			 SIZE 65 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET cCGC_CPF SIZE 45,10 OF _oDLG PIXEL Picture If(cTipoPessoa=="J","@R! NN.NNN.NNN/NNNN-99","@R999.999.999-99") Valid (!Empty(cCGC_CPF) .AND. If(cTipoPessoa=='F',Len(Alltrim(cCGC_CPF))=11,Len(Alltrim(cCGC_CPF))=14))
		@ nLin+=12,10	SAY "Banco:"                                       	 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET cBco picture "9999" Valid !Empty(cBco)         SIZE 15 ,10 OF _oDLG PIXEL
		@ nLin+=12,10	SAY "Agencia:"                                     	 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET cAgencia picture "9999" Valid !Empty(cAgencia) SIZE 18 ,10 OF _oDLG PIXEL
		If nTipCon == 3
			@ nLin+=12,10	SAY "Conta Corrente:"                            SIZE 60 ,10 OF _oDLG PIXEL
			@ nLin,90		MSGET cConta picture "9999999999" Valid !Empty(cConta)  SIZE 33 ,10 OF _oDLG PIXEL
		Endif
		@ nLin+=12,10	SAY "N£mero do Cheque:"              	 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET cNumCheque picture "9999999" Valid !Empty(cNumCheque)	   	SIZE 42 ,10 OF _oDLG PIXEL
		@ nLin+=12,10	SAY "Data do Vencimento:"                       	 SIZE 60 ,10 OF _oDLG PIXEL
		@ nLin,90		MSGET dData Valid dData >= dDataBase	         	 SIZE 33 ,10 OF _oDLG PIXEL
		If nTipCon <> 2
			@ nLin+=12,10	SAY "Quantidade de Cheques:"                 	 SIZE 60 ,10 OF _oDLG PIXEL
			@ nLin,90		MSGET ctQCheque  Valid (Val(ctQCheque) > 0)  	 SIZE 28 ,10 OF _oDLG PIXEL When If(nTipCon <> 3,.T.,.F.)
		Endif
		If nTipCon==4
			ctTipCons := "A"
			@ nLin+=12,10 SAY "RG:" 										 SIZE 71,10 OF _oDLG PIXEL
			@ nLin,90    MSGet ctRg Pict "@!"								 SIZE 45,10 OF _oDLG PIXEL
			@ nLin+=12,10 SAY "[T]elecheque [S]cpc [A]mbos:" 				 SIZE 71,10 OF _oDLG PIXEL
			@ nLin,90     MSGET ctTipCons Pict "@!" Valid ctTipCons $ "TSA"  SIZE 28,10 OF _oDLG PIXEL
		Endif
		If nTipCon == 3 .OR. nTipCon == 4
			nAlt := 141 //136
		Else
			nAlt := 135 //130
		Endif
		@ nAlt, 35	BUTTON "Continua" SIZE 50, 15 OF _oDlg PIXEL ACTION (If(nValor > 0,(lCancela:=.F.,_oDlg:End()),) )
		@ nAlt, 85	BUTTON "Cancela"  SIZE 50, 15 OF _oDlg PIXEL ACTION (lCancela:=.T.,_oDlg:End())
		ACTIVATE MSDIALOG _oDlg CENTERED

        If lCancela
		   Exit
	    Endif

	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ROTINA DE CANCELAMENTOS TEF   ³
		//³CC - Cartao de Crédito        ³
		//³CD - Cartao de Debito         ³
		//³CH - Garantia de Cheque TECBAN³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//Mex
		If cPaisLoc == "BRA"
			If cFormPg == "CC" 									//Cancelamento CartÆo de Cr‚dito
				//Verifica se a administradora do Cartao e a Infocard
				If (Upper(AllTrim(aLeTef[__INSTITU])) $ __INFOCARD)
					lInfoCard   := .T.
					//Se for cancelamento as transações permanecem sendo as normais não existe diferença para as redes SOFTWAY e INFOCARD
					If cOpera == "X" .OR. cOpera == "E"
						cTrans:=""
	        		Else
						cTrans:="F0"
					Endif
					If lVer300
						ctFuncao:="38"
					Else
						ctFuncao:="30"
					Endif
				Else
					lInfoCard:= .F.
				Endif
				ctOrcam   	:= aLeTef[__DOCTEF]
				ctDat_Vend	:= StrZero(Day(aLeTef[__EMISNF]),2) + StrZero(Month(aLeTef[__EMISNF]),2) + Str(Year(aLeTef[__EMISNF]),4)
				ctData		:= StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+Str(Year(dDatabase),4)
				ctHoraVend	:= SubStr(Time(),1,2) + SubStr(Time(),4,2) + SubStr(Time(),7,2)
				
				nTipCar := 0
							
				If SuperGetMV("MV_INTEEMS")
					DEFINE MSDIALOG oDlg TITLE "Tipo de Cartão" FROM 13,21 TO 18,78 OF GetWNDDefault()
					@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
					@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
					@ 10,115 BUTTON "CPF"	    	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
					@ 10,170 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
				Else
					DEFINE MSDIALOG oDlg TITLE "Tipo de Cartão" FROM 13,21 TO 18,64 OF GetWNDDefault()
					@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
					@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
					@ 10,115 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
				Endif       
				ACTIVATE MSDIALOG oDlg CENTERED
				If nTipCar=4
					Exit
				Endif
	
				If nTipCar=1
					If lInfoCard
						//Se for cancelamento as transações permanecem sendo as normais não existe diferença para as redes SOFTWAY e INFOCARD
						If cOpera == "X" .OR. cOpera == "E"
							cTrans   := "36"					
						Else
							ctTipOp  := "1"
							ctPlano  := ""
						Endif						
					Else
						cTrans   := "36"
					Endif
					ctCartao := Space( 140 )
					lLeu	 := L010LeCartao("C")
	
				ElseIf nTipCar=2
					If lInfoCard
						//Se for cancelamento as transações permanecem sendo as normais não existe diferença para as redes SOFTWAY e INFOCARD
						If cOpera == "X" .OR. cOpera == "E"
							cTrans  := "37"
						Else
							ctTipOp  := "2"
							ctPlano  := ""
						Endif
					Else
						cTrans  := "37"
					Endif
					
					DEFINE MSDIALOG oDlg TITLE "Dados do Cartão" FROM 13,21 to 21,60  of GetWNDDefault()
					@	.074,.1 TO 4,19
					@	6,12 SAY "Informe o número do cartão: " SIZE 130, 10 OF oDlg PIXEL
					@	6,88 MSGET ctCartao SIZE  55, 10 OF oDLG PIXEL	Picture "@!" Valid L010VerNumCart( ctCartao )
					If !lInfoCard
						@ 18,12 SAY "Informe a validade: " SIZE 130, 10 OF oDlg PIXEL
						@ 18,88 MSGET ctDat_Venc SIZE 18,10 OF oDlg PIXEL Picture "@R 99/99" Valid L010ValidDt( ctDat_Venc )
					Endif
					DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION (lSaiCanc:=.F.,oDlg:End() ) ENABLE OF oDlg
					DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION (lSaiCanc:=.T.,oDlg:End() ) ENABLE OF oDlg				
					ACTIVATE MSDIALOG oDlg CENTERED
					ctCartao := AllTrim( ctCartao )
					
					//HOMOLOGACAO: Tratamento para a saida da tela
					If Empty(ctCartao) .OR. lSaiCanc
					   lRet  :=.F.
					   Exit
					Endif
	
	            ElseIf (nTipCar == 3)
					If lInfoCard
						//Se for cancelamento as transações permanecem sendo as normais não existe diferença para as redes SOFTWAY e INFOCARD
						If cOpera == "X" .OR. cOpera == "E"
							cTrans  := "37"
						Else
							ctTipOp  := "1"
							ctPlano := ""
						Endif						
					Else
						cTrans  := "37"
					Endif
	
	        		// Obtém o número do Cartão, a partir do CPF
	        		If SuperGetMV("MV_INTEEMS")
				        ctCartao   := LjConsCPF()
						If Alltrim(ctCartao)=="*"
	    		       		Return (.F.)
						ElseIf Empty( ctCartao )
			        		MsgInfo("CPF inválido")
					        Loop
	         			Endif
			            nOpd       := 3
			            ctDat_Venc := StrZero(Month(dDatabase),2) + SubStr( StrZero( YEAR(dDatabase) ,4) ,3,2)
			        	lRet       := .T.
				        lCPFInfo   := .T.
			        Endif
				Endif
				If !lInfoCard
					If lLeu
						lLeu := L010BinCar( "0" , .F., .F. )  			// Consulta ao Bin do Cartao
					Endif
				Endif
				nValor := aLeTEF[__CARTAO]
				If lVer300 .AND. !lLeu
					Exit
				Endif
			
			ElseIf (cFormPg == "CD") 									//Cancelamento CartÆo de D‚bito
					cTrans := "23"
					lLeu   := L010LeCartao("D")
					If !lInfoCard
						If lLeu
							lLeu := L010BinCar( "1" , .F., .F. )  		//Consulta ao Bin do Cartao
						Endif
					Endif
					nValor := aLeTef[__VLRDEBI]
			
			ElseIf (cFormPg == "CH") .AND. ;
				   Type("aTefChq") == "A" .AND. Len(aTefChq)>0  		//Cancelamento de Garantia de Cheque TecBan
				   lEstTecBan := .T.
			Else
				MsgInfo("Não é possível efetuar o cancelamento deste documento!")
				Exit	
			Endif
     		cContr := AllTrim( Str( Val( aLeTef[__DOCTEF] ) ) )
		ElseIf cPaisLoc=="MEX" //Mex
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Para o Mexico o tratamento de cancelamento e feito na funcao L010Mex      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cContr := AllTrim(aLeTef[__DOCTEF])
		Endif
		// Verifica se pode cancelar - Mudar para a função L010IsTrHabilita
		If lVer300 .AND. (!lLeu .AND. !lEstTecBan) //Se não for estorno ou não leu o cartão
			Exit
		Endif
		
		If lVer300
			cData := Subs(DToC(aLeTef[4]),1,5)
			nValorBak:=Valor
		
			//HOMOLOGACAO: O valor não pode vir da base, pois não gravamos os valores de Saque
			//o valor deve ser informado de acordo com o VALOR FINAL impresso no cupom.
			nValor	  := 0

			If cFormPg == "CH"
				cTitulo	  := "Dados da Transação - Cheque"
				nValorBak := If(Len(aTefChq)>0,aTefChq[5],0)
			Else
				cTitulo	  := "Dados da Transação "+Alltrim(aLeTef[2])
			Endif			
				
			nLin := 6

			DEFINE MSDIALOG oDlg TITLE cTitulo FROM 13,21 to 22,60  of GetWNDDefault()

			@  nLin,12 SAY "Valor Final" 	 			SIZE 130, 10 OF oDlg PIXEL
			@  nLin,88 MSGET nValor SIZE  55, 10 		OF oDLG PIXEL	Picture "@E 9,999,999.99" VALID (nValor>0 .AND. nValor>=nValorBak)
		    nLin += 12

			@ nLin,12 SAY "NSU (Número Seq. Único)" 	SIZE 130, 10 OF oDlg PIXEL
			@ nLin,88 MSGET cContr SIZE  18, 15 		OF oDlg PIXEL Picture "@!" WHEN .F.
			nLin += 12

			@ nLin,12 SAY "Data da Compra (DD/MM)"  	SIZE 130, 10 OF oDlg PIXEL
			@ nLin,88 MSGET cData SIZE  55, 10 		OF oDLG PIXEL	Picture "@R 99/99" Valid L010DtCompra( cData )

			DEFINE SBUTTON FROM 47, 082 TYPE 1 PIXEL ACTION ( lSaiCanc:=.F.,oDlg:End() ) ENABLE OF oDlg
			DEFINE SBUTTON FROM 47, 112 TYPE 2 PIXEL ACTION ( lSaiCanc:=.T.,oDlg:End() ) ENABLE OF oDlg
			ACTIVATE MSDIALOG oDlg CENTERED

			//HOMOLOGACAO: Tratamento para a saida da tela
			If lSaiCanc
		   	   nOpc1 :=0
			   lRet  :=.F.
			   Exit
			Endif
		Endif
		
		cData 	         	:= Right( aLeTef[__DATATEF], 2 ) + Left( aLeTef[__DATATEF], 2 )
		ctValor	         	:= L010ValStr( nValor )
		ctDocument_ctData 	:= Alltrim( Str( Val( cContr ) ) ) + cSep + cData

	Endif
	
	If lVer300
		nOpc1:=1
		lLeu := .T.
	Else
		nOpc1 := 3
	Endif
	
	If ExistBlock("L010TCPF") // Ponto de Entrada para alguma validacao/controle relativo ao CPF consultado
		If ! ExecBlock("L010TCPF",.F.,.F.,{cTipoPessoa,cCGC_CPF})
			nOpc1 := 4
		Endif
	Endif
	
	If nOpc1 == 3
		DEFINE MSDIALOG oDlg TITLE cCaption From 13,21 to 18,64	of GetWndDefault()
		@ 10,005 BUTTON "Confirma" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=1,oDlg:End() )
		@ 10,060 BUTTON "Redigita" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=2,oDlg:End() )
		@ 10,115 BUTTON "Abandona" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=3,oDlg:End() )
		ACTIVATE MSDIALOG oDlg CENTERED
	Endif
	
	If (nOpc1 <> 2)
		Exit
	Endif
End

If !lCancela
	
	If nOpc1 == 1  .AND. lLeu .AND. !lConsCDC
	
		If nTipCon == 3 .OR. nTipcon == 4
			If cTipoPessoa == "F"
				cTipoPessoa:= "2"
			Else
				cTipoPessoa:= "1"
			Endif
		Endif
		
		If lEstTecBan .AND. Len(aTefChq) > 0						 //Pego os dados da base 
			cTrans			 := "70"
			ctFuncao		 := "7" 
			ctValor			 := L010ValStr( nValor )
			ctBanco			 := Left( AllTrim( aTefChq[1]  ) , 4  )
			ctAgencia		 := Left( AllTrim( aTefChq[2]  ) , 5  )
			ctCCorrente	     := Left( AllTrim( aTefChq[3]  ) , 10 )
			ctNum_Cheq		 := Left( AllTrim( aTefChq[4]  ) , 7  )
			ctDt_H			 := StrZero(Day(aLeTef[__EMISNF]),2) + StrZero(Month(aLeTef[__EMISNF]),2) + Str(Year(aLeTef[__EMISNF]),4)
			ctCompens		 := "018"
			ctDad_Chq        := ctCompens + cSep + ctBanco + cSep + ctAgencia + cSep + ctCCorrente + cSep + ctNum_Cheq
			ctNum_H			 := cContr
		Else
			aTip			 := { "72", "74", "70", "78", cTrans, cTrans, cTrans }
			cTrans			 := aTip[ nTipCon ]
			ctValor			 := L010ValStr( nValor )
			ctTipo_Pes		 := cTipoPessoa
			ctCgc_Cpf		 := AllTrim( cCGC_CPF )
			ctBanco			 := AllTrim( cBco )
			ctAgencia		 := Left( AllTrim( cAgencia	), 5 )
			ctNum_Cheq		 := Left( AllTrim( cNumCheque ), 7 )
			ctBco_Agenc 	 := ctBanco + ctAgencia
			ctDt_ChQ_Ser   	 := Left( DtoC( dData ), 2 ) + Substr( DtoC( dData ), 4, 2 ) + Str( Year( dData ), 4 )
			ctDt_Cheq_TelD	 := Left( DtoC( dData ), 2 ) + Substr( DtoC( dData ), 4, 2 ) + Right( DtoC( dData ), 2 )
			ctCCorrente  	 := ctBanco + cSep + ctAgencia + cSep + AllTrim( cConta )
			ctCartao         := AllTrim( ctCartao )
			ctDad_Chq        := cSep + ctCCorrente + cSep + ctNum_Cheq
			ctFuncao		 := "1" 
		Endif		
		
		If nTipCon == 4
			If ctTipCons == "T"
				ctTipCons := "1"
			ElseIf cttipCons == "S"
				ctTipCons := "2"
			Else
				ctTipCons := "3"
			Endif
		Endif
	    
		//HOMOLOGACAO: Para a Nova Consulta Garantia TecBan' deve usar a funcao '2', incluso o cancelamento
	    If nTipConAnt == 9
	   		ctFuncao:="2"		//Consulta Cheque
		ElseIf nTipConAnt == 10
			ctFuncao:="6"       //Telecheque Garantido
	    Endif
	    
	    lCcs := At('C.C.S.',Upper(AllTrim(SL1->L1_INSTITU)))<>0
	
		While .T.
		    If lCcs .AND. SuperGetMV("MV_TEFMILH",NIL,.F.)
	
	            IFPegCupom( nHdlECF, @cNumCupFis)          	// Pegar o Numero do Cupom da Impressora
	        	cSaida	:= 'S' 								// Saida da consulta
				cTrans	:= '50'								// Codigo da transacao
	
				ctDados	:= '0000'			               	// Versão da Mensagem. Identifica o mapeamento
				ctDados	+= StrZero(Val(cNumCupFis)+1,7)     // Numero do cupom fiscal da operacao de estorno
				ctDados += DtoS(SL1->L1_EMISSAO)           	// Data do cupom fiscal 
				ctDados += SL1->L1_HORATEF       			// Hora do cupom fiscal 
				ctDados += StrZero(0,6,0) 					// Matricula do funcionario PDV
				ctDados += StrZero(0,30,0)					// Nome do funcionario do PDV
	
				aTrans := { "50",;
							{ctRedeDest,;
							ctADM,;
							'1',;																					// Valor fixo
							'36',;																					// Codigo da rede
							'11',;																					// SubFuncao - Venda/troca de milhas 
							StrZero(Val(ctCartao),16),;																// Numero do Cartão ou Trilha 2
							ctValor,;																				// Valor da Venda
	            			SL1->L1_DOCTEF,;																		// Numero do Documento
	            			Subs(SL1->L1_DATATEF,3,2)+Subs(SL1->L1_DATATEF,1,2)+Subs(DtoS(SL1->L1_EMISSAO),1,4),;	// Data da venda/troca
							ctDados } }																				// Dados adicionais da venda  
			    aRetorno := L010MonEnvResp( cTrans,"C",,aTrans,nX)
			Else
				//Mex
				If cPaisLoc == "MEX"
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Executa o cancelamento da transacao³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					lRet :=L010Mex(cOpera,,aLetef)
					cTrans		:="" 
					lImprime	:= .T.
					aRetorno:=Array(7)
					aRetorno[1]	:=If(lRet,1,0)					// Resultado da Transacao
					aRetorno[2]	:=""							// Codigo de Retorno da Transacao, Nao Utilizado
					aRetorno[3]	:=If(lRet,aClone(aTicket),"")	// Ticket a ser impresso
					aRetorno[4]	:=""							// Ticket de Promissoria, Nao Utilizado
					aRetorno[5]	:=""                            // Nao Utilizado
					aRetorno[6]	:=""							// Nao Utilizado
					aRetorno[7]	:=""							// Nao Utilizado
		        Else
		        	aRetorno := L010MonEnvResp( AllTrim(cTrans),,,,nX)
		        Endif	
		    Endif
		
			cCodRet	:= aRetorno[ 2 ]
			
			cGaranCh    := "ERRO: "+aRetorno[ 2 ]+" - "+aRetorno[ 7 ]
			
			Do Case
				Case aRetorno[ 1 ] == 1 
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Se resposta = ok, Exibir e confirmar	  				     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					lRet:= .T.
					//HOMOLOGACAO: Não precisamos gerar os dados pois a trn retorna sozinha
					If AllTrim( cTrans ) $ "72/74/78" //"70/72/74/78"
					    //HOMOLOGACAO: A transacao 70 garantia de cheque tecban necessita de confirmacao
						lConfOk := .F.
						If ValType( aRetorno[ 3 ] ) == "A"
							If nTipCon == 3 .OR. nTipCon == 4
								If cTipoPessoa == "2"
									cTipoPessoa := "F"
								Else
									cTipoPessoa := "J"
								Endif
							Endif
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Dados adicionais especificos para cupom serasa/Ass.Com.³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							aAdd( aRetorno[ 3 ],"VALOR..............: " + Transform( nValor, "@e 999,999,999.99" ) )
							aAdd( aRetorno[ 3 ],"BANCO..............: " + cBco )
							aAdd( aRetorno[ 3 ],"AGENCIA............: " + cAgencia )
							aAdd( aRetorno[ 3 ],"TIPO DE PESSOA.....: " + If( cTipoPessoa == "F", "Fisica", "Juridica" ) )
							If cTipoPessoa == "F"
								aAdd( aRetorno[ 3 ],"CPF................: " + Transform( cCGC_CPF, "@R 999.999.999-99" ))
							Else
								aAdd( aRetorno[ 3 ],"CNPJ...............: " + Transform( cCGC_CPF, "@R! NN.NNN.NNN/NNNN-99" ))
							Endif
							if ! Empty(ctRg)
								aAdd( aRetorno[ 3 ],"RG.................: " + ctRG )
							Endif
							aAdd( aRetorno[ 3 ],"NUMERO DO CHEQUE...: " + cNumCheque )
							aAdd( aRetorno[ 3 ],"DATA DO VENCIMENTO.: " + DToC( dData ) )
							aAdd( aRetorno[ 3 ],"QUANTID. DE CHEQUES: " + ctQcheque )
							
							If (AllTrim(cTrans) == "74")
								aAdd( aRetorno[ 3 ],"CODIGO DE GARANTIA DE CHEQUE:" )
								aAdd( aRetorno[ 3 ], aRetorno[ 7 ] )
							Endif
							
							aAdd( aRetorno[ 3 ], "" )
							aAdd( aRetorno[ 3 ], "" )
							aAdd( aRetorno[ 3 ], "" )
						Endif
					Endif
	
					If ValType( aRetorno[ 3 ] ) == "A"
						aTicket:={}
						If cOpera <> "C" 	.OR. ;
							(cOpera == "C"  .AND. (TEF_IMPTEFC == "S" .OR. cTrans == "70"))
							For nIndice:=1 To Len(aRetorno[3])
								AAdd(aTicket,aRetorno[3][nIndice])
							Next
						Endif
					Endif
					
					If cOpera == "C"
						If TEF_IMPTEFC == "S" .AND. lImprime
							cTipVenCon := "S"
							If lTefMult .AND. Type("aTefMult") == "A"
								aTefMult[nX][7][1][14] := Aclone(aTicket)
							Else
							   If lImprime
									If lUsaDisplay
										DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do Cupom" )         
		   		   						DisplayEnv(StatDisplay(), "2C" + " ")                             
		  	   						Endif
		  	   						// Aguarde ... imprimindo o cupom TEF "
							    	ljMsgRun( STR0065 ,, { || L010TefImpr( aRetorno[ 3 ], aRetorno[ 4 ], lConfOk)})
							   Endif	
							Endif
						Endif
					Else
						cTipVenCon := "S"
						If lImprime
							lAceite:=.T.
							If lUsaDisplay
								DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do Cupom" )         
	   		   					DisplayEnv(StatDisplay(), "2C" + " ")                             
	  	   					Endif
	  	   					// "Aguarde ... imprimindo o cupom TEF " 
							ljMsgRun( STR0065 ,, { || L010TefImpr( aRetorno[ 3 ], aRetorno[ 4 ], lConfOk)})
						Endif
					Endif
					
					lRet := .T.
					
					If AllTrim(cTrans) == "74"
						cGaranCh := aRetorno[ 7 ]
						MsgInfo(cGaranCh,"Informe o Cliente")
					Else
						cGaranCh := "OK"
					Endif
					
				Case aRetorno[ 1 ] == 3
					If aRetorno[ 5 ]
						Loop
					Endif
			EndCase
			Exit
		End
	Endif
Endif

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010DtCompra() Autor ³ Sergio Siveira	    ³ Data ³ 03/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a data de compra para cancelamento 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010DtCompra( ExpC1 )							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 -> Data no formato DDMM 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Data valida 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010DtCompra( cData )

Local lRet := .F.

cData := Left( cData, 2 ) + "/" + Right( cData, 2 )

If !Empty( CToD( cData + "/" + Str( Year( dDataBase ), 4 ) , "ddmmyy" ) ) .OR.  ;
	!Empty( CToD( cData + "/" + Str( Year( dDataBase ) - 1, 4 ) , "ddmmyy" ) )
	lRet := .T.
Endif

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010RenTry()³ Autor ³ Sergio Silveira    ³ Data ³ 15/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua o rename do arquivo de transmissao 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010RenTry()									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Sucesso da operacao								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010RenTry()

Local lSucess := .F.
Local nLoop

For nLoop := 1 To 25
	lSucess := If( FRename( cDirT_Tx+"TEMP."+cPTerm, cDirT_Tx+"PDV."+cPTerm ) == 0, .T., .F. )
	If lSucess
		Exit
	Endif
	Sleep(50)
Next
Return lSucess

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010Choice()³ Autor ³ Sergio Silveira    ³ Data ³ 24/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de controle para o aChoice						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN1 := L010Choice()				 				      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpN1 -> Retorno para o aChoice							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010Choice()

Local nRet	 := 2
Local nTecla := LastKey()

Do Case
	Case (nTecla == 13)
		nRet := 1
	Case (nTecla == 27)
		nRet := 0
EndCase

Return nRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010InstVal()³ Autor ³ Sergio Silveira    ³ Data ³ 01/09/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao da Instituicao									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010InstVal( ExpC1 ) 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 -> Codigo da Instituicao							  ³±±
±±³			 ³ ExpL1 -> Retorna se existe a Administradora cadastrada     ³±±
±±³			 ³ 			no SAE.											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> .T. ou .F.										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010InstVal( cInstitu, lSemRede )

Local nReg	:= SAE->( RecNo() )
Local aInst := {}
Local aReg	:= {}
If Empty(cInstitu)
	Return (.T.)
Endif
SAE->( dbEval( { || aAdd( aInst, Upper(Left( AE_DESC, 16 ))), ;
aAdd(aReg , Recno()) } ) )
nElem := aScan( aInst, Upper(cInstitu) )
If (nElem > 0) .AND. lCdc
	SAE->( dbGoto( aReg[nElem] ) )
	cCartao := SAE->AE_COD
ElseIf lCdc
	cCartao := ""
Endif

lSemRede := ( nElem == 0 )

SAE->( dbGoto( nReg ) )
Return ( nElem <> 0 )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010WaitWin()³ Autor ³ Sergio / Fernando  ³ Data ³ 12/09/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Espera o arquivo de resposta do SiTef no Windows			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010WaitWin()									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Indica se ocorreu TimeOut					      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010WaitWin(cFile)

Local lTimeOut := .F.
Local nSecIni := Seconds()

If ValType(ntTimeOut) <> "N"
	ntTimeOut := 20
Endif

While !File(cFile)
	If (( Seconds() - nSecIni ) > ntTimeOut)
		lTimeOut := .T.
		Exit
	Endif
	Sleep(50)											// Pausa para nao consumir muitos recursos do processador
End

Return( lTimeOut )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010ValStr() ³ Autor ³ Sergio Silveira    ³ Data ³ 19/05/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte um valor numerico para o valor caractere padrao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := L010ValStr( ExpN1 )								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 -> Valor a ser convertido							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpC1 -> Valor convertido								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010ValStr( nValOrig )

Local cValRet

cValRet := AllTrim( StrTran( Str( Round( nValOrig, 2 ),12,2 ), ".", "" ) )

Return( cValRet )


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010CCSPG    ³ Autor ³ Luis Marcelo Kotaki³ Data ³ 13/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua o pagamento de titulos com cartao CSS       		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := Array[4]                      					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ [ <ExpA1> ] - Elemento 1 -> Codigo da rede (36)            ³±±
±±³			 ³ 				- Elemento 2 -> Valor do pagamento            ³±±
±±³			 ³ 				- Elemento 3 -> Tipo de Entrada do doc (03)   ³±±
±±³			 ³ 				- Elemento 4 -> Dados do carne                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Pagamento com sucesso ou n„o         			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CCSPG(aDados)
Local lRet	:= .F.      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

nParCred:= 0
nParDeb := 0
nParDin := 0
nParCheq:= 0

If !lInfoCard  // CCS e nao InfoCard
	lCCs    := .T.
	
	ctDados  := SPACE(47)
	ctEntDoc := SPACE(2)
	ctCodNet := SPACE(2)
	
	ctCodNet  := aDados[1]
	ctParc_Val:= AllTrim( StrTran( Str( Round( aDados[2], 2 ),12,2 ), ".", "" ) )
	ctEntDoc  := aDados[3]
	ctDados   := aDados[4]
	
	cTrans := "40"
	
Else
	cTrans    := "F0"
	ctFuncao  := "20"
	ctTipOp   := "2"
	ctCartao	 := aDados[1]
	ctValor	 := AllTrim( StrTran( Str( Round( aDados[2], 2 ),12,2 ), ".", "" ) )
	ctPlano   := aDados[3]
	ctOrcam   := aDados[4]
	ctData    := aDados[5]
	ctHoraVend:= aDados[6]
	ctDat_Venc:= aDados[7]
Endif

// Sempre vai ser cheque
// alterado para contemplar pagamento em dinheiro tb.
If Upper(aDados[5])=="R$"
	nDin     := aDados[2]
	nParDin  := 1
Else
	nCheq    := aDados[2]
	nParCheq := 1
Endif

aRet := L010MonEnvResp( AllTrim(cTrans) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se tudo Ok, mando a impressÆo de pagamento             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (aRet[1] == 1)
	cTipVenCon := "PG"
	If lUsaDisplay
		DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do Cupom" )         
   		DisplayEnv(StatDisplay(), "2C" + " ")                             
  	Endif
  	// ### "Aguarde ... imprimindo o cupom TEF "
	ljMsgRun(STR0065 ,,{ || lRet := L010TefImpr( aTicket, aPromis, .T.)})
Else
	L010Confirma( lRet )
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "8) Confirmação com "+Iif(lRet,".T.",".F.") )
	Endif						
Endif

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010CCSCA    ³ Autor ³ Luis Marcelo Kotaki³ Data ³ 13/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua o cancelamento de pagamentos feitos com o CSS   	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := Array[4]                      					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ [ <ExpA1> ] - Elemento 1 -> Codigo da rede (36)            ³±±
±±³			 ³ 				- Elemento 2 -> Valor do pagamento            ³±±
±±³			 ³ 				- Elemento 3 -> Tipo de Entrada do doc (03)   ³±±
±±³			 ³ 				- Elemento 4 -> Dados do carne                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Pagamento com sucesso ou n„o         			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function L010CCSCA(aDados)

Local lRet	:= .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

If !lInfoCard //CCS e nao InfoCard
	ctDados  := SPACE(9)
	ctEntDoc := SPACE(2)
	ctCodNet := SPACE(2)
	ctDados  := "" //SPACE(47)
	ctNsu    := "" //SPACE(9)
	ctDatPag := SPACE(8)
	
	ctCodNet  := aDados[1]
	ctParc_Val:= AllTrim( StrTran( Str( Round( aDados[2], 2 ),12,2 ), ".", "" ) )
	ctEntDoc  := aDados[3]
	ctDados   := aDados[4]
	ctNsu 	 := aDados[5]
	
	If VAL(Substr(aDados[6],7,2)) > 90
		ctDat_Pag  := Substr(aDados[6],1,2)+Substr(aDados[6],4,2)+"19"+Substr(aDados[6],7,2)
	Else
		ctDat_Pag  := Substr(aDados[6],1,2)+Substr(aDados[6],3,2)+"20"+Substr(aDados[6],7,2)
	Endif
	
	cTrans := "41"
	
Else
	// InfoCard
	cTrans     := "F0"
	ctFuncao   := "31"
	ctTipOp    := "2"
	ctCartao	  := aDados[1]
	ctValor	  := AllTrim( StrTran( Str( Round( aDados[2], 2 ),12,2 ), ".", "" ) )
	ctOrcam    := aDados[3]
	ctDat_Vend := aDados[4]
	ctData     := aDados[5]
	ctHoraVend := aDados[6]
	ctDat_Venc := ""
Endif


aRet := L010MonEnvResp( AllTrim(cTrans) )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se tudo Ok, mando a impressÆo de CANCELAMENTO          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (aRet[1] == 1)
	cTipVenCon := "R"
	If lUsaDisplay
		DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do Cupom" )         
   		DisplayEnv(StatDisplay(), "2C" + " ")                             
  	Endif	
  	// ### "Aguarde ... imprimindo o cupom TEF "
	ljMsgRun( STR0065 ,,{ || lRet := L010TefImpr( aTicket, aPromis, .T.)})
Else
	L010Confirma( lRet )
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', "9) Confirmação com "+Iif(lRet,".T.",".F.") )
	Endif						
Endif

Return( lRet )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010CCSCO    ³ Autor ³ Luis Marcelo Kotaki³ Data ³ 13/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua a consulta dos dados do cartao CSS          		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := Array[4]                  						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ [ <ExpA1> ] - Elemento 1 -> Codigo da rede (36)            ³±±
±±³			 ³ 			   - Elemento 2 -> Valor do pagamento             ³±±
±±³			 ³ 			   - Elemento 3 -> Tipo de Entrada do doc (03)    ³±±
±±³			 ³ 			   - Elemento 4 -> Dados do carne                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Pagamento com sucesso ou n„o       				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 							     				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³04/10/01  ³Mauro Mancio - Em algumas transacoes nao ha necessidade de  ³±±
±±³			 ³ 			     imprimir o cupom. Por isso, foi incluido  o  ³±±
±±³			 ³ 			     novo parametro: "lImprime"                   ³±±
±±³			 ³ 			     DEFAULT = .T. (Imprime)                      ³±±
±±³			 ³ 			     Quando estiver True, o array aDados, poderah ³±±
±±³			 ³ 			     ser usado por @referencia                    ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function L010CCSCO(aDados, lImprime, cTipVenCon)
Local lRet	:= .F.
Local cTemp :=''  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

nParCred := 0
nParDeb  := 0
nParDin  := 0
nParCheq := 1
nCheq    := 1
lCCs     := .T.

If lImprime == Nil
	lImprime := .T.
EndIf

ctCodNet := Space(2)
ctEntDoc := Space(2)
ctDados  := Space(47)
ctCodNet := aDados[1]
ctEntDoc := aDados[2]
ctDados  := aDados[3]
cTrans   := "60"
If cTipVenCon=="CS"
   cSaida:='S'
Endif   
If .Not. lImprime
	cSaidaAnt := cSaida
	cSaida    := "S" // Saída para o Sistema (CCS/EMS)
Endif
aRet     := L010MonEnvResp( AllTrim(cTrans) )
If .Not. lImprime
	cSaida    := cSaidaAnt
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se tudo Ok, e a Instituicao est  cadastrada no SAE     ³
//³ vai p/ a funcao de impressao / confirmacao			   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aRet[1] == 1
	aDados := Aclone(aTicket)
	If cTipVenCon=="CS"
        // retona uma sting com os dados conforme layout da CCS
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Código de Operação = 001 - Consulta Milhas  (exclusivamente saída para Sistema)                                                                                             ³
		//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
		//³Nome                          X (20)    Nome no Cartão                                                                                                                      ³
		//³Cartão                        N (16)    Numero do cartão                                                                                                                    ³
		//³CPF                           N (11)    Número identificador do CPF                                                                                                         ³
		//³Situação do Cartão            X (10)    Situação atual do cartão ("CANCELADO", "INATIVO", "ATIVO", "BLOQUEADO", "NAO SOLIC.", "ATENDIM.", "NAO APROV.", "CHEQUE")           ³
		//³Envio para Gráfica            X (01)    Indica se cartão já foi enviado para gráfica (S/N)                                                                                  ³
		//³Lista Negativa Interna        X (01)    Indica se cliente está em cobrança (S/N)                                                                                            ³
		//³Lista Negativa Manual         X (01)    Indica se cliente está na Lista Negativa Manual (S/N)                                                                               ³
		//³Primeira Compra do Cliente    X (01)    Indica se for a primeira compra do cliente (S/N)                                                                                    ³
		//³Data do Cadastramento         N (08)    AAAAMMDD  do Cadastramento do cartão                                                                                                ³
		//³Saldo Atual em Qtd. Milhas    N (08)    Saldo atual em milhas (Acumuladas - Utilizadas - Prescritas)                                                                        ³
		//³Saldo Atual em Vlr. Milhas    N (12,2)  Saldo atual em moeda corrente (Saldo Atual em Milhas * Vlr. Unitário da Milha p/ Trocar)                                            ³
		//³                                                                                                                                                                            ³
		//³Limite de Crédito Disponível                                                                                                                                                ³
		//³com Percentual Adicional      N (12,2)  Valor do saldo disponível para crédito acrescido do % adicional permitido (Perc. Limite de Crédito) do cliente (que faz a consulta) ³
		//³                                                                                                                                                                            ³
		//³Dia do Vencimento para                                                                                                                                                      ³
		//³Parcelado Programado          N (02)    Dia do vencimento do cliente cuja modalidade no seu cadastro é de parcelamento programado.                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cTemp  :=Subs(aDados[1],3)
		aDados:={}
		aadd(aDados,{"Nome do Cliente no Cartão"      ,Subs(cTemp,1,20)});cTemp:=Subs(cTemp,21)
		aadd(aDados,{"Numero do Cartão"               ,Subs(cTemp,1,16)});cTemp:=Subs(cTemp,17)
		aadd(aDados,{"CPF do cliente"                 ,Subs(cTemp,1,11)});cTemp:=Subs(cTemp,12)
		aadd(aDados,{"Situação do Cliente"            ,Subs(cTemp,1,10)});cTemp:=Subs(cTemp,11)
		aadd(aDados,{"Enviado para a grafica"         ,Subs(cTemp,1,1)}) ;cTemp:=Subs(cTemp,2)
		aadd(aDados,{"Lista negativa interna"         ,Subs(cTemp,1,1)}) ;cTemp:=Subs(cTemp,2)
		aadd(aDados,{"Lista negativa manual"          ,Subs(cTemp,1,1)}) ;cTemp:=Subs(cTemp,2)
		aadd(aDados,{"Primeira compra do cliente"     ,Subs(cTemp,1,1)}) ;cTemp:=Subs(cTemp,2)
		aadd(aDados,{"Data do Cadastramento"          ,Subs(cTemp,7,2)+'/'+Subs(cTemp,5,2)+'/'+Subs(cTemp,3,2)});cTemp:=Subs(cTemp,9)
		aadd(aDados,{"Saldo atual de milhas"          ,Subs(cTemp,1,8 )});cTemp:=Subs(cTemp,9)
		aadd(aDados,{"Saldo atual em valor de milhas" ,Subs(cTemp,1,12)});cTemp:=Subs(cTemp,13)
		aadd(aDados,{"Limite de crédito disponível"   ,Subs(cTemp,1,12)});cTemp:=Subs(cTemp,13)
		aadd(aDados,{"Dia do vencimento"              ,Subs(cTemp,1,2)})
	Endif
	If lImprime
		cTipVenCon := "S"
		If lUsaDisplay
			DisplayEnv(StatDisplay(), "1C" + "Aguarde a impressao do Cupom" )         
   		   	DisplayEnv(StatDisplay(), "2C" + " ")                             
  		Endif
  		// ### "Aguarde ... imprimindo o cupom TEF "
		ljMsgRun( STR0065 ,,{ || L010TefImpr( aTicket,,.F.)})
	Endif
	lRet   := .T.
Else
	aDados := {"ERRO"}
Endif

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ L010CCSAMºAutor  ³Marcos Alves        º Data ³  19/12/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Aquisicao de Milhas com outras forma de pagamento          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CCSAM(aDados)

Local cSerie   	:= Space(TamSx3("L1_SERIE")[1])
Local cNota    	:= Space(TamSx3("L1_DOC")[1])
Local nValor   	:= 0
Local cDoc     	:= '000000'
Local dEmissao 	:= ctod("")
Local oDlg1
Local nOpc     	:= 0
Local cData    	:= ''
Local aTrans 	:= {}
Local aFPag  	:= {{'R$', 'Dinheiro'			},;
					{'CH', 'Cheque a Vista'		},;
                    {'CH', 'Cheque Pre'			},;
                    {'CD', 'Cartao de Debito'	},;
                    {'CC', 'Cartao de Credito'	},;
                   	{'TK', 'Ticket'				},;
                   	{'TC', 'Traveller Check'	},;
                   	{'DF', 'Desconto em Folha'	},;
                   	{'VA', 'Vales'				}}

Private nTotal  := 0
Private aParcTef:= {}

aCartaoMH       :=If(Type("aCartaoMH")=="A",aCartaoMH,{})

nParCred 		:= 0
nParDeb  		:= 0
nParDin  		:= 0
nParCheq 		:= 0
nDin    		:= 0
nCartao 		:= 0
nCheq   		:= 0
nDebito 		:= 0
ctTrilha_2 		:= ''
cTrans	   		:= '50'	// Codigo da transacao

If cTipVenCon=="AM"
	If lFiscal
		cSerie := LjGetStation("SERIE")
	Endif
	DEFINE MSDIALOG oDlg1 TITLE "Aquisição de Milhagem" From 13,11 To 23,50	of GetWndDefault()
	@ 14,15	SAY "Serie    " SIZE 60 ,10 OF oDLG1 PIXEL
	@ 14,85	MSGET cSerie Valid !Empty(cSerie) SIZE 50 ,10 OF oDLG1 PIXEL
	@ 26,15	SAY "Documento Fiscal" SIZE 60 ,10 OF oDLG1 PIXEL
	@ 26,85	MSGET cNota Valid !Empty(cNota) SIZE 50 ,10 OF oDLG1 PIXEL
	DEFINE SBUTTON FROM 48,080 TYPE 1 PIXEL ACTION ( nOpc:=1,oDlg1:End() ) ENABLE OF oDlg1
	DEFINE SBUTTON FROM 48,110 TYPE 2 PIXEL ACTION ( nOpc:=2,oDlg1:End() ) ENABLE OF oDlg1
	ACTIVATE MSDIALOG oDlg1 CENTERED
	If nOpc == 2
		Return .F.
	Endif

	dbSelectArea("SL1")
	dbSetOrder(2)
	If !DbSeek(xFilial("SL1") + cSerie + cNota,.T.)
		Aviso( "Atenção","Nota Fiscal não encontrada.",{"&Ok"})
		Return nil
	Endif
    nValor   := SL1->L1_VLRTOT
    cDoc     := SL1->L1_DOC
    dEmissao := SL1->L1_EMISSAO
ElseIf cTipVenCon=="AO"
	aTrans:=L010VLRENTR(cTipVenCon, aDados)
	If aTrans[1]=='9'
		Return(.F.)
	Endif
    nValor   := SL1->L1_VLRTOT
    cDoc     := aDados[2][1]
    dEmissao := dDataBase
Endif
DEFINE MSDIALOG oDlg1 TITLE "Dados da Compra para Aquisição de Milhagem" FROM 13,21 to 22,60  of GetWNDDefault()
@	6,12 SAY "Valor da compra"  SIZE 130, 10 OF oDlg1 PIXEL
@	6,88 MSGET nValor SIZE  55, 10 OF oDLG1 PIXEL	Picture "@E 9,999,999.99" WHEN .F.
	
@ 18,12 SAY "Cupom fiscal" SIZE 130, 10 OF oDlg1 PIXEL
@ 18,88 MSGET cDoc SIZE  18, 15 OF oDlg1 PIXEL Picture "@!" WHEN .F.
	
@ 30,12 SAY "Data da Compra "  SIZE 130, 10 OF oDlg1 PIXEL
@ 30,88 MSGET dEmissao SIZE  55, 10 OF oDLG1 PIXEL	Picture "@R 99/99/99" WHEN .F.
	
DEFINE SBUTTON FROM 47,  090 TYPE 1 PIXEL ACTION ( nOpc:=1,oDlg1:End()) ENABLE OF oDlg1
DEFINE SBUTTON FROM 47,  120 TYPE 2 PIXEL ACTION ( nOpc:=2,oDlg1:End()) ENABLE OF oDlg1
ACTIVATE MSDIALOG oDlg1 CENTERED
If nOpc == 2
	Return .F.
Endif
If cTipVenCon=="AM" .OR. ( cTipVenCon=="AO" .AND. Len(aCartaoMH)>0 .AND. Empty(aCartaoMH[2]) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³aRet[1] ->Retorna o tipo do cartao³
	//³1 - Magnetico                     ³
	//³2 - Não Magnético                 ³
	//³3- CPF                            ³
	//³4- Abandona                       ³
	//³aRet[2] -> O dado sendo:          ³
	//³1,2 -> Numero do cartão           ³
	//³3 -> numero do cpf                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	// "C" - Pesquisa o CPF no CCS e retorna o numero do cartao
	aRet:=L010TCart('C')
	If aRet[1] == 4
		Return Nil
	Else
		ctCartao := AllTrim(aRet[2])
	Endif
Else
   ctCartao := Iif( Len(aCartaoMH)>0 , aCartaoMH[2] , "" )
Endif
dbSelectArea("SL4")
dbSetOrder(1)
cPag:='01'
If DbSeek(xFilial('SL4')+SL1->L1_NUM)
	// so podera ser efetuado uma unica transacao por cupom fiscal, sendo assim
	// esta fixando a forma de pagemanto com a primeira do SL4
	nPos:=Ascan(aFPag, {|X| X[1]==AllTrim(SL4->L4_FORMA)})
	cPag:=If(nPos=0,'01',StrZero(nPos,2))
Endif
ctDados	:= '0000'			                // * Versão da Mensagem. Identifica o mapeamento
ctDados	+= StrZero(Val(cDoc),7)     // * Numero do cupom fiscal emitido pelo terminal
ctDados += DtoS(dEmissao)                   // Data do cupom fiscal
ctDados += StrTran(Alltrim(SL1->L1_HORA),":","")+'00' // Hora do cupom fiscal
ctDados += StrZero(0,6,0) 					// Matricula do funcionario PDV
ctDados += StrZero(0,30,0)					// Nome do funcionario do PDV
ctDados	+= StrZero(0,12,0)               	// * Valor das milhas utilizadas como forma de pagamento
ctDados	+= StrZero(SL1->L1_DESCONT*100,12,0) // * Valor com desconto, sobre o valor a vista
ctDados	+= cPag								// Forma principal de pagamento:
// 01- Dinheiro
// 02- Cheque a vista
// 03- Cheque Pre
// 04- Cartão de debito
// 05- Cartão de Credito
// 06- Ticket
// 07- Cheque Administrativo
// 08- Traveller check
// 09- Desconto em folha
// 10- Vale
aTrans := { "50",;
			{ctRedeDest       		  ,;
			ctADM            		  ,;
			'1'               		  ,;												// Valor fixo
			'36'             		  ,;												// Codigo da rede
			'02'              		  ,;												// SubFuncao - Aquisição de milhas
			StrZero(Val(ctCartao),16),;													// Numero do Cartão ou Trilha 2
			StrZero(nValor*100,12,0),;													// Valor da Venda
			StrZero(Month(dDatabase),2) + SubStr( StrZero( YEAR(dDatabase) ,4) ,3,2),;	// Vencimento do cartao
			ctDados } }																	// Dados adicionais da venda
aRet := L010MonEnvResp( cTrans,"C",,aTrans)
If cPag=='01'
	nDin    := SL1->L1_VLRTOT
	nParDin := 1
ElseIf cPag=='02'.OR.cPag=='03'
	nCheq   := SL1->L1_VLRTOT
	nParCheq:= 0
ElseIf cPag=='04'
	nDebito := SL1->L1_VLRTOT
	nParDeb := 1
ElseIf cPag=='05'
	nCredito:= SL1->L1_VLRTOT
	nParCred:= 1
Endif
lRet := (aRet[1] == 1)
If aRet[1] == 1
	cTipVenCon := "S"
	// ### "Aguarde ... imprimindo o cupom TEF "
	ljMsgRun( STR0065 ,,{ || L010TefImpr( aTicket,,.T.)})
Else
	lRet:= .F.
Endif
Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ L010CCSEMºAutor  ³Marcos Alves        º Data ³  23/12/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Estorno de Aquisicao de Milhas com outras forma de pagamen º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CCSEM()

Local  cNota  := Space(TamSx3("L1_DOC")[1])
Local  oDlg1
Local  nOpc        := 0
Local  nValor      := 0
Local  cData       := ''
Local  aTrans      := {}
Local   cNumCupFis := '0000000'
Private nTotal     := 0
Private aParcTef   := {}

nParCred:= 0
nParDeb := 0
nParDin := 0
nParCheq:= 0

nDin    := 0
nCartao := 0
nCheq   := 0
nDebito := 0

ctTrilha_2 := ''
cTrans	   := '50'	// Codigo da transacao
nValor := 0
cNota  := Space(9)
cData  := ctod('')

DEFINE MSDIALOG oDlg1 TITLE "Estorno da aquisição de milhagem" FROM 13,21 to 22,60  of GetWNDDefault()
@	6,12 SAY "Valor da compra"  SIZE 130, 10 OF oDlg1 PIXEL
@	6,88 MSGET nValor SIZE  55, 10 OF oDLG1 PIXEL	Picture "@E 9,999,999.99" VALID nValor>0

@ 18,12 SAY "Numero documento " SIZE 130, 10 OF oDlg1 PIXEL
@ 18,88 MSGET cNota SIZE  18, 15 OF oDlg1 PIXEL Picture "@E 999999999" 

@ 30,12 SAY "Data da Compra "  SIZE 130, 10 OF oDlg1 PIXEL
@ 30,88 MSGET cData SIZE  55, 10 OF oDLG1 PIXEL	Picture "@R 99/99/99" 

DEFINE SBUTTON FROM 47,  090 TYPE 1 PIXEL ACTION ( nOpc:=1,oDlg1:End()) ENABLE OF oDlg1
DEFINE SBUTTON FROM 47,  120 TYPE 2 PIXEL ACTION ( nOpc:=2,oDlg1:End()) ENABLE OF oDlg1
ACTIVATE MSDIALOG oDlg1 CENTERED
If nOpc == 2
	Return .F.
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³aRet[1] ->Retorna o tipo do cartao  ³
//³1 - Magnetico                       ³
//³2 - Não Magnético                   ³
//³3- CPF ( quando 'C', retorna CARTAO ³
//³4- Abandona                         ³
//³aRet[2] -> O dado sendo:            ³
//³1,2 -> Numero do cartão             ³
//³3 -> numero do cpf                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// "C" - Pesquisa o CPF no CCS e retorna o numero do cartao
aRet:=L010TCart('C')
If aRet[1] == 4
	Return Nil
Else
	ctCartao := AllTrim(aRet[2])
Endif

IFPegCupom( nHdlECF, @cNumCupFis)           // Pegar o Numero do Cupom da Impressora
ctDados	:= '0000'			                // * Versão da Mensagem. Identifica o mapeamento
ctDados	+= StrZero(Val(cNumCupFis),7)      // * Numero do cupom fiscal emitido pelo terminal
ctDados += DtoS(SL1->L1_EMISSAO)           // Data do cupom fiscal
ctDados += StrTran(Alltrim(SL1->L1_HORA),":","")+'00' // Hora do cupom fiscal
ctDados += StrZero(0,6,0) 					// Matricula do funcionario PDV
ctDados += StrZero(0,30,0)					// Nome do funcionario do PDV
cData:=dtos(cData)
aTrans := { "50",;
			{ctRedeDest,;
			ctADM,;
			'1',;														// Valor fixo
			'36',;														// Codigo da rede
			'12',;														// SubFuncao - Aquisição de milhas
			StrZero(Val(ctCartao),16),;									// Numero do Cartão ou Trilha 2
			StrZero(nValor*100,12,0),;									// Valor da Venda
			StrZero(Val(cNota),9),;										// Valor da Venda
			SubStr(cData,7,2)+ SubStr(cData,5,2)+SubStr(cData,1,4),;	// Vencimento do cartao
			ctDados } }													// Dados adicionais da venda

aRet := L010MonEnvResp( cTrans,"C",,aTrans)
lRet := (aRet[1] == 1)
If aRet[1] == 1
	cTipVenCon := "S"
	// ### "Aguarde ... imprimindo o cupom TEF "
	ljMsgRun( STR0065 ,,{ || L010TefImpr( aTicket,,.F.)})
Else
	lRet:= .F.
Endif

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Loja017T()  ³ Autor ³ Edney S. Souza	    ³ Data ³ 25/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para gera‡„o do TEF em LOTE    					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LOJA017T()                   							  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Loja017T()
if lUsaTef .AND. cTipTEF == TEF_LOTE
	ExecBlock("LOTETEF")
Else
	Help(" ","1","NAOTEF")
Endif
Return NIL


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ LJ017Header ³ Autor ³ Edney S. Souza	    ³ Data ³ 25/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para gera‡„o do Header do arquivo TEF em LOTE 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ017Header(nRegistros) -> nHandle              			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nRegistros -> Quantidade de registros que ser„o gravados	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ nHandle -> Handle do arquivo criado						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Cria‡„o de TEF em Lote						  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LJ017Header(nRegistros)

Local TipRegistr := "HD"
Local LblArquivo := PADR(STRZERO(DAY(dDataBase),2)+ProxNum(),8)+".TEF"
Local SeqProcess := "000"
Local NumRegistr := STRZERO(nRegistros,5)
Local nHandle

cDirT_Tx   := LjGetStation("DIRTTX")
nHandle	  := Fcreate(cDirT_Tx+LblArquivo)
FWrite(nHandle,TipRegistr+LblArquivo+SeqProcess+NumRegistr+SPACE(167)+CHR(13)+CHR(10))

Return {nHandle,LblArquivo}

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ017Detalhe ³ Autor ³ Edney S. Souza	³ Data ³ 25/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para gera‡„o do Detalhe do arquivo TEF em LOTE 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ017Detalhe(nHandle,aDetalhe) -> cMens          		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nHandle -> Handle do Arquivo criado pela LJ017Header  	  ³±±
±±³          ³ aDetalhe -> Matriz com os dados para gerar o detalhe  	  ³±±
±±³          ³ aDetalhe[1] -> Parcelas									  ³±±
±±³          ³ aDetalhe[2] -> Juros da Administradora ? "S" ou "N"	  	  ³±±
±±³          ³ aDetalhe[3] -> N£mero do Cartao							  ³±±
±±³          ³ aDetalhe[4] -> Validade									  ³±±
±±³          ³ aDetalhe[5] -> Valor da Compra							  ³±±
±±³          ³ aDetalhe[6] -> Taxa de Servi‡o (opcional)				  ³±±
±±³          ³ aDetalhe[7] -> C¢digo de autoriza‡„o (opcional)		  	  ³±±
±±³          ³ aDetalhe[8] -> Campo Livre, m ximo de 40 (opcional)        ³±±
±±³          ³ aDetalhe[9] -> Codigo de Seguranca                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ cMens -> Mensagem de erro ou brancos qdo n„o ocorrer erros ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Cria‡„o de TEF em Lote						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ017Detalhe(nHandle,aDetalhe)
Local TipOper
Local EstOper
Local CodRet
Local CodEmp
Local nAsc
Local NumCart
Local ValCart
Local nMes
Local ValCompr
Local ValTax
Local NumParc
Local CodAut
Local DatCompr
Local HorCompr
Local NumComp
Local CodEstab
Local NumAut
Local NumContr
Local NomRede
Local CampLivr
Local CodSeg
Local lTefVer := (SuperGetMV( "MV_TEFVERS" ) == "03.00")
Local cRet	:= "" 

if ! valtype(aDetalhe[1]) == "N" .OR. aDetalhe[1] < 1 .OR. aDetalhe[1] > 99
	Return ("O Número de parcelas deve ser numérico e estar entre 1 e 99.")
Endif
if ! valtype(aDetalhe[2]) == "C" .OR. ! aDetalhe[2] $ "SN"
	Return ("Juros da administradora deve ser caracter e conter 'S' ou 'N'.")
Endif
if aDetalhe[1] == 1
	TipOper := "00"
elseif aDetalhe[1] > 1 .AND. aDetalhe[2] == "N"
	TipOper := "01"
else
	TipOper := "02"
Endif
EstOper := "00"
CodRet  := "00"
CodEmp  := SuperGetMV("MV_EMPTEF")
if ! len(CodEmp) == 8
	Return ("O c¢digo da empresa no TEF (MV_EMPTEF) deve possuir 8 digitos, para empresa £nica deve ser 00000000.")
Endif
For nAsc := 1 to 8
	if ! Substr(CodEmp,nAsc,1) $ "0123456789"
		Return ("O c¢digo da empresa no TEF (MV_EMPTEF) deve ser composto apenas r apenas de n£meros, devendo ser preenchido com zeros a esquerda quando necess rio.")
	Endif
Next nAsc
if ! valtype(aDetalhe[3]) == "C"
	Return ("O n£mero do cart„o do cliente deve ser caracter.")
Endif
NumCart := padr(aDetalhe[3],If(lTefVer,20,40))
If Empty(NumCart)
	Return ("O n£mero do cart„o do cliente n„o pode estar em branco")
Endif
if ! valtype(aDetalhe[4]) == "C"
	Return ("A validade do cart„o deve ser caracter no formato MMAA.")
Endif
ValCart := aDetalhe[4]
if ! len(ValCart) == 4
	Return ("A validade do cart„o deve possuir 4 d¡gitos, sendo os dois primeiros para o mes e os dois £ltimos para o ano .")
Endif
For nAsc := 1 to 4
	if ! Substr(Valcart,nAsc,1) $ "0123456789"
		Return ("A validade do cartao deve ser compsta apenas de n£meros.")
	Endif
Next nAsc
nMes := Val(Left(ValCart,2))
if nMes < 1 .OR. nMes > 12
	Return ("O mˆs da validade do cart„o (2 primeiras posi‡”es) deve estar entre 01 e 12.")
Endif
if ! valtype(aDetalhe[5]) == "N"
	Return ("O valor da compra deve ser num‚rico.")
Endif
if aDetalhe[5] > 9999999999.99 .OR. aDetalhe[5] < 0.01
	Return ("O valor da compra n„o pode ser maior que 9.999.999.999,99 ou menor que 0,01.")
Endif
ValCompr := STRZERO(aDetalhe[5]*100,12)
if ! valtype(aDetalhe[6]) == "N"
	Return ("O valor da taxa de servi‡o deve ser num‚rico.")
Endif
if aDetalhe[6] > 9999999.99 .OR. aDetalhe[6] < 0.00
	Return ("O valor da taxa de servi‡o n„o pode ser maior que 9.999.999,99 ou menor que 0.")
Endif
ValTax := STRZERO(aDetalhe[6]*100,9)
NumParc := STRZERO(aDetalhe[1],2)
if ! valtype(aDetalhe[7]) == "N"
	Return ("O c¢digo da autoriza‡„o deve ser num‚rico, caso n„o exista deve ser zero.")
Endif
if aDetalhe[7] > 999999
	Return ("O c¢digo da autoriza‡„o nÆo pode ser maior do que 999999.")
Endif
CodAut   := STRZERO(aDetalhe[7],6)
DatCompr := SPACE(4)
HorCompr := SPACE(6)
NumComp  := SPACE(9)
CodEstab := SPACE(15)
NumAut   := SPACE(6)
NumContr := SPACE(6)
NomRede  := SPACE(16)
if ! valtype(aDetalhe[8]) == "C" .OR. len(aDetalhe[8]) > 40
	Return ("O campo livre deve ser caracter e conter no m ximo 40 posi‡”es.")
Endif
CampLivr := PADR(aDetalhe[8],40)
If lTefVer
   CodSeg   := PADR(aDetalhe[9],5)
   fWrite(nHandle, 	TipOper 	+ EstOper 	+ CodRet 	+ CodEmp 	+ ;
   					NumCart		+ CodSeg 	+ Space(15)	+ ValCart 	+ ;
   					ValCompr	+ ValTax	+ NumParc	+ CodAut	+ ;
   					DatCompr	+ HorCompr  + NumComp  	+ CodEstab	+ ;
   					NumAut		+ NumContr	+ NomRede	+ space(16)	+ ;
   					Space(8)	+ Space(8)	+ CampLivr	+ CHR(13)	+ ;
   					CHR(10) )
Else
   fWrite(nHandle,	TipOper	+	EstOper	+	CodRet	+	CodEmp	+	;
   					NumCart	+	ValCart	+	ValCompr+	ValTax	+	;
   					NumParc	+	CodAut	+	DatCompr+	HorCompr+	;
   					NumComp	+	CodEstab+	NumAut	+	NumContr+	;
   					NomRede	+	CampLivr+	CHR(13)	+	CHR(10)	)
Endif

Return cRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ017Trailer ³ Autor ³ Edney S. Souza	³ Data ³ 25/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para gera‡„o do Trailer do arquivo TEF em LOTE	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ017Trailer(nHandle) -> NIL                    			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nHandle -> Handle do arquivo criado						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Cria‡„o de TEF em Lote						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ017Trailer(nHandle, LblArquivo)
Local TipRegistr := "TR"
Local SeqProcess := "000"
Local TRUltProc  := "00000"
Local TROKProc   := "00000"
Local TRErrProc  := "00000"
Local TRNComProc := "00000"
Local TROKArq    := "00000"
Local TRErrArq   := "00000"
Local TRNComArq  := "00000"
Local TRPendArq  := "00000"

FWrite(nHandle    , TipRegistr + LblArquivo + SeqProcess + ;
       TRUltProc  + TROKProc   + TRErrProc  + TRNComProc + ;
       TROKArq    + TRErrArq   + TRNComArq  + TRPendArq  + ;
       SPACE(132) + CHR(13)    + CHR(10))
FClose (nHandle)

Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ Loja018T()  ³ Autor ³ Edney S. Souza	    ³ Data ³ 26/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para gera‡„o do TEF em LOTE    					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LOJA018T()                   							  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja018T()
if lUsaTef .AND. cTipTEF == TEF_LOTE
	LjGrvLog( NIL , " Antes da execução de RECEBTEF")
	ExecBlock("RECEBTEF")
	LjGrvLog( NIL , " Depois da execução de RECEBTEF")
Else
	Help(" ","1","NAOTEF")
EndIf
Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ018Header ³ Autor ³ Edney S. Souza	    ³ Data ³ 26/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para abertura do arquivo de retorno do TEF em LOTE  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ018Header() -> nRegistros                     			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ nHandle -> Handle do arquivo aberto    					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Cria‡„o de TEF em Lote						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ018Header(cFileName)
Local cBuffer := SPACE(191)
Local nHandle

cDirT_Rx := LjGetStation("DIRTRX")  
nHandle  := FOpen(cDirT_Rx+cFileName)

If nHandle >= 0
	Fread(nHandle,@cBuffer,191)
EndIf

Return nHandle

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ018Detalhe ³ Autor ³ Edney S. Souza    ³ Data ³ 26/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para leitura do Detalhe do arquivo TEF em LOTE 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ018Detalhe(nHandle) -> aDetalhe               			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nHandle -> Handle do Arquivo aberto pela LJ018Header  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ aDetalhe -> Matriz com os dados lidos do detalhe  	      ³±±
±±³          ³ aDetalhe[ 1] -> Estado da Opera‡„o (C 2)     		  	  ³±±
±±³          ³ aDetalhe[ 2] -> C¢digo de Retorno (C 2)     		          ³±±
±±³          ³ aDetalhe[ 3] -> Data da Efetiva‡„o da compra (D) 	  	  ³±±
±±³          ³ aDetalhe[ 4] -> Hora da Efetiva‡„o da compra (C 8)		  ³±±
±±³          ³ aDetalhe[ 5] -> N£mero do Comprovante de Compra (C 9)      ³±±
±±³          ³ aDetalhe[ 6] -> C¢digo do Estabelecimento (C 15)		      ³±±
±±³          ³ aDetalhe[ 7] -> N£mero da Autoriza‡„o (C 6)  			  ³±±
±±³          ³ aDetalhe[ 8] -> N£mero de Controle do SITEF (C 6)  	      ³±±
±±³          ³ aDetalhe[ 9] -> Nome da rede (C 16)				    	  ³±±
±±³          ³ aDetalhe[10] -> Campo Livre (C 40)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Leitura de TEF em Lote						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ018Detalhe(nHandle)
Local EstOper
Local CodRet
Local DatCompr
Local DiaCompr
Local MesCompr
Local AnoCompr
Local HorCompr
Local NumComp
Local CodEstab
Local NumAut
Local NumContr
Local NomRede
Local CampLivr
Local cBuffer := SPACE(191)
Local aRet := {}

Fread(nHandle,@cBuffer,191)
If Left(cBuffer,2) == "TR"
	aRet := {}
Else
	EstOper  := Substr(cBuffer,3,2)
	CodRet   := Substr(cBuffer,5,2)
	MesCompr := Substr(cBuffer,88,2)
	DiaCompr := Substr(cBuffer,90,2)
	
	If MesCompr == "12" .AND. Month(dDatabase) == 1
		AnoCompr := STRZERO(YEAR(dDatabase)-1,4)
	Else
		AnoCompr := STRZERO(YEAR(dDatabase),4)
	EndIf
	
	DatCompr := ctod(DiaCompr+"/"+MesCompr+"/"+AnoCompr , "ddmmyy" )
	HorCompr := Substr(cBuffer,92,2)+":"+Substr(cBuffer,94,2)+":"+Substr(cBuffer,96,2)
	NumComp  := Substr(cBuffer,98,9)
	CodEstab := Substr(cBuffer,107,15)
	NumAut   := Substr(cBuffer,122,6)
	NumContr := Substr(cBuffer,128,6)
	NomRede  := Substr(cBuffer,134,16)
	CampLivr := Substr(cBuffer,150,40)
	
	aRet := {EstOper, CodRet   , DatCompr, HorCompr , ;
	         NumComp, CodEstab , NumAut  , NumContr , ;
	         NomRede, CampLivr}
EndIf

Return aRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ018Trailer ³ Autor ³ Edney S. Souza	³ Data ³ 26/04/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para fechar arquivo de resposta do TEF em LOTE	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJ018Trailer(nHandle) -> NIL                    			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nHandle -> Handle do arquivo aberto						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ RDMAKES de Leitura de TEF em Lote						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ018Trailer(nHandle)
FClose(nHandle)
Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³L010VerGrP³ Autor ³ Edney Soares de Souza ³ Data ³ 26.06.00 ³±±
±±³          ³          ³       ³                       ³      ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ TEF DISCADO                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ L010VerGrP()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010VerGrP( aDados, cMsgCupom	, nVlrTotal, cId800	,;
					 oWsTrn, aDadosTrn	, lRecCel  , lTemTEFOk,;
					 lConfNF, lTitulo , lBaixa, aParcTef)
Local nParc
Local nOp
Local oDlg
Local oCheq
Local oDebito
Local oCartao
Local oPre
Local nPre
Local oGroup
Local oFnt
Local oFntTela
Local nI
Local lRet			:= .T.
Local lContinua		:= .T.
Local nNumCred	 	:= 0
Local aCartoes 	 	:= {}	// Array de parcelas de cartoes
Local aNaoCartao 	:= {}	// Array de parcelas que nao sao cartoes
Local cUltimoID  	:= ""	// Ultimo ID de cartao processado
Local nCount	 	:= 0	// Contador para o a cartao processado no momento
Local nSomaCred	 	:= 0	// Soma das parcelas para cartao de credito
Local nSomaDeb	 	:= 0	// Soma das parcelas para cartao de debito
Local nNumParcelas	:= 1	// Numero de parcelas do cartao atual
Local cUltimaForma	:= ""   // Ultima Forma de Pagamento processada
Local aDadoTrn		:= {}   //Dado da transação
Local nBandeira     := L010GetGPAtivo()					        // Controla a administradora que foi efetuada a transacao TEF baseado no aTefDisc

Private cPTermTmp  := "TMP"
Private cPTermReq  := "001"
Private cPTermResp := "STS"
Private cRegFim    := "0"
Private nSeqArq    := GetMV( "MV_TEFNSTR" ) // Numero Sequencial, identifica a transacão

DEFAULT cMsgCupom	:= ""	// Mensagem de fechamento do cupom fiscal
DEFAULT nVlrTotal	:= 0	// Valor total da venda
DEFAULT cId800		:= ""	//Id da Transacao TEF
DEFAULT oWsTrn		:= NIL	//Objeto da Trn de envio para retaguarda 
DEFAULT aDadosTrn	:= {}	//Dados da Transacao	    
DEFAULT lRecCel		:= .F.	//Transacao de pagamento em cartão com recarga de celular?
DEFAULT lTemTEFOk	:= .F. 
DEFAULT lConfNF     := .F. //Confirmação de Venda Direta (não-fiscal)
DEFAULT lTitulo		:= .F.
DEFAULT lBaixa		:= .F.
DEFAULT aParcTef	:= {}	// Parcelas TEF

ntTimeout := 5 // TimeOut para abertura e ativamento = 5 s

If cOpera $ "VAGCSXERBD"
	If TamSX3("L1_NSUTEF")[1] < 12 .OR. Len(SL1->L1_NSUTEF) < 12
		MsgStop("Aumente o campo L1_NSUTEF para 12 posicoes e atualize o SX3.")
	Endif                      

	nI := L010ATVDISC(aDados)
	
	If L010IsDirecao(nI) .OR. L010IsPayGo(nI) .OR. L010IsAuttar(nI)
		If cOpera == "A"
			LJProcDTEF(.T.,,,,.T.)
		EndIf
	Else
		If lDiscado  
			If !File("NCNCNF." + cPTerm)
				L010rArq()
			EndIf
		Endif
		
		If File("NCNCNF." + cPTerm)
			L010Aceite("NCNCNF." + cPTerm,.T.)
			lRet := .F.
		EndIf
	EndIf

	If lRet .And. nI == 0
       lRet := .F.
	Endif
Endif         

If lRet

	If lDiscado
		ntTimeOut := (15 * 60) // TimeOut para trabalho = 15 min - TEF Discado
	Else
		ntTimeOut := (5 * 60) // TimeOut para trabalho = 5 min - TEF Dedicado
	Endif
	
	// Verifica a operacao deseja, se for uma opcao valida chama a funcao correspondente.
	If cOpera $ "VGCSXERBD" //Adicionada a operação CB
		// Variaveis estaticas que devem ser reincializadas nos processoas V, G, C, S, X e E
		cImpress := ""
		aTicket  := {}
		aConf    := {}
		nParCred := 0
		nParDeb  := 0
		nParDin  := 0
		nParCheq := 0
		nDin	 := 0
		nCartao  := 0
		nCheq    := 0
		nDebito  := 0
		nPre     := 0
		c030     := ""
		c006	 := ""
		c007	 := ""
	Endif
EndIf

If lRet
	If cOpera == "S"
		If lDiscado
			ntTimeOut := (30 * 60) // TimeOut para transacoes administrativas = 30 min - TEF Discado
			L1:=02
			C1:=28
			L2:=19
			C2:=48
			L3:=05
			nI:=0
			DEFINE MSDIALOG oDlg TITLE STR0096  From L1,C1 to L2,C2  of GetWNDDefault() 	// "Escolha a Bandeira"
			@ L3, 05  BUTTON STR0097	            SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=1,oDlg:End() ) When aTefDisc[1][5] .OR. aTefDisc[27][5] .OR. aTefDisc[36][5]	// "TecBan"
			L3+=20
			@ L3, 05  BUTTON STR0098 				SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=2,oDlg:End() ) When aTefDisc[2][5]  .OR. aTefDisc[9][5]  .OR. aTefDisc[23][5] .OR. aTefDisc[26][5] .OR. ;
																											   aTefDisc[35][5] .OR. aTefDisc[25][5] .OR. aTefDisc[34][5] .OR. aTefDisc[28][5] .OR. ;
																											   aTefDisc[37][5] .OR. aTefDisc[43][5] .OR. aTefDisc[48][5] .OR. aTefDisc[44][5] .OR. ;
																											   aTefDisc[49][5] .OR. aTefDisc[16][5] // "RedeCard/Visanet/Amex" 
			L3+=20
			@ L3, 05  BUTTON STR0099	            SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=5,oDlg:End() ) When aTefDisc[5][5] 						// "Banesecard"
			L3+=20
			@ L3, 05  BUTTON STR0100             	SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=6,oDlg:End() ) When aTefDisc[6][5] .OR. aTefDisc[29][5] .OR. aTefDisc[38][5]					// "HiperCard"
			L3+=20
			@ L3, 05  BUTTON STR0101             	SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=7,oDlg:End() ) When aTefDisc[7][5] .OR. aTefDisc[14][5]	// "CrediShop"
			L3+=20
			@ L3, 05  BUTTON STR0146             	SIZE 70, 20 OF oDlg PIXEL ACTION ( nI:=8,oDlg:End() ) When aTefDisc[47][5] .OR. aTefDisc[52][5]	// "GetNet"
			ACTIVATE MSDIALOG oDlg CENTERED
			If nI == 0
				lRet := .F.
			Endif
		Else
			ntTimeOut := (15 * 60) // TimeOut para transacoes administrativas = 15 min - TEF Dedicado
		Endif
		
		If lRet
			If ! L010Adm(nI)
				lRet := .F.
			Else
				// Faz a confirmacao no servidor TEF
				L010NCNCNF(.F.)			
			Endif
		EndIf
	
	ElseIf cOpera $ "VGC" //Venda - Consultas

		If EmpTy(aParcTef) .AND. cOpera == "V"
			lRet := .F.
			LjGrvLog( NIL , "TEF DISCADO - Parcelas não informadas - array aParcTef vazio!",  )
		Else
		
			For nParc := 1 To Len(aParcTef)
				If AllTrim( aParcTef[nParc][3] ) $ "CC|CD"
					If Empty(aParcTef[nParc][6])
						aParcTef[nParc][6] := "1"
					EndIf
					If AllTrim(cUltimaForma) <> AllTrim(aParcTef[nParc][3]) .OR. AllTrim(cUltimoID) <> AllTrim(aParcTef[nParc][6])
						cUltimaForma    := aParcTef[nParc][3]			
						cUltimoID 		:= aParcTef[nParc][6]
						
						If nSomaCred > 0
							aCartoes[Len(aCartoes)][2] := nSomaCred
							aCartoes[Len(aCartoes)][3] := "CC"
							nSomaCred := 0
						ElseIf nSomaDeb > 0
							aCartoes[Len(aCartoes)][2] := nSomaDeb
							aCartoes[Len(aCartoes)][3] := "CD"
							nSomaDeb := 0
						EndIf   
						
						If Len(aCartoes) > 0
							aCartoes[Len(aCartoes)][1] := nNumParcelas															// Armazena o numero de parcelas
							nNumParcelas := 1						   															// Reseta o numero de parcelas
						EndIf
						
						If nModulo == 23  
							aAdd(aCartoes, {0				, 0					, " "		, .F.					,;
											0				, AllTrim(aParcTef[nParc][4])	, " "		, DTEF_NAO_TRANSMITIDA	,;
											nI				, " "				, " "		, " "					,;
											" "				, cUltimoId})
						ElseIf nModulo == 5  .OR. nModulo == 12
						
							aAdd(aCartoes, {0				, 0					, " "		, .F.					,;
											0				, AllTrim(aParcTef[nParc][5])	, " "		, DTEF_NAO_TRANSMITIDA	,;
											nI				, " "				, " "		, " "					,;
											" "				, cUltimoId})		    	
						Endif
						
					Else
						nNumParcelas++
					EndIf
		
					If aParcTef[nParc][3] = "CC"
						nCartao 	+= aParcTef[nParc][2]
						nSomaCred	+= aParcTef[nParc][2]
						nParCred 	++
					ElseIF aParcTef[nParc][3] = "CD"
						nDebito 	+= aParcTef[nParc][2]
						nSomaDeb 	+= aParcTef[nParc][2]
						nParDeb 	++
					EndIf 
					
				Else
					If AllTrim(aParcTef[nParc][3]) == "CH"
						nCheq += aParcTef[nParc][2]
						nParCheq ++
					EndIf
					
					aAdd(aNaoCartao, aParcTef[nParc])    
				EndIf
			Next nParc
		EndIf
		
		// Verifica se foi utilizado cartao como pagamento
		If Len(aCartoes) > 0
			aCartoes[Len(aCartoes)][1] := nNumParcelas
		
			If nSomaCred > 0
				aCartoes[Len(aCartoes)][2] := nSomaCred
				aCartoes[Len(aCartoes)][3] := "CC"
			ElseIf nSomaDeb > 0
				aCartoes[Len(aCartoes)][2] := nSomaDeb
				aCartoes[Len(aCartoes)][3] := "CD"
			EndIf
	
			Lj010PendPag(aCartoes, lRecCel, lTemTEFOk)		// Remonta array de cartoes com base no que ja foi transmitido
	
			If LJSaveDTEF(aCartoes, lRecCel)

				If !LJProcDTEF(.F., .T.)	// Executa o processamento dos cartoes (somente solicitacoes de vendas)
					lRet := .F.
				EndIf
			Else
				lRet := .F.
			EndIf
		EndIf
		
		If lRet
			For nCount := 1 To Len(aNaoCartao)
				nNumCred := 0 
				
			    If AScan(aNaoCartao, { |x| x[3] == "CH" } ) > 0
		 			nNumCred := AScan(aNaoCartao, {|x| x[3] == "CC" .OR. x[3] == "CD"} )
			    EndIf
			    
				If cOpera == "V" .AND. (Substr(LjGetStation("TEFCONS"),3,1) == "S" .OR. Substr(LjGetStation("TEFCONS"),8,1) == "S") 
					If SuperGetMV("MV_INFCHEQ") .AND. ValType(aDados) == "A"
						If nNumCred == 0						
							If !MsgYesNo(STR0114 + CRLF + STR0115 + Alltrim(aDados[nParc][4]) , STR0116)      
								lRet := .F.
							Endif
							
							If lRet .And. !L010CrtChq("CH", aParcTef[nParc][2], {aParcTef[nParc][1], Alltrim(aDados[nParc][5]),;
							 	Alltrim(aDados[nParc][6]), Alltrim(aDados[nParc][7]), Alltrim(aDados[nParc][4])}, nI)
								lRet := .F.
							EndIf
						Else
							Aviso(STR0104, STR0112 + CRLF + STR0113 , {"Ok"})		// #"TEF Discado" #"A Consulta de cheque não sera realizada," #"pois existe venda a Credito ou Debito na mesma venda!" 
						EndIf
					Else
						If nNumCred == 0											
							If !MsgYesNo(STR0104, STR0116)								// #"Confirma consulta de cheque? " #"Venda com Cheque"
								lRet := .F.
							Endif
		
							If lRet .And. !L010CrtChq("CH", aParcTef[nParc][2], , nI)
								lRet := .F.
							Endif						
						Else
							Aviso( STR0104 , STR0112 + CRLF + STR0113 ,{"Ok"} )		// #"TEF Discado" #"A Consulta de cheque não sera realizada," #"pois existe venda a Credito ou Debito na mesma venda!" 
						EndIf
					Endif
					
					If !lRet
						Exit
					EndIf
				Endif
			Next nCount
		EndIf
			
		If lRet .And. (cOpera $ "GC")
			nOp:= 0
			DEFINE FONT oFnt NAME "Arial" BOLD
			DEFINE MSDIALOG oDlg FROM 84,71 TO 160,309 TITLE "Consulta de Cheque" PIXEL
			@ 2, 3 GROUP oGroup TO 27, 84 LABEL " Valor do Cheque: " OF oDlg PIXEL
			oGroup:SetFont(oFnt)
			@ 8,10 MSGET oCheq VAR nCheq SIZE 70,10 OF oDLG PIXEL ;
			 	PICTURE PesqPict("SL1","L1_CHEQUES",_PICTURE,nMoedaCor) VALID Lj010VlZer(nCheq) FONT oFnt
			DEFINE SBUTTON FROM	03,89 TYPE 1 ENABLE OF oDlg ACTION ( nOp := 1,oDlg:End() )
			DEFINE SBUTTON FROM 16,89 TYPE 2 ENABLE OF oDlg ACTION ( nOp := 2,oDlg:End() )
			ACTIVATE MSDIALOG oDlg CENTERED
			
			If nOp <> 1
				lRet := .F.
			Endif
			
			If lRet .And. nCheq > 0
				If !L010CrtChq("CH",nCheq,,nI)
					lRet := .F.
				Endif
			Endif
		Endif
		
	ElseIf cOpera $ "XE" //Cancelamento
		If !lTitulo
			If nModulo == 13 .OR. nModulo == 73
				nCartao := aDados[1]
			Else
				nCartao := SL1->L1_CARTAO
				nDebito := SL1->L1_VLRDEBI
			Endif
		Else
			If RTrim(SLV->LV_FORMA) == "CC"
				nCartao := SLV->LV_VALOR
			Else
				nDebito := SLV->LV_VALOR
			EndIf
		EndIf
		
		If (nDebito + nCartao) > 0
			DEFINE FONT oFntTela NAME "Arial" SIZE 08,17 BOLD
		EndIf
		
		nOp := 0
		If nDebito > 0
			DEFINE MSDIALOG oDlg TITLE "Cancelamento venda TEF" FROM 0,0 TO 7,50 OF GetWNDDefault()
				@ 7,005 SAY "Valor do cancelamento " SIZE 130,10 OF oDlg PIXEL FONT oFntTela
				@17,005 SAY "com cartão de débito:" SIZE 130,10 OF oDlg PIXEL FONT oFntTela
				
				@ 7,110 MSGET oDebito VAR nDebito SIZE 70,10 OF oDLG PIXEL ;
					FONT oFntTela ;
					PICTURE PesqPict("SL1","L1_VLRDEBI",_PICTURE,nMoedaCor);
					VALID Lj010VlZer(nDebito) WHEN .F. // o campo bloqueado, pois se trata do valor da venda

				@ 30,005 SAY "(Campo valor não pode ser alterado)" SIZE 130,10 OF oDlg ;
					PIXEL FONT oFntTela COLOR CLR_HRED						
				
			DEFINE SBUTTON FROM 40,110 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
			DEFINE SBUTTON FROM 40,140 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
			ACTIVATE MSDIALOG oDlg CENTER
			
			If (nOp <> 1)
				lRet := .F.
			Endif
		Endif
		
		If lRet .AND. nCartao > 0 
			nOp := 0
			DEFINE MSDIALOG oDlg TITLE "Cancelamento venda TEF" FROM 13,10 TO 20,60 OF GetWNDDefault()
				@ 7,005 SAY "Valor do cancelamento " SIZE 130,10 OF oDlg PIXEL FONT oFntTela
				@17,005 SAY "com cartão de crédito:" SIZE 130,10 OF oDlg PIXEL FONT oFntTela

				@ 7,110 MSGET oCartao VAR nCartao SIZE 70,10 OF oDLG PIXEL ;
					FONT oFntTela ;
					PICTURE PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor) ;
					VALID Lj010VlZer(nCartao) WHEN .F. // o campo bloqueado, pois se trata do valor da venda
				
				
								
				@ 30,005 SAY "(Campo valor não pode ser alterado)" SIZE 130,10 OF oDlg ;
					PIXEL FONT oFntTela COLOR CLR_HRED
					
			DEFINE SBUTTON FROM 40,110 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
			DEFINE SBUTTON FROM 40,140 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
			ACTIVATE MSDIALOG oDlg CENTER

			If (nOp <> 1)
				lRet := .F.
			Endif
		Endif
		
		If lRet
			If (nModulo == 13 .OR. nModulo == 73) .AND. ValType(aDados) == "A"
				aDadoTrn := aClone(aDados)
			EndIf
			
			If (nModulo == 13 .OR. nModulo == 73)  .AND. Len(aDadoTrn) > 0 
				If nDebito > 0 
					If !L010TipCnc(nDebito,aDadoTrn,nI)
						lRet := .F.
					Endif
				Endif
				
				If lRet .And. nCartao > 0
					If !L010TipCnc(nCartao,aDadoTrn,nI)
						lRet := .F.
					Endif
				Endif
			Else //rotina de cancelamento de venda
				If nCartao + nDebito > 0
					If !L010TipCnc(nCartao + nDebito,aDadoTrn,nI, lTemTEFOk, lTitulo)
						lRet := .F.
					Endif
				Endif
			EndIf
		EndIf
			
	ElseIf cOpera == "R" // Pre-Autorizacao
		nOp:= 0
		DEFINE MSDIALOG oDlg TITLE "Valor da pré autorização" FROM 13,10 TO 19,65 OF GetWndDefault()
		@ 7,005 SAY "Valor da pré autorização:" SIZE 130,10 OF oDlg PIXEL
		@ 7,130 MSGET oPre VAR nPre SIZE 70,10 OF oDLG PIXEL ;
				PICTURE PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor) VALID  Lj010VlZer(nPre)
		DEFINE SBUTTON FROM 30,110 TYPE 1 PIXEL ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg
		DEFINE SBUTTON FROM 30,140 TYPE 2 PIXEL ACTION ( oDlg:End(),nOp:= 2 ) ENABLE OF oDlg
		ACTIVATE MSDIALOG oDlg CENTER
		
		If nOp <> 1
			lRet := .F.
		Endif
		
		If lRet .And. nPre > 0
			If !L010CrtChq("PR",nPre,,nI)
				lRet := .F.
			Endif
			
			If lRet
				lRet := LImpTefDsc(nI)
			EndIf
		Endif
	ElseIf cOpera == "I" // Impressao dos cupons TEF
		// REIMPRESSAO BANESECARD
		lContinua := .T.
		If cTipTef == TEF_DISCADO .AND. cTipVenCon == "R"
			If nI == 5
				If !File("BANESE." + cPTerm)
					MsgStop("Não existe arquivo para reimpressão!")
					lRet		:= .T.
					lContinua	:= .F.
				Endif
				
				If lContinua
					aTicket := {}
					lRet	:= .T.
					cTicket	:= MemoRead( "BANESE." + cPTerm )
					nPos    := 0
					cVarTick:= ""
					lReimp	:= .T.
					While nPos < Len( cTicket )
						nPos ++
						nAsc := Asc( SubStr( cTicket, nPos, 1 ) )
						If nAsc == 10
							aAdd(aTicket, cVarTick)
							//HOMOLOGACAO: Imprimir a palavra reimpressao apos a 1º linha
							If lReimp .AND. !Empty(cVarTick) .AND. Len(aTicket) >=1
						 	    aAdd(aTicket,Space(14)+"REIMPRESSAO")
						 	    lReimp := .F.
							Endif		 		    
							cVarTick := ""
						Else
							cVarTick += SubStr( cTicket, nPos, 1 )
						Endif
					End
				EndIf
			Endif
		Endif		
		
		If lContinua
			lRet := LImpTefDsc(nI, cMsgCupom, nVlrTotal, lRecCel, , lConfNF)
		EndIf
		
	ElseIf cOpera == "F" // Desfazimento das transacoes
		If L010IsDirecao(nBandeira) .OR. L010IsPayGo(nBandeira) .OR. L010IsAuttar(nBandeira)
			LJProcDTEF(!lConfNF)
		EndIf
	ElseIf cOpera = "B" //Corespondente Bancario 
		nI	:= L010ATVDISC({})
		lRet := L010CBD(nI, cId800, oWsTrn, @aDadosTrn)  
	ElseIf cOpera = "D" //Recarga de Celular
		nI := L010ATVDISC({})
		lRet := L010RCD(nI, cId800, oWsTrn, @aDadosTrn) 
	Endif
EndIf

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³L010CrtChq³ Autor ³ Edney Soares de Souza ³ Data ³ 27.06.00 ³±±
±±³          ³          ³       ³                       ³      ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Venda pelo TEf Discado usando a operacao CHQ ou CRT        ³±±
±±³          ³ - CHQ  => Pedido de autorizacao para transacao com cheque  ³±±
±±³          ³ - CRT  => Pedido de autorizacao para transacao com cartao  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ L010CrtChq()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ cOrigem: "CH" - Cheque                                     ³±±
±±³          ³          "CC" - Cartão de Crédito                          ³±±
±±³          ³          "CD" - Cartão de Débito                           ³±±
±±³          ³          "PR" - Pré Aurotizaçào                            ³±±
±±³          ³ nValTot: Valor da transacao                                ³±±
±±³          ³ aCheque: {Data,Banco,Agencia,Conta,Cheque}                 ³±±
±±³          ³ nI     : Rede da Transacao 1- TecBan, 2 - RedeCard         ³±± 
±±³          ³                            3- VisaNet 4 - Amex             ³±± 
±±³          ³                            5- Banese  6 - HiperCard        ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010CrtChq(cOrigem, nValTot, aCheque, nI, cPrefixo001)

Local c000    	:= "000-000 = "
Local aMessage	:={}
Local lRet
Local cBotoesTEF
Local cMsg530	:= Space(12)

DEFAULT cPrefixo001 := ""

// PE para inclusao do codigo do cliente no campo ADM da transação Sitef (Válido somente para a versão desenvolvimento da BrasilTelecom)
If ExistBlock("LJTEFMSG")
	LjGrvLog(Nil, "Ponto de Entrada LJTEFMSG")
	cMsg530 := Subs(ExecBlock("LJTEFMSG",.F.,.F.),1,12)
	LjGrvLog(Nil, "Ponto de Entrada LJTEFMSG - Retorno :", cMsg530)
Endif

If cOrigem == "CH"
	aMessage :=  {C000 + "CHQ",;
	"001-000 = " + ALLTRIM(STR(nSeqArq,10)),;
	"003-000 = " + if(lDiscado,If(nValTot<1,If(nValTot<.10,"00","0"),"")+ALLTRIM(STR(nValTot*100,12)),StrTran(ALLTRIM(STR(nValTot,12,2)),".",","))}
	If ValType(aCheque) == "A"
		If Empty(c006)
			c006 := if(SA1->A1_TIPO=="F","F","J")
		Endif
		If Empty(c007)
			c007 := AllTrim(SA1->A1_CGC)
		Endif
		aadd(aMessage,"006-000 = " + c006)
		aadd(aMessage,"007-000 = " + c007)
		aadd(aMessage,"008-000 = " + StrZero(Day(aCheque[1]),2) + StrZero(Month(aCheque[1]),2) + StrZero(Year(aCheque[1]),4))
		aadd(aMessage,"033-000 = " + StrZero(Val(aCheque[2]),3))
		If Len(aCheque[3]) > 4
			aadd(aMessage,"034-000 = " + AllTrim(Substr(aCheque[3],1,4)))
			aadd(aMessage,"035-000 = " + AllTrim(Substr(aCheque[3],5,1)))
		Else
			aadd(aMessage,"034-000 = "+AllTrim(aCheque[3]))
		Endif
		If At("-",aCheque[4]) > 0 .OR. len(aCheque[4]) > 9
			If At("-",aCheque[4]) > 0
				aadd(aMessage,"036-000 = " + AllTrim(Substr(aCheque[4],1,At("-",aCheque[4])-1)))
				aadd(aMessage,"037-000 = " + AllTrim(Substr(aCheque[4],At("-",aCheque[4])+1,Len(aCheque[4]))))
			Else
				aadd(aMessage,"036-000 = " + AllTrim(Substr(aCheque[4],1,9)))
				aadd(aMessage,"037-000 = " + AllTrim(Substr(aCheque[4],10,1)))
			Endif
		Else
			aadd(aMessage,"036-000 = "+AllTrim(aCheque[4]))
		Endif
		If At("-",aCheque[5]) > 0 .OR. len(aCheque[5]) > 14
			If At("-",aCheque[5]) > 0
				aadd(aMessage,"038-000 = " + AllTrim(Substr(aCheque[5],1,At("-",aCheque[5])-1)))
				aadd(aMessage,"039-000 = " + AllTrim(Substr(aCheque[5],At("-",aCheque[5])+1,Len(aCheque[5]))))
			Else
				aadd(aMessage,"038-000 = " + AllTrim(Substr(aCheque[5],1,14)))
				aadd(aMessage,"039-000 = " + AllTrim(Substr(aCheque[5],15,1)))
			Endif
		Else
			aadd(aMessage,"038-000 = "+AllTrim(aCheque[5]))
		Endif
		
	Endif
	aadd(aMessage,"999-999 = 0")
Elseif cOrigem == "PR"
	aMessage :=  {C000 + "PRE",;
	"001-000 = " + ALLTRIM(STR(nSeqArq,10)),;
	"003-000 = " + if(lDiscado,If(nValTot<1,If(nValTot<.10,"00","0"),"")+ALLTRIM(STR(nValTot*100,12)),StrTran(ALLTRIM(STR(nValTot,12,2)),".",",")),;
	"999-999 = 0"}
Else
	If cOrigem == "CC"
		cBotoesTEF := "000010000000000000000000000000"
	else
		nFormaDeb := If(Len(aParcTEF)>1,2,If(aParcTEF[1][1]=dDataBase,0,1))
		Do Case
			Case nFormaDeb=0   // aVista
				If nI=6 //HiperCard
					cBotoesTEF := "10"
				Else
					cBotoesTEF := "000000000010000000000000000000"
				Endif
			Case nFormaDeb=1   // Pre- autorizazao
				If nI=6 //HiperCard
					cBotoesTEF := "00"
				Else
					cBotoesTEF := "000000000000100000000000000000"
				Endif
			Case nFormaDeb=2   // Parcelado
				If nI=6 //HiperCard
					cBotoesTEF := if(SE4->E4_JURCART == "1","12","11")
				Else
					cBotoesTEF := "000000000000001011000000000000"
				Endif
			OtherWise
				If nI=6 //HiperCard
					cBotoesTEF := "10"
				Else
					cBotoesTEF := "00000000001010111100000"
				Endif
		EndCase
	Endif
	If nI == 5		//BANESECARD
		aMessage := {"SP00000000C"+If(nValTot<1,If(nValTot<.10,"00","0"),"")+ALLTRIM(STR(nValTot*100,12))}
	Else
		aadd(aMessage,C000 + "CRT")
		aadd(aMessage,"001-000 = " + cPrefixo001 + ALLTRIM(STR(nSeqArq,10)))
		aadd(aMessage,"003-000 = " + if(lDiscado,If(nValTot<1,If(nValTot<.10,"00","0"),"")+ALLTRIM(STR(nValTot*100,12)),StrTran(ALLTRIM(STR(nValTot,12,2)),".",",")))
		If  lHomTEF	// HOMOLOGACAO
			AAdd(aMessage,"777-777 = teste REDES")
		EndIf			
		If nI = 6 //HiperCard
			aadd(aMessage,"501-001 = " + cBotoesTEF)
		EndIf
	    If !Empty(cMsg530)
			aadd(aMessage,"530-000 = " + cMsg530)
		Endif	
		aadd(aMessage,"999-999 = 0")
		Asort(aMessage)
	Endif
Endif
// ### "Efetuando solicitação TEF" ### "Ativando gerenciador padrão do TEF "
MsgRun( STR0073 , STR0074 , { || lRet := L010MArq(aMessage,nI)})
If lRet 
	lRet := L010LeImp(nValTot, nI, Nil, cPrefixo001)
Endif

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³L010Adm   ³ Autor ³ Edney Soares de Souza ³ Data ³ 01.08.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Geracao de transacao administrativa para o TEF discado     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ L010Adm()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010Adm(nI)
Local lRet := .F.

If nI == 5 	//BANESECARD
	// ### "Efetuando a Impressão do Movimento" ### "Cartão BANESECARD"
	MsgRun( STR0075 , STR0076 , {|| lRet := L010MArq( { "SPOOOOTTTTM"}, nI )})
Else
	// "Ativando gerenciador padrão do TEF "
	MsgRun( STR0079 ,,;
		{|| lRet := L010MArq( { "000-000 = ADM",;
								"001-000 = " + ALLTRIM(STR(nSeqArq,10)),;
								"999-999 = 0"}, nI)})
EndIf

If lRet
	lRet := L010LeImp(0,nI)
	If lRet
		lRet := LImpTefDsc(nI)
	Endif
Endif

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³L010TipCnc³ Autor ³ Edney Soares de Souza ³ Data ³ 27.06.00 ³±±
±±³          ³          ³       ³                       ³      ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cancelamento de Venda pelo TEF - Discado                   ³±±
±±³          ³ - CNC  => Cancelamento de venda efetuada por qualquer      ³±±
±±³          ³           meio de pagamento.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ L010TipCnc()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ nValor                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 29.10.01 ³ XXXXX³MMancio ³Para manter-se compatível com outros módulos³±±
±±³          ³      ³        ³do SIGA. Ao invés de pegar as informações da³±±
±±³          ³      ³        ³Tabela "SL1". Esta função passou a pegar  do³±±
±±³          ³      ³        ³array a aDados. Que por sua vez já foi abas-³±±
±±³          ³      ³        ³tecido com o próprio "SL1".                 ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TipCnc(nValor, aInfDados, nI, lTemTEFOk, lTitulo)

Local aDados		:= {}
Local cKeySL4		:= ""
Local nX			:= 0
Local aTEFConfOK	:= {}	//informacoes das TEF confirmadas que serao canceladas durante a venda.
Local lRet			:= .T.

Default nValor		:= Nil
Default aInfDados	:= Nil
Default nI			:= Nil
Default lTemTEFOk	:= .F.
DEFAULT lTitulo		:= .f.

If (nModulo==13 .OR. nModulo==73) .AND. ValType(aInfDados)=="A" .AND. Len(aInfDados)>0
	
	//inserido no aDados mais posições , para evitar erro abaixo insero manualmente 
	If Len(aInfDados) <= 7
		AAdd(aInfDados,nValor)
		AAdd(aInfDados,1)
	EndIf

	AAdd(aDados, aInfDados)

ElseIf !lTemTEFOk .and. !lTitulo
	// Devido aos Multiplos Cartoes, adiciona no array pela tabela SL4
	DbSelectArea("SL4")
	SL4->(DbSetorder(1))
	SL4->( DbSeek(xFilial("SL4") + SL1->L1_NUM ) )
	While !SL4->(Eof()) .AND. SL4->L4_FILIAL + SL4->L4_NUM == xFilial("SL4") + SL1->L1_NUM
		If !(Upper(AllTrim(SL4->L4_FORMA)) $ 'CH/CD/CC')
			SL4->(DbSkip())
			Loop
		Endif

		// Não considera registros que foram realizados através do Tef Manual
		If Empty(SL4->L4_AUTORIZ)
			SL4->(DbSkip())
			Loop
		EndIf

		nX := aScan( aDados, { |x| x[4] == Alltrim(SL4->L4_NSUTEF)})
		If nX == 0			
			Aadd(aDados,{0,0,AllTrim(SL4->L4_INSTITU),AllTrim(SL4->L4_NSUTEF),StrTran(dToc(sTod(SL4->L4_DATATEF)), "/", ""),SL4->L4_HORATEF,Upper(AllTrim(SL4->L4_FORMA)),SL4->L4_VALOR,1})
		Else
			aDados[nX][8] += SL4->L4_VALOR
			aDados[nX][9] += 1 //Qtde de parcelas
		EndIf
		SL4->(DbSkip())
	EndDo
ElseIf lTitulo
	Aadd(aDados,{0,0,AllTrim(SLV->LV_INSTITU),AllTrim(SLV->LV_NSUTEF),SLV->LV_DATATEF,SLV->LV_HORATEF,Upper(AllTrim(SLV->LV_FORMA)),SLV->LV_VALOR,1})

Else
	//renomeia TEFPEND para TEFOK para seguir o fluxo padrao de cancelamento
	LJRenDTEF()

	//recupera os dados das transacoes (TEFOK.TMP)
	aTEFConfOK := LJLoadDTEF(.T.)

	For nX := 1 to Len(aTEFConfOK)

		// CC|CD|CH E "CONFIRMADA
		If aTEFConfOK[nX][3] $ _FORMATEF+"|CH" .AND. aTEFConfOK[nX][8] == DTEF_CONFIRMADA

			Aadd( aDados,{	0					,;
							0					,;
							aTEFConfOK[nX][06]	,;
							aTEFConfOK[nX][07]	,;
							aTEFConfOK[nX][10]	,;
							aTEFConfOK[nX][11]	,;
							aTEFConfOK[nX][03]	,;
							aTEFConfOK[nX][02]	,;
							1					})
		Else
			Loop
		EndIf
	Next
EndIf

// envia as solicitacoes de cancelamento uma-a-uma	
For nX := 1 to Len(aDados)
	If lRet
		If lTemTEFOk
			lRet := L010CNTEFC(aDados[nX], nI)	//CaNcela Tef Confirmadas
		Else
			lRet := L010PCnc(aDados[nX], nI)
		EndIf
	EndIf
Next

Return lRet


//-----------------------------------------------------
/*/{Protheus.doc} L010PCnc
Função que processa o cancelamento (envia o CNC)
@param aDados
@param nI
@return lProssegue
@author michael.gabriel
@since 21/08/2017                                                   
/*/
//-----------------------------------------------------
static function L010PCnc(aDados, nI)

Local lRet
Local cBotoesTEF
Local aEnvio    	:= {}
Local cTipoFin		:= ""
Local cTipoParc		:= ""
Local cKeySL4		:= ""
Local nX			:= 0
Local nParcelas		:= 0
Local lUsaDisplay	:= !Empty(LjGetStation("DISPLAY")) 		// Verifica se a estacao possui Display
Local lProssegue	:= .T.
Local cTipoCartao	:= ""									// Tipo do Cartao (CC, CD ou Voucher)
Local lMvLjCDVOU	:= SuperGetMv("MV_LJCDVOU",, 0) == 1	// Define se utiliza venda com Voucher

Default aDados		:= Nil
Default nI			:= Nil

If lProssegue

	If nI == 5 	//BANESECARD
		If aDados[7] == "CC"
			cTipTrans := "CC1"
		ElseIf aDados[7] == "CD"
			cTipTrans := "CD1"
		Endif
	
		aEnvio := {"OD000000000"}
		// ### "Insira ou passe o cartão magnético pelo leitor" ### "Cartão BANESECARD"
		MsgRun( STR0077, STR0076, ;
		{|| lRet:=L010MArq( aEnvio,nI ), If(lRet, lRet:=L010LeImp(aDados[8],nI), .F.)})
		If lRet
			aEnvio := {"CP00000000"+cTipTrans+cBANESETril+StrZero(Val(aDados[4]),15,0)+Left(aDados[5],4)}
			// Eh necessario este Sleep para o BANESECARD nao se perder entre as transacoes de obtencao de dados e de cancelamento
			MsgRun( STR0078, STR0079, { || Sleep(10000)}) // ### "Efetuando solicitação de cancelamento TEF" ### "Ativando gerenciador padrão do TEF "
		Else
			lProssegue := .F.
		Endif

	Else

		aadd(aEnvio,"000-000 = CNC")
		aadd(aEnvio,"001-000 = " + AllTrim(STR(nSeqArq,10)))
		aadd(aEnvio,"003-000 = " + If(lDiscado,;
									If(aDados[8]<1,If(aDados[8]<.10,"00","0"),"")+ALLTRIM(STR(aDados[8]*100,12)),;
									StrTran(ALLTRIM(STR(aDados[8],12,2)),".",",")))
		aadd(aEnvio,"010-000 = " + Iif( Upper(aDados[3])=="HIPERCARD" .AND. Upper(Alltrim(aTefDisc[nI,2])) $ "HIPERTEF|TEF_DIAL",;
									"HCARD", allTrim(aDados[3])) )

		If aDados[7] == "CC" 
			cTipoCartao := "1"
		Else
			If lMvLjCDVOU
				cTipoCartao := Lj010GtOpc(aDados, 2)
			Else
				cTipoCartao := "2"
			EndIf
		EndIf

		cTipoFin := ""
		cTipoParc:= ""

		If aDados[9] == 1
			If aDados[7] == "CC"
				cTipoFin := "10"	// Credito a Vista
			Else
				cTipoFin := "20"	//Debito a Vista
			EndIf
			
			cTipoParc := "1"		//Tipo de Parcelamento : a vista
			
		ElseIf aDados[9] > 1
		
			If aDados[7] == "CC"
				cTipoFin := "11"	// Credito Parcelado
			Else
				cTipoFin := "22"	// Debito Parcelado
			EndIf
			
			cTipoParc := "3"		//Tipo de Parcelamento : parcelado pelo estabelecimento
		EndIf
		
		aadd(aEnvio,"011-000 = " + cTipoFin)
		aadd(aEnvio,"012-000 = " + AllTrim(aDados[4]))
		aadd(aEnvio,"022-000 = " + aDados[5])
		aadd(aEnvio,"023-000 = " + aDados[6])
		
		IF lCliente
			cBotoesTEF := IF(aDados[7]=="CC","000000100000000000000000000000","000000000000010000000000000000")
			aadd(aEnvio, "501-001 = " + cBotoesTEF)
		Endif
		
		aadd(aEnvio,"731-000 = " + cTipoCartao)
		aadd(aEnvio,"732-000 = " + cTipoParc)
		aadd(aEnvio,"999-999 = " + cRegFim)
	Endif
	
	If lProssegue	
		If lUsaDisplay
			DisplayEnv(StatDisplay(), "1C" + "Efetuando solicitacao de canc. TEF" )         
			DisplayEnv(StatDisplay(), "2C" + "Ativando gerenciador padrao do TEF")                             
		Endif
		
		LjMsgRun( STR0078, STR0079 , { || lRet := L010MArq( aEnvio,nI )}) // ### "Efetuando solicitação de cancelamento TEF" ### "Ativando gerenciador padrão do TEF "
		
		If ! lRet
			lProssegue := .F.
		Endif
		
		If lProssegue
		
		
			If ! L010LeImp(aDados[8]		, nI			, /*lLeSoStatusTXT*/, /*cPrefixo001 */	,;
 							/*cId800*/		,/*aDadosTrn*/	,/*cIDCart*/		,/*cFormaCart*/	,;
 							/*aCartoes */	,/*nIndaCrt*/	, .T.)
				lProssegue := .F.
			Endif
			
			If lProssegue
				// Faz a confirmacao no servidor TEF	
				L010NCNCNF(.F.)
			EndIf
		EndIf
	EndIf
	
	/*Precisa estar dentro do looping pois a impressão deve ocorrer por cancelamento 
	  (se fora imprime somente do ultimo cancelamento , o que nas vendas com TEF Multiplo
	  deve imprimir de todos os cartões)*/
	lProssegue := lProssegue .AND. LImpTefDsc(nI, Nil, Nil, Nil, .T./*lCancTEF2*/)		
EndIf

Return lProssegue

/*/{Protheus.doc} L010LeImp
Leitura do arquivo enviado pelo Gerenciador Padrao com o a resposta da solicitacao. 
@type  Function
@author Edney Soares de Souza
@since 04/07/00
@example L010LeImp()
/*/
Function L010LeImp(	nValor	, 	nI			, lLeSoStatusTXT	,	cPrefixo001	,;
 					cId800	, 	aDadosTrn	,	cIDCart			,	cFormaCart	,;
 					aCartoes,	nIndaCrt	, lCNC)
Local aRetorno	:= {}
Local aImpres	:= {}
Local cString	:= ""
Local c000		:= "" // Header
Local c001		:= "" // Identificacao
Local c003		:= "" // Valor
Local c006		:= "" //
Local c007		:= "" //
Local c009      := "" // Status da Transacao
Local c011      := "" // Codigo da Transacao efetuada
Local c010      := "" // Nome da Instituicao
Local c012		:= "" // NSU
Local c013		:= "" // Codigo de Autorizacao
Local c014      := "" // Numero de Lote
Local c017      := "" // Tipo de Parcelamento ("0" - Estabelecimento / "1" - Administradora)
Local c018      := "" // Quantidade de Parcelas
Local c022		:= "" // Data da Transacao
Local c023		:= "" // Hora da Transacao
Local c027		:= "" // Finalizacao
Local n028		:= 0  // Linhas do Cupom
Local c038		:= ""    
Local c040		:= "" // Nome da bandeira do cartao
Local nHanDisc
Local nSize
Local cRetorno
Local nRead
Local nAction
Local nPosAspa	:= 0
Local lItem     := .F.
Local nPos		:= 0
Local nParc		:= 0
Local cFileTmp
Local lTimeOut
Local lNumOpDif	:= .T.    //Compara o Numero de Requisição com o Numero de Resposta  
Local c801		:= "" //Registro 801 
Local nCopias	:= Max(LjGetStation("TEFVIAS"),1)	// Numero de vias impresso nos cupons
Local nX		:= 0								// Controla o numero de vias impressas nos cupons
Local nTamArray	:= 0								// Contem o tamanho do array
Local nRegATef	:= 0								// Controla se ja existe cumpom no array
Local n71x		:= 0
Local lTefMult	:= SuperGetMV("MV_TEFMULT", NIL, .F.)
Local lImpCanc	:= .F. 								//avalia se é a impressão de uma venda cancelada para imprimir corretamente o comprovante
Local lReImpTEF	:= .F.
Local aArCrt	:= {}								//Usado para gerar o novo acartoes
Local aNewImpres:= {}								//Impressao se estiver com o novo Pay&Go
Local lCorteIns := .F.

DEFAULT lLeSoStatusTXT := .F.
DEFAULT cPrefixo001 := ""
DEFAULT cId800		:= ""
DEFAULT aDadosTRN	:= {}
DEFAULT cIDCart		:= ""
DEFAULT cFormaCart	:= ""
DEFAULT aCartoes	:= {}
DEFAULT nIndaCrt	:= 0
DEFAULT lCNC		:= .F.  //Transação de cancelamento - não valida o texto do comprovante

If nI==5		// BANESECARD
	cFileTmp := cDirT_Rx + "status.txt"
	If ! File( cFileTmp )
		// ### "Aguardando resposta da rede TEF" ### "BANESECARD - Em comunicação"
		MsgRun( STR0068, STR0080 ,{ || lTimeOut := L010WaitWin( cFileTmp )})
		If lTimeOut
			Return (.F.)
		Endif
	Endif

	While .T.
		nHandisc := Fopen( cFileTmp, 0 )
		If (nHandisc == -1)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End

	While .T.
		nSize := FSeek( nHanDisc, 0, 2 )
		FSeek( nHanDisc, 0 )
		cRetorno := Space( nSize )
		nRead := FRead( nHanDisc, @cRetorno, nSize )
		if ! nRead == nSize .OR. ! (FError() == 0)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End

	While .T.
		FClose( nHandisc )
		If ! (Ferror() == 0)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End

	If ! lLeSoStatusTXT
		// Quando for a transação de CONFIRMACAO, NAO DEVEMOS APAGAR O status.txt!!!
		FErase(cFileTmp)
	Endif
	If cRetorno <> "00"
		MsgStop(BANESERESP(cRetorno), "TEF BANESECARD")
		Return(.F.)
	Endif
           
	If lLeSoStatusTXT							// Na 3a. perna da transacao financeira do BANESECARD, so eh gerado o status.txt
		Return(.T.)
	Endif
	cFileTmp := cDirT_Rx + "resposta.txt"
	
	// Aguarda 10 segundos somente para alertar o usuario enquanto faz a discagem
	// ### "Aguarde ... Estabelecendo conexão TEF discado BANESECARD..."
	LjMsgRun( STR0081,,{ || Sleep(10000)})
Else
	cFileTmp := cDirT_Rx + "INTPOS." + cPTermReq
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o numero de opração do arquivo de resposta é diferente  do de requisição ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While lNumOpDif
	aImpres  := {}
	aNewImpres:= {}
	cRetorno := ""
	aRetorno := {}
	n71x	 := 0

	If nITmp <> 5 .AND. !L010IsDirecao(nI) .AND. !L010IsPayGo(nI) .AND. !L010IsAuttar(nI)
	   	aEval( Directory( cDirT_Rx + "INTPOS.001"), { |aFile| If(aFile[ 1 ]<>'INTPOS.REI',FErase( cDirT_Rx + aFile[ 1 ]),) } )
	EndIf

	If ! File( cFileTmp )
		If L010WaitWin( cFileTmp )
			Return (.F.)
		Endif
	Endif
	
	While .T.
		nHandisc := Fopen( cFileTmp, 0 )
		If (nHandisc == -1)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End
	
	While .T.
		nSize := FSeek( nHanDisc, 0, 2 )
		FSeek( nHanDisc, 0 )
		cRetorno := Space( nSize )
		nRead := FRead( nHanDisc, @cRetorno, nSize )
		if ! nRead == nSize .OR. ! (FError() == 0)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End
	
	While .T.
		FClose( nHandisc )
		If ! (Ferror() == 0)
			nAction := L010ErroRede( 9 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End
	
	If nI == 5	//BANESECARD
		If "FALHA INTERFACE DE COMUNICACAO" $ cRetorno
			c009 := "01"
			c030 := cRetorno
		Endif
		If Substr(cRetorno,1,2) == "ST"													// Identificador da Mensagem
			// O ultimo arquivo nao deve ser apagado, no caso da transacao ST eh o resposta.txt
			c009 := Substr(cRetorno,11,2)												// Status da Interface
			If c009 > "00"
				c030 := Substr(cRetorno,17,Val(Substr(cRetorno,13,4)))                  // Resposta/Mensagem de Erro
			Endif
		ElseIf Substr(cRetorno,1,2) == "OD"												// Identificador da Mensagem
			FErase(cFileTmp)
			c009 := Substr(cRetorno,12,2)												// Status da Interface
			If c009 > "00"
				c030 := Substr(cRetorno,18,Val(Substr(cRetorno,14,4)))                  // Resposta/Mensagem de Erro
			Endif
			cBANESETril := PadR(AllTrim(Substr(cRetorno,18,Val(Substr(cRetorno,14,4)))),40)
		ElseIf Substr(cRetorno,1,2) == "SP" .OR. Substr(cRetorno,1,2) == "CP"			// Identificador da Mensagem
			FErase(cFileTmp)
			c009 := Substr(cRetorno,17,2)												// Status Fisico
			If c009 == "00"
				c009 := Substr(cRetorno,19,2)		   									// Status Logico
			Endif
			n028 := Val(Substr(cRetorno,21,4))
			If c009 == "00"
				cImpArq := ""
				For nPos := 25 To n028 Step 40
					AAdd(aImpres, Substr(cRetorno,nPos,40))
					cImpArq += Substr(cRetorno,nPos,40)+Chr(10)
				Next
				// Prepara a Reimpressao BANESECARD
				While .T.
					nHandle := FCreate("BANESE." + cPTerm)
					If (nHandle == -1)
						nAction := L010ErroRede( 1 )
						If (nAction == 1)
							MsgInfo( "Não será possível a reimpressão !!!" )
						Else
							Loop
						Endif	
					Else
						FWrite( nHandle, cImpArq )
						FClose( nHandle )
					Endif
					Exit
				End
				c001 := ""																// Identificacao da transacao (MV_TEFNSTR)
				c010 := "BANESECARD"													// Nome da Instituicao (e igual a Rede no TEF Discado)
				c011 := Substr(cRetorno,11,3)
				If Left(c011,2)=="CR"
					c011 := "CC"														// Codigo da transacao efetuada
				ElseIf Left(c011,2)=="DB"
					c011 := "CD"														// Codigo da transacao efetuada
				Endif
				nPos := At("NSU: ",cRetorno)
				c012 := Substr(cRetorno,nPos+5,6)										// NSU do Sitef (Numero do Documento do TEF - utilizado no Cancelamento)
				// o BANESECARD nao tem codigo de autorizacao - Gravar o NSU
				c013 := c012															// Codigo de Autorizacao
				nPos := At("Data: ",cRetorno)
				c022 := Substr(cRetorno,nPos+6,2)+;
						Substr(cRetorno,nPos+9,2)+;
						Substr(cRetorno,nPos+12,2)										// Data da transacao
				c023 := Substr(cRetorno,nPos+15,2)+;
						Substr(cRetorno,nPos+18,2)+;
						Substr(cRetorno,nPos+21,2)										// Hora da transacao
				c030 := "APROVADO NSU: "+c012
			Else
				c030 := Substr(cRetorno,25,n028)
			Endif
		Endif
	Else
		For nPos := 1 to len(cRetorno)
			Do Case
				Case Substr(cRetorno,nPos,3) == " = " .AND. ! lItem
					aadd(aRetorno,{rtrim(cString),""})
					cString := ""
					nPos += 2
					lItem := .T.
				Case Substr(cRetorno,nPos,1) == CHR(13)
					aRetorno[len(aRetorno)][2] := rtrim(cString)
					cString := ""
					lItem := .F.
				Case Substr(cRetorno,nPos,1) == CHR(10)
					// NÆo considera o CHR(10)
				otherwise
					cString += substr(cRetorno,nPos,1)
			EndCase
		Next npos
	
		For npos := 1  to len(aRetorno)
			Do Case
				Case aRetorno[npos][1] == "000-000"
					c000 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "001-000"
					c001 := aRetorno[npos][2]
					lNumOpDif := L010CompNOp(c001, cPrefixo001)
				Case aRetorno[npos][1] == "003-000"
					c003 := aRetorno[npos][2] // Valor
					If Val(c003) > 0
						nValor := Val(c003)
						If At(".",c003) == 0 .AND. At(",",c003) == 0
							nValor := nValor / 100
						Endif
					Endif
				Case aRetorno[npos][1] == "006-000"
					c006 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "007-000"
					c007 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "009-000"
					c009 := alltrim(aRetorno[npos][2])
				Case aRetorno[npos][1] == "010-000"
					c010 := aRetorno[npos][2]  // Rede
				Case aRetorno[npos][1] == "011-000"
					c011 := AllTrim(Str(Val(aRetorno[npos][2])))
					
					//Se Auttar na homologação, não tenho como pesquisar 'CANCELAMENTO' e 'REIMPRESSAO' para direcionar para aquele fim.
					If L010IsAuttar(nI)
						//Reimpressão == 00
						//Cancelamento == 99
						nPosAspa := At('"',aRetorno[npos][2])
						
						If "99" $ Upper(AllTrim(aRetorno[npos][2]))	//Cancelamento de Venda
							lImpCanc := .T.
						EndIf
	
						IF "00" $ Upper(AllTrim(aRetorno[nPos][2]))	//Reimpressão
							lReImpTEF := .T.
						EndIf					
						
						If nPosAspa > 0
							If Len(aRetorno[npos][2]) > 2
								aadd(aImpres,Subs(aRetorno[npos][2],2,Len(aRetorno[npos][2])-2))
							Else
								aadd(aImpres,"")
							Endif
						Else                                                      
							aadd(aImpres,aRetorno[npos][2])
						Endif
					EndIf				

				Case aRetorno[npos][1] == "012-000"
					c012 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "013-000"
					c013 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "014-000"
					c014 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "017-000"
					c017 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "018-000"
					c018 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "022-000"
					c022 := aRetorno[npos][2] // Data
					// Validação para o formato de data
					If SubStr(c022,5,2) == "20"
						c022 := DtoS(GetDtoDate(c022))
					EndIf
				Case aRetorno[npos][1] == "023-000"
					c023 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "027-000"
					c027 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "028-000"
					n028 := val(aRetorno[npos][2])
				Case Substr(aRetorno[npos][1],1,4) == "029-"
					nPosAspa := At('"',aRetorno[npos][2])
					If !L010IsAuttar(nI)
						If lCNC .OR. ( "CANCELAMENTO DE VENDA" $ Upper(AllTrim(aRetorno[npos][2])) .OR. ( L010IsPayGo(nI) .AND.  "ESTORNO " $ Upper(AllTrim(aRetorno[npos][2])) )  )
							lImpCanc := .T.
						EndIf
						
						IF "REIMPRESSAO" $ Upper(AllTrim(aRetorno[nPos][2]))
							lReImpTEF := .T.
						EndIf
					EndIf
						
					If nPosAspa > 0
						If Len(aRetorno[npos][2]) > 2
							aadd(aImpres,Subs(aRetorno[npos][2],2,Len(aRetorno[npos][2])-2))
						Else
							aadd(aImpres,"")
						Endif
					Else                                                      
						aadd(aImpres,aRetorno[npos][2])
					Endif
				Case aRetorno[npos][1] == "030-000"
					c030 := aRetorno[npos][2] // Autorização
				Case aRetorno[npos][1] == "038-000"
					c038 := aRetorno[npos][2]					
				Case aRetorno[npos][1] == "040-000"
					c040 := aRetorno[npos][2]
				Case aRetorno[npos][1] == "712-000"
					n71x := val(aRetorno[npos][2])
				Case Substr(aRetorno[npos][1],1,4) == "713-" //Via do Cliente
					nPosAspa := At('"',aRetorno[npos][2])
					
					If !L010IsAuttar(nI)
						If lCNC .OR. ( "CANCELAMENTO DE VENDA" $ Upper(AllTrim(aRetorno[npos][2])) .OR. ( L010IsPayGo(nI) .and. "ESTORNO " $ Upper(AllTrim(aRetorno[npos][2])) ) )
							lImpCanc := .T.
						EndIf
	
						IF "REIMPRESSAO" $ Upper(AllTrim(aRetorno[nPos][2]))
							lReImpTEF := .T.
						EndIf
					EndIf					
					
					If nPosAspa > 0
						If Len(aRetorno[npos][2]) > 2
							aadd(aNewImpres,Subs(aRetorno[npos][2],2,Len(aRetorno[npos][2])-2))
						Else
							aadd(aNewImpres,"")
						Endif
					Else                                                      
						aadd(aNewImpres,aRetorno[npos][2])
					Endif
				
				Case aRetorno[npos][1] == "714-000"
					If Val(aRetorno[npos][1]) > 0 .And. Len(aNewImpres) > 0
						Aadd(aNewImpres,Replicate("-",30)) //Insere uma quebra de linha entre as vias
					EndIf
					
				Case Substr(aRetorno[npos][1],1,4) == "715-" //Via do estabelecimento
					
					//Valida NFC-e / SAT / Venda Direta
					//Se estiver configurado, mando um corte entre as vias
					If LjEmitNFCe() .Or. LjFTVD() 
						If !lCorteIns .And. SuperGetMV("MV_FTTEFGU",,.T.) 
							Aadd(aNewImpres,TAG_GUIL_INI+TAG_GUIL_FIM)
							lCorteIns := .T. //Sinaliza somente um corte
						EndIf
					Endif
					
					nPosAspa := At('"',aRetorno[npos][2])
					
					If !L010IsAuttar(nI)
						If lCNC .OR. ( "CANCELAMENTO DE VENDA" $ Upper(AllTrim(aRetorno[npos][2])) .OR. ( L010IsPayGo(nI) .AND. "ESTORNO " $ Upper(AllTrim(aRetorno[npos][2]))) )
							lImpCanc := .T.
						EndIf
						
						IF "REIMPRESSAO" $ Upper(AllTrim(aRetorno[nPos][2]))
							lReImpTEF := .T.
						EndIf
					EndIf					
					
					If nPosAspa > 0
						If Len(aRetorno[npos][2]) > 2
							aadd(aNewImpres,Subs(aRetorno[npos][2],2,Len(aRetorno[npos][2])-2))
						Else
							aadd(aNewImpres,"")
						Endif
					Else                                                      
						aadd(aNewImpres,aRetorno[npos][2])
					Endif
				Case aRetorno[npos][1]  == "801-000"
					c801 := aRetorno[npos][2] 					
			EndCase
	
		Next npos

		// Forca saida do laco		                       
		lNumOpDif := .F.
		
		If c030 == "OPERACAO CANCELADA" .OR. (aRetorno[1][2] == "CRT" .AND. Empty(c012) .AND. Alltrim(c030) <> "APROVADA") 
			LjGrvLog( SL1->L1_NUM , "TEF - " + If(c000 == "ADM","Transação encerrada","Transação negada"), Alltrim(c030) )
			While ! LjMsg(Alltrim(c030),If(c000 == "ADM","Transação encerrada","Transação negada"),__STOP)
			End 
			L010TClear()
			Return (.F.)
		EndIf
	Endif
End

If Type("M->LQ_NUM") <> "U" .And. !Empty(LOG_TEF) .And. Len(aRetorno) > 0 
	LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT', " ")
	For nX := 1 to len(aRetorno) 
		LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT','leitura dos dados na gerado na pasta RESP : '+ aRetorno[nX][1])
		LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT','leitura dos dados na gerado na pasta RESP : '+ aRetorno[nX][2])	
	Next
	nX := 0 
	LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT', " ")	
EndIf

nITmp := nI

If Upper(Alltrim(aTefDisc[6, 4])) $ "HIPERTEF"
	c010 := "HCARD"
Else
	c010 := If(Alltrim(Upper(c010))=='HCARD','HIPERCARD',c010) // Rede
EndIf

// Homologacao TECBAN
//If c000 == "CHQ"
//	While ! LjMsg(Alltrim(c030),"Resultado da Consulta",__INFO)
//	End
//	If  ! Alltrim(c009) $ "P1/0"   // Fazer pergunta para P8
//		Return (.F.)
//	Endif
//	Return (.T.)
//Endif
             
nTamArray := Iif(!Empty(aTefDados[1][1]), Len(aTefDados)+1, 1) 

/*Variavel c009 pode retornar 3 bits, então
 foi tratado essas 3 possíbilidade      */
If c009 == "0" .OR. c009 == "00" .OR. c009 == "000"                       	
	// Verifica o Tipo de transação "CC" ou "CD"
	If Val(c011) >= 10 .AND. Val(c011) <= 12   // Cartao Credito		
		cFormaCart := "CC"
	ElseIf (Val(c011) >= 20 .AND. Val(c011) <= 22) .Or. (Val(c011) == 24)   // Cartao Debito
		cFormaCart := "CD"		
	EndIf		

	/* ----------------------------------------------------------------------------------------------------------------
		-Nas versões novas de Pay&Go o intpos retorna com a via do cliente 
		 e do estabelecimento portanto mando esta informação
		
		- No cancelamento não vem preenchido, vem somente o c029 então deve manter o TEFVIAS caso deseja mais de 1 via
	   ------------------------------------------------------------------------------------------------------------------*/
	If Len(aNewImpres) > 0 .And. n71x > 0
		aImpres := aNewImpres
		If nCopias > 1
			LjGrvLog(nil,"Atenção, conteudo do campo  LG_TEFVIAS será desconsiderado pois serão impressas as duas vias (cliente e estabelecimento)")
			Conout("Atenção, conteudo do campo LG_TEFVIAS será desconsiderado pois serão impressas as duas vias (cliente e estabelecimento)")
			nCopias := 1
		EndIf
	EndIf

	If nTamArray == 1
		aTefDados :=      {{"S",c022,c023,C001,C013,"","",c010,c012,"",c010,"","",aImpres,"",c017,c018,c040,cIDCart,cFormaCart}}
	Else  
		// Verifica se ja existe cartao no array 
		nRegATef := aScan( aTefDados, { |x| AllTrim(x[20])+Alltrim(x[19]) == cFormaCart+Alltrim(cIDCart) } )
		If nRegATef == 0
			Aadd(aTefDados,{"S",c022,c023,C001,C013,"","",c010,c012,"",c010,"","",aImpres,"",c017,c018,c040,cIDCart,cFormaCart})
		Endif	
	EndIf			

	If L010IsDirecao(nI) .AND. !Empty(c801) .and. !Empty(cId800)
		L010DTr801(cId800, c801, @aDadosTrn) //Trata o array de retorno da transação Direção
	EndIf	

Else      
	/*Qualquer retorno diferente de "0" e considerado erro, pelo manual*/
	LjGrvLog( SL1->L1_NUM , "TEF - " + If(c000 == "ADM","Transação encerrada","Transação negada"), Alltrim(c030) )
	While ! LjMsg(Alltrim(c030),If(c000 == "ADM","Transação encerrada","Transação negada"),__STOP)
	End 
	L010TClear()
	Return(.F.)
Endif

Do Case
	Case c011 $ "10;11;12;18;19;20;21;48;50;54;55;56;63"
		aTefDados[1][15] := "CC"
	Case c011 $ "20;22;21;24;32;33;34;35;36;39;40;42"	
		aTefDados[1][15] := "CD"
	Case c011 $ "112;114;116;119"
		aTefDados[1][15] := "CH"
EndCase

If cOpera $ "XE"
	aTefDados[1][6]	:= c001 // Identificacao da transacao de Cancelamento (MV_TEFNSTR)
	aTefDados[1][7]	:= c023 // Hora da transacao de Cancelamento
	aTefDados[1][12]:= c022 // Data da transacao de Cancelamento
Endif

/* O Numero de Linhas (n028) sera igual a zero, quando o SITEF(ConfServ.exe) estiver configurado para o servico "I",
 desabilitado. No Client quando esta configuracao esta feita o cliente nao esta respondendo.*/
If n028 == 0
	If !Empty(AllTrim(c030))
		While ! LjMsg(Alltrim(c030),"Transação aprovada",__INFO)
		End
	Endif
Else
	// Monta o cupom de acordo com o numero de vias definido no Cadastro de Estacoes
	For nX := 1 To nCopias
		For nPos := 1 to Len(aImpres)
			aadd(aTicket,aImpres[nPos])
			cImpress += aImpres[nPos]
	
			If L010IsDirecao(nI) .OR. L010IsPayGo(nI) .OR. L010IsAuttar(nI)
				cImpress += Chr(10) 
				If cId800 $ "103#104#106#107" .AND.  L010IsDirecao(nI)
					aTicket[Len(aTicket)] += Chr(10)
				EndIf
			EndIf
		Next

		// Imprime uma linha pontilhada entre as vias dos cartoes
		If lTefMult
			For nPos = 1 to 5              
				If nPos == 3	
					aadd(aTicket, Replicate("-",30) + Chr(10))
					cImpress += Replicate("-",30) + Chr(10)
				Else                                       
					aadd(aTicket, "" + Chr(10))
					cImpress += "" + Chr(10)			
				Endif	
			Next nPos  
		Endif

		//somente insere se for uma versão de Pay&Go que não tem os campos 713 e 715 não preenchidos
		If nCopias > 1 .And. n71x == 0
			// Poe um tracejado entre as cópias
			cImpress += Replicate("-",30) + Chr(10)
			aTicket[Len(aTicket)] += Replicate("-",30) + Chr(10)
		EndIf
	Next nX             
	             		
		//Salva o texto do cupom vinculado em arquivo
	If (L010IsDirecao(nI) .AND. !(cId800 $ "103#104#106#107")) .OR. L010IsPayGo(nI) .OR. L010IsAuttar(nI)
		
		If lImpCanc .Or. lReImpTEF

			// No arquivo de trans. ADM, essa opção (C012) pode não existir, portanto insiro um identificador
			If lReImpTEF .And. Empty(Alltrim(c012))  
				c012 := c001
			EndIf

			/* Mando gerar outro arquivo TEFOK, pois nesse momento 
			o comprovante que deve ser impresso é de cancelamento de venda */
			aArCrt := {}
			Aadd(aArCrt,{1,nValor,aTefDados[1][15],.T.,Val(c001),c010,c012,DTEF_CANCELADA,nI,c022,c023,c027,c013,"1"})
			LJSaveDTEF(aArCrt)			//salva o TEFPEND de acordo com o arquivo de cancelamento/reimpressao
			
			If c000 == "CNC"
				LJRenDTEF() 				//renomeia para TEFOK , para prosseguir e respeitar a alteração qdo lCancTEF2 == .T.
			EndIf
		Else
			If n028 > 0 .And. (Empty(AllTrim(c012)) .Or. (Val(AllTrim(c012)) == 0)) .And. !Empty(AllTrim(cImpress))
				c012 := c001	//preciso desse numero para gerar o numero certo do arquivo e achar as informações para imprimir
			EndIf
		EndIf
		
		LJ010SaveImp(c012, cImpress)
		//Quando tenho uma transação adm (nI == 2), o sistema espera essa variável preenchida para caso haja alguma impressão
		If nI <> 2
			aTicket := {}
		EndIf
		cImpress := ""
    Endif

	nSeqArq++
	PutMv("MV_TEFNSTR",AllTrim(Str(nSeqArq)))
	aadd(aConf,{ALLTRIM(STR(nSeqArq,10)),c010,c012,c027,nValor})

	/*
		Altera-se os valores do array pois se houver queda na impressão do TEF e precisar cancelar 
		a operação posteriormente preciso dos dados corretos, de acordo com o retorno do arquivo, para o cancelamento 
	*/
	If Len(aCartoes) > 0 .And. nIndaCrt > 0
		aCartoes[nIndaCrt][5]	:= nSeqArq
		aCartoes[nIndaCrt][6]	:= c010
		aCartoes[nIndaCrt][7]	:= c012
		aCartoes[nIndaCrt][12]	:= c027
	EndIf

	If !L010IsDirecao(nI) .AND. nI <> 5 	//BANESECARD
		nHandle := fCreate("ACEITE." + cPTerm)
		For nParc := 1 to len(aConf)
			If ! ValType(aConf[nParc][5]) == "N"
				aConf[nParc][5] := Space(1)
			Else
				aConf[nParc][5] := aConf[len(aConf)][5] := Alltrim(TransForm(aConf[len(aConf)][5],PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor)))
			Endif
			fWrite(nHandle,"NSTR="+aConf[nParc][1]+";"+aConf[nParc][2]+";"+aConf[nParc][3]+";"+aConf[nParc][4]+";"+aConf[nParc][5]+";")
		Next nParc
		fClose(nHandle)
		__CopyFile("ACEITE." + cPTerm,"NCNCNF." + cPTerm)
		__CopyFile("ACEITE." + cPTerm,"INTPOS." + cPTerm)
	Endif
	
	If lDiscado
		Sleep(4)
	Endif
Endif

Return .T.

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³L010MArq    ³ Autor ³ Edney S. de Souza   ³ Data ³ 05.07.00 ³±±
±±³          ³            ³       ³                     ³      ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄ--ÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ-ÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Monta os arquivos para o Tef Discado                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ L010MArq(conteudo)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGALOJA                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ DATA     ³ BOPS ³Prograd.³ALTERACAO                                   ³±±
±±³----------³------³--------³--------------------------------------------³±±
±±³ 12.11.01 ³ XXXXX³MMancio ³ A última versão do TECBAN ( TEF Discado ), ³±±
±±³          ³      ³        ³ requer um Time-Out maior.                  ³±±
±±³          ³      ³        ³ De 5 passou para 20.                       ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010MArq(aConteudo, nI)
Local nCont
Local nHandisc
Local nLoop    	:= 0
Local cFileTmp
Local cFileReq 
Local nIDTrans 	:= 0 // Numero do ID da transacao

If lDiscado
	cDirT_TX := LJGetStation("DIRTTX")
	cDirT_RX := LJGetStation("DIRTRX")
Endif   
L010TClear()
If nI == 5	// BANESECARD
	cFileTmp := cDirT_Tx + "pergunta.tmp"
	cFileReq := cDirT_Tx + "pergunta.txt"
Else
	cFileTmp := cDirT_Tx + "INTPOS." + cPTermTmp
	cFileReq := cDirT_Tx + "INTPOS." + cPTermReq
Endif
While .T.
	nHandisc := FCreate( cFileTmp )
	If (nHandisc == -1)
		nAction := L010ErroRede( 1 )
		If ! (nAction == 1)
			Loop
		Else
			Return (.F.)
		Endif
	Endif
	Exit
End

For nCont := 1 to Len(aConteudo)  
	// Captura o numero do ID enviado
	If "001-000" $ aConteudo[nCont]
		nIDTrans := Val(AllTrim(SubStr(aConteudo[nCont], 10, Len(aConteudo[nCont]))))
	EndIf    
	
	While .T.

		FWrite( nHandisc, aConteudo[nCont] + CHR(13) + CHR(10) )
		If ! (Ferror() == 0)
			nAction := L010ErroRede( 1 )
			If ! (nAction == 1)
				Loop
			Else
				Return (.F.)
			Endif
		Endif
		Exit
	End
Next nCont

While .T.
	FClose( nHandisc )
	If ! (Ferror() == 0)
		nAction := L010ErroRede( 1 )
		If ! (nAction == 1)
			Loop
		Else
			Return (.F.)
		Endif
	Endif
	Exit
End

While .T.
	if ! (FRename( cFileTmp, cFileReq) == 0)
		nAction := L010ErroRede( 2 )
		If ! (nAction == 1)
			Loop
		Else
			Return (.F.)
		Endif
	Endif
	Exit
End

nSeqArq++
PutMv("MV_TEFNSTR",AllTrim(Str(nSeqArq)))

If lDiscado .AND. !L010IsDirecao(nI) .AND. !L010IsPayGo(nI) .AND. !L010IsAuttar(nI)
	ntTimeout := 20 // TimeOut para verificar se esta carregado ou nao - 5 s
Else
	ntTimeout := 10 // TimeOut para verificar se esta carregado ou nao - 10 s
Endif

If nI <> 5 .AND. !L010IsDirecao(nI)
	For nLoop := 1 to If("NCN" $ aConteudo[1] .OR. "CNF" $ aConteudo[1],2,1)

		If !L010WaitWin( cDirT_Rx + "INTPOS." + cPTermResp )
			If "NCN" $ aConteudo[1].OR. "CNF" $ aConteudo[1]
				fErase("NCNCNF." + cPTerm)
				fErase("INTPOS." + cPTerm)
			Endif
			Exit
		Endif

		If "NCN" $ aConteudo[1] .OR. "CNF" $ aConteudo[1]
			If nLoop == 2
				If lDiscado
					MsgStop("Verifique se o Gerenciador Padrão "+aTefDisc[nI][1]+" está ativo.","Aceite não recebido")
				Else
					MsgStop("Verifique se o Cliente Sitef está ativo."+CHR(13)+CHR(10)+"Verifique o Status da Transaçao no Servidor Sitef.","Aceite não recebido")
				Endif
				Return (.T.)
			Endif
		Else
			If ! lDiscado
				MsgStop("Verifique se o Cliente Sitef está ativo.","Solicitação interrompida")
			Endif
			Return (.F.)
		Endif

	Next nLoop
Endif

If Type("M->LQ_NUM") <> "U" .And. !Empty(LOG_TEF) .And. Len(aConteudo) > 0 
	LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT', " ")
	For nI := 1 to len(aConteudo) 		
		LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT','Dados utilizado na geração do arquivo INTPOS 	'+ aConteudo[nI])	
	Next	
	LjWriteLog(LOG_TEF + M->LQ_NUM + '.TXT', " ")
EndIf

// Validacao do retorno para o TEF DIRECAO
If L010IsDirecao(nI)
	If L010WaitWin(cDirT_Rx + "INTPOS." + cPTermResp)
		Return (.F.)
	Else
		If !Lj010VldRet(nIDTrans, cDirT_Rx + "INTPOS." + cPTermResp)
			Return (.F.)
		EndIf
	Endif
EndIf                                     

If lDiscado
	ntTimeOut := (15 * 60) // TimeOut para trabalho = 15 min - TEF Discado
Else
	ntTimeOut := (5 * 60) // TimeOut para trabalho = 5 min - TEF Dedicado
Endif

Return .T.

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o    ³LImpTefDsc  ³ Autor ³Edney Soares de Souza³ Data ³ 19/09/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄ--ÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ-ÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Imprime o Cupom do TEF e confirma a transacao			  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LImpTefDsc(nI	, cMsgCupom, nVlrTotal, lRecCel, lCancTEF2, lConfNF)
Local lCancTef 			:= .F.					// Controla se ira ou nao cancelar a transacao TEF
Local lRet	   			:= .F.					// Retorno da funcao
Local lTentarNovamente 	:= .T.					// Tentar fechar o cupom novamente ?
Local lFechouCupom 		:= .F.					// Controla se o cupom fiscal foi fechado
Local nCount 			:= 0					// Contador
Local nBandeira 		:= L010GetGPAtivo()		// Bandeira TEF utilizada
Local lFtvdVer12		:= LjFTVD() 	//Verifica se é Release 11.7 - Compatibilização Venda Direta x Venda Assisitida

DEFAULT cMsgCupom 	 	:= ""					// Mensagem do fechamento do cupom fiscal
DEFAULT nVlrTotal	 	:= 0					// Valor total da venda
DEFAULT lRecCel			:= .F.					// Recarga de Celular
DEFAULT lCancTEF2		:= .F.
Default lConfNF			:= .F.

If L010IsDirecao(nBandeira) .Or. L010IsPayGo(nBandeira) .Or. L010IsAuttar(nBandeira)
	If nI == 2									
		/* Funcoes ADM TEF */	
		For nCount := 1 To Len(aTicket)
			aTicket[nCount] += Chr(10)
		Next nCount
	Else
		/* Carrega o texto dos cupons vinculados */ 
		aTicket := LJ010LoadImp(lCancTEF2)		
	EndIf                        	
Else
	/* Carrega o texto dos cupons vinculados */
	aTicket := LJ010LoadImp(lCancTEF2)
EndIf

LjGrvLog(Nil, "LImpTefDsc - Retorno aTicket", "Tamanho do aTicket: "+Alltrim(STR(Len(aTicket))) )	

//Caso o usuario cancele a transação e utilize o POS, então não precisa imprimir o comprovante
//de debito e credito, da transação que foi abortada.
//Obs. Aqui efetua o tratamento quando o usurio da esc na tela do PayGo e vai para o POS.
If ValType(aTicket) == "A" .AND. Len(aTicket) < 5 
	LjGrvLog(Nil, "LImpTefDsc - Tela do PayGo abortada pelo usuário ou aTicket menor que 5", STR(Len(aTicket)) )	
	Return (.T.) 
Endif

//Para o BANESECARD deve-se enviar a confirmacao antes da impressao
If nI == 5	// BANESECARD
	If ! L010NCNCNF (lCancTef) 
		Return (.F.)
	Endif
Endif

If !lRecCel .Or. ValType(cTipVenCon) <> "C"  
	cTipVenCon := "V"   
EndIf   

If !lFtvdVer12
	// Verifica se o cupom esta fechado   
	While lTentarNovamente
		lFechouCupom := (IFStatus(nHdlECF, "5", "") == 0) 
		If !lFechouCupom
			lFechouCupom := (IFFechaCup(nHdlECF, cMsgCupom , Nil , nVlrTotal) == 0)
		EndIf
		lTentarNovamente := !lFechouCupom .AND. MsgYesNo(STR0119) // "Tentar fechar o cupom fiscal novamente?"
	End

	If lFechouCupom
		c030 := ""
	EndIf
EndIf

If (!lFtvdVer12 .And. lFechouCupom) .Or. lFtvdVer12
	LJMsgRun ( If(Empty(c030), STR0082, c030), If(Empty(c030),, STR0082), { ||;
	 				lRet := L010TefImpr(aTicket , Nil	,L010IsDirecao(nBandeira) .OR. L010IsPayGo(nBandeira) .OR. L010IsAuttar(nBandeira), Nil	,;
	 									Nil		, Nil	,Nil												 ,lRecCel,;
										lCancTEF2, nI	, lConfNF)}) // ### "Aguarde a impressão do cupom TEF" ###
EndIf

If !lRet .And. !lFtvdVer12
	lCancTef := .T.                
Endif
	
If L010IsDirecao(nBandeira)
	If lCancTef
		If LJProcDTEF(.T.)
			LJDelDTEF(.T.)
		EndIf
	EndIf
//Para o BANESECARD deve-se enviar a confirmacao antes da impressao
ElseIf nI <> 5	// BANESECARD
	If ! L010NCNCNF (lCancTef)
		Return .F.
	Endif
EndIf

lRet := !lCancTef

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³ LOJA014T ºAutor  ³Edney S. Souza      º Data ³  28/09/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Chama o Gerenciador padrao do TEF discado para trans. PRE  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA 	 ³ BOPS ³Prograd.³ALTERACAO								      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 27.11.01 ³ XXXXX³MMancio ³"ESTA FUNÇÃO FOI ALTERADA P/ ATENDER A ESPE-³±±
±±³			 ³		³ 		 ³ CIFICAÇÃO   3.00 DO SITEF"                 ³±±
±±³          ³ 		³		 ³ Ao passar o primeiro parâmetro ao LOJA010T,³±±
±±³		     ³		³		 ³ como "R". O segundo parâmetro funcionará da³±±
±±³		 	 ³		³		 ³ seguinte forma:                            ³±±
±±³          ³ 		³		 ³ 0 » pedido de pré-autorização              ³±±
±±³          ³ 		³	     ³ 2 » pedido de estorno pré-autorização      ³±±
±±³          ³      ³        ³ 3 » pedido de captura pré-autorização      ³±±
±±³          ³      ³        ³ 4 » pedido de estorno da captura pré-autor ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja014T()
If lUsaTef
	If LjGetStation("TIPTEF") == "6"	
		LjRotTef()
	Else
		Loja010T( "R", "0" )
	EndIf	
Else
	Help(" ","1","NAOTEF")
Endif
Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³ LOJA019T ºAutor  ³Edney S. Souza      º Data ³  28/09/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Chama o Gerenciador padrao do TEF discado para trans. ADM  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja019T()

If lVer300
	ALERT("Função não disponível !!!"+Chr(10)+"Verifique no cadastro de estações;o tipo do TEF.")
Else
	If lUsaTef .AND. (lDiscado .OR. lCliente)
		Loja010T( "S" )
	Else
		Help(" ","1","NAOTEF")
	Endif
Endif

Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010TClearºAutor  ³Edney S. Souza      º Data ³  29/09/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Limpa os arquivos nos diretórios do TEF.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010TClear()
aEval( Directory( cDirT_Tx + "INTPOS.*"), { |aFile| FErase( cDirT_Tx + aFile[ 1 ] ) } )
aEval( Directory( cDirT_Rx + "INTPOS.*"), { |aFile| If(aFile[ 1 ]<>'INTPOS.REI',FErase( cDirT_Rx + aFile[ 1 ]),) } )
// Apaga os arquivos do 
If aTEFDisc[5][5]
	aEval( Directory( cDrvTEFDsc+aTEFDisc[5][2] + "*.*"), { |aFile| FErase( cDrvTEFDsc+aTEFDisc[5][2] + aFile[ 1 ] ) } )
	aEval( Directory( cDrvTEFDsc+aTEFDisc[5][3] + "*.*"), { |aFile| FErase( cDrvTEFDsc+aTEFDisc[5][3] + aFile[ 1 ] ) } )
Endif
Return NIL

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010NCNCNFºAutor  ³Edney S. Souza      º Data ³  29/09/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Envia o cancelamento ou a confirmação das transacoes TEF.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ lCancTef := .T. = Cancela, .F. = Confirma                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010NCNCNF (lCancTef)        
Local nI 	 := 1
Local nParc  := 1
Local cRet   := Space(1)
Local cValor := "" 
Local nCount :=	1
Local lContinua	:= .T.
Local nBandeira     := L010GetGPAtivo()					        // Controla a administradora que foi efetuada a transacao TEF baseado no aTefDisc
Local lAuttar		:= L010IsAuttar(nBandeira)					// Verifico se é gerenciador Auttar
Local lTentar		:= .T.   									// Tentar dentro do for/next
Local aTEFPend		:= nil										// Pendências TEF
   
If aConf <> NIL 
	nParc  := Iif(lCancTef, Iif(Len(aConf)>0,Len(aConf),1), 1)    // Se for cancelamento e passado somente a ultima transacao
	nCount := Iif(lCancTef, Len(aConf), 1)    					  // Se for cancelamento e passado somente a ultima transacao
Endif
	
// Para Banese nao confirmar a reimpressao
If !aConf == NIL .AND. !L010IsDirecao(nBandeira) 
	If !(cOpera=='S' .AND. !lCancTef .AND. Ascan(aTefDisc,{|X| cDrvTEFDsc+X[2] }) = 3)
		For nParc := Iif(lAuttar, 1, len(aConf)) to len(aConf)
			lTentar := .T.
			//Antes da última parcela, e se Auttar, verifico se ainda tem algum "PENDENTE"
			IF lAuttar .AND. nParc < Len(aConf)	//Se Auttar e não for a última parcela
				If aTEFPend = NIL
					//Faz a leitura do arquivo TEFPEND.TEMP localizado na pasta do smartclient
					aTEFPend := LJLoadDTEF()
				EndIf
				If !(aScan(aTEFPend, {|x| x[8]=="PENDENTE"}) > 0)
					lTentar := .F.
				EndIf
			EndIf
			
			If lTentar
				If lDiscado .And. len(aConf) > 0
					nI  := Ascan(aTefDisc,{|X| At(X[1], Alltrim(Upper(aConf[nParc][2]))) <> 0 .AND. File(cDrvTEFDsc+X[4])})
				Endif
				If nI == 5	//BANESECARD
					If !L010MArq({"CF00000000"+If(lCancTEF,"01","00")},nI)
						lContinua := .F.
					Endif
					If !L010LeImp(,nI,.T.)
						lContinua := .F.
					Endif
				Else
					If Len(aConf) > 0
						If !L010MArq({	"000-000 = " + if(lCancTef,"NCN","CNF")		, ;
										"001-000 = " + (aConf[nParc][1] := cValToChar(Val(aConf[nParc][1])+1))	, ;
										"010-000 = " + Iif(	Upper(aConf[nParc][2]) == "HIPERCARD" .AND. ;
															 ( "HIPERTEF" $ Upper(Alltrim(aTefDisc[nI,2])) .OR. ;
															   "TEF_DIAL" $ Upper(Alltrim(aTefDisc[nI,2])) ), "HCARD", aConf[nParc][2]) , ; 
										"012-000 = " + aConf[nParc][3]				, ;
										"027-000 = " + aConf[nParc][4]				, ;
										"999-999 = " + cRegfim}, nI)
							lContinua := .F.
						Endif
					Endif
					
					If lContinua
						If lCancTef
							nSeqArq++
							PutMv("MV_TEFNSTR",AllTrim(Str(nSeqArq)))
						EndIf
			
						fErase("NCNCNF." + cPTerm)
						fErase("INTPOS." + cPTerm)
					EndIf
				Endif
	                                  
				If lContinua .And. lCancTef
					If Len(aConf) > 0 .AND. ValType(aConf[nParc][5]) == "N"
						aConf[nParc][5] := Alltrim(TransForm(aConf[nParc][5],PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor)))
					Endif
	
					If Len(aConf) > 0	
						MsgInfo("Última transação TEF foi cancelada"+CHR(10)+;
								"Rede: "+aConf[nParc][2]+CHR(13)+CHR(10)+;
								IIF(Empty(aConf[nParc][5]) .OR. aConf[nParc][5] == "0,00", "", "NSU: "+aConf[nParc][3])+CHR(13)+CHR(10)+; 
								If(Empty(aConf[nParc][5]) .OR. aConf[nParc][5] == "0,00","",If(("AMEX"$aConf[nParc][2] .OR. "REDECARD"$aConf[nParc][2] .OR. "HIPERCARD"$aConf[nParc][2] .OR. aConf[nParc][5] == "0,00")  .AND. cOpera$"SXE","","Valor: "+aConf[nParc][5])),;
								"Atenção")
						LJDelDTEF(lDiscado)
					Endif			
					//Homologacao Seven PDV
					//Quando for a rede AMEX, o valor nao deve ser exibido na resposta
				Endif
			EndIf

			If !lContinua
				Exit
			EndIf
			
		Next nParc
	Else
		fErase("NCNCNF." + cPTerm)
		fErase("INTPOS." + cPTerm)
	Endif
	
	If lContinua
		SendToFore()
	EndIf
Endif

If lContinua
	fErase("ACEITE." + cPTerm)
	
	If lDiscado
		Sleep(4)
	Endif
Endif

Return lContinua

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³ LjMsg    ºAutor  ³Edney S. Souza      º Data ³  05/10/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Mostra a mensagem do TEF para o usuario.	                  º±±
±±º          ³ essa funcao mostra a janela e mata a cada 5 segundos, para º±±
±±º          ³ evitar que a aplicacao da visa esconda a janela.	          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cCaption=Mensagem, cTitle=Titulo, nType = 1-INFO, 2-STOP   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LjMsg( cCaption, cTitle ,nType)
Local oDlg
Local nWidth
Local oFont
Local nRight
Local oTimer
Local nOp := 0

DEFINE FONT oFont NAME "Arial" SIZE 6,14 BOLD

DEFAULT cCaption := "Aguarde por favor...", cTitle := "Siga Advanced Protheus", nType := 1

nRight := Max( Len( cCaption ), Len( cTitle ) ) + 16

DEFINE DIALOG oDlg FROM 0,0 TO 6,nRight  TITLE cTitle STYLE DS_MODALFRAME
oDlg:SetFont(oFont)
nWidth := oDlg:nRight - oDlg:nLeft
@ 3,0 Say xPadc(cCaption,nWidth) of oDlg PIXEL

oDlg:cMsg	:= cCaption

DEFINE TIMER oTimer INTERVAL 5000 ACTION oDlg:End() OF oDlg
oTimer:Activate()

DEFINE SBUTTON FROM 30,(nRight*3)-8 TYPE nType ACTION ( oDlg:End(),nOp:= 1 ) ENABLE OF oDlg

SendToFore()
ACTIVATE DIALOG oDlg CENTER

Return nOp==1

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010AceiteºAutor  ³Edney Souza         º Data ³  03/02/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se o aceite foi recebido e reenvia o NCNCNF       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³LOJA010T                                                    º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010Aceite(cFile,lCancel)

Local nParc := 0

If File(cFile)  
    lCancFile:= (Upper(Alltrim("NCNCNF." + cPTerm))==Upper(Alltrim(cFile))) .OR. ;
			    (Upper(Alltrim("INTPOS." + cPTerm))==Upper(Alltrim(cFile))) .OR. ;
                (Upper(Alltrim("ACEITE." + cPTerm))==Upper(Alltrim(cFile)))
    

	nHandle	:= fOpen(cFile)
	If nHandle == -1
		Aviso( "Atenção",, {"&Ok"},cFile, "Não foi possível abrir o arquivo" )
		Return (.F.)
	Endif
	nSize		:= FSeek( nHandle, 0, 2 )
	If nSize < 15
		fClose (nHandle)
		fErase(cFile)
	Else
		FSeek( nHandle, 0 )
		cRBuffer	:= Space( nSize )
		nRead		:= FRead( nHandle, @cRBuffer, nSize )
		FClose (nHandle)
		aConf		:= {}
		nIni		:= At("NSTR=",cRBuffer)
		While !nIni == 0
			aadd(aconf,{"","","","",""})
			cRBuffer := Substr(cRBuffer,nIni+5,Len(cRBuffer))
			For nParc := 1 to 5
				aConf[len(aConf)][nParc] := Substr(cRBuffer,1,at(";",cRBuffer)-1)
				If nParc == 1
					aConf[1][1] := cValToChar(Val(aConf[1][1])+1)  
				EndIf
				cRBuffer := Substr(cRBuffer,at(";",cRBuffer)+1,Len(cRBuffer))
			Next nParc
			nIni := At("NSTR=",cRBuffer)
		End
	Endif
	If !L010NCNCNF(lCancel)
		Return (.F.)
	Endif
Endif
Return .T.

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010AskImpºAutor  ³Edney S. Souza      º Data ³  28/09/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Solicita ao usuário confirmação para reimprimir cupom TEF. º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ xOpt := Opcao escolhida (passar por referencia)            º±±
±±º          ³ xOpt := .F. - Nao faz a pergunta de tentar novamente.      º±±
±±º          ³ nRet := Resultado da ultima impressao                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAnalista  ³ Data/Bops/Ver ³Manutencao Efetuada                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºMarcio L. ³23/03/06³8.11  ³ Criado um parametro para fazer a           º±±
±±º			 ³				 ³ reimpressao do cupom TEF caso de algum     º±±
±±º			 ³				 ³ problema.                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010AskImp(xOpt, nRet, lTef)

Local lRet := .F.

DEFAULT lTef := .F.

// Tudo o que for diferente de zero é erro antes o sistema so tratava nRet == 1
// Nao colocar opcao de espera para nao gerar "Sonda" no servidor das Administradoras
// Retornar somente se for sensor..
If nRet <> 0
	If ValType(xOpt) == "N" // .AND. !lVer300
        If lTef
			xOpt := Aviso( STR0002 ,STR0064, {STR0046, STR0047},, STR0044 ) // "Atenção" ### "Foi detectado erro de comunicação com a impressora. Confirma reimpressão do compravante TEF?" ### {"&Sim","&Não"} ### "Impressora não responde"
			If xOpt == 1
				lRet := .T.
			EndIF
        Else
			xOpt := if(Aviso( STR0002, STR0045, {STR0046, STR0047},, STR0044 )=1,2,0) // ### "Atenção" ### "Tentar imprimir novamente ?" ### {"&Sim","&Não"} ### "Impressora não responde"
			If xOpt == 1
				lRet := .T.
			EndIF
		Endif
	Else
        If ValType(xOpt) == "N"
           xOpt := 3
        Endif   
		Aviso( STR0002, STR0048, {STR0017}) // "Atenção" ### "Existem problemas com sua impressora fiscal" ### "&OK"
	Endif
Endif
Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³Loja012T()   ³ Autor ³ Luiz Vergari		³ Data ³ 04/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada da Funcao Consulta TEF 	 		 			      ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja012T(lImprime)
Local lConsulta  :=.T.           
DEFAULT lImprime := .T.

Private aParcTef := {}         

If lUsaTef .AND. cTipTEF $ TEF_SEMCLIENT_DEDICADO+";"+TEF_COMCLIENT_DEDICADO+";"+TEF_DISCADO
	Loja010T( "C" ,,,@lConsulta,lImprime )
Else
	Help(" ","1","NAOTEF")
Endif

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Funcao    ³ Loja013S()  ³ Autor ³ Aline C. do Vale   ³ Data ³ 28/11/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada da Funcao Reimpressao Tef						  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja013S()
Private aTefMult := {}

// So tem reimpressao para TEF SEM CLIENT DEDICADO e BANESECARD DISCADO
If lUsaTef .AND. (cTipTEF = TEF_SEMCLIENT_DEDICADO .OR. cTipTEF = TEF_DISCADO)
	Loja010T( "I" , "R" )
Else
	Help(" ","1","NAOTEF")
Endif

Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³Loja011T()   ³ Autor ³ Luiz Vergari		³ Data ³ 04/07/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada da Funcao Cancelamento Manual	 			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros|ExpL1 =Indica se eh tratamento para recebimento de titulo   ³±±
±±³          |ExpA2 =Array temporaria utilizada para impressao TEF.       ³±±
±±³          |ExpL3 =Indica se foi chamado pelo processo de baixa(LOJA701)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³                         ATUALIZACOES SOFRIDAS                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³           Motivo da Alteracao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Adrianne F. ³09.08.05³085243³Trocada a chamada da funcao Lj010Autor por³±±
±±³            ³        ³      ³LjProfile(17) - A funcao anterior aceitava³±±
±±³            ³        ³      ³apenas a senha do Administrador, a nova   ³±±
±±³            ³        ³      ³funcao aceita senha do Administrador ou de³±±
±±³            ³        ³      ³Supervisor autorizado a efetuar um cance- ³±±
±±³            ³        ³      ³mento manual de TEF (Conforme configurado ³±±
±±³            ³        ³      ³no cadastro de caixas).                   ³±±
±±³Hanna       ³20/12/05³087653³Retorna valor logico ao final da funcao,  ³±±
±±³            ³        ³      ³para tratamento do retorno                ³±±
±±³Marcio L.   ³03/07/06³100896³Inserido o parametro para cancelamento de ³±±
±±³            ³        ³      ³transacao TEF, em baixa de titulo.        ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Loja011T( lTitulo, aTefTmp, lBaixa )

Local oDlg1
Local cSerie     	:= Space(TamSx3("L1_SERIE")[1])
Local cNota      	:= Space(TamSx3("L1_DOC")[1])
Local nOpc       	:= 0
Local nParc            
Local lChkSLV		:= ChkFile("SLV")				// Verifica se possui o arquivo SLV
Local oDlg											// Objeto de tela
Local lVenda		:= .T.							// Variavel que indica qual cancelamento foi selecionado
Local lRetorn		:= .F.							// Controla o retorno da funcao

Private aParcTef	:= {}
Private aTefMult	:= {}
Private lTefMult	:= SuperGetMV("MV_TEFMULT", NIL, .F.)
Private nTotal     	:= 0
Private aTefDados   := {}
Private aTefChq		:= {}										//Bco,Age,CC,Chq,Valor

DEFAULT lTitulo 	:= .F.
DEFAULT aTefTmp     := {}
DEFAULT lBaixa      := .F.

If lFiscal
	cSerie := LjGetStation("SERIE")
Endif

If !lBaixa .AND. lChkSLV
	If L010SelCanc(@lVenda, @oDlg)
		lTitulo := !lVenda
	Else
		Return(.F.)
	Endif
Endif


If lUsaTef .AND. cTipTEF $ TEF_SEMCLIENT_DEDICADO+";"+TEF_COMCLIENT_DEDICADO+";"+TEF_DISCADO
	If LjProfile(17) // Senha do Administrador ou do Supervisor
		If lTitulo
			
			lRetorn := Loja010T(	"E", Nil, Nil, Nil, ;
									Nil, Nil, Nil, Nil, ;
									lTitulo, @aTefTmp, lBaixa )
			If !lVer300 .AND. Len(aTefTmp) = 0				
				If  lTefMult
					aTefTmp := {{ /*1*/,/*2*/ ,/*3*/ , /*4*/,/*5*/ ,/*6*/, {{}}  }}
					aTefTmp[01, 07, 01] := Array(12)
					aTefTmp[1][7][1][6] := aTail(aTEFDados)[05]
					aTefTmp[1][7][1][7] := aTail(aTEFDados)[03]
					aTefTmp[1][7][1][12] := aTail(aTEFDados)[02]
				Else
					aTefTmp := {{}}
					aTefTmp[01] :=  Array(12)
					aTefTmp[1][6] := aTail(aTEFDados)[05]
					aTefTmp[1][7] := aTail(aTEFDados)[03]
					aTefTmp[1][12] := aTail(aTEFDados)[02]
				EndIf	
			EndIf					
						
		Else
			L011TTela(@cSerie, @cNota, @nOpc, @oDlg1)
			If nOpc == 1
				DbSelectArea("SL1")
				SL1->(DbSetOrder(2))
				If SL1->(DbSeek(xFilial("SL1") + cSerie + cNota, .T. ))
				   	If !Empty(SL1->L1_ORCRES)
						SL1->(DbSetOrder(1))
						If !SL1->(DbSeek(SL1->L1_FILRES+ SL1->L1_ORCRES,.T.))
							Aviso( "Atenção", "Não foi encontrado, pedido relacionado a este cupom fiscal", {"&Ok"},, "Pedido não Localizado" )
							Return NIL
						Endif
				   	Endif

					If lTefMult

						//VERIFICANDO SE EFETUOU OPERAÇÃO TEF				
						If SL1->L1_VENDTEF <> "S"
							Aviso( "Atenção", "Documento não finalizado por operação TEF", {"&Ok"},, "Impossível Cancelar" )
							Return(NIL)
						Endif
						If !SL1->L1_FORMPG$"CC|CD|CH"
							// A entrada pode ter sido em outra forma de pagamento R$, CO, VA, FI
							// Pesquisa todas as parcelas e localiza uma parcela TEF (CC, CD ou CH)
							DbSelectArea("SL4")
							SL4->(DbSetorder(1))
							SL4->(DbSeek( xFilial("SL4") + SL1->L1_NUM ))
							nParc := 0
							While SL4->L4_FILIAL == xFilial("SL4") .AND. SL4->L4_NUM == SL1->L1_NUM .AND. !SL4->(Eof())
								If AllTrim(SL4->L4_FORMA) $ "CC|CD|CH"
									nParc := 1
									Exit
								Endif
								SL4->(DbSkip())
							End
							
							// Se nao achou nenhuma parcela TEF na venda
							If nParc == 0
								Aviso( "Atenção", " Forma de pagamento não efetuada por operação TEF!", {"&Ok"},, "Form.Pgto :"+Alltrim(SL1->L1_FORMPG) )
								Return(NIL)
							Endif
						Endif
						If Val(SL1->L1_DOCCANC)>0
							Aviso( "Atenção", "Transação TEF já cancelada", {"&Ok"},, "Canc: "+Alltrim(SL1->L1_DOCCANC) )
							Return(NIL)
						Endif   
						//Cancelamento de Garantia de Cheque TecBan
						If AllTRim(SL1->L1_FORMPG) == "CH" .AND. At(__TECBAN,Upper(AllTrim(SL1->L1_INSTITU)))<>0
							//Buscando os dados do cheque para enviar a transação
							aTefChq	:= {}
							DbSelectArea("SEF")
							DbSetOrder(3) //EF_FILIAL+EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO+EF_NUM+EF_SEQUENC
							If DbSeek(xFilial("SEF") + cSerie + cNota )
							   aTefChq := {	SEF->EF_BANCO	,;
							   				SEF->EF_AGENCIA	,; 
							   				SEF->EF_CONTA	,; 
							   				SEF->EF_NUM     ,;
							   				SEF->EF_VALOR    }
							Endif
						Endif
					Endif
					
					If ExistBlock("LJ011AC")
						LjGrvLog(Nil, " Ponto de Entrada LJ011AC - Parametros: ", { cSerie, cNota } )
						
						If ! ExecBlock("LJ011AC", .F., .F., { cSerie, cNota })
							LjGrvLog(Nil, " Ponto de Entrada LJ011AC - Retorno Falso ")
							Return Nil
						Endif
						
						LjGrvLog(Nil, " Ponto de Entrada LJ011AC - Retorno Verdadeiro ")
					Endif
						
					If Loja010T("E")
						If lTefMult
							DbSelectArea("SL4")
							SL4->(DbSetOrder(1))
							If SL4->(DbSeek( xFilial("SL4") + SL1->L1_NUM ))
								nParc:=0
								While SL4->L4_FILIAL == xFilial("SL4") .AND. SL4->L4_NUM == SL1->L1_NUM .AND. !SL4->(Eof())
									nParc++
							        If (nPos := Ascan(aTefMult,{|x| Ascan(x[10],StrZero(nParc,2))>0 }))<>0
			                			aTefDados:=aClone(aTefMult[nPos][7])
					 					RecLock("SL4",.F.)
										REPLACE SL4->L4_DOCCANC With aTefDados[1][6]
										REPLACE SL4->L4_DATCANC With aTefDados[1][12]
										REPLACE SL4->L4_HORCANC With aTefDados[1][7]
										SL4->(MsUnLock())
									Endif	
									SL4->(DbSkip())
								End	
							Else
								RecLock("SL1",.F.)
								REPLACE SL1->L1_DOCCANC With aTefDados[1][6]
								REPLACE SL1->L1_DATCANC With aTefDados[1][12]
								REPLACE SL1->L1_HORCANC With aTefDados[1][7]
								SL1->(MsUnLock())
							Endif
						Else
							RecLock("SL1",.F.)
							SL1->L1_DOCCANC := aTefDados[1][6]
							SL1->L1_DATCANC := aTefDados[1][12]
							SL1->L1_HORCANC := aTefDados[1][7]
							SL1->(MsUnLock())
						Endif	
						
						If ExistBlock("LJ011DC")
							LjGrvLog(Nil, " Ponto de Entrada LJ011DC - Parametros: ", { cSerie, cNota } )
							ExecBlock("LJ011DC", .F., .F., { cSerie, cNota })
							LjGrvLog(Nil, " Ponto de Entrada LJ011DC - Retorno ")
						Endif
						
						lRetorn		:= .T.
					Else
						MsgAlert("Cancelamento não pode ser finalizado com sucesso!","Atenção")
					Endif
				Else
					MsgAlert("Documento não foi localizado, impossível efetuar o cancelamento!","Atenção")
				Endif
			Endif
		Endif
	Endif
Else
	lRetorn := .T.
	Help(" ","1","NAOTEF")
Endif

Return lRetorn

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Função    ³LJDadosCh    ³ Autor ³ Mauro Mancio       ³ Data ³ 14/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Armazena os dados do cheque                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := LJDadosCh( [ <ExpA1> ] )                      	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ [ <ExpA1> ] - Elemento 1 -> Número do Banco                ³±±
±±³Opcionais ³             - Elemento 2 -> Número da Agência              ³±±
±±³          ³             - Elemento 3 -> Número da Conta Corrente       ³±±
±±³          ³             - Elemento 4 -> Número do Cheque               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna       ³ ExpL1 -> Retorna um array com os dados armazenados...  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso          ³ LOJA010T                                               ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJDadosCh( cBan,cAge,cCon,cChe )
Local nTamEF_BAN := TamSx3("EF_BANCO")[1]   	// Tamanho do campo nTamEF_BAN
Local nTamEF_AGE := TamSx3("EF_AGENCIA")[1]		// Tamanho do campo nTamEF_AGE
Local nTamEF_CON := TamSx3("EF_CONTA")[1]		// Tamanho do campo nTamEF_CON
Local nTamEF_NUM := TamSx3("EF_NUM")[1]			// Tamanho do campo nTamEF_NUM


Static aRetorno
if cBan<>Nil .AND. cAge<>Nil .AND. cCon<>Nil .AND. cChe<>Nil
	aRetorno:={}
	aadd(aRetorno,cBan)
	aadd(aRetorno,cAge)
	aadd(aRetorno,cCon)
	aadd(aRetorno,cChe)
else
	if aRetorno==Nil
		aRetorno:={}
		aadd(aRetorno, Space( nTamEF_BAN ))
		aadd(aRetorno, Space( nTamEF_AGE ))
		aadd(aRetorno, Space( nTamEF_CON ))
		aadd(aRetorno, Space( nTamEF_NUM ))
	Endif
Endif
Return aRetorno

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³LjConsCPF ºAutor  ³Mauro Mancio        º Data ³  03/09/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Solicita ao usuário qual é a forma de consulta do cartao.  º±±
±±º          ³ (Magnetico, Nao magnetico ou pelo CPF (Infocard)           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º04/10/01  ³Mauro Mancio -  Caso o cliente, nao esteja munido de seu 	  º±±
±±º          ³              cartao Infocard.                              º±±
±±º          ³o Sistema ira fazer uma consulta de seus dados junto ao     º±±
±±º          ³provedor EMS/CCS, através do número do seu CPF			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LjConsCPF(cRet)
Local nOpd		:= 0
Local _aDados   := {}
Local aVar      := {}
Local nI
Local cRetorno	:= ''

Private _cCPF    := Space(11)
Private _nRotina := 0, oDlgCon
//Salvando as variaveis pois qdo usa dentro de uma trn, tem que retornar os valores 
aadd(aVar,{nParCred,'nParCred'})
aadd(aVar,{nParDeb ,'nParDeb' })
aadd(aVar,{nParDin ,'nParDin' })
aadd(aVar,{nParCheq,'nParCheq'})
aadd(aVar,{nCheq   ,'nCheq'   })
aadd(aVar,{lCCs    ,'lCCs'    })
aadd(aVar,{ctCodNet,'ctCodNet'})
aadd(aVar,{ctEntDoc,'ctEntDoc'})
aadd(aVar,{ctDados, 'ctDados' })
aadd(aVar,{ctCodNet,'ctCodNet'})
aadd(aVar,{ctEntDoc,'ctEntDoc'})

cRet:=If(cRet==nil,'C','A')
While .T.
	DEFINE MSDIALOG oDlgCon TITLE "Consulta do cartão" FROM 13,21 TO 20,64 OF GetWNDDefault()
	@ 06,12 SAY		"CPF para consulta:"	SIZE 130,10 OF oDlgCon PIXEL
	@ 06,88 MSGET	_cCPF  SIZE 055,10 OF oDLGCon PIXEL PICTURE "@R 999.999.999-99" VALID ! EMPTY( _CCPF )
	DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlgCon:End() ) ENABLE OF oDlgCon
	DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=2,oDlgCon:End() ) ENABLE OF oDlgCon
	ACTIVATE MSDIALOG oDlgCon CENTERED
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Texto extraido do documento de Especificação da interface do PDV                      ³
   //³                                                               edição 1.7 - 08/01/2002³
   //³Código Rede	Indica que a transação é da EMS . Assume o valor ´36´.                  ³
   //³Tipo de Consulta	'01'- Consulta Valor por Numero de          Documento CCS           ³
   //³                                '02'- Consulta Extrato CCS por cartão                 ³
   //³                                '03'- Consulta Extrato por CPF                        ³
   //³                                '04'- Consulta Genérica                               ³
   //³                                '05'- Consulta Formato Livre                          ³
   //³                                                                                      ³
   //³Saida da Consulta 	'V' - Saída da consulta em vídeo                                ³
   //³                      'I' - Saída da consulta na impressora                           ³
   //³                      'S' - Saída da consulta do sistema                              ³
   //³                                                                                      ³
   //³Dados da Consulta	Os dados variam dependendo do Tipo de Consulta. Vide tabelas abaixo.³
   //³	                                                                                    ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpd=1 .AND. !Empty(_cCPF)
		_aDados:={"36"}       			// Codigo da Rede EMS= 36
		AAdd(_aDados,"04")  			// Tipo da Consulta   - '04' - Consulta Generica
		sDado008 := "001"   			// Codigo da consulta - '001'- Situacao do cliente
		sDado008 += "02"      			// Tipo de entrada    - '04' - Numero do CPF e Data inicio 
		sDado008 += PadL(AllTrim(_cCPF),11,"0")
		//sDado008 += Str(Year(dDatabase),4)+StrZero(Month(dDatabase),2)+ StrZero(Day(dDatabase),2)
		//sDado008 += "1"
		AAdd(_aDados,sDado008)
		L010CCSCO(@_aDados,.F.)
        If cRet=='C'
	        cRetorno:=Substr(_aDados[1],23,16)
		Else
	    	cRetorno:=_aDados
		Endif    
	ElseIf nOpd=2
        cRetorno:='*'
	Endif
	Exit
End
// restaurando os valores
For nI:=1 to len(aVar)
    &(aVar[nI][2]):=aVar[nI][1]
Next nI

Return cRetorno


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010Bloco1   ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ "ESTA FUNÇÃO FOI CRIADA PARA ATENDER A ESPECIFICAÇÃO   3.00³±±
±±³          ³ DO SITEF"             									  ³±±
±±³          ³ Armazena as informações do "Serviço 1" para posterior      ³±±
±±³          ³ tratamento.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ [ <ExpA1> ] - Elemento 1 -> Retorno da Consulta BIN        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010Bloco1( cBuffer, cModalidade )
aBlocoResp[1][ 1]:= Substr( cBuffer, 1, 4) // rede destino
aBlocoResp[1][ 2]:= Substr( cBuffer, 5, 5) // bandeira do cartao
aBlocoResp[1][ 3]:= Substr( cBuffer,10, 1) // valida embosso
aBlocoResp[1][ 4]:= Substr( cBuffer,11, 4) // codigo validacao
aBlocoResp[1][ 5]:= Substr( cBuffer,15, 1) // tipo senha
aBlocoResp[1][ 6]:= Substr( cBuffer,16, 1) // tx servico
aBlocoResp[1][ 7]:= Substr( cBuffer,17, 2) // nr min parcela
aBlocoResp[1][ 8]:= Substr( cBuffer,19, 2) // nr. max parcela
aBlocoResp[1][ 9]:= Substr( cBuffer,21, 4) // perc.max.parcela (taxa de serviço)
aBlocoResp[1][10]:= Substr( cBuffer,25, 8) // dt lim pre-data
aBlocoResp[1][11]:= Substr( cBuffer,33, 8) // dt. lim da prim parcela
aBlocoResp[1][12]:= Substr( cBuffer,41, 1) // captura CSEG
aBlocoResp[1][13]:= Substr( cBuffer,42, 1) // garantia pre-dat
aBlocoResp[1][14]:= Substr( cBuffer,43, 1) // transacao com chip
// Alimentar aBlocoResp[3] e aBlocoResp[2] com a modalidade do cartao.
// Quando ha situacoes onde so retorna o Bloco 1, estava dando erro.
If cModalidade=="C"
	aBlocoResp[3][ 1]:= "C"
ElseIf cModalidade=="D"
	aBlocoResp[2][ 1]:= "D"
Endif
Return (.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³L010Bloco2   ³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ ESTA FUNCAO FOI CRIADA PARA ATENDER A ESPECIFICACAO  3.00  ³±±
±±³          ³ DO SITEF              									  ³±±
±±³          ³ Armazena as informações do "Serviço 2" para posterior      ³±±
±±³          ³ tratamento.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ [ <ExpA1> ] - Elemento 1 -> Retorno da Consulta BIN        ³±±
±±³          ³               Elemento 2 -> Modalidade C = CRÉDITO         ³±±
±±³          ³                                        D = DÉBITO          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010Bloco2( cBuffer, cModalidade )
Local i
If cModalidade="D"
	aBlocoResp[2][ 1]:= "D"
	aBlocoResp[2][ 2]:= Substr( cBuffer, 1, 1) //  Venda a vista
	aBlocoResp[2][ 3]:= Substr( cBuffer, 2, 1) //  Venda pre-datada
	aBlocoResp[2][ 4]:= Substr( cBuffer, 3, 1) //  Venda parc com 1a.parce a vista
	aBlocoResp[2][ 5]:= Substr( cBuffer, 4, 1) //  Venda parcelada sem entrada
	aBlocoResp[2][ 6]:= Substr( cBuffer, 5, 1) //  Venda a vista forcada
	aBlocoResp[2][ 7]:= Substr( cBuffer, 6, 1) //  Venda CDC
	aBlocoResp[2][ 8]:= Substr( cBuffer, 7, 1) //  Venda parcelada com Juros
	aBlocoResp[2][ 9]:= Substr( cBuffer, 8, 1) //  Venda parcelada sem Juros
	aBlocoResp[2][10]:= Substr( cBuffer, 9, 1) //  Cancelamento
	aBlocoResp[2][11]:= Substr( cBuffer,10, 1) //  Consulta CDC
	aBlocoResp[2][12]:= ""
	aBlocoResp[2][13]:= ""
	aBlocoResp[2][14]:= ""
	FOR i:=1 to Len(aBlocoResp)
		aBlocoResp[3][i] := ""
	NEXT i
Else
	aBlocoResp[3][ 1]:= "C"
	aBlocoResp[3][ 2]:= Substr( cBuffer, 1, 1) // Venda a Vista
	aBlocoResp[3][ 3]:= Substr( cBuffer, 2, 1) // Venda parcelada com juros pelo estabelecimento
	aBlocoResp[3][ 4]:= Substr( cBuffer, 3, 1) // Venda parcelada com juros pela administradora do cartao
	aBlocoResp[3][ 5]:= Substr( cBuffer, 4, 1) // Venda pro rata a vista
	aBlocoResp[3][ 6]:= Substr( cBuffer, 5, 1) // Venda pro rata parcelada
	aBlocoResp[3][ 7]:= Substr( cBuffer, 6, 1) // Cancelamento
	aBlocoResp[3][ 8]:= Substr( cBuffer, 7, 1) // Pre-autorizacao
	aBlocoResp[3][ 9]:= Substr( cBuffer, 8, 1) // Consulta Venda parcelada
	aBlocoResp[3][10]:= Substr( cBuffer, 9, 1) // Cancelamento de Pre-autorizacao
	aBlocoResp[3][11]:= Substr( cBuffer,10, 1) // Captura de pre-autorizacao
	aBlocoResp[3][12]:= Substr( cBuffer,11, 1) // Consulta AVS
	aBlocoResp[3][13]:= ""
	aBlocoResp[3][14]:= ""
	FOR i:=1 to Len(aBlocoResp)
		aBlocoResp[2][i] := ""
	NEXT i
Endif
Return (.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010IsCashBack³ Autor ³ Mauro Mancio      ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcaofoi criada para atender as especificacoes 3.00   ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ Verifica se a transação possui verificação de Cash Back    ³±±
±±³          ³ (Compre &Saque).                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010IsCashBack()
If cTipVenCon == "DV" .AND. aBlocoResp[2][2]="2"  // a Vista
	Return .T.
ElseIf cTipVenCon == "DR" .AND. aBlocoResp[2][3]="2" // Pré-datada
	Return .T.
ElseIf cTipVenCon == "DA" .AND. (aBlocoResp[2][8]="2" .OR. aBlocoResp[2][9]="2" .OR. aBlocoResp[2][7]="2") // Parcelada
	Return .T.
Endif
Return .F.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010SolicDados³ Autor ³ Mauro Mancio      ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcaofoi criada para atender as especificacoes 3.00   ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ Após a consulta BIN, verifica se há necessidade de consis- ³±±
±±³          ³ tir os valores.                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010PreAutoriz( ) 			                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Valor Lógico                            		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function L010SolicDados(nQtdGet,cModalidade,nformaDeb,lApresenta)
nQtdGet:=20
IF 	aBlocoResp[1][6]<>"0" .AND. lApresenta
	nQtdGet += 6
Endif
IF aBlocoResp[1][12]<>"0"
	nQtdGet += 6
Endif
IF cModalidade="D" .AND. aBlocoResp[1][13]<>"0"
	If nformaDeb=1
		nQtdGet += 12
	Endif
Endif
IF L010IsCashBack() .AND. lApresenta
	nQtdGet += 6
Endif
Return (nQtdGet<>20)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010Dimens    ³ Autor ³ Mauro Mancio      ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ nFator                       	                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³                                                  		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function L010Dimens(nFator)
Return	(((nFator - 4)/2)+1)*2


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010VerCSEG  ³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcao foi criada para atender as especificacoes 3.00  ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ Verifica se o código de segurança, digitado é válido.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010VerCSEG( ) 						              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> transação efetuada com sucesso ou não			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010VerCSEG(cSEG)

If ( Alltrim(cSeg) $ "0;1" ) 
	Return .T.
Else
	If Val(cSeg)>99 .AND. Val(cSeg)<=99999
		Return .T.
	Endif
Endif
MsgInfo( 'Codigo de Segurança inválido')

Return .F.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010VerNumCar³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcao foi criada para atender as especificacoes 3.00  ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ Verifica se o número do cartão digitado é válido.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010VerNumCar( ) 					              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> transação efetuada com sucesso ou não			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static function L010VerNumCart( cCartao )
Local lResult := .T.
If lVer300
	If Empty(cCartao)
		MsgStop("É obrigatório o preenchimento do número do cartão !")
		lResult := .F.
	ElseIf Len(AllTrim(cCartao))>19
		MsgStop("Número do Cartão maior que 19 dígitos !")
		lResult := .F.
	Endif
Endif
Return (lResult)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010VerTax   ³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcao foi criada para atender as especificacoes 3.00  ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ Verifica se a taxa digitada é válida.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010VerTax( ) 					                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> transação efetuada com sucesso ou não			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010VerTax( nTx, nVlrTotal )
Local nValorCalc
If nVlrTotal<>Nil
	If aBlocoResp[1][ 9]<>Nil
		If Val(aBlocoResp[1][ 9]) > 0
			nValorCalc := ( nVlrTotal * ( Val(aBlocoResp[1][ 9])/100) / 100)
			If (nTx > nValorCalc)
				Aviso( "Atenção", "Excede Taxa", {"&Ok"})
				Return .F.
			Endif
		Endif
	Endif
Endif
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ L010BIN     ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Esta funcao foi criada para atender as especificacoes 3.00  ³±±
±±³          ³do SITEF                                                    ³±±
±±³          ³ O SITEF, retorna no Serviço 1, as transações habilitadas do³±±
±±³          ³ cartão. Caso a transação seja uma venda ou pré-autorização ³±±
±±³          ³ a variável lApresenta deve ser verdadeira.  			   	  ³±±
±±³          ³ Caso a transação seja um Cancelamento esta variável deve   ³±±
±±³          ³ ser Falsa.                                  			   	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010BIN( [ <ExpA1> ] ) 						  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parâmetros³ [ <ExpA1> ] - Elemento 1 -> Apresenta Transação            ³±±
±±³Opcionais ³ 			                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Tr.efetuada com sucesso ou não                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Bin(lApresenta,nX)

Local oDlg
Local nSaque   	:= 0
Local nLinha   	:= -5
Local nLinhaBox	:= 0
Local nGarantia	:= 0
Local nServico 	:= 0
Local nFormaDeb
Local cOrigem
Local nOpd		:= 1
Local nParcela  
Local dData1    
Local cMsgDados	:= "Cartão "
Local lSintVisu	:= If(Type("lVisuSint")<>"U",lVisuSint,.F.)	//Tratamento para a nova identificação do cartão no Multi-TEF

If Type("aParcTef") == "U"
   aParcTef := {}
Endif   

If lTefMult .AND. nX<>nil
   If cOpera=="E"
	   cMsgDados+=AllTrim(Subst(aTefMult[nX][2],7))+" Doc.: "+AllTrim(Str(Val(aTefMult[nX][7][3])))
   Else	   
	   If lSintVisu
	   	   //Nova mensagem com ID do Cartão		
	       cMsgDados += AllTrim(Subst(aTefMult[nX][2],1,4))
	   Else
	   	   //Mensagem com os 4 Ultimos Dígitos do Cartão
	       cMsgDados += AllTrim(Subst(aTefMult[nX][2],8))+If(Val(aTefMult[nX][2])>0," Final "+Subst(aTefMult[nX][2],1,4),"")
	   Endif		       
   Endif	  
Endif

nX:=If(nX==Nil,0,nX)
nParcela  :=If(lTefMult.AND.nX<>0,aTefMult[nX][5],Len(aParcTef))
dData1:=dDataBase
If Len(aParcTef)>0
   dData1    :=If(lTefMult.AND.nX<>0,aTefMult[nX][6][1],aParcTef[1][1])
Endif   
// Inicializar o Valor de Saque
ctCashBack:=""
If !Empty(aBlocoResp[3][1])
	cOrigem:="C"
ElseIf !Empty(aBlocoResp[2][1])
	cOrigem:="D"
Endif
ctGarantia	:=	""
//ctGarantia	:=	Nil"G:2"
ctCVV2		:=	Space(10)

// nFormaDeb= 2 -> Venda parcelada
// nFormaDeb= 1 -> Venda Pre-datada
// nFormaDeb= 0 -> Venda Vista
nFormaDeb := If(nParcela>1,2,If(dData1=dDataBase,0,1))
If L010SolicDados(@nLinhaBox, cOrigem, nFormaDeb, lApresenta)
	DEFINE MSDIALOG oDlg TITLE cMsgDados FROM 13,21 TO nLinhaBox,65 OF GetWNDDefault()
	//  Verifica se há necessidade de digitar o código de segurança...
	IF  aBlocoResp[1][12]<>"0"
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Digite 0 se não possui" SIZE 90, 10 OF oDlg PIXEL
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Digite 1 se está ilegível" SIZE 90, 10 OF oDlg PIXEL
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Informe o número do CSEG:" SIZE 90,10 OF oDlg PIXEL
		@ nLinha,90 MSGET ctCVV2 SIZE 55,10 OF oDlg PIXEL PICTURE "99999"  Valid L010VerCSEG(ctCVV2)
	Endif
	//  Verifica se há necessidade de digitar se a transação é Com ou Sem garantia...
	IF cOrigem="D" .AND. aBlocoResp[1][13]<>"0" .AND. nFormaDeb=1   // Pre
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Digite 1 para Tr. Com Garantia" SIZE 90, 10 OF oDlg PIXEL
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Digite 2 para Tr. Sem Garantia" SIZE 100, 10 OF oDlg PIXEL
		@ nLinha,90 MSGET nGarantia SIZE 55,10 OF oDlg PIXEL PICTURE "9" Valid nGarantia>=1 .AND. nGarantia<=2
	Endif
	//  Verifica se há necessidade de digitar a Tx de Serviço...
	IF 	aBlocoResp[1][6]<>"0" .AND. lApresenta
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Taxa de Serviço:"  SIZE 90,10 OF oDlg PIXEL
		@ nLinha,90 MSGET nServico SIZE 55,10 OF oDlg PIXEL PICTURE "@E 9,999,999.99" Valid L010VerTax( nServico, If(lTefMult.AND.nX<>0,aTefMult[nX][4],nTotal ))
	Endif
	IF L010IsCashBack() .AND. lApresenta
		nLinha := nLinha + 15
		@ nLinha,005 SAY "Compre & Saque:" SIZE 90, 10 OF oDlg PIXEL
		@ nLinha,90 MSGET nSaque SIZE 55,10 OF oDlg PIXEL PICTURE "@E 999,999.99" VALID nSaque>=0
	Endif
	nLinha := nLinha + 30
	
	DEFINE SBUTTON FROM nLinha,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
	DEFINE SBUTTON FROM nLinha,120 TYPE 2 PIXEL ACTION ( nOpd:=2,oDlg:End() ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
	
	// Formata os valores digitados...
	IF aBlocoResp[1][12]<>"0"
		IF Upper(Substr(ctCVV2,1,1)) == "I"
			ctCVV2 := "CSEG:I"
		ElseIf Upper(Substr(ctCVV2,1,1)) == "N"
			ctCVV2 := "CSEG:N"
		Else
			ctCVV2 := "CSEG:"+AllTrim(ctCVV2)
		Endif
	Endif
	IF nSaque>0
		//ctCashBack := L010ValStr( nSaque )
		ctCashBack := "T:"+L010ValStr( nSaque )
	Endif
	If nServico>0
		ctTaxa :=  L010ValStr( nServico )
	Endif
	If nGarantia>0
		ctGarantia := "G:"+Str(nGarantia,1)
	Endif
Endif
ctCVV2:=If(Empty(ctCVV2),"",ctCVV2)

Return (nOpd = 1)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ L010RedeDest³Autor  ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ "ESTA FUNÇÃO FOI CRIADA PARA ATENDER A ESPECIFICAÇÃO   3.00³±±
±±³          ³ DO SITEF"             									  ³±±
±±³          ³ Nas transações, pode ser enviado a Rede Destino. A tabela  ³±±
±±³          ³ com as administradoras, pode ser encontrada em:            ³±±
±±³          ³ SX5->X5_TABELA="L9"                         			   	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ L010BIN( [ <ExpA1> ] ) 						  	          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010RedeDest()
ctRedeDest := ""  						// Código da Administradora válido para a SoftwareExpress
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Quando não estivermos trabalhando com cartão, o código da rede deve ser gerado em branco, desta forma        ³
//³o próprio Sitef se encarrega, através de sua tabela de roteamento a gerar a inf. apropriada, pois não temos  ³
//|algumas Adm. cadastradas no sistema, como por exemplo a SERASA                                               |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(ctCartao)
	DbSelectArea("SAE")
	DbSetOrder(1)
	DbSeek(xFilial("SAE") + ctCartao,.T.)
	If !Empty(SAE->AE_REDE)
		ctRedeDest:=PadL(SAE->AE_REDE,4,"0")
	Endif
	If !Empty(LOG_TEF)
		LjWriteLog( LOG_TEF + SL1->L1_NUM + '.TXT', 'TEF - Alterou a Rede Destino '+ ctRedeDest )
	Endif
Endif
RETURN (.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010PreAutori³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ "ESTA FUNÇÃO FOI CRIADA PARA ATENDER A ESPECIFICAÇÃO   3.00³±±
±±³          ³ DO SITEF"             									  ³±±
±±³          ³ Efetua Pré Autorização e Estorno da Pré-Autorização        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010PreAutoriz( ) 			                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> transação efetuada com sucesso ou não			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010PreAutoriz(cTipo)
Local lRet				:= .T.
Local nTipCar			:= 2
Local oDlg
Local nOpd				:= 1
Local aRet
Local lContinua	 		:= .T.
Local lLerPin			:= .F.
Local nTentar			:= 0
Local oVlrCap
Local nVlrCap 			:= 0
Local oCodAUT
Local oNsu
Local oData
Local cNsu 				:= Space(9)
Local cCodAut 			:= Space(9)
Local cData				:= Space(5)   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

Private nTotal			:= 0
Private aParcTef		:= {}

If ! Type("nMoedaCor") == "N"
	Private nMoedaCor 	:= 1
Endif

ctTipOp					:= cTipo
ctNSU					:= ""
ctDat_Venc				:= Space( 4 )
ctCartao				:= Space( 19 )
ctTrilha_2				:= ""
ctData					:= StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+Str(Year(dDatabase),4)

// Solicita o preenchimento do Valor da Pré-Autorização, antes de ler o cartão...
If cTipo="0"
	DEFINE MSDIALOG oDlg TITLE "Valor da pré-autorização" FROM 13,10 TO 19,65 OF GetWndDefault()
	@ 7,005 SAY "Valor da pré-autorização:" SIZE 130,10 OF oDlg PIXEL
	@ 7,80 MSGET oPre VAR nTotal SIZE 70,10 OF oDLG PIXEL PICTURE PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor) VALID nTotal > 0
	DEFINE SBUTTON FROM 30,080 TYPE 1 PIXEL ACTION ( oDlg:End(),nOpd:= 1 ) ENABLE OF oDlg
	DEFINE SBUTTON FROM 30,110 TYPE 2 PIXEL ACTION ( oDlg:End(),nOpd:= 2 ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTER
Endif

If nOpd == 1
	If Empty( LjGetStation("PINPAD") )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Caso a execução seja feita através do CALL CENTER o tipo do cartão será não magnético  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTipCar:=2
	Else
		If SuperGetMV("MV_INTEEMS")
			DEFINE MSDIALOG oDlg TITLE "Tipo de Cartão" FROM 13,21 TO 18,78 OF GetWNDDefault()
			@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
			@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
			@ 10,115 BUTTON "CPF"	    	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
			@ 10,170 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
		Else
			DEFINE MSDIALOG oDlg TITLE "Tipo de Cartão" FROM 13,21 TO 18,64 OF GetWNDDefault()
			@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
			@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
			@ 10,115 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
		Endif
		ACTIVATE MSDIALOG oDlg CENTERED
	Endif
	
	cModCart := If( nTipCar == 1, "M", "N" )
	While nTipCar <> 4
		If (nTipCar == 1)
			// Se tipo de cartao = magnetico, fazer a leitura do cartao
			nOpd := If( L010LeCartao("C"), 1, 3 )
		ElseIf (nTipCar == 2)
			// Se tipo de cartao = nao magnetico, coletar o numero
			nOpd := 3
			DEFINE MSDIALOG oDlg TITLE "Dados do Cartão" FROM 13,21 TO 21,60 OF GetWNDDefault()
			@	.074,.1 TO 4,19
			@ 06,12 SAY		"Informe o número do cartão:"	SIZE 130,10 OF oDlg PIXEL
			@ 06,88 MSGET	ctCartao						SIZE 055,10 OF oDLG PIXEL PICTURE "@!" VALID L010VerNumCart( ctCartao )
			@ 18,12 SAY		If(lInfoCard,"Informe a data do cartão:","Informe a validade:") SIZE 130, 10 OF oDlg PIXEL
			@ 18,88 MSGET	ctDat_Venc						SIZE 024,10 OF oDlg PIXEL PICTURE If(lInfoCard,"@R 99/9999","@R 99/99") VALID L010ValidDt( ctDat_Venc )
			DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
			DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=3,oDlg:End() ) ENABLE OF oDlg
			ACTIVATE MSDIALOG oDlg CENTERED
			
			ctCartao := AllTrim( ctCartao )
			
			If (nOpD == 3)
				lRet:= .F.
				Exit
			Endif
		Endif
		If (nOpD == 3)
			Return (.F.)
		Endif
		
		lContinua := L010BinCar( "0" , @lLerPin )  // Consulta ao Bin do Cartao
		
		If lContinua
			If cTipo="2" .OR. cTipo="3" .OR. cTipo="4"
				// Solicita o preenchimento dos dados do Estorno, após ler o cartão
				// e antes de ler a senha no PINPAD...
				DEFINE MSDIALOG oDlg TITLE "Estorno da Captura de pré-autorização" FROM 13,21 to 24,60  of GetWNDDefault()
				@	6,12 SAY "Valor da Captura"  SIZE 130, 10 OF oDlg PIXEL
				@	6,88 MSGET oVlrCap VAR nVlrCap SIZE  55, 10 OF oDLG PIXEL	Picture "@E 999,999.99"
				
				@ 18,12 SAY "Código da Autorização" SIZE 130, 10 OF oDlg PIXEL
				@ 18,88 MSGET oCodAUT VAR cCodAut SIZE  18, 10 OF oDlg PIXEL Picture "999999" Valid !Empty(cCodAut)
				
				@ 30,12 SAY "Código de NSU" SIZE 130, 10 OF oDlg PIXEL
				@ 30,88 MSGET oNSU    VAR cNSU    SIZE  18, 10 OF oDlg PIXEL Picture "999999" Valid !Empty(cNSU)
				
				@ 42,12 SAY "Data (DD/MM).:"  SIZE 130, 10 OF oDlg PIXEL
				@ 42,88 MSGET oData VAR cData SIZE  55, 10 OF oDLG PIXEL	Picture "@R 99/99" Valid !Empty( cData )
				DEFINE SBUTTON FROM 60,  115 TYPE 1 PIXEL ACTION (oDlg:End() ) ENABLE OF oDlg
				ACTIVATE MSDIALOG oDlg CENTERED
				
				ctCodAut := StrZero(Val(cCodAut),6)
				ctNsu    := StrZero(Val(cNsu),9)
				ctData   := Subs(cData,1,4)+Str(Year(dDatabase),4)
				// mesma rotina de cancelamento de captura de pre autorixacao
				//ctTipOp  := "4"
				nTotal   := nVlrCap
			Endif
			ctValor := L010ValStr( nTotal )
			
			While (nTentar < 4)
				nTentar ++
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Preencher o parametro MV_TEFCRIP com TESTE apenas para trabalhar com o Simulador do Sitef         ³
				//³Existem alguns cartoes de credito com senha (VISA - Bradesco), mas o simulador nao distingue      ³
				//³qual tem senha e qual nao optamos por nunca pedir senha para CC no ambiente de testes, pois estava³
				//³gerando erro na DLL do Pinpad.                                                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lLerPin //.AND. ! SuperGetMV("MV_TEFCRIP") == "TESTE"
					If lUsaDisplay
						DisplayEnv(StatDisplay(), "1C" + "Digitar senha do cartao magnetico")         
 					   	DisplayEnv(StatDisplay(), "2C" + " ")                             
					Endif
					//HOMOLOGACAO: A mensagem do pinpad nao esta aparecendo corretamente
					lRet := L010LePinPad()
					If !lRet
						Exit
					Endif
				Endif
				If (nOpD == 1)
					aRet := L010MonEnvResp( "12", "C" )
					If Substr(aRet[ 2 ],2,2) == "55" .AND. !lVer300
						// Se senha incorreta, repete leitura ate tentativas = 3
						If (nTentar == 3)
							Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
							lRet := .F.
							Exit
						Endif
					ElseIf (aRet[1] == 1)
						lAceite := .T.
						If lUsaDisplay
	   						DisplayEnv(StatDisplay(), "1C" + "Aguarde a Impressao do Cupom")         
 						   	DisplayEnv(StatDisplay(), "2C" + " ")                             
	 					Endif
	 					// ### "Aguarde ... imprimindo o cupom TEF "
            			ljMsgRun( STR0065, MsgImpressao(),{ || xRetorno := L010TefImpr( aTicket, aPromis, .T.)})
						lRet := .T.
						Exit
					Else
						If !aRet[ 5 ]
							Exit
						Endif
					Endif
				Endif
			End
		Else
			lRet:= .F.
		Endif
		Exit
	End
Else
	lRet	:= .F.
Endif

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Loja021T     ³ Autor ³ Mauro Mancio  	    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ "ESTA FUNÇÃO FOI CRIADA PARA ATENDER A ESPECIFICAÇÃO   3.00³±±
±±³          ³ DO SITEF"             									  ³±±
±±³          ³ Ao passar o primeiro parâmetro ao LOJA010T, como "R"       ³±±
±±³          ³ O segundo parâmetro funcionará da seguinte forma:          ³±±
±±³          ³ 0 » indica pedido de pré-autorização                       ³±±
±±³          ³ 2 » indica pedido de estorno pré-autorização               ³±±
±±³          ³ 3 » indica pedido de captura pré-autorização               ³±±
±±³          ³ 4 » indica pedido de estorno da captura pré-autorização    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Loja021T( ) 					                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> transação efetuada com sucesso ou não			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja021T()
If lUsaTef
	Loja010T( "R", "2" )
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³L010IsTrHabilit ³ Autor ³ Mauro Mancio    ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Verifica se a transação é válida                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Obs       ³ Esta funcao foi criada para atender as especificacoes  3.00³±±
±±³          ³ do SITEF             									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
*/
Static Function L010IsTrHabilit(nX)

Local nFormaDeb := 0
Local cOrigem   := ""
Local cMsgTrn   := ""
Local lResult   := .T.
Local nParcela  
Local dData1    

DEFAULT nX := 0

If Type("aParcTef") == "U"
   aParcTef := {}
Endif   

nParcela  :=If(lTefMult .AND. nX<>0 , aTefMult[nX][5], Len(aParcTef) )
dData1    := dDataBase

If Len(aParcTef)>0
   dData1 := If( lTefMult .AND. nX<>0 , aTefMult[nX][6][1] , aParcTef[1][1] )
Endif   

// nFormaDeb= 2 -> Venda Parcelada
// nFormaDeb= 1 -> Venda Pre-Datada
// nFormaDeb= 0 -> Venda Vista

nFormaDeb := If(nParcela>1,2,If(dData1=dDataBase,0,1))

If !Empty(aBlocoResp[3][1])
	cOrigem:="C"
ElseIf !Empty(aBlocoResp[2][1])
	cOrigem:="D"
Endif
cMensag:="" 

If cOrigem == "C"  // Cartao de Credito
	Do Case
		Case nFormaDeb=0   // aVista
			If (aBlocoResp[3][2]="0")
				cMsgTrn := "Transação não habilitada" 
				cMensag += "Crédito - À Vista"
				lResult := .F.
			ElseIf (aBlocoResp[3][7]="0").AND. cOpera="E"
				cMsgTrn := "Transação não habilitada" 
				cMensag += "Crédito - Cancelamento"
				lResult := .F.
			ElseIf aBlocoResp[3][ 8] = '0'.AND. Alltrim(Upper(FunName())) == "LOJA014T"   // Pre-Autorizacao
				cMsgTrn := "Transação não habilitada" 
     			cMensag += "Crédito - Pré-Autorização"
	    		lResult := .F.
		    ElseIf aBlocoResp[3][10] = '0'.AND. Alltrim(Upper(FunName())) == "LOJA021T"   // Cancelamento de Pre-autorizacao
				cMsgTrn := "Transação não habilitada" 
			    cMensag += "Crédito - Cancelamento Pré-Autorização"
			    lResult := .F.
			Endif
		Case nFormaDeb=2// Venda Parcelada ou consulta CDC
            // Quando o Numero max. de parcelas for igual 1 somente podera efetuar venda a vista
			If Val(aBlocoResp[1][8])= 1
				cMsgTrn := "Transação não habilitada"
				cMensag := "Crédito - Venda Parcelada"
				lResult := .F.
			ElseIf ( nParCred > Val(aBlocoResp[1][8]) ) .OR. ( nParCred < Val(aBlocoResp[1][7]) )
				cMsgTrn := "Atenção" 
				cMensag := "Crédito - Excede Parcelas"
				lResult := .F.
			Else
				//HOMOLOGACAO - Antes pegava direto do SE$ e não verificava o parâmetro -  SE4->E4_JURCART == "1" // Juros Pela ADM 1 ou 3
				If ctTipPar == "3" // Juros Pela ADM 
					If (aBlocoResp[3][4]="0")
						cMsgTrn := "Transação não habilitada" 
						cMensag += "Crédito - Parcelado com juros pela ADM"
						lResult := .F.
					Endif
				Endif
				If ctTipPar == "4" // Juros Pela loja
					If (aBlocoResp[3][3]="0")
						cMsgTrn := "Transação não habilitada" 
						cMensag += "Crédito - Parcelado sem juros pela Loja"
						lResult := .F.
					Endif
				Endif
			Endif
	EndCase
ElseIf cOrigem == "D" // Cartao de Debito
	Do Case
		Case nFormaDeb=0   							//A Vista
			If (aBlocoResp[2][2]="0")
				cMsgTrn := "Transação não habilitada" 
				cMensag += "Débito - À Vista"
				lResult := .F.
			ElseIf (aBlocoResp[2][10]="0").AND. cOpera="E"
				cMsgTrn := "Transação não habilitada" 
				cMensag += "Débito - Cancelamento"
				lResult := .F.
			Endif
		Case nFormaDeb=1 .AND. cOpera<>"E"      	//Pre-Datado
			If (aBlocoResp[2][3]="0")
				cMsgTrn := "Transação não habilitada" 
				cMensag += "Débito - Pré-Datado"
				lResult := .F.
			ElseIf dData1>(dDatabase+365).OR. dData1 > CToD( Subs(aBlocoResp[1][10],1,2)+'/'+Subs(aBlocoResp[1][10],3,2)+'/'+Subs(aBlocoResp[1][10],7,2))
				cMsgTrn := "Atenção" 
				cMensag := "Débito - Excede Prazo"+Chr(13)
				lResult := .F.
			Endif
		Case nFormaDeb=2 .AND. cOpera<>"E" .AND. nConsRot==0 // Venda Débito Parcelado
			If nParcela>0
				If nParcela>Val(aBlocoResp[1][8]) .OR. nParcela < Val(aBlocoResp[1][7])
					cMsgTrn := "Atenção" 
					cMensag := "Débito Parcelado - Excede Parcelas"+Chr(13)
					lResult := .F.
				ElseIf (dData1 >(dDatabase+365)) .OR. ;
				       (dData1 == dDatabase .AND. dData1 > CToD( Subs(aBlocoResp[1][11],1,2)+'/'+Subs(aBlocoResp[1][11],3,2)+'/'+Subs(aBlocoResp[1][11],7,2)) ) .OR. ;
				       (dData1 > dDatabase	.AND. dData1 > CToD( Subs(aBlocoResp[1][10],1,2)+'/'+Subs(aBlocoResp[1][10],3,2)+'/'+Subs(aBlocoResp[1][10],7,2)) )
					cMsgTrn := "Atenção" 
					cMensag := "Débito Parcelado - Excede Prazo"+Chr(13)
					lResult := .F.
				ElseIf dData1=dDataBase .AND. (aBlocoResp[2][4]="0") .AND. Val(aBlocoResp[1][1])<>4
					cMsgTrn := "Transação não habilitada"
					cMensag += "Débito Parcelado - Com primeira parcela à vista"+Chr(13)
					lResult := .F.
				ElseIf dData1>dDataBase .AND. (aBlocoResp[2][5]="0")
					cMsgTrn := "Transação não habilitada" 
					cMensag += "Débito Parcelado - Sem Entrada"+Chr(13)
					lResult := .F.
				ElseIf  dData1 > CToD( Subs(aBlocoResp[1][11],1,2)+"/"+Subs(aBlocoResp[1][11],3,2)+"/"+Subs(aBlocoResp[1][11],5,4) ) .AND. ;
				        SE4->E4_JURCART == "1" // Juros pelo estabelecimento
					cMsgTrn := "Atenção" 
					cMensag := "Débito Parcelado - Data limite para a primeira parcela com juros pela Adm"+Chr(13)
					lResult := .F.
				ElseIf (aBlocoResp[2][8]="0") .AND. aBlocoResp[2][ 1]= "C" // Parcelada Com Juros
					cMsgTrn := "Transação não habilitada" 
					cMensag := "Débito Parcelado - Com Juros"+Chr(13)
					lResult := .F.
				ElseIf (aBlocoResp[2][9]="0") .AND. aBlocoResp[2][ 1]= "C" // Parcelada Sem Juros
					cMsgTrn := "Transação não habilitada" 
					cMensag := "Débito Parcelada - Sem Juros"+Chr(13)
					lResult := .F.
				Endif
			Endif
		Case nFormaDeb=2 .AND. cOpera<>"E" .AND. nConsRot>0 // Venda ou Consulta CDC
			If nParcela>0
				//HOMOLOGACAO: Retirada a consistencia de validar Venda CDC somente para a Visa, pois outra Rede pode vir a Ter como é o caso da TECBAN
				If  aBlocoResp[2][7]="0" .AND. nConsRot=2 //Val(aBlocoResp[1][1])=4 .AND. aBlocoResp[2][7]="0" .AND. nConsRot=2
					cMsgTrn := "Transação não habilitada" 
					cMensag += "Débito CDC - Venda"+Chr(13)
					lResult := .F.
				ElseIf aBlocoResp[2][11]="0" .AND. nConsRot=1 //Val(aBlocoResp[1][1])=4 .AND. aBlocoResp[2][11]="0" .AND. nConsRot=1
					cMsgTrn := "Transação não habilitada" 
					cMensag += "Débito CDC - Consulta"+Chr(13)
					lResult := .F.
				ElseIf (dData1 >(dDatabase+365) .OR. ;
	 				   dData1 > CToD(Subs(aBlocoResp[1][11],1,2)+'/'+Subs(aBlocoResp[1][11],3,2)+'/'+Subs(aBlocoResp[1][11],7,2)) )
					cMsgTrn := "Atenção" 
					cMensag := "Débito CDC - Excede Prazo"+Chr(13)
					lResult := .F.
				ElseIf nParcela>Val(aBlocoResp[1][8]) .OR. nParcela < Val(aBlocoResp[1][7])
					cMsgTrn := "Atenção" 
					cMensag := "Débito CDC - Excede Parcelas"+Chr(13)
					lResult := .F.
				Endif	
			Endif
		Case nFormaDeb=2 .AND. cOpera=="E" .OR. cOpera=="X"	 // Cancelamento de Débito
			 lResult := .T.
	Otherwise
		cMsgTrn := "Situação não prevista" 
		cMensag := "Entrar em contato com a Microsiga"+Chr(13)
		lResult := .F.
	EndCase
Else
	cMsgTrn := "Situação não prevista - cOrigem" 
	cMensag := "Entrar em contato com a Microsiga"+Chr(13)
	lResult := .F.
Endif
If !lResult
	Aviso( "Atenção", cMensag, {"&Ok"},, cMsgTrn )
Endif

Return (lResult)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Consulta_CDC ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ "ESTA FUNÇÃO FOI CRIADA PARA ATENDER A ESPECIFICAÇÃO   3.00³±±
±±³          ³ DO SITEF"             									  ³±±
±±³          ³ Apresenta na Tela o que iria para impressão.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function Consulta_CDC(aDadosCons,cCaption)

Local icont

Private oListBox
Private oDlgCon
Private cListBox     := ""
Private oFnt
Private oFnt1

If cCaption==nil
   cCaption:="Consulta CDC"
Endif   

If Len(aDadosCons)>0
	For iCont:=1 To Len(aDadosCons)
		aDadosCons[iCont] := StrTran(aDadosCons[icont],Chr(13),'')
		aDadosCons[iCont] := StrTran(aDadosCons[icont],Chr(10),'')
	Next iCont
	DEFINE MSDIALOG oDlgCon FROM  25.1,0.1 TO 355,320 TITLE cCaption PIXEL OF oMainWnd
	DEFINE FONT oFnt1 NAME "Courier New"
	@   5,10 LISTBOX oListBox VAR cListBox SIZE 150,140  PIXEL ITEMS aDadosCons OF oDlgCon
	@ 15,29 BUTTON "&Ok" SIZE 40,11 ACTION (oDlgCon:End()) OF oDlgCon CANCEL
	oListBox:SetFont(oFnt1)
	ACTIVATE MSDIALOG oDlgCon CENTERED
Endif
RETURN .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Cupom_TEF    ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna o Cupom TEF a ser impresso, posteriormente por     ³±±
±±³          ³ outro módulo.         									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Cupom_TEF
Local aResult:={}
If ValType( aTicket ) == "A"
	aResult := aTicket
Endif
Return aResult

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MsgImpressao ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Retorna a Mensagem a ser mostrada, durante a impressão do  ³±±
±±³          ³ cupom TEF            									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function MsgImpressao( cDisp )
Static cMsg
If cDisp<>Nil
	cMsg:=cDisp
Endif
If cMsg==Nil
	cMsg:=""
Endif
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Loja022T     ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Transação da captura de pré-autorização  				  ³±±
±±³          ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja022T()
If lUsaTef
	Loja010T( "R", "3" )
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Loja023T     ³ Autor ³ Mauro Mancio       ³ Data ³ 27/11/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Estorno da captura da pré-autorização  				      ³±±
±±³          ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja023T()
If lUsaTef
	If Loja010T( "R", "4" )
		MsgInfo('Transação OK')
	Endif
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³L010ConfTrn  ³ Autor ³ Marcos Alves       ³ Data ³ 31/01/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Efetiva o comando de confirmacao da ultima transacao TEF   ³±±
±±³          ³ efetuada.             									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Obs		 ³ Para executar a transacao de confirmacao e necessario, re- ³±±
±±³     	 ³ cuperar as informacoes do arquivo NCNTRN.001 que estao dis-³±±
±±³     	 ³ postos no seguinte layout :                                ³±±
±±³     	 ³ 	00=S;02=dddd;09=nnnnnnnn;11=rrrrrrr;                      ³±±
±±³     	 ³ 	ddd   - Data da operacao                                  ³±±
±±³     	 ³ 	nnnnn - Numero da transacao NSU                           ³±±
±±³     	 ³ 	rrr   - rede da transacao                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista  ³ Data/Bops/Ver ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Hanna     ³87653 ³20/12/05³Valida se existe arquivo de LOG, igual a AP7³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010ConfTrn()
Local nHandle
Local nSize
Local nRead
Local cRbuffer
Local lConf
Local lConsulta	:=.T.
Local nPos		:= 0
Local cCont
Local cMensagem	:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VERIFICACAO PARA A TRATAMENTO DOS LOGS DE RECUPERACAO DE VENDA #TEF#                         ³
//³Nao poderemos deixar que o desfazimento seja efetuado se o cliente trabalhar com os logs para³
//³tratamento de recuperacao de venda e transacao (LOJXLOG E LOJA010D)                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local aDirectLJ := Directory(LOG_DIR+"LJ??????."+xNumCaixa())   // Arquivo de Log com o Apontamento de Erro
Local aDirectSL := Directory(LOG_DIR+"SL??????."+xNumCaixa())   // Arquivo de recuperação da venda com dados do TEF
Local aArq		:= Directory("NCNTRN*."+cPTerm)
Local nI
Local aTefBak	:= aClone(aTefDados)
Local aTransAtu := TEF_aTrans()

For nI:=1 to Len(aArq)
	
	nHandle	:= FOpen( aArq[nI][1] )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao existir arquivo de log, retorna falso³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nHandle == -1
		MsgAlert( "O arquivo de nome " + Alltrim( aArq[nI][1] ) + " nao pode ser aberto! ","Atencao! " )
		Return .F.
	Endif

	aTefDados	:= { { 	"", "", "", "", ;
						"", "", "", "", ;
						"", "", "", "", ;
						"", {}, "", "", ;
						"", "", "", ""} }

	ctDt_H 		:= NIL
	ctNum_H		:= NIL
	ctRede_H	:= NIL
	nSize 		:= FSeek( nHandle, 0, 2 )

	FSeek( nHandle, 0 )

	cRBuffer 	:= Space( nSize )

	FRead( nHandle, @cRBuffer, nSize )
	FClose( nHandle )

	While Len( Alltrim( cRBuffer ) ) > 0
		nPos 	:= Val( Subs( cRBuffer, 1, 2 ) )
		cCont 	:= Subs( cRBuffer, At( "=", cRBuffer ) + 1, At( ";", cRBuffer ) - ( At( "=", cRBuffer ) + 1 ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³HOMOLOGACAO: Verificando se enviaremos o desfazimento a confirmação ou a não confirmação³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nPos == 0
			cConf := cCont
		Else
			If cConf == "D"
				aTransAtu[nPos] := cCont			//Carregando a array com os dados do desfazimento
			Else
				aTefDados[1][nPos] := cCont
			Endif
		Endif

		cRBuffer := Subs( cRBuffer, At( ";", cRBuffer ) + 1 )
	End

	TEF_aTrans( aTransAtu[1], aTransAtu[2], aTransAtu[3], aTransAtu[4], aTransAtu[5] )

	lAceite 	:= .T.
	cMensagem	:= IIF( cConf=="D", "o desfazimento", IIF( cConf == "N", "a não confirmação", "a confirmação" ) )

    // ### "Enviando " ### " da  transação TEF pendente"
	MsgRun( STR0083 + cMensagem + STR0084,, { || lConsulta := L010Confirma( ( cConf == "S" ), aArq[nI][1], cConf ) } )

	If !Empty( LOG_TEF )
		LjWriteLog( LOG_TEF + SL1->L1_NUM + ".TXT", "10) Confirmação com " + IIF( cConf == "S", ".T.", ".F." ) )
		LjWriteLog( LOG_TEF + SL1->L1_NUM + ".TXT", "NCN(8) " + "Enviando " + cMensagem + " da  transação TEF pendente" )
	Endif

	ctDt_H 		:= NIL
	ctNum_H		:= NIL
	ctRede_H	:= NIL
	aTefDados 	:= aClone( aTefBak )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se conseguiu enviar o arquivo apago³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lConsulta
		fErase( aArq[nI][1] )
	Else
		Exit
	Endif
Next nI

Return lConsulta

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³L010Reimp    ³ Autor ³ Marcos Alves       ³ Data ³ 31/01/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ ReImpressao do cupom TEF                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Obs		 ³ Para a rede VISANET podera ser ReImpresso qualquer cupom,  ³±±
±±³     	 ³ para isso devera ser informado os dados necessarios:       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T, LOJA024T                               		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010Reimp(aTicket, aPromis, lConsulta, cTipVenCon)

Local cData		:=	space(5)
Local cVarTick	:= ""
Local cTrans	:= 'F0'
Local aRetorno	:= {}
Local lRet		:= .F.
Local nOp		:= 1
Local cContr	:= ''
Local cDataCp
Local cNsu
Local aTicketBk := aClone(aTicket)
Local lReimp	:= .T.					//Tratamento para a impressao da palavra Reimpressao
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

If lVer300
	If !Lj010Autor("TEF-EXC") // Senha do Administrador
		Return .F.
	Endif
Endif

aTicket := {}
cTicket := ''

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Somente para as transações VISA existe a possibilidade de     ³
//³de esolhermos uma transação para reimpressão, para todas      ³
//³as outras fazemos um controle interno através de arquivo texto³
//³R  -  Arquivos Texto                                          ³
//³RX - Visa                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If cTipVenCon=='R'
	If !File("RX_PRN." + cPTerm)
		MsgStop("Não existe arquivo para reimpressão!")
		Return .T.
	Endif
	lRet:=.T.
	cTicket := MemoRead( "RX_PRN." + cPTerm )
	If SuperGetMV( "MV_TEFCRIP" ) == "S"
		cTicket:= DecSiTef(cTicket)
	Endif
	nPos     := 0
	cVarTick := ""
	lReimp	 := .T.
	While nPos < Len( cTicket )
		nPos ++
		nAsc := Asc( SubStr( cTicket, nPos, 1 ) )
		If nAsc == 10
			aAdd(aTicket, cVarTick)
			//HOMOLOGACAO: Imprimir a palavra reimpressao apos a 1º linha
			If lReimp .AND. !Empty(cVarTick) .AND. Len(aTicket) >=1
		 	    aAdd(aTicket,Space(15)+"REIMPRESSAO")
		 	    lReimp := .F.
			Endif		 		    
			cVarTick := ""
		Else
			cVarTick += SubStr( cTicket, nPos, 1 )
		Endif
	End
Else
	cData   := Subs(cData,3,2)+'/'+Subs(cData,1,2)
	cDataCp := If(Empty(cData),'  /  ',cData)
	cNsu    := If(Empty(cContr),Space(9),cContr)
	
	DEFINE MSDIALOG oDlg TITLE "Dados do Cartão" FROM 13,21 to 22,60  of GetWNDDefault()
	@ 18,12 SAY "NSU (Número Seq. Único):" SIZE 130, 10 OF oDlg PIXEL
	@ 18,88 MSGET cNsu SIZE  18, 15 OF oDlg PIXEL Picture "@!"
	
	@ 30,12 SAY "Data da Compra (DD/MM):"  SIZE 130, 10 OF oDlg PIXEL
	@ 30,88 MSGET cDataCp SIZE  55, 10 OF oDLG PIXEL	Picture "@R 99/99"
	
	DEFINE SBUTTON FROM 47,  80 TYPE 1 PIXEL ACTION (oDlg:End() ) ENABLE OF oDlg
	DEFINE SBUTTON FROM 47,  114 TYPE 2 PIXEL ACTION (oDlg:End(),nOp:=2 ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
	
	If nOp =2
		Return( .T. )
	Endif

	cDataCp   := Alltrim(StrTran( cDataCp, "/"	, "" ))
	cData     := Alltrim(StrTran( cData, "/"	, "" ))
	ctRede_H  := '0004'
	ctNsu     := StrZero(Val(cNsu),9)
	ctDat_Pag := cDataCp
	aRetorno  := L010MonEnvResp( AllTrim(cTrans),,.T. )
	If (aRetorno[ 1 ] == 1)
		lRet:=.T.
	Endif
Endif

If lRet
	If lUsaDisplay
		DisplayEnv(StatDisplay(), "1C" + "Aguarde a reimpressao do cupom TEF")         
 	  	DisplayEnv(StatDisplay(), "2C" + " ")                             
	Endif
	// ### "Aguarde ... Reimprimindo o cupom TEF"
	LjMsgRun( STR0085,MsgImpressao(),{|| lRet:=L010TefImpr(aTicket,aPromis,.F.,@lConsulta)})
Endif

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Loja024T     ³Autor  ³ Marcos Alves       ³ Data ³ 31/01/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chamada da Funcao Reimpressao Tef, para rede VISANET       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Loja024T()

If lUsaTef .AND. cTipTEF = TEF_SEMCLIENT_DEDICADO
	Loja010T( "I" , "RV" )
Else
	Help(" ","1","NAOTEF")
Endif

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010ConsAVS()³Autor  ³ Marcos Alves       ³ Data ³ 31/01/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Consulta AVS                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010ConsAVS()
Local lRet				:=.F.
Local oDlg
Local oCPF,cCPF			:= space(11)
Local oEnd,cEnd			:= space(50)
Local oNumero,cNumero	:= space(10)
Local oCompl,cCompl		:= space(20)
Local oCep,cCep			:= space(9)
Local nColGet			:= 50
Local nOpd				:= 0
Local lContinua 		:= .F.
Local lLerPin 			:= .F.
Local lSaiAVS			:= .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a estacao possui Display ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lUsaDisplay := !Empty(LjGetStation("DISPLAY"))

DEFINE MSDIALOG oDlg TITLE "Dados do Cartão" FROM 13,21 TO 21,60 OF GetWNDDefault()
@ 06,12 SAY		"Informe o número do cartão:"	SIZE 130,10 	OF oDlg PIXEL
@ 06,88 MSGET	ctCartao						SIZE 055,10 	OF oDLG PIXEL PICTURE "@!" VALID ! EMPTY( ctCartao )
@ 18,12 SAY		"Informe a validade:"  			SIZE 130,10 	OF oDlg PIXEL
@ 18,88 MSGET	ctDat_Venc						SIZE 024,10 	OF oDlg PIXEL PICTURE If(lInfoCard,"@R 99/9999","@R 99/99") VALID L010ValidDt( ctDat_Venc )

DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=3,oDlg:End() ) ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTERED

If nOpd=1
	ctCartao := AllTrim( ctCartao )
	lContinua := L010BinCar( "0" , @lLerPin )  // Consulta ao Bin do Cartao
	
	If lContinua
		
		DEFINE MSDIALOG oDlg TITLE "Captura de Dados - Consulta AVS" FROM 13,21 to 25,60  of GetWNDDefault()
		
		@ 6,12 SAY "CPF"  SIZE 80, 10 OF oDlg PIXEL
		@ 6,nColGet MSGET oCPF VAR cCPF SIZE  55, 10 OF oDLG PIXEL	Picture "@E"
		
		@ 18,12 SAY "Endereco"  SIZE 80, 10 OF oDlg PIXEL
		@ 18,nColGet MSGET oEnd VAR cEnd SIZE  100, 10 OF oDLG PIXEL	Picture "@E"
		
		@ 30,12 SAY "Numero" SIZE 80, 10 OF oDlg PIXEL
		@ 30,nColGet MSGET oNumero VAR cNumero SIZE  22, 10 OF oDlg PIXEL Picture "@E"
		
		@ 42,12 SAY "Complemento" SIZE 80, 10 OF oDlg PIXEL
		@ 42,nColGet MSGET oCompl    VAR cCompl   SIZE  55, 10 OF oDlg PIXEL Picture "@E"
		
		@ 54,12 SAY "Cep"  SIZE 80, 10 OF oDlg PIXEL
		@ 54,nColGet MSGET oCep VAR cCep SIZE  22, 10 OF oDLG PIXEL	Picture "@R 99999-999"
		
		DEFINE SBUTTON FROM 70, 082 TYPE 1 PIXEL ACTION ( lSaiAVS:=.F.,oDlg:End() ) ENABLE OF oDlg
		DEFINE SBUTTON FROM 70, 112 TYPE 2 PIXEL ACTION ( lSaiAVS:=.T.,oDlg:End() ) ENABLE OF oDlg
		
		ACTIVATE MSDIALOG oDlg CENTERED
		
		If lSaiAvs
			lRet:=.F.
		Else
			ctCEP     := Alltrim(cCep)
			ctEnd     := Alltrim(cEnd)
			ctNumero  := Alltrim(cNumero)
			ctCompl   := Alltrim(cCompl)
			ctCgc_Cpf := Alltrim(cCpf)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Preencher o parametro MV_TEFCRIP com TESTE apenas para trabalhar com o Simulador do Sitef         ³
			//³Existem alguns cartoes de credito com senha (VISA - Bradesco), mas o simulador nao distingue      ³
			//³qual tem senha e qual nao optamos por nunca pedir senha para CC no ambiente de testes, pois estava³
			//³gerando erro na DLL do Pinpad.                                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lLerPin 	//.AND. ! SuperGetMV("MV_TEFCRIP") == "TESTE"
				If lUsaDisplay
					DisplayEnv(StatDisplay(), "1C" + "Digitar senha do cartao magnetico")
					DisplayEnv(StatDisplay(), "2C" + " ")
				Endif
				//HOMOLOGACAO: A mensagem do pinpad nao esta aparecendo corretamente
				lRet := L010LePinPad()
			Endif
			aRet := L010MonEnvResp( "16", "C" )
			If Substr(aRet[ 2 ],2,2) == "55" .AND. !lVer300
				lRet:=.F.
			ElseIf (aRet[1] == 1)
				lAceite := .T.
				If lUsaDisplay
					DisplayEnv(StatDisplay(), "1C" + "Aguarde a Impressao do Cupom")
					DisplayEnv(StatDisplay(), "2C" + " ")
				Endif
				// ### "Aguarde ... imprimindo o cupom TEF "
				ljMsgRun( STR0065 ,,{ || lRet := L010TefImpr( aTicket, aPromis, .T.)})
				lRet := .T.
			Else
				lRet:=.F.
			Endif
		Endif
	Endif
Else
	lRet := .F.
Endif
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³LJAtvStat ³ Autor ³ Marcos Alves & Fabinho³ Data ³ 25.09.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ativar as variaveis estaticas da rotina do TEF ( loja010t) ³±±
±±³          ³ no Protheus existe um novo conceito para as variaveis esta-³±±
±±³          ³ ticas, baseia-se na criacao em duas estacias, uma antes do ³±±
±±³          ³ menu e outra depois do menu principal, as variaveis criadas³±±
±±³          ³ na segunda estancia, sao inicializadas sempre que voltar   ³±±
±±³          ³ para o menu                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ LJAtvStat()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJXFUNB - AbreLoja()                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function LjAtvStat()
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³Loja025T     ³ Autor ³ Marcos Alves       ³ Data ³ 19/10/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Saque para a administradora FININVEST                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Loja025T()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja025T()
If lUsaTef
	Loja010T( "Q", "1" )
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Loja026T     ³ Autor ³ Marcos Alves       ³ Data ³ 05/11/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Consulta para a administradora FININVEST                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Loja026T()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja026T()
If lUsaTef
	Loja010T( "Q", "2" )
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Loja027T     ³ Autor ³ Marcos Alves       ³ Data ³ 05/11/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Estorno para a administradora FININVEST                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Loja027T()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function Loja027T()
If lUsaTef
	Loja010T( "Q", "3" )
Else
	Help(" ","1","NAOTEF")
Endif
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³L010Finin    ³ Autor ³ Marcos Alves       ³ Data ³ 19/10/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Transacoes de saque e estorno do cartao Fininvest          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ L010Finin(cTipo)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³lRet - status da transcao                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function L010Finin(cTip)

Local oDlg
Local oTipo
Local cTipo     := space(10)
Local aTipo     := {}

Local oConsulta
Local cConsulta := space(10)
Local aConsulta := {} 

Local cCaption  := "Fininvest "

Local oValor,nValor := 0
Local oCodAUT,cCodAut :=Space(9)
Local oNsu,cNsu :=Space(9)
Local oData,cData:=Space(5)

Local nParc  := 1
Local nPlano := 0
Local cItem  := space(11)

Local nTentar :=0
Local lLerPin := .F.
Local lRet    := .F.
Local aRet    := {}
Local nOpd    := 0 
// Variavel utilizada na funcao de habilitar as transaçoes
Private aParcTef	:= {}

cModCart   := "M"
ntOffSet   := 2
ctNSU	   :=	""
ctDat_Venc := 	Space( 4 )
ctCartao   := 	Space( 19 )
ctData	   := 	StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+Str(Year(dDatabase),4)
ctRede_H   := '9'
ctFuncao   := '2'
ctOrcam    := '1' 
cCaption   += "Operação "
aConsulta  := {'Venda','Saque'}
aTipo      := {'Parcelado a vista','Parcelado pre-datado'}

If cTip=='2' // Consulta
   cCaption   += "Consulta "
   DEFINE MSDIALOG oDlg TITLE cCaption FROM 14,21 to 22,65  of GetWNDDefault()
  
   @ 05,07 TO 30,75 PROMPT 'Consultas' PIXEL OF Odlg // 
   @ 15,15 COMBOBOX oConsulta VAR cConsulta ITEMS aConsulta ON CHANGE L010CFini(cConsulta,@aTipo,oTipo) PIXEL SIZE 55,50 OF oDlg

   @ 05,85 TO 30,158 PROMPT 'Tipo' PIXEL OF Odlg // 
   @ 15,93 COMBOBOX oTipo VAR cTipo ITEMS aTipo PIXEL SIZE 60,50 OF oDlg

   DEFINE SBUTTON FROM 40,105 TYPE 1 PIXEL ACTION (oDlg:End() ) ENABLE OF oDlg
   DEFINE SBUTTON FROM 40,135 TYPE 2 PIXEL ACTION (cTip:='',oDlg:End() ) ENABLE OF oDlg
   ACTIVATE MSDIALOG oDlg CENTERED
Endif

If  cTip=='' // Saida
   Return .F.
ElseIf cTip=='3' // Estorno
    cCaption   += "Estorno "
	DEFINE MSDIALOG oDlg TITLE cCaption FROM 13,21 to 24,60  of GetWNDDefault()
	@ 6,12 SAY "Valor "  SIZE 130, 10 OF oDlg PIXEL
	@	6,88 MSGET oValor VAR nValor SIZE  55, 10 OF oDLG PIXEL	Picture "@E 999,999.99" VALID nValor > 0
	
	@ 18,12 SAY "Código da Autorização" SIZE 130, 10 OF oDlg PIXEL
	@ 18,88 MSGET oCodAUT VAR cCodAut SIZE  18, 10 OF oDlg PIXEL Picture "999999" Valid !Empty(cCodAut)
	
	@ 30,12 SAY "Código de NSU" SIZE 130, 10 OF oDlg PIXEL
	@ 30,88 MSGET oNSU    VAR cNSU    SIZE  18, 10 OF oDlg PIXEL Picture "99999999" Valid !Empty(cNSU)
	
	@ 42,12 SAY "Data (DD/MM).:"  SIZE 130, 10 OF oDlg PIXEL
	@ 42,88 MSGET oData VAR cData SIZE  55, 10 OF oDLG PIXEL	Picture "@R 99/99" Valid !Empty( cData )

	DEFINE SBUTTON FROM 60,080 TYPE 1 PIXEL ACTION ( nOpd:= 1,oDlg:End() ) ENABLE OF oDlg
	DEFINE SBUTTON FROM 60,110 TYPE 2 PIXEL ACTION ( nOpd:= 2,oDlg:End() ) ENABLE OF oDlg
	//DEFINE SBUTTON FROM 60,  115 TYPE 1 PIXEL ACTION (oDlg:End() ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
	ctCodAut := StrZero(Val(cCodAut),6)
	ctNsu    := StrZero(Val(cNsu),9)
	ctData   := Subs(cData,1,4)+Str(Year(dDatabase),4)
	
Else // Saque // Consulta
    cCaption   += If(cTip=="1","Saque ",cTipo)

	DEFINE MSDIALOG oDlg TITLE cCaption FROM 13,10 TO 25,45 OF GetWndDefault()
	@ 7,005 SAY "Valor " SIZE 130,10 OF oDlg PIXEL
	@ 7,80 MSGET oValor VAR nValor SIZE 51,10 OF oDLG PIXEL Picture "@E 999,999.99" VALID nValor > 0
	
	@ 23,005 SAY "Quantidade Parcelas " SIZE 130,10 OF oDlg PIXEL
	@ 23,80 MSGET oParc VAR nParc SIZE 50,10 OF oDLG PIXEL PICTURE '99' VALID nParc > 0 WHEN cTip=='1'

	@ 39,005 SAY "Item " SIZE 130,10 OF oDlg PIXEL
	@ 39,80 MSGET oItem VAR cItem SIZE 50,10 OF oDLG PIXEL PICTURE '@!' VALID !empty(cItem)

	@ 55,005 SAY "Plano " SIZE 130,10 OF oDlg PIXEL
	@ 55,80 MSGET oPlano VAR nPlano SIZE 50,10 OF oDLG PIXEL PICTURE '99999999' VALID nPlano > 0 WHEN nParc>1
	
	DEFINE SBUTTON FROM 75,075 TYPE 1 PIXEL ACTION ( nOpd:= 1,oDlg:End() ) ENABLE OF oDlg
	DEFINE SBUTTON FROM 75,105 TYPE 2 PIXEL ACTION ( nOpd:= 2,oDlg:End() ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTER
Endif

If nOpd == 1 .AND.! Empty( LjGetStation("PINPAD") )
	While .T.
		nOpd := If( L010LeCartao("D"), 1, 3 )
		If (nOpD == 3)
			Return (.F.)
		Endif
		If !(lContinua := L010BinCar( "1" , @lLerPin ))  // Consulta ao Bin do Cartao
			Return (.F.)
		Endif
		ctValor := L010ValStr( nValor )
		While (nTentar < 4)
			nTentar ++
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Preencher o parametro MV_TEFCRIP com TESTE apenas para trabalhar com o Simulador do Sitef         ³
			//³Existem alguns cartoes de credito com senha (VISA - Bradesco), mas o simulador nao distingue      ³
			//³qual tem senha e qual nao optamos por nunca pedir senha para CC no ambiente de testes, pois estava³
			//³gerando erro na DLL do Pinpad.                                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lLerPin //.AND. !SuperGetMV("MV_TEFCRIP") == "TESTE"
				lRet := L010LePinPad()
				If !lRet
					Exit
				Endif
			Endif
			If (nOpD == 1)
                ctOrcam    := Alltrim(cItem)
                ctDat_Venc := ''
				If cTip=='1'      // Saque
			    	If nParc=1    // Rotativo
				    	ctTipOp    := '1'
					    ctParc_Val := ''
					    ctPlano    := ''
				    Else           // Parcelado Vista
					    ctTipOp    := '3'
					    ctParc_Val := allTrim(Str(nParc))
                        ctPlano    := Alltrim(Str(nPlano))
				    Endif
				Else             // Consulta
                   If Ascan(aConsulta,cConsulta)=1 // Venda - "Parcelado Vista/ Parcelado Pre-datado"
                      ctFuncao   := '3'
                      If Ascan(aTipo,cTipo)=1 // Parcelado a vista
                     	ctTipOp    := '3'
					    ctParc_Val := '1'
                        ctPlano    := Alltrim(Str(nPlano))
                      Else       // Parcelado pre-datado
                     	ctTipOp    := '4'
					    ctParc_Val := '1'
                        ctPlano    := Alltrim(Str(nPlano))
                      Endif
                   Else          // Saque
                      ctFuncao   := '4'
                      If Ascan(aTipo,cTipo)=1 // Rotativo a vista
                     	ctTipOp    := '1'
					    ctParc_Val := ''
					    ctPlano    := ''
                      Else       // Parcelado a vista
                     	ctTipOp    := '3'
					    ctParc_Val := '1'
                        ctPlano    := Alltrim(Str(nPlano))
                      Endif
                   Endif
				Endif
				If cTip=='3' // Estorno
				   aRet:= {"F0", {ctRedeDest, ctADM, ctRede_H,"10","5",ctCartao, ctValor,ctData ,ctCodAut }}
				Else         // Saque/Consulta
				   aRet:= {"F0", {ctRedeDest, ctADM, ctRede_H,"10",ctFuncao, ctTipOp, ctCartao, ctValor, ctDat_Venc,ctOrcam,'2' ,ctParc_Val,ctPlano, ctSenha }}
				Endif   
				aRet := L010MonEnvResp( "F0","D",,aRet)
				If Substr(aRet[ 2 ],2,2) == "55" .AND. !lVer300
					// Se senha incorreta, repete leitura ate tentativas = 3
					If (nTentar == 3)
						Aviso( "Atenção", "Já foram feitas 3 tentativas de digitação de senha !", {"&Ok"},, "Venda cancelada" )
						lRet := .F.
						Exit
					Endif
				ElseIf (aRet[1] == 1).AND. (cTip=='1' .OR. cTip=='3' )
					lAceite := .T.
					// ### "Aguarde ... imprimindo o cupom TEF "
					ljMsgRun(STR0065 ,,{ || lRet := L010TefImpr( aTicket, aPromis, .T.)})
					lRet := .T.
					Exit
				ElseIf (aRet[1] == 1).AND. cTip=='2'
                    Consulta_CDC(aTicket,cCaption)
                    lRet := .T.
					Exit
				Else
					If !aRet[ 5 ]
						Exit
					Endif
				Endif
			Endif
		End
		Exit
	End
Endif

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010Cheque   ³ Autor ³ Marcos Alves       ³ Data ³ 04/09/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Efetua a transacao de chq no pacote com milhagem            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Function L010Cheque( ExpN1 )                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 - Numero da percela da Transacao Multipla            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Venda com sucesso ou nao						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010Cheque( nX, aDados, cOrcamen )
Local lRet			:= .T.
Local cTrans
Local aRet          :={4,''}
Local cNumCupFis 

ctCartao 			  := '0000000000000000'
ctCgc_Cpf             := '00000000000'
ctPlano				  := "1"
ctTrilha_2			  := ""
ctOrcam               := cOrcamen
ctData                := StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+;
Str(Year(dDatabase),4)
ctHoraVend            := SubStr(Time(),1,2) + SubStr(Time(),4,2) + SubStr(Time(),7,2)
//Inserido taxa de juros para Venda com cheque.
ctJur      			  := StrZero(SL1->L1_JUROS*100,7,0)
ctValor := AllTrim( StrTran( Str( Round( nCheq, 2 ),12,2 ), ".", "" ) )

cTrans		:= '50'		// Codigo da transacao
ctCodNet	:= '36'		// Codigo da rede
ctFuncao	:= '03'		// Autorizacao generica
ctEntDoc	:= '04'		
cSaida		:= 'I' 		// Saida da consulta
ctTipOp		:= '001'	// Codigo da operacao - Venda a cheque
ctEntrada 	:= '01'		// Tipo de entrada - Entrada da venda

// Pega dados do cartão, identificado no inicio da rotina
If Len(aCartaoMH)>0 .AND. aCartaoMH[1]<>4
  ctCartao := aCartaoMH[2]
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³aRet[1] ->Retorna o tipo do cartao³
	//³1 - Magnetico                     ³
	//³2 - Não Magnético                 ³
	//³3- CPF                            ³
	//³4- Abandona                       ³
	//³aRet[2] -> O dado sendo:          ³
	//³1,2 -> Numero do cartão           ³
	//³3 -> numero do cpf                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aRet:=L010TCart()  
	If aRet[1] == 4
		Return Nil
	ElseIf aRet[1] == 1 .OR. aRet[1] == 2 //Magnetico /nao Magnetico
		ctCartao := AllTrim(aRet[2])
	ElseIf aRet[1] == 3 // CPF
		ctCgc_Cpf := AllTrim(aRet[2])
	Endif
Endif
aRet       :=L010VLRENTR("CH", aDados)
If aRet[1]='9'
    Return(.F.)
Endif
cNumCupFis :=Space(7)
IFPegCupom( nHdlECF, @cNumCupFis)           // Pegar o Numero do Cupom da Impressora
ctDados	:= StrZero(Val(ctCartao),16)		// Comeca os dados para a consulta
ctDados	+= StrZero(aRet[6]*100,12,0)	    // Valor financiado
ctDados	+= StrZero(aRet[13],2,0)		    // Quantidade de parcelas financiadas ("cheques pre-datados"). Zero no caso de venda a vista
ctDados	+= StrZero(Val(ctJur),7,0)	        // Percentual de juros mensal do financiamento
ctDados	+= StrZero(aRet[3]*100,12,0)    	// Valor da entrada
ctDados	+= StrZero(Val(cNumCupFis)+1,7)    // Numero do cupom fiscal emitido pelo terminal
ctDados += Str(Year(dDatabase),4,0)+StrZero(Month(dDatabase),2,0)+StrZero(Day(dDatabase),2,0) // Data do cupom fiscal 
ctDados += StrTran(Time(),":","")			// Hora do cupom fiscal 
ctDados += StrZero(0,6,0) 					// Matricula do operador
ctDados += StrZero(0,17,0)					// Reservado
ctDados += StrZero(aRet[7]*100,12,0)   	// Valor da entrada paga com cheque a vista
ctDados += aRet[1]							// Acumula milhas ? '0'-Acumula  '1'-Nao acumula
ctDados += StrZero(0,12,0)					// Valor das milhas utilizadas para pagamento da entrada
ctDados	+= '1'								// Indicador da venda  '1'-Venda normal '2'-Venda forcada '3'-Contigencia
ctDados += StrZero(0,6,0)					// Matricula do funcionario que forcou a venda
ctDados += StrZero(0,30,0)					// Reservado
ctDados += StrZero(0,12,0)					// Reservado
ctDados += StrZero(0,12,0)					// Reservado
ctDados += '0'								// Opcao de parcelamento. Campo Reservado, preencher com zero.
ctDados += aRet[9]                          // Data compensacao do 1o cheque
ctDados += StrZero(0,3,0)					// Reservado
ctDados += StrZero(0,7,0)					// Reservado
ctDados += StrZero(0,1,0)   				// Reservado
ctDados += StrZero(Val(Subs(ctCgc_Cpf,1,11)),11) // CPF
ctDados += StrZero(0,6,0)    				    // Matricula do funcionario responsavel pela venda
ctDados += PadL(aRet[10],4,"0")				// No. do banco onde o cliente possui conta
ctDados += PadL(aRet[11],5,"0")			    // No. da agencia
ctDados += PadL(aRet[12],10,"0")				// No. da conta corrente
ctDados += '0000000000'                       	// Codigo da consulta realizada na prestadora do servico de protecao ao cheque (Ex.SERASA)
ctDados += '00000'                          	// Reservado
ctDados += aRet[14]                             // Numero do cheque dado como entrada. Preencher com zeros se a entrada nao for em cheque
ctDados += aRet[8]                              //Numeros dos cheques
/*********************************************
//Lay-out da Transacao cheque a vista funcionando
ctDados := '9000019990245595'              // Numero do Cartao
ctDados += '000000000000'                  // Valor financiado
ctDados += '00'                            // Quantidade de parcelas financiadas ("cheques pre-datados"). Zero no caso de venda a vista
ctDados += '0000000'                       	// Percentual de juros mensal do financiamento
ctDados += '000000003000'                  	// Valor da entrada
ctDados += '0009007'                        // Numero do cupom fiscal emitido pelo terminal
ctDados += '20021203'                       // Data do cupom fiscal
ctDados += '090037'                        	// Hora do cupom fiscal
ctDados += '000000'                        	// Matricula do operador
ctDados += '00000000000000000'             	// Reservado
ctDados += '000000003000'                  	// Valor da entrada paga com cheque a vista
ctDados += '0'                             	// Acumula milhas ? '0'-Acumula  '1'-Nao acumula
ctDados += '000000000000'                  	// Valor das milhas utilizadas para pagamento da entrada
ctDados += '1'                             	// Indicador da venda  '1'-Venda normal '2'-Venda forcada '3'-Contigencia
ctDados += '000000'                        	// Matricula do funcionario que forcou a venda
ctDados += '000000000000000000000000000000'	// Reservado
ctDados += '000000000000'                  	// Reservado
ctDados += '000000000000'                  	// Reservado
ctDados += '0'                             	// Opcao de parcelamento. Campo Reservado, preencher com zero.
ctDados += '20021203'                       // Data compensacao do 1o cheque
//ctDados += '00000000'                       // Data compensacao do 1o cheque
ctDados += '000'                           	// Reservado
ctDados += '0000000'                       	// Reservado
ctDados += '0'                             	// Reservado
ctDados += '00000000000'                    // CPF
ctDados += '000000'                        	// Matricula do funcionario responsavel pela venda
ctDados += '0341'                          	// No. do banco onde o cliente possui conta
ctDados += '01024'                         	// No. da agencia
ctDados += '1234567890'                    	// No. da conta corrente
ctDados += '0000000000'                    	// Codigo da consulta realizada na prestadora do servico de protecao ao cheque (Ex.SERASA)
ctDados += '00000'                         	// Reservado
ctDados += '100011'                         //Numeros dos cheques
*********************************************/
//Ponto de entrada para poder alterar a Taxa de juros da venda, com Cheque
If ExistBlock("LJTEFDCH")
	ctDados:=ExecBlock("LJTEFDCH",.F.,.F.,{ctDados})
Endif   
aRet := L010MonEnvResp( AllTrim(cTrans), "C",,,nX )
If (aRet[ 1 ] == 1)
	lRet    := .T.
	lAceite := .T.
	lCCS    := .T.
Else
	lRet:= .F.
Endif

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ L010CFiniºAutor  ³Marcos Alves        º Data ³  19/12/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CFini(cConsulta, aTipo, oTipo)

aTipo:=If(Upper(Alltrim(cConsulta))=='VENDA',;
         {'Parcelado a vista','Parcelado pre-datado'},;
         {'Rotativo a Vista','Parcelado vista'})
oTipo:SetItems(aTipo)
oTipo:Refresh()

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ L010TCARTºAutor  ³Marcos Alves        º Data ³  19/12/02    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T      	                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºData      ³Analista       ³Manutencao Efetuada                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º03/09/05  ³Andrea Farias  ³Passagem de parametro que contem o CPF/CNPJ  º±±
±±º          ³               ³DEFAULT na inicializacao do campo para identiº±±
±±º          ³               ³ficar o cliente.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º20/01/06  ³Thiago Honorato³Passagem de parametro que contem o array c/  º±±
±±º          ³               ³informacoes de variaveis do tipo STATIC      º±±
±±º          ³               ³aVarTemplate	                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º20/04/07  ³Norbert Waage  ³Criacao do bloqueio do campo CPF/CNPJ para   º±±
±±º          ³               ³nao permitir a alteracao do CPF/CNPJ quando  º±±
±±º          ³               ³este ja estiver preenchido pelo televendas.  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010TCART(	cRet, cCpfDefault, aVarTemplate )

Local nTipCar       := 0
Local nMV_LJIDCLI   := SuperGetMV("MV_LJIDCLI",,1)  	//Controla a forma de apresentacao da tela de identificacao do cliente
//1 - CPF;2 - CNPJ;3 - Ambos
Local nContVld      := 1 								// Variavel auxiliar para controlar a digitacao do cartao
Local lCPF          := .T.           					//Determina se o documento de identificacao do cliente eh CPF
Local oDlg
Local otCgc_Cpf                     					// Objeto do CPF
Local oCNPJ                         					// Objeto do CNPJ
Local oCheckDoc                     					// Objeto do check box que define se o documento eh CPF ou CNPJ
Local aRet          := {4,''}
Local sDado008
Local cStrDocID     := ""    							//String do documento de identificacao do cliente(CPF/CNPJ)
Local cCNPJ         := Space(14)		 				//Numero do CNPJ
Local lCRDOPCAOt    := ExistTemplate("CRDOPCAO")		// verifica se existe o PONTO DE ENTRADA CRDOPCAO
Local lCRDTCARTt    := ExistTemplate("CRDTCART")	    // verifica se existe o PONTO DE ENTRADA CRDTCART
Local lBloqCpf		:= .F.

Private lTefMult	:= SuperGetMV("MV_TEFMULT", NIL, .F.)

DEFAULT cPTerm		:= StrZero( LjGetStation("TERMTEF"), 3 )
DEFAULT cCpfDefault := ""								// Esse campo e obrigatoriamente iniciado por DEFAULT 
														//para que nao ocorra problemas com os outros fontes que executam essa funcao
DEFAULT aVarTemplate := {"","",""}// Array que contera valores de Variaveis do tipo 'STATIC'
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Estrutura do array aVarTemplate  - Template Drogaria                 ³
//³---------------------------------------------------------------------³
//³-    aVarTemplate[1]  =  codigo do cliente                           ³
//³-    aVarTemplate[2]  =  loja                                        ³
//³-    aVarTemplate[3]  =  numero do cartao                            ³
//³---------------------------------------------------------------------³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajusta as variaveis cDirT_Tx, cDirT_Rx e cPTerm pois essa     ³
//³função pode ser chamada de fora do LOJA010T                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cDirT_Tx == Nil
	cDirT_Tx := LjGetStation("DIRTTX")
Endif

If cDirT_Rx == Nil
	cDirT_Rx := LjGetStation("DIRTRX")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Limpar o diretorio de transamissão de mensagens                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aEval( Directory( cDirT_Tx + "*." + cPTerm ),{ |aFile| FErase( cDirT_Tx + aFile[ 1 ] ) } )

// Limpar o diretorio de Recepção de mensagens
aEval( Directory( cDirT_Rx + "*." + cPTerm ),{ |aFile| FErase( cDirT_Rx + aFile[ 1 ] ) } )

While .T.
	//Inicializa as variaveis
	ctDat_Venc := Space(8)
	ctCgc_Cpf  := Space(11)
	ctRede_H   := ' '
	ctCartao   := Space(16)
	
	If SuperGetMV("MV_INTEEMS") .OR. CrdxInt()
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Ponto de Entrada de Templates para selecao do tipo de identificacao do cliente.      ³
		//³Chamado da tela do Front Loja. Acrescenta a opcao de matricula para conveniados      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCRDTCARTt
			nTipCar := ExecTemplate("CRDTCART",.F.,.F.)
			
		ElseIf SuperGetMV("MV_INTEEMS") .OR. CrdxInt()
			
			If nMV_LJIDCLI == 1
				cStrDocID  := "CPF"
			ElseIf nMV_LJIDCLI == 2
				cStrDocID  := "CNPJ"
			ElseIf nMV_LJIDCLI == 3
				cStrDocID  := "CPF/CNPJ"
			Endif
			If Empty( LjGetStation("PINPAD") )
				
                                       
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Alterado  a posicao dos botoes e o texto para diferenciar            ³
			//³quando deve ser enviado para o crediario e quando e TEF.             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	  			DEFINE MSDIALOG oDlg TITLE IIF( !Empty(cCPFDefault), STR0042,STR0031 ) FROM 13,10 TO 18,70 OF GetWNDDefault()  //"Tipo de Cartão"				
					@ 10,005 BUTTON IIF( !Empty(cCPFDefault), STR0041 ,STR0032)	SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )  //"Não Magnético"
					@ 10,85 BUTTON cStrDocID	SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
					@ 10,165 BUTTON STR0033		SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )  //"Abandona"
					
				ACTIVATE MSDIALOG oDlg CENTERED
			Else
				                                                                     
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Alterado  a posicao dos botoes e o texto para diferenciar            ³
				//³quando deve ser enviado para o crediario e quando e TEF.             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DEFINE MSDIALOG oDlg TITLE STR0031 FROM 13,21 TO 18,96 OF GetWNDDefault()  //"Tipo de Cartão"					
					@ 10,005 BUTTON IIF( !Empty(cCPFDefault), STR0040 ,STR0034) SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )  //"Magnético - TEF"
					@ 10,078 BUTTON IIF( !Empty(cCPFDefault), STR0041 ,STR0032) SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )  //"Não Magnético - TEF"
					@ 10,151 BUTTON cStrDocID       SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
					@ 10,224 BUTTON STR0033         SIZE 70,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )  //"Abandona"
					
				ACTIVATE MSDIALOG oDlg CENTERED
			Endif
		Endif
		
		If (nTipCar == 1)
			// Se tipo de cartao = magnetico, fazer a leitura do cartao
			nOpd := If( L010LeCartao("C"), 1, 4 )
			If !empty(ctCartao)
				aRet:={nTipCar,ctCartao}
			Endif
		ElseIf (nTipCar == 2)
			// Se tipo de cartao = nao magnetico, coletar o numero
			nOpd := 3
			DEFINE MSDIALOG oDlg TITLE STR0035 FROM 13,21 TO 21,60 OF GetWNDDefault()  //"Dados do Cartão"
				@	.074,.1 TO 4,19
				@ 06,12 SAY		STR0036	                        SIZE 130,10 OF oDlg PIXEL  //"Informe o número do cartão:"
				@ 06,88 MSGET	ctCartao						SIZE 055,10 OF oDLG PIXEL PICTURE "@!" VALID VldGetCartao(@nContVld)

				//"Informe a data do cartão:","Informe a validade:"
				@ 18,12 SAY		If(lInfoCard,STR0037,STR0038) SIZE 130, 10 OF oDlg PIXEL
				@ 18,88 MSGET	ctDat_Venc						SIZE 024,10 OF oDlg PIXEL PICTURE If(lInfoCard,"@R 99/9999","@R 99/99") VALID L010ValidDt( ctDat_Venc )
		
				DEFINE SBUTTON FROM 35,090 TYPE  1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
				DEFINE SBUTTON FROM 35,120 TYPE  2 PIXEL ACTION ( nOpd:=3,oDlg:End() ) ENABLE OF oDlg
		
			ACTIVATE MSDIALOG oDlg CENTERED
			If nOpd=1              
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso exista o Template, passa o parametro "ctDat_Venc"³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                If lCRDOPCAOt
                	aRet:={nTipCar,ctCartao,ctDat_Venc}
                Else
					aRet:={nTipCar,ctCartao}
				Endif
			Endif
		ElseIf (nTipCar == 3) .AND. (SuperGetMV("MV_INTEEMS") .OR. CrdxInt())
			// Obtem o numero do Cartao, a partir do CPF
			nOpd := 2
			lCPF := .T.
			DEFINE MSDIALOG oDlg TITLE STR0039 FROM 13,21 TO 20,64 OF GetWNDDefault()  //"Consulta do cartão"
				
  				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se foi enviado via parametro o numero do CPF/CGC o sistema assume como valor inicial³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(cCpfDefault) .AND. ValType(cCpfDefault) == "C"
					
					Do case
						Case nMV_LJIDCLI == 1			//1 - CPF
							ctCgc_Cpf := cCpfDefault
							
						Case nMV_LJIDCLI == 2			//2 - CNPJ
							ctCgc_Cpf := cCpfDefault	
							
						Case nMV_LJIDCLI == 3			//3 - Ambos
							If Len(cCpfDefault) > 11
								cCNPJ := cCpfDefault
							Else
								ctCgc_Cpf := cCpfDefault
							Endif	
					Endcase	
					
					If IsInCallStack("TMKA271")
						lBloqCpf := .T.
					EndIf
					
				Endif	
				
				If nMV_LJIDCLI == 1
					@ 06,30 SAY	"CPF"+STR0030	SIZE 130,10 OF oDlg PIXEL      //"CPF para consulta:"
					@ 06,88 MSGET	ctCgc_Cpf SIZE 055,10 OF oDLG PIXEL PICTURE "@R 999.999.999-99" VALID (!EMPTY(ctCgc_Cpf) .AND. Cgc(ctCgc_Cpf)) WHEN !lBloqCpf
					
				ElseIf nMV_LJIDCLI == 2
					@ 06,30 SAY	"CNPJ"+STR0030	SIZE 130,10 OF oDlg PIXEL      //"CPF para consulta:"
					ctCgc_Cpf  := Space(14)
					@ 06,88 MSGET	ctCgc_Cpf SIZE 055,10 OF oDLG PIXEL PICTURE PesqPict("SA1","A1_CGC") VALID (!EMPTY(ctCgc_Cpf) .AND. Cgc(ctCgc_Cpf)) WHEN !lBloqCpf
				
				Else
					@ 05,05 CHECKBOX oCheckDoc VAR lCPF PROMPT "CPF" SIZE 30,08 OF oDLG PIXEL ON CHANGE (otCgc_Cpf:Refresh(),oCNPJ:Refresh(),IIf(lCPF,otCgc_Cpf:SetFocus(),oCNPJ:SetFocus()))
					@ 05,30 SAY	"CPF"+STR0030	SIZE 130,10 OF oDlg PIXEL      //"CPF para consulta:"

					@ 05,95 MSGET otCgc_Cpf VAR ctCgc_Cpf SIZE 055,10 OF oDLG PIXEL PICTURE "@R 999.999.999-99" VALID (Vazio(ctCgc_Cpf) .OR. Cgc(ctCgc_Cpf)) WHEN (lCPF .AND. !lBloqCpf)
					@ 20,30 SAY	"CNPJ"+STR0030	SIZE 130,10 OF oDlg PIXEL      //"CNPJ para consulta:"

					@ 20,95 MSGET oCNPJ VAR cCNPJ SIZE 055,10 OF oDLG PIXEL PICTURE PesqPict("SA1","A1_CGC") VALID (Vazio(cCNPJ) .OR. Cgc(cCNPJ)) WHEN (!lCPF .AND. !lBloqCpf)
				Endif
				
				DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
				DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=2,oDlg:End() ) ENABLE OF oDlg
			
			ACTIVATE MSDIALOG oDlg CENTERED
			If nOpd == 1
				If lCRDTCARTt
					// Bloco de codigo criado pela equipe de Template (somente as 3 linhas abaixo)
					ctDat_Venc := Posicione("SA1",3,xFilial("SA1")+ctCgc_Cpf,"A1_VENCLC")
					ctDat_Venc := DtoS(ctDat_Venc)
					ctDat_Venc := SubStr(ctDat_Venc,5,2)+SubStr(ctDat_Venc,3,2)	//somente mes e ano						
				Endif
				If cRet=='C' 								// Opcao de retornar o numero do cartao
					_aDados:={"36"}       					// Codigo da Rede EMS= 36
					AAdd(_aDados,"04")  					// Tipo da Consulta   - '04' - Consulta Generica
					sDado008 := "001"   					// Codigo da consulta - '001'- Situacao do cliente
					sDado008 += "02"      					// Tipo de entrada    - '04' - Numero do CPF e Data inicio
					sDado008 += PadL(AllTrim(ctCgc_Cpf),11,"0")
					AAdd(_aDados,sDado008)
					Loja010t("P","CO",@_aDados,.F.,.F.)
					//Retorna o numero do cartao
					If lCRDTCARTt					
						//criado pela equipe de Templates para verificar a data de validade do Cartao
						aRet:={nTipCar,Subs(_aDados[1],23,16),ctDat_Venc}
					Else
						aRet:={nTipCar,Subs(_aDados[1],23,16)}
					Endif
				Else
					If Empty(ctCgc_Cpf)
						ctCgc_Cpf  := cCNPJ
					Endif
					If lCRDTCARTt					
						//criado pela equipe de Templates para verificar a data de validade do Cartao
						aRet:={nTipCar,ctCgc_Cpf,ctDat_Venc}
					Else
						aRet:={nTipCar,ctCgc_Cpf}
					Endif
				Endif
			Else
				Loop
			Endif
		Endif
		
 		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tratamento para entrada do numero de matricula para identicacao do conveniado³
		//³quando houver template de drogaria. So abre esta opcao se for para pegar a   ³
		//³matricula do funcionario (nTipCar == 5)                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lCRDOPCAOt .And. nTipCar == 5
			aRet  := ExecTemplate("CRDOPCAO",.F.,.F.,{nTipCar,aRet,ctCartao,aVarTemplate})
		Endif
	Endif
	Exit
End

Return (aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ LOJA015T ºAutor  ³Marcos Alves        º Data ³  19/12/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Aquisicao de Milhas com outras forma de pagamento          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Loja030T()

If lUsaTef
   LOJA010T("P","AM")
Else
	Help(" ","1","NAOTEF")
Endif
Return (NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ LOJA015T ºAutor  ³Marcos Alves        º Data ³  19/12/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Aquisicao de Milhas com outras forma de pagamento          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Loja031T()

If lUsaTef
   LOJA010T("P","EM")
Else
	Help(" ","1","NAOTEF")
Endif

Return (NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010cTefMult ³ Autor ³ Marcos Alves      ³ Data ³ 09/01/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ ExpC1 - Tipo da operacao                                   ³±±
±±³          ³ ExpL2 - Verifica se tem TEF pendente						  ³±±
±±³          ³ ExpL3 - Determina se abandona a transacao TEF           	  ³±±
±±³          ³ ExpL4 - Informa se eh baixa de titulo                   	  ³±±
±±³          ³ ExpA5 - Array auxiliar para baixa de titulo             	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 - Numero da percela da Transacao Multipla            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CTefMult(	cOpera, lTemTEFPend, lAbandonaTEF, lTitulo, ;
								aTefTmp, lBaixa )
Local nValor	  := 0
Local oDlg
Local cContr	  := Space(9)
Local cSep 		  := Chr( 4 )
Local lLeu		  := .T.
Local lRet		  := .F.
Local nOpc1 	  := 0
Local cTrans      := ""
Local aRetorno	  := {0}
Local cCaption    := ""
Local cData       := "  /  "
Local cFormPg     := ""
Local aLeTef	  := {}
Local nValorBak   := 0
Local nParc       := 0
Local nPos        := 0
Local nY          := 0
Local cMsgTit     := "Cartão "
Local cMsgDados   := "Dados  "
Local cNumCupFis  := '0000000'
Local aTefBak     := {}
Local lEstTecBan  := .F.
Local lSaiCanc	  := .T.
Local cNota 	  := SL1->L1_DOC
Local cSerie 	  := SL1->L1_SERIE	
Local cTitulo	  := ""
Local nLin		  := 6	
Local lChkSLV     := ChkFile("SLV")            // Verifica se existe o arquivo SLV
Local nTentativa  := 0                         // Tentativas para leitura do cartao

DEFAULT lTitulo   := .F.				// Indica eh baixa de titulo
DEFAULT aTefTmp	  := {}					// Array auxiliar para baixa de titulo
DEFAULT lBaixa    := .F.				// Define se eh uma baixa

ctRg := Space(15)

//Alimentar aTefMult com as parcelas do SL4
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³aTefMult[1]Moeda,                    ³
//³        [2]Administradora,           ³
//³        [3]Entrada,                  ³
//³        [4]Total da Transação,       ³
//³        [5]Número parcelas}          ³
//³        [6][1]Data PrimeiraParcela   ³
//³        [6][2]Data Ultima Parcela    ³
//³        [7]Resposta da Transação     ³
//³        [8]Resultado da Transação    ³
//³        [9]Cconfirmacao da Transacao ³
//³        [10]Numero da Parcel no Apgto³
//³        [11]Texto de Promissoria     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aTefMult:={} 

If !lTitulo
	DbSelectArea("SL4")
	SL4->(DbSetorder(1))
	If SL4->(DbSeek( xFilial("SL4") + SL1->L1_NUM ))
		If Empty(SL4->L4_DOCTEF) .AND. SL1->L1_PARCELA==1
			/*Esta situação ocorre nos casos de trocas onde as multiplas  
			transações não estavam previstas pois é efetuada pelo VB   
			Pegar os dados gravados no proprio SL1                     */
			If SL1->L1_VENDTEF=='S' .AND. Val(SL1->L1_DOCCANC)=0
				aadd(aTefMult,{SL4->L4_FORMA,SL4->L4_ADMINIS,0,0,0,{ctod(''),ctod('')},{},.F.,.F.,{},{}})
				aTefMult[1][3]:=SL4->L4_VALOR
				aTefMult[1][4]:=SL4->L4_VALOR
				aTefMult[1][5]:=1
				//Inserindo o numero da parcela correspondente ao SL4
				aadd(aTefMult[1][10],StrZero(1,2))
				// Data da ultima parcela
				aTefMult[1][6][2]:=SL4->L4_DATA
				// Data da primeira parcela
				aTefMult[1][6][1]:=SL4->L4_DATA
				// Nao mudar a ordem dos 6 primeiros campos, pois e utilizado para comparacao
				aTefMult[1][7]	:=	{	SL1->L1_DATATEF	, ;
										SL1->L1_HORATEF	, ;
										SL1->L1_DOCTEF	, ;
										SL1->L1_AUTORIZ	, ;
										SL1->L1_INSTITU	, ;
										SL4->L4_FORMA	, ;
										SL1->L1_TIPCART	, ;
										SL1->L1_EMISNF	, ;
										SL4->L4_VALOR}
			Endif
			
		Else
			
			While SL4->L4_FILIAL == xFilial("SL4") .AND. SL4->L4_NUM == SL1->L1_NUM .AND. !Eof()
				nParc++
				nPos:=0
				If SL4->L4_VENDTEF=='S' .AND. Val(SL4->L4_DOCCANC) == 0
					// Procurando se a parcela tem que ser somada
					For nY:= 1 to Len(aTefMult)
						If Len(aTefMult[nY][7])>0 .AND. ;
							aTefMult[nY][7][1]==SL4->L4_DATATEF	.AND.;
							aTefMult[nY][7][2]==SL4->L4_HORATEF	.AND.;
							aTefMult[nY][7][3]==SL4->L4_DOCTEF		.AND.;
							aTefMult[nY][7][4]==SL4->L4_AUTORIZ	.AND.;
							aTefMult[nY][7][5]==SL4->L4_INSTITU	.AND.;
							aTefMult[nY][7][6]==SL4->L4_FORMA		.AND.;
							aTefMult[nY][7][7]==SL4->L4_TIPCART
							nPos:=nY
							Exit
						Endif
					Next nY
					If nPos=0
						aadd(aTefMult,{SL4->L4_FORMA,SL4->L4_ADMINIS,0,0,0,{ctod(''),ctod('')},{},.F.,.F.,{},{}})
						nPos:=Len(aTefMult)
					Endif
					If SL4->L4_DATA==SL1->L1_EMISSAO
						aTefMult[nPos][3]+=SL4->L4_VALOR
					Endif
					aTefMult[nPos][4]+=SL4->L4_VALOR
					aTefMult[nPos][5]+=1
					
					//Inserindo o numero da parcela correspondente ao SL4
					aadd(aTefMult[nPos][10],StrZero(nParc,2))
					
					// Data da ultima parcela
					If Empty(aTefMult[nPos][6][2]).OR.aTefMult[nPos][6][2] < SL4->L4_DATA
						aTefMult[nPos][6][2]:=SL4->L4_DATA
					Endif
					
					// Data da primeira parcela
					If Empty(aTefMult[nPos][6][1]).OR.aTefMult[nPos][6][1] < SL4->L4_DATA
						aTefMult[nPos][6][1]:=SL4->L4_DATA
					Endif
					
					// Nao mudar a ordem dos 6 primeiros campos, pois e utilizado para comparacao
					aTefMult[nPos][7]:={	SL4->L4_DATATEF , ;
											SL4->L4_HORATEF , ;
											SL4->L4_DOCTEF  , ;
											SL4->L4_AUTORIZ , ;
											SL4->L4_INSTITU , ;
											SL4->L4_FORMA   , ;
											SL4->L4_TIPCART , ;
											SL1->L1_EMISNF  , ;
											SL4->L4_VALOR     }
				Endif
				dbSkip()
			End
		Endif
	Endif
ElseIf lChkSLV
	If !lBaixa
		L010CTela( @nLin	, @nValor	, @nValorBak, @cContr, ;
					@cData	, @lSaiCanc	, "Cancelamento de Recebimento - TEF", lTitulo)
		If lSaiCanc
			Loja011T()
			Return(.F.)
		Endif
	Endif
	AADD(aTefMult,{SLV->LV_FORMA, SLV->LV_INSTITU, 0, 0, 0, {CToD(''), CToD('')}, {}, .F., .F., {}, {}})	
	aTefMult[1][3] := 0
	aTefMult[1][4] := SLV->LV_VALOR
	aTefMult[1][5] := 1
	AADD(aTefMult[1][10],StrZero(1,2))
	// Data da ultima parcela
	aTefMult[1][6][2]:=SLV->LV_DATA
	// Data da primeira parcela
	aTefMult[1][6][1]:=SLV->LV_DATA
	
	// Nao mudar a ordem dos 6 primeiros campos, pois e utilizado para comparacao
	aTefMult[1][7]:={	SLV->LV_DATATEF	, ;
						SLV->LV_HORATEF	, ;
						SLV->LV_DOCTEF	, ;
						SLV->LV_AUTORIZ	, ;
						SLV->LV_INSTITU	, ;
						SLV->LV_FORMA	, ;
						SLV->LV_TIPCART	, ;
						SLV->LV_DATA	, ;
						0					}

Endif

If Empty(aTefMult)
	MsgStop("Cupom Fiscal não localizado!")
	Return(.F.)
Endif

If ExistBlock("LJTEFCNC")
	LjGrvLog(Nil, "Ponto de Entrada LJTEFCNC - Parametros:", {aTefMult,cOpera})
	
	aTefMult:=ExecBlock("LJTEFCNC",.F.,.F.,{aTefMult,cOpera})

	LjGrvLog(Nil, "Ponto de Entrada LJTEFCNC - Retorno:", aTefMult)
Endif

For nY :=1 To Len(aTefMult)
	aLeTef := {	aTefMult[nY][7][6] 			,; 	//L4_FORMA
				aTefMult[nY][7][5] 			,; 	//L4_INSTITU
				aTefMult[nY][7][3] 			,;	//L4_DOCTEF
				aTefMult[nY][7][8] 			,;  //L1_EMISNF
				aTefMult[nY][7][7] 			,;	//L4_TIPCART
				aTefMult[nY][4]    			,;  //L4_VALOR
				aTefMult[nY][4]            	,;  //L4_VALOR das parcelas da mesma transacao
				Alltrim(aTefMult[nY][7][1]),;
				aTefMult[nY][7][2]}            //L4_HORATEF
				
	nParCred  	:= aTefMult[nY][5]
	ctTrilha_2 	:= ""
	cFormPg  	:= AllTrim(aLeTef[__FORMPG])
	cCaption 	:= "Cancelamento Venda"
	aRetorno 	:= {0}

    //Salvar dados da Trn, para o caso de falha, poder restaurar
    aTefBak		:= Aclone(aTefMult[nY][7])

	While .T.
		//Cancelamento CartÆo de Cr‚dito
		lRet        := .F.
   		lEstTecBan 	:= .F.		
   		nTentativa++  

		If cFormPg == "CC"
			//Verifica se a administradora do Cartao e a Infocard
			lInfoCard	:= .F.
			If (Upper(AllTrim(aLeTef[__INSTITU])) $ __INFOCARD)
				lInfoCard   := .T.
				cTrans  	:= "F0"
				If lVer300
					ctFuncao:="38"
				Else
					ctFuncao:="30"
				Endif
			Endif				
			
   			cMsgTit  := "Cartão " + AllTrim(Subst(aTefMult[nY][2],7))+" Doc.: "+AllTrim(aTefMult[nY][7][3])
   			cMsgDados:= "Dados  " + AllTrim(Subst(aTefMult[nY][2],7))+" Doc.: "+AllTrim(Str(Val(aTefMult[nY][7][3])))
	        
			ctOrcam   	:= aLeTef[__DOCTEF]
			ctDat_Vend	:= StrZero(Day(aLeTef[__EMISNF]),2) + StrZero(Month(aLeTef[__EMISNF]),2) + Str(Year(aLeTef[__EMISNF]),4)
			ctData		:= StrZero(Day(dDatabase),2)+StrZero(Month(dDatabase),2)+Str(Year(dDatabase),4)
			ctHoraVend	:= SubStr(Time(),1,2) + SubStr(Time(),4,2) + SubStr(Time(),7,2)
			nTipCar := 0
			
			If SuperGetMV("MV_INTEEMS")
				DEFINE MSDIALOG oDlg TITLE cMsgTit FROM 13,21 TO 18,78 OF GetWNDDefault()
				@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
				@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
				@ 10,115 BUTTON "CPF"	    	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=3,oDlg:End() )
				@ 10,170 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
			Else
				DEFINE MSDIALOG oDlg TITLE cMsgTit FROM 13,21 TO 18,64 OF GetWNDDefault()
				@ 10,005 BUTTON "Magnético"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=1,oDlg:End() )
				@ 10,060 BUTTON "Não Magnético"	SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=2,oDlg:End() )
				@ 10,115 BUTTON "Abandona"		SIZE 50,15 OF oDlg PIXEL ACTION ( nTipCar:=4,oDlg:End() )
			Endif
			ACTIVATE MSDIALOG oDlg CENTERED
			
			If nTipCar=4
			   lRet :=.F.
			   Exit
			Endif
			
			If nTipCar=1
				If lInfoCard
					ctTipOp  := "1"
					ctPlano := ""
				Else
					cTrans  := "36"
				Endif
				ctCartao := Space( 140 )
	     		lLeu   	 := L010LeCartao("C",nY,cOpera)

			ElseIf nTipCar=2
				If lInfoCard
					ctTipOp := "2"
					ctPlano := ""
				Else
					cTrans  := "37"
				Endif
				ctCartao 	:= Space(19)
				ctDat_Venc  := space(4)

				DEFINE MSDIALOG oDlg TITLE cMsgDados FROM 13,21 to 21,60  of GetWNDDefault()
				@	.074,.1 TO 4,19
				@	6,12 SAY "Informe o número do cartão: " SIZE 130, 10 OF oDlg PIXEL
				@	6,88 MSGET ctCartao SIZE  55, 10 OF oDLG PIXEL	Picture "@!" Valid L010VerNumCart( ctCartao )
				If !lInfoCard
					@ 18,12 SAY "Informe a validade: " SIZE 130, 10 OF oDlg PIXEL
					@ 18,88 MSGET ctDat_Venc SIZE 18,10 OF oDlg PIXEL Picture "@R 99/99" Valid L010ValidDt( ctDat_Venc )
				Endif
				DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION (lSaiCanc:=.F.,oDlg:End() ) ENABLE OF oDlg
				DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION (lSaiCanc:=.T.,oDlg:End() ) ENABLE OF oDlg				
				ACTIVATE MSDIALOG oDlg CENTERED
				ctCartao := AllTrim( ctCartao )
				
				//HOMOLOGACAO: Tratamento para a saida da tela
				If Empty(ctCartao) .OR. lSaiCanc
				   lRet  :=.F.
				   Exit
				Endif

	   		ElseIf (nTipCar == 3)
				If lInfoCard
					ctTipOp	:= "1"
					ctPlano := ""
				Else
					cTrans  := "37"
				Endif
	        	// Obtém o número do Cartão, a partir do CPF
	        	If SuperGetMV("MV_INTEEMS")
				    ctCartao   := LjConsCPF()
					If Alltrim(ctCartao)=="*"
	             		Loop
					ElseIf Empty( ctCartao )
			        	MsgInfo("CPF inválido")
					    Loop  // Ponto de correcao
	         		Endif
			        nOpd       := 3
			        ctDat_Venc := StrZero(Month(dDatabase),2) + SubStr( StrZero( YEAR(dDatabase) ,4) ,3,2)
			        lRet       := .T.
				    lCPFInfo   := .T.
		        Endif
			Endif
						
			If !lInfoCard .AND. At('C.C.S.',Upper(AllTrim(aLeTef[__INSTITU])))=0
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se nao conseguiu ler na primeira tentativa deve realizar uma nova      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lLeu .OR. (nTentativa > 1 .AND. !lLeu)
					lLeu := L010BinCar( "0" ,.F. ,.F. , nY )  	//Consulta ao Bin do Cartao
				Endif
			Endif
			nValor := aLeTEF[__CARTAO]

		ElseIf cFormPg == "CD"										//Cancelamento CartÆo de D‚bito
			cTrans := "23"
			lLeu   := L010LeCartao("D",nY,cOpera)
			If !lInfoCard
				if lLeu
					lLeu := L010BinCar( "1" , .F., .F.,nY )  		//Consulta ao Bin do Cartao
				Endif
			Endif
			nValor := aLeTef[__VLRDEBI]
		
		ElseIf cFormPg == "CH" .AND. Upper(AllTrim(aLeTef[__INSTITU])) $ __TECBAN
				//Buscando os dados do cheque para enviar a transação de cancelamento
				aTefChq	:= {}
				DbSelectArea("SEF")
				DbSetOrder(3) //EF_FILIAL+EF_PREFIXO+EF_TITULO+EF_PARCELA+EF_TIPO+EF_NUM+EF_SEQUENC
				If DbSeek(xFilial("SEF") + cSerie + cNota )
				   lEstTecBan := .T.
				   lLeu		  := .T.	
				   cTrans     := "70"
				   ctFuncao   := "7"
				   While !Eof() .AND. ;
				   		 SEF->EF_FILIAL  == xFilial("SL4") .AND. ;
				   		 SEF->EF_PREFIXO == cSerie .AND. ;
						 SEF->EF_TITULO  == cNota
						 aTefChq := {SEF->EF_BANCO,SEF->EF_AGENCIA,SEF->EF_CONTA,SEF->EF_NUM,SEF->EF_VALOR}
						 DbSkip()
				   End
				Endif
		Else
			MsgInfo("Não é possível efetuar o cancelamento deste documento!","Atenção")
			Exit	
		Endif

		cContr := AllTrim( Str( Val( aLeTef[__DOCTEF] ) ) )

		// Verifica se pode cancelar - Mudar para a função L010IsTrHabilita
		If lLeu
			If lVer300
				cData 	  := Subs(DToC(aLeTef[4]),1,5)
				If !lTitulo
					nValorBak := nValor
				
					//HOMOLOGACAO: O valor não pode vir da base, pois não gravamos os valores de Saque
					//o valor deve ser informado de acordo com o VALOR FINAL impresso no cupom.
					nValor	  := 0
				Endif

				If cFormPg == "CH"
					cTitulo	  := "Dados da Transação - Cheque"
					nValorBak := If(Len(aTefChq)>0,aTefChq[5],0)
				Else
					cTitulo	  := "Dados da Transação "+Alltrim(aLeTef[2])
				Endif			
				
				nLin := 6
	
				If !lTitulo
					L010CTela(	@nLin	, @nValor	, @nValorBak, @cContr, ;
								@cData	, @lSaiCanc	, cTitulo	, lBaixa)
	            Else
	            	lSaiCanc:=.F.
	            Endif
				//HOMOLOGACAO: Tratamento para a saida da tela
				If lSaiCanc
			   	   nOpc1 :=0
				   lRet  :=.F.
				   Exit
				Endif

				aLeTef[__DATATEF]:=Right(cData,2)+Left(cData,2)
				aLeTef[__DOCTEF] :=AllTrim( Str( Val( cContr ) ) )
				
				If cFormPg == "CD"
				   aLeTef[__VLRDEBI]:=nValor
				ElseIf cFormPg == "CC"
				   aLeTEF[__CARTAO] :=nValor
				Endif
				
            Endif

			ctValor	          := L010ValStr(If(cFormPg=="CD",aLeTef[__VLRDEBI],aLeTEF[__CARTAO]))
			ctDocument_ctData := Alltrim( Str( Val( aLeTef[__DOCTEF]) ) )+cSep+Right(aLeTef[__DATATEF],2)+Left(aLeTef[__DATATEF],2)
			
			If lVer300
				nOpc1:=1
				lLeu := .T.
			Else
				nOpc1 := 3
			Endif
			
			If nOpc1 == 3
				DEFINE MSDIALOG oDlg TITLE cCaption From 13,21 to 18,64	of GetWndDefault()
				@ 10,005 BUTTON "Confirma" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=1,oDlg:End() )
				@ 10,060 BUTTON "Redigita" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=2,oDlg:End() )
				@ 10,115 BUTTON "Abandona" SIZE 50, 15 OF oDlg PIXEL ACTION ( nOpc1:=3,oDlg:End() )
				ACTIVATE MSDIALOG oDlg CENTERED
			Endif
			
			If (nOpc1 = 2)
				Loop
			Endif

			If nOpc1 == 1 
				If cFormPg     	== "CD"
					ctValor	:= L010ValStr(aLeTef[__VLRDEBI])
				ElseIf 	cFormPg	== "CC"
					ctValor	:= L010ValStr(aLeTEF[__CARTAO])
				ElseIf 	cFormPg	== "CH"
					ctValor	:= L010ValStr( If(aLeTEF[__CARTAO]<>aTefMult[nY][3],aLeTEF[__CARTAO]-aTefMult[nY][3],aLeTEF[__CARTAO]))
				Endif
				ctCartao 	:= AllTrim( ctCartao )
				ctFuncao	:= "1"
			    lCcs := At('C.C.S.',Upper(AllTrim(aLeTef[__INSTITU])))<>0
			    
				If lCcs .AND. SuperGetMV("MV_TEFMILH", NIL, .F.)
				   IFPegCupom( nHdlECF, @cNumCupFis)          	// Pegar o Numero do Cupom da Impressora
				   cSaida	:= 'S' 								// Saida da consulta
				   cTrans	:= '50'								// Codigo da transacao
			       // Cancelamento dos cartOES CCS
			       If cFormPg=="CC"
						ctDados	:= '0000'			               	// * Versão da Mensagem. Identifica o mapeamento
						ctDados	+= StrZero(Val(cNumCupFis)+1,7)     // Numero do cupom fiscal da operacao de estorno
						ctDados += DtoS(aLeTef[__EMISNF])           // Data do cupom fiscal 
						ctDados += aLeTef[9]                        // Hora do cupom fiscal 
						ctDados += StrZero(0,6,0) 					// Matricula do funcionario PDV
						ctDados += StrZero(0,30,0)					// Nome do funcionario do PDV
			
						aTrans:={"50"				,;
						{ctRedeDest       		 	,;
						ctADM            		  	,;
						'1'                		  	,;  // Valor fixo
						'36'             		  	,;  // Codigo da rede
						'11'              		  	,;  // SubFuncao - Venda/troca de milhas 
						StrZero(Val(ctCartao),16)	,;   // Numero do Cartão ou Trilha 2
						ctValor          		  	,;  // Valor da Venda
				        aLeTef[__DOCTEF]          	,;  // Numero do Documento
				        Subs(aLeTef[__DATATEF],3,2)+Subs(aLeTef[__DATATEF],1,2)+Subs(DtoS(aLeTef[__EMISNF]),1,4) ,;  // Data da venda/troca
						ctDados } }                   // Dados adicionais da venda  
			       ElseIf cFormPg=="CH" .AND. !lEstTecBan
                        // Na transacao de cancelamento de cheque enviar somente o valor financiado
                        // erro na transação CCS.
			            ctDados     := Strzero(Val(aLeTef[__DOCTEF]),9) // Numero do Documento
			            ctDados     += StrZero(Val(ctCartao),16)   	    // Numero do Cartão ou Trilha 2
						ctDados     += strzero(Val(ctValor),12,0) 	    // Valor da Venda
						ctDados     += Strzero(0,11)                    // CPF (sempre 0, pois esta enviando CARTAO)
						ctDados		+= StrZero(Val(cNumCupFis)+1,7)      // Numero do cupom fiscal da operacao de estorno
						ctDados 	+= Subs(DtoS(aLeTef[__EMISNF]),1,4)+Subs(aLeTef[__DATATEF],1,2)+Subs(aLeTef[__DATATEF],3,2) // Data da venda/troca
						ctDados 	+= aLeTef[9]                    // Hora do cupom fiscal 
						ctDados 	+= StrZero(0,6,0)				// Matricula do funcionario PDV
						ctDados 	+= StrZero(0,30,0)  			// Nome do funcionario do PDV
			
						aTrans := 	{ 	"50"		, ;
									{ 	ctRedeDest	, ;
										ctADM		, ;
										'1'			, ;		// Valor fixo
										'36'		, ;		// Codigo da rede
										'03'		, ;		// SubFuncao - Autorizacao Generica
										'002'		, ;		// Codigo da Operacao  - Cancelamento de Venda Cheque
										'02'		, ;		// Tipo da Entrada  - Entrada do Cancelamento
										'I'			, ;		// Saida da Consulta - Impressora
										ctDados } }			// Dados adicionais da venda  
			       Endif
                   aRetorno := L010MonEnvResp( cTrans,"C",,aTrans,nY)
			    ElseIf cFormPg=="CH" .AND. lEstTecBan .AND. Len(aTefChq) > 0
					cTrans			 := "70"
					ctFuncao		 := "7" 
					ctValor			 := L010ValStr( nValor )
					ctBanco			 := Left( AllTrim( aTefChq[1]  ) , 4  )
					ctAgencia		 := Left( AllTrim( aTefChq[2]  ) , 5  )
					ctCCorrente	     := Left( AllTrim( aTefChq[3]  ) , 10 )
					ctNum_Cheq		 := Left( AllTrim( aTefChq[4]  ) , 7  )
					ctDt_H			 := StrZero(Day(aLeTef[__EMISNF]),2) + StrZero(Month(aLeTef[__EMISNF]),2) + Str(Year(aLeTef[__EMISNF]),4)
					ctCompens		 := "018"
					ctDad_Chq        := ctCompens + cSep + ctBanco + cSep + ctAgencia + cSep + ctCCorrente + cSep + ctNum_Cheq
					ctNum_H			 := cContr
					aRetorno 		 := L010MonEnvResp( AllTrim(cTrans),,,,nY)
			    Else
 				    aRetorno 		 := L010MonEnvResp( AllTrim(cTrans),,,,nY)
				Endif
				
				If aRetorno[ 1 ] == 1
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Se resposta = ok, Exibir e confirmar                         ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cTipVenCon := "S"
					lAceite    :=.T.
					lRet       :=.T.                         
				Else
	        	    If cFormPg=="CH" .AND. lEstTecBan
		        	    Aviso("Atenção","Só é necessário o cancelamento dos Cheques GARANTIDOS TECBAN!",{'OK'})
		        	    lRet := .T.
					Endif		        	    
		        Endif		        
			Endif
		Endif

	    If !lRet
			If MsgYesNo( "Deseja efetuar novamente a transação TEF?" )
			    aTefMult[nY][7] := Aclone(aTefBak)
				Loop
	        Else
	        	If !lTemTEFPend
		        	lAbandonaTEF := .T.
		        Endif
		        Exit
		    Endif   
		Else
		   Exit    
	    Endif
	End

	If !lRet
	   Exit
	Endif   

	// Faz a confirmacao no servidor TEF
	L010NCNCNF(.T.)			    										
	
Next nY
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se resposta = ok, Exibir e confirmar						 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	cTipVenCon := "S"
	lAceite    :=.T.
	//Impressao do cupom TEF, atraves do relatorio gerencial
	lRet := LOJA010T( "I" , "X" )
	If lRet .AND. lTitulo .AND. lChkSLV
		If lTefMult
			aTefTmp := aClone(aTefMult)
		Else
			aTefTmp := aClone(aTefDados)
		Endif
		If !lBaixa
			L010AtuSlv(aTefTmp, lTefMult)
		Endif
	Endif
Endif

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010TCVar ºAutor  ³Vendas Clientes     º Data ³  21/01/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Limpa as Variaveis private antes de uma nova trn, para     º±±
±±º          ³ Multiplas TRN TEF                                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ LOJA010T                                                   º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010TCVar()

If ! Type("nMoedaCor") == "N"
	nMoedaCor := 1
Endif
If !Type("aTefDados") == "A"
	aTefDados := {}
Endif
ctGarantia			:= ""
ctDt_H 				:= Nil
ctNum_H				:= Nil
ctRede_H			:= Nil
ctValor 			:= ""
ctTrilha_2 		 	:= Nil
ctSenha 			:= "" 
ctData				:= Nil
ctMod_Entr 			:= "0"
ctDocument_ctData	:= Nil
ctDad_Parc			:= Nil
ctPlano				:= Nil
ctTaxa				:= ""
ctCartao			:= ""
ctDat_Venc			:= ""
ctCod_Autor			:= Nil
ctCCorrent			:= Nil
ctTipo_Pes			:= Nil
ctCgc_Cpf			:= Nil	
ctBanco				:= Nil
ctAgencia			:= Nil
ctNum_Cheq			:= Nil
ctDt_ChQ_Ser		:= Nil
ctBco_Agenc			:= Nil
ctDt_Cheq_TelD		:= Nil
ctTipOp				:= Nil
ctTipPar			:= Nil
ctJur				:= Nil
ctParc_Val			:= Nil
ctTipVen			:= Nil
ctEntrada			:= Nil
ctTipCons 			:= "1"
ctDad_Chq			:= Nil
ctQCheque			:= "1"
ctOrcam    			:= Nil
ctCompens			:= "018"
ctHoraVend			:= ""
ctDat_Vend			:= Nil
ctFuncao			:= ""
cWork   			:= Nil
cModCart			:= ""
cInst				:= Nil
ctSucess 			:= "00"
ctRg 				:= Space(15)
cDados  			:= Space(80)		// Dados complementares da forma de pagamento para o SL4
ctCEP 				:= Space(9)
ctEnd	    		:= Space(50)
ctNumero			:= Space(10)
ctCompl 			:= Space(2)
dtDataAgen          := Nil

lCdc 				:= .F.
//lCcs				:= .F.
lAltera 			:= .T.
lFininvest 			:= .F.

nMod				:= 3
nOpc 				:= 2
nTotal 				:= 0
nOffSet             := Nil
ntOffSet		 	:= 0
nConsRot  			:= 0
nTime 				:= 1
nParcelas			:= 0
aPromis             := {}

Return (NIL)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³LJ010TSL4    ³ Autor ³ Vendas Clientes    ³ Data ³ 17/03/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a existencia dos campos na tabela SL4, necessarios  ³±±
±±³          ³ para as Multiplas transacoes TEF                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := LJ010TSL4()  									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> .T. ou .F.										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ Funcao devera ser eliminada na versao 8.11                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj010TSL4()
// Array para efetuar a verificação da existencia dos campos para gravacao
// dos dados da transacao TEF na tebala SL4, eliminar estas linhas na versao 8.11
Local lRet          := .T.
Local cCampo		:=	''
Local aCheckSL4     := {}

If SuperGetMV("MV_TEFMULT",NIL,.F.)
	
	aAdd( aCheckSL4,"L4_VENDTEF")
	aAdd( aCheckSL4,"L4_DATATEF")
	aAdd( aCheckSL4,"L4_HORATEF")
	aAdd( aCheckSL4,"L4_DOCTEF" )
	aAdd( aCheckSL4,"L4_AUTORIZ")
	aAdd( aCheckSL4,"L4_INSTITU")
	aAdd( aCheckSL4,"L4_DOCCANC")
	aAdd( aCheckSL4,"L4_DATCANC")
	aAdd( aCheckSL4,"L4_HORCANC")
	aAdd( aCheckSL4,"L4_NSUTEF" )
	aAdd( aCheckSL4,"L4_TIPCART")
	aAdd( aCheckSL4,"L4_FORMPG"	)
	
	// Array para efetuar a verificação da existencia dos campos para gravacao
	// dos dados da transacao TEF na tebala SL4, eliminar estas linhas na versao 8.11
	cCampo:=''
	Ascan(aCheckSL4,{|X| cCampo+=If( SL4->(ColumnPos(X))=0,X+' - ','')})
	If !Empty(cCampo)
		Aviso( "Atenção","Para efetuar multiplas transaços TEF, será necessário criar o(s) campo(s): "+cCampo, {"&Ok"},, "Contate o Administrador" )
		lRet     := .F.
	Endif
Endif

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010ATVDISC  ³ Autor ³ Vendas Clientes    ³ Data ³ 23/06/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ativa o gerenciador padrao do TEF discado e define a rede  ³±±
±±³          ³ que serao enviado as transacoes                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010ATVDISC(aDados)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ aDados - Dados das parcelas da transacao TEF         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Indice do array aTefDisc, que determina a Rede	  ³±±
±±³       	 ³          que serao enviados as transacoes                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010ATVDISC(aDados)

Local nI       		:= 0						// Variável do FOR
Local nParc    		:= 1						// Variavel de parcela
Local nP       		:= 0						// Variavel de rede
Local lRet     		:= .F.						// Retorno da criacao do arquivo
Local cRede    		:= ''						// Nome da Rede
Local nRet     		:= 0						// Retorno da funcao
Local lLoadGP  		:= .F.						// Indica se ja carregou o gerenciador padrao REDECARD/VISANET/AMEX
Local lRetTefAtivo	:= .F.						// Se houve retorno do arquivo no TEF Discado, é que o gerenciador TEF está ativo
Local cAuttarReq	:= "C:\AUTTAR_TEFIP\REQ\"	// Pasta da Transmissão do Gerenciador TEF-Auttar  
Local cAuttarResp	:= "C:\AUTTAR_TEFIP\RESP\"	// Pasta da Recepção do Gerenciador TEF-Auttar
Local cCadEstReq	:= ""						// Pasta da Transmissão no Cadastro de Estações (LOJA121)
Local cCadEstResp	:= ""						// Pasta da Recepção no Cadastro de Estações (LOJA121)

If ExistBlock("LJDrvTEF")
	LjGrvLog( Nil, "Antes da Execução do P.E. LJDrvTEF")
	cDrvTEFDsc := ExecBlock("LJDrvTEF",.F.,.F.)
	LjGrvLog( Nil, "Retorno da Execução do P.E. LJDrvTEF", cDrvTEFDsc)
Else
	cDrvTEFDsc := "C"
EndIf

//Definindo qual a Rede da venda
If cOpera="V".AND.ValType(aDados) == "A"
   //Varendo as parcelas, para saber a rede da primeira
   For nP = 1 to Len(aDados)
	  If ValType(aDados[nP][4])=="C"
	     If Alltrim(Upper(aDados[nP][3]))$'CC#CD'
	     	cRede:=Alltrim(Upper(aDados[nP][4]))
	     	Exit
	     ElseIf Alltrim(Upper(aDados[nP][3]))$'CH' .AND. At("S",LjGetStation("TEFCONS")) <> 0
			If Substr(LjGetStation("TEFCONS"),8,1) == "S"
				cRede := 'REDECARD'
			Else	
				cRede:='TECBAN'
			EndIf	
			// Prioridade para o cartão de c´redito ou débito, por isso não sai mais do for.
			//Exit
	     Endif
	  Endif
   Next nP
Endif   		   
//Definindo qual a rede para o cancelamento
If cOpera$"EX"
	cRede:=AllTrim(Upper(SL1->L1_INSTITU))
Endif   		   
//Definicao da rede TECBAN, para consulta de cheques
If cOpera$"GC"
	cRede:='TECBAN'
Endif	
For nI:=1 to Len(aTefDisc)
	If nI == 5	// BANESECARD
		If !File(cDrvTEFDsc+aTefDisc[nI][4]).OR.;              
			(!Empty(cRede) .AND. !(aTefDisc[nI][1]$cRede))
	       Loop
    	Endif
		nHandle := FCreate(cDrvTEFDsc+aTEFDisc[nI][2]+"temp")
		If nHandle < -1
			Aviso("TEF Discado", "O módulo WTPDV Estação do TEF discado BANESECARD NÃO está instalado."+CHR(13)+CHR(10)+;
				"Instale o WTPDV Estação.",{"Ok"})
			Return nRet
		Else
			FClose(nHandle)
			FErase(cDrvTEFDsc+aTEFDisc[nI][2]+"temp")
			aTefDisc[nI][5]:= .T.
		Endif
	Else
	    // Ativa o Gerenciador padrao se existir os respectivos executaveis nos diretortios, definidos no array aTefDisc, ou
    	// somente a rede especificada nas transacoes definidas no cOpera
    	If !File(cDrvTEFDsc+aTefDisc[nI][4])
	       Loop
    	Endif
		// O gerenciador padrao dos tres (REDECARD/VISANET/AMEX) eh o mesmo
	    If (nI >= 2 .AND. nI <= 4) .OR. nI = 8
			// Chama o gerenciador padrao somente na primeira vez
			If !lLoadGP
				If SuperGetMV("MV_LJATVGP",.F., .T.)
					//Tenta ativar por 2 vezes
					For nParc := 1 to 2

						// ### "Verificando se o gerenciador padrão REDECARD/VISANET/AMEX do TEF discado está ativo" ### "Aguarde"
						LjMsgRun( STR0086, STR0087, { || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
						If !lRet
							// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
							If !SuperGetMV("MV_LJTSC") .AND. nParc == 1
							    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
								Aviso(STR0104, STR0109 + CRLF,{"Ok"})		// #"TEF Discado" #"Gerenciador padrão não esta ativo e será ativado automaticamente"
								WinExec(cDrvTEFDsc+aTefDisc[nI][4],3)
								Sleep(5)
								
								Loop
							Else
								MsgStop(STR0117)			// "O gerenciador padrão do TEF discado REDECARD/VISANET/AMEX nao esta ativo"
								Return nRet
							Endif
						Endif
						lLoadGP := .T.
						Exit
					Next nParc	
				Else
					For nParc := 1 to 50
						// ### "Verificando se o gerenciador padrão REDECARD/VISANET/AMEX do TEF discado está ativo" ### "Aguarde"
						LjMsgRun( STR0086, STR0087, { || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
						If !lRet
							// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
							If !SuperGetMV("MV_LJTSC")
							    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
								Alert(STR0118)		// "Gerenciador padrao nao esta ativo - favor ativar"
								Sleep(5)
								
								Loop
							Else
								MsgStop(STR0117)			// "O gerenciador padrão do TEF discado REDECARD/VISANET/AMEX nao esta ativo"
								Return nRet
							Endif
						Endif
						lLoadGP := .T.
						Exit
					Next nParc
				EndIf
			 Endif  
		ElseIf L010IsDirecao(nI) .OR. L010IsPayGo(nI) .OR. L010IsAuttar(nI)  //TEF DIRECAO / Pay&GO
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ativa o Gerenciador Padrao ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SuperGetMV("MV_LJATVGP",.F.,.T.)
				LjMsgRun("Verificando se o gerenciador padrão "+aTefDisc[nI][1]+" do TEF está ativo","Aguarde",;
				{ || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
				If !lRetTefAtivo
					lRetTefAtivo := lRet
				EndIf
				If !lRet
					If !SuperGetMV("MV_LJTSC")
						If WinExec(cDrvTEFDsc + aTefDisc[nI][4], 2) == 0
							lRet := .T.
						EndIf
					EndIf
				EndIf
			Else
				lRet := .T.
			EndIf
			
			If !lRet
				MsgStop("O gerenciador padrão do TEF discado " + aTefDisc[nI][1] + " não está ativo")
			EndIf
			
	    ElseIf nI == 7 //ATIVANDO GERENCIADOR DA BARISUL
			If SuperGetMV("MV_LJATVGP",.F.,.T.)
	    	    For nParc := 1 to 2
		    	    // Gera o arquivo de solicitacao de ativacao do terminal no respectivo diretorio da rede e espera o arquivo de resposta
					LjMsgRun("Verificando se o gerenciador padrão "+aTefDisc[nI][1]+" do TEF discado está ativo","Aguarde",;
					{ || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
					If !lRet
						// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
						If !SuperGetMV("MV_LJTSC") .AND. nParc == 1
						    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
							Aviso(STR0104, STR0109,{"Ok"})		// #"TEF DISCADO" #"Gerenciador padrão não esta ativo e será ativado automaticamente"
							WinExec(cDrvTEFDsc+aTefDisc[nI][4],2)
							Sleep(5)
					   		Loop
						Else
							MsgStop("O gerenciador padrão do TEF discado "+aTefDisc[nI][1]+" não está ativo")
							Return nRet
						Endif
					Endif
					Exit
				Next nParc	
			Else			
				For nParc := 1 to 50
		    	    // Gera o arquivo de solicitacao de ativacao do terminal no respectivo diretorio da rede e espera o arquivo de resposta
					LjMsgRun("Verificando se o gerenciador padrão "+aTefDisc[nI][1]+" do TEF discado está ativo","Aguarde",;
					{ || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
					If !lRet
						// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
						If !SuperGetMV("MV_LJTSC")// .AND. nParc == 1
						    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
							Alert("Gerenciador padrao nao esta ativo - favor ativar")
							Sleep(5)
							
							Loop
						Else
							MsgStop("O gerenciador padrão do TEF discado "+aTefDisc[nI][1]+" não está ativo")
							Return nRet
						Endif
					Endif
					Exit
				Next nParc
			EndIf	
	    Else
			If SuperGetMV("MV_LJATVGP",.F.,.T.)
				For nParc := 1 to 2
		    	    // Gera o arquivo de solicitacao de ativacao do terminal no respectivo diretorio da rede e espera o arquivo de resposta
					LjMsgRun("Verificando se o gerenciador padrão "+aTefDisc[nI][1]+" do TEF discado está ativo","Aguarde",;
					{ || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
					If !lRet
						// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
						If !SuperGetMV("MV_LJTSC") .AND. nParc == 1
						    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
							Aviso(STR0104, STR0109,{"Ok"})		// #"TEF DISCADO" #"Gerenciador padrão não esta ativo e será ativado automaticamente"
							WinExec(cDrvTEFDsc+aTefDisc[nI][4],2)
							Sleep(5)
							Loop
						Else
							MsgStop("O gerenciador padrão do TEF discado "+aTefDisc[nI][1]+" não está ativo")
							Return nRet
						Endif
					Endif
					Exit
				Next nParc						
			Else
				For nParc := 1 to 50
		    	    //Ativa HiperLink do HiperTef
	                If aTefDisc[nI][2] == ':\HIPERTEF\HIPERLINK\REQ\'      
					    WinExec(cDrvTEFDsc + SUBSTR(aTefDisc[nI][2],1,21) + 'HIPERLINK.EXE',1)
				   	Endif
		    	    // Gera o arquivo de solicitacao de ativacao do terminal no respectivo diretorio da rede e espera o arquivo de resposta
					LjMsgRun("Verificando se o gerenciador padrão "+aTefDisc[nI][1]+" do TEF discado está ativo","Aguarde",;
					{ || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(STR(nSeqArq,10)), "999-999 = " + cRegFim},nI)})
					If !lRet
						// Se estiver usando o TEF DISCADO com Terminal Services, a inicializacao do discador do TEF deve ser manual
						If !SuperGetMV("MV_LJTSC")
						    // Caso o gerenciador padrao da rede nao responda a solicitacao da transacao, avisa que ira ativar o gerenciador
							Alert("Gerenciador padrao nao esta ativo - favor ativar")
							Sleep(5)
							Loop
						Else
							MsgStop("O gerenciador padrão do TEF discado "+aTefDisc[nI][1]+" não está ativo")
							Return nRet
						Endif
					Endif
					Exit
				Next nParc			
			EndIf
		Endif
		aTefDisc[nI][5]:= .T.
	Endif
	nRet := nI
Next nI

If Ascan(aTefDisc,{|X| X[5]}) == 0
	MsgStop("Não foi encontrado nenhum gerenciador padrão de TEF discado.")
	If lHomTEF
		Final(STR0111)		// ###"O sistema sera finalizado."
	EndIf
Endif

If !Empty(cRede).AND.nRet=0
	MsgStop("O gerenciador padrão do TEF discado "+cRede+" não está ativo")
Endif

If !lRetTefAtivo .AND. L010IsAuttar(nRet)	//Se o gerenciador TEF não estiver ativo, e se TEF-Auttar		
	cCadEstReq	:= LJGetStation("DIRTTX")	//Cadastro de Estações, Transmissão de Arquivos
	cCadEstResp := LJGetStation("DIRTRX")	//Cadastro de Estações, Recepção de Arquivos
	If cAuttarReq <> cCadEstReq		//Verifico se a pasta de transmissão do Auttar está configurado corretamente no Cadastro de Estações
		MsgAlert(STR0147+CRLF+CRLF+STR0148+cAuttarReq)
							//"Configuração Incorreta!"###"No cadastro de Estações (LOJA121), aba TEF, favor preencher no campo Transmissão dos Arquivos a informação "
	EndIf
	If cAuttarResp <> cCadEstResp
		MsgAlert(STR0147+CRLF+CRLF+STR0149+cAuttarResp)
							//"Configuração Incorreta!"###"No cadastro de Estações (LOJA121), aba TEF, favor preencher no campo Recepção dos Arquivos a informação "
	EndIf
EndIf 

Return (nRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³LjVldMult    ³ Autor ³ Vendas Clientes    ³ Data ³ 21/08/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a rotina que esta solicitando uma transacao TEF³±±
±±³          ³ pode trabalhar com multiplas TRN TEF                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := LjVldMult(cOper)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ cOper - Codigo da operacao, valida "V" - Venda, "F" - Desfa³±±
±±³          ³ zimento                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> Pode trabalhar com multiplas trn TEF.         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA 	 ³ BOPS ³Prograd.³ALTERACAO								      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 14.09.05 ³085607³Geronimo³Criticar e abortar operação da Venda Rapida ³±±
±±³          ³      ³        ³e venda Balcao com multiTEF ou seja CC e CD ³±±
±±³          ³      ³        ³como forma de pagamentos nas parcelas       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LjVldMult(cOper, aDados)

Local lRet:=.T.
Local nP  := 0
Local cProg:= ProcName(0)
Local lCC := .F.
Local lCD := .F.
Local _Ni

If cOper$"VF"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Laço para verifica se a rotina esta sendo chamada de qualquer ponto, pois a função   |
	//| estiver sendo chamada de uma customização  a função FUNNAME(), não funciona.         |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While !Empty(cProg)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³A utilização de múltiplas transações foi liberada a partir da versão 710 para o Front ³
		//³e Venda Assistida.  Não esta disponivel na venda Balcão e Venda Rapida                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cProg$"LOJA010#LOJA220"
			If lTefMult
				If cOper$"V" 
		   		MsgInfo("O recurso de Multiplas transações TEF, não está habilitado para esta rotina, desative o parâmetro MV_TEFMULT")
		   Endif		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se parametro para venda Multi Tef esta habilitado aviso o usuario e não efetivo a     ³
				//³Transação TEF                                                                         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lRet		:=.F.
	       Exit
			Else 
				If cOper$"V"		// Venda
					If valtype(aDados) == "A"
						For _Ni := 1 to len(adados)
							If subs(adados[_Ni][3] ,1,2) == "CC"
								lCC := .T.
							ElseIf subs(adados[_Ni][3] ,1,2) == "CD"
								lCD := .T.
							Endif
						Next
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Se na Venda Balcao ou Venda Rapida for informado simultaneamente pagamentos nas formas³
						//³ "CC" e "CD" aviso o usuario e não efetivo a Transação TEF                            ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						if lCC .AND. lCD
							MsgInfo("Transação TEF não será efetuada ,  pois, o recurso de Multiplas transações TEF não está habilitado para esta rotina e foi informado 2 tipos diferentes de formas de pagamentos que utilizam TEF ( 'CC' e 'CD' )." , "Atenção" )
							lRet		:=.F.
						Endif
					Endif
				Endif
			Endif   
		Endif   
		cProg:= ProcName(nP+=1)
    End
Endif

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010VLRENTR  ³ Autor ³ Vendas Clientes    ³ Data ³ 04/09/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Prepara dados para as transaoes de milhagem CCS             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ L010VLRENTR(cTipo) - cTipo: "CH" Analisa Trn cheque        ³±±
±±³        	 ³                      cTipo: "CC" Analisa Trn CARTAO        ³±±
±±³        	 ³                      cTipo: "AO" Analisa Milhagem outros Me³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³aRet														  ³±±
±±³        	 ³[1]Acumula milhas? 0-sim, 1-nao							  ³±±
±±³        	 ³[2]Valor financiado no cartao CCS                           ³±±
±±³        	 ³[3]Valor pago como entrada, todo diferente de CCS           ³±±
±±³        	 ³[4]Valor pago com milhas                                    ³±±
±±³        	 ³[5]Numero de parcelas do financiamento                      ³±±
±±³        	 ³[6]Valor financiando em cheque                              ³±±
±±³        	 ³[7]Valor do Cheque pago a vista                             ³±±
±±³        	 ³[8]Numero dos cheques                                       ³±±
±±³        	 ³[9]Data de vencimento do primeiro cheque financiado         ³±±
±±³        	 ³[10]Banco                                                   ³±±
±±³        	 ³[11]Agencia                                                 ³±±
±±³        	 ³[12]Conta                                                   ³±±
±±³        	 ³[13]Numero de parcelas cheque                               ³±±
±±³        	 ³[14]Numero do cheque dado como a vista                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010VLRENTR(cTipo, aDados)

Local nParc,nP 	:= 0
Local aRet     	:= {}
Local aPgtosB 	:= aClone(aPgtos)
Local lCCsB    	:= .F.

//Atualizar o Valor da parcela com o valor do aDados que pode ter sido alterado pelo Pe ljtefparc.
If cTipo<>"AO"
	For nParc:= 1 to Len(aPgtosB)
		For nP:=1 to Len(aDados)
			If ValType(aPgtosB[nParc][4])=='A'
				If Len( aPgtosB[nParc][4] ) >= 5
					lCcsB := At('C.C.S.',Upper(aPgtosB[nParc][4][5])) <> 0
				Endif
			Else
				lCcsB := At('C.C.S.',Upper(aPgtosB[nParc][4])) <> 0
			Endif
			// Se for a mesma data e mesma finalizadora (cc, ch. cd), antao atualiza o valor
			If ((Alltrim(aPgtosB[nParc][3])=="CC".AND.lCcsB).OR.Alltrim(aPgtosB[nParc][3])=="CH").AND.;
				aPgtosB[nParc][1]==aDados[nP][1].AND.aPgtosB[nParc][3]==aDados[nP][3]
				aPgtosB[nParc][2]:=aDados[nP][2]
			Endif
		Next nP
	Next nParc
Endif

aadd(aRet,If(SuperGetMV("MV_TEFMILH", NIL, .F.),'0','1'))  //aRet[1]Acumula milhas? 0-sim, 1-nao
aadd(aRet, 0)             //aRet[2]Valor financiado no cartao CCS
aadd(aRet,0)              //aRet[3]Valor pago como entrada, todo diferente de CCS
aadd(aRet,0)              //aRet[4]Valor pago com milhas
aadd(aRet,0)              //aRet[5]Numero de parcelas do financiamento
aadd(aRet,0)     		  //aRet[6]Valor financiando em cheque
aadd(aRet,0)    		  //aRet[7]Valor do Cheque pago a vista
aadd(aRet,'')   		  //aRet[8]Numero dos cheques
aadd(aRet,'00000000')     //aRet[9]Data de vencimento do primeiro cheque financiado
aadd(aRet,'0000')         //aRet[10]Banco
aadd(aRet,'00000')        //aRet[11]Agencia
aadd(aRet,'0000000000')   //aRet[12]Conta
aadd(aRet,0)              //aRet[13]Numero de parcelas cheque
aadd(aRet,'000000')       //aRet[14]Numero do cheque dado como a vista

For nParc := 1 To Len(aPgtosB)
	If ValType(aPgtosB[nParc][4])=='A'
		If Len( aPgtosB[nParc][4] ) >= 5
			lCcsB:=At('C.C.S.',Upper(aPgtosB[nParc][4][5]))<>0
		Endif
	Else
		lCcsB:=At('C.C.S.',Upper(aPgtosB[nParc][4]))<>0
	Endif
	cMoeda := AllTrim(aPgtosB[nParc][3])
	If ("CC" $ cMoeda).AND.lCcsB
		aRet[2]+=aPgtosB[nParc][2]
		aRet[5]++
	ElseIf ("MH" $ cMoeda)
		aRet[4]+=aPgtosB[nParc][2]
	ElseIf ("CH" $ cMoeda)
		aRet[10]:= aPgtosB[nParc][4][4] //Banco
		aRet[11]:= aPgtosB[nParc][4][5] //Agencia
		aRet[12]:= aPgtosB[nParc][4][6] //Conta
		If aPgtosB[nParc][1] == dDatabase
			aRet[7] :=aPgtosB[nParc][2] //Cheque pago a vista
			aRet[14]:=StrZero(Val( Subs(aPgtosB[nParc][4][7],1,6)),6) //Numero do Cheque pago a vista
			aRet[3]+= aPgtosB[nParc][2] //Valor pago como entrada
		Else
			aRet[9]:=If(aRet[9]=='00000000',Str(Year(aPgtosB[nParc][1]),4,0)+StrZero(Month(aPgtosB[nParc][1]),2,0)+StrZero(Day(aPgtosB[nParc][1]),2,0),aRet[9]) // Data compensacao do 1o cheque
			aRet[6]+=aPgtosB[nParc][2] // Valor cheques pagos financiados
			aRet[8]+= StrZero(Val( Subs(aPgtosB[nParc][4][7],1,6)),6) // Numero dos Cheques financiados
			aRet[13]++
		Endif
	Else
		aRet[3]+= aPgtosB[nParc][2]
	Endif
Next nParc
// No sistema CCS nao e possivel a confeccao de uma forma de pagamento que contemple cheque pre-datado e
// e cartão CCS, configurado para milhas e multiplas transacoes TEF.
If aRet[2]>0 .AND.aRet[6]>0
	MsgInfo('Não é possivel venda com cheque pré-datado e cartão de CCS')
	aRet[1]:='9'
Else
	// Se tem cheque e cartão CCS, as milhas vai na transação de cartão.
	If cTipo='CH'.AND.aRet[2]>0
		aRet[1]:='1'
		aRet[3]:=aRet[7]
		// Caso houve algum pagamento com a forma de cheque ou cartão CCS, nao pode
		// executar a rotina de aquisição de milhas por outros meios.
	ElseIf cTipo='AO'.AND.(aRet[2]>0.OR.aRet[7]>0)
		aRet[1]:='9'
	Endif
Endif

Return(aRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³L010TefDesf  ³Autora ³ Vendas Clientes    ³ Data ³ 03/06/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se existem transações a serem desfeitas antes de  |±±
±±³          ³ prosseguir com a operacao                                  |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := L010TefDesf()									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ Nenhum													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ ExpL1 -> .T. ou .F.						 			      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010TefDesf()
Local lRet 		:= .F.							//Se retornarmos .T. significa que o desfazimento deverá ser efetuado
Local nI		:= 0
Local aArq		:= Directory("NCNTRN*."+cPTerm)
Local nHandle
Local nSize
Local cRbuffer

For nI:=1 To Len(aArq)
	nHandle := FOpen(aArq[nI][1])
	If nHandle == -1
		Aviso("ATENÇÃO"	,"O arquivo de controle de desfazimento "+Alltrim( aArq[nI][1] )+" nao pode ser aberto!"+;
						 "Alguma transação pode ter ficado PENDENTE, verifique no Relatório de Manutenção de Pendências no Sitef!", {"&Ok"} ,, "" )
		lRet := .F.
	Else
		nSize	:= FSeek(nHandle,0,2)
		FSeek(nHandle,0)
		cRBuffer := Space(nSize)
		FRead(nHandle,@cRBuffer,nSize)
		FClose(nHandle)             
		If Subs(cRBuffer,1,4) == "00=D"
 			lRet := .T.	
		Else
			lRet := .F.		
		Endif
	Endif
Next nI

Return({lRet,nI-1})

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³L010DelImp   ³Autor  ³ Vendas Clientes    ³ Data ³ 09/06/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Apos ser realizado a impressao do cupom TEF, eh gerado um  |±±
±±³          ³ arquivo para a sua reimpressao, exceto VISANET. Este       |±±
±±³          ³ arquivo deve ser apagado sempre que iniciar uma nova venda |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Todas as interfaces de venda                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010DelImp()

Local cPTerm := StrZero( LjGetStation("TERMTEF") , 3 )

FErase("RX_PRN." + cPTerm)

Return(Nil)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Função    ³ LJ010ProcName ³ Autor ³Vendas Clientes³ Data ³ 19/07/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descrição ³ Pesquisa as chamadas de funções, o parâmetro indica a     |±±
±±³           ³ função desejada, caso exista na lista de chamadas a       |±±
±±³           ³ função LJ010ProcName retorna .T. caso contrário retorna   |±±
±±³           ³ .F.                                                       |±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJ010ProcName(cFunName)

Local lRet := .F., nCount := 0, cFunction

While ( ( cFunction := AllTrim(Upper(ProcName(nCount))) ) <> "" )

	If ( lRet := ( cFunction == Alltrim(Upper(cFunName)) ) )
		Exit
	Endif

	nCount ++

End

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VldGetCartaoºAutor³Vendas Clientes     º Data ³  10/07/04   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Funcao para validar o campo do cartao quando for escolhida  º±±
±±º          ³a opcao "nao magnetico"                                     º±±
±±º          ³Forca a digitacao de um enter para sair do campo pois quandoº±±
±±º          ³esta funcao eh utilizada pelo SIGACRD e o cartao private    º±±
±±º          ³label eh com codigo de barras, o usuario utiliza um scanner º±±
±±º          ³para ler o cartao e nao utiliza o pinpad.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³LOJA010T - Funcao L010TCART                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function VldGetCartao(nContVld)

Local lRet := .F.

If Len(Alltrim(ctCartao)) < 16 .AND. !Empty(ctCartao) 
	lRet := .T.
	nContVld := 1
Else
	If Len(Alltrim(ctCartao)) == 16 .AND. nContVld == 1
		lRet := .F. 
		nContVld++
	Else
		lRet := .T. 
		nContVld := 1
	Endif
Endif

Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³BANESEResp   ³Autor  ³ Vendas Clientes    ³ Data ³ 29/09/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Esta funcao retorna todas as mensagens do BANESECARD       |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ BANESECARD                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function BANESEResp(cErro)

Local cRet := "Erro: "+cErro
// Erros de interface
If     cErro == "01" ; cRet := "FALHA DE CONEXAO COM WTPDVEstacao"
ElseIf cErro == "02" ; cRet := "FALHA DE CONEXAO COM WTPDVConcentrador"
ElseIf cErro == "03" ; cRet := "FALHA DE CONEXAO COM A REDE WTNET"
ElseIf cErro == "04" ; cRet := "FALHA DE ENVIO DA P1 (WTPDVArquivos -> WTPDVEstacao)"
ElseIf cErro == "05" ; cRet := "FALHA DE ENVIO DA P1 (WTPDVEstacao -> WTPDVConcentrador)"
ElseIf cErro == "06" ; cRet := "FALHA DE ENVIO DA P1 (WTPDVConcentrador -> REDE WTNET)"
ElseIf cErro == "07" ; cRet := "FALHA DE ENVIO DA P3 (WTPDVArquivos -> WTPDVEstacao)"
ElseIf cErro == "08" ; cRet := "FALHA DE ENVIO DA P3 (WTPDVEstacao -> WTPDVConcentrador)"
ElseIf cErro == "09" ; cRet := "FALHA DE ENVIO DA P3 (WTPDVConcentrador -> REDE WTNET)"
ElseIf cErro == "10" ; cRet := "QUEDA DE CONEXAO COM WTPDVEstacao"
ElseIf cErro == "11" ; cRet := "QUEDA DE CONEXAO COM WTPDVConcentrador"
ElseIf cErro == "12" ; cRet := "QUEDA DE CONEXAO COM A REDE WTNET"
ElseIf cErro == "13" ; cRet := "TIME-OUT DE P2 (REDE WTNET -> WTPDVConcentrador)"
ElseIf cErro == "14" ; cRet := "TIME-OUT DE P3 (APLICATIVO -> WTPDVEstacao)"
ElseIf cErro == "15" ; cRet := "BASE DE DADOS INACESSIVEL"
ElseIf cErro == "16" ; cRet := "FALHA DE ACESSO AO PIN-PAD"
ElseIf cErro == "17" ; cRet := "FALHA NA CAPTURA DE SENHA NO PIN-PAD"
ElseIf cErro == "18" ; cRet := "TIME-OUT NA CAPTURA DE TRILHAS NO PIN-PAD"
//Erros de consistencia
ElseIf cErro == "30" ; cRet := "MODO DE TRANSACAO INVALIDO"
ElseIf cErro == "31" ; cRet := "TIPO DE TRANSACAO INVALIDO"
ElseIf cErro == "32" ; cRet := "TIPO DE TRANSACAO NAO PERMITIDO"
ElseIf cErro == "33" ; cRet := "CODIGO DE PROCESSAMENTO INVALIDO"
ElseIf cErro == "34" ; cRet := "CODIGO DE PROCESSAMENTO NAO PERMITIDO"
ElseIf cErro == "35" ; cRet := "ESTACAO INVALIDA"
ElseIf cErro == "36" ; cRet := "ESTACAO NAO CADASTRADA"
ElseIf cErro == "37" ; cRet := "ESTACAO JA CONECTADA"
ElseIf cErro == "38" ; cRet := "ESTACAO SEM PERMISSOES"
ElseIf cErro == "39" ; cRet := "OPERADOR INVALIDO"
ElseIf cErro == "40" ; cRet := "TURNO DO OPERADOR INVALIDO"
ElseIf cErro == "41" ; cRet := "NSU DA ESTACAO INVALIDO"
ElseIf cErro == "42" ; cRet := "NSU DE CANCELAMENTO INVALIDO"
ElseIf cErro == "43" ; cRet := "CODIGO DO ESTABELECIMENTO INVALIDO"
ElseIf cErro == "44" ; cRet := "CONTA CREDITO INVALIDA"
ElseIf cErro == "45" ; cRet := "CONTA DEBITO INVALIDA"
ElseIf cErro == "46" ; cRet := "CODIGO DA MOEDA INVALIDO"
ElseIf cErro == "47" ; cRet := "VALOR DA TRANSACAO INVALIDO"
ElseIf cErro == "48" ; cRet := "VALOR DA TRANSACAO INFERIOR AO PERMITIDO"
ElseIf cErro == "49" ; cRet := "VALOR DA TRANSACAO SUPERIOR AO PERMITIDO"
ElseIf cErro == "50" ; cRet := "TRILHA 2 DO CARTAO INVALIDA"
ElseIf cErro == "51" ; cRet := "NUMERO DO CARTAO INVALIDO"
ElseIf cErro == "52" ; cRet := "VIA DO CARTAO INVALIDA"
ElseIf cErro == "53" ; cRet := "TITULARIDADE DO CARTAO INVALIDA"
ElseIf cErro == "54" ; cRet := "DATA DE VENCIMENTO DO CARTAO INVALIDA"
ElseIf cErro == "55" ; cRet := "CARTAO VENCIDO"
ElseIf cErro == "56" ; cRet := "SENHA DO CARTAO INVALIDA"
ElseIf cErro == "57" ; cRet := "NUMERO DE PARCELAS INVALIDO"
ElseIf cErro == "58" ; cRet := "NUMERO DE PARCELAS NAO PERMITIDO"
ElseIf cErro == "59" ; cRet := "TIPO DE PARCELAMENTO INVALIDO"
ElseIf cErro == "61" ; cRet := "AUTORIZADOR NAO PERMITIDO"
ElseIf cErro == "62" ; cRet := "DATA DO CANCELAMENTO INVALIDA"
ElseIf cErro == "67" ; cRet := "CNPJ DO ESTABELECIMENTO INVALIDO"
ElseIf cErro == "68" ; cRet := "NOME DO ESTABELECIMENTO INVALIDO"
ElseIf cErro == "69" ; cRet := "ENDERECO DO ESTABELECIMENTO INVALIDO"
ElseIf cErro == "70" ; cRet := "TRANSACAO NAO ENCONTRADA"
ElseIf cErro == "71" ; cRet := "CONFIGURACAO DO PIN-PAD INVALIDA"
ElseIf cErro == "72" ; cRet := "OPERACAO CANCELADA"
ElseIf cErro == "73" ; cRet := "TIPO DE SUB-TRANSACAO INVALIDA"
ElseIf cErro == "74" ; cRet := "CODIGO DE REDE INVALIDO"
ElseIf cErro == "75" ; cRet := "DADOS DO PAGAMENTO INVALIDOS"
ElseIf cErro == "76" ; cRet := "VALOR ORIGINAL DO DOCUMENTO INVALIDO"
ElseIf cErro == "77" ; cRet := "VALOR DO DOCUMENTO INVALIDO"
ElseIf cErro == "78" ; cRet := "VALORES DESCONTOS/ACRESCIMOS INVALIDOS"
ElseIf cErro == "79" ; cRet := "DATA DE VENCIMENTO DO DOCUMENTO INVALIDA"
ElseIf cErro == "80" ; cRet := "LINHA DIGITAVEL/CODIGO DE BARRAS DO DOCUMENTO INVALIDO"
ElseIf cErro == "81" ; cRet := "INDICACAO DE ESPECIE INVALIDA"
ElseIf cErro == "82" ; cRet := "INDICACAO DO MODO DE CAPTURA DO DOCUMENTO INVALIDA"
ElseIf cErro == "83" ; cRet := "INDICACAO DO MODO DE PAGAMENTO INVALIDA"
ElseIf cErro == "84" ; cRet := "RESPOSTA (P2) DO AUTORIZADOR INVALIDA"
ElseIf cErro == "85" ; cRet := "INTERVALO/DATA INVÁLIDOS"
ElseIf cErro == "90" ; cRet := "BIT FALTANTE (BIT <NUMERO DO BIT >)"
ElseIf cErro == "99" ; cRet := "ERRO INDETERMINADO"
//Status da confirmacao
ElseIf cErro == "95" ; cRet := "STATUS DE REJEIÇÃO DE UMA TRANSAÇÃO APROVADA"
Endif

Return cRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010TermTef³ Autora³ Solange Zanardi       ³ Data ³14/10/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chama a funcao para impedir a repetição do nº de terminal   ³±±
±±³          ³ o arquivo fica bloqueado ento estiver no sistema            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ LOJA010T                                                    ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TermTef()
Local cTerm 	:= StrZero( LjGetStation("TERMTEF"), 3 )
Local nHandTerm	:= -1
Local lRet		:= .T.
Local cMsg		:= ""
Local nHandle	:= -1
Local nFError	:= 0

lRet := LJXFTermTef()

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³L010Mex    ³ Autora³ Vendas Clientes       ³ Data ³13/12/2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Transacoes TEF para o banco BANORTE, atraves da BANORTE.DLL ³±±
±±³          ³ em conjunto com a AUTOCOM.DLL.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ LOJA010T                                                    ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010Mex(cOper,nX,aDados)
Local lRet			:= .F.									// Variavel para retorno da Funcao
Local cNumCart		:= ""									// Numero do cartao (TRACK2) de Credito lido por leitora para funcao de TEF_BANORTE 
Local cNomeCli		:= SuperGetMV("MV_TEFCLI2", NIL, "clientes")	// Nome do usuario configurado no Banorte
Local cSenha		:= SuperGetMV("MV_TEFPSW", NIL, "clien01")	// Password do usuario configurado no Banorte
Local cClientID		:= Alltrim(SE4->E4_TEFID) 				// Id para do comercio, fonecido pelo Banorte, que refere-se a un contrato por forma de pagamento
Local cPipeline		:= "Payment"							// Variavel para a funcao TEF_BANORTE, (nao eh mais tratada, enviar sempre "Payment")
Local cMode			:= SuperGetMV("MV_TEFMODE", NIL, "P")   // Variavel para a funcao TEF_BANORTE,sendo :"P"-Producao, "Y"-Simulacao para aceitar, "N"-Simulacao Recusada e "R"-Simulacao Aleatoria
Local cTransType	:= "" 									// Variavel para a funcao TEF_BANORTE, sendo :"Auth" - venda e "Void"-Cancelamento
Local cTotal		:= Alltrim(TransForm(If(cOper="V",aDados[nX,2],aDados[6]),PesqPict("SL1","L1_VALIMP1",,1))) // Variavel da Funcao TEF_BANORTE, valor da transacao
Local lLeu			:= .F.									// Status da leitura do cartao de credito
Local aRetorno		:= {}									// Array para armazenar o retorno da Transacao TEF
Local cTemp			:= ""									// Variavel para armazenar valores temporarios
Local cMsgDados		:= STR0001								// Titulo da tela de digitacao do cartao magnetico //"Leitura de Cartao"
Local cTipo			:= "M"									// Tipo de entrada do dado do cartao, sendo: "M"-Magnetico, "D"-Digitado
Local cOrderId		:= ""									// Variavel da funcao TEF_BANORTE, Numero Sequencial Unico (NSU)
Local oDlg                                                  // Objetos da tela de digitacao do cartao magnetico
Local octCartao                                             // Objetos da tela de digitacao do cartao magnetico
Local octDat_Venc							                // Objetos da tela de digitacao do cartao magnetico
Local nOpd			:= 0									// Variavel para receber o botao da tela de digitaco do cartao magnetico.
Local nPos													// Variaveis temporarias, para posicoes da string da resposta da transacao						
Local nPos2											        // Variaveis temporarias, para posicoes da string da resposta da transacao
Local nV													// Variavel do laco For...Next

Private nRet		:=0										// Recebe o resultado da transacao TEF.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao da transacao "V" - Venda 			cTransType=>"Auth"      ³
//³             transacao "E" - Cancelamento 	cTransType=>"Void"      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cMode<>"P"
	nRet:=Aviso(STR0002, STR0003, {STR0004, STR0005},,STR0006) //"Atenção"###"Esta transação esta sendo realizadas em modo se SIMULAÇÃO,ou seja, as transaçoes não serão cobradas dos clientes, para modo de produção, altere o parâmetro MV_TEFMODE, para 'P'"###"Ok"###"Cancela"###"Modo simulação"
	If nRet=2
	   Return .F.
	Endif  
Endif
If cOper=="V"
	cTransType	:="Auth"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao de um sequencial (OrderId)de transacao para que seja possivel³
	//³efetuar o cancelamento da transacao.                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SLG->(DbSeek(xFilial("SLG")+cEstacao)).AND.SLG->(ColumnPos("LG_ORDERID"))<>0
		cOrderId := StrZero(Val(SLG->LG_ORDERID), TamSx3("LG_ORDERID")[1],0)
		RecLock("SLG",.F.)
		REPLACE SLG->LG_ORDERID With StrZero(Val(cOrderId)+1,TamSx3("LG_ORDERID")[1],0)
		MsUnlock("SLG")
		cOrderId := cEstacao + cOrderId
	Else
        MsgInfo(STR0007) //"Campo de sequencial unico LG_ORDERID, não encontrado"
		Return lRet
	Endif
ElseIf cOper=="E" .OR. cOper=="X" // Cancelamento/Devolucao
	cTransType	:="Void"
	cOrderId	:=aDados[3] //L1_DOCTEF
Else
   MsgInfo(STR0008) //"Transacao invalida"
   Return lRet
Endif	
If Empty( LjGetStation("PINPAD") )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tela para digitacao do numero do cartao magnetico quando nao tem Pin ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ctCartao 	:= Space( 19 )
	ctDat_Venc	:= Space( 4 )
	DEFINE MSDIALOG oDlg TITLE cMsgDados FROM 13,21 TO 21,60 OF GetWNDDefault()
	@	.074,.1 TO 4,19
	@ 06,12 SAY		STR0009	SIZE 130,10 OF oDlg PIXEL //"Informe o número do cartão:"
	@ 06,88 MSGET	octCartao VAR ctCartao		SIZE 055,10 OF oDLG PIXEL PICTURE "@!" VALID ! EMPTY( ctCartao )
	@ 18,12 SAY		If(lInfoCard,STR0010,STR0011) SIZE 130, 10 OF oDlg PIXEL //"Informe a data do cartão:"###"Informe a validade:"
	@ 18,88 MSGET	octDat_Venc VAR ctDat_Venc	SIZE 024,10 OF oDlg PIXEL PICTURE "@R 99/99" VALID L010ValidDt( ctDat_Venc )
	DEFINE SBUTTON FROM 35,090 TYPE 1 PIXEL ACTION ( nOpd:=1,oDlg:End() ) ENABLE OF oDlg
	DEFINE SBUTTON FROM 35,120 TYPE 2 PIXEL ACTION ( nOpd:=3,oDlg:End() ) ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
    If nOpd==1
		aadd(aRetorno,{"CLICART"  ,""})
		ctCartao	:=Alltrim(ctCartao)
    	cTipo	 	:="D"
    	lLeu 		:= .T.
    Endif
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Executa a funcao da Autocom, de leitura do cartao magnetico pelo Pin ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ctTrilha_2:=Space(150)
	For nV:= 1 to 3
		lLeu := L010LeCartao()
		If !lLeu
			If !MsgYesNo(STR0012) //"Error de leitura do cartão magnetico, tentar novamente?"
				Exit
			Endif
		Else
			Exit
		Endif
	Next nV
Endif
ctTrilha_2:=Alltrim(ctTrilha_2)
If oAutocom:lAtivo.AND.lLeu.AND.!Empty(ctTrilha_2+ctCartao)
	If cTipo="M" //Magnetico
		//Tratamento da Variavel ctTrilha_2
		//Pegando os 16 Primeiros digitos do Cartao
		cNumCart:=Subs(ctTrilha_2,1,At("&",ctTrilha_2)-1)+"="
		ctTrilha_2:=Subs(ctTrilha_2,At("&",ctTrilha_2)+1)
		// Salvando o nome do Cliente que esta no Cartao
		aadd(aRetorno,{"CLICART"  ,Upper(Subs(ctTrilha_2,1,At("&",ctTrilha_2)-1))})
		ctTrilha_2:=Subs(ctTrilha_2,At("&",ctTrilha_2)+1)
		// Completando a variavel do numero do cartao para enviar
		ctDat_Venc:=Subs(ctTrilha_2,3,2)+"/"+Subs(ctTrilha_2,1,2)
		cNumCart+=ctTrilha_2
	Endif
	MsgRun( STR0013 ,STR0014 + STR0015 ,{ || nRet := oAutocom:Tef_Banorte(cNumCart,cNomeCli,cSenha,cClientID,cPipeline,cMode,cTransType,ctCartao,ctDat_Venc,cOrderId,cTotal)}) //"Aguardando resposta da rede BANORTE"###"Venda TEF"###" - Em comunicação"
	If nRet=1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Estrutura do array aRetorno                                     			    		  ³
		//³                                                                   			    		  ³
		//³ aRetorno:{Nome da Variavel,Valor da Variavel}, sendo:                                     ³
		//³ TRACK2 	- Numero do cartao                           			    		  			  ³
		//³ VENC	- Vencimento do cartao                               			    		  	  ³
		//³ CLIENTID- Codigo do contrato do comercio junto ao Banorte    			    		  	  ³
		//³ CLIENTID- Codigo do contrato do comercio junto ao Banorte    			    		  	  ³
		//³                                                                   			    		  ³
		//³Armazena as variaveis da resposta da transacao (oAutocom:cBuffer) no array aRetorno, sendo:³
		//³#ProcReturnCode																			  ³
		//³#TimeIn       																			  ³
		//³#TimeOut                                                                					  ³
		//³#Cvv2Resp                                                               					  ³
		//³#CcErrCode                                                              					  ³
		//³#E3                                                                   					  ³
		//³#E2                                                                   					  ³
		//³#E1                                                                   					  ³
		//³#Text                             |   Descricao das Variaveis no Manual :                  ³
		//³#OrderId                          |   ManualPW_IntegracionYSeguridadPOS.doc (Pag. 9) 	  ³
		//³#CcReturnMsg                                                            					  ³
		//³#Authcode                                                            					  ³
		//³#ProcReturnMsg                                                            				  ³
		//³#MaxSev                                                            		       			  ³
		//³#Total                                                            			    		  ³
		//³                                                                   			    		  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cTemp:=Upper(oAutocom:cBuffer)
		If Subs(cTemp,1,1)=="#"
			cTemp:=Subs(cTemp,2)
		Endif
		aadd(aRetorno,{"TRACK2"  ,If(cTipo="M",cNumCart,ctCartao)})  // Armazena o numero do carto
		aadd(aRetorno,{"VENC" ,ctDat_Venc})							 //
		aadd(aRetorno,{"CLIENTID",cClientID})
		While !empty(cTemp)
			nPos	:=At("=",cTemp)
			nPos2:=At("#",cTemp)
			nPos2:=If(nPos2<>0,nPos2,Len(cTemp))
			aadd(aRetorno,{Subs(cTemp,1,nPos-1),Subs(cTemp,nPos+1,(nPos2-nPos)-1)})
			cTemp:=Subs(cTemp,nPos2+1)
		End
		nPos:=Ascan(aRetorno,{|X| X[1]=="CCERRCODE"})
		cTemp:=aRetorno[nPos][2]
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Qualquer valor na variavel "CCERRCODE" diferente de 1, transacao reprovada³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cTemp=="1" // Transacao Aprovada
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Funcao para geracao do layout do comprovante do ticket					 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aTicket := aClone(LJ025TCK(cOper,nX,aDados,aRetorno))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Armazena o ticket a ser impresso e o status da transacao.                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aTefDados[1][14] := aTicket
			Aviso( STR0016+Alltrim(aRetorno[Ascan(aRetorno,{|X| X[1]=="STATUS"})][2]),Alltrim(aRetorno[Ascan(aRetorno,{|X| X[1]=="AUTHCODE"})][2]), {STR0017},,STR0018) //"Status - "###"&Ok"###"Autorizacao"
			If lTefMult .AND. nX <> 0
				// O array ATEFDADOS, contem as informacoes necessarias para impressao do cupom e
				// tb para confirmacao ou cancelamento do cupom
				aTefMult[nX][7] := aClone(aTefDados)
				aTefMult[nX][8] := .T.
			Endif
			lRet:=.T.
		Else    // Transacao Reprovada
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Quando a funcao eh reprovada, existe duas possibilidade de mensagem, sendo:³
			//³CCRETURNMSG  - Mensaje normalizado retornado por Clear Commerce que        ³
			//³               corresponde al campo CcErrCode. Válido para transacciones   ³
			//³               aprobadas, declinadas y referidas.                          ³
			//³TEXT       	- Texto del mensaje de Error regresado.                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(cTemp)
				cTemp+=" - "+ Alltrim(aRetorno[Ascan(aRetorno,{|X| X[1]=="CCRETURNMSG"})][2])
			Else
				cTemp:=Alltrim(aRetorno[Ascan(aRetorno,{|X| X[1]=="TEXT"})][2])
			Endif
			Aviso( STR0002,Alltrim(cTemp), {STR0017},,STR0019) //"AtenÇÃO"###"&Ok"###"Erro - Banorte"
			lRet:=.F.
		Endif
	Endif
Endif

Return lRet 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Função   ³ LJ025TCK      ³ Autor ³Vendas Clientes| Data ³ 13/12/2004  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descrição³ Geracao do layout do ticket das transacoes TEF - Banorte   |±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LJ025TCK( ExpC1, ExpN1, ExpA1, ExpA2 )           		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 - Tipo de Transacao 							      ³±±		
±±³		     ³	"V" - Venda										          ³±±
±±³		     ³	"E" - Exclusao ( cancelamento )						      ³±±
±±³		     ³					  								          ³±±
±±³		     ³ ExpN1 (Indice do array da Multipla transacao TEF) 		  ³±±
±±³		     ³ ExpA1 (Array com variaveis das parcelas da venda           ³±±
±±³		     ³ ExpA2 (Array com variaveis do retorno da transacao		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³ Rotinas TEF- Mexico									      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function LJ025TCK(cOper,nX,aDados,aRetorno)

Local aTickTmp	:={}												// Array para conter o layout do ticket
Local aMeses	:={	"JAN", 	"FEB", "MAR", "APR", ;
					"MAY",	"JUN", "JUL", "AUG", ;
					"SEP",  "OCT", "NOV", "DEC" } // Array para interpretacao da data da transacao
Local aADM		:={"","","American Express","Visa","MasterCard"} 	// Array para interpretacao atraves do Bin do Cartao identificar a administradora
Local cTemp		:=""												// Valores temporarios
Local cData		:=""												// Resultado da interpretacao da data da transacao
Local aInfo		:={}												// Array para enviar dados para PE "LJTEFTCK"
Local nI		:=0    												// Lacos For...Next
Local nPos		:=0    												// Variavel temporaria
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Interpretacao da data da transacao												 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nPos:=Ascan(aRetorno,{|X| X[1]=='TIMEOUT'})
cTemp:=Subs(Alltrim(aRetorno[nPos][2]),9,2)+"/"
cTemp+=Strzero(Ascan(aMeses,Upper(Subs(Alltrim(aRetorno[nPos][2]),5,3))),2)+"/"
cTemp+=Right(Alltrim(aRetorno[nPos][2]),4)

aadd(aInfo,{"Data",cTemp})
aadd(aInfo,{"Hora",Subs(Alltrim(aRetorno[Ascan(aRetorno,{|X| X[1]=='TIMEOUT'})][2]),12,8)})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Identificar a Administradora (Visa, CrediCard,etc) pelo, primeiro Numero do cartao³
//³Sendo:                                                                            ³
//³3 - American Express                                                              ³
//³4 - Visa                                                                          ³
//³5 - MasterCard                                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nPos:=Val(Subs(aRetorno[Ascan(aRetorno,{|X| X[1]=="TRACK2"})][2],1,1))
aadd(aInfo,{"Adm"			, If(nPos>0 .AND. nPos<6,aAdm[nPos],"OUTRAS")})
aadd(aInfo,{"Autorizacao"	, aRetorno[Ascan(aRetorno,{|X| X[1]=="AUTHCODE"})][2]})
aadd(aInfo,{"Cartao"		, aRetorno[Ascan(aRetorno,{|X| X[1]=="TRACK2"})][2]})
aadd(aInfo,{"Nome"			, aRetorno[Ascan(aRetorno,{|X| X[1]=="CLICART"})][2]})
aadd(aInfo,{"Filiacao"		, aRetorno[Ascan(aRetorno,{|X| X[1]=="CLIENTID"})][2]})
aadd(aInfo,{"Valor"			, aRetorno[Ascan(aRetorno,{|X| X[1]=="TOTAL"})][2]})
aadd(aInfo,{"DocTef"		, aRetorno[Ascan(aRetorno,{|X| X[1]=="ORDERID"})][2]})
aadd(aInfo,{"Venc"			, aRetorno[Ascan(aRetorno,{|X| X[1]=="VENC"})][2]})
aadd(aInfo,{"Transacao"		, cOper})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atuaizar o array aTefDados, para gravacao das tabelas SL1, SL4.					 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cOper=="V"
	aTefDados[1][1]:= "S"
	aTefDados[1][2]:=aInfo[1,2]								//SL1->L1_DATATEF
	aTefDados[1][3]:=Subs(aInfo[2,2],1,5)					//SL1->L1_HORATEF
	aTefDados[1][4]:=aInfo[9,2] 							//SL1->L1_DOCTEF
	aTefDados[1][5]:=aInfo[4,2] 							//SL1->L1_AUTORIZ
	aTefDados[1][8]:=aInfo[3,2] 							//SL1->L1_INSTITU
	aTefDados[1][9]:=aInfo[9,2] 							//SL1->L1_NSUTEF
	aTefDados[1][10]:="M"									//SL1->L1_TIPCART
	aTefDados[1][11]:="BANORTE"								//Nome da Rede
	aTefDados[1][13]:=Left(aInfo[5,2],At("=",aInfo[5,2])-1)	//Numero do cartao
	aTefDados[1][15]:="CC"									//SL1->L1_FORMPG
ElseIf cOper=="E" .OR. cOper=="X" 
    aTefDados[1][6] 	:= aInfo[9,2] 						//SL1->L1_DOCCANC
    aTefDados[1][7] 	:= Subs(aInfo[2,2],1,5) 			//SL1->L1_HORCANC
    aTefDados[1][12] := aInfo[1,2]							//SL1->L1_DATCANC
Endif
If cOper=="V"
	aAdd( aTickTmp,STR0020) //"------------Venda ----------------------"
ElseIf cOper=="E" 
	aAdd( aTickTmp,STR0021) //"----------Cancelamento------------------"
Endif	
aAdd( aTickTmp,"               BANORTE                  ")
aAdd( aTickTmp,Space(10)+Upper(aInfo[3,2]))
aAdd( aTickTmp,STR0022+aInfo[7,2]) //"Afiliacao  :"
aAdd( aTickTmp,STR0023+aInfo[1,2]+STR0024+aInfo[2,2]) //"Data       :"###"     Hora:"
aAdd( aTickTmp,STR0025+aInfo[4,2]+"        NSU :"+aInfo[9,2]) //"Autorizacao:"
aAdd( aTickTmp,STR0026+Subs(aInfo[5,2],1,4)+"."+Subs(aInfo[5,2],5,4)+"."+Subs(aInfo[5,2],9,4)+"."+Subs(aInfo[5,2],13,4)+STR0027+aInfo[10,2] ) //"Cartao :"###"    Venc:"
aAdd( aTickTmp,STR0028+aInfo[8,2]) //"Valor - $ "
aAdd( aTickTmp,"                                        ")
aAdd( aTickTmp,"Pagarei a quantia especificada neste doc")
aAdd( aTickTmp,"umento.                                 ")
aAdd( aTickTmp,"                                        ")
aAdd( aTickTmp,"----------------------------------------")
aAdd( aTickTmp,space(10)+aInfo[6,2])

If ExistBlock("LJTEFTCK")
	LjGrvLog(Nil, " Ponto de Entrada LJTEFTCK - Parametros: " , {aTickTmp,aInfo})

	aTickTmp := Aclone(ExecBlock("LJTEFTCK",.F.,.F.,{aTickTmp,aInfo}))

	LjGrvLog(Nil, " Ponto de Entrada LJTEFTCK - Retorno " , aTickTmp)
Endif

Return aTickTmp

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L011TTela ºAutor  ³Vendas Clientes     º Data ³  05/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tela de cancelamento de TEF.                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA/FRONT LOJA                                        º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function	L011TTela(cSerie, cNota, nOpc, oDlg1)

DEFINE MSDIALOG oDlg1 TITLE STR0055 From 13,11 To 23,55	of GetWndDefault()   //"Cancelamento TEF Manual"
	
@ 14,15	SAY STR0056 	  SIZE 60 ,10 OF oDLG1 PIXEL       // "Série"
@ 14,85	MSGET cSerie Valid !Empty(cSerie) SIZE 50 ,10 OF oDLG1 PIXEL
@ 26,15	SAY STR0057 SIZE 60 ,10 OF oDLG1 PIXEL            // "Nota Fiscal"
@ 26,85	MSGET cNota Valid !Empty(cNota) SIZE 50 ,10 OF oDLG1 PIXEL
	
@ 50,35 BUTTON STR0058 SIZE 50,15 OF oDlg1 PIXEL ACTION (nOpc:=1,oDlg1:End())    // "Continua"
@ 50,85 BUTTON STR0059 SIZE 50,15 OF oDlg1 PIXEL ACTION (nOpc:=0,oDlg1:End())    // "Abandona"
	
ACTIVATE MSDIALOG oDlg1 CENTERED

Return .T.


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010SelCancºAutor  ³Vendas Clientes     º Data ³  05/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tela de cancelamento de TEF.                                º±±
±±º          ³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA/FRONT LOJA                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function	L010SelCanc(lVenda, oDlg)

Local lRet := .T.

DEFINE MSDIALOG oDlg TITLE STR0060  FROM 00,00 TO 240,200 OF GetWndDefault() PIXEL   // "Rotinas TEF"

	@ 10,10 BUTTON STR0061   		        SIZE 80,20 OF oDlg PIXEL ACTION ( lVenda := .T., oDlg:End() )   // "Cancela Venda"
	@ 30,10 BUTTON STR0062   	            SIZE 80,20 OF oDlg PIXEL ACTION ( lVenda := .F., oDlg:End() )   // "Cancela Recebimento"
	@ 90,10 BUTTON STR0063			    	SIZE 80,20 OF oDlg PIXEL ACTION ( lRet   := .F., oDlg:End() )   // "&Sair"
	
ACTIVATE MSDIALOG oDlg CENTERED

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010CTela ºAutor  ³Vendas Clientes     º Data ³  05/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Tela de cancelamento de TEF.                               º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA/FRONT LOJA                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CTela(	nLin	, nValor	, nValorBak	, cContr, ;
							cData	, lSaiCanc	, cTitulo	, lBaixa )
							
DEFINE MSDIALOG oDlg TITLE cTitulo FROM 13,21 to 22,60  of GetWNDDefault()
	
@  nLin,12 SAY STR0052      	 			SIZE 130, 10 OF oDlg PIXEL        //"Valor Final"
@  nLin,88 MSGET nValor SIZE  55, 10 		OF oDLG PIXEL	Picture "@E 9,999,999.99" VALID (nValor>0 .AND. nValor>=nValorBak)
nLin += 12
	
@ nLin,12 SAY STR0053 					 	SIZE 130, 10 OF oDlg PIXEL       // "NSU (Número Seq. Único)"
@ nLin,88 MSGET cContr SIZE  18, 15 		OF oDlg PIXEL Picture "@!" WHEN lBaixa VALID L010VldNsu(cContr)
nLin += 12
	
@ nLin,12 SAY STR0054  					  	SIZE 130, 10 OF oDlg PIXEL      // "Data da Compra (DD/MM)"
@ nLin,88 MSGET cData SIZE  55, 10 		OF oDLG PIXEL	Picture "@R 99/99" Valid L010DtCompra( cData )
	
DEFINE SBUTTON FROM 47, 082 TYPE 1 PIXEL ACTION ( lSaiCanc:=.F.,oDlg:End() ) ENABLE OF oDlg
DEFINE SBUTTON FROM 47, 112 TYPE 2 PIXEL ACTION ( lSaiCanc:=.T.,oDlg:End() ) ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTERED

Return( lSaiCanc )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010VldNsu ºAutor  ³Vendas Clientes     º Data ³  05/07/06  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Valida se existe o NSU no SLV.                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA/FRONT LOJA                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010VldNsu(cContr)

Local lRet 		:= .T.			// Retorno da funcao

SLV->(DbSetOrder(3))
If !SLV->(DbSeek(xFilial("SLV") + cContr))
	lRet := .F.
	//"NSU nao encontrado! Processo cancelado."		
	MsgInfo(STR0051)
Else
	If !Empty(SLV->LV_DOCCANC)
	    //"Transação TEF já cancelada!"
		MsgInfo(STR0050)
		lRet := .F.
	Endif
Endif

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010AtuSlv ºAutor  ³Vendas Clientes     º Data ³  05/07/06  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Atualiza o SLV apos cancelamento de TEF                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpA1 = Array com o retorno da transacao TEF               º±±
±±º          ³ ExpL1 = Indica se eh multiplas transacoes				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±    

±±ºUso       ³ SIGALOJA                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010AtuSlv( aTefTmp, lTefMult )

Local cNsu  	:= SLV->LV_NSUTEF
Local aArea 	:= GetArea()

DbSelectArea("SLV")
While !Eof() .AND. xFilial("SLV") == SLV->LV_FILIAL .AND. cNsu == SLV->LV_NSUTEF
	Reclock("SLV", .F.)
	If lTefMult
		REPLACE SLV->LV_DOCCANC	With aTefTmp[1][7][1][6]
		REPLACE SLV->LV_HORCANC	With aTefTmp[1][7][1][7]
		REPLACE SLV->LV_DATCANC	With aTefTmp[1][7][1][12]
	Else
		REPLACE SLV->LV_DOCCANC	With aTefTmp[1][6]
		REPLACE SLV->LV_HORCANC	With aTefTmp[1][7]
		REPLACE SLV->LV_DATCANC	With aTefTmp[1][12]
	EndIf
	MsUnLock()
	DbSkip()
End
RestArea(aArea)
Return(.T.)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010CompNOpºAutor  ³Vendas Clientes     º Data ³  05/07/06  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Verifica se o Numero da operacao de Resposta e igual       º±±
±±º          ³ o de Requisicao                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpA1 = Numero de operacao da resposta                     º±±
±±º          ³                                          				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/    

Static Function L010CompNOp(c001, cPrefixo001)
Local lRet := .F.
DEFAULT cPrefixo001 := "" 

If SubStr(c001, Len(cPrefixo001) + 1) <> AllTrim(Str(nSeqArq - 1))
	lRet := .T.
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010rArq	 ºAutor  ³Vendas Clientes     º Data ³  05/07/06  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Le o arquivo de backup para o envio do cancelamento        º±±
±±º          ³ da transacao TEF                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpA1 = Numero de operacao da resposta                     º±±
±±º          ³                                          				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGALOJA                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/    

Function L010rArq()
    
Local aRetorno	:= {}
Local aImpres	:= {}
Local cString	:= ""
Local c000		:= "" // Header
Local c001		:= "" // Identificacao
Local c003		:= "" // Valor
Local c006		:= "" //
Local c007		:= "" //
Local c009      := "" // Status da Transacao
Local c011      := "" // Codigo da Transacao efetuada
Local c010      := "" // Nome da Instituicao
Local c012		:= "" // NSU
Local c013		:= "" // Codigo de Autorizacao
Local c014      := "" // Numero de Lote
Local c017      := "" // Tipo de Parcelamento ("0" - Estabelecimento / "1" - Administradora)
Local c018      := "" // Quantidade de Parcelas
Local c022		:= "" // Data da Transacao
Local c023		:= "" // Hora da Transacao
Local c027		:= "" // Finalizacao
Local n028		:= 0  // Linhas do Cupom   
Local c040		:= "" // Nome da bandeira do cartao
Local nHanDisc
Local nSize
Local cRetorno
Local nRead
Local nPosAspa	:= 0
Local lItem     := .F.
Local nPos		:= 0
Local nParc		:= 0
Local nValor := 0
Local lCriaArq := .T.
    
While .T.

	nHandisc := Fopen( cDirT_Rx + "INTPOS.001", 0 )
	If (nHandisc == -1)
			Return (.F.)
	Endif
	Exit
End
While .T.
	nSize := FSeek( nHanDisc, 0, 2 )
	FSeek( nHanDisc, 0 )
	cRetorno := Space( nSize )
	nRead := FRead( nHanDisc, @cRetorno, nSize )
	if ! nRead == nSize .OR. ! (FError() == 0)
			Return (.F.)
	Endif
	Exit
End
	
While .T.
	FClose( nHandisc )
	If ! (Ferror() == 0)
			Return (.F.)
	Endif
	Exit
End

For nPos := 1 to len(cRetorno)
	Do Case
		Case Substr(cRetorno,nPos,3) == " = " .AND. ! lItem
			aadd(aRetorno,{rtrim(cString),""})
			cString := ""
			nPos += 2
			lItem := .T.
		Case Substr(cRetorno,nPos,1) == CHR(13)
			aRetorno[len(aRetorno)][2] := rtrim(cString)
			cString := ""
			lItem := .F.
		Case Substr(cRetorno,nPos,1) == CHR(10)
			// NÆo considera o CHR(10)
		otherwise
			cString += substr(cRetorno,nPos,1)
		EndCase
Next npos

For npos := 1  to len(aRetorno)
	Do Case
		Case aRetorno[npos][1] == "000-000"
			c000 := aRetorno[npos][2]
			If c000 == "ADM"
				lCriaArq := .F.
			EndIf
		Case aRetorno[npos][1] == "001-000"
			c001 := aRetorno[npos][2]
			Case aRetorno[npos][1] == "003-000"
			c003 := aRetorno[npos][2]
			If Val(c003) > 0 
				nValor := Val(c003)
				If At(".",c003) == 0 .AND. At(",",c003) == 0
					nValor := nValor / 100
				Endif
			Endif
		Case aRetorno[npos][1] == "006-000"
			c006 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "007-000"
			c007 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "009-000"
			c009 := alltrim(aRetorno[npos][2])         
		Case aRetorno[npos][1] == "010-000"
			c010 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "011-000"
			c011 := AllTrim(Str(Val(aRetorno[npos][2])))
		Case aRetorno[npos][1] == "012-000"
			c012 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "013-000"
			c013 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "014-000"
			c014 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "017-000"
			c017 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "018-000"
			c018 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "022-000"
			c022 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "023-000"
			c023 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "027-000"
			c027 := aRetorno[npos][2]
		Case aRetorno[npos][1] == "028-000"
			n028 := val(aRetorno[npos][2])
		Case Substr(aRetorno[npos][1],1,4) == "029-"
			nPosAspa := At('"',aRetorno[npos][2])
			If nPosAspa > 0
				If Len(aRetorno[npos][2]) > 2
					aadd(aImpres,Subs(aRetorno[npos][2],2,Len(aRetorno[npos][2])-2))
				Else
					aadd(aImpres,"")
				Endif
			Else
				aadd(aImpres,aRetorno[npos][2])
			Endif
		Case aRetorno[npos][1] == "030-000"
			c030 := aRetorno[npos][2]   
		Case aRetorno[npos][1] == "040-000"
			c040 := aRetorno[npos][2]			
	EndCase
Next npos 

If lCriaArq .AND. !L010IsDirecao(L010GetGPAtivo())
	aConf := {}
	
	nSeqArq++
	PutMv("MV_TEFNSTR",AllTrim(Str(nSeqArq)))
		          
	aadd(aConf,{ALLTRIM(STR(nSeqArq,10)),iif(empty(c010)," ",c010),iif(empty(c012)," ",c012),iif(empty(c027)," ",c027),nValor})
	          
	nHandle := fCreate("NCNCNF." + cPTerm)
	For nParc := 1 to len(aConf)
		If ! ValType(aConf[nParc][5]) == "N"
			aConf[nParc][5] := Space(1)
		Else
			aConf[nParc][5] := aConf[len(aConf)][5] := Alltrim(TransForm(aConf[len(aConf)][5],PesqPict("SL1","L1_CARTAO",_PICTURE,nMoedaCor)))
		Endif
		fWrite(nHandle,"NSTR="+aConf[nParc][1]+";"+aConf[nParc][2]+";"+aConf[nParc][3]+";"+aConf[nParc][4]+";"+aConf[nParc][5]+";")
	Next nParc
	fClose(nHandle)
EndIf
Return .T.


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJLoadDTEF ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega arquivo de transacoes TEF pedentes		          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ aCartoes	- Array de cartoes com processamento pendente 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se procedeu com sucesso / .F. Para erro       	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function LJLoadDTEF(lCancTEF2)
Local nHnd 		  := -1								// Handle do arquivo a ser lido
Local aRet 		  := {}								// Array de retorno da função (segue a mesma estrutura do aCartoes)
Local aTmpLinhas  := {}								// Array temporario que carrega as linhas do arquivo texto
Local cBuffer 	  := Space(10000)					// Buffer que recebe o texto do arquivo
Local nCount	  := 0								// Contador
Local nCountCP	  := 0								// Contador de campos
Local cArqTefPend := ""

Default lCancTEF2 := .F.

If lCancTEF2
	cArqTefPend := GetClientDir() + "TEFOK.TMP"
Else
	cArqTefPend := GetClientDir() + "TEFPEND.TMP"	// Arquivo de transacoes TEF Pendentes
EndIf

LjGrvLog(Nil, "LJLoadDTEF(lCancTEF2) - Retorno do cArqTefPend", cArqTefPend)	

/* Verifica se o arquivo existe */
If File(cArqTefPend)
	nHnd := FOpen(cArqTefPend)   
	
	If nHnd > 0
		FRead(nHnd, @cBuffer, Len(cBuffer))
		FClose(nHnd)
		/* Transforma o texto do arquivo em Array */
		aTmpLinhas := StrTokArr(AllTrim(cBuffer), Chr(10))
		For nCount := 1 To Len(aTmpLinhas)
			aTmpLinhas[nCount] := StrTran(aTmpLinhas[nCount],"||","| |")
			aAdd(aRet, StrTokArr(AllTrim(aTmpLinhas[nCount]), "|"))
			/* Converte o tipo de dado dos campos */
			For nCountCP := 1 To Len(aRet[nCount])  
				If ("|" + AllTrim(Str(nCountCP)) + "|") $ "|1|2|5|9|"	   		// Campos numericos
					aRet[nCount][nCountCP] := Val(aRet[nCount][nCountCP])
				ElseIf ("|" + AllTrim(Str(nCountCP)) + "|") $ "|4|"			// Campos logicos
					aRet[nCount][nCountCP] := IIF(aRet[nCount][nCountCP] == "S", .T., .F.)
				EndIf                                              
			Next nCountCP  
		Next nCount 		                                               
	EndIf
EndIf
Return aRet


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJSaveDTEF ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Salva arquivo de transacoes TEF pedentes		         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpA1		- Array de cartoes com processamento pendente ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se procedeu com sucesso / .F. Para erro       	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/                        
Static Function LJSaveDTEF(aCartoes, lRecCel)
Local nHnd 		  := -1								// Handle do arquivo a ser gravado
Local cBuffer	  := ""								// Buffer do texto que sera gravado no arquivo
Local nCount	  := 0								// Contador
Local lRet		  := .F.							// Retorno da função (.T. = OK / .F. = Erro na gravacao)	
Local cArqTefPend := GetClientDir() + "TEFPEND.TMP"	// Arquivo de transacoes TEF Pendentes
Local aTEFRecCel  := {}  							// Array com recarga de celular
Local nPosRec	  := 0                              //Posicao do array de recarga de celular

DEFAULT aCartoes  := {}   
DEFAULT lRecCel	  := .F.

If lRecCel  
	aTEFRecCel := LJLoadDTEF()
	nPosRec := aScan(aTEFRecCEl, {|a| a[3]== "009" .AND. a[8] == "PENDENTE"})
	If nPosRec > 0 //Salva as trasações de recarga de celular
		cBuffer = 	AllTrim(Str(aTEFRecCel[nPosRec][1]))	+ "|" + ;
					AllTrim(Str(aTEFRecCel[nPosRec][2])) 	+ "|" + ;
					aTEFRecCel[nPosRec][3] 				+ "|" + ;
					IIf(aTEFRecCel[nPosRec][4], "S", "N") + "|" + ;
					AllTrim(Str(aTEFRecCel[nPosRec][5])) 	+ "|" + ;
					aTEFRecCel[nPosRec][6] 				+ "|" + ;
					aTEFRecCel[nPosRec][7] 				+ "|" + ;
					aTEFRecCel[nPosRec][8] 				+ "|" + ;
					AllTrim(Str(aTEFRecCel[nPosRec][9]))	+ "|" + ;
					aTEFRecCel[nPosRec][10]				+ "|" + ;
					aTEFRecCel[nPosRec][11]				+ "|" + ;
					aTEFRecCel[nPosRec][12]				+ "|" + ;  
					aTEFRecCel[nPosRec][13]				+ "|" + ; 
					aTEFRecCel[nPosRec][14]				+ "|" + ;
					Chr(10)	
	EndIf
EndIf

For nCount := 1 To Len(aCartoes)
	cBuffer += 	AllTrim(Str(aCartoes[nCount][1]))	+ "|" + ;
				AllTrim(Str(aCartoes[nCount][2])) 	+ "|" + ;
				aCartoes[nCount][3] 				+ "|" + ;
				IIf(aCartoes[nCount][4], "S", "N") + "|" + ;
				AllTrim(Str(aCartoes[nCount][5])) 	+ "|" + ;
				Iif(!Empty(aCartoes[nCount][6]),aCartoes[nCount][6],Space(1)) 	+ "|" + ;
				aCartoes[nCount][7] 				+ "|" + ;
				aCartoes[nCount][8] 				+ "|" + ;
				AllTrim(Str(aCartoes[nCount][9]))	+ "|" + ;
				aCartoes[nCount][10]				+ "|" + ;
				aCartoes[nCount][11]				+ "|" + ;
				aCartoes[nCount][12]				+ "|" + ; 
				aCartoes[nCount][13]				+ "|" + ; 
				aCartoes[nCount][14]				+ "|" + ;
				Chr(10)
Next nCount

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta e recria o arquivo de TEF pendentes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LJDelDTEF()
	nHnd := FCreate(cArqTefPend)
	
	If nHnd > 0
		FWrite(nHnd, cBuffer, Len(cBuffer))
		FClose(nHnd)   
		lRet := .T.
	EndIf		 
EndIf
Return lRet


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJProcDTEF ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa transacoes MULTI-TEF DISCADO		          	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpL1		- .T. para cancelamentos/desfazimentos 		  ³±±
±±³          ³           	- .F. para vendas/confirmacoes		 		  ³±±
±±³          ³ ExpL2		- .T. para somente solicitacoes de vendas	  ³±±
±±³          ³            	- .F. para todas as transacoes 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se procedeu com sucesso / .F. Para erro/abortagem	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/  
Static Function LJProcDTEF(	lCancelar	, lSolicVend	, cId800	, aDadosTrn	,;
							lCanPend	)

Local aCartoes		 	:= LJLoadDTEF()						// Array de cartoes
Local nCount		 	:= 0								// Contador
Local lRet			 	:= .T.								// Retorno da funcao
Local aTrans		 	:= {}								// Array com texto de transacao
Local cPrefixo001	 	:= ""								// Prefixo do campo 001, indicado por 999 ou 002 para multiplos cartoes
Local lTentarNov	 	:= .F.								// Tentar transmitir novamente (caso de falha)
Local cValorTrans	 	:= ""								// Valor da transacao formatado
Local lDeleta		 	:= .T.								// Deleta arquivo de cartoes pendentes, quando nao ha mais nenhuma pendencia
Local cMsgCancela	 	:= ""								// Mensagem de cancelamento/desfazimento
Local nQtdeTotalTran 	:= 0								// Quantidade de transmissoes a realizar (controle do prefixo)
Local nQtdeAtualTran 	:= 0								// Quantidade de transmissoes ja realizadas (controle do prefixo)
Local lRecarga		 	:= .F.             					// Operacao de recarga de celular
Local nX			 	:= 0				
Local nI			 	:= 0
Local nCopias		 	:= Max(LjGetStation("TEFVIAS"),1)  	// Numero de vias impressas no comprovante TEF
Local aTicketTEF	 	:= {}
Local cCodAdmFin		:= ""								// Codigo da Adm Financeira
Local cDescRede			:= ""								// Descricao da Rede
Local lRedeAdquirente	:= .F.								// Valida se exite Adm
Local cParcelas			:= ""								// Quantidade de parcelas
Local cTipoCartao		:= ""								// Tipo do Cartao (CC, CD ou Voucher)
Local cFinancPro		:= ""								// Financiamento proprio ou nao
Local cTipoFin			:= ""								// Tipo Financiamento
Local cTipoParc			:= ""					
Local aAreaSAE			:= {}								// Guarda area
Local lMVTELAFIN		:= SuperGetMv("MV_TELAFIN",,.T.)	// Define se mostra a tela de administradora
Local nBandeira     	:= L010GetGPAtivo()					// Controla a administradora que foi efetuada a transacao TEF baseado no aTefDisc
Local lAuttar			:= L010IsAuttar(nBandeira)			// Verifico se é gerenciador Auttar
Local lMvLjCDVOU		:= SuperGetMv("MV_LJCDVOU",, 0) == 1// Define se utiliza venda com Voucher

DEFAULT lCancelar  	 	:= .F.
DEFAULT lSolicVend   	:= .F.
DEFAULT cId800		 	:= ""
DEFAULT aDadosTrn	 	:= {}
DEFAULT lCanPend	 	:= .f.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obtem a quantidade de transacoes que serao transmitidas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCount := 1 To Len(aCartoes)
	If lCancelar
		If aCartoes[nCount][8] == DTEF_PENDENTE .OR. aCartoes[nCount][8] == DTEF_CONFIRMADA
			nQtdeTotalTran++
		EndIf
	Else
		If lSolicVend
			If aCartoes[nCount][8] == DTEF_NAO_TRANSMITIDA
				nQtdeTotalTran++ 

			EndIf
		Else
			If aCartoes[nCount][8] == DTEF_PENDENTE
				nQtdeTotalTran++
			EndIf
		EndIf
	EndIf 
Next nCount 
   
/* Processa todos os cartoes */
For nCount := 1 To Len(aCartoes)
	lRet 	:= .F.		// Reseta o valor
	aTrans 	:= {}		// Reseta o valor
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Formata o valor da transacao, eliminando o separador decimal (remove , e .) ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cValorTrans := Transform(aCartoes[nCount][2], "@E 999,999.99")
	cValorTrans := StrTran(cValorTrans, ",", "")
	cValorTrans := AllTrim(StrTran(cValorTrans, ".", ""))

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o prefixo para transacoes multitef - envia 999 indicando que haverá mais cartoes e 002 para o ultimo cartao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nQtdeTotalTran == 1
		cPrefixo001 := ""
	ElseIf (nQtdeAtualTran + 1) < nQtdeTotalTran  .OR.  (cId800 == "009") //Se for recarga, a transação será 999
		cPrefixo001 := "999"
	Else
		cPrefixo001 := "002"
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ativa o Gerenciador Padrao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SuperGetMV("MV_LJATVGP",.F.,.T.)
		PutMv("MV_TEFNSTR", AllTrim(Str(++nSeqArq)))	// Grava numero sequencial no parametro	
		LjMsgRun(STR0102 + aCartoes[nCount][6] + STR0103, STR0087, { || lRet := L010MArq({"000-000 = ATV", "001-000 = " + ALLTRIM(Str(nSeqArq)), "999-999 = 0"}, aCartoes[nCount][9])}) //"Verificando se o gerenciador padrão " " do TEF discado está ativo." "Aguarde"
		If !lRet
			If !SuperGetMV("MV_LJTSC")
				If WinExec(cDrvTEFDsc + aTefDisc[aCartoes[nCount][9]][4], 3) == 0
					lRet := .T.
				EndIf
			EndIf
		EndIf
	Else
		lRet := .T.
	EndIf
	
	If lRet
		If lCancelar .And. (nCount == Len(aCartoes) .OR. lAuttar)
			/* Monta o arquivo de envio para o Gerenciador Padrao : CANCELAMENTOS(CNC) E DESFAZIOMENTOS(NCN) */
			Do Case  
				Case aCartoes[nCount][8] == DTEF_PENDENTE
					/* Transacao pendente -> Enviar desfazimento */
					aTrans := 											   	   				{ ;
							"000-000 = " + "NCN"						  	   				, ;	// Header
							"001-000 = " + /*cPrefixo001 +*/ AllTrim(Str(aCartoes[nCount][5]))	, ;	// Identificacao (o mesmo da solicitacao de venda)
							"010-000 = " + aCartoes[nCount][6]				   				, ;	// Nome da rede
							"012-000 = " + aCartoes[nCount][7]				  				, ;	// NSU
							"027-000 = " + aCartoes[nCount][12]			 	  				}	// Finalizacao
							If lAuttar .AND. nCount > 1
							  aAdd(aTrans,"099-000 = " + "1")									// Múltiplos Cartões: 1-Sim, 0-Não
							EndIf
							aAdd(aTrans, "999-999 = " + "0"	)									// Trailer registro final

				Case aCartoes[nCount][8] == DTEF_CONFIRMADA	
					/* Transacao confirmada -> Enviar cancelamento */
					PutMv("MV_TEFNSTR", AllTrim(Str(++nSeqArq)))  								// Grava numero sequencial no parametro	
					aTrans := 							   				   	 				{ ;
							"000-000 = " + "CNC"							  				, ;	// Header
							"001-000 = " + cPrefixo001 + AllTrim(Str(nSeqArq)) 				, ;	// Identificacao (novo numero)
							"003-000 = " + cValorTrans					  					, ;	// Valor da transacao
							"010-000 = " + aCartoes[nCount][6]			 					, ;	// Nome da rede
							"012-000 = " + aCartoes[nCount][7]				 				, ;	// NSU
							"022-000 = " + aCartoes[nCount][10]			   					, ;	// Data de transação
							"023-000 = " + aCartoes[nCount][11]			   					}	// Hora de transação
							If lAuttar .AND. nCount > 1
							  aAdd(aTrans,"099-000 = " + "1")									// Múltiplos Cartões: 1-Sim, 0-Não
							EndIf
							aAdd(aTrans, "999-999 = " + "0"	)									// Trailer registro final
			EndCase
		Else
			/* Monta o arquivo de envio para o Gerenciador Padrao : SOLICITACAO DE VENDA(CRT) E CONFIRMACAO DE VENDA(CNF) */
			Do Case
				Case aCartoes[nCount][8] == DTEF_PENDENTE .AND. !lSolicVend
					/* Transacao pendente -> Enviar confirmacao */
					aTrans := 										   	   					{ ;
							"000-000 = " + "CNF"					  	  					, ;	// Header
							"001-000 = " + cPrefixo001 + AllTrim(Str(aCartoes[nCount][5]))	, ;	// Identificacao (o mesmo da solicitacao de venda)
							"010-000 = " + aCartoes[nCount][6]			 					, ;	// Nome da rede
							"012-000 = " + aCartoes[nCount][7]			 					, ;	// NSU
							"027-000 = " + aCartoes[nCount][12]			  					}	// Finalizacao
							If lAuttar .AND. nCount > 1
							  aAdd(aTrans,"099-000 = " + "1")												// Múltiplos Cartões: 1-Sim, 0-Não
							EndIf
							aAdd(aTrans, "999-999 = " + "0"	)									// Trailer registro final
				
				Case aCartoes[nCount][8] == DTEF_NAO_TRANSMITIDA
					/* Transacao nao transmitida -> Enviar solicitacao de venda */
					If lSolicVend
						PutMv("MV_TEFNSTR", AllTrim(Str(++nSeqArq)))  								// Grava numero sequencial no parametro	
						aCartoes[nCount][5]	:= nSeqArq												// Armazena numero sequencial da transacao
						
						If aCartoes[nCount][3] <> "009"
							// Numero de Parcelas informado pelo usuario
							cParcelas			:= Alltrim(Str(aCartoes[nCount][1]))
																					 
							//Tipo de cartao CC, CD ou Voucher
							If Alltrim(aCartoes[nCount][3]) == "CC"
								cTipoCartao := "1"
							Else
								If lMvLjCDVOU
									cTipoCartao := Lj010GtOpc(aCartoes[nCount], 1)
								Else
									cTipoCartao := "2"
								EndIf	
							EndIf

							// Tipo de Financiamento / 	A Vista ou Parcelado pelo Emissor 							
							If Val(cParcelas)==1
								If cTipoCartao == "1"
									cTipoFin := "10"  			// Credito a Vista
								ElseIf  cTipoCartao == "2"
									cTipoFin := "20"  			// Debito a Vista									 
								Endif
								
								cTipoParc		:= "1"			//Tipo de Parcelamento : a vista
								
							Elseif Val(cParcelas)>1
								If cTipoCartao == "1"
									cTipoFin := "11"  			// Credito Parcelado
								ElseIf  cTipoCartao == "2"
									cTipoFin := "22"  			// Debito Parcelado									 
								Endif
								
								cTipoParc		:= "3"			//Tipo de Parcelamento : parcelado pelo estabelecimento											
							Else
								cTipoFin := ""
								cTipoParc:= "0"
							Endif
																																																																																									
							aTrans := {}
							
							Aadd(aTrans, "000-000 = " + "CRT")		// Header
							Aadd(aTrans, "001-000 = " + cPrefixo001 + AllTrim(Str(aCartoes[nCount][5])))	// Identificacao
							Aadd(aTrans, "003-000 = " + cValorTrans)		   								// Valor da transacao		
							//Aadd(aTrans, "011-000 = " + cTipoFin)											// Tipo de Transacao /*/ Em contato com o time da Pay&Go não precisamos mais mandar esse campo
							
							If Val(cParcelas) > 1
								Aadd(aTrans, "018-000 = " + cParcelas)										// Numero de Parcelas
								Aadd(aTrans, "017-000 = " + "0")											// Tipo de parcelamento
							EndIf
							
							If lAuttar .AND. nCount > 1
							  aAdd(aTrans,"099-000 = " + "1")												// Múltiplos Cartões: 1-Sim, 0-Não
							EndIf
							Aadd(aTrans, "706-000 = " + "128")												// Solicitação do NSU Estendido
							Aadd(aTrans, "730-000 = " + "1")												// Venda com cartao
							Aadd(aTrans, "731-000 = " + cTipoCartao)										// Tipo Cartao (1-CC / 2-CD)
							
							If(cTipoCartao == "1") .Or. (cTipoCartao == "2" .And. cTipoParc == "1" )
								Aadd(aTrans, "732-000 = " + cTipoParc)										// Tipo de Parcelamento Em contato com o time da Pay&Go não precisamos mais mandar esse campo no debito parcelado
							EndIf
							
							Aadd(aTrans, "999-999 = " + "0")					   			 				// Trailer registro final								 								
																																														
					    Else
					         
					    	aTrans := 												  		 	{ ;
								"000-000 = " + "ADM"							   				, ;	// Header
								"001-000 = " + cPrefixo001 + AllTrim(Str(aCartoes[nCount][5]))	, ;	// Identificacao
								"800-000 = " + aCartoes[nCount][3]					   			, ;	// Transacao de Correspondente bancário
								"999-999 = " + "0"					   			 				}	// Trailer registro final

					    EndIf
					EndIf
			EndCase  
		EndIf        	
	
		If Len(aTrans) > 0
						
			If lCanPend .And. ("NCN" $ aTrans[1])
				MsgInfo(STR0139  +; //"Atenção foi enviado ao servidor uma solicitação de Desfazimento de Operação "
						STR0140  +; //"se configurada, a tela de confirmação/anulação da transação será mostrada "
						STR0141  +;//""independente da seleção do operador, todos os dados da venda para o Protheus " "
						STR0142  + CHR(13)+ CHR(10) +; //"serão CANCELADAS."
						STR0143  +; //"Portanto caso deseja que a tela não seja mostrada e operação cancelada automaticamente "
						STR0144  + CHR(13)+ CHR(10) +; //"configure o gerenciador padrão (Opção 'Acata Desfazimento')"						
						STR0145  + CHR(13)+ CHR(10) +; //""Consulte o link do TDN, para maiores informações:""
						"http://tdn.totvs.com/display/PROT/SIGALOJA_0324_Cancelamento_Venda_Queda_Energia")
			EndIf
			
			/* Transmite arquivo para o Gerenciador Padrao e aguarda o retorno */
			lTentarNov := .T.
			While lTentarNov
				MsgRun(STR0073, STR0074, {|| lRet := L010MArq(aTrans, aCartoes[nCount][9])})
				lTentarNov := !lRet .AND. MsgYesNo("Passar o cartão novamente?", "Venda com cartão")
			End 
			
			If lRet
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza a quantidade de transacoes ja transmitidas no processamento atual ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nQtdeAtualTran++
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Definicao de novos Status ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
				Do Case
					Case aCartoes[nCount][8] == DTEF_PENDENTE									
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ (Venda) DTEF_PENDENTE -> DTEF_CONFIRMADA / (Cancelamento) DTEF_PENDENTE -> DTEF_DESFEITA ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aCartoes[nCount][8] := IIF(lCancelar, DTEF_DESFEITA, DTEF_CONFIRMADA)
						
					Case aCartoes[nCount][8] == DTEF_CONFIRMADA
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ (Venda) DTEF_CONFIRMADA -> DTEF_CANCELADA / (Cancelamento) DTEF_CONFIRMADA -> DTEF_CANCELADA ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aCartoes[nCount][8] := IIF(lCancelar, DTEF_CANCELADA, DTEF_CONFIRMADA)
		
					Case aCartoes[nCount][8] == DTEF_NAO_TRANSMITIDA
						If !lCancelar				
							
							/* Backup das transacoes TEF ativas */
							LJSaveDTEF(aCartoes)							
							
							/* Obtem retorno da solicitacao de venda */
							aCartoes[nCount][4] := L010LeImp(	aCartoes[nCount][2]	, aCartoes[nCount][9]	, Nil					, cPrefixo001			,;
							 									cId800				, @aDaDosTrn			, aCartoes[nCount][14]	, aCartoes[nCount][3]	,;
							 									@aCartoes			, nCount				)
							If aCartoes[nCount][4]
								/* Armazena os dados de retorno da solicitacao de venda no array aCartoes */
								aCartoes[nCount][7]	 := IIF(Empty(aTefDados[nCount][9])	, " ", aTefDados[nCount][9])		// Numero do DOC (NSU)
								aCartoes[nCount][10] := IIF(Empty(aTefDados[nCount][2])	, " ", aTefDados[nCount][2])		// Data da operacao
								aCartoes[nCount][11] := IIF(Empty(aTefDados[nCount][3])	, " ", aTefDados[nCount][3])		// Hora da operacao
								aCartoes[nCount][13] := IIF(Empty(aTefDados[nCount][5]) , " ", aTefDados[nCount][5]) //Numero da autorizacao
								MsgRun(c030, IIF(cId800 <> "009", "Venda com CARTAO", "RECARGA DE CELULAR"), {||Sleep(5000)})
								
								lDeleta := .F.
								lRet := .T.																	
							Else
								lRet := .F.
								Exit
							EndIf
						EndIf 
						
						/* (Venda) DTEF_NAO_TRANSMITIDA -> DTEF_PENDENTE / (Cancelamento) DTEF_NAO_TRANSMITIDA -> DTEF_NAO_TRANSMITIDA */
						aCartoes[nCount][8]	 := IIF(lCancelar, DTEF_NAO_TRANSMITIDA, DTEF_PENDENTE)
				EndCase
			   
				/* Backup das transacoes TEF ativas */
				LJSaveDTEF(aCartoes)
				
				If aCartoes[nCount][8] == DTEF_DESFEITA .OR. aCartoes[nCount][8] == DTEF_CANCELADA
					
					If lCanPend .And. aConf == NIL
						aConf := {}
						Aadd(aConf,	{	AllTrim(aCartoes[nCount][5]),; //Numero Seq da Trans.
										aCartoes[nCount][6],; //Rede
										aCartoes[nCount][7],; //Hora e codigo
										aCartoes[nCount][12],;	//data e a posicao anterior
										aCartoes[nCount][2]})	//Valor da operação					
					EndIf
				EndIf
                               
				// Faz a confirmacao no servidor TEF
				If lCancelar .AND. nCount == Len(aCartoes) .AND. (lCanPend .OR. (aConf <> NIL .AND. Len(aConf) > 0))
					L010NCNCNF(.T.)
				ElseIf nCount < Len(aCartoes) .AND. !lAuttar		//Se Auttar, não marco os pendentes como Confirmada. Os pendentes ao retornar ao Protheus serão desfeitos.
					If L010NCNCNF(.F.) .AND. aCartoes[nCount][8] == DTEF_PENDENTE
						aCartoes[nCount][8] := IIF(lCancelar, aCartoes[nCount][8], DTEF_CONFIRMADA)
						//faz o backup do status das transacoes
						LJSaveDTEF(aCartoes)
					Endif
				Endif	
			Else
				lRet := .F.
				Exit
			EndIf      
		EndIf  
	EndIf
Next nCount   

/*
Remove arquivos de pendencia caso nao haja mais nada a processar.
Se nao houve nenhuma operacao (aTrans alimentado) no Desfazimento(F),
mantemos o arquivo, pois ele será usado para reutilizar as transacoes confirmadas
*/
If lDeleta .AND. !lSolicVend .AND. ( cOpera == "F" .AND. Len(aTrans)>0 )
	LJDelDTEF(.T.) 
Else
	LJSaveDTEF(aCartoes)
EndIf

Return lRet

                      
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJDelDTEF  ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Deleta os arquivos de transacao TEF pendentes          	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpL1		- Caso .T. tanbém remove arquivos temporarios ³±±
±±³          ³                do TEF Discado							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se excluiu com sucesso / .F. Se houve erro          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LJDelDTEF(lDelDiscado)
Local cArqTefPend := GetClientDir() + "TEFPEND.TMP"					// Arquivo de transacoes TEF Pendentes
Local lRet 		  := .T.											// Retorno da funcao
Local nCount	  := 0												// Contador
Local aArqDisq	  := {"NCNCNF."	+ cPTerm						,;	// Arquivo temporarios do TEF Discado [1]
				      "NCNTRN"	+ StrZero(1, 2) + "." + cPTerm	,; 	// Arquivo temporarios do TEF Discado [2]
					  "ACEITE."	+ cPTerm						,;	// Arquivo temporarios do TEF Discado [3]
					  "INTPOS."	+ cPTerm						}	// Arquivo temporarios do TEF Discado [4]

DEFAULT lDelDiscado := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se o arquivo existir, deleta ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cArqTefPend)
	lRet := (FErase(cArqTefPend) == 0)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se cada arquivo existe e deleta ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lDelDiscado
	For nCount := 1 To Len(aArqDisq)
		If File(aArqDisq[nCount])
			lRet := ( (FErase(aArqDisq[nCount]) == 0) .AND. lRet )
		EndIf
	Next nCount
EndIf
Return lRet
        

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ L010GetGPAt³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o 'nI' (identif. operadora) da bandeira ativa  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ nI da bandeira ativa do TEF Discado						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010GetGPAtivo()
Local nRet := 0	//Retorno

If aTefDisc = NIL
	aTefDisc			:= L010LoadTef()
EndIf  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Varre o array em busca da bandeira ativa ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !aTefDisc[++nRet][5]
	If nRet == Len(aTefDisc)
		nRet := -1
		Exit
	EndIf
End
Return nRet



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Lj010VldRet³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o ID de transacao de um arquivo de retorno		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1 - Numero do ID de transmissao         				  ³±±
±±³          ³ ExpC2 - Arquivo de retorno que tera o ID comparado  		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. - Se o arquivo conter o mesmo ID						  ³±±
±±³       	 ³ .F. - Se o arquivo conter o ID diferente 				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Lj010VldRet(nIDTrans, cArquivo)
Local nHandle	 := -1	// Handle do arquivo
Local cBuffer 	 := ""	// Buffer de leitura do arquivo
Local aTmpArq 	 := {}	// Array que armazena as linhas do arquivo
Local nCount  	 := 0	// Contador
Local nIDRetorno := 0	// Valor do ID da transacao capturado (campo 001-000)

DEFAULT nIDTrans := 0
DEFAULT cArquivo := ""
                     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre o arquivo de retono ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If File(cArquivo)
	nHandle := FOpen(cArquivo)
	If nHandle > 0 
		cBuffer := Space(10000)
		FRead(nHandle, @cBuffer, Len(cBuffer))
		FClose(nHandle)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Varre o arquivo ate encontrar o campo 001-000 -> ID da transacao ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aTmpArq := StrTokArr(AllTrim(cBuffer), Chr(10))
		For nCount := 1 To Len(aTmpArq)
			If "001-000" $ aTmpArq[nCount]
				nIDRetorno := Val(AllTrim(SubStr(aTmpArq[nCount], 10, Len(aTmpArq[nCount]))))
				Exit
			EndIf
		Next nCount
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Compara o ID de envio com o ID de retorno ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return (nIDRetorno == nIDTrans)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ Lj010PendP ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recria arquivo de cartoes pendentes tratando cartoes ja    ³±±
±±³			 ³ transmitidos 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpA1 - Array aCartoes			         				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ Nil 														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function Lj010PendPag(aCartoesNew, lRecCel, lTemTEFOk)
Local aCartoesOld 	:= LJLoadDTEF()	// Array de cartoes
Local nIOld 		:= 0			// Contador aCartoesOld
Local nINew 		:= 0			// Contador aCartoesNew
Local aCancelar 	:= {}			// Array de cartoes pendentes que devem ser cancelados
Local lAchou		:= .F.			// Controla se encontrou a transacao pendente no aCartoesNew
Local aCartaoSav	:= {}			//Array dos Cartoes a Serem salvos

DEFAULT aCartoesNew := {}
DEFAULT lRecCel		:= .F.			//Operacao de recarga de celular pendente
DEFAULT lTemTEFOk	:= .F.

aCartaoSav          := aClone(aCartoesNew)

For nIOld := 1 To Len(aCartoesOld)
	
	If aCartoesOld[nIOld][8] $ DTEF_PENDENTE +"|"+ DTEF_CONFIRMADA .AND. !(lRecCel .AND. aCartoesOld[nIOld][3] == "009")
		lAchou := .F.
		
		For nINew := 1 To Len(aCartoesNew)
			If  (aCartoesOld[nIOld][1] == aCartoesNew[nINew][1]) .AND. ;	// Numero de parcelas do cartão
				(aCartoesOld[nIOld][2] == aCartoesNew[nINew][2]) .AND. ;	// Valor da transacao do cartao
			   	(aCartoesOld[nIOld][3] == aCartoesNew[nINew][3]) .AND. ;	// Tipo de cartao CC/CD
			   	(aCartoesOld[nIOld][6] == aCartoesNew[nINew][6] .OR.   ;	// Nome da operadora
				lTemTEFOk .AND. aCartoesOld[nIOld][8] == DTEF_CONFIRMADA)	// Transacao Confirmada
				
				aCartoesNew[nINew] := aClone(aCartoesOld[nIOld])			// Assume os valores ja transmitidos
				lAchou := .T.
				Exit
			EndIf
		Next nINew   
		
		If !lAchou
			aAdd(aCancelar, aCartoesOld[nIOld])
		EndIf
	ElseIf aCartoesOld[nIOld][8] == DTEF_PENDENTE  .AND. lRecCel .AND. aCartoesOld[nIOld][3] == "009"	
		//insere esta transação pendente no aCartoesNew 
		aAdd(aCartaoSav, aCartoesOld[nIOld])
	EndIf
Next nIOld

If Len(aCancelar) > 0 
	LJSaveDTEF(aCancelar)	// Cria arquivo de transacoes pendentes para cancelar
	LJProcDTEF(.T.)			// Cancela as transacoes pendentes 
EndIf

LJSaveDTEF(aCartaoSav)		// Cria arquivo de transacoes a serem transmitidas
Return Nil


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ010SaveI ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Salva arquivo de retorno do TEF para impressao posterior   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1 - Numero da NSU da trnsacao						  ³±±
±±³          ³ ExpC2 - Texto de espelho do cupom vinculado				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ Nil 														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LJ010SaveImp(cNSU, cImpress)
Local nHdn 		 := 0	// Handle do arquivo
Local cArquivo 	 := ""	// Nome do arquivo 

DEFAULT cNSU 	 := ""
DEFAULT cImpress := ""

cArquivo := "NSU" + AllTrim(Str(Val(cNSU))) + ".TMP"

If File(cArquivo)
	FErase(cArquivo)
EndIf

nHdn := FCreate(cArquivo)
If nHdn > 0
	FWrite(nHdn, cImpress)
	FClose(nHdn)
EndIf                       
                                        
// Operacao de Estono e Reimpressao adiciona no array aCartCan
If (cOpera == "E" .OR. cOpera == "S")
	Aadd(aCartCan,cNSU)
Endif
Return Nil

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ010LoadI ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Carrega os arquivos de retorno do TEF para impressao.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ aRet 	- Retorna array preenchido com cada linha do texto³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LJ010LoadImp(lCancTEF2)
Local aCartoes	:= {}			// Array aCartoes carregado com base no arquivo de transacoes pendentes
Local cArquivo	:= ""			// Nome do arquivo de espelho do cupom vinculado
Local cBuffer	:= ""			// Buffer de leitura de arquivo
Local nHdn		:= 0			// Handle do arquivo
Local nSize		:= 0			// Tamanho do arquivo
Local nCount 	:= 0			// Contador
Local cRet 		:= ""			// Texto de retorno da funcao
Local aRet 		:= {}			// Array de retorno da funcao

Default lCancTEF2	:= .F.

aCartoes := LJLoadDTEF(lCancTEF2)

For nCount := 1 To Len(aCartoes)
	cBuffer := ""

	// Operacao de Estorno e Reimpressao pega do array aCartCan
	If cOpera <> "E" .AND. cOpera <> "S"
		cArquivo := "NSU" + AllTrim(Str(Val(aCartoes[nCount][7]))) + ".TMP"  
    Else
    	If Len(aCartCan) >= nCount 
			cArquivo := "NSU" + AllTrim(Str(Val(aCartCan[nCount] ))) + ".TMP"	
		Endif 	
	Endif

	LjGrvLog(Nil, "LJ010LoadImp(lCancTEF2) - conteudo da variavel cArquivo: ", cArquivo)	

	If File(cArquivo)
		nHdn := FOpen(cArquivo)
		If nHdn > 0                                                       
			nSize := FSeek(nHdn, 0, 2)
			FSeek(nHdn, 0)
			cBuffer := Space(nSize)
			FRead(nHdn, @cBuffer, nSize)
			FClose(nHdn)
		EndIf
	EndIf
	
	// Espacamento de 3 linhas a cada cupom
	cRet += cBuffer +	(Chr(10) + Chr(13) + "  ") + ;
					   	(Chr(10) + Chr(13) + "  ") + ;
						(Chr(10) + Chr(13) + "  ")

	LjGrvLog(Nil, "LJ010LoadImp(lCancTEF2) - Conteúdo da variavel cRet: ", cRet)	

	// Limpa o conteudo de cArquivo						
	cArquivo := ""						
Next nCount

aRet := GeraArrayTexto(cRet, Chr(10))
Return aRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ GeraArrayT ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Transforma o texto recebido em array, quebrando no CHR(10) ³±±
±±³          ³ (criada devido a StrTokArr nao considerar linhas em branco)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpC1	- Texto que deseja transformar em array			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ aRet 	- Retorna array preenchido com cada linha do texto³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function GeraArrayTexto(cTexto)
Local aRet 		:= {}	// Retorno da funcao
Local nCount 	:= 0	// Contador
Local nPos 		:= 0	// Posicao do texto que se encontra o CHR(10)

For nCount := 1 To Len(cTexto)
	nPos := At(Chr(10), cTexto)
	If nPos > 0
		aAdd(aRet, Substr(cTexto, 1, nPos))
		cTexto := Substr(cTexto, nPos + 1, Len(cTexto))
		nCount := nPos + 1
	Else
		aAdd(aRet, cTexto)
		Exit
	EndIf
Next nCount
Return aRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ LJ010DelNsu³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Deleta arquivos de impressao temporarios (NSU*.TMP)		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ lRet	- .T. - Sucesso / .F. - Erro ao deletar algum arquivo)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function LJ010DelNsuTxt()
Local cMascara 			:= "NSU*.TMP"	// Mascara de pesquisa de arquivos
Local aArquivos 		:= {}			// Array com a lista de arquivos, resultado da pesquisa
Local nQuantArquivos 	:= 0			// Quantidade de arquivos encontrados na pesquisa
Local nCount 			:= 0			// Contador
Local lRet 				:= .T.			// Retorno da funcao (.T. - Todos arquivos deletados com sucesso / .F. - Problema ao deletar algum dos arquivos)

// Carrega a lista de arquivos
nQuantArquivos := ADir(cMascara, aArquivos)

// Percorre array deletando os arquivos temporarios
For nCount := 1 To nQuantArquivos
	lRet := (FErase(aArquivos[nCount]) <> -1) .AND. lRet
Next nCount
	
Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010IsPayGo³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a bandeira é do tipo TEF-PAY&GO           	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1	- nI da bandeira referente ao aTefDisc			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se for TEF-PAY&GO / .F. se não for TEF-PAY&GO		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010IsPayGo(nBandeira)
Local lRet := .F.

lRet := ("|" + AllTrim(Str(nBandeira)) + "|" $ "|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|25|26|27|28|29|30|31|32|33|34|35|36|37|38|39|40|41|42|")

Return lRet 

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±³Fun‡„o	 ³ L010IsDire ³ Autor ³Venda CRM            ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a bandeira é do tipo TEF-DIRECAO           	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parƒmetros³ ExpN1	- nI da bandeira referente ao aTefDisc			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorna	 ³ .T. 	- Se for TEF-DIRECAO / .F. se não for TEF-DIRECAO	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ LOJA010T 												  ³±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010IsDirecao(nBandeira)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ TEF DIRECAO utiliza as bandeiras: 21, 22, 23 e 24 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return ("|" + AllTrim(Str(nBandeira)) + "|" $ "|21|22|23|24|")
           
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³RetDescrFPºAutor  ³Vendas Clientes       º Data ³  21/09/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a descricao da forma de pagamento.                    º±±
±±º          ³                                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                              º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function RetDescrFP(cFP)

Local cChave	:= ""		//Chave de pesquisa
Local cRet		:= ""		//Retorno

Default cFP		:= ""

cChave := xFilial("SX5") + "24" + AllTrim(cFP)
dbSelectArea("SX5")
SX5->(dbSetOrder(1))
SX5->(dbSeek(cChave))
If SX5->(Found())
	cRet := SX5->X5_DESCRI
Endif

Return cRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºFun‡„o    ³L010TAtRC ºAutor  ³Vendas CRM            º Data ³  10/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ A Função indica se a estacao esta configurada para           º±±
±±º          ³ recarga de celular                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja010T.      		                                        º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TAtRC()
Local lRet := .F. // Retorno da funcao

lRet := Subs( Alltrim( LjGetStation( "REDES" ) ), 3, 1 ) == "1"  .AND.   !Empty(LjGetStation("WSSRV"))  //CB habilitado e WS ativivo

Return lRet

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºFun‡„o    ³L010TRCl  ºAutor  ³VendasCRM             º Data ³  10/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ A Função realiza a operação de recarga de celular no         º±±
±±º          ³campo 800-000 caso o WebService de Comunicação com o PDV      º±±
±±º          ³estiver ativo.                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Loja019T, Loja010T.                                           º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TRCl(oWsFinaCel, aDadosTrn, lCancela)

Local lRecCel		:= .F. //Operacao de recarga de celular com sucesso?
Local cId800		:= ""  //código da transação no campo 800
Local lDestObj		:= .F.  //Destruição do objeto do WS de recarga de celular? 


DEFAULT lCancela	:= .F.
aDadosTrn	:= {}

If !lCancela 
	If oWsFinaCel = Nil 
		oWsFinaCel		:= WSLJFINACEL():New()
		iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oWsFinaCel),Nil) //Monta o Header de Autenticação do Web Service
		oWsFinaCel:_URL 	:= "http://"+Alltrim(LJGetStation("WSSRV"))+"/LJFINACEL.apw" 
		lDestObj := .T. //Destrir o objeto
	EndIf

	lRecCel := oWsFinaCel:Conecta("")
	
	
	If lRecCel 
		cId800	:= "009"
		lRecCel := Loja010T( "D"   	, /*cVenCon*/    	, /*aDados*/		, /*lConsulta*/   	,;
				/*lImprime*/, /*cFPagReceb*/ 	, /*lTemTEFPend*/	, /*lAbandonaTEF*/	,;
	            /*lTitulo*/	, /*aTefTmp*/		, /*lBaixa*/		, /*lSemRede*/		,;
	            /*cOrcamen*/, /*lRelGer*/		, /*MsgCupom*/		, /*nVlrTotal*/		,;
			   	cId800, oWsFinaCel, @aDadosTrn)
	Else
		MsgStop("PDV não está conectado com a retaguarda via WebService.")  
	EndIf
	
	If lDestObj
		FreeObj(oWsFinaCel)
		oWsFinaCel := Nil
	EndIf
Else 
	lRecCel := LJProcDTEF(lCancela)
EndIf	
Return lRecCel

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010RCD        ºAutor  ³Vendas CRM     º Data ³  10/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Recarga de Celular Direcao                                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function L010RCD(nI, cId800, oWsFinaRC, aDadosTrn)
Local lRet := .F.      //Retorno da transação de Recarga de Celular
Local nValor	:= 0   //Valor da Transação 
Local aCartoes	:= {} //Array de transacoes pendentes

DEFAULT oWsFinaRC := nil

aAdd(aCartoes, {1				, 0					, cId800		, .T.					,;
				    					1				, aTefDisc[nI][1]	, " "		, DTEF_NAO_TRANSMITIDA	,;
				    					nI				, " "				, " "		, " "					,;
				    					" "				, "1"})
aDadosTrn := {}

If LJSaveDTEF(aCartoes)
	lRet := LJProcDTEF(.F., .T., cId800, @aDadosTrn)	// Executa o processamento dos cartoes (somente solicitacoes de vendas)
EndIf

If lRet
 
	If Len(aDadosTrn) >= 24 
	
		aDadosTrn[24] := aClone(aTicket)	 
			
		If nValor = 0
			nValor := aDadosTrn[13] 
		EndIf
								
		If Empty(aTefDados[1][9])  //NSU
			aTefDados[1][9] := aDadosTrn[19]
		EndIf  
		
		If Empty(aTefDados[1][2]) //Data da Transacao
			aTefDados[1][2] := Dtos(aDadosTrn[04])
		EndIf 
		
		If Empty(aTefDados[1][3]) //Hora da Transacao 
			aTefDados[1][2] := aDadosTrn[05]
		EndIf
		
	EndIf 

EndIf

Return lRet  


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010TCB        ºAutor  ³Vendas CRM     º Data ³  10/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario                                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TCB()

Local oDlg := nil     //Dialogo do Correspondente Bancario
Local nOp := 500      //Opção selecionada
Local oWSFinaCB :=  nil //Objeto do WebService do Correspondente Bancário
Local lAtivo := .T.   //WebService do CB Ativo

DEFINE MSDIALOG oDlg TITLE "Correspondente Bancario"  From 13,21 to 38,70 of GetWndDefault()

@ 010,060  	BUTTON "Pagamento de Conta"    		 		SIZE 80,25 OF oDlg PIXEL ACTION ( nOp:=103,oDlg:End() )
@ 040,060  	BUTTON "Cancelamento de Conta"   		 	SIZE 80,25 OF oDlg PIXEL ACTION ( nOp:=104,oDlg:End() )
@ 070,060  	BUTTON "Reimpressao de Comprovante"   		SIZE 80,25 OF oDlg PIXEL ACTION ( nOp:=106,oDlg:End() )
@ 100,060 	BUTTON "Configuracoes" 		  		 		SIZE 80,25 OF oDlg PIXEL ACTION ( nOp:=107,oDlg:End() )
@ 130,060	BUTTON "Sair"   						 	SIZE 80,25 OF oDlg PIXEL ACTION ( nOp:=500,oDlg:End() )

ACTIVATE MSDIALOG oDlg CENTERED 

If nOp < 500 
	
	lAtivo := L010VeInCB(dDataBase)
		
	If lAtivo  
	
		If nOp == 103 .OR. nOp == 104   
			oWSFinaCB 		:= WSLJFINACB():New()
			iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oWSFinaCB),Nil) //Monta o Header de Autenticação do Web Service
			oWSFinaCB:_URL 	:= "http://"+Alltrim(LJGetStation("WSSRV"))+"/LJFINACB.apw"   
			lAtivo := oWSFinaCB:Conecta("")
		EndIf
		
		If lAtivo
			L010TrCB(StrZero(nOp,3), @oWSFinaCB)
		Else
			MsgStop("PDV não está conectado com a retaguarda via WebService.")
		EndIf

	EndIf 
		
	If oWSFinaCB <> Nil
		FreeObj(oWSFinaCB)
		oWSFinaCB := Nil
	EndIf	
EndIf

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010TAtCB      ºAutor  ³Vendas CRM     º Data ³  10/12/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario - Verifica se o CB está configuradoº±±
±±º          ³ na estação                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TAtCB()     
Local lRet := .F. // Retorno da funcao

lRet := Subs( Alltrim( LjGetStation( "REDES" ) ), 4, 1 ) == "1"  .AND.   !Empty(LjGetStation("WSSRV"))  //CB habilitado e WS ativivo

Return lRet     

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³L010VeInCB     ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario  - Verifica a inicializacao do CB  º±±
±±º          ³ na data                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010VeInCB(dData)
Local lRet := .T. 				//Retorno da função
Local aAreaSLI := SLI->(GetArea())  //WorkArea da tabela SLI 
Local cId	:= "101" 			//Id da transação do campo 0800
Local lFecha := .F. 			//Executa a operação de fechamento do CB?
Local lAbre  := .F. 			//Executa a operação de abertura do CB?
Local cEst	 := PadR(cEstacao, SLI->(TamSx3("LI_ESTACAO")[1]) ) //Codigo da estação      

Default dData := dDataBase

SLI->(DbSetOrder(1)) //LI_FILIAL+LI_ESTACAO+LI_TIPO 

If !SLI->(DbSeek(xFilial("SLI") + cEst + "CBD"))  //Registro de abertura?
    lAbre := .T.
Else
	lFecha := SLI->( RTrim(LI_MSG) <> "102" .AND. (LI_DATA < dData) ) 
	lAbre := SLI->( RTrim(LI_MSG) <> "101"  ) 
	  
EndIf    

If lFecha
	cId := "102" 
	If (lRet := L010InCB(cEst,cId , SLI->LI_DATA)) //Abre/Fecha o CB	
		lAbre := .T. //Seta para abrir o CB
	Endif
EndIf

If lAbre 
	cId := "101"
	lRet := L010InCb(cEst, cId, dData) //Abre/fecha o CB
EndIf

RestArea(aAreaSLI)
Return lRet  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010VeFeCB     ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario  - Verifica o fechamento do CB     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010VeFeCB(dData)
Local lRet := .T. 					//Retorno da operação de fechamento
Local aAreaSLI := SLI->(GetArea())  //WorkArea da Tabela SLI 
Local cId	:= "101" 				//Id da Transação 
Local cEst	 := PadR(cEstacao, SLI->(TamSx3("LI_ESTACAO")[1]) ) //Codigo da Transacao

Default dData := dDataBase

SLI->(DbSetOrder(1)) //LI_FILIAL+LI_ESTACAO+LI_TIPO 

If SLI->(DbSeek(xFilial("SLI") + cEst + "CBD"))  //Registro de Abertura?
	 
	If RTrim(SLI->LI_MSG) == cId .AND. SLI->LI_DATA <= dData 
		cId := "102"
		lRet := L010InCB(cEst,cId , SLI->LI_DATA)   
	EndIf
EndIf


RestArea(aAreaSLI)
Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010InCB       ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario  - Abre/Fehca o CB                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010InCB(cEst, cIdTipo,  dData)
Local lRet := .F.   //Retorno de execução da Função
Local aArea := GetArea()  //WorkArea Ativa
Local aAreaSLI := Nil     //WorkArea SLI
Local cLI_Filial := xFilial("SLI")  //Filial SLI                                                                    ADMI

Default cEstacao := ""
Default cIdTipo := ""
Default dData := dDataBase 


If  !Empty(cEstacao) .AND. !Empty(cIdTipo)
    lRet := Loja010T(  "B"   	, /*cVenCon*/    	, /*aDados*/		, /*lConsulta*/   	,;
                   /*lImprime*/ 	, /*cFPagReceb*/ 	, /*lTemTEFPend*/	, /*lAbandonaTEF*/	,;
                   /*lTitulo*/		, /*aTefTmp*/		, /*lBaixa*/		, /*lSemRede*/		,;
                   /*cOrcamen*/     , /*lRelGer*/		, /*MsgCupom*/ 	, /*nVlrTotal*/		,;
                   cIdTipo)
                    
    If lRet  
    
		aAreaSLI := SLI->(GetArea()) 
    
	    DbSelectArea("SLI")
	    DbSetOrder(1)//LI_FILIAL+LI_ESTACAO+LI_TIPO    
	    
    	If DbSeek(cLI_Filial + cEst + "CBD")
    		RecLock("SLI", .F.)
    	Else
    		RecLock("SLI", .T.)
    		Replace LI_FILIAL With cLI_Filial
    		Replace LI_ESTACAO With cEst   
    		Replace LI_TIPO With "CBD"
    		
    	EndIf
    	
    	Replace LI_MSG With cIdTipo    	
    	Replace LI_DATA With dData
    	
    	MsUnLock() 
    	
    	RestArea(aAreaSLI) 
    EndIf
    
EndIf

RestArea(aArea)
Return lRet        



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010TrCB       ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario  - Transacao                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010TrCB(cId800, oWsFinaCB) 

Local aDadosTrn	:= {} //Array contendo os dados do campo 801 da transação de CB
   
Loja010T(  "B"   	, /*cVenCon*/    	, /*aDados*/		, /*lConsulta*/   	,;
          /*lImprime*/ 	, /*cFPagReceb*/ 	, /*lTemTEFPend*/	, /*lAbandonaTEF*/	,;
          /*lTitulo*/		, /*aTefTmp*/		, /*lBaixa*/		, /*lSemRede*/		,;
          /*cOrcamen*/     , /*lRelGer*/		, /*MsgCupom*/ 	, /*nVlrTotal*/		,;
          cId800  , oWsFinaCB, @aDadosTrn)

Return   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010CBD        ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Correspondente Bancario Direcao  - Efetua a transacao e    º±±
±±º          ³ imprime o comprovante                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function L010CBD(nI, cId800, oWsFinaCB, aDadosTrn)
Local lRet := .F.       //Retorno da Transação de Correpondente Bancario
Local nValor	:= 0    //Valor da Transação
Local aCartoes	:= {} //Array de transacoes pendentes
Local lImpCFV	:= (cId800 == "103") //Imprime Cupom Fiscal Vinculado?     
Local nRet		:= 0  //Retorno da impressao
Local cTotRecNFis	:= ""  //Totalizador de recebimento não-fiscal
Local cForma	:= ""  //forma de pagamento

DEFAULT oWsFinaCB := nil


aDadosTrn	:= {} //Array com os dados da transacao 

MsgRun( STR0079 ,,;
	   {|| lRet := L010MArq({"000-000 = ADM",;
	   						  "001-000 = " + ALLTRIM(STR(nSeqArq,10)),;
	   						  "800-000 = " + cId800,;
	   						   "999-999 = 0";	
	   						}, nI)})


If lRet
	lRet := L010LeImp(@nValor,nI,,,cId800,@aDadosTrn)       
	
	If lRet .AND. !Empty(cId800)
	 
		If Len(aDadosTrn) >= 22 
		
			//Inicializacao das variaveis das formas de pagamento
			nParCred  := 0
			nParMilh  := 0
			nParDeb   := 0
			// Inicializacao dos totalizadores de valor para CCS
			nParDin   := 0
			nParcheq  := 0
			nDin      := 0
			nCartao   := 0
			nCheq     := 0
			nDebito   := 0
			nMilhas   := 0
			nEntrada  := 0  
			
			If nValor = 0
				nValor := aDadosTrn[21] 
			EndIf
					
			If !Empty(aDadosTrn[16]) 
			
				Do Case
					Case aDadosTrn[16] == "001" //Dinheiro     
						aTefDados[1][15] := SuperGetMV( "MV_SIMB1" )  
						nDin   := nPadDin := nValor 
					Case aDadosTrn[16] == "002" //Cheque   	
						aTefDados[1][15] := "CH"  
						nParCheq  := nCheq := nValor
					Case aDadosTrn[16] == "003" //Cartão de Debito
						aTefDados[1][15] := "CD"  
						nDebito := nParDeb := nValor
					Case aDadosTrn[16] == "004" //Cartão de Credito
						aTefDados[1][15] := "CC"
						nCartao :=  nParCred := nValor
				EndCase 
										
				If Empty(aTefDados[1][9])  //NSU
					aTefDados[1][9] := aDadosTrn[12]
				EndIf  
				
				If Empty(aTefDados[1][2]) //Data da Transacao
					aTefDados[1][2] := Dtos(aDadosTrn[03])
				EndIf 
				
				If Empty(aTefDados[1][3]) //Hora da Transacao 
					aTefDados[1][2] := aDadosTrn[04]
				EndIf
							
			EndIf 
			
		EndIf 
	
	    cForma := RTrim(RetDescrFP(aTefDados[1][15]) )
	
		If Len(aTicket) > 0
		
			aAdd(aCartoes, {1				, 0					, " "		, .T.					,;
				    					nSeqArq				, aTefDisc[nI][1]	, " "		, DTEF_PENDENTE	,;
				    					nI				, " "				, " "		, " "				,;
				    					" "				, "1"})
			
			aCartoes[1][2] := nValor
			aCartoes[1][3] := aTefDados[1][15]
			aCartoes[1][7]	 := IIF(Empty(aTefDados[1][9])	, " ", aTefDados[1][9])		// Numero do DOC (NSU)
			aCartoes[1][10] := IIF(Empty(aTefDados[1][2])	, " ", aTefDados[1][2])		// Data da operacao
			aCartoes[1][11] := IIF(Empty(aTefDados[1][3])	, " ", aTefDados[1][3])		// Hora da operacao
			//aCartoes[nCount][12] := "000000000"											// Finalizacao (fixo)
			aCartoes[1][12] := IIF(Len(aConf) == 0 .OR. Empty(aConf[1][4])	, "000000000", aConf[1][4])		// Finalizacao (fixo)   
			aCartoes[1][13] :=  IIF(Empty(aTefDados[1][5])	, " ", aTefDados[1][5])
			
			LJSaveDTEF(aCartoes) 
	
	
			If lImpCFV .AND. !(aTefDados[1][15] $ "CC/CD")
				cTotRecNFis := GetPvProfString("Correspondente Bancario","Totalizadores", "01", GetClientDir()+"SIGALOJA.INI") 
				lImpCFV  := .F.   
				nRet := IFRecebNFis( nHdlECF, cTotRecNFis, nValor, cForma ) 
				lRet := (nRet == 0)
			EndIf 
			
			If !lRet 
				MsgStop("Erro ao realizar impressão do Correspondente Bancário", "Atenção.") 
				//Envia o desfazimento		
				LJProcDTEF(!lRet)		
			Else
				lRet := L010TefImpr( aTicket    ,/*aPromis     */,.T.  ,.F. ,;
		                          /*formapagrec*/    ,.F. , !lImpCFV )  
		    EndIf 

	                           
	    EndIf
	    
	    		    
	    If lRet
	    	//Envia o Registro de Pagamento/Cancelamento da Transação para a retaguarda  via WS
	    	L010CBDGr(cId800, nValor, aDadosTrn, cForma, @oWsFinaCB)
	    EndIf

	EndIf 
	
EndIf

Return lRet  



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010DTr801     ºAutor  ³Vendas CRM     º Data ³  09/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Direção  - Retorna os Dados do campo 801                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010DTr801(cId800, c801, aDadosTrn) 

If cId800 $ "103#104#107"  

	aDadosTrn := Array(22)
	aDadosTrn[01] := Left(c801,3) //Loja
	aDadosTrn[02] := SubsTr(c801,4,3)	//PDV
	aDadosTrn[03] := CtoD(TransForm(SubsTr(c801,7,8), "99/99/9999"))	 //Data
	aDadosTrn[04] := SubsTr(c801,15,2) + ":" + SubsTr(c801,17,2) + ":" + SubsTr(c801,19,2) //Hora     
	aDadosTrn[05] := Substr(c801, 21, 3) //Codigo da Trn
	aDadosTrn[06] := Substr(c801, 24, 2) //Resultado da Operacao
	aDadosTrn[07] := SubsTr(c801,26,6) //Numero do Cupom	
	aDadosTrn[08] := SubsTr(c801,32,1) //Tipo da Transacao
	aDadosTrn[09] := SubsTr(c801,33, 48) //Documento
	aDadosTrn[10] := SubsTr(c801,81,1) //Tipo de Documnto	
	aDadosTrn[11] := CtoD(TransForm(SubsTr(c801,82,8), "99/99/9999")) //Data de Vencimento	
	aDadosTrn[12] := Substr(c801,90,6) //NSU DTEF
	aDadosTrn[13] := Substr(c801, 96, 6) //NSU Cartão	
	aDadosTrn[14] := SubsTr(c801, 102, 6) //NSU DTEF Cancelada			
	aDadosTrn[15] := SubsTr(c801, 108, 6) //NSU Cartão Cancelado	
	aDadosTrn[16] := SubsTr(c801, 114, 3) //Forma de Pagamento
	aDadosTrn[17] := Val(SubsTr(c801, 117, 12))/100 //Valor do Documento	
	aDadosTrn[18] := Val(SubsTr(c801, 129, 12))/100 //Valor do Desconto	
	aDadosTrn[19] := Val(SubsTr(c801, 141, 12))/100 //Valor do Acrescimo
	aDadosTrn[20] := Val(SubsTr(c801, 153, 12))/100 //Valor Cobrado	
	aDadosTrn[21] := Val(SubsTr(c801, 165, 12))/100 //Valor Recebido  
	aDadosTrn[22] := SubsTr(c801, 177, 2) //Status				

ElseIf cId800 == "009" //Operacao de Recarga de Celular

	aDadosTrn := Array(24)
	aDadosTrn[01] := Left(c801,4) //Numero da Empresa
	aDadosTrn[02] := SubsTr(c801,05,06)	//Numero da Loja
	aDadosTrn[03] := Substr(c801,11,03) //Tipo do REgistro CEL
	aDadosTrn[04] := CtoD(TransForm(SubsTr(c801,14,08), "99/99/9999"))	 //Data
	aDadosTrn[05] := SubsTr(c801,22,02) + ":" + SubsTr(c801,24,02) + ":" + SubsTr(c801,26,2) //Hora     
	aDadosTrn[06] := Substr(c801, 28, 02) //Codigo da Transacao
	aDadosTrn[07] := SubsTr(c801,30,03) //Numero do Equipamento	
	aDadosTrn[08] := SubsTr(c801,33,08) //Numero Fiscal do Equipamento
	aDadosTrn[09] := SubsTr(c801,41,06) //Numero do Cupom
	aDadosTrn[10] := SubsTr(c801,47,02) //Codigo da Area	
	aDadosTrn[11] := SubsTr(c801,49, 08) //Numero do telefone
	aDadosTrn[12] := Substr(c801,57,05) //Reservado
	aDadosTrn[13] := Val(Substr(c801,62, 12))/100 //Valor da Recarga	
	aDadosTrn[14] := SubsTr(c801, 74, 05) //Codigo da Operadora		
	aDadosTrn[15] := SubsTr(c801, 79, 02) //Numero do Autorizador	
	aDadosTrn[16] := SubsTr(c801, 81, 69) //Reservado
	aDadosTrn[17] := SubsTr(c801, 150, 02) //Codigo da Resposta	
	aDadosTrn[18] := SubsTr(c801, 152, 09) //NSU Rede		
	aDadosTrn[19] := SubsTr(c801, 161, 06) //NSU Controle	
	aDadosTrn[20] := SubsTr(c801, 167, 07) //Numero de Autorização da Rede
	aDadosTrn[21] := SubsTr(c801, 173, 20) //Mensagem de Erro 
	aDadosTrn[22] := SubsTr(c801, 193, 59) //Reservado
	aDadosTrn[23] := SubsTr(c801, 252, 04) //Numero do Autorizador 
	aDadosTrn[24] := aTicket				
	
EndIf

Return 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010CBDGr ºAutor  ³Microsiga           º Data ³  16/11/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Gravação dos dados da transação de Correspondente Bancario º±±
±±º          ³ Direcao para a retaguarda                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CBDGr(	cId800	, nValTit	, aDadosTrn	, 	cForma, oWsFinaCB)       
Local cOpera 		:= ""					//Descricao da Operacao no WS
Local lDestObj 		:= .F.                  //Destruição do Objeto WS
Local cPref			:= ""					// Prefixo do titulo
Local lUsaDisplay 	 := .F.					//Mostra display do cheque
Local cNumCupFis	:= ""										// Numero do cupom fiscal
Local cParcela		:= ""		// Parcela
Local cNatureza		:= SuperGetMV("MV_NATCB", , "")				// Natureza da operacao
Local cCliente		:= SuperGetMV("MV_CLIPAD",, "")				// Cliente padrao
Local cLoja			:= superGetMV("MV_LOJAPAD",,"") 							// Loja
Local dEmiss		:= dDataBase								// Data de emissao
Local dVencto		:= dDataBase								// Data de vencimento
Local cHist			:= "CORRESPONDENTE BANCARIO"				// Historico da operacao  
Local nMoeda		:= 1										// Tipo da moeda
Local cPrw			:= "LOJA01T"								// Programa que gerou o lancamento    
Local cPortado		:= ""								// Caixa que realizou a opercao
Local nVezes 		:= 0										// Parametro da Tela
Local cTipo			:= ""					   					// Sigla da forma de pagamento
Local nCont			:= 0										// Parametro da Tela
Local lBotaoOK		:= .F.										// Variavel de retorno
Local lCancela		:= .F.										// Variavel de cancelamento
Local cBanco 		:= ""			   	// Banco do cliente
Local cAgencia		:= ""			// Agencia
Local cConta		:= ""			// Conta
Local cNumChq		:= ""				// Numero do Cheque
Local cCompensa		:= ""				// Compensação
Local cRg			:= ""    			// Rg do cliente
Local cTel			:= ""            	// Telefone
Local lTerceiro		:= .F.										// Indica se o cheque e de terceiro
Local lRet			:= .F.                                      // Controla o retorno da funcao
Local cNsuTEF     	:= ""    // Numero de NSU-Sitef de pagamento
Local cNsuTEFCar     := ""    // Numero de NSU-Sitef de cartão
Local aParcelas      := {} //Clone de pagtos	
Local aDadosCh		 := {} //Dados do Cheque
Local aCheques		 := {} //Cheques     
Local cCodAdm		 := "" //Codigo da Adm Financeira
Local aAreaSAE		 := Nil	//WorkArea da SAE
Local cFiltro 		:= ""				//Filtro da tabela SAE
Local nDias			 := 0 

If cId800 $ "103#104"   

    If oWSFinaCB == Nil
    	oWSFinaCB 		:= WSLJFINACB():New()
		iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oWSFinaCB),Nil) //Monta o Header de Autenticação do Web Service
		oWSFinaCB:_URL 	:= "http://"+Alltrim(LJGetStation("WSSRV"))+"/LJFINACB.apw"  
		lDestObj := .T. 
    EndIf
    
	cTipo := aTEFDados[1][15]   

	
	If cId800 == "103" //Gravação   
	 
		cOpera := "Pagamento de Conta"
		cPref			:= LjGetStation("SERIE")					// Prefixo
		lUsaDisplay 	:= !Empty(LjGetStation("DISPLAY"))
		cParcela		:= LjParcela(1, SuperGetMV("MV_1DUP"))		// Parcela
		cPortado		:= xNumCaixa()							// Loja
		 
		IFPegCupom( nHdlECF, @cNumCupFis )
		dEmiss := LJCalcVenc(.F., dDataBase, .F.)	     
		cNsuTEF := aTefDados[1][9] //Transacao de pagamento
		
		If cTipo == "CH"
						

	 		aParcelas := { { dDataBase ,  nValTit 	,	cTipo ,  "" , ;
	 						"" ,		"" 		, "" 		, 1 , ;
	 						"" ,		""		, ""		, "",;
	 						"" }}  

			AAdd(aCheques,{aParcelas[1][1], aParcelas[1][2], aParcelas[1][3], aParcelas[1][4],;
						   aParcelas[1][5], aParcelas[1][6], aParcelas[1][7], aParcelas[1][8],;
						   aParcelas[1][9]})
								   
			aDadosCh := LjxDGetCh(GetWndDefault(), "", aCheques, cCliente, ; 
											cLoja	, @aParcelas, @lUsaDisplay	)
									
			cBanco 		:= aDadosCh[1][2]
			cAgencia	:= aDadosCh[1][4]
			cConta		:= aDadosCh[1][5]
			cNumChq		:= aDadosCh[1][3]
			cCompensa	:= aDadosCh[1][9]
			cRg			:= aDadosCh[1][6]
			cTel		:= aDadosCh[1][7]
			lTerceiro	:= aDadosCh[1][8]
		
		ElseIf cTipo $ "CC#CD" .AND. Len(aDadosTrn) >= 20 .AND. !Empty(Val(aDadosTrn[13]))
			
			cNsuTEFCar := aDadosTrn[13]
    
           	If !Empty(aTefDados[1][8])
				aAreaSAE := SAE->(GetArea())
	           	
				cFiltro :=  "AE_FILIAL =  '" + xFilial("SAE") + "' .AND. "+;
							"Upper(Left( AE_DESC, 16 ) ) == '" + Upper(PadR(aTefDados[1][8],16))  + "'  "
					
				SAE->(dbSetFilter({|| &cFiltro},cFiltro)) 
				SAE->(dbGoTop()) 
			
			   	If SAE->(!Eof()) 
			   		cCodAdm := SAE->AE_COD
			   	EndIf  
			   
			   	SAE->(dbClearFilter()) 
				RestArea(aAreaSAE)  
			
			EndIf
			
		EndIf 
		
		LJMsgRun("Gravando Movimento Transação Bancária",, { |oDlg| lRet := oWSFinaCB:IncluiTit(	cPref		, cNumCupFis, cParcela	, cTipo		, ;		// Gravando Movimento
															cNatureza	, cCliente	, cLoja		, dEmiss	, ;
															dVencto		, cHist		, nMoeda	, cPrw		, ;
															nValTit		, cPortado	, cBanco 	, cAgencia	, ;
															cConta 		, cNumChq	, cCompensa , cRg 		, ;
															cTel		, lTerceiro , cNsuTEF	, cNsuTEFCar,;
															cCodAdm		,	.T. ) } )
		
	Else //If cId800 = "104" //Estorno
	    
		If Len(aDadosTrn) >= 20 .AND. !Empty(aDadosTrn[14])
           cNsuTEF := aDadosTrn[14] 
           		
			cOpera := "Cancelamento de Pagamento de Conta" 
			LJMsgRun("Cancelando Movimento Transação Bancária",, { |oDlg| lRet := oWSFinaCB:EstornaTit( cNsuTEF ) } )
		
        EndIf 
	EndIf
	
	If lDestObj 
		FreeObj(oWSFinaCB)
		oWSFinaCB := Nil
	EndIf      

Else
	lRet := .T.
EndIf

If !lRet
	MsgStop("Não foi possivel a gravacao de Correspondente Bancario " + cOpera)
EndIf

Return lRet    

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010RCGr  ºAutor  ³Microsiga           º Data ³  26/11/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Realização da transação de impressão de Recarga de Celular º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function L010RCGr(cId800		, nValor		, aDadosTrn		, oWsFinaCel	,;
					aPgtos	 	, nTroco       , cTotRCelNFis 	, aTEFMult		,;
					lTEFPendRec , lTEFMult)  

Local lRet := .F.     //Retorno da transação de recarga de celular
Local nRet		:= 0  //Retorno da impressao
Local cForma	:= ""  //forma de pagamento  
Local cTipo		:= "" //Tipo da forma de pagamento       
Local aTEFCCBkp	:= {} //Array aTEFDados da transacao de Cartao 
Local lRelGer	:= .T. //Impressão em relatorio gerencial


DEFAULT lTEFPendRec := .F. 
DEFAULT lTEFMult	:= SuperGetMV("MV_TEFMULT", ,.F.)

cForma := RTrim(RetDescrFP(aPgtos[1][3]) )


If !(cTipo := RTrim(aPgtos[1][3])) $ "CC/CD"
	nRet := IFRecebNFis( nHdlECF, cTotRCelNFis, nValor, cForma ) 
	lRet := (nRet == 0)
Else
	lRet := .T.
	aTEFCCBkp := aClone(aTEFDados)  
	lRelGer := .F.
EndIf 

If !lRet 
	MsgStop("Erro ao realizar impressão da Recarga de Celular", "Atenção.") 
	LOJA010T( "F"   ,"D"   ,NIL   ,NIL   ,;	//Antigamente esta função nao retornava verdadeiro ou falso
			         	NIL   ,NIL   ,NIL   ,NIL   )		
Else

   lRet := 	LOJA010T( "I"   ,IIF(lRelGer,"N","V")     ,NIL           ,NIL   ,;
		       NIL   ,cForma ,lTEFPendRec   ,NIL   ,;
		       NIL	 ,NIL	 ,NIL			,NIL	,;
		       NIL	 ,NIL	 ,NIL			,NIL	,;
		       cId800,NIL	 ,NIL			,.T.)  	       
EndIf 
  		    
If ValType(lRet) == "L" .and. lRet
	//Envia o Registro de Pagamento/Cancelamento da Transação para a retaguarda  via WS
	L010CRCDGr(cId800	, nValor	, aDadosTrn	, cTipo	,;
			 @oWsFinaCel, aTEFCCBkp , aPgtos) 
EndIf

Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³L010CRCDGrºAutor  ³Microsiga           º Data ³  11/16/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Gravação dos dados da transação de Recarga de Celular      º±±
±±º          ³ Direcao para a retaguarda                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function L010CRCDGr(	cId800		, nValTit	, aDadosTrn	, 	cTipo, ;
							oWsFinaCel	, aTEFCCBkp , aPgtos)       

Local cPref			:= LjGetStation("SERIE")					// Prefixo
Local cNumCupFis	:= ""										// Numero do cupom fiscal
Local cParcela		:= LjParcela(1, SuperGetMV("MV_1DUP"))		// Parcela
Local cNatureza		:= SuperGetMV("MV_NATRC", , "")				// Natureza da operacao
Local cCliente		:= SuperGetMV("MV_CLIPAD",, "")				// Cliente padrao
Local cLoja			:= superGetMV("MV_LOJAPAD",,"") 							// Loja
Local dEmiss		:= dDataBase								// Data de emissao
Local dVencto		:= dDataBase								// Data de vencimento
Local cHist			:= "RECARGA DE CELULAR"						// Historico da operacao  
Local nMoeda		:= 1										// Tipo da moeda
Local cPrw			:= "LOJA010T"								// Programa que gerou o lancamento
Local cPortado		:= xNumCaixa()								// Caixa que realizou a opercao
Local cBanco 		:= ""										// Banco do cliente
Local cAgencia		:= ""										// Agencia
Local cConta		:= ""										// Conta
Local cNumChq		:= ""										// Numero do Cheque
Local cCompensa		:= ""										// Compensação
Local cRg			:= ""										// Rg do cliente
Local cTel			:= ""                                      	// Telefone
Local lTerceiro		:= .F.										// Indica se o cheque e de terceiro
Local lRet			:= .F.
Local cNsuTEF     	:= ""    // Numero de NSU-Sitef de pagamento
Local cNsuTEFCar     := ""    // Numero de NSU-Sitef de cartão
Local aParcelas      := {} //Clone de pagtos	
Local aDadosCh		 := {} //Dados do Cheque
Local aCheques		 := {} //Cheques     
Local cCodAdm		 := "" //Codigo da Adm Financeira
Local aAreaSAE		 := Nil	//WorkArea da SAE
Local cFiltro := ""         //filtro da tabela SAE
Local lDestObj		:= .F.   //Destruição do Objeto do WS de Recarga de Celular

If cId800 $ "009"   

    If oWsFinaCel == Nil
    	oWsFinaCel 		:= WSLjFinaCel():New()
		iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oWsFinaCel),Nil) //Monta o Header de Autenticação do Web Service
		oWsFinaCel:_URL 	:= "http://"+Alltrim(LJGetStation("WSSRV"))+"/LJFINACEL.apw"  
		lDestObj := .T. 
    EndIf

	IFPegCupom( nHdlECF, @cNumCupFis )
	dEmiss := LJCalcVenc(.F., dDataBase, .F.)	     
	cNsuTEF := aDadosTrn[19]  //Nsu
	
	If cTipo == "CH"				

		cBanco 		:= aPgtos[1,4, 4]
		cAgencia	:= aPgtos[1,4, 5]
		cConta		:= aPgtos[1,4, 6]
		cNumChq		:= aPgtos[1,4, 7]
		cCompensa	:= aPgtos[1,4, 8]
		cRg			:= aPgtos[1,4, 9]
		cTel		:= aPgtos[1,4,10]
		lTerceiro	:= aPgtos[1,4,12]
	
	ElseIf cTipo $ "CC#CD" .AND. Len(aTEFCCBkp[1]) > 9 .AND. !Empty(Val(aTEFCCBkp[1][9]))
		
		cNsuTEFCar := aTEFCCBkp[1][9]    //realizar este tratamento aki
		//VErificar como é localizada a administradora financeira para gerar o SE1  
		//administradora enviada pelo campo 
     
     	If !Empty(aTEFCCBkp[1][8])
			aAreaSAE := SAE->(GetArea())
           	
			cFiltro :=  "AE_FILIAL =  '" + xFilial("SAE") + "' .AND. "+;
						"Upper(Left( AE_DESC, 16 ) ) == '" + Upper(PadR(aTEFCCBkp[1][8],16))  + "'  "
				
			SAE->(dbSetFilter({|| &cFiltro},cFiltro)) 
			SAE->(dbGoTop()) 
		
		   	If SAE->(!Eof()) 
		   		cCodAdm := SAE->AE_COD
		   	EndIf  
		   
		   	SAE->(dbClearFilter()) 
			RestArea(aAreaSAE)  
		
		EndIf
		
	EndIf 
	
	LJMsgRun("Gravando Recarga de Celular",, { |oDlg| lRet := oWsFinaCel:IncluiTit(	cPref		, cNumCupFis, cParcela	, cTipo		, ;		// Gravando Movimento
													cNatureza	, cCliente	, cLoja		, dEmiss	, ;
													dVencto		, cHist		, nMoeda	, cPrw		, ;
													nValTit		, cPortado	, cBanco 	, cAgencia	, ;
													cConta 		, cNumChq	, cCompensa , cRg 		, ;
													cTel		, lTerceiro , cEmpAnt	, cFilAnt	, ;
																, cNsuTEF	, Right(cNSUTEFCar,6), cCodAdm	, ;
													.T.) } )	


	If lDestObj 
		FreeObj(oWsFinaCel)
		oWSFinaCB := Nil
	EndIf  
Else
	lRet := .T.
EndIf 

If !lRet
	MsgStop("Não foi possivel a gravacao da recarga de celular ")
EndIf

Return lRet     


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ºPrograma  ³LOJA010T  ºAutor  ³Microsiga           º Data ³  11/28/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna a aTEFDados					                     º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function L010TefD()

REturn aTEFDados


//--------------------------------------------------------
/*/{Protheus.doc} LjRedesPayGo(cRede)
Retornar se a Adm Financeira selecionda, faz parte das Ad. Pay&Go
@param cRede		Rede selecionada pelo Usuario

@author  Varejo
@version P11.8
@since   10/04/2015
@return	 Verdadeiro de Adm Financeira selecionda for uma Adm do Pay&Go ou False caso contrario 
/*/
//--------------------------------------------------------
Static Function LjRedesPayGo(cRede)
Local aRedesPayGo 	:= {}		// Array com as Adm Financeira passadas pel NTK
Local lRet			:= .F.		// Retorno da funcao
Local nPosRede		:= 0		// Posicao da Adm dentro do array aRedesPayGo

Aadd(aRedesPayGo,{"VISANET"})
Aadd(aRedesPayGo,{"REDECARD"})
Aadd(aRedesPayGo,{"BANRISUL"})
Aadd(aRedesPayGo,{"GETNET"})
Aadd(aRedesPayGo,{"ELAVON"})
Aadd(aRedesPayGo,{"CSHOP"})
Aadd(aRedesPayGo,{"TRIBANCO"})
Aadd(aRedesPayGo,{"POLICARD"})
Aadd(aRedesPayGo,{"FANCARD"})
Aadd(aRedesPayGo,{"BCARD"})
Aadd(aRedesPayGo,{"COOPERCR"})
Aadd(aRedesPayGo,{"VLCARD"})
Aadd(aRedesPayGo,{"TKTCAR"})
	
nPosRede := aScan(aRedespayGo, {|x| AllTrim(Upper(x[1])) == Alltrim(Upper(cRede)) })

lRet := nPosRede > 0
	
Return lRet 


//--------------------------------------------------------
/*/{Protheus.doc} LjRmvCRLF(cTexto)
Retorna uma string sem os caracteres de quebra de linha
@param cTexto		String a ser tratada

@author  Varejo
@version P11.8
@since   18/11/2015
@return	 String sem os caracteres de quebra de linha 
/*/
//--------------------------------------------------------
Static Function LjRmvCRLF(cTexto)

Local cCaracter := ""
Local cTextoAux := ""
Local nTamTexto := 0
Local nI		:= 0

Default cTexto	:= ""

cTexto := AllTrim(cTexto)
nTamTexto := Len( cTexto )

For nI := 1 to nTamTexto
	cCaracter := SubStr(cTexto, nI,1) 
	If (cCaracter <> CHR(10)) .AND. (cCaracter <> CHR(13)) .AND. (cCaracter <> CRLF)  	
		cTextoAux += cCaracter
	EndIf
Next

Return cTextoAux


//--------------------------------------------------------
/*/{Protheus.doc} LJRenDTEF(cTexto)
Renomeia o arquivo TEFPEND.TMP para TEFOK.TMP
quando o cupom fiscal da venda for impresso com sucesso
@author  Varejo
@version P11.8
@since   03/05/2016
@return	 Nulo 
/*/
//--------------------------------------------------------
Static Function LJRenDTEF()  
Local cPathSC	:= GetClientDir()
Local nRetAux	:= 0
Local lRetAux	:= .T.

//se o arquivo EXISTE
If File( cPathSC + "TEFOK.TMP" )
	//APAGA o arquivo
	nRetAux := FErase( cPathSC + "TEFOK.TMP" )
	If nRetAux <> 0
		lRetAux := .F.
		MsgStop( "Erro ao APAGAR o arquivo TEFOK.TMP - FERROR: " + Str(FError(),4) )			
	EndIf
EndIf

//RENOMEIA o arquivo TEFPEND.TMP para TEFOK.TMP 
If lRetAux
	nRetAux := FRename( cPathSC + "TEFPEND.TMP" , cPathSC + "TEFOK.TMP" )
	If nRetAux <> 0	
		MsgStop( 'Falha ao renomear o arquivo TEFPEND.TMP - FError: ' + Str(FError(),4) )
	EndIf
EndIf

Return Nil


//-----------------------------------------------------
/*/{Protheus.doc} LJ010CNTC
Função que exibe o espelho do cupom TEF a ser cancelado.
Ela somente e chamada se houver erro durante a venda.
@param aDados
@param nI
@return lRet
@author michael.gabriel
@since 21/08/2017                                                   
/*/
//-----------------------------------------------------
Static Function L010CNTEFC(aDados, nI)

Local oDlg
Local oSayInfo	:= Nil	//objeto TSay com informacoes sobre o uso do espelho
Local cSayInfo	:= ""
Local oMultiGet := Nil	//objeto TMultiGet que contem o espelho do cupom
Local cMultiGet
Local oBtnInfo  := Nil	//botao que contém mais informacoes sobre o cancelamento
Local oBtnGoOn  := Nil	//botao continuar que prossegue com o cancelamento
Local lRet		:= .F.

DEFINE MSDIALOG oDlg TITLE "Cancelamento TEF CONFIRMADAS" FROM 000, 000  TO 360, 265 COLORS 0, 16777215 PIXEL

	@ 005, 005 SAY oSayInfo PROMPT "Cupom TEF da transação que foi confirmada e que precisa ser cancelada: " + CRLF + ;
	"Clique no botão Continuar e informe o dado solicitado para prosseguir com o cancelamento." SIZE 130, 036 OF oDlg COLORS 0, 16777215 PIXEL
	
    @ 050, 005 GET oMultiGet VAR cMultiGet OF oDlg MULTILINE SIZE 125, 110 COLORS 0, 16777215 READONLY HSCROLL PIXEL
	L010CECTEF( aDados, @cMultiGet )	//carrega o espelho do cupom TEF a ser cancelado
	@ 163, 005 BUTTON oBtnInfo PROMPT "+ Informações" SIZE 044, 012 OF oDlg ACTION LJ010TMsg(1) PIXEL
	@ 163, 055 BUTTON oBtnGoOn PROMPT "Continuar" SIZE 037, 012 OF oDlg ACTION (lRet := L010PCnc(aDados, nI), oDlg:End()) PIXEL

	//desabilita a tecla ESC
	oDlg:lEscClose := .F.

ACTIVATE MSDIALOG oDlg

Return lRet


//-----------------------------------------------------------------
/*/{Protheus.doc} L010CECTEF
Função que carrega no label o espelho do cupom TEF a ser cancelado.
@param aDados
@param oCupomTEF
@return Nil
@author michael.gabriel
@since 21/08/2017                                                   
/*/
//-----------------------------------------------------------------
Static Function L010CECTEF(aDados, cSayCupTEF)

Local cNSUTEF       := ""
Local cTexto        := ""
Local cPathCTEF     := ""   //caminho do espelho do cupom TEF

//obtem o NSU da transacao
cNSUTEF := cValToChar(Val(aDados[4]))	//remove o 0 a esquerda, pois o nome do arquivo nao é criado com ele

//atraves do NSU, obtemos o caminho do arquivo do espelho do cupom
cPathCTEF := GetSrvProfString("rootPATH", "\undefined") + GetSrvProfString("STARTPATH", "\undefined") + "nsu" + cNSUTEF + ".tmp"

//recupera o conteudo do espelho do cupom
cTexto := MemoRead( cPathCTEF )    
If !Empty(cTexto)
	cSayCupTEF := cTexto
Else
	cSayCupTEF := "Não foi possível ter acesso ao espelho do cupom TEF." + CRLF + "Caso não consiga realizar o cancelamento, faça-o através do Gerenciador."
	LjGrvLog(Nil, "Não foi possível ter acesso ao arquivo com o espelho do cupom TEF.", cPathCTEF)
EndIf

Return Nil


//-----------------------------------------------------
/*/{Protheus.doc} LJ010TMsg
Função com as mensagens que são exibidas para o usuário
@param nOpcao
@return lRet
@author michael.gabriel
@since 21/08/2017                                                   
/*/
//-----------------------------------------------------
Static Function LJ010TMsg(nOpcao)

Local cMsg := ""
Local lRet := .F.

Default nOpcao := 2

If nOpcao == 1

    cMensagem := "ATENÇÃO:"
    cMensagem += CRLF + "1 - O cancelamento das TEF só podem ser feitos MANUALMENTE e um por um."
    cMensagem += CRLF + "2 - Em caso de problemas no cancelamento, ele deverá ser realizado via Gerenciador."
	cMensagem += CRLF + "3 - Na tela anterior, é exibido o espelho do cupom TEF que será cancelado."
	cMensagem += CRLF + "4 - Clique no botão Continuar para que o cancelamento do TEF prossiga "

    Aviso("Info sobre Cancelamento TEF", cMensagem, {"OK, Entendi"}, 2 )

ElseIf nOpcao == 2 

    cMensagem := "Somente feche essa janela se:
    cMensagem += CRLF + "- Todas as TEFs foram canceladas OU
    cMensagem += CRLF + "- Se serão canceladas através do Gerenciador TEF"
    cMensagem += CRLF + "Tem certeza que deseja continuar?"
    lRet := MsgYesNo(cMensagem)
EndIf

Return lRet


//--------------------------------------------------------
/*/{Protheus.doc} L010LoadTef()
Carrega informações de Bandeira, pasta para leitura (requisição), pasta para gravação (retorno), executável do Gerenciador Padrão.
ATENÇAO: Os 10 últimos elementos do array são do TEF-AUTTAR. Caso ocorra a alteração, favor alterar a ordem do elemento na função L010IsAuttar().
@author  Varejo
@version P12
@since   23/08/2017
@return	 aRet
/*/
//--------------------------------------------------------
Function L010LoadTef()

Local cDirReq  := ":\TEF_DIAL\REQ\"
Local cDirResp := ":\TEF_DIAL\RESP\"
Local cDirTBReq  := ":\TEF_DISC\REQ\"
Local cDirTBResp := ":\TEF_DISC\RESP\"
Local aRet := {}

If !Empty(LJGetStation("DIRTTX")) .AND. !Empty(LjGetStation("DIRTRX"))
	cDirReq		:= LJGetStation("DIRTTX")
	cDirResp 	:= LJGetStation("DIRTRX")
	cDirTBReq	:= LJGetStation("DIRTTX")
	cDirTBResp	:= LJGetStation("DIRTRX")
EndIf

aRet 	:= {{'TECBAN'    ,cDirTBReq		, cDirTBResp,':\tef_disc\tef_disc.exe'		,.F.}	,;											//01
			{'REDECARD'  ,cDirReq    	, cDirResp  ,':\tef_dial\tef_dial.exe'		,.F.}	,;											//02
 			{'VISANET'   ,cDirReq    	, cDirResp  ,':\tef_dial\tef_dial.exe'		,.F.}	,;											//03
			{'AMEX'      ,cDirReq    	, cDirResp  ,':\tef_dial\tef_dial.exe'		,.F.}	,;											//04
			{'BANESECARD',':\BCARD\REQ\'      		,':\BCARD\RESP\'     ,''                       		,.F.}	,;						//05
			IIf(File("C:\HIPERTEF\HIPERTEF.EXE")	,	;
				{'HIPERCARD', ':\HIPERTEF\REQ\'			, ':\HIPERTEF\RESP\'	, ':\HIPERTEF\HIPERTEF.EXE'	,.F.}	,;
				{'HIPERCARD', ':\DISKTEF\DIR_SOLI\'		, ':\DISKTEF\DIR_RESP\'	, ':\DISKTEF\BIN\GC.EXE'	,.F.})	,;					//06
			IIf(File("C:\TEF_CSHP\VPOS_AC.EXE")		,	;							 
				{'CSHOP'	, ':\TEF_CSHP\REQ\'			, ':\TEF_CSHP\RESP\'	, ':\TEF_CSHP\VPOS_AC.EXE'	,.F.}  ,;
				{'CSHOP'	, cDirReq		, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.}) ,;	//07
			{'CERTIF'   ,cDirReq    	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//08
			{'REDECARD' ,cDirReq    	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//09
			{'VISANET'  ,cDirReq    	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//10
			{'AMEX'     ,cDirReq      	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//11
			{'BANRISUL' ,cDirReq      	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//12
			{'HIPERCARD',cDirReq      	, cDirResp    , ':\Arquivos de programas\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;		//13
			; //Para Win8 e Win10
			IIf(File("C:\TEF_CSHP\VPOS_AC.EXE")		,	;							 
				{'CSHOP'	, ':\TEF_CSHP\REQ\'			, ':\TEF_CSHP\RESP\'	, ':\TEF_CSHP\VPOS_AC.EXE'	,.F.}  ,;
				{'CSHOP'	, cDirReq		, cDirResp  , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.}) ,;		//14
			{'CERTIF'   ,cDirReq    	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//15
			{'REDECARD' ,cDirReq    	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//16
			{'VISANET'  ,cDirReq    	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//17
			{'AMEX'     ,cDirReq      	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//18
			{'BANRISUL' ,cDirReq      	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//19
			{'HIPERCARD',cDirReq      	, cDirResp    , ':\Program Files (x86)\SETIS\Pay&Go Cliente\' + cPayGoExec	,.F.} ,;			//20
			;//Para Win8 e Win10
			{'REDECARD'	,cDirReq      	, cDirResp    , ':\DPOS8\Bin\GPDirecao.exe'									,.F.} ,;			//21
			{'AMEX'		,cDirReq      	, cDirResp    , ':\DPOS8\Bin\GPDirecao.exe'									,.F.} ,;			//22
			{'CIELO'	,cDirReq      	, cDirResp    , ':\DPOS8\Bin\GPDirecao.exe'									,.F.} ,;			//23
			{'VISANET'	,cDirReq      	, cDirResp    , ':\DPOS8\Bin\GPDirecao.exe'									,.F.} ,;			//24
			{'VISANET'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//25
			{'REDECARD'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//26
			{'TECBAN'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//27
			{'AMEX'		 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//28
			{'HIPERCARD' ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//29
			{'CIELO'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//30
			{'CSHOP'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//31
			{'BANRISUL'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//32
			{'MASTERCARD',cDirReq      	, cDirResp    , ':\Program Files (x86)\MacroSistem\Express TEF\ExpressTEF.exe'	,.F.} ,;		//33
			{'VISANET'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//34
			{'REDECARD'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//35
			{'TECBAN'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//36
			{'AMEX'		 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//37
			{'HIPERCARD' ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//38
			{'CIELO'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//39
			{'CSHOP'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//40
			{'MASTERCARD',cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//41
			{'BANRISUL'	 ,cDirReq      	, cDirResp    , ':\Program Files\MacroSistem\Express TEF\ExpressTEF.exe'			,.F.} ,;	//42
			{'REDECARD'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\Auttar\CTFClient\bin\ClientCTF.exe'			,.F.} ,;	//43
			{'VISANET'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\Auttar\CTFClient\bin\ClientCTF.exe'			,.F.} ,;	//44
			{'CIELO'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\Auttar\CTFClient\bin\ClientCTF.exe'			,.F.} ,;	//45
			{'BANRISUL'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\Auttar\CTFClient\bin\ClientCTF.exe'			,.F.} ,;	//46
			{'GETNET'	 ,cDirReq      	, cDirResp    , ':\Program Files (x86)\Auttar\CTFClient\bin\ClientCTF.exe'			,.F.} ,;	//47						
			{'REDECARD'	 ,cDirReq      	, cDirResp    , ':\Program Files\Auttar\CTFClient\bin\ClientCTF.exe'				,.F.} ,;	//48					
			{'VISANET'	 ,cDirReq      	, cDirResp    , ':\Program Files\Auttar\CTFClient\bin\ClientCTF.exe'				,.F.} ,;	//49
			{'CIELO'	 ,cDirReq      	, cDirResp    , ':\Program Files\Auttar\CTFClient\bin\ClientCTF.exe'				,.F.} ,;	//50
			{'BANRISUL'	 ,cDirReq      	, cDirResp    , ':\Program Files\Auttar\CTFClient\bin\ClientCTF.exe'				,.F.} ,;	//51
			{'GETNET'	 ,cDirReq      	, cDirResp    , ':\Program Files\Auttar\CTFClient\bin\ClientCTF.exe'				,.F.}}		//52
							
Return aRet


//--------------------------------------------------------
/*/{Protheus.doc} L010IsAuttar()
Verifica se a bandeira é do tipo Auttar
@author  Varejo
@version P12
@since   23/08/2017
@param   nBandeira : nI da Bandeira referente ao aTefDisc
@return	 lRet : .T. se for Auttar, .F. se não for Auttar
/*/
//--------------------------------------------------------
Function L010IsAuttar(nBandeira)
Local lRet := .F.

lRet := ("|" + AllTrim(Str(nBandeira)) + "|" $ "|43|44|45|46|47|48|49|50|51|52|")

Return lRet 

//-------------------------------------------------------------------
/*/{Protheus.doc} Lj010GtOpc
Função para mostrar a opção de Débito ou Voucher.
@param aCartoes
@param nTipo
@return  
@author  eduardo.sales
@since   24/11/2017
/*/
//-------------------------------------------------------------------
Static Function Lj010GtOpc(aCartoes, nTipo)

Local oDlg
Local lConfirm 		:= .F.
Local cRet	 		:= ""
Local oFont 		:= TFont():New("Courier",,-15,.T.)
Local aBotoes 		:= {}
Local cCaption 		:= ""
Local cToolTip		:= ""
Local nTamTitBot	:= 0
Local nLinhaObj		:= 15
Local nColunaObj	:= 25
Local nBotLargur	:= 150
Local nBotAltura	:= 18
Local nInd 			:= 0
Local bBlocoCmd 	:= {|| Nil}
Local cOpcao 		:= Space(1)
Local nOpcoes 		:= 0

//Botoes
// 				Titulo do botao   	ToolTip
AAdd( aBotoes, { "Débito"			, "Efetua a venda com Débito" 	} ) // "Débito"
AAdd( aBotoes, { "Voucher"			, "Efetua a venda com Voucher"	} ) // "Voucher"

aEval( aBotoes, { |x| nTamTitBot := If(Len(x[1]) > nTamTitBot, Len(x[1]), nTamTitBot)} )

/*-----------------------------------------------------------------------
	Monta a tela para a escolha do tipo de cartão (Débito / Voucher)
-----------------------------------------------------------------------*/
DEFINE MSDIALOG oDlg TITLE "Pay&Go - Tipo de Cartão" FROM 00,00 TO 15,50 STYLE DS_MODALFRAME 
@ 005,005 TO 110,195 LABEL "Informe o tipo de cartão para:" OF oDlg PIXEL

oDlg:lEscClose	:= .F.	// Desabilita a tecla ESC

If nTipo == 1
	@ nLinhaObj		, nColunaObj SAY "ID do Cartão: " + aCartoes[14] OF oDlg PIXEL
	@ nLinhaObj + 7	, nColunaObj SAY "Parcela: " + AllTrim(Str(aCartoes[1])) OF oDlg PIXEL
	@ nLinhaObj + 14, nColunaObj SAY "Valor: R$ " + AllTrim(Str(aCartoes[2])) OF oDlg PIXEL
Else
	@ nLinhaObj		, nColunaObj SAY "Data TEF: " + aCartoes[5] OF oDlg PIXEL
	@ nLinhaObj + 7	, nColunaObj SAY "NSU: " + aCartoes[4] OF oDlg PIXEL
	@ nLinhaObj + 14, nColunaObj SAY "Parcela: " + AllTrim(Str(aCartoes[9])) OF oDlg PIXEL
	@ nLinhaObj + 21, nColunaObj SAY "Valor: R$ " + AllTrim(Str(aCartoes[8])) OF oDlg PIXEL

	nLinhaObj := nLinhaObj + 5
EndIf

nLinhaObj := nLinhaObj + 25

For nInd := 1 To Len(aBotoes)
	cCaption 	:= StrZero(nInd,1) + "-" + PadR(aBotoes[nInd][1], nTamTitBot) 				// Titulo do Botao
	cToolTip 	:= aBotoes[nInd][2] 														// Mensagem ToolTip do botao
	bBlocoCmd	:= &("{|| (lConfirm:=.T.,cOpcao:='" + StrZero(nInd,1) + "',oDlg:End()) }") 	// Bloco de comando do botao
	
	//Monta o Botao
	TButton():New( nLinhaObj, nColunaObj, cCaption, oDlg, bBlocoCmd, nBotLargur, nBotAltura, , oFont, .F., .T., .F., cToolTip, .F., , , .F. )
	nLinhaObj := nLinhaObj + 20
Next nInd

nOpcoes 	:= nInd - 1
nLinhaObj 	:= nLinhaObj + 5

@ nLinhaObj + 2, nColunaObj SAY "Opção:" OF oDlg PIXEL
oGetOpc := TGet():New(nLinhaObj,nColunaObj+20,bSETGET(cOpcao),oDlg,010,010,,;
			{|| If( Empty(cOpcao), .T., If(Lj010VlOpc(cOpcao,nOpcoes), (lConfirm:=.T.,oDlg:End()), .F.) ) },,,,,,.T.,,,,,,,.F.,,,)

oGetOpc:SetFocus()

ACTIVATE MSDIALOG oDlg CENTERED

If lConfirm
	Do Case
	    // Venda com Débito
		Case cOpcao == "1"
			cRet := "2"
			
	    // Venda com Voucher
		Case cOpcao == "2"
			cRet := "3"
	EndCase
EndIf

Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Lj010VlOpc
Função para validar a opção.
@param cOpcao
@param nOpcoes
@return lRet, lógico 
@author  eduardo.sales
@since   24/11/2017
/*/
//-------------------------------------------------------------------
Static Function Lj010VlOpc(cOpcao, nOpcoes)

Local lRet 		:= .T.
Local cValidos 	:= "12" 	//Valores Validos
Local nInd 		:= 1

cOpcao := AllTrim(cOpcao)

While nInd <= Len(cOpcao)
	If !(SubStr(cOpcao,nInd,1) $ cValidos)
		MsgAlert("Opção inválida!") //"Opção inválida!"
		lRet := .F.
		Exit
	EndIf
	nInd++
End

If lRet
	If Val(cOpcao) < 1 .Or. Val(cOpcao) > nOpcoes
		MsgAlert("Opção inválida!") //"Opção inválida!"
		lRet := .F.
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Lj010VlZer
Função para validar o valor digitado
@param nValor, numerico, valor do Get
@return lRet, lógico 
@author  julio.nery
@since   02/12/2019
/*/
//-------------------------------------------------------------------
Static Function Lj010VlZer(nValor)
Local lRet := .T.
Local cMsg := ""

Default nValor := 0

If nValor == 0
	cMsg := "Valor Zerado é inválido! " + CHR(10) + CHR(13) +;
			"Por favor digite valor maior que zero"
ElseIf nValor < 0
	cMsg := "Valor abaixo de $ 0.01! " + CHR(10) + CHR(13) +;
			"Por favor digite valor maior que zero"
EndIf

If !Empty(cMsg)
	MsgAlert(cMsg)
	lRet := .F.
EndIf

Return lRet
