#INCLUDE "TOTVS.CH"
#include "qiea030.ch"
#include "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWADAPTEREAI.CH"

Static lIntPOS := SuperGetMV("MV_LJSYNT",.F.,"0") == "1"

/*/


Ŀ
Funo     QIEA030   Autor  Vera Lucia S. Simoes   Data  05/11/97 
Ĵ
Descrio  Programa de atualizacao de Unidades de Medida              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function MenuDef()

aRotina := {}
ADD OPTION aRotina Title STR0002 Action 'VIEWDEF.QIEA030'	OPERATION 2 ACCESS 0 //Visualizar
ADD OPTION aRotina Title STR0003 Action 'VIEWDEF.QIEA030'	OPERATION 3 ACCESS 0 //Incluir
ADD OPTION aRotina Title STR0004 Action 'VIEWDEF.QIEA030'	OPERATION 4 ACCESS 0 //Alterar
ADD OPTION aRotina Title STR0005 Action 'VIEWDEF.QIEA030'	OPERATION 5 ACCESS 0 //Excluir

Return aRotina

Function QIEA030(xRotAuto,nOpc)

Private lQ030Auto := (xrotAuto <> Nil)
Private aRotAuto := Nil

//Ŀ
// Gravar no arquivo SAH o conteudo da tabela 62 do SX5         
//
If FindFunction('A207UM'+cPaisLoc )
	&('A207UM'+cPaisLoc+'()')
Else
	A207UM()
Endif

If lQ030Auto     
	Private aRotina := MenuDef()
	aRotAuto:=xRotAuto
	FWMVCRotAuto(ModelDef(),"SAH",nOpc,{{"SAHMASTER",aRotAuto}})	
Else
	oBrowse := BrowseDef()
	oBrowse:Activate()
EndIf

Set Key VK_F12 To
Return .T.

/*/{Protheus.doc} BrowseDef
Criao da rotina BrowseDef.
@author felipe.morais
@since 05/02/2018
@version 1.0
@return oBrowse, Object
@type function
/*/

Function BrowseDef()
Local oBrowse as Object

Private aRotina as Array

aRotina := MenuDef()

oBrowse := FwMBrowse():New()
oBrowse:SetAlias("SAH")
Return oBrowse

/*/

Ŀ
Funo     A030DUMe  Autor  Vera Lucia S. Simoes   Data  10/11/97 
Ĵ
Descrio  Programa de atualizacao de Unidades de Medida - Delecao    
Ĵ
Sintaxe    A030DUMe(ExpC1,ExpN1,ExpN2)                                
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       QIEA030                                                    
ٱ


/*/
Function A030DUMe(cAlias,nReg,nOpc)
Local aArea 	:= GetArea()
Local aParam	:= {{|| .T.}, {|| A030TudoOk(nOpc)}, {|| .T.}, {|| .T.}}	//Bloco de codigo executado apos a transacao de Excluso
Local lRetorno  := .T.
Local nOpcA		:= 0    

//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  :=  FWHasEAI("QIEA030",.T.,,.T.) 

lQ030Auto := If (Type("lQ030Auto") == "U", .f., lQ030Auto) 

nOpcA:= AxDeleta(cAlias,nReg,nOpc,,,,aParam,aRotAuto)

If nOpcA <> 2
	lRetorno := .F.		       
EndIf	
//-- Chamada manual da IntegDef
If lIntegDef .And. lRetorno
	FwIntegDef("QIEA030")
EndIf
	       
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo     A030IUMe  Autor  Vera Lucia S. Simoes   Data  10/11/97 
Ĵ
Descrio  Programa atualizacao Unidades Medida - Inclusao/Alteracao  
Ĵ
Sintaxe    A030IUMe(ExpC1,ExpN1,ExpN2)                                
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       QIEA030                                                    
ٱ


/*/
FUNCTION A030IUMe(cAlias,nReg,nOpc)
LOCAL nOpcA:=0  
//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef  := FWHasEAI("QIEA030",.T.,,.T.) 
Local lQE030ALT := ExistBlock("QE030ALT")

//Ŀ
// Monta a entrada de dados do arquivo                          
//
PRIVATE aTELA[0][0],aGETS[0]
lQ030Auto := If (Type("lQ030Auto") == "U", .f., lQ030Auto)
If ( lQ030Auto )
	If nOpc == 3  // Inclusao
  		AxIncluiAuto(cAlias) 
	Elseif nOpc == 4  // Alteracao 
		AxAltera(cAlias,nReg,nOpc,,,,,,,,,,aRotAuto,)

		If lQE030ALT
			ExecBlock("QE030ALT",.F.,.F.)
		EndIf
	EndIf
Else      
	While .T.

		//Ŀ
		// Envia para processamento dos Gets          
		//
		nOpcA:=0

		Begin Transaction
			If nOpc == 3  // Inclusao
				nOpcA:=AxInclui( cAlias, nReg, nOpc,,,,"A030TudoOk(nOpc)")
	
			ElseIf nOpc == 4  // Alteracao
				nOpcA:=AxAltera( cAlias, nReg, nOpc,,,,,"A030TudoOk(nOpc)")
	
				If lQE030ALT
					ExecBlock("QE030ALT",.F.,.F.)
				EndIf
			Endif
		End Transaction
		Exit
	EndDo
	
	//-- Chamada manual da IntegDef
	If lIntegDef .And. nOpcA == 1
		FwIntegDef("QIEA030")
	EndIf
EndIf

//Ŀ
// Integracao PIMS GRAOS                             
//			    
If nOpcA == 1
	If (nOpc == 3 .Or. nOpc == 4) .And. SuperGetMV("MV_PIMSINT",.F.,.F.) 
		PIMSGeraXML("UnitOfMeasure","Unidade de Medida","2","SAH")
	EndIf
EndIf	  
						
dbSelectArea(cAlias)
Return

/*


Ŀ
Funcaoo    QIE030UM  Autor  Paulo Emidio de Barros Data  04/06/02 
Ĵ
Descrio  Verifica se a Unidade de Medida podera ser excluida		  
Ĵ
 Uso       QIEA030													  
ٱ


*/
Static Function QIE030UM(cUM)   
Local aAreaAnt := GetArea()
Local lRetorno := .T.
Local cCond1   := ''
Local cIndex1  := ''
Local cChave1  := ''
Local nIndex1  := 0  
Local nCount1  := 0

Local cCond2   := ''
Local cIndex2  := ''
Local cChave2  := ''
Local nIndex2  := 0
Local nCount2  := 0

Local cCond3   := ''
Local cIndex3  := ''
Local cChave3  := ''
Local nIndex3  := 0
Local nCount3  := 0

Local cCond4   := ''
Local cIndex4  := ''
Local cChave4  := ''
Local nIndex4  := 0
Local nCount4  := 0

Local cCond5   := ''
Local cIndex5  := ''
Local cChave5  := ''
Local nIndex5  := 0
Local nCount5  := 0

Local cCond6   := ''
Local cIndex6  := ''
Local cChave6  := ''
Local nIndex6  := 0
Local nCount6  := 0

//Ŀ
// Filtra a Unidade de Medida a se excluida					 
//

//Ŀ
// Verifica as unidades cadastradas nas Especificacoes 		 
//
cCond1 += 'QE6_FILIAL == "'+xFilial("QE6")+'" .And.'
cCond1 += '(QE6_UNMED1 == "'+cUM+'" .Or. '
cCond1 += 'QE6_UNMED2 == "'+cUM+'" .Or. '
cCond1 += 'QE6_UNAMO1 == "'+cUM+'" .Or. '
cCond1 += 'QE6_UNAMO2 == "'+cUM+'")'                   

dbSelectArea("QE6")
dbSetOrder(1)
cIndex1 := CriaTrab(NIL,.F.)
cChave1 := IndexKey()
IndRegua("QE6",cIndex1,cChave1,,cCond1,STR0012) //"Verificando Unidade de Medida..."
nIndex1 := RetIndex("QE6") 
dbSetOrder(nIndex1+1)
Count to nCount1

RetIndex("QE6") 
Set Filter To
fErase(cIndex1+OrdBagExt())

//Ŀ
// Verifica as unidades cadastradas nos Ensaios mensuraveis	 
//
If (nCount1 == 0) .And. lRetorno
	cCond2 += 'QE7_FILIAL == "'+xFilial("QE7")+'" .And.'
	cCond2 += 'QE7_UNIMED == "'+cUM+'"'
	
	dbSelectArea("QE7")
	dbSetOrder(1)
	cIndex2 := CriaTrab(NIL,.F.)
	cChave2 := IndexKey()
	IndRegua("QE7",cIndex2,cChave2,,cCond2,STR0012) //"Verificando Unidade de Medida..."
	nIndex2 := RetIndex("QE7") 
	dbSetOrder(nIndex2+1)
	Count to nCount2
	
	RetIndex("QE7") 
	Set Filter To
	fErase(cIndex2+OrdBagExt())
Else
	lRetorno := .F.	
EndIf

//Ŀ
// Verifica as unidades cadastradas nas Entradas a Inspecionar/ 
// Inspecionadas												  
//
If (nCount2 == 0) .And. lRetorno
	cCond3 += 'QEK_FILIAL == "'+xFilial("QEK")+'" .And.'
	cCond3 += 'QEK_UNIMED == "'+cUM+'"'
	dbSelectArea("QEK")
	dbSetOrder(1)
	cIndex3 := CriaTrab(NIL,.F.)
	cChave3 := IndexKey()
	IndRegua("QEK",cIndex3,cChave3,,cCond3,STR0012) //"Verificando Unidade de Medida..."
	nIndex3 := RetIndex("QEK") 
	dbSetOrder(nIndex3+1)
	Count to nCount3
	
	RetIndex("QEK") 
	Set Filter To
	fErase(cIndex3+OrdBagExt())
Else
	lRetorno := .F.	
EndIf	

//Ŀ
// Verifica as unidades cadastras no cadastro de Produto		 
//
If (nCount3 == 0) .And. lRetorno
	cCond4 += 'B1_FILIAL == "'+xFilial("SB1")+'" .And.'
	cCond4 += 'B1_UM == "'+cUM+'"'
	dbSelectArea("SB1")
	dbSetOrder(1)
	cIndex4 := CriaTrab(NIL,.F.)
	cChave4 := IndexKey()
	IndRegua("SB1",cIndex4,cChave4,,cCond4,STR0012) //"Verificando Unidade de Medida..."
	nIndex4 := RetIndex("SB1") 
	dbSetOrder(nIndex4+1)
	Count to nCount4
	
	RetIndex("SB1")  
	Set Filter To
	fErase(cIndex4+OrdBagExt())
Else
	lRetorno := .F.	
EndIf	

//Ŀ
// Verifica as unidades cadastras no Especificacao de Produtos  
//
If (nCount4 == 0) .And. lRetorno
	cCond5 := 'QP6_FILIAL == "'+xFilial("QP6")+'" .And. '
	cCond5 += '( QP6_UNMED1 == "'+cUM+'" .Or. QP6_UNMED2 == "'+cUM+'" .Or. '
	cCond5 += 'QP6_UNAMO1 == "'+cUM+'" .Or. QP6_UNAMO2 == "'+cUM+'" )'
	dbSelectArea("QP6")
	dbSetOrder(1)
	cIndex5 := CriaTrab(NIL,.F.)
	cChave5 := IndexKey()
	IndRegua("QP6",cIndex5,cChave5,,cCond5,STR0012) //"Verificando Unidade de Medida..."
	nIndex5 := RetIndex("QP6") 
	dbSetOrder(nIndex5+1)
	Count to nCount5
	
	RetIndex("QP6")  
	Set Filter To
	fErase(cIndex5+OrdBagExt())
Else
	lRetorno := .F.	
EndIf	

//Ŀ
// Verifica as unidades cadastras nos Ensaios Mensuraveis       
//
If (nCount5 == 0) .And. lRetorno
	cCond6 := 'QP7_FILIAL == "'+xFilial("QP7")+'" .And. QP7_UNIMED == "'+cUM+'"'
	dbSelectArea("QP7")
	dbSetOrder(1)
	cIndex6 := CriaTrab(NIL,.F.)
	cChave6 := IndexKey()
	IndRegua("QP7",cIndex6,cChave6,,cCond6,STR0012) //"Verificando Unidade de Medida..."
	nIndex6 := RetIndex("QP7") 
	dbSetOrder(nIndex6+1)
	Count to nCount6
	
	RetIndex("QP7")  
	Set Filter To
	fErase(cIndex6+OrdBagExt())
Else
	lRetorno := .F.	
EndIf	

If (nCount6 > 0) .And. lRetorno
	lRetorno := .F.	
EndIf

RestArea(aAreaAnt)

Return(lRetorno)	       

/*


Ŀ
Funcaoo   QIE030UM1  Autor  Aline Sebrian          Data  18.02.09 
Ĵ
Descrio  Verifica se a Unidade de Medida podera ser excluida		  
Ĵ
 Uso       QIEA030													  
ٱ


*/
Static Function QIE030UM1(cUM)   
Local aAreaAnt    := GetArea()
Local aFiliais    := {}
Local aSM0CodFil  := {}
Local lPodeApagar := .T.                  
Local nBusca      := 0 
Local nRecnoSM0   := SM0->(RecNo())
	               
//Ŀ
// Preenche um array com as filiais		   
//	
dbSelectArea("SM0")
dbGoTop()

Do While ! Eof()
	If SM0->M0_CODIGO == cEmpAnt
		Aadd(aSM0CodFil, FWGETCODFILIAL)
	Endif
	dbSkip()
EndDo
dbGoto(nRecnoSM0) 


//Ŀ
// Verifica se est cadastrado na Tabela de Converso de UM	   
//
If lPodeApagar
	dbSelectArea("NNX")
	aFiliais := If(FWModeAccess("NNX")=="E" .and. FWModeAccess("SAH")=="C", aClone(aSM0CodFil), {xFilial()}) //	// ! Empty(xFilial()) .and. Empty(xFilial("SAH"))
	For nBusca := 1 to Len(aFiliais)
		dbSetOrder(1)
		If dbSeek( aFiliais[nBusca] + SAH->AH_UNIMED)
			lPodeApagar := .F.
			Exit
		EndIf   
		
		dbSetOrder(2)
		If dbSeek( aFiliais[nBusca] + SAH->AH_UNIMED)
			lPodeApagar := .F.
			Exit
		EndIf
	Next
Endif


RestArea(aAreaAnt)
Return(lPodeApagar)


/*


Ŀ
Funcaoo    QIE030UM  Autor  Emerson Rony Oliveira  Data  22/06/09 
Ĵ
Descrio  Verifica se a Unidade de Medida podera ser excluida		  
Ĵ
 Uso       QIEA030													  
ٱ


*/
Static Function QIE030UM2(cUM)   
Local aAreaAnt := GetArea()
Local lRetorno := .T.
Local cCond1   := ''
Local cIndex1  := ''
Local cChave1  := ''
Local nIndex1  := 0  
Local nCount1  := 0

//Ŀ
// Filtra a Unidade de Medida a ser excluida                    
//

//Ŀ
// Verifica as unidades cadastras no cadastro de Produto        
//
cCond1 += 'B1_FILIAL == "'+xFilial("SB1")+'" .And.'
cCond1 += 'B1_UM == "'+cUM+'"'
dbSelectArea("SB1")
dbSetOrder(1)
cIndex1 := CriaTrab(NIL,.F.)
cChave1 := IndexKey()
IndRegua("SB1",cIndex1,cChave1,,cCond1,STR0012) //"Verificando Unidade de Medida..."
nIndex1 := RetIndex("SB1") 
dbSetOrder(nIndex1+1)
Count to nCount1

RetIndex("SB1")  
Set Filter To
fErase(cIndex1+OrdBagExt())

If (nCount1 > 0)
	lRetorno := .F.	
EndIf
	
RestArea(aAreaAnt)

Return(lRetorno)	       

/*

Ŀ
Funo    A030TudoOk Autor Aecio Ferreira Gomes    Data  07/04/11 
Ĵ
Descrio  Faz consistencias na confirmao da gravao  			  	   
Ĵ
Parametros Nenhum												                  
Ĵ
Retorno    .T. / .F. 												            
Ĵ
 Uso       QIEA030                                                    
ٱ


*/
Function A030TudoOk(nOpc)
Local lRet 		:= .T.
Local lQ030VLD  := .F.
Local lIntSFC 	:= ExisteSFC("SAH") .And. !IsInCallStack("AUTO045")// Determina se existe integracao com o SFC
Local lIntDPR 	:= IntegraDPR() .And. !IsInCallStack("AUTO045")// Determina se existe integracao com o DPR
Local aCampos := {}

// ====================================================================
// VERIFICA INTEGRACAO COM SIGATAF 
Local lIntTAF  := FindFunction("TAFExstInt") .AND. TAFExstInt()

If Type("lQ030Auto") <> "L"
	Private lQ030Auto := .F.
EndIf

If nOpc == 5 
	If	!(GetRpoRelease() >= "R7")
		//Ŀ
		// Realiza a verificacao da existencia das Unidades de Medidas  
		//
		If SubStr(cModulo,1,1) == "Q"  //Somente os modulos do Quality		
			If lQ030Auto
				lRet := QIE030UM(SAH->AH_UNIMED)
			Else
				MsgRun(STR0010,STR0011,{||lRet := QIE030UM(SAH->AH_UNIMED)}) //"Verificando a existencia da Unidade de Medida a ser excluida" ### "Aguarde..."
			EndIf
			If !lRet
				Help(" ",1,"QIE30DELUM")    
			EndIf       
		Else
			If lQ030Auto
				lRet := QIE030UM1(SAH->AH_UNIMED)
			Else
				MsgRun(STR0010,STR0011,{||lRet := QIE030UM1(SAH->AH_UNIMED)}) //"Verificando a existencia da Unidade de Medida a ser excluida" ### "Aguarde..."
			EndIf
			If !lRet
				Help(" ",1,"QIE30DCUM")    
			EndIf
		
			If lQ030Auto
				lRet := QIE030UM2(SAH->AH_UNIMED)
			Else
				MsgRun(STR0010,STR0011,{||lRet := QIE030UM2(SAH->AH_UNIMED)}) //"Verificando a existencia da Unidade de Medida a ser excluida" ### "Aguarde..."
			EndIf
			If !lRet
				Help(" ",1,"QIE30DELUM")    
			EndIf
		EndIf	
	EndIf		
	//Ŀ
	// INTEGRACAO NG - VERIFICA SE EXISTE O REGISTRO NAS TABELAS DA NG  
	//
	If lRet
		lRet := NGINTUMEDID(SAH->AH_UNIMED,.T.)
	Endif
	
	//Ŀ
	// Apos passar por todas as verificacoes, exclui o registro	 
	//
	
	// Funo usada para validar se existe integrao do Protheus com o RM Solum e
	// Para chamar a validao especfica do RM Solum na deleo do Produto. (PMSXFUNB)
	If lRet .AND. !PmsSlmPrd("UM")
	   lRet := .F.
	Endif 
Else
	If ExistBlock("QE030VLD")
		lQ030VLD:=ExecBlock("QE030VLD",.F.,.F.)
		If ValType(lQ030VLD) == "L"
			lRet := lQ030VLD
		EndIf
	EndIf
EndIf

//Ŀ
//Chama rotina para integracao com SFC e DPR	  
//    
If lRet .And. (lIntSFC .Or. lIntDPR)
	lRet := Q30IntSFC(nOpc)
EndIf	
	
// ==============================================
// Demetrio - 11/2014 - Integrao TAF 
// ==============================================
If lRet .AND. lIntTAF
	MsgRun( "Realizando integrao com SIGATAF","Aguarde..." , {|| TAFIntOnLn("T005",nOpc,cFilAnt) } ) // "Aguarde"
EndIf 	

Return lRet

/*


ͻ
Programa  IntegDef  Autor   Marcelo C. Coutinho   Data   28/10/11   
͹
Descricao  Mensagem nica												
͹
Uso        Mensagem nica                                            	
ͼ


*/
Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransac, lEAIObj )
Default xEnt := ""
Default nTypeTrans := ""
Default cTypeMessage := ""
Default cVersion := ""
Default cTransac := ""
Default lEAIObj := .F.
	
Return QIEI030(xEnt,nTypeTrans,cTypeMessage, cVersion, cTransac, lEAIObj )


//-------------------------------------------------------------------
/*{Protheus.doc} Q30IntSFC
Atualiza tabelas do SFC conforme modelagem dos dados(MVC)

@author Leonardo Quintania
@since 13/11/2012
@version 11.80
*/
//-------------------------------------------------------------------
Function Q30IntSFC(nOpc,cError,cNome,oModel)
Local aArea   := GetArea()	// Salva area atual para posterior restauracao
Local lRet    := .T.		// Conteudo de retorno
Local aCampos := {}			// Array dos campos a serem atualizados pelo modelo
Local aAux    := {}			// Array auxiliar com o conteudo dos campos
Local nX	  	:= 0			// Indexadora de laco For/Next
//Local oModel  := NIL		// Objeto de modelo de dados
Default oModel  := FWLoadModel("SFCA023")		// Objeto de modelo de dados

If nOpc == 3
	aAdd(aCampos,{"CZ9_CDUN",M->AH_UNIMED})
EndIf

If nOpc # 5  
	if IsInCallStack('CARGASFC') .Or. IsInCallStack('CARGASFC2') .Or. IsInCallStack('CARGADPR') 
		aAdd(aCampos,{"CZ9_DSUN",M->AH_UMRES})
	Else
		aAdd(aCampos,{"CZ9_DSUN",fwfldget('AH_UMRES')})	
	EndIf
EndIf

//Ŀ
//Instancia modelo de dados(Model) de Unidades de Medidas - SFC e DPR 	
//
//oModel := FWLoadModel("SFCA023")
oModel:SetOperation(nOpc)

If nOpc # 3
	//Ŀ
	//Quando se tratar de alteracao ou exclusao primeiramente o registro devera ser posicionado 
	//
	CZ9->(dbSetOrder(1))
	CZ9->(dbSeek(xFilial("CZ9")+SAH->AH_UNIMED))
EndIf
		
//Ŀ
//Ativa o modelo de dados 
//
If (lRet := oModel:Activate())
	//Ŀ
	//Obtem a estrutura de dados do Model 
	//
	aAux := oModel:GetModel("CZ9MASTER"):GetStruct():GetFields()
	
	//Ŀ
	//Loop para validacao e atribuicao de dados dos campos do Model 
	//
	For nX := 1 To Len(aCampos)
		//Ŀ
		//Valida os campos existentes na estrutura do Model 
		//
		If aScan(aAux,{|x| AllTrim(x[3]) ==  AllTrim(aCampos[nX,1])}) > 0
			//Ŀ
			//Atribui os valores aos campos do Model caso passem pela validacao do formulario 
			//referente a tipos de dados, tamanho ou outras incompatibilidades estruturais.   
			//
			If !(oModel:SetValue("CZ9MASTER",aCampos[nX,1],aCampos[nX,2]))
				lRet := .F.
				Exit       
			EndIf
		EndIf
	Next nX
Endif

If lRet
	//Ŀ
	//Valida os dados e integridade conforme dicionario do Model 
	//
	If (lRet := oModel:VldData())
		//Ŀ
		//Efetiva gravacao dos dados na tabela 
		//
		lRet := oModel:CommitData()
	EndIf
EndIf

//Ŀ
//Gera log de erro caso nao tenha passado pela validacao 
//
If !lRet
	A010SFCErr(oModel,@cError,NIL,cNome,SAH->AH_UNIMED)
EndIf

//Ŀ
//Desativa o Model 
//
oModel:DeActivate()

RestArea(aArea)
Return lRet

//-------------------------------------------------------------------
/*	Modelo de Dados
@autor  	Ramon Neves
@data 		05/04/2012
@return 		oModel Objeto do Modelo*/
//-------------------------------------------------------------------
Static Function ModelDef()
Local oStruSAH := FWFormStruct( 1, "SAH")                              
Local oModel := MPFormModel():New("QIEA030",, {|oModel|QE030VldP(oModel)}, {|oModel| Q030Commit(oModel)})
oModel:AddFields("SAHMASTER",, oStruSAH)
oModel:GetModel("SAHMASTER"):SetDescription(STR0006) 
Return oModel
//-------------------------------------------------------------------
/*	Interface da aplicacao
@autor  	Ramon Neves
@data 		20/04/2012
@return 		oView Objeto da Interface*/
//-------------------------------------------------------------------
Static Function ViewDef()
Local oModel   := FWLoadModel("QIEA030")
Local oStruSAH := FWFormStruct(2,"SAH")
Local oView  

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField("VIEW_SAH",oStruSAH,"SAHMASTER")
Return oView  
//-------------------------------------------------------------------
//Centraliza as pos-validacoes 
//@autor  	Ramon Neves
//@data 		20/04/2012
//-------------------------------------------------------------------
Static Function QE030VldP(oModel)
Local aArea:= GetArea()
Local lRet := .T.
Local nOpc := oModel:GetOperation()

lRet := A030TudoOk(nOpc)

RestArea(aArea)
Return(lRet)

//-------------------------------------------------------------------
//Commit do formulrio MVC
//@autor  	julio.nery
//@data 	26/09/2014
//-------------------------------------------------------------------
Function Q030Commit(oModel)
Local lRet		:= .T.
local nOpcX	:= oModel:GetOperation()

	If FWFormCommit(oModel)
		QE030Trans(nOpcX)
	Else
		lRet := .F.
	EndIf

Return lRet

//-------------------------------------------------------------------
//Funo chamada dentro do bloco de transao da alterao
//@autor  	julio.nery
//@data 	26/09/2014
//-------------------------------------------------------------------
Function QE030Trans(nOpcX)
Local lPIMSINT := SuperGetMV("MV_PIMSINT",.F.,.F.) // Indica se Existe Integrao Protheus x PIMS Graos
Local lUsaMsg  := SuperGetMV("MV_MSGPIMS",.F.,.F.) // Nova integrao via MGS Unica

lUsaMsg := Iif(ValType(lUsaMsg) == 'C', Iif((UPPER(lUsaMsg) == '.T.' .Or. UPPER(lUsaMsg) == 'T'), .T., .F.), lUsaMsg)

//-- Tratamento para integracao POS - Synthesis
If (nOpcX == MODEL_OPERATION_UPDATE) .And. lIntPOS .And. SAH->AH_POSFLAG == "1"	
	SAH->AH_POSDTEX := CToD("")
EndIf

//Ŀ
// Integracao PIMS GRAOS                             
//			    
If lPIMSINT .And. !lUsaMsg .And. (nOpcX == MODEL_OPERATION_INSERT .Or. nOpcX == MODEL_OPERATION_UPDATE)
	PIMSGeraXML("UnitOfMeasure","Unidade de Medida","2","SAH")
EndIf

Return
