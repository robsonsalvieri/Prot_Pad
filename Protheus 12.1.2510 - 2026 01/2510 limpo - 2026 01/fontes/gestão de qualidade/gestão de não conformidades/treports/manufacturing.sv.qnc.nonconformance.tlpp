#include "manufacturing.sv.qnc.nonconformance.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qnc

/*/{Protheus.doc} SmartViewQNCNonConformance
Classe responsável pelos métodos de criação e manipulação do Smart View
@author brunno.costa
@since 10/04/2024
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                   ;
	active         = .T.                                    ,;
	team           = "SIGAQNC"                              ,;
	tables         = "QI2,QI3,QAA"                          ,;
	customTables   = "QI2,QAA"                              ,;
	name           = "Ficha de Não Conformidade (Qualidade)",;
	country        = "ALL"                                  ,;
	initialRelease = "12.1.2310"         )

Class SmartViewQNCNonConformance From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                 as Character
	Public Data cUsedFields            as Character
    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aCoalesceTestFields as Array
	Protected Data aFields             as Array
	Protected Data aMirrorFields       as Array
	Protected Data aMirrorTables       as Array
    Protected Data aPlanFields         as Array
	Protected Data aPlanStruct         as Array
    Protected Data aStruct             as Array
	Protected Data cQuery              as Character
	Protected DATA oQLTTReports        as object
	Protected DATA oQueryManager       as object

    Public Method getData(nPage, oFilter)        as object
    Public Method getDescription()               as Character
    Public Method getDisplayName()               as Character
    Public Method getSchema()                    as object
    Public Method new()                          as object

	Protected Method aninhaDadosPlanos(jParams)
	Protected Method exibicaoSigilosa(cMatFil, cMatCod) as Logical
	Protected Method mountDefaulFields()
	Protected Method mountDefaultPlanFields()
	Protected Method setUpQuery(nPage, oFilter)

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author brunno.costa
@since 10/04/2024
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQNCNonConformance
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) // "Qualidade - Gestão de Não Conformidades"

    self:setIsLookUp(.T.)

	self:cPergunte := "QNR050SV"
    self:setPergunte(self:cPergunte) // Indica o pergunte que será utilizado no relatório

	self:aCoalesceTestFields := {}
	self:aMirrorFields       := {}
	self:lForceAllTrim       := .T.

	self:oQLTTReports  := QLTTReportsManager():New()
	self:oQueryManager := QLTQueryManager()   :New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author brunno.costa
@since 10/04/2024
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQNCNonConformance
Return STR0002 // "Ficha de Não Conformidade (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author brunno.costa
@since 10/04/2024
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQNCNonConformance
Return STR0003 //"Objeto com os registros das tabelas QAA e QI2. Propriedade aninhada 'PlanoDeAcaoRelacionado' com registros da tabela QI3."

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author brunno.costa
@since 10/04/2024
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQNCNonConformance
  
    Local aArea        := GetArea()          as Array
	Local aBindParam   := {}                 as Array
	Local aPriori      := {}                 as Array
	Local aStatus      := Nil                as Array
	Local aUsrMat      := QNCUSUARIO()       as Array
	Local cMatCod      := aUsrMat[3]         as Character
	Local cMatFil      := aUsrMat[2]         as Character
	Local jParams      := Nil                as Json
	Local lChangeQuery := .F.                as Logical
	Local oComentarios := JsonObject():New() as Json
	Local oDisposicao  := JsonObject():New() as Json

	//Preenche Arrays Auxiliares
	aStatus      := {OemtoAnsi(STR0004),OemToAnsi(STR0005),OemToAnsi(STR0006),OemToAnsi(STR0007),OemToAnsi(STR0008)}	// "Registrada" ### "Em Analise" ### "Procede" ### "Nao Procede" ### "Cancelada"
	QNCCBOX("QI2_PRIORI",@aPriori)

	//Método para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    self:setPageSize(10)

	DBSelectArea("QI2")
	DBSelectArea("QI3")
	DBSelectArea("SA1")
	DBSelectArea("SA2")
	DBSelectArea("QAA")
	DBSelectArea("SYP")
	DBSelectArea("QI9")

	QI2->(dbSetOrder(1))
	QI9->(dbSetOrder(1))

	self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	While !(self:cAlias)->(Eof())

		self:jData  := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct, {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]       ) } )			

		//Adiciona dados do Cabeçalho do Relatório
		QLTTReportsManager():addReportHeader(self)

		If self:exibicaoSigilosa(cMatFil, cMatCod)

			self:jData['EXIBICAO_SIGILOSA'] := .T.

		Else

			self:jData['EXIBICAO_SIGILOSA'] := .F.

			If jParams['MV_PAR10'][1] == 1
				//Aninha dados dos Planos de Ações Relacionados
				self:aninhaDadosPlanos(jParams)
			EndIf

			//Popula Descrição Detalhada
			self:jData['DESCRICAO']                   := AllTrim(MSMM((self:cAlias)->QI2_DDETA))
			
			//Popula Resultado Esperado ANINHADO
			oComentarios["COMENTARIOS"]               := AllTrim(MSMM((self:cAlias)->QI2_COMEN))
			If !Empty(oComentarios["COMENTARIOS"])
				self:jData['Comentarios'] := {oComentarios}
			Else
				self:jData['Comentarios'] := {}
			EndIf

			//Popula Disposição ANINHADA
			oDisposicao["DISPOSICAO"]                  := AllTrim(MSMM((self:cAlias)->QI2_DISPOS))
			If !Empty(oDisposicao["DISPOSICAO"])
				self:jData['Disposicao'] := {oDisposicao}
			Else
				self:jData['Disposicao'] := {}
			EndIf

			self:jData[ 'A_CATEGORIA' ]  := FQNCNTAB("4",(self:cAlias)->QI2_CODCAT)
			self:jData[ 'A_CAUSA' ]      := FQNCNTAB("1",(self:cAlias)->QI2_CODCAU)
			self:jData[ 'A_DISPOSICAO' ] := FQNCCHKDIS((self:cAlias)->QI2_CODDIS)
			self:jData[ 'A_EFEITO' ]     := FQNCNTAB("2",(self:cAlias)->QI2_CODEFE)
			self:jData[ 'A_ORIGEM' ]     := FQNCNTAB("3",(self:cAlias)->QI2_CODORI)
			self:jData[ 'CLIENTE' ]      := AllTrim(Posicione( 'SA1' ,1,xFilial( 'SA1' )+(self:cAlias)->QI2_CODCLI+(self:cAlias)->QI2_LOJCLI, 'A1_NOME' ))
			self:jData[ 'CONTATO' ]      := AllTrim((self:cAlias)->QI2_CONTAT)
			self:jData[ 'FORNECEDOR' ]   := AllTrim(Posicione( 'SA2' ,1,xFilial( 'SA2' )+(self:cAlias)->QI2_CODFOR+(self:cAlias)->QI2_LOJFOR, 'A2_NOME' ))
			self:jData[ 'ORIGINADOR' ]   := AllTrim(QA_NUSR((self:cAlias)->QI2_FILMAT, (self:cAlias)->QI2_MAT, .F.))
			self:jData[ 'PRIORIDADE' ]   := aPriori[Val((self:cAlias)->QI2_PRIORI)]
			self:jData[ 'STATUS' ]       := aStatus[Val((self:cAlias)->QI2_STATUS)]
			self:jData[ 'TIPO' ]         := QA_CBOX("QI2_TPFIC",(self:cAlias)->QI2_TPFIC)

		EndIf

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório de Planos de Acoes
@author brunno.costa
@since 10/04/2024
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQNCNonConformance

	Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
	Local jParams      := Nil                        as Json

	//Campos usados em chamadas Externas - INICIO
	cExternalUse := "QI2_ANO"
	cExternalUse += ",QI2_CODACA"
	cExternalUse += ",QI2_CODCAT"
	cExternalUse += ",QI2_CODCAU"
	cExternalUse += ",QI2_CODCLI"
	cExternalUse += ",QI2_CODDIS"
	cExternalUse += ",QI2_CODEFE"
	cExternalUse += ",QI2_CODFOR"
	cExternalUse += ",QI2_CODORI"
	cExternalUse += ",QI2_COMEN"
	cExternalUse += ",QI2_CONTAT"
	cExternalUse += ",QI2_DDETA"
	cExternalUse += ",QI2_DESCR"
	cExternalUse += ",QI2_DISPOS"
	cExternalUse += ",QI2_FILIAL"
	cExternalUse += ",QI2_FILMAT"
	cExternalUse += ",QI2_FILRES"
	cExternalUse += ",QI2_FNC"
	cExternalUse += ",QI2_LOJCLI"
	cExternalUse += ",QI2_LOJFOR"
	cExternalUse += ",QI2_MAT"
	cExternalUse += ",QI2_MATRES"
	cExternalUse += ",QI2_OCORRE"
	cExternalUse += ",QI2_PRIORI"
	cExternalUse += ",QI2_REV"
	cExternalUse += ",QI2_REVACA"
	cExternalUse += ",QI2_SIGILO"
	cExternalUse += ",QI2_STATUS"
	cExternalUse += ",QI2_TPFIC"
	//Campos usados em chamadas Externas - FIM

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery := " SELECT " + self:oQLTTReports:getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	
	self:cQuery +=    " ( SELECT DISTINCT ' ' A "
	self:cQuery +=                  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QI2", "QAA"},"QI2_FILIAL,QI2_ANO,QI2_FNC,QI2_REV,QI2_TPFIC", "")

	//Cabeçalho da Inspeção de Processos
	self:cQuery += " FROM " + RetSQLName("QI2") + " QI2 "

	//Responsáveis
	aAdd(aBindParam, {" "             , "S"})
	self:cQuery += " LEFT JOIN " + RetSQLName("QAA") + " QAA "
	self:cQuery += " ON "
	self:cQuery +=        " QAA.QAA_FILIAL = QI2.QI2_FILMAT "
	self:cQuery +=    " AND QAA.QAA_MAT    = QI2.QI2_MAT "
	self:cQuery +=    " AND QAA.D_E_L_E_T_ = ? "

	self:cQuery += " WHERE 1=1 "

    If !Empty(jParams['MV_PAR01'][1])
        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
        self:cQuery  += " AND ? <= QI2.QI2_FILIAL "
    Endif

    If !Empty(jParams['MV_PAR02'][1])
        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"}) 
        self:cQuery  += " AND QI2.QI2_FILIAL <= ? "
    Endif

	If !Empty(jParams['MV_PAR03'][1])
        aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery  += " AND ? <= QI2.QI2_ANO "
    Endif

    If !Empty(jParams['MV_PAR04'][1])
        aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"}) 
        self:cQuery  += " AND QI2.QI2_ANO <= ? "
    Endif

	If !Empty(jParams['MV_PAR05'][1])
        aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})
        self:cQuery  += " AND ? <= QI2.QI2_FNC "
    Endif

    If !Empty(jParams['MV_PAR06'][1])
        aAdd(aBindParam, {jParams['MV_PAR06'][1], "S"}) 
        self:cQuery  += " AND QI2.QI2_FNC <= ? "
    Endif

	If !Empty(jParams['MV_PAR07'][1])
        aAdd(aBindParam, {jParams['MV_PAR07'][1], "S"})
        self:cQuery  += " AND ? <= QI2.QI2_REV "
    Endif

    If !Empty(jParams['MV_PAR08'][1])
        aAdd(aBindParam, {jParams['MV_PAR08'][1], "S"}) 
        self:cQuery  += " AND QI2.QI2_REV <= ? "
    Endif
		
	If jParams['MV_PAR09'][1] <> 4 //Todos
		If(!Empty(jParams['MV_PAR09'][1]))
			aAdd(aBindParam, {AllTrim(Str(jParams['MV_PAR09'][1])), "N"}) 
			self:cQuery += " AND QI2.QI2_TPFIC = ? "
		Endif
	EndIf

	aAdd(aBindParam, {" "      ,"S"}) 
	self:cQuery += " AND QI2.D_E_L_E_T_ = ? "  

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

	self:cQuery  += " ) DIST "

Return

/*/{Protheus.doc} aninhaDadosPlanos
Aninha dados dos Planos de Ações Relacionados
@author brunno.costa
@since 10/04/2024
@version 1.0
@param 01 - jParams     , json  , objeto json com os parâmetros do relatório
/*/
Method aninhaDadosPlanos(jParams as Json) class SmartViewQNCNonConformance

	Local cCamposSeek := "" as Character
	Local cChaveSeek  := "" as Character
	Local cIFWhile    := "" as Character
	Local cLineExec   := "" as Character

	If QI9->(dbSeek((self:cAlias)->QI2_FILIAL + (self:cAlias)->QI2_CODACA + (self:cAlias)->QI2_REVACA))

		While QI9->(!Eof()) .And. QI9->QI9_FILIAL +           QI9->QI9_CODIGO +           QI9->QI9_REV ==;
		                (self:cAlias)->QI2_FILIAL + (self:cAlias)->QI2_CODACA + (self:cAlias)->QI2_REVACA

			cIFWhile    := ""
			cCamposSeek := "QI3_FILIAL+QI3_ANO+QI3_CODIGO+QI3_REV"
			cChaveSeek  := QI9->QI9_FILIAL+Right(QI9->QI9_CODIGO,4)+QI9->QI9_CODIGO+QI9->QI9_REV

			self:oQLTTReports:aninharAliasEmPropriedade(self, "PlanoDeAcaoRelacionado", self:aPlanStruct, "QI3", @self:jData, {}, 1, cCamposSeek, cChaveSeek, cIFWhile, cLineExec, "D")

			QI9->(dbSkip())
		Enddo

	Endif

Return 

/*/{Protheus.doc} exibicaoSigilosa
Indica se o registro deve ser exibido de forma sigilosa
@author brunno.costa
@since 10/04/2024
@version 1.0
@param 01 - cMatFil, caracter, filial do usuário corrente para checagem
@param 02 - cMatCod, caracter, matricula do usuário corrente para checagem
@return lSigiloso, lógico, indica se o registro se refere a exibição sigilosa
/*/
Method exibicaoSigilosa(cMatFil as Character, cMatCod as Character) as Logical class SmartViewQNCNonConformance

	Local lSigiloso := .f. as Logical

	If (self:cAlias)->QI2_SIGILO == "1"

		If !(cMatFil+cMatCod == (self:cAlias)->QI2_FILMAT+(self:cAlias)->QI2_MAT .OR.;
		     cMatFil+cMatCod == (self:cAlias)->QI2_FILRES+(self:cAlias)->QI2_MATRES )

			lSigiloso := .T.						

		Endif
		
	Endif

Return lSigiloso

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author brunno.costa
@since 10/04/2024
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQNCNonConformance

    Local oQLTTReports := QLTTReportsManager():New() as object

	self:mountDefaulFields()
	self:mountDefaultPlanFields()

	self:aStruct     := oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)
	aadd(self:aStruct, {"ORIGINADOR"       , STR0009 + " (ORIGINADOR)"       , "string" , STR0009 + " (ORIGINADOR)"       , ""}) // "Originador"
	aadd(self:aStruct, {"DESCRICAO"        , STR0010 + " (DESCRICAO)"        , "string" , STR0010 + " (DESCRICAO)"        , ""}) // "Descrição Detalhada"
	aadd(self:aStruct, {"CLIENTE"          , STR0011 + " (CLIENTE)"          , "string" , STR0011 + " (CLIENTE)"          , ""}) // "Cliente"
	aadd(self:aStruct, {"FORNECEDOR"       , STR0012 + " (FORNECEDOR)"       , "string" , STR0012 + " (FORNECEDOR)"       , ""}) // "Fornecedor"
	aadd(self:aStruct, {"CONTATO"          , STR0013 + " (CONTATO)"          , "string" , STR0013 + " (CONTATO)"          , ""}) // "Contato"
	aadd(self:aStruct, {"STATUS"           , STR0014 + " (STATUS)"           , "string" , STR0014 + " (STATUS)"           , ""}) // "Status"
	aadd(self:aStruct, {"PRIORIDADE"       , STR0015 + " (PRIORIDADE)"       , "string" , STR0015 + " (PRIORIDADE)"       , ""}) // "Prioridade"
	aadd(self:aStruct, {"TIPO"             , STR0016 + " (TIPO)"             , "string" , STR0016 + " (TIPO)"             , ""}) // "Tipo"
	aadd(self:aStruct, {"EXIBICAO_SIGILOSA", STR0017 + " (EXIBICAO_SIGILOSA)", "boolean", STR0017 + " (EXIBICAO_SIGILOSA)", ""}) // "Exibição Sigilosa"
	aadd(self:aStruct, {"A_DISPOSICAO"     , STR0018 + " (A_DISPOSICAO)"     , "string" , STR0018 + " (A_DISPOSICAO)"     , ""}) // "Disposição Análise"
	aadd(self:aStruct, {"A_ORIGEM"         , STR0019 + " (A_ORIGEM)"         , "string" , STR0019 + " (A_ORIGEM)"         , ""}) // "Origem Análise"
	aadd(self:aStruct, {"A_CAUSA"          , STR0020 + " (A_CAUSA)"          , "string" , STR0020 + " (A_CAUSA)"          , ""}) // "Causa Análise"
	aadd(self:aStruct, {"A_EFEITO"         , STR0021 + " (A_EFEITO)"         , "string" , STR0021 + " (A_EFEITO)"         , ""}) // "Efeito Análise"
	aadd(self:aStruct, {"A_CATEGORIA"      , STR0022 + " (A_CATEGORIA)"      , "string" , STR0022 + " (A_CATEGORIA)"      , ""}) // "Categoria Análise"

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )
	
	//Transforma propriedades em dados aninhados para permitir ocultar banda de detalhes sem consumir linhas do relatório
	self:addNestedProperty("Comentarios", "Comentarios", "Comentarios",, {{"COMENTARIOS", STR0023 + " (COMENTARIOS)", "string", STR0023 + " (COMENTARIOS)"}}) //"Comentários"
	self:addNestedProperty("Disposicao" , "Disposicao" , "Disposicao" ,, {{"DISPOSICAO" , STR0024 + " (DISPOSICAO)" , "string", STR0024 + " (DISPOSICAO)" }}) //"Disposição"

	//Propriedade aninhada com campos do SX3 - Ações
	self:aPlanStruct := oQLTTReports:GetNestedStruct(self:aPlanFields)
	self:addNestedProperty("PlanoDeAcaoRelacionado", "PlanoDeAcaoRelacionado", "PlanoDeAcaoRelacionado",, self:aPlanStruct)

Return self:oSchema

/*/{Protheus.doc} mountDefaultPlanFields
Monta Array aPlanFields Default
@author brunno.costa
@since 10/04/2024
@version 1.0
/*/
Method mountDefaultPlanFields() class SmartViewQNCNonConformance
	self:aPlanFields := {}
	aadd(self:aPlanFields, "QI3_ABERTU")
	aadd(self:aPlanFields, "QI3_CODIGO")
	aadd(self:aPlanFields, "QI3_ENCPRE")
	aadd(self:aPlanFields, "QI3_ENCREA")
	aadd(self:aPlanFields, "QI3_FILMAT")
	aadd(self:aPlanFields, "QI3_MAT")
	aadd(self:aPlanFields, "QI3_REV")
	aadd(self:aPlanFields, "QI3_STATUS")
	aadd(self:aPlanFields, "QI3_TIPO")
Return 

/*/{Protheus.doc} mountDefaulFields
Monta Array aFormFields Default
@author brunno.costa
@since 10/04/2024
@version 1.0
/*/
Method mountDefaulFields() class SmartViewQNCNonConformance
	self:aFields := {}
	aadd(self:aFields, "QAA_NOME")
	aadd(self:aFields, "QI2_ANO")
	aadd(self:aFields, "QI2_CODACA")
	aadd(self:aFields, "QI2_CODCAT")
	aadd(self:aFields, "QI2_CODCAU")
	aadd(self:aFields, "QI2_CODCLI")
	aadd(self:aFields, "QI2_CODDIS")
	aadd(self:aFields, "QI2_CODEFE")
	aadd(self:aFields, "QI2_CODFOR")
	aadd(self:aFields, "QI2_CODORI")
	aadd(self:aFields, "QI2_COMEN")
	aadd(self:aFields, "QI2_CONPRE")
	aadd(self:aFields, "QI2_CONREA")
	aadd(self:aFields, "QI2_CONTAT")
	aadd(self:aFields, "QI2_DDETA")
	aadd(self:aFields, "QI2_DESCR")
	aadd(self:aFields, "QI2_DISPOS")
	aadd(self:aFields, "QI2_FILIAL")
	aadd(self:aFields, "QI2_FILMAT")
	aadd(self:aFields, "QI2_FILRES")
	aadd(self:aFields, "QI2_FNC")
	aadd(self:aFields, "QI2_LOJCLI")
	aadd(self:aFields, "QI2_LOJFOR")
	aadd(self:aFields, "QI2_MAT")
	aadd(self:aFields, "QI2_MATRES")
	aadd(self:aFields, "QI2_OCORRE")
	aadd(self:aFields, "QI2_PRIORI")
	aadd(self:aFields, "QI2_REGIST")
	aadd(self:aFields, "QI2_REV")
	aadd(self:aFields, "QI2_REVACA")
	aadd(self:aFields, "QI2_SIGILO")
	aadd(self:aFields, "QI2_STATUS")
	aadd(self:aFields, "QI2_TPFIC")
Return 

