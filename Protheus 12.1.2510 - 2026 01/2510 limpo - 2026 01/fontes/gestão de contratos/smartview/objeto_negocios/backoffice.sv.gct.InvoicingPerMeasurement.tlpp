#INCLUDE 'msobject.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.gct.invoicingpermeasurement.ch'

//-------------------------------------------------------------------------------
/*{Protheus.doc} invoicingpermeasurementTReportsBusinessObject
Classe para criação do Objeto de Negocio de gestão de contratos para SmartView
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
namespace custom.gestaodecontratos.invoicingpermeasurement.integratedprovider
using namespace totvs.protheus.backoffice.sv.gct.integratedProvider

// Annotation
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAGCT', tables='CN9,CND,CNE,SA1,SA2,SC5,SC6,SC7,SD1,SD2,SE1,SE2,SF1,SF2', name='Mediciones por Factura', country='ALL', initialRelease='12.1.2310', customTables='CN9,CND,CNE' )
class invoicingpermeasurementTReportsBusinessObject from totvs.protheus.backoffice.sv.gct.integratedProvider.GctIntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} NEW
Método de instância da classe: Define a lista de importações que serão acessados no objeto de negocios
@return object: self
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() class invoicingpermeasurementTReportsBusinessObject

	_Super:new()

	// Define a Área
	self:appendArea( STR0002 ) /*'Gestión de Contratos'*/

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 ) /*'Mediciones por Factura'*/

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 ) /*'Mediciones por Factura'*/

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CNTSV050' ) // Indica o pergunte que será utilizado
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') //'Sin Preguntas' //'¡Verifique el grupo de preguntas dado!' //'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) /*'Grupo de preguntas no encontrado!'*/
		self:lEnvironmentOK := .F.
	EndIf

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura de campos 
@return object: self:oSchema 
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema( ) as object class invoicingpermeasurementTReportsBusinessObject
	
	self:gctAliasToSchema('CNE', {'CNE_FILIAL', 'CNE_CONTRA', 'CNE_NUMMED', 'CNE_NUMERO',  'CNE_VLTOT', 'CNE_PEDIDO'})
	self:gctAliasToSchema('CN9', {'CN9_ESPCTR', 'CN9_FILCTR'})

	self:gctAddProperty('CN9_CLIFOR', STR0007, 'string') /*'Cliente/Proveedor'*/
	self:gctAddProperty('CN9_LOJA', STR0008, 'string') /*'Tienda'*/
	self:gctAddProperty('CN9_NOME', STR0009, 'string') /*'Razón Social'*/
	self:gctAddProperty('CN9_DOC', STR0010, 'string') /*'No. Factura'*/
	self:gctAddProperty('CN9_SERIE', STR0011, 'string') /*'Serie'*/
	self:gctAddProperty('CN9_TOTAL', STR0012, 'number') /*'Valor Fact'*/
	self:gctAddProperty('CN9_SALDO', STR0013, 'number') /*'Valor de Pago'*/
	self:gctAddProperty('CN9_RETCT', STR0014, 'number') /*'Valor Retención'*/
	self:gctAddProperty('CND_MOEDA', STR0015, 'number') /*'Moneda Medición'*/
	self:gctAddProperty('CND_DESMOE', STR0016, 'string') /*'Desc. Moneda Medición'*/
	self:gctAddProperty('CN9_MOEDA', STR0017, 'number') /*'Moneda Contrato'*/
	self:gctAddProperty('CN9_DESMOE', STR0018, 'string') /*'Desc. Moneda Contrato'*/
	self:gctAddProperty('FCT_MOEDA', STR0019, 'number') /*'Moneda Factura'*/
	self:gctAddProperty('FCT_DESMOE', STR0020, 'string') /*'Desc. Moneda Factura'*/

Return( self:oSchema )


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os datos do objeto de negócios
@param nPage, numérico, indica a pagina atual
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class invoicingpermeasurementTReportsBusinessObject

	// Declaracao de variaveis
	Local jParams as json
	Local jItems as json

	Local cQuery 	as character
	Local cAliasA := '1' 	as character
	Local cAliasB := '1' 	as character
	Local cAliasC := '1'	as character
	Local cDescMon	as character

	Local nX as numeric
	Local nDecs := MsDecimais( 1 ) as numeric
	Local nQtMoedas := Moedfin() as numeric
	Local nPosMon as numeric
	Local nMVPAR05 as numeric
	Local nMVPAR06 as numeric
	Local nMoedaDest as numeric

	Local aPDFields := {} as array
	Local aNomeMoed := {} as array

	Local lObfuscated as logical

	Local oExecA as object
	Local oExecB as object
	Local oExecC as object

	if !self:lEnvironmentOK
		return self:oData
	endIf

	self:loadLGPDFields()
	self:setFilter(oFilter)
	self:loadCustomFieldsToArrays()

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	nMVPAR05 := IIf( ValType( jParams['MV_PAR05'][1] ) == 'C', Val( jParams['MV_PAR05'][1] ), jParams['MV_PAR05'][1] )
	nMVPAR06 := IIf( ValType( jParams['MV_PAR06'][1] ) == 'C', Val( jParams['MV_PAR06'][1] ), jParams['MV_PAR06'][1] )

	// Lista as moedas e suas descrições
	For nX := 1 To nQtMoedas
		aAdd( aNomeMoed, { AllTrim( Str( nX ) ), SuperGetMv( 'MV_MOEDA' + AllTrim( Str( nX ) ) ) } )
	Next

	// Coleta os campos personalizados pra agregar no filtro de preenchimento dos campos
	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery += " SELECT CNE.CNE_FILIAL, CNE.CNE_CONTRA, CNE.CNE_REVISA, CNE.CNE_NUMMED, CNE.CNE_NUMERO, SUM(CNE.CNE_VLTOT) CNE_VLTOT, CNE.CNE_PEDIDO, "
	cQuery += " SD1.D1_DOC CN9_DOC, SD1.D1_SERIE CN9_SERIE, SD1.D1_FORNECE CN9_CLIFOR, SD1.D1_LOJA CN9_LOJA, F1_VALMERC CN9_TOTAL, "
	cQuery += " CN9.CN9_FILCTR, CN9.CN9_ESPCTR, CND.CND_MOEDA, CN9.CN9_MOEDA "
	cQuery += self:getCustomFieldsToSQL()
	cQuery += " FROM " + RetSQLName( 'CNE' ) + " CNE "
	cQuery += " INNER JOIN " + RetSQLName( 'CND' ) + " CND ON CND.CND_FILIAL = ? AND CND.CND_NUMMED = CNE.CNE_NUMMED AND CND.CND_CONTRA = CNE.CNE_CONTRA AND CND.CND_REVISA = CNE.CNE_REVISA AND CND.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName( 'CN9' ) + " CN9 ON CN9.CN9_FILIAL = CND.CND_FILCTR AND CN9.CN9_NUMERO = CND.CND_CONTRA AND CN9.CN9_REVISA = CND.CND_REVISA AND CN9_ESPCTR = ? AND CN9.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'CN1' ) + " CN1 ON CN1.CN1_FILIAL = ? AND CN1.CN1_CODIGO = CN9.CN9_TPCTO AND CN1.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SC7' ) + " SC7 ON SC7.C7_FILIAL  = ? AND SC7.C7_NUM = CNE.CNE_PEDIDO AND SC7.C7_ITEMED = CNE.CNE_ITEM AND SC7.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SD1' ) + " SD1 ON SD1.D1_FILIAL  = ? AND SD1.D1_PEDIDO = SC7.C7_NUM AND SD1.D1_ITEMPC = SC7.C7_ITEM AND SD1.D1_FORNECE = SC7.C7_FORNECE AND SD1.D1_LOJA = SC7.C7_LOJA AND SD1.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SF1' ) + " SF1 ON SD1.D1_FILIAL  = ? AND SF1.F1_DOC = SD1.D1_DOC AND SF1.F1_SERIE = SD1.D1_SERIE AND SF1.F1_FORNECE = SD1.D1_FORNECE AND SF1.F1_LOJA = SD1.D1_LOJA AND SF1.D_E_L_E_T_ = ?"
	cQuery += " WHERE CNE.CNE_FILIAL = ? "
	cQuery += " AND CNE.CNE_CONTRA BETWEEN ? AND ? "
	cQuery += " AND CNE.CNE_NUMMED BETWEEN ? AND ? "
	cQuery += " AND CNE.D_E_L_E_T_  = ? "
	
	cQuery += self:getFilterToSQL()

	cQuery += " GROUP BY CNE.CNE_FILIAL, CNE.CNE_CONTRA, CNE.CNE_REVISA, CNE.CNE_NUMMED, CNE.CNE_NUMERO, CND.CND_MOEDA, CN9.CN9_MOEDA, CN1.CN1_ESPCTR, CNE.CNE_PEDIDO, SD1.D1_DOC, SD1.D1_SERIE, SD1.D1_FORNECE, SD1.D1_LOJA, SF1.F1_VALMERC, CN9.CN9_FILCTR, CN9.CN9_ESPCTR "
	cQuery += self:getCustomFieldsToSQL()

	cQuery += " UNION "

	cQuery += " SELECT CNE.CNE_FILIAL, CNE.CNE_CONTRA, CNE.CNE_REVISA, CNE.CNE_NUMMED, CNE.CNE_NUMERO, SUM(CNE.CNE_VLTOT) CNE_VLTOT, SC5.C5_NUM CNE_PEDIDO,"
	cQuery += " SD2.D2_DOC CN9_DOC, SD2.D2_SERIE CN9_SERIE, SD2.D2_CLIENTE CN9_CLIFOR, SD2.D2_LOJA CN9_LOJA, SUM(SD2.D2_TOTAL) CN9_TOTAL,  "
	cQuery += " CN9.CN9_FILCTR, CN9.CN9_ESPCTR, CND.CND_MOEDA, CN9.CN9_MOEDA"
	cQuery += self:getCustomFieldsToSQL()
	cQuery += " FROM " + RetSQLName( 'CNE' ) + " CNE "
	cQuery += " INNER JOIN " + RetSQLName( 'CND' ) + " CND ON CND.CND_FILIAL = ? AND CND.CND_NUMMED = CNE.CNE_NUMMED AND CND.CND_CONTRA = CNE.CNE_CONTRA AND CND.CND_REVISA = CNE.CNE_REVISA AND CND.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'CN9' ) + " CN9 ON CN9.CN9_FILIAL = CND.CND_FILCTR AND CN9.CN9_NUMERO = CND.CND_CONTRA AND CN9.CN9_REVISA = CND.CND_REVISA AND CN9_ESPCTR = ? AND CN9.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'CN1' ) + " CN1 ON CN1.CN1_FILIAL = ? AND CN1.CN1_CODIGO = CN9.CN9_TPCTO AND CN1.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SC5' ) + " SC5 ON SC5.C5_FILIAL  = ? AND SC5.C5_NUM = CNE.CNE_PEDIDO AND SC5.D_E_L_E_T_  = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SC6' ) + " SC6 ON SC6.C6_FILIAL  = ? AND SC6.C6_NUM = SC5.C5_NUM AND SC6.C6_ITEMED = CNE.CNE_ITEM AND SC6.D_E_L_E_T_  = ?"
	cQuery += " INNER JOIN " + RetSQLName( 'SD2' ) + " SD2 ON SD2.D2_FILIAL  = ? AND SD2.D2_PEDIDO = SC5.C5_NUM AND SD2.D2_ITEMPV = SC6.C6_ITEM AND SD2.D2_CLIENTE = SC5.C5_CLIENT AND SD2.D2_LOJA = SC5.C5_LOJACLI AND SD2.D_E_L_E_T_  = ?"
	cQuery += " WHERE CNE.CNE_FILIAL = ? "
	cQuery += " AND CNE.CNE_CONTRA BETWEEN ? AND ? "
	cQuery += " AND CNE.CNE_NUMMED BETWEEN ? AND ? "
	cQuery += " AND CNE.D_E_L_E_T_  = ? "
	
	cQuery += self:getFilterToSQL()

	cQuery += " GROUP BY CNE.CNE_FILIAL, CNE.CNE_CONTRA, CNE.CNE_REVISA, CNE.CNE_NUMMED, CNE.CNE_NUMERO, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_CLIENTE, SD2.D2_LOJA, CND.CND_MOEDA, CN9.CN9_FILCTR, CN9.CN9_ESPCTR, CN9.CN9_MOEDA, SC5.C5_NUM "
	cQuery += self:getCustomFieldsToSQL()
	
	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	oExecA:SetString( 01, xFilial('CND') )   		// CND_FILIAL
	oExecA:SetString( 02, ' ' )              		// CND.D_E_L_E_T_	
	oExecA:SetString( 03, '1' )              		// CN9_ESPCTR
	oExecA:SetString( 04, ' ' )				 		// CN9.D_E_L_E_T_	
	oExecA:SetString( 05, xFilial('CN1') )   		// CN1_FILIAL
	oExecA:SetString( 06, ' ' )				 		// CN1.D_E_L_E_T_	
	oExecA:SetString( 07, xFilial('CN1') )   		// C7_FILIAL
	oExecA:SetString( 08, ' ' )				 		// SC7.D_E_L_E_T_	
	oExecA:SetString( 09, xFilial('SD1') )   		// D1_FILIAL
	oExecA:SetString( 10, ' ' )				 		// SD1.D_E_L_E_T_
	oExecA:SetString( 11, xFilial('SF1') )   		// F1_FILIAL
	oExecA:SetString( 12, ' ' )				 		// SF1.D_E_L_E_T_
	oExecA:SetString( 13, xFilial( 'CNE' ) ) 		// CNE_FILIAL
	oExecA:SetString( 14, jParams['MV_PAR01'][1] )	// CNE_CONTRA
	oExecA:SetString( 15, jParams['MV_PAR02'][1] )  // CNE_CONTRA
	oExecA:SetString( 16, jParams['MV_PAR03'][1] )  // CNE_NUMMED
	oExecA:SetString( 17, jParams['MV_PAR04'][1] )  // CNE_NUMMED
	oExecA:SetString( 18, ' ' )						// CNE.D_E_L_E_T_
	oExecA:SetString( 19, xFilial('CND') )   		// CND_FILIAL
	oExecA:SetString( 20, ' ' )				 		// CND.D_E_L_E_T_
	oExecA:SetString( 21, '2' )				 		// CN9_ESPCTR
	oExecA:SetString( 22, ' ' )						// CN9.D_E_L_E_T_	
	oExecA:SetString( 23, xFilial('CN1') )   		// CN1_FILIAL
	oExecA:SetString( 24, ' ' )						// CN1.D_E_L_E_T_
	oExecA:SetString( 25, xFilial('SC5') )   		// C5_FILIAL
	oExecA:SetString( 26, ' ' )						// SC5.D_E_L_E_T_
	oExecA:SetString( 27, xFilial('SC6') )   		// C6_FILIAL
	oExecA:SetString( 28, ' ' )				 		// SC6.D_E_L_E_T_
	oExecA:SetString( 29, xFilial('SD2') )   		// D2_FILIAL
	oExecA:SetString( 30, ' ' )						// SD2.D_E_L_E_T_
	oExecA:SetString( 31, xFilial( 'CNE' ) )		// CNE_FILIAL
	oExecA:SetString( 32, jParams['MV_PAR01'][1] )	// CNE_CONTRA
	oExecA:SetString( 33, jParams['MV_PAR02'][1] )	// CNE_CONTRA
	oExecA:SetString( 34, jParams['MV_PAR03'][1] )	// CNE_NUMMED
	oExecA:SetString( 35, jParams['MV_PAR04'][1] )	// CNE_NUMMED
	oExecA:SetString( 36, ' ' )						// CNE.D_E_L_E_T_ 
	
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		If ( ( cAliasA )->CN9_ESPCTR == '1' )
			cQuery := "SELECT SUM(E2_VALOR) CN9_VALOR, SUM(E2_SALDO) CN9_SALDO, SUM(E2_RETCNTR) CN9_RETCNTR, E2_MOEDA "
			cQuery += " FROM " + RetSQLName( 'SE2' ) + " SE2 "
			cQuery += " WHERE E2_FILIAL = ?"
			cQuery += " AND E2_NUM = ?
			cQuery += " AND E2_PREFIXO = ?
			cQuery += " AND E2_FORNECE = ?
			cQuery += " AND E2_LOJA = ?
			cQuery += " AND SE2.D_E_L_E_T_  = ? "
			cQuery += " GROUP BY SE2.E2_PREFIXO, SE2.E2_NUM, SE2.E2_MOEDA "
			oExecB := FwExecStatement():New( cQuery )
			oExecB:SetString( 01, ( cAliasA )->CNE_FILIAL )
			oExecB:SetString( 02, ( cAliasA )->CN9_DOC )
			oExecB:SetString( 03, ( cAliasA )->CN9_SERIE )
			oExecB:SetString( 04, ( cAliasA )->CN9_CLIFOR )
			oExecB:SetString( 05, ( cAliasA )->CN9_LOJA )
			oExecB:SetString( 06, ' ' )
			cAliasB := oExecB:OpenAlias( )
			DbSelectArea( cAliasB )
		ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
			cQuery := "SELECT SUM(E1_VALOR) CN9_VALOR, SUM(E1_SALDO) CN9_SALDO, SUM(E1_RETCNTR) CN9_RETCNTR, E1_MOEDA "
			cQuery += " FROM " + RetSQLName( 'SE1' ) + " SE1 "
			cQuery += " WHERE E1_FILIAL = ?"
			cQuery += " AND E1_NUM = ?"
			cQuery += " AND E1_PREFIXO = ?"
			cQuery += " AND E1_CLIENTE = ?"
			cQuery += " AND E1_LOJA = ?"
			cQuery += " AND SE1.D_E_L_E_T_  = ? "
			cQuery += " GROUP BY SE1.E1_PREFIXO, SE1.E1_NUM, SE1.E1_MOEDA "
			oExecB := FwExecStatement():New( cQuery )
			oExecB:SetString( 01, ( cAliasA )->CNE_FILIAL )
			oExecB:SetString( 02, ( cAliasA )->CN9_DOC )
			oExecB:SetString( 03, ( cAliasA )->CN9_SERIE )
			oExecB:SetString( 04, ( cAliasA )->CN9_CLIFOR )
			oExecB:SetString( 05, ( cAliasA )->CN9_LOJA )
			oExecB:SetString( 06, ' ' )
			cAliasB := oExecB:OpenAlias( )
			DbSelectArea( cAliasB )
		EndIf
		jItems := JsonObject():new( )

		// Verifica qual a moeda de destino para conversão de valores
		If ( nMVPAR05 == 1 )
			nMoedaDest := ( cAliasA )->CN9_MOEDA
			nDecs := MsDecimais( nMoedaDest )
		ElseIf ( nMVPAR05 == 2 )
			nMoedaDest := ( cAliasA )->CND_MOEDA
			nDecs := MsDecimais( nMoedaDest )
		ElseIf ( nMVPAR05 == 3 )
			nMoedaDest := IIf( ( ( cAliasA )->CN9_ESPCTR == '1' ), ( cAliasB )->E2_MOEDA, ( cAliasB )->E1_MOEDA )
			nDecs := MsDecimais( nMoedaDest )
		ElseIf ( nMVPAR05 == 4 )
			nMoedaDest := nMVPAR06
			nDecs := MsDecimais( nMoedaDest )
			dDataBase := FwDateTimeToLocal( jParams['MV_PAR07'][1] )[1]
		EndIf

		// Alimenta o objeto de dados da classe para retornar ao SmartView
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_NOME' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					cQuery := "SELECT SA2.A2_NOME "
					cQuery += " FROM " + RetSQLName( 'SA2' ) + " SA2 "
					cQuery += " WHERE SA2.A2_FILIAL = ? "
					cQuery += " AND SA2.A2_COD = ? "
					cQuery += " AND SA2.A2_LOJA = ? "
					cQuery += " AND SA2.D_E_L_E_T_ = ? "
					oExecC := FwExecStatement():New( cQuery )
					oExecC:SetString( 01, xFilial( 'SA2' ) )
					oExecC:SetString( 02, ( cAliasA )->CN9_CLIFOR )
					oExecC:SetString( 03, ( cAliasA )->CN9_LOJA )
					oExecC:SetString( 04, ' ' )
					cAliasC := oExecC:OpenAlias( )
					DbSelectArea( cAliasC )
					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A2_NOME
					EndIf
					( cAliasC )->( DbCloseArea( ) )
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					cQuery := "SELECT SA1.A1_NOME "
					cQuery += " FROM " + RetSQLName( 'SA1' ) + " SA1 "
					cQuery += " WHERE SA1.A1_FILIAL = ?"
					cQuery += " AND SA1.A1_COD = ?"
					cQuery += " AND SA1.A1_LOJA = ?"
					cQuery += " AND SA1.D_E_L_E_T_ = ?"
					oExecC := FwExecStatement():New( cQuery )
					oExecC:SetString( 01, xFilial( 'SA1' ) )
					oExecC:SetString( 02, ( cAliasA )->CN9_CLIFOR )
					oExecC:SetString( 03, ( cAliasA )->CN9_LOJA )
					oExecC:SetString( 04, ' ' )
					cAliasC := oExecC:OpenAlias( )
					DbSelectArea( cAliasC )
					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A1_NOME
					EndIf
					( cAliasC )->( DbCloseArea( ) )
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CN9_DESMOE' )
				// Moneda
				nPosMon := aScan( aNomeMoed, { | x, y | AllTrim( x[1] ) == StrZero( ( cAliasA )->CN9_MOEDA, 1 ) } )
				cDescMon := IIf( ( nPosMon > 0 ), aNomeMoed[nPosMon][2], 'Moeda Fora de Uso' )

				jItems[self:aStruct[nX][1]] := cDescMon
			ElseIf ( self:aStruct[nx][5] == 'CND_DESMOE' )
				// Moneda
				nPosMon := aScan( aNomeMoed, { | x, y | AllTrim( x[1] ) == StrZero( ( cAliasA )->CND_MOEDA, 1 ) } )
				cDescMon := IIf( ( nPosMon > 0 ), aNomeMoed[nPosMon][2], 'Moeda Fora de Uso' )

				jItems[self:aStruct[nX][1]] := cDescMon
			ElseIf ( self:aStruct[nx][5] == 'FCT_DESMOE' )
				// Moneda
				nPosMon := aScan( aNomeMoed, { | x, y | AllTrim( x[1] ) == StrZero( nMoedaDest, 1 ) } )
				cDescMon := IIf( ( nPosMon > 0 ), aNomeMoed[nPosMon][2], 'Moeda Fora de Uso' )

				jItems[self:aStruct[nX][1]] := cDescMon
			ElseIf ( self:aStruct[nx][5] == 'FCT_MOEDA' )
				jItems[self:aStruct[nX][1]] := nMoedaDest
			ElseIf ( self:aStruct[nx][5] == 'CN9_ESPCTR' )
				jItems[self:aStruct[nX][1]] := x3Combo(self:aStruct[nx][5], ( cAliasA )->&( self:aStruct[nX][5] ))
			ElseIf ( self:aStruct[nx][5] == 'CNE_VLTOT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->CNE_VLTOT, ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNE_VLTOT', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_TOTAL' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->CN9_TOTAL, ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNE_VLTOT', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_SALDO' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( ( cAliasB )->CN9_VALOR - ( cAliasB )->CN9_SALDO ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNE_VLTOT', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_RETCT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasB )->CN9_RETCNTR, ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNE_VLTOT', 'X3_TAMANHO' ) )
			Else
				jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( jItems )

		( cAliasA )->( DbSkip( ) )

	End

	// fecha tabela e objeto
	( cAliasA )->( DbCloseArea( ) )
	If oExecA <> Nil
		oExecA:Destroy( )
		oExecA := Nil
	endif
	If oExecB <> Nil
		oExecB:Destroy( )
		oExecB := Nil
	endif
	If oExecC <> Nil
		oExecC:Destroy( )
		oExecC := Nil
	endif
Return( self:oData )
