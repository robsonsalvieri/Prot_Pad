#INCLUDE 'msobject.ch'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.gct.ContractMeasurements.ch'

//-------------------------------------------------------------------------------
/*{Protheus.doc} contractmeasurementsTReportsBusinessObject
Classe para criação do Objeto de Negocio de gestão de contratos para SmartView
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
namespace custom.gestaodecontratos.contractmeasurements.integratedprovider
using namespace totvs.protheus.backoffice.sv.gct.integratedProvider

// Annotation
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAGCT', tables='CN9,CND,CNE,CXN,CNQ,CNR,CNT', name='Mediciones de Contratos', country='ALL', initialRelease='12.1.2310', customTables='CN9,CND,CNE' )
class contractmeasurementsTReportsBusinessObject from totvs.protheus.backoffice.sv.gct.integratedProvider.GctIntegratedProvider

	public Method new() as object
	public Method getSchema() as object
	public Method getData() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} NEW
Método de instância da classe: Define a lista de importações que serão acessados no objeto de negocios
@return object: self
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() class contractmeasurementsTReportsBusinessObject

	_Super:new()

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 ) /*'Mediciones de Contratos'*/

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 ) /*'Mediciones de Contratos'*/

	// Define a Área
	self:appendArea( STR0002 ) /*'Gestão de Contratos'*/

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CNTSV031' ) // Indica o pergunte que será utilizado
		IIf( !self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') //'Sin Preguntas' //'¡Verifique el grupo de preguntas dado!' //'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) /*'Grupo de preguntas no encontrado!'*/
		self:lEnvironmentOK := .F.
	EndIf

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura de campos 
@return object: self:oSchema 
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema( ) as object class contractmeasurementsTReportsBusinessObject

	self:gctAliasToSchema("CND", {'CND_FILIAL', 'CND_CONTRA', 'CND_REVISA', 'CND_NUMMED', 'CND_COMPET', 'CND_DTVENC', 'CND_DTFIM', 'CND_VLMEAC', 'CND_VLSALD', 'CND_VLTOT' })
	self:gctAliasToSchema("CXN", {'CXN_NUMPLA', 'CXN_TIPPLA', 'CXN_VLSALD', 'CXN_VLPREV', 'CXN_VLMULT', 'CXN_VLLIQD', 'CXN_VLBONI', 'CXN_VLDESC', 'CXN_VLTOT' })
	self:gctAliasToSchema("CN9", {'CN9_VLATU', 'CN9_DTULST', 'CN9_SITUAC', 'CN9_ESPCTR' })
	self:gctAliasToSchema("CNE", {'CNE_ITEM', 'CNE_PRODUT', 'CNE_QTDSOL', 'CNE_QTAMED', 'CNE_QUANT', 'CNE_PERC', 'CNE_VLUNIT', 'CNE_VLTOT' })
	self:gctAliasToSchema("SB1", {'B1_DESC', 'B1_UM' })
	
	self:gctAddProperty('CN9_CLIFOR', STR0007, 'string', STR0007 ) /*'Cliente/Fornecedor'*/
	self:gctAddProperty('CN9_LOJA', STR0008, 'string', STR0008 ) /*'Loja'*/
	self:gctAddProperty('CN9_NOME', STR0009, 'string', STR0009 ) /*'Razón Social'*/
	self:gctAddProperty('CNE_QACU', STR0010, 'number', STR0010 ) /*'Qtd. Acumulada Ant.'*/
	self:gctAddProperty('CNE_QACUT', STR0011, 'number', STR0011 ) /*'Qtd. Acumulada Tot.'*/
	self:gctAddProperty('CNE_VACU', STR0012, 'number', STR0012 ) /*'Valor Acumulado Ant.'*/
	self:gctAddProperty('CNE_VACUT', STR0013, 'number', STR0013 ) /*'Valor Acumulado Tot.'*/
	self:gctAddProperty('CNR_VALOR', STR0014, 'number', STR0014 ) /*'Vl. Multas/Bonif. Total'*/
	self:gctAddProperty('CNQ_VALOR', STR0015, 'number', STR0015 ) /*'Vl. Descontos Total'*/
	self:gctAddProperty('CNT_VLRET', STR0016, 'number', STR0016 ) /*'Vl. Retenido Total'*/
	self:gctAddProperty('CNT_VLBX', STR0017, 'number', STR0017 ) /*'Vl. Baja Total'*/

Return( self:oSchema )


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os datos do objeto de negócios
@param nPage, numérico, indica a pagina atual
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author Leonardo Pereira
@since 27/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class contractmeasurementsTReportsBusinessObject

	// Declaracao de variaveis
	Local jParams as json
	Local jItems as json

	Local cQuery 	as character
	Local cAliasA 	as character
	Local cAliasB 	as character

	Local nX as numeric
	Local nMoedaDest as numeric
	Local nMVPAR07 as numeric
	Local nMVPAR08 as numeric

	Local aPDFields := {} as array

	Local lObfuscated as logical

	Local oExecA as object
	Local oExecB as object
	Local oExecC as object

	Local cDescSit as character
	Local aSituac := {} as array

	Local cDescEsp as character
	Local aEspCTR := {} as array

	Local nSeqPar as numeric

	if !self:lEnvironmentOK
		return self:oData
	endIf

	self:loadLGPDFields()
	self:setFilter(oFilter)
	self:loadCustomFieldsToArrays()

	aSituac := RetSx3Box( Posicione("SX3", 2, "CN9_SITUAC", "X3CBox()" ),,, 1 )
	aEspCTR := RetSx3Box( Posicione("SX3", 2, "CN9_ESPCTR", "X3CBox()" ),,, 1 )

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	nMVPAR07 := IIf( ValType( jParams['MV_PAR07'][1] ) == 'C', Val( jParams['MV_PAR07'][1] ), jParams['MV_PAR07'][1] )
	nMVPAR08 := IIf( ValType( jParams['MV_PAR08'][1] ) == 'C', Val( jParams['MV_PAR08'][1] ), jParams['MV_PAR08'][1] )

	cQuery := "SELECT CN9.CN9_FILCTR, CN9.CN9_ESPCTR, CN9.CN9_VLATU, CN9.CN9_DTULST, CN9.CN9_MOEDA, CN9_SITUAC, " 
	cQuery += " CND.CND_FILIAL, CND.CND_CONTRA, CND.CND_REVISA, CND.CND_NUMMED, CND.CND_NUMERO, CND.CND_COMPET, CND.CND_DTVENC, CND.CND_DTFIM, CND.CND_VLMEAC, CND.CND_VLSALD, CND.CND_VLTOT, CNA.CNA_FORNEC, CNA.CNA_LJFORN, CNA.CNA_CLIENT, CNA.CNA_LOJACL, CND.CND_MOEDA, "
	cQuery += " CNE.CNE_ITEM, CNE.CNE_PRODUT, CNE.CNE_QTDSOL, CNE.CNE_QTAMED, CNE.CNE_QUANT, CNE.CNE_PERC, CNE.CNE_VLUNIT, "
	cQuery +=  "CNE.CNE_VLTOT, COALESCE(CXN.CXN_FILIAL,'') AS CXN_FILIAL, COALESCE(CXN.CXN_NUMPLA,'') AS CXN_NUMPLA, COALESCE(CXN.CXN_TIPPLA,'') AS CXN_TIPPLA, COALESCE(CXN.CXN_VLSALD,0) AS CXN_VLSALD, COALESCE(CXN.CXN_VLPREV,0) AS CXN_VLPREV, COALESCE(CXN.CXN_VLMULT,0) AS CXN_VLMULT, COALESCE(CXN.CXN_VLLIQD,0) AS CXN_VLLIQD, COALESCE(CXN.CXN_VLBONI,0) AS CXN_VLBONI, COALESCE(CXN.CXN_VLDESC,0) AS CXN_VLDESC, COALESCE(CXN.CXN_VLTOT,0) AS CXN_VLTOT, "
	cQuery += " SB1.B1_DESC, SB1.B1_UM "
	cQuery += self:getCustomFieldsToSQL()
	cQuery += " FROM " + RetSQLName( "CN9" ) + " CN9"
	cQuery += " INNER JOIN " + RetSQLName( "CNA" ) + " CNA ON CNA.CNA_FILIAL = ? AND CNA.CNA_CONTRA = CN9.CN9_NUMERO AND CNA.CNA_REVISA = CN9.CN9_REVISA AND CNA.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName( "CND" ) + " CND ON CND.CND_FILIAL = ? AND CND.CND_CONTRA = CN9.CN9_NUMERO AND CND.CND_REVISA = CN9.CN9_REVISA AND CND.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSQLName( "CXN" ) + " CXN ON CXN.CXN_FILIAL = ? AND CXN.CXN_CONTRA = CND.CND_CONTRA AND CXN.CXN_REVISA = CND.CND_REVISA AND CXN.CXN_NUMMED = CND.CND_NUMMED AND CXN.CXN_NUMPLA = CNA.CNA_NUMERO AND CXN.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName( "CNE" ) + " CNE ON CNE.CNE_FILIAL = ? AND CNE.CNE_CONTRA = CND.CND_CONTRA AND CNE.CNE_REVISA = CND.CND_REVISA AND CNE.CNE_NUMERO =CNA.CNA_NUMERO AND CNE.CNE_NUMMED = CND.CND_NUMMED AND CNE.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName( "SB1" ) + " SB1 ON SB1.B1_FILIAL = ? AND SB1.B1_COD = CNE.CNE_PRODUT AND SB1.D_E_L_E_T_ = ? "
	cQuery += " WHERE CN9.CN9_FILIAL = ? "
	cQuery += " AND CN9.CN9_NUMERO BETWEEN ? AND ? "
	cQuery += " AND CN9.CN9_REVISA BETWEEN ? AND ? "
	cQuery += " AND CN9.CN9_DTINIC >= ? "
	cQuery += " AND CN9.CN9_DTFIM <= ? "
	cQuery += " AND CN9.D_E_L_E_T_ = ? "	
	
	cQuery += self:getFilterToSQL()
	
	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New(cQuery)

	oExecA:SetString( nSeqPar += 1, xFilial("CNA") )	// CNA_FILIAL 
	oExecA:SetString( nSeqPar += 1, ' ' )				// CNA.D_E_L_E_T_
	oExecA:SetString( nSeqPar += 1, xFilial("CND") )	// CND_FILIAL
	oExecA:SetString( nSeqPar += 1, ' ' )				// CND.D_E_L_E_T_
	
	oExecA:SetString( nSeqPar += 1, xFilial("CXN") )//CXN_FILIAL
	oExecA:SetString( nSeqPar += 1, ' ' )			// CXN.D_E_L_E_T_
	
	oExecA:SetString( nSeqPar += 1, xFilial("CNE") )	// CNE_FILIAL
	oExecA:SetString( nSeqPar += 1, ' ' )				// CNE.D_E_L_E_T_
	oExecA:SetString( nSeqPar += 1, xFilial("SB1") )	// B1_FILIAL
	oExecA:SetString( nSeqPar += 1, ' ' )				// SB1.D_E_L_E_T_
	oExecA:SetString( nSeqPar += 1, xFilial("CN9") )	// CN9_FILIAL
	oExecA:SetString( nSeqPar += 1, jParams['MV_PAR01'][1] )	// CN9_NUMERO
	oExecA:SetString( nSeqPar += 1, jParams['MV_PAR02'][1] )	// CN9_NUMERO
	oExecA:SetString( nSeqPar += 1, jParams['MV_PAR03'][1] )	// CN9_REVISA
	oExecA:SetString( nSeqPar += 1, jParams['MV_PAR04'][1] )	// CN9_REVISA
	oExecA:SetString( nSeqPar += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )	// CN9_DTINIC
	oExecA:SetString( nSeqPar += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )	// CN9_DTFIM
	oExecA:SetString( nSeqPar += 1, ' ' )	// CN9.D_E_L_E_T_	
	
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		jItems := JsonObject():new( )

		If ( nMVPAR07 == 1 )
			nMoedaDest := ( cAliasA )->CN9_MOEDA
		ElseIf ( nMVPAR07 == 2 )
			nMoedaDest := ( cAliasA )->CND_MOEDA
		ElseIf ( nMVPAR07 == 3 )
			nMoedaDest := nMVPAR08
			dDataBase := StoD( DtoS( FwDateTimeToLocal( jParams['MV_PAR09'][1] )[1] ) )
		EndIf

		// Alimenta o objeto de dados da classe para retornar ao SmartView
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_CLIFOR' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_FORNEC
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_CLIENT
				EndIf
			ElseIf( self:aStruct[nx][5] == 'CN9_LOJA' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LJFORN
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					jItems[self:aStruct[nX][1]] := ( cAliasA )->CNA_LOJACL
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CN9_NOME' )
				If ( ( cAliasA )->CN9_ESPCTR == '1' )
					cQuery := "SELECT SA2.A2_NOME "
					cQuery += "FROM " + RetSQLName( 'SA2' ) + " SA2 "
					cQuery += "WHERE SA2.A2_FILIAL = ? "
					cQuery += "AND SA2.A2_COD = ? "
					cQuery += "AND SA2.A2_LOJA = ? "
					cQuery += "AND SA2.D_E_L_E_T_ = ? "

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					oExecC := FwExecStatement():New()

					// Define a consulta e os parâmetros
					oExecC:SetQuery( cQuery )
					oExecC:SetString( 01, xFilial( 'SA2' ) )
					oExecC:SetString( 02, ( cAliasA )->CNA_FORNEC )
					oExecC:SetString( 03, ( cAliasA )->CNA_LJFORN )
					oExecC:SetString( 04, ' ' )

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					cQuery := oExecC:getFixQuery( )
					cAliasC := oExecC:OpenAlias( )

					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A2_NOME
					EndIf

					( cAliasC )->( DbCloseArea( ) )
				ElseIf ( ( cAliasA )->CN9_ESPCTR == '2' )
					cQuery := "SELECT SA1.A1_NOME "
					cQuery += "FROM " + RetSQLName( 'SA1' ) + " SA1 "
					cQuery += "WHERE SA1.A1_FILIAL = ? "
					cQuery += "AND SA1.A1_COD = ? "
					cQuery += "AND SA1.A1_LOJA = ? "
					cQuery += "AND SA1.D_E_L_E_T_ = ? "

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					oExecC := FwExecStatement():New()

					// Define a consulta e os parâmetros
					oExecC:SetQuery( cQuery )
					oExecC:SetString( 01, xFilial( 'SA1' ) )
					oExecC:SetString( 02, ( cAliasA )->CNA_CLIENT )
					oExecC:SetString( 03, ( cAliasA )->CNA_LOJACL )
					oExecC:SetString( 04, ' ' )

					// Executa a QUERY e cria uma tabela temporaria com os dados retornados
					cQuery := oExecC:getFixQuery( )
					cAliasC := oExecC:OpenAlias( )

					If !( cAliasC )->( EOF() )
						jItems[self:aStruct[nX][1]] := ( cAliasC )->A1_NOME
					EndIf

					( cAliasC )->( DbCloseArea( ) )
				EndIf
			ElseIf ( self:aStruct[nx][5] == 'CNE_QACU' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( ( cAliasA )->CNE_QTDSOL - ( cAliasA )->CNE_QTAMED ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CNE_QACUT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( ( cAliasA )->CNE_QTDSOL - ( cAliasA )->CNE_QTAMED + ( cAliasA )->CNE_QUANT ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CNE_VACU' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( ( ( cAliasA )->CNE_QTDSOL - ( cAliasA )->CNE_QTAMED ) * ( cAliasA )->CNE_VLUNIT ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CNE_VACUT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( ( ( cAliasA )->CNE_QTDSOL - ( cAliasA )->CNE_QTAMED + ( cAliasA )->CNE_QUANT ) * ( cAliasA )->CNE_VLUNIT ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_VLATU' ) .Or. ( self:aStruct[nx][5] == 'CND_VLSALD' ) .Or. ( self:aStruct[nx][5] == 'CND_VLTOT' ) .Or. ( self:aStruct[nx][5] == 'CNE_VLTOT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CNE_VLUNIT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CXN_VLBONI' ) .Or. ( self:aStruct[nx][5] == 'CXN_VLDESC' ) .Or. ( self:aStruct[nx][5] == 'CXN_VLLIQD' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CXN_VLMULT' ) .Or. ( self:aStruct[nx][5] == 'CXN_VLPREV' ) .Or. ( self:aStruct[nx][5] == 'CXN_VLSALD' ) .Or. ( self:aStruct[nx][5] == 'CXN_VLTOT' )
				jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasA )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
			ElseIf ( self:aStruct[nx][5] == 'CN9_ESPCTR' )
				cDescEsp := AllTrim( aEspCTR[Ascan( aEspCTR, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescEsp
			ElseIf ( self:aStruct[nx][5] == 'CN9_SITUAC' )
				cDescSit := AllTrim( aSituac[Ascan( aSituac, { |aBox| substr(aBox[1],1,At("=",aBox[1])-1) = AllTrim(( cAliasA )->&( self:aStruct[nX][5] ))} )][3] )
				jItems[self:aStruct[nX][1]] := cDescSit
			ElseIf ( self:aStruct[nx][5] == 'CNR_VALOR' )
				// Realiza a montagem da QUERY que será enviada para o banco de dados [ Multas e Bonificações ]
				cQuery := 'SELECT SUM(CNR.CNR_VALOR) AS CNR_VALOR '
				cQuery += 'FROM ' + RetSQLName( 'CNR' ) + ' CNR '
				cQuery += "WHERE CNR.CNR_FILIAL = ? "
				cQuery += "AND CNR.CNR_NUMMED = ? "
				cQuery += "AND CNR.CNR_CONTRA = ? "
				cQuery += "AND CNR.CNR_CODPLA = ? "
				cQuery += "AND CNR.D_E_L_E_T_ = ? "

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				oExecB := FwExecStatement():New()

				// Define a consulta e os parâmetros
				oExecB:SetQuery( cQuery )
				oExecB:SetString( 01, xFilial("CNR") )
				oExecB:SetString( 02, ( cAliasA )->CND_NUMMED )
				oExecB:SetString( 03, ( cAliasA )->CND_CONTRA )
				oExecB:SetString( 04, ( cAliasA )->CND_NUMERO )
				oExecB:SetString( 05, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cQuery := oExecB:getFixQuery( )
				cAliasB := oExecB:OpenAlias( )
				jItems[self:aStruct[nX][1]] := 0

				If !( cAliasB )->( EOF( ) )
					jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasB )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
				EndIf

				( cAliasB )->( DbCloseArea( ) )
			ElseIf ( self:aStruct[nx][5] == 'CNQ_VALOR' )
				// Realiza a montagem da QUERY que será enviada para o banco de dados [ Descontos ]
				cQuery := 'SELECT SUM(CNQ.CNQ_VALOR) AS CNQ_VALOR '
				cQuery += 'FROM ' + RetSQLName( 'CNQ' ) + ' CNQ '
				cQuery += "WHERE CNQ.CNQ_FILIAL = ? "
				cQuery += "AND CNQ.CNQ_NUMMED = ? "
				cQuery += "AND CNQ.CNQ_CONTRA = ? "
				cQuery += "AND CNQ.CNQ_NUMPLA = ? "
				cQuery += "AND CNQ.D_E_L_E_T_ = ? "

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				oExecB := FwExecStatement():New()

				// Define a consulta e os parâmetros
				oExecB:SetQuery( cQuery )
				oExecB:SetString( 01, xFilial("CNQ") )
				oExecB:SetString( 02, ( cAliasA )->CND_NUMMED )
				oExecB:SetString( 03, ( cAliasA )->CND_CONTRA )
				oExecB:SetString( 04, ( cAliasA )->CND_NUMERO )
				oExecB:SetString( 05, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cQuery := oExecB:getFixQuery( )
				cAliasB := oExecB:OpenAlias( )
				jItems[self:aStruct[nX][1]] := 0

				If !( cAliasB )->( EOF( ) )
					jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasB )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
				EndIf

				( cAliasB )->( DbCloseArea( ) )
			ElseIf ( self:aStruct[nx][5] == 'CNT_VLRET' ) .Or. ( self:aStruct[nx][5] == 'CNT_VLBX' )
				// Realiza a montagem da QUERY que será enviada para o banco de dados
				cQuery := 'SELECT SUM(CNT_VLRET) AS CNT_VLRET, SUM(CNT_VLBX) AS CNT_VLBX '
				cQuery += 'FROM ' + RetSQLName( 'CNT' ) + ' CNT '
				cQuery += "WHERE CNT.CNT_FILIAL = ? "
				cQuery += "AND CNT.CNT_CONTRA = ? "
				cQuery += "AND CNT.CNT_NUMMED = ? "
				cQuery += "AND CNT.CNT_NUMPLA = ? "
				cQuery += "AND CNT.D_E_L_E_T_ = ? "

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				oExecB := FwExecStatement():New()

				// Define a consulta e os parâmetros
				oExecB:SetQuery( cQuery )
				oExecB:SetString( 01, xFilial("CNT")  )
				oExecB:SetString( 02, ( cAliasA )->CND_CONTRA  )
				oExecB:SetString( 03, ( cAliasA )->CND_NUMMED  )
				oExecB:SetString( 04, ( cAliasA )->CND_NUMERO  )
				oExecB:SetString( 05, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cQuery := oExecB:getFixQuery( )
				cAliasB := oExecB:OpenAlias( )

				jItems[self:aStruct[nX][1]] := 0

				DbSelectArea( cAliasB )
				If !( cAliasB )->( EOF( ) )
					jItems[self:aStruct[nX][1]] := Round( xMoeda( ( cAliasB )->&( self:aStruct[nX][5] ), ( cAliasA )->CND_MOEDA, nMoedaDest, dDataBase, 6 ), GetSX3Cache( 'CNR_VALOR', 'X3_TAMANHO' ) )
				EndIf
				( cAliasB )->( DbCloseArea( ) )
			Else
				jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( jItems )

		If oExecB <> Nil
			oExecB:Destroy()
			oExecB := Nil
		EndIf

		If oExecC <> Nil
			oExecC:Destroy()
			oExecC := Nil
		EndIf

		( cAliasA )->( DbSkip( ) )

	End

	// Se não for o último registro indica que terá próxima página
	( cAliasA )->( DbCloseArea( ) )
	
	If oExecA <> Nil
		oExecA:Destroy()
		oExecA := Nil
	EndIf

Return( self:oData )
