#INCLUDE "CNTA130.ch"
#INCLUDE "Protheus.ch"
#INCLUDE "FWMVCDEF.CH"

//Situacoes de contrato
#DEFINE DEF_SCANC "01" //Cancelado
#DEFINE DEF_SELAB "02" //Em Elaboracao
#DEFINE DEF_SEMIT "03" //Emitido
#DEFINE DEF_SAPRO "04" //Em Aprovacao
#DEFINE DEF_SVIGE "05" //Vigente
#DEFINE DEF_SPARA "06" //Paralisado
#DEFINE DEF_SSPAR "07" //Sol Fina.
#DEFINE DEF_SFINA "08" //Finalizado
#DEFINE DEF_SREVS "09" //Revisao
#DEFINE DEF_SREVD "10" //Revisado
#DEFINE DEF_TRAVIS "035" //Visualizacao de Medicoes

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130ManutЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Manutencao de Medicoes                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130Manut()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Alias do arquivo                                  Ё╠╠
╠╠Ё          Ё ExpN1 -> Numero do registro                          	  Ё╠╠
╠╠Ё          Ё ExpN2 -> Numero da opcao selecionada                       Ё╠╠
╠╠Ё          Ё ExpX1 -> Campos do arquivo                                 Ё╠╠
╠╠Ё          Ё ExpC2 -> Numero do contrato                            	  Ё╠╠
╠╠Ё          Ё ExpC3 -> Numero da revisЦo                             	  Ё╠╠
╠╠Ё          Ё ExpC4 -> Competencia                             		  Ё╠╠
╠╠Ё          Ё ExpC5 -> Numero da Planilha                            	  Ё╠╠
╠╠Ё          Ё ExpL1 -> .T. - Contrato com Medicao Eventual               Ё╠╠
╠╠Ё          Ё ExpL2 -> .T. - Contrato com cronograma fisico              Ё╠╠
╠╠Ё          Ё ExpC6 -> Parcela selecionada                           	  Ё╠╠
╠╠Ё          Ё ExpN3 -> Numero do adiantamento                            Ё╠╠
╠╠Ё          Ё ExpL3 -> .T. - Exibe Dialog                                Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                          Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130Manut(cAlias,nReg,nOpc,xParam,cContra,cRevisa,cCompet,cPlan,lMedeve,lFisico,cParcel,nTipo,lDialog,nServico,nSaveSX8)
Local aInfo
Local cFilCod
Local nA
Local nX
Local nPosCampo
Local nPosItem
Local nPosProd
Local nPosDescr
Local nPosVlUn
Local nPosPerc
Local nPosQMed
Local nPosQSol
Local nPosQtd
Local nPosPDesc
Local nPosDesc
Local nPosVlT
Local nPosNum
Local nPosItm
Local nPosVlDesc
Local nPosTpDesc
Local nPosTES
Local nPosQOri
Local nPosVOri
Local nPosCodNE
Local nPosItemNE
Local nPosArrend := 0
Local nPosCC
Local nPosConta
Local nPosItemCT
Local nPosCLVL
Local nPosPedTit
Local nOpca
Local nUsado     := 0
Local nValor     := 0

Local aCpoEnch  := {}
Local aNCpo     := {"CND_VLTOT","CND_VLPREV","CND_DTINIC","CND_VLMEAC","CND_VLSALD","CND_DATAIN","CND_DATAFI","CND_VLCONT","CND_VLADIT","CND_VLREAJ","CND_VLGER","CND_MEDRET","CND_RETIFI","CND_FLREAJ","CND_DESCME","CND_RETCAC","CND_TOTADT","CND_SITUAC","CND_VLLIQD","CND_VLMULT","CND_VLBONI","CND_ULTMED","CND_RECMED","CND_REVGER"}
Local aNalter   := {}
Local aAlter    := {}
Local cCadastro := STR0007//"MediГЦo"
Local aCond     := {}
Local lRet      := .T.
Local aButtons  := {}
Local aDescs    := {}
Local aHeadDcs  := {}
Local aCTBEnt	  := CTBEntArr()
Local nPercCNF  := 0 //Percentual da parcela em relacao ao total da planilha
Local cFilCNP   := ""
Local lSugVal   := ((GetNewPar("MV_CNSUGME","1")=="1") .OR. lFisico)//Informa se sugere ou nao as quantidade da medicao
Local lRealMed  := (GetNewPar( "MV_CNREALM", "S" ) == "S")
Local lReajMed  := (GetNewPar( "MV_CNREAJM", "S" ) == "S")
Local lHabiExce := SuperGetMV("MV_CNBTEXC",.F.,"N") == "S" .And. Str(nOpc,1) $ '1*3*4' //-- 1: excedente med. encer
Local lContinua := .T.
Local nContaIt  := 0

Local oGetDesc
Local oGetPrev
Local oGetMultas
Local oGetBoni
Local oSize 		:= FwDefSize():New()
Local oDlg

Local lPreench
Local cAliasCNR := ""
Local cAliasCNF := ""

Local aMultParc := {}
Local aMultas   := {}

Local lCaucRet := .F.
Local lTesSB1  := .T.
Local lPosNE	:= .F.

Local aNoFields := {}

Local cSeek    := ""
Local cWhile   := ""

Local aRet     := {}
Local aUsButtons := {}
Local lCn130VRet := ExistBlock("CN130VRET")
Local nCn130VRet := 0
Local lCN130LBM  := ExistBlock("CN130LBM")
Local cCN130LBM  := ""

Local cAliasQry := ""
Local cQuery    := ""
Local cCodEd		:= ""

Local cParcela    := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"
Local nParcelas   := SuperGetMv("MV_NUMPARC")
Local lNotaEmp	:= SuperGetMV("MV_NOTAEMP",.F.,.F.)
Local cNumPed     := ""
Local aRatGCT	:= {} //Array com os rateios dos itens do contrato
Local aColsCpos := {}
Local aHeadCpos	:= {}
Local nLinMax	:= GetNewPar("MV_COMLMAX", 9999)
Local cChaveCNS	:= ""
Local lLoadCNB	:= .T.

Static aAdiants	:= {}
Static aColsAux	:= {}
Static oGetAdia
Local nArredMed:= 0
Local lCn130PrG := ExistBlock( "CN130PrG" )
Local cField	:= ""
Local lTECItGSEn := ExistFunc("TECItGSEn")

Public aListBox   := {}
Public oList

Private lAuto := IIF(Type("lAuto") <> "L",.F.,lAuto)

aAdiants   := {}

If Type("cFilCTR") <> "C"
	cFilCtr := cFilAnt
EndIf

If !IsInCallStack("A097Visual") .And. aRotina[nOpc,4] == 2
	cFilCtr 	:= CND->CND_FILCTR
	lContinua 	:= CN240VldUsr(CND->CND_CONTRA,DEF_TRAVIS,.T.,,cFilCtr)
EndIf

cFilCNP   := xFilial("CNP",cFilCTR)

Private cEspCtr := "1"//Contrato de compra

PRIVATE aCols     := {}
PRIVATE aHeader   := {}
PRIVATE aAuxRat   := {}
DEFAULT nTipo     := 1
DEFAULT nServico  := 1 //1=Medicao de Contrato; 2=Medicao de Servico
DEFAULT nSaveSX8  := GetSX8Len()
DEFAULT lDialog   :=.T.
DEFAULT cPlan     := ""
//Desabilita na tela da medicao do contrato a tecla F4
SetKey( VK_F4, Nil )

//Autorizacao de Fornecimento
If nOpc != 3 .And. nOpc != 9 .And. nOpc != 12
	nTipo := If(CND->CND_AUTFRN=="1",1,2)
EndIf

//Medicao de Servicos
If nOpc != 3 .And. nOpc != 9 .And. nOpc != 12
	nServico := If(CND->CND_SERVIC=="1",1,2)
EndIf

If !Empty(cContra) .And. !Empty(cRevisa)
	CND->(dbSetOrder(1))
	If CND->(dbSeek(xFilial("CND",cFilCTR)+cContra+cRevisa))
		//Verifica se estА posicionado corretamente no contrato
		If !(CN9->(CN9_NUMERO+CN9_REVISA) == CND->(CND_CONTRA+CND_REVISA)) 
			CN9->(dbSetOrder(1))
			CN9->(dbSeek(xFilial("CN9",cFilCTR)+CND->CND_CONTRA+CND->CND_REVISA))
		EndIf
	EndIf
	CND->(dbGoTo(nReg))
EndIf

If nServico == 1

	If Cn300RetSt("FIXO",0,cPlan,,cFilCtr,.F.)
 		aNalter   := {"CNE_PRODUT","CNE_VLUNIT","CNE_PDESC"}
 		If CNE->( ColumnPos( "CNE_ARREND" ) ) > 0
 			aAdd( aNalter,  "CNE_ARREND" )
 		EndIf
 	EndIf

 	aNoFields := {"CNE_FILIAL","CNE_TIPO","CNE_DTANIV","CNE_CONTRA","CNE_REVISA","CNE_NUMERO","CNE_NUMMED","CNE_ITSOMA","CNE_QTRETI","CNE_QTDORI","CNE_VUNORI","CNE_15ANOS","CNE_20ANOS","CNE_25ANOS"}

 	cCadastro :=  STR0007

Else

	If Cn300RetSt("FIXO",0,cPlan,,,.F.)
 		aNalter   := {"CNE_PRODUT","CNE_QUANT ","CNE_PDESC","CNE_QTDORI","CNE_VUNORI","CNE_VLTOT"}
 		If CNE->( ColumnPos( "CNE_ARREND" ) ) > 0
 			aAdd( aNalter,  "CNE_ARREND" )
 		EndIf
 	Else
 		aNalter   := {"CNE_QUANT ","CNE_QTDORI","CNE_VLTOT"}
	EndIf

	aNoFields := {"CNE_FILIAL","CNE_TIPO","CNE_DTANIV","CNE_CONTRA","CNE_REVISA","CNE_NUMERO","CNE_NUMMED","CNE_ITSOMA","CNE_QTRETI","CNE_15ANOS","CNE_20ANOS","CNE_25ANOS"}

 	cCadastro := STR0105

EndIf

If CND->(FieldPos("CND_VLMPED")) > 0 .And. CND->(FieldPos("CND_VLBPED")) > 0
    aAdd(aNCpo, "CND_VLMPED")
    aAdd(aNCpo, "CND_VLBPED")
EndIf

If CNE->(FieldPos("CNE_VLMPED")) > 0 .And. CNE->(FieldPos("CNE_VLBPED")) > 0
    aAdd(aNoFields, "CNE_VLMPED")
    aAdd(aNoFields, "CNE_VLBPED")
EndIf

// Monta texto descritivo da operaГЦo para a MediГЦo, Ex: MediГЦo - Incluir, etc
If nOpc == 2 // Visualizar
	cCadastro := cCadastro+" - "+STR0003
Elseif nOpc == 3 // Incluir
	cCadastro := cCadastro+" - "+STR0004
Elseif nOpc == 4 // Alterar
	cCadastro := cCadastro+" - "+STR0005
Elseif nOpc == 5 // Excluir
	cCadastro := cCadastro+" - "+STR0006
Elseif nOpc == 12 // Med. ServiГos
	cCadastro := cCadastro+" - "+STR0004
Endif

If lContinua
	DEFAULT cParcel := ""
	//зддддддддддддддддддддддддддддддд©
	//Ё Inicializa lancamento do PCO  Ё
	//юддддддддддддддддддддддддддддддды
	PcoIniLan("000355")

	PRIVATE M->CND_VLTOT  := 0
	PRIVATE M->CND_DESCME := 0
	PRIVATE M->CND_VLPREV := 0
	PRIVATE M->CND_TOTADT := 0
	PRIVATE nTotAdia2 := 0
	PRIVATE nTotBoni  := 0
	PRIVATE nTotMultas:= 0
	PRIVATE lTodos    := .F.
	PRIVATE lFixo     := .T.
	PRIVATE lServico  := (nServico == 2)

	CN9->(dbSetOrder(1))
	If CN9->(dbSeek(xFilial("CN9",cFilCTR)+If(cContra!=NIL,cContra+cRevisa,CND->CND_CONTRA+CND->CND_REVISA)))
		If cFilCtr  <> CN9->CN9_FILCTR .and. ! Empty(CN9->CN9_FILCTR)
			cFilCtr:=CN9->CN9_FILCTR
		EndIf
		If nOpc == 3 .Or. nOpc == 4
			dbSelectArea("CNA")
			CNA->(dbSetOrder(1))
			CNA->(MsSeek(xFilial("CNA",cFilCTR)+cContra+cRevisa+cPlan))//Ё Verifica se o contrato possui planilha      Ё
			lFixo	:= Cn300RetSt("FIXO"	,0,cPlan,CN9->CN9_NUMERO,CN9->CN9_FILCTR,.F.)
			lFisico	:= Cn300RetSt("FISICO"	,0,cPlan,CN9->CN9_NUMERO,CN9->CN9_FILCTR,.F.)
		EndIf

		cEspCtr := CN9->CN9_ESPCTR //Verifica especie do contrato: 1 - Compra ; 2 - Venda
	EndIf

	If !lFixo
		aAdd(aNoFields,"CNE_QTDSOL")
		aAdd(aNoFields,"CNE_QTAMED")
		aAdd(aNoFields,"CNE_PERC")
	EndIf

	If cEspCtr == "1"
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Nao exibe campos do cliente para contrato de compra Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aAdd(aNCpo,"CND_CLIENT")
		aAdd(aNCpo,"CND_LOJACL")

		For nX:=1 to nParcelas
			If !Empty(CND->(FieldPos("CND_PARC"+Substr(cParcela,nx,1)))) .And. !Empty(CND->(FieldPos("CND_DATA"+Substr(cParcela,nx,1))))
				aAdd(aNCpo,"CND_PARC"+Substr(cParcela,nx,1))
				aAdd(aNCpo,"CND_DATA"+Substr(cParcela,nx,1))
			EndIf
		Next nX
		aAdd(aNoFields,"CNE_TS")
	Else
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Nao exibe campos do fornecedor para contrato de venda Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aAdd(aNCpo,"CND_FORNEC")
		aAdd(aNCpo,"CND_LJFORN")
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Nao exibe campos se condiГЦo de pagamento for diferente do tipo 9 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("SE4")
		dbSetOrder(1)
		MsSeek(xFilial("SE4")+CN9->CN9_CONDPG)
		If SE4->E4_TIPO<>'9'
			For nX:=1 to nParcelas
				If !Empty(CND->(FieldPos("CND_PARC"+Substr(cParcela,nx,1)))) .And. !Empty(CND->(FieldPos("CND_DATA"+Substr(cParcela,nx,1))))
					aAdd(aNCpo,"CND_PARC"+Substr(cParcela,nx,1))
					aAdd(aNCpo,"CND_DATA"+Substr(cParcela,nx,1))
				EndIf
			Next nX
		EndIf

		aAdd(aNoFields,"CNE_TE")

	EndIf
	
	If CND->( ColumnPos( "CND_TXMOED" ) ) > 0
		aAdd( aNCpo, "CND_TXMOED" )
	EndIf

	dbSelectArea("SX3")
	SX3->(dbSetOrder(1))
	SX3->(MsSeek("CND"))
	While SX3->(!Eof()) .And. SX3->X3_ARQUIVO == "CND"//Ё Seleciona os campos do cabecalho da medicao        Ё
		cField := SX3->X3_CAMPO
		If X3Uso( GetSX3Cache( cField, "X3_USADO" ) ) .And. cNivel >= GetSX3Cache( cField, "X3_NIVEL" ) .And. cField != "CND_FILIAL" .And. aScan(aNCpo,{|x| x == AllTrim( cField )}) == 0
			Aadd( aCpoEnch, cField )
		EndIF

		If	( GetSX3Cache( cField, "X3_CONTEXT" ) == "V"  .Or. nOpc == 3 .Or. nOpc == 9 .Or. nOpc == 12 )
			If cField == "CND_NUMMED" .And. lAuto .And. (nPosNum := aScan(aAutoCab,{|x| x[1]=="CND_NUMMED"})) > 0
				M->CND_NUMMED := aAutoCab[nPosNum,2]
			Else
				Do Case
					Case AllTrim( cField ) == "CND_CONTRA"
						M->CND_CONTRA := cContra
					Case AllTrim( cField ) == "CND_REVISA"
						M->CND_REVISA := cRevisa
					Case AllTrim( cField ) == "CND_COMPET"
						M->CND_COMPET := cCompet
					Case AllTrim( cField ) == "CND_FILCTR"
						M->CND_FILCTR := cFilCTR
					Otherwise
						If lAuto .And. (nPosNum := aScan(aAutoCab,{|x| AllTrim(x[1])== AllTrim(cField) })) > 0
					  		M->&( cField ):= aAutoCab[nPosNum,2]
						Else
					 		M->&( cField ) := CriaVar( cField )
						EndIf
				End Case
			EndIf
		ElseIf lAuto .And. nOpc == 4 .And.  cField $ "CND_FORNEC|CND_LJFORN|CND_CLIENT|CND_LOJACL" .And. (nPosNum := aScan(aAutoCab,{|x| x[1]== cField })) > 0
			M->&( cField ):= aAutoCab[nPosNum,2]
		Else
			M->&( cField ) := CND->(FieldGet(FieldPos( cField )))
		EndIf

		dbSelectArea("SX3")
		SX3->(dbSkip())
	EndDo

	//- Preenche o cliente/fornecedor
	If !lAuto
		If cEspCtr == "2"
		    If nOPC == 3 .Or. nOpc==12
				M->CND_CLIENT := CNA->CNA_CLIENT
				M->CND_LOJACL := CNA->CNA_LOJACL
			Else
				M->CND_CLIENT := CND->CND_CLIENT
				M->CND_LOJACL := CND->CND_LOJACL
			EndIf
		Else
			If nOPC == 3 .Or. nOpc==12
				M->CND_FORNEC := CNA->CNA_FORNEC
				M->CND_LJFORN := CNA->CNA_LJFORN
			Else
				M->CND_FORNEC := CND->CND_FORNEC
				M->CND_LJFORN := CND->CND_LJFORN
			EndIf
		EndIf
	EndIf

	//зддддддддддддддддддддддддддд©
	//Ё Preenche moeda da medicao Ё
	//юддддддддддддддддддддддддддды
	If cPaisLoc = 'RUS'
       M->CND_MOEDA :=Posicione("CN9",1,xFilial("CN9",cFilCTR)+If(cContra!=NIL,cContra+cRevisa,CND->CND_CONTRA+CND->CND_REVISA),"CN9_MOEDA")
       M->CND_CONUNI :=Posicione("CN9",1,xFilial("CN9",cFilCTR)+If(cContra!=NIL,cContra+cRevisa,CND->CND_CONTRA+CND->CND_REVISA),"CN9_CONUNI")
       M->CND_MOEDES :=Posicione("CTO",1,xFilial("CTO")+StrZero(M->CND_MOEDA,TamSX3("CTO_MOEDA")[1]),"CTO_SIMB")     
	else
		M->CND_MOEDA := If(nOpc!=3 .And. nOpc!=9.And. nOpc!=12,M->CND_MOEDA,If(!Empty(CN9->CN9_MOEDA),CN9->CN9_MOEDA,1))
	EndIf

	//здддддддддддддддддддддддддддддддд©
	//Ё Preenche parcela do cronograma Ё
	//юдддддддддддддддддддддддддддддддды
	If (nOpc == 3 .Or. nOpc == 9 .Or. nOpc==12)
		M->CND_PARCEL := cParcel
	EndIf

	//- Preenche grupo de aprovadores
	If Empty(M->CND_APROV) .And. (GetNewPar("MV_CNMDALC","N") == "S" ) 
		M->CND_APROV := If(!Empty(CN9->CN9_GRPAPR),CN9->CN9_GRPAPR,CN1->CN1_GRPAPR)
	EndIf

	//зддддддддддддддддддддддддддддддддддд©
	//Ё Definicao de variaveis Privates.  Ё
	//юддддддддддддддддддддддддддддддддддды
	Private aTela := {}
	Private aGets := {}

	//здддддддддддддддддддддддддддддд©
	//Ё Total da medicao             Ё
	//юдддддддддддддддддддддддддддддды
	Private oGetTot
	Private oGetVlMed
	Private oGetCauc
	Private nCaucRet   := Nil
	Private nCaucVlr   := Nil
	Private nTotMedDsc := 0//Valor total dos descontos aplicados na medicao
	Private nTotMedMlt := 0//Valor total das multas aplicadas na medicao
	Private nTotMedBnf := 0//Valor total das bonificaoes aplicadas na medicao
	Private nTotVlMed  := 0//Valor total da medicao

	oSize:AddObject("CABECALHO",100,50,.T.,.T.)
	oSize:AddObject("GETDADOS" ,100,38,.T.,.T.)
	oSize:AddObject("RODAPE"   ,100,50,.T.,.F.)

	oSize:lProp 	:= .T. // Proporcional
	oSize:aMargins := {3,3,3,3} // Espaco ao lado dos objetos 0, entre eles 3

	oSize:Process() 	   // Dispara os calculos

	//здддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica campos editaveis                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SX3")
	dbSetOrder(1)
	MsSeek("CNE")
	While !EOF() .And. (SX3->X3_ARQUIVO == "CNE")
		cField := SX3->X3_CAMPO
		If X3Uso( GetSX3Cache( cField, "X3_USADO" ) ) .And. cNivel >= GetSX3Cache( cField, "X3_NIVEL" ) .And. Ascan(aNoFields,{|x| x == alltrim( cField )}) == 0

			//здддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Impede alteracao dos campos no array aNAlter     Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддды
			If GetSX3Cache( cField, "X3_VISUAL" ) == "A" .And. aScan(aNAlter,AllTrim(SX3->X3_CAMPO)) == 0
				aAdd( aAlter, cField )
			EndIf
		EndIf
		dbSelectArea("SX3")
		dbSkip()
	EndDo

	//здддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a caucao de retencao esta disponivel Ё
	//Ё para contrato e para o ambiente                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддды
	If (lCaucRet := (CN9->CN9_FLGCAU == "1" .And. CN9->CN9_TPCAUC == "2"))
		nCaucRet := CN9->CN9_MINCAU
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ponto de Entrada desenvolvido para Queiroz Galvao para   Ё
		//Ё permitir alteracao no percentual de retencao baseando-se Ё
		//Ё em um campo de usuario criado no cadastro de planilhas   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
    	If lCn130VRet
	    	nCaucRet := If(Valtype(nCn130VRet:=ExecBlock("CN130VRET",.F.,.F.,{cContra,cRevisa,cPlan}))=="N",nCn130VRet,nCaucRet)
    	EndIf
	EndIf

	if nOpc == 3 .Or. nOpc == 9 .Or. nOpc == 12//Inclusao
		FillGetDados(nOpc,"CNE",1,,,,aNoFields,,,,,.T.,aHeader)
		aCols:={}

		nUsado := len(aHeader)

		M->CND_SERVIC := If(nServico==1,"1","2")

		M->CND_NUMERO := cPlan

		If lFixo
			dbSelectArea("CNA")
			dbSetOrder(1)
			cFilCod := xFilial('CNA',cFilCTR)
			//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Seleciona planilha                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
			If dbSeek(cFilCod+cContra+cRevisa+cPlan)

				If !lAuto .Or. (lAuto .And. (Empty(M->CND_FORNEC) .Or. Empty(M->CND_LJFORN)))
					M->CND_FORNEC := CNA->CNA_FORNEC
					M->CND_LJFORN := CNA->CNA_LJFORN
				EndIf

				If !lAuto .Or. (lAuto .And. (Empty(M->CND_CLIENT) .Or. Empty(M->CND_LOJACL)))
					M->CND_CLIENT := CNA->CNA_CLIENT
					M->CND_LOJACL := CNA->CNA_LOJACL
				EndIf

				M->CND_TIPPLA := CNA->CNA_TIPPLA
				M->CND_DESCTP := Posicione("CNL",1,xFilial("CNL",cFilCTR)+M->CND_TIPPLA,"CNL_DESCRI")

				//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Quando o contrato nao estiver configurado para     Ё
				//Ё medicao eventual, seleciona o valor previsto do    Ё
				//Ё cronograma                                         Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
				If !lMedeve
					If !Empty(cParcel)
						dbSelectArea("CNF")
						dbSetOrder(3)
						dbSeek(xFilial("CNF",cFilCTR)+cContra+cRevisa+CNA->CNA_CRONOG+cParcel)
					Else
						dbSelectArea("CNF")
						dbSetOrder(2)
						dbSeek(xFilial("CNF",cFilCTR)+cContra+cRevisa+CNA->CNA_CRONOG+cCompet)
					EndIf
					If CNF->CNF_SALDO > 0
						M->CND_VLPREV := CNF->CNF_SALDO
						If lSugVal .Or. lServico
							nPercCNF := CNF->CNF_SALDO / CNA->CNA_SALDO  //-- Calcula o percentual da parcela
						EndIf
					Else
						lContinua := .F.
						If lAuto
							AutoGrLog("Parcela sem saldo")
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf

		If lContinua
			dbSelectArea("CN9")
			dbSetOrder(1)
			cFilCod := xFilial('CN9',cFilCTR)
			If dbSeek(cFilCod+cContra+cRevisa)
				M->CND_CONDPG := CN9->CN9_CONDPG
				M->CND_DESCCP := Posicione("SE4",1,xFilial("SE4")+M->CND_CONDPG,"E4_DESCRI")
				M->CND_VLCONT := CN9->CN9_VLATU
				M->CND_VLADIT := CN9->CN9_VLADIT
				M->CND_VLREAJ := CN9->CN9_VLREAJ

				If !lMedeve
					M->CND_DTVENC := CNF->CNF_DTVENC
				else
					aCond := Condicao(0,M->CND_CONDPG,,dDataBase)
					If len(aCond) > 0
						M->CND_DTVENC := aCond[1][1]
					Else
						M->CND_DTVENC := dDataBase
					EndIf
				EndIf
			EndIf

			If lFixo
				nPosItem  	:= GDFIELDPOS("CNE_ITEM  ")
				nPosProd  	:= GDFIELDPOS("CNE_PRODUT")
				nPosDescr 	:= GDFIELDPOS("CNE_DESCRI")
				nPosVlUn  	:= GDFIELDPOS("CNE_VLUNIT")
				nPosPerc  	:= GDFIELDPOS("CNE_PERC  ")
				nPosQMed  	:= GDFIELDPOS("CNE_QTAMED")
				nPosQSol  	:= GDFIELDPOS("CNE_QTDSOL")
				nPosQtd   	:= GDFIELDPOS("CNE_QUANT ")
				nPosPDesc 	:= GDFIELDPOS("CNE_PDESC ")
				nPosDesc  	:= GDFIELDPOS("CNE_VLDESC")
				nPosVlT   	:= GDFIELDPOS("CNE_VLTOT ")
				nPosDEnt  	:= GDFIELDPOS("CNE_DTENT ")
				nPosCms  	:= GDFIELDPOS("CNE_FLGCMS")
				nPosQOri  	:= GDFIELDPOS("CNE_QTDORI")
				nPosVOri  	:= GDFIELDPOS("CNE_VUNORI")
				nPosCC		:= GDFIELDPOS("CNE_CC")
				nPosConta	:= GDFIELDPOS("CNE_CONTA")
				nPosItemCT	:= GDFIELDPOS("CNE_ITEMCT")
				nPosCLVL  	:= GDFIELDPOS("CNE_CLVL")
				nPosPedTit	:= GDFIELDPOS("CNE_PEDTIT")
				nPosCodNE	:= GDFIELDPOS("CNE_CODNE")
				nPosItemNE	:= GDFIELDPOS("CNE_ITEMNE")
				If CNE->( ColumnPos( "CNE_ARREND" ) ) > 0
					nPosArrend := GDFIELDPOS("CNE_ARREND")
				EndIf
				
				If cEspCtr == "1"
					nPosTES  := aScan(aHeader,{|x| x[2] == "CNE_TE    "})
				Else
					nPosTES  := aScan(aHeader,{|x| x[2] == "CNE_TS    "})
				EndIf

				cFilCod	:= xFilial("CNB",cFilCTR)				
				
				CNS->(dbSetOrder(1)) //CNS_FILIAL+CNS_CONTRA+CNS_REVISA+CNS_CRONOG+CNS_PARCEL+CNS_ITEM
				cChaveCNS := xFilial("CNS",cFilCTR)+cContra+cRevisa+CNF->CNF_NUMERO+CNF->CNF_PARCEL //Chave parcial da CNS

				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Monta o aCols.                                 Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("CNB")
				CNB->(dbSetOrder(1))
				CNB->(DbSeek(cFilCod + cContra + cRevisa + cPlan))				

				While CNB->( CNB_FILIAL+CNB_CONTRA+CNB_REVISA+CNB_NUMERO == (cFilCod + cContra + cRevisa + cPlan) .And. !Eof() )
					
					lLoadCNB := (CNB->CNB_SLDMED > 0)

					If lLoadCNB .And. lFisico						
						If (lLoadCNB := CNS->(dbSeek(cChaveCNS+CNB->CNB_ITEM)))
							lLoadCNB := (CNS->CNS_SLDQTD > 0)//Em planilhas fisicas, sС deve trazer item com saldo na CNS
						EndIf
					EndIf
					
					if lLoadCNB

						Aadd(aCols, Array(nUsado + 1))

						//здддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Inicializa campos da getdados                  Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддды
						For nA:=1 to len(aHeader)
							If !(IsHeadRec(aHeader[nA,2]) .OR. IsHeadAlias(aHeader[nA,2]))
								aCols[len(aCols),nA] := CriaVar(aHeader[nA,2])
							EndIf
						Next

						aCols[len(aCols),nPosItem] 	:= CNB->CNB_ITEM
						aCols[len(aCols),nPosProd] 	:= CNB->CNB_PRODUT
						aCols[len(aCols),nPosDescr]	:= CNB->CNB_DESCRI
						aCols[len(aCols),nPosVlUn] 	:= CNB->CNB_VLUNIT
						aCols[len(aCols),nPosQSol]  := CNB->CNB_QUANT
						aCols[len(aCols),nPosQtd] 	:= CNB->CNB_SLDMED

						If (!Empty(aAutoItens) .And. AScan(aAutoItens, {|x| ChkProd(x, CNB->CNB_PRODUT,CNB->CNB_ITEM)}) == 0)
							lMedeve := .T. //Zera quantidades do item
						EndIf	
					
						aCols[len(aCols),nPosQMed] 	:= CNB->CNB_SLDMED
						aCols[len(aCols),nPosPDesc]	:= CNB->CNB_DESC
						aCols[len(aCols),nPosCodNE]	:= CNB->CNB_CODNE
						aCols[len(aCols),nPosItemNE]:= CNB->CNB_ITEMNE

						If nPosArrend > 0
							aCols[len(aCols),nPosArrend] := CNB->CNB_ARREND
						EndIf
						
						If nPosCC > 0
							aCols[len(aCols),nPosCC]     := CNB->CNB_CC
							aCols[len(aCols),nPosConta]  := CNB->CNB_CONTA
							aCols[len(aCols),nPosItemCT] := CNB->CNB_ITEMCT
							aCols[len(aCols),nPosCLVL]   := CNB->CNB_CLVL
						EndIf

						If nPosPedTit > 0
							aCols[len(aCols),nPosPedTit]   := CNB->CNB_PEDTIT
						EndIf
						
						For nX := 1 To Len(aCTBEnt)
							If GDFieldPos("CNE_EC"+aCTBEnt[nX]+"CR") > 0.And. CNB->(FieldPos("CNB_EC"+aCTBEnt[nX]+"CR")) > 0
			  					aCols[Len(aCols),GDFieldPos("CNE_EC"+aCTBEnt[nX]+"CR")] := CNB->&("CNB_EC"+aCTBEnt[nX]+"CR")
			 					aCols[Len(aCols),GDFieldPos("CNE_EC"+aCTBEnt[nX]+"DB")] := CNB->&("CNB_EC"+aCTBEnt[nX]+"DB")
							EndIf
						Next nX

						If nPosCms > 0
							aCols[len(aCols),nPosCms]  := CNB->CNB_FLGCMS
						EndIf
						
						If nPosTES > 0
							If !Empty(CNB->CNB_TE) .Or. !Empty(CNB->CNB_TS)
								aCols[len(aCols),nPosTES]  := iIf(cEspCtr=="1",CNB->CNB_TE,CNB->CNB_TS)
								lTesSB1 := .F.
							EndIf
						EndIf
						

						If nPosTES > 0 .And. lTesSB1
							SB1->(dbSetOrder( 1 ))
							dbSelectArea("SB1")
							If MSSeek(xFilial("SB1")+aCols[len(aCols),nPosProd])
								If !Empty(SB1->B1_TE) .Or. !Empty(SB1->B1_TS)
							   		aCols[len(aCols),nPosTES]  := iIf(cEspCtr=="1",SB1->B1_TE,SB1->B1_TS)
								EndIf
							EndIf
						EndIf

						If ((lMedeve .OR. !lSugVal) .And. !lServico) .OR. (lTECItGSEn .AND. TECItGSEn(cContra, cRevisa, cPlan, CNB->CNB_ITEM, lAuto, CNB->CNB_PRODUT)) //Verifica se deve o item do contrato И do GS e estА  configurado para apurar zerado o item encerrado
							aCols[len(aCols),nPosQtd] := 0.00 //Zera quantidades do item
							aCols[len(aCols),nPosVlT] := 0.00
							aCols[len(aCols),nPosPerc]:= 0.00
							aCols[len(aCols),nPosDesc]:= 0.00
						Else
							If !lFisico
								If nServico == 1
									aCols[len(aCols),nPosQtd] := A410Arred( CNB->CNB_SLDMED * nPercCNF , "CNE_QUANT" )
									If CNB->CNB_SLDMED < aCols[len(aCols),nPosQtd] .And. (aCols[len(aCols),nPosQtd]-CNB->CNB_SLDMED) > 0.0001
										aCols[len(aCols),nPosQtd] := CNB->CNB_SLDMED
									EndIf
								Else
									aCols[len(aCols),nPosQtd] := 1
									aCols[len(aCols),nPosQOri] := If( lMedeve , CNB->CNB_SLDMED , A410Arred( CNB->CNB_SLDMED * nPercCNF , "CNE_QUANT" ) )
									aCols[len(aCols),nPosVOri]:= CNB->CNB_VLUNIT
									If CNB->CNB_SLDMED < aCols[len(aCols),nPosQOri] .And. (aCols[len(aCols),nPosQOri]-CNB->CNB_SLDMED) > 0.0001
										aCols[len(aCols),nPosQOri] := CNB->CNB_SLDMED
									EndIf
								EndIf
							Else
								aCols[len(aCols),nPosQtd] := IIF((CNB->CNB_SLDMED < CNS->CNS_SLDQTD), CNB->CNB_SLDMED, CNS->CNS_SLDQTD)
							EndIF

							If nServico == 1
								aCols[len(aCols),nPosVlT] := A410Arred(aCols[len(aCols),nPosQtd]*CNB->CNB_VLUNIT,"CNE_VLTOT")
								aCols[len(aCols),nPosPerc]:= A410Arred((aCols[len(aCols),nPosQtd]*100)/CNB->CNB_QUANT,"CNE_PERC")

								If cEspCtr == "2"
									If aCols[len(aCols),nPosPDesc] > 0
										FtDescItem(0,@aCols[len(aCols),nPosVlUn],@aCols[len(aCols),nPosQtd],0,CNB->CNB_DESC,@aCols[len(aCols),nPosDesc],0,1,,)
									Endif
									nTotUnt := A410Arred(aCols[len(aCols),nPosVlT]-(aCols[len(aCols),nPosQMed]*CNB->CNB_VLUNIT),"CNE_VLTOT")
	        		            Else
									aCols[len(aCols),nPosDesc]:= A410Arred((aCols[len(aCols),nPosVlT]*CNB->CNB_DESC)/100,"CNE_VLDESC")
								EndIF
							Else
								aCols[len(aCols),nPosVlT] := aCols[len(aCols),nPosQOri]*CNB->CNB_VLUNIT
								aCols[len(aCols),nPosVlUn]:= aCols[len(aCols),nPosVlT]
								aCols[len(aCols),nPosPerc]:= (aCols[len(aCols),nPosQOri]*100)/CNB->CNB_QUANT

								If cEspCtr == "2"
									If aCols[len(aCols),nPosPDesc] > 0
										FtDescItem(0,@aCols[len(aCols),nPosVOri],@aCols[len(aCols),nPosQOri],0,CNB->CNB_DESC,@aCols[len(aCols),nPosDesc],0,1,,)
									Endif
						        Else
									aCols[len(aCols),nPosDesc]:= A410Arred((aCols[len(aCols),nPosVlT]*CNB->CNB_DESC)/100,"CNE_VLDESC")
								EndIf
							EndIf
							M->CND_VLTOT += Round(aCols[len(aCols),nPosVlT]-aCols[len(aCols),nPosDesc],TamSx3("CNE_VLTOT")[2])
						EndIf

						aCols[len(aCols),nPosDEnt] := dDataBase

						//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Atribui .F. p/ a coluna que determina se a linha do aCols esta deletada.Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						aCols[Len(aCols)][nUsado + 1] := .F.
						nContaIt++
						C130CpyCNZ({CNB->CNB_CONTRA,CNB->CNB_REVISA,CNB->CNB_NUMERO,CNB->CNB_ITEM},aRatGCT)//Efetua a copia do rateio para a medicao Ё
					EndIf
					
					CNB->(dbSkip())
				EndDo

				If M->CND_VLTOT > CNA->CNA_SALDO // Verifica se o valor da mediГЦo irА ultrapassar o saldo do contrato
					nArredMed := Round(M->CND_VLTOT,TamSX3("CND_VLTOT")[2]) - CNA->CNA_SALDO
					If nArredMed <= 0.02 // Verifica se a diferenГa И de atИ 2 centavos
						M->CND_VLTOT -= nArredMed
					Endif
				Endif
			EndIf

			//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Ponto de entrada executado na inclusao da medicao Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ExistBlock("CN130Inc")
				aRet := ExecBlock("CN130Inc",.f.,.f.,{aHeader,aCols,aHeadDcs,aDescs})

				If Valtype(aRet) == "A"
					If Len(aRet)>=1 .And. Valtype(aRet[1]) == "A"
						aHeader := aClone(aRet[1])
					EndIf
					If Len(aRet)>=2 .And. Valtype(aRet[2]) == "A"
						aCols   := aClone(aRet[2])
					EndIf
					If Len(aRet)>=3 .And. Valtype(aRet[3]) == "A"
						aHeadDcs:= aClone(aRet[3])
					EndIf
					If Len(aRet)>=4 .And. Valtype(aRet[4]) == "A"
						aDescs  := aClone(aRet[4])
					EndIf
				EndIf
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica se contrato possui saldo              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			If nContaIt==0 .And. lFixo
				Help (" ",1,"CN130NOIT")
				lContinua := .F.
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Calcula valor da retencao                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			If lContinua
				If lCaucRet
					nCaucVlr:=((M->CND_VLTOT*nCaucRet)/100)
				EndIf

				If CN300RetSt("TPMULT",0,cPlan,,cFilCtr,.F.) == "2"
					CN130ReprMT(,,,@aMultas)
				EndIf

				If cEspCtr == "1"
					nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT - nTotMultas + nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
				Else
					nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT + nTotMultas - nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
				EndIf
			EndIf
		EndIf
	Else//Alteracao,Visualizacao,Exclusao
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta aCols e aHeader                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("CNE")					
		cFilCod := CND->CND_FILIAL
		
		cSeek	 := cFilCod+CND->CND_CONTRA+CND->CND_REVISA+CND->CND_NUMERO+CND->CND_NUMMED
		cWhile := "CNE_FILIAL+CNE_CONTRA+CNE_REVISA+CNE_NUMERO+CNE_NUMMED"
 	 	FillGetDados(nOPc,"CNE",1,cSeek,{|| &cWhile },{|| .T. },aNoFields,,,,,,aHeader,aCols)

		//здддддддддддддддддддддддддддддддддддд©
		//Ё Carrega valor total do desconto    Ё
		//юдддддддддддддддддддддддддддддддддддды
		M->CND_DESCME:=CND->CND_DESCME
		M->CND_VLTOT :=CND->CND_VLTOT
		M->CND_VLPREV:=CND->CND_VLPREV
		M->CND_TOTADT:=CND->CND_TOTADT
		nTotAdia2:=CND->CND_TOTADT //armazenando valor para futuros recalculos

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ponto de entrada executado na alteraГЦo, exclusЦo e visualizaГЦo da medicao Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock("CN130Alt")
			ExecBlock("CN130Alt",.F.,.F.,{nOpc})
		EndIf

		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Carrega valor de retencao                   Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If !Empty(CND->CND_RETCAC)
			nCaucVlr:=CND->CND_RETCAC
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддд©
		//Ё Carrega o array de multas / bonificacoes Ё
		//юдддддддддддддддддддддддддддддддддддддддддды

		cAliasCNR := GetNextAlias()

		dbSelectArea("CNR")

		cQuery := " SELECT * "
		cQuery += " FROM "+RetSqlName("CNR")
		cQuery += " WHERE CNR_FILIAL = '"+xFilial("CNR")+"'"
		cQuery += " AND CNR_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += " AND D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery( cQuery )
		dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasCNR, .F., .T. )

		//здддддддддддддддддддддддддддддддддддддддддд©
		//Ё Acerta o campo numerico                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддды
		TcSetField( cAliasCNR, "CNR_VALOR", "N", 14, 2 )

		While !( cAliasCNR )->( Eof() )

			If !Empty(( cAliasCNR )->CNR_CONTRA) .And. (( cAliasCNR )->CNR_CONTRA) <> CND->CND_CONTRA
				( cAliasCNR )->( dbSkip() )
				Loop
			EndIf

			aMultParc := Array( 6 )

			aMultParc[ 1 ] := ( cAliasCNR )->CNR_TIPO
			aMultParc[ 2 ] := ( cAliasCNR )->CNR_DESCRI
			aMultParc[ 3 ] := ( cAliasCNR )->CNR_VALOR
			aMultParc[ 4 ] := ( cAliasCNR )->CNR_MODO
			aMultParc[ 5 ] := ( cAliasCNR )->CNR_FLGPED
			aMultParc[ 6 ] := ( cAliasCNR )->CNR_CODIGO

			AAdd( aMultas, aMultParc )

			If ( cAliasCNR )->CNR_TIPO == "1"
				nTotMultas += ( cAliasCNR )->CNR_VALOR
				//Soma as multas que interferem pedido
				If  ( cAliasCNR )->CNR_FLGPED == "1"
					nTotMedMlt += ( cAliasCNR )->CNR_VALOR
				EndIf
			ElseIf ( cAliasCNR )->CNR_TIPO == "2"
				nTotBoni   += ( cAliasCNR )->CNR_VALOR
				//Soma as bonificacoes que interferem pedido
				If  ( cAliasCNR )->CNR_FLGPED == "1"
					nTotMedBnf += ( cAliasCNR )->CNR_VALOR
				EndIf
			EndIf

			( cAliasCNR )->( dbSkip() )

		EndDo

		( cAliasCNR )->( dbCloseArea() )
			dbSelectArea( "CNR" )

		//Carrega Descontos
		CN130PrcDesc(nOpc,aHeadDcs,aDescs)
		//Encontra posicoes
		nPosVlDesc:=aScan(aHeadDcs,{|x| x[2] == "CNQ_VALOR "})
		nPosTpDesc:=aScan(aHeadDcs,{|x| x[2] == "CNQ_TPDESC"})

		dbSelectArea("CNP")
		If nPosVlDesc > 0 .And. nPosTpDesc > 0
			For nx:=1 to len(aDescs)
				//Soma os valores apenas dos descontos que afetam o pedido
				If dbSeek(cFilCNP+aDescs[nx,nPosTpDesc]) .And. CNP->CNP_FLGPED == "1"
					nTotMedDsc += aDescs[nx,nPosVlDesc]
				EndIf
			Next
        EndIf
	EndIf


	If lContinua
		// Tratamento para calculo do desconto na rotina automatica
		// Essencial para MATA410
		If INCLUI .And. lAuto .And. (nX := aScan(aAutoCab,{|x| AllTrim(x[1]) == "CND_DESCME"})) > 0
			//Carrega estrutura dos arrays de descontos
			CN130PrcDesc(nOpc,aHeadDcs,aDescs)

			M->CND_DESCME := aAutoCab[nX,2]
			For nX := 1 To Len(aHeadDcs)-2
				Do Case
					Case AllTrim(aHeadDcs[nX,2]) == "CNQ_TPDESC"
						aTail(aDescs)[nX] := CN130DesPV()
					Case AllTrim(aHeadDcs[nX,2]) == "CNQ_VALOR"
						aTail(aDescs)[nX] := M->CND_DESCME
					OtherWise
						aTail(aDescs)[nX] := CriaVar(AllTrim(aHeadDcs[nX,2]))
				EndCase
			Next nX
		EndIf

		// Carrega OBS ja que nao ha tratamento para memo
		If INCLUI .And. lAuto .And. (nX := aScan(aAutoCab,{|x| AllTrim(x[1]) == "CND_OBS"})) > 0
			M->CND_OBS := aAutoCab[nX,2]
		EndIf

		If INCLUI .And. lAuto .And. Len(aMultasGCT) > 0
			aMultas := aMultasGCT
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддд©
		//Ё Atualiza valor total da medicao          Ё
		//юдддддддддддддддддддддддддддддддддддддддддды
		If cEspCtr == "1"
			nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT - nTotMultas + nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
		Else
			nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT + nTotMultas - nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
		EndIf

		If lAuto
			//-- Carrega excedentes no aCols
			If cEspCtr == '2' .And. lHabiExce .And. Type("aAutoExced") # "U"
				CN130Exce(nOpc)
			EndIf

			aAuxRat:= aClone(aRatGCT)

			//зддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁExecuta rotina automatica para os itens quando Ё
			//Ёo usuario informar os itens, caso contrario    Ё
			//Ёaceita o valor sugerido pela medicao se o      Ё
			//Ёparametro MV_CNSUGME estiver ativo             Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддды
			If len(aAutoItens) > 0
				If MsGetDAuto(aAutoItens,,{|| CN130TudOk(aCols)},aAutoCab,aRotina[nOpc][4])
				 	nOpca := 1
				Else
					nOpca := 0
				EndIf
			Else
				nOpca := 1
			EndIf
		Else
			aAuxRat:= aClone(aRatGCT)
			If lDialog
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Botao do Tracker                                        Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If nOpc # 1 // Excedentes
					If aRotina[ nOpc, 4 ] == 2
						aAdd(aButtons, { "bmpord1", { || CN130Track(aHeader,aCols) }, STR0012, STR0011 } ) //"System Tracker", "Tracker"
						If (GetNewPar("MV_CNMDALC","N") == "S" ) .And. !Empty(CND->CND_APROV)
							If MtExistDBM('IM',CND->CND_NUMMED)
                        		aAdd(aButtons,{"BUDGET",   {|| a120Posic(cAlias,nReg,nOpc,"IM",.F.)},STR0076,STR0077 }) //"Consulta Aprovacao por Item de MediГЦo"
                  			Else
                        		aAdd(aButtons,{"BUDGET",   {|| a120Posic(cAlias,nReg,nOpc,"MD",.F.)},STR0076,STR0077 }) //"Consulta Aprovacao por MediГЦo"
                 			EndIf
						EndIf
					EndIf

					Aadd(aButtons,{ "BAIXATIT",{|| CN130Desc(nOpc,aDescs,aHeadDcs),oGetDesc:Refresh(),CN130TotMed(5)},OemToAnsi(STR0014),OemtoAnsi(STR0013)})//"Descontos"

					If lFisico
						Aadd(aButtons,{ "note",{|| (If(CN110VisFis(M->CND_CONTRA,M->CND_REVISA,M->CND_NUMERO,M->CND_COMPET,oGetDados,(aRotina[ nOpc, 4 ] == 2),M->CND_PARCEL),(M->CND_VLTOT:=M->CND_VLPREV,CN130TotMed(5)),))},OemToAnsi(STR0065),OemtoAnsi(STR0064)})//Cronograma FМsico
					EndIf

					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Botao das multas / bonificacoes                         Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

					AAdd( aButtons,{ "checked", {|| CN130Multas( aMultas, oGetDados:aCols, oGetDados:aHeader, aRotina[ nOpc, 4 ], @nTotMultas, @nTotBoni, M->CND_CONTRA, M->CND_REVISA,@oGetMultas,@oGetBoni, cPlan ), CN130TotMed(5) }, STR0021, STR0022 } ) // "Seleciona Multas / Bonificacoes", "Multas"


			   	    //зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Botao dos Adiatamentos                                  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					Aadd(aButtons,{ "BUDGET",{|| If(nOpc=3 .or. nOpc=4 .or. nOpc=9 .or. nOpc=12,CN130Adiant(M->CND_CONTRA,M->CND_NUMERO, oGetDados:aCols, oGetDados:aHeader, aRotina[ nOpc, 4 ],M->CND_CONTRA, M->CND_REVISA,nOpc,@aHeadCpos,@aColsCpos ),), CN130TotMed(5)}, "Selecione os Adiantamentos", "Adiantam." } )
					Aadd(aButtons,{'S4WB013N',{|| oGetDados:oBrowse:lDisablePaint := .T. , GCTRateio(oGetDados,aRatGCT,nOpc,{M->CND_CONTRA,M->CND_REVISA,M->CND_NUMMED},@aAuxRat), oGetDados:oBrowse:lDisablePaint := .F. },STR0090,STR0091})//"Rateio por Item do pedido"##"Rateio "

				EndIf

		   	    //зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Botao para inclusao de excedente						Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If cEspCtr == '2' .And. lHabiExce
					aAdd(aButtons,{ "SOLICITA",{|| CN130Exce(nOpc)},OemToAnsi(STR0095),OemtoAnsi(STR0092)}) //"Excedente"
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Avalia botoes do usuario                                     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ExistBlock( "CN130BUT" )
					If ValType( aUsButtons := ExecBlock( "CN130BUT", .F., .F. ) ) == "A"
						AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
					EndIf
				EndIf

				// Monta texto descritivo da operaГЦo para a MediГЦo, Ex: MediГЦo - Incluir, etc
				If nOpc == 3 .and. IsInCallStack("CN120Serv") // ServiГos
					cCadastro := cCadastro+" (ServiГos)"
				Endif

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Dialog.                                                                 Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				DEFINE MSDIALOG oDlg TITLE cCadastro From oSize:aWindSize[1],oSize:aWindSize[2] to oSize:aWindSize[3],oSize:aWindSize[4] of oMainWnd PIXEL

				EnChoice( cAlias, nReg, nOpc,,,,aCpoEnch,{oSize:GetDimension("CABECALHO","LININI"),oSize:GetDimension("CABECALHO","COLINI"),oSize:GetDimension("CABECALHO","LINEND"),oSize:GetDimension("CABECALHO","COLEND")}, , , , , , , , .T.)

				If ValType(xParam) == "C" .And. xParam == "EXCEDE"
					oGetDados:=MsNewGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),GD_DELETE,"CN130LinOk()","CN130TudOk()",,,,nLinMax,,,"CN130DelOk(.T.)",oDlg,aHeader,aCols)
				ElseIf lFixo
					oGetDados:=MsNewGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),IIF(nOpc!=3 .And. nOpc!=4 .And. nOpc!=9.And. nOpc!=12,0,GD_UPDATE+GD_DELETE),"CN130LinOk()","CN130TudOk()",,aAlter,,len(aCols),,,"CN130DelOk(.T.)",oDlg,aHeader,aCols)
				Else
					oGetDados:=MsNewGetDados():New(oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),oSize:GetDimension("GETDADOS","LINEND"),oSize:GetDimension("GETDADOS","COLEND"),IIF(nOpc!=3 .And. nOpc!=4 .And. nOpc!=9,0,GD_INSERT+GD_UPDATE+GD_DELETE),"CN130LinOk()","CN130TudOk()",'+CNE_ITEM',,, nLinMax,"CN130FldOk",,"CN130DelOk(.F.)",oDlg,aHeader,aCols)
				EndIf

				oFolder := TFolder():New(oSize:GetDimension("RODAPE","LININI"),oSize:GetDimension("RODAPE","COLINI"),{OemToAnsi(STR0015)},,oDlg,,,, .T., .F.,oSize:GetDimension("RODAPE","XSIZE"),oSize:GetDimension("RODAPE","YSIZE"))

				@ 003,003 Say OemToAnsi(STR0018) Of oFolder:aDialogs[1] PIXEL //Total itens
				@ 002,037 MsGet oGetTot Var M->CND_VLTOT Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]

				@ 019,003 Say OemToAnsi(STR0079) Of oFolder:aDialogs[1] PIXEL//Total Medicao
				@ 018,037 MsGet oGetVlMed Var NoRound(nTotVlMed,TamSx3("CND_VLTOT")[2]) Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]

				@ 003,117 Say OemToAnsi(STR0019) Of oFolder:aDialogs[1] PIXEL //Valor previsto
				@ 002,151 MsGet oGetPrev Var M->CND_VLPREV Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]

				@ 003,231 Say OemToAnsi(STR0013) Of oFolder:aDialogs[1] PIXEL //Total do desconto
				@ 002,275 MsGet oGetDesc Var M->CND_DESCME Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]


				@ 019,117 Say  If(lCN130LBM,(If(ValType(cCN130LBM:= ExecBlock("CN130LBM",.F.,.F.))=="C",cCN130LBM, OemToAnsi(STR0054))),OemToAnsi(STR0054))  Of oFolder:aDialogs[1] PIXEL //Multas
				@ 018,151 MsGet oGetMultas Var nTotMultas Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]

				@ 019,231 Say OemToAnsi(STR0055)   Of oFolder:aDialogs[1] PIXEL //Bonificacoes
				@ 018,275 MsGet oGetBoni Var nTotBoni   Picture PesqPict("CND","CND_VLTOT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]



				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Valor Total do Aditamento                   Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				@ 003,355 Say OemToAnsi(STR0066) Of oFolder:aDialogs[1] PIXEL //Adiantamento
				@ 002,402 MsGet oGetAdia Var M->CND_TOTADT Picture PesqPict("CND","CND_TOTADT") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]
				//зддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Valor retido da caucao                      Ё
				//юддддддддддддддддддддддддддддддддддддддддддддды
				If lCaucRet
					@ 019,355 Say OemToAnsi(STR0063) Of oFolder:aDialogs[1] PIXEL//"Ret. CauГЦo"
					@ 018,402 MsGet oGetCauc Var nCaucVlr Picture PesqPict("CND","CND_RETCAC") PIXEL When .F. Size 65,5 Of oFolder:aDialogs[1]
				EndIf

				If Len(aRatGCT) == 0
					aRatGCT := GetRatInBase(oGetDados,"CND")
					If Empty(aAuxRat)
						aAuxRat:= aClone(aRatGCT)
					EndIf
				Endif
				ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(Obrigatorio(aGets,aTela) .And. oGetDados:TudoOk() .And.  If( nOpc == 3 .Or. nOpc == 4,CN130VldMed(cContra,cRevisa,cPlan,cCompet,lMedeve,aMultas),.T.),{nOpca:=1,oDlg:End()},nOpca:=0)},{||(nOpca:=2,oDlg:End())},,aButtons)
			EndIf
		EndIf

		If nOpca == 1

			If lCn130PrG
				lContinua := ExecBlock( "CN130PrG", .F., .F., { cAlias, nReg, nOpc } )
			EndIf

			If lContinua
				If ValType(xParam) == "C" .And. xParam == "EXCEDE"
					nOpc := 13
				EndIf

				CN130Grv(nOpc,lMedeve,cCompet,@nSaveSX8,aDescs,aHeadDcs,aMultas,aHeader,aCols,nTipo,aRatGCT,nServico)

				If ValType(xParam) == "C" .And. xParam == "EXCEDE" .And. lRet
					Processa({|| CN120GrvPeD(CND->CND_NUMMED,CND->CND_REVISA,@cNumPed,NIL,CND->CND_CONTRA,NIL,.T.)},,STR0097) //-- Gerando Pedido de Venda
					If !Empty(cNumPed)
						Aviso("CNTA120",STR0096 +cNumPed,{STR0046}) //Gerado pedido de venda nЗmero ###
					EndIf
				EndIf
			EndIf

		Else
		   If nOpc == 3
		   		lRet := .F.
		   		While ( GetSX8Len() >= nSaveSX8 )
				   RollBackSX8()
				EndDo
			Else
			   While ( GetSX8Len() > nSaveSX8 )
				   RollBackSX8()
				EndDo
			EndIf
		EndIf

		aListBox   := {}

		//здддддддддддддддддддддддддддддд©
		//Ё Finaliza lancamentos do PCO  Ё
		//юдддддддддддддддддддддддддддддды
		PcoFinLan("000355")
		PcoFreeBlq("000355",,,,,(nOpc == 4 .And. nOpca!=1))
	EndIf
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130Desc Ё Autor Ё Marcelo Custodio      Ё Data Ё20.05.2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Rotina de manutencao dos descontos                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130Desc(nExp01,aExp03,aExp04)                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ  nExp01 - Opcao Atual                                      Ё╠╠
╠╠Ё          Ё  lExp02 - Descontos aplicados - acols                      Ё╠╠
╠╠Ё          Ё  cExp03 - Cabecalho da getdados de desconto                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130Desc(nOpc,aDescs,aHeadDcs)

Local oGetDados

Local cFilCNP   := xFilial("CNP",cFilCTR)
Local cQuery
Local cCampo

Local nUsado
Local nPosTp
Local nx
Local nOpca:=2
Local nPosVl
Local oDlgDesc

Local aStrucCNQ

If len(aHeadDcs) == 0
	CN130PrcDesc(nOpc,aHeadDcs,aDescs)
EndIf

DEFINE MSDIALOG oDlgDesc TITLE OemToAnsi(STR0061) From 165,115 TO 450,600 PIXEL//"Descontos"

@ 07,05 GROUP oGrp To __DlgHeight(oDlgDesc)-30,__DlgWidth(oDlgDesc)-5 Label OemToAnsi(STR0062) Of oDlgDesc PIXEL//"Descontos Aplicados"

oGetDadDe:=MsNewGetDados():New(15,10,__DlgHeight(oDlgDesc)-35,__DlgWidth(oDlgDesc)-10,IIF(nOpc==2,0,GD_UPDATE+GD_INSERT+GD_DELETE),"CN130VldDes(oGetDadDe)",,,,,,,,,oDlgDesc,aHeadDcs,aDescs)

DEFINE SBUTTON FROM __DlgHeight(oDlgDesc)-25, __DlgWidth(oDlgDesc)-60 TYPE 1 ACTION (If((CN130VldDes(oGetDadDe)),(nOpca:=1,oDlgDesc:End()),)) ENABLE OF oDlgDesc PIXEL
DEFINE SBUTTON FROM __DlgHeight(oDlgDesc)-25, __DlgWidth(oDlgDesc)-30 TYPE 2 ACTION (nOpca:=2,oDlgDesc:End()) ENABLE OF oDlgDesc PIXEL

ACTIVATE MSDIALOG oDlgDesc CENTERED

If nOpca==1
	nUsado := len(aHeadDcs)

	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza descontos                          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	aDescs:=oGetDadDe:aCols
	nPosVl:=aScan(aHeadDcs,{|x| x[2] == "CNQ_VALOR "})
	nPosTp:=aScan(aHeadDcs,{|x| x[2] == "CNQ_TPDESC"})
	M->CND_DESCME:=0
	nTotMedDsc:=0

	dbSelectArea("CNP")
	dbSetOrder(1)

	For nx:=1 to len(aDescs)
		If !aDescs[nx,nUsado+1]
			M->CND_DESCME+=aDescs[nx,nPosVl]

			//Soma os valores apenas dos descontos que afetam o pedido
			If dbSeek(cFilCNP+aDescs[nx,nPosTp]) .And. CNP->CNP_FLGPED == "1"
				nTotMedDsc += aDescs[nx,nPosVl]
			EndIf
		EndIf
	Next
EndIf

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130PrcDescЁ Autor Ё Marcelo Custodio      Ё Data Ё20.05.2006Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Preenche aCols e aHeader para o controle de descontos        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130PrcDesc(nExp1,aExp2,aExp3)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ  nExp01 - Opcao Atual                                        Ё╠╠
╠╠Ё          Ё  aExp02 - aHeader - Descontos                                Ё╠╠
╠╠Ё          Ё  aExp03 - aCols - Descontos                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130PrcDesc(nOpc,aHeadDcs,aDescs)
Local nUsado    := 0
Local nX
Local nPosTp

Local aNoFields := {"CNQ_NUMMED","CNQ_CONTRA","CNQ_NUMPLA","CNQ_FILIAL"}
Local aHeadBck  := aClone(aHeader)
Local aColsBck  := aClone(aCols)

Local cQuery    := ""
Local cSeek     := ""
Local cWhile    := ""

aHeadDcs := {}
aDescs   := {}

If nUsado == 0
	If nOpc != 3 .And. nOpc != 9 .And. nOpc!=12
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Carrega descontos na edicao                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды

		cQuery := "SELECT CNQ.* "
		cQuery += "  FROM "+RetSQLName("CNQ")+" CNQ "
		cQuery += " WHERE CNQ.CNQ_FILIAL = '"+xFilial("CNQ")+"'"
		cQuery += "   AND CNQ.CNQ_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += "   AND CNQ.D_E_L_E_T_ = '' "
		cQuery += " ORDER BY CNQ.CNQ_TPDESC"

		cSeek  := xFilial("CNQ") + CND->CND_NUMMED
		cWhile := "CNQ_FILIAL+CNQ_NUMMED"

		FillGetDados(nOpc,"CNQ",1,cSeek,{|| &cWhile},{|| .T. },aNoFields,,,,,,aHeadDcs,aDescs)
	Else
		FillGetDados(nOpc,"CNQ",1,,,,aNoFields,,,,,.T.,aHeadDcs,aDescs)
	EndIf

	nUsado := len(aHeadDcs)

	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Carrega item vazio                          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	If len(aDescs) == 0
		aAdd(aDescs,Array(nUsado+1))
		For nX := 1 To nUsado
			aDescs[1, nX] := CriaVar(aHeadDcs[nX, 2])
		Next nX
		aDescs[1,nUsado+1] := .F.
	EndIf
EndIf

aHeader  := aClone(aHeadBck)
aCols    := aClone(aColsBck)

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130Grv  Ё Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Grava Medicao                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130Grv(nExp01,lExp03,cExp04,cExp04)                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ  nExp01 - Opcao Atual                                      Ё╠╠
╠╠Ё          Ё  lExp02 - Medicao Eventual                                 Ё╠╠
╠╠Ё          Ё  cExp03 - Competencia                                      Ё╠╠
╠╠Ё          Ё  cExp04 - ncSX8                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                            Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                          Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130Grv(nOpc,lMedeve,cCompet,nSaveSX8,aDescs,aHeadDcs,aMultas,aHeader,aCols,nTipo,aRatGCT,nServico)

Local nPosIt      := aScan(aHeader,{|x| x[2] == "CNE_ITEM  "})
Local nLoop       := 0
Local nCntFor     := 0
Local nCntFor2    := 0
Local nX          := 0
Local nY		  := 0
Local nA          := 0
Local nUsado      := 0

Local cFilCod     := ""
Local cCronog     := ""
Local cQuery      := ""

Local lAlc        := .T.
Local lRet        := .T.
Local aListEnce   := {}
Local aCampos			:={}
Local nPosiItem   := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_ITEM"})

Default aRatGCT	:= {}
Default nServico:= 1

nTotMultas	:= Iif(Type("nTotMultas")=='N',nTotMultas,0)
nTotBoni	:= Iif(Type("nTotBoni")=='N',nTotBoni,0)

nSaveSX8 	 :=  GetSX8Len()

If !lAuto
	aCols := oGetDados:aCols
Endif

If ExistBlock("MD130GRV")
	ExecBlock("MD130GRV",.F.,.F.,{nOpc})
EndIf

//-Trata aRatGCT para remover linhas em branco.
For nX := Len(aRatGCT) To 1 Step -1
	For nY := Len(aRatGCT[nX,5]) To 1 Step -1
		If aRatGCT[nX,5,nY,2] == 0 .And. Empty(aRatGCT[nX,5,nY,3])
			ADel(aRatGCT[nX,5],nY)
			aSize(aRatGCT[nx,5],Len(aRatGCT[nX,5]) - 1)
		EndIf
	Next nY
Next nX

Do Case
	Case nOpc == 3 .OR. nOpc == 9 .Or. nOpc=12//Inclusao
		Begin Transaction
		dbSelectArea("CND")
		cFilCod := xFilial("CND")

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava cabecalho da medicao                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		Reclock("CND",.T.)
			For nCntFor := 1 To FCount()
				If (FieldName(nCntFor)!="CND_FILIAL")
					If (FieldName(nCntFor)!="CND_RETCAC" )
						FieldPut(nCntFor,M->&(FieldName(nCntFor)))
					EndIf
				Else
					CND->CND_FILIAL := cFilCod
				EndIf
			Next nCntFor
			CND->CND_VLPREV:= M->CND_VLPREV
			CND->CND_VLTOT := M->CND_VLTOT
			CND->CND_DESCME:= M->CND_DESCME
			CND->CND_TOTADT:= M->CND_TOTADT
			CND->CND_VLMULT:= nTotMultas
			CND->CND_VLBONI:= nTotBoni
			CND->CND_REVGER := CND->CND_REVISA

			//Filial do contrato
			CND->CND_FILCTR	 := cFilCTR

			//Define situaГЦo em Aberto/Forn. Aberto.
			CND->CND_SITUAC  := Iif(nTipo == 1,"A","FA")

	        //Numero da Parcela
			If !Empty(M->CND_PARCEL)
				CND->CND_PARCEL	 := M->CND_PARCEL
			EndIf

			//Autorizacao de Fornecimento
	       CND->CND_AUTFRN := If(nTipo == 1,"1","2")


			//MediГЦo de Servicos
	       CND->CND_SERVIC := M->CND_SERVIC


			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Preenche o valor retido na medicao             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			CND->CND_RETCAC:= If(nCaucVlr!=Nil,nCaucVlr,0)

		MsUnlock()

		CND->(FKCommit())

		//зддддддддддддддддддддддддддд©
		//Ё Executa lancamento do PCO Ё
		//юддддддддддддддддддддддддддды
		PcoDetLan("000355","01","CNTA120")

		dbSelectArea("CNE")
		nUsado := Len(aHeader)
		cFilCod := xFilial("CNE")

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava itens da medicao                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		For nCntFor := 1 To Len(aCols)
			If ( !aCols[nCntFor][len(aCols[nCntFor])] )
				dbSelectArea("CNE")
				Reclock("CNE",.T.)
					For nCntFor2 := 1 To nUsado
						If ( aHeader[nCntFor2][10] != "V" ) .And. aHeader[nCntFor2,2] # "CNE_PEDIDO"
							FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2])
							If Alltrim(aHeader[nCntFor2,2]) == "CNE_VLTOT"
								CNE->CNE_VLTOT := A410Arred(aCols[nCntFor][nCntFor2],If(cEspCtr=="1","C7_TOTAL","C6_VALOR"))
							EndIf
						EndIf
					Next nCntFor2

					CNE->CNE_FILIAL := cFilCod
					CNE->CNE_NUMERO := CND->CND_NUMERO
					CNE->CNE_NUMMED := CND->CND_NUMMED
					CNE->CNE_REVISA := CND->CND_REVISA
					CNE->CNE_CONTRA := CND->CND_CONTRA

					If CNE->(Columnpos('CNE_VLLIQD')) > 0
						If CNE->CNE_EXCEDE == '1'
							CNE->CNE_VLLIQD := CNE->CNE_VLTOT
						Else
							CNE->CNE_VLLIQD := CN130Liq(cEspCtr, CNE->CNE_VLTOT, CNE->CNE_VLDESC, CND->CND_VLTOT, nTotMultas, nTotBoni, CND->CND_DESCME, CND->CND_RETCAC, CND->CND_TOTADT)
						EndIf
					EndIf

				MsUnlock()

				//зддддддддддддддддддддддддддд©
				//Ё Executa lancamento do PCO Ё
				//юддддддддддддддддддддддддддды
				PcoDetLan("000355","02","CNTA120")
			EndIf
		Next

		CN130GrvDesc(aDescs,aHeadDcs,nOpc)

		If Len(aRatGCT) > 0
			GravaCNZ(aRatGCT,iif(!lAuto,oGetDados,),nOpc,,,,aHeader,aCols,,,cFilCTR)
		Endif

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Efetua a gravacao das multas / bonificacoes associadas Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nLoop := 1 to Len( aMultas )
			RecLock( "CNR", .T. )
				CNR->CNR_FILIAL := xFilial( "CNR" )
				CNR->CNR_NUMMED := CND->CND_NUMMED
				CNR->CNR_CONTRA := CND->CND_CONTRA
				CNR->CNR_TIPO   := aMultas[ nLoop, 1 ]
				CNR->CNR_DESCRI := aMultas[ nLoop, 2 ]
				CNR->CNR_VALOR  := aMultas[ nLoop, 3 ]
				CNR->CNR_MODO   := aMultas[ nLoop, 4 ]
				CNR->CNR_FLGPED := aMultas[ nLoop, 5 ]
				CNR->CNR_CODIGO := aMultas[ nLoop, 6 ]
			CNR->( MsUnlock() )
		Next nLoop

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Processa os Adiantamentos                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If Len(aAdiants) > 0
			CnGrvAdia(aAdiants, nOpc)
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra itens gravados no banco                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
      	While ( GetSX8Len() > nSaveSX8 )
		   ConfirmSX8()
		EndDo

		End Transaction

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Valida controle de alcada                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		If (GetNewPar("MV_CNMDALC","N") == "S" )
			If Len(aRatGCT) > 0
				aRatGCT := {}
			Endif
			aAdd(aCampos, {CND->CND_NUMMED,"MD",CND->CND_VLTOT,"","",CND->CND_APROV,"",CND->CND_MOEDA,0,CND->CND_DTINIC})
			lAlc := GCTAlcEnt(,,1,"MD",CND->CND_NUMMED,CND->CND_REVISA,aCampos)
			If !lAlc
			RecLock("CND",.F.)
				CND->CND_ALCAPR := Iif(lAlc,"L",IIF(CNL->CNL_ALCMED == "3","L","B"))
				CND->CND_SITUAC := Iif(lAlc,CND->CND_SITUAC,IIF(CNL->CNL_ALCMED == "3","L","B"))
			MsUnlock("CND")
			EndIf
		EndIf

		If nTipo == 2
			//зддддддддддддддддддддддддд©
			//Ё Gera pedido de compra   Ё
			//юддддддддддддддддддддддддды
			Processa( {|| CN120MedEnc( CND->( RECNO() ),.T.,.T. ) } )
		EndIf
	Case nOpc == 4 .Or. nOpc == 13 //Alteracao ou Excedente pos encerramento
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Exclui documento de alcada                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		If (GetNewPar("MV_CNMDALC","N") == "S" )
       	aAdd(aCampos,{CND->CND_NUMMED,"MD",CND->CND_VLTOT,,,CND->CND_APROV,,CND->CND_MOEDA,,CND->CND_DTINIC})
			GCTAlcEnt(,,3,"MD",CND->CND_NUMMED,CND->CND_REVISA,aCampos)
		End

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra itens gravados no banco                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		cQuery := "SELECT CNE.CNE_ITEM, CNE.CNE_PEDIDO, CNE.R_E_C_N_O_ as RECNO "
		cQuery += "  FROM "+RetSQLName("CNE")+" CNE "
		cQuery += " WHERE CNE.CNE_FILIAL = '"+xFilial("CNE")+"'"
		cQuery += "   AND CNE.CNE_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += "   AND CNE.CNE_REVISA = '"+CND->CND_REVISA+"'"
		cQuery += "   AND CNE.D_E_L_E_T_ = ''"

		cQuery := ChangeQuery(cQuery)
		dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCNE", .f., .t. )

		cFilCod := xFilial("CND")
		dbSelectArea("CND")
		dbSetorder(1)
		dbSeek(cFilCod+CND->CND_CONTRA+CND->CND_REVISA+CND->CND_NUMERO+CND->CND_NUMMED)

		If nTipo == 2
			//здддддддддддддддддддддддддддд©
			//Ё Estorna pedido de compra   Ё
			//юдддддддддддддддддддддддддддды
			If !Empty(CND->CND_PEDIDO)
				Processa( {|| lRet := CN120MedEst( CND->( RECNO() ) , 2 , .F.,.T. ) } )
			EndIf
	    EndIf

		If lRet
			Begin Transaction
			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Altera cabecalho                               Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			Reclock("CND",.F.)
				For nCntFor := 1 To FCount()
					If (FieldName(nCntFor)!="CND_FILIAL")
						If (FieldName(nCntFor)!="CND_RETCAC" )
							FieldPut(nCntFor,M->&(FieldName(nCntFor)))
						EndIf
					Else
						CND->CND_FILIAL := cFilCod
					EndIf
				Next nCntFor
				CND->CND_VLPREV:= M->CND_VLPREV
				CND->CND_VLTOT := M->CND_VLTOT
				CND->CND_DESCME:= M->CND_DESCME
				CND->CND_TOTADT:= M->CND_TOTADT
			   	//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Preenche o valor retido na medicao             Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				CND->CND_RETCAC:= If(nCaucVlr!=Nil,nCaucVlr,0)
			MsUnlock()

			//зддддддддддддддддддддддддддд©
			//Ё Executa lancamento do PCO Ё
			//юддддддддддддддддддддддддддды
			PcoDetLan("000355","01","CNTA120")

			dbSelectArea("CNE")
			nUsado := Len(aHeader)
			cFilCod := xFilial("CNE")

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Altera itens                                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			While !TRBCNE->(Eof())
				dbSelectArea("CNE")
				CNE->( dbGoTo(TRBCNE->RECNO) )

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁGrava itens ja encerrados em um vetor para que nao     Ё
				//Ёsejam atualizados quando da utilizacao da rotina de    Ё
				//Ёexcedente.                                             Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If nOpc == 13 .And. !Empty(TRBCNE->CNE_PEDIDO) .And. !Empty(CND->CND_DTFIM)
					aAdd(aListEnce,TRBCNE->CNE_ITEM)
				Else
					//зддддддддддддддддддддддддддд©
					//Ё Exclui lancamento do PCO  Ё
					//юддддддддддддддддддддддддддды
					PcoDetLan("000355","02","CNTA120",.T.)

					RecLock("CNE",.F.)
					dbDelete()
					MsUnlock()
				EndIf
				TRBCNE->(dbSkip())
			EndDo

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava itens da medicao                         Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			For nCntFor := 1 To Len(aCols)
				If ( !aCols[nCntFor][nUsado+1] ) .And. aScan(aListEnce,aCols[nCntFor,nPosiItem]) == 0
					dbSelectArea("CNE")

					Reclock("CNE",.T.)
						For nCntFor2 := 1 To nUsado
							If ( aHeader[nCntFor2][10] != "V" ) .And. aHeader[nCntFor2,2] # "CNE_PEDIDO"
								FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2])
							EndIf
						Next nCntFor2
						CNE->CNE_FILIAL := cFilCod
						CNE->CNE_NUMERO := CND->CND_NUMERO
						CNE->CNE_NUMMED := CND->CND_NUMMED
						CNE->CNE_REVISA := CND->CND_REVISA
						CNE->CNE_CONTRA := CND->CND_CONTRA

						If CNE->(Columnpos('CNE_VLLIQD')) > 0
							If CNE->CNE_EXCEDE == '1'
								CNE->CNE_VLLIQD := CNE->CNE_VLTOT
							Else
								CNE->CNE_VLLIQD := CN130Liq(cEspCtr, CNE->CNE_VLTOT, CNE->CNE_VLDESC, CND->CND_VLTOT, nTotMultas, nTotBoni, CND->CND_DESCME, CND->CND_RETCAC, CND->CND_TOTADT)
							EndIf
						EndIf
					MsUnlock()

					//зддддддддддддддддддддддддддд©
					//Ё Executa lancamento do PCO Ё
					//юддддддддддддддддддддддддддды
					PcoDetLan("000355","02","CNTA120")
				EndIf
			Next

			CN130GrvDesc(aDescs,aHeadDcs,nOpc)
			GravaCNZ(aRatGCT,iif(!lAuto,oGetDados,),nOpc,,,,aHeader,aCols,,,cFilCTR)

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Filtra multas / bonificacoes                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды

			cAliasCNR := GetNextAlias()

			cQuery := "SELECT CNR.R_E_C_N_O_ RECNO "
			cQuery += "  ,CNR.CNR_CONTRA "
			cQuery += "  FROM "+RetSQLName("CNR")+" CNR "
			cQuery += " WHERE CNR.CNR_FILIAL = '"+xFilial("CNR")+"'"
			cQuery += "   AND CNR.CNR_NUMMED = '"+CND->CND_NUMMED+"'"
			cQuery += "   AND CNR.D_E_L_E_T_ = ''"

			cQuery := ChangeQuery(cQuery)
			dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), cAliasCNR, .f., .t. )

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Exclui multas / bonificacoes                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды

			While !( cAliasCNR )->(Eof())
				CNR->( dbGoto( ( cAliasCNR )->RECNO ) )
				If !Empty(( cAliasCNR )->CNR_CONTRA) .And. (( cAliasCNR )->CNR_CONTRA) <> CND->CND_CONTRA
					( cAliasCNR )->( dbSkip() )
					Loop
				EndIf
				RecLock("CNR", .F.)
				CNR->( dbDelete() )
				CNR->( MsUnlock() )
				( cAliasCNR )->(dbSkip())
			EndDo

			CNR->( FKCommit() )

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Efetua a gravacao das multas / bonificacoes associadas Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			For nLoop := 1 to Len( aMultas )
				RecLock( "CNR", .T. )
					CNR->CNR_FILIAL := xFilial( "CNR" )
					CNR->CNR_NUMMED := CND->CND_NUMMED
					CNR->CNR_CONTRA := CND->CND_CONTRA
					CNR->CNR_TIPO   := aMultas[ nLoop, 1 ]
					CNR->CNR_DESCRI := aMultas[ nLoop, 2 ]
					CNR->CNR_VALOR  := aMultas[ nLoop, 3 ]
					CNR->CNR_MODO   := aMultas[ nLoop, 4 ]
					CNR->CNR_FLGPED := aMultas[ nLoop, 5 ]
					CNR->CNR_CODIGO := aMultas[ nLoop, 6 ]
					CNR->( MsUnlock() )
			Next nLoop

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Processa os Adiantamentos                              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If Len(aAdiants) > 0
				CnGrvAdia(aAdiants, nOpc)
			EndIf

			End Transaction

			//здддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Valida controle de alcada                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддды
			If (GetNewPar("MV_CNMDALC","N") == "S" )
				lAlc := MaAlcDoc({CND->CND_NUMMED,"MD",CND->CND_VLTOT,,,CND->CND_APROV,,CND->CND_MOEDA,,CND->CND_DTINIC},,1)

				RecLock("CND",.F.)
					CND->CND_ALCAPR := Iif(lAlc,"L",IIF(CNL->CNL_ALCMED == "3","L","B"))
					CND->CND_SITUAC := Iif(lAlc,CND->CND_SITUAC,IIF(CNL->CNL_ALCMED == "3","L","B"))
					MsUnlock("CND")
			EndIf

			If nTipo == 2
				//здддддддддддддддддддддддддддд©
				//Ё Limpa o codigo do pedido   Ё
				//юдддддддддддддддддддддддддддды
				RecLock("CND",.F.)
					CND->CND_PEDIDO := CriaVar("CND_PEDIDO")
				MsUnlock()
				//зддддддддддддддддддддддддд©
				//Ё Gera pedido de compra   Ё
				//юддддддддддддддддддддддддды
				Processa( {|| CN120MedEnc( CND->( RECNO() ),.T.,.T. ) } )
			EndIf


		Else
			Aviso( "CNTA130", STR0083, { STR0046 }, 2 )//"NЦo foi possМvel estornar o pedido de compra, as alteraГУes nЦo foram salvas"
		EndIf

		TRBCNE->(dbCloseArea())//Fecha arquivo de trabalho
	Case nOpc == 5 //Exclusao

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Exclui documento de alcada                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		If (GetNewPar("MV_CNMDALC","N") == "S" )
       		aAdd(aCampos,{CND->CND_NUMMED,"MD",CND->CND_VLTOT,,,CND->CND_APROV,,CND->CND_MOEDA,,CND->CND_DTINIC})
			GCTAlcEnt(,,3,"MD",CND->CND_NUMMED,CND->CND_REVISA,aCampos)
		End

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra itens gravados no banco                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		cQuery := "SELECT CNE.CNE_ITEM,CNE.R_E_C_N_O_ as RECNO "
		cQuery += "  FROM "+RetSQLName("CNE")+" CNE "
		cQuery += " WHERE CNE.CNE_FILIAL = '"+xFilial("CNE")+"'"
		cQuery += "   AND CNE.CNE_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += "   AND CNE.CNE_REVISA = '"+CND->CND_REVISA+"'"
		cQuery += "   AND CNE.D_E_L_E_T_ = ''"

		cQuery := ChangeQuery(cQuery)
		dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCNE", .f., .t. )

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra planilhas			                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		cQuery := "SELECT CXN.CXN_NUMPLA,CXN.R_E_C_N_O_ as RECNO "
		cQuery += "  FROM "+RetSQLName("CXN")+" CXN "
		cQuery += " WHERE CXN.CXN_FILIAL = '"+xFilial("CXN")+"'"
		cQuery += "   AND CXN.CXN_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += "   AND CXN.D_E_L_E_T_ = ''"
		cQuery := ChangeQuery(cQuery)
		dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCXN", .f., .t. )

		dbSelectArea("CNQ")
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Filtra descontos                               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		cQuery := "SELECT CNQ.R_E_C_N_O_ as RECNO "
		cQuery += "  FROM "+RetSQLName("CNQ")+" CNQ "
		cQuery += " WHERE CNQ.CNQ_FILIAL = '"+xFilial("CNQ")+"'"
		cQuery += "   AND CNQ.CNQ_NUMMED = '"+CND->CND_NUMMED+"'"
		cQuery += "   AND CNQ.D_E_L_E_T_ = ''"

		cQuery := ChangeQuery(cQuery)
		dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCNQ", .f., .t. )

		If nTipo == 2
			//здддддддддддддддддддддддддддд©
			//Ё Estorna pedido de compra   Ё
			//юдддддддддддддддддддддддддддды
			If !Empty(CND->CND_PEDIDO)
				Processa( {|| lRet := CN120MedEst( CND->( RECNO() ) , 2 , .F., .T. ) } )
			EndIf
		EndIf


		If lRet
			Begin Transaction

				//-- Exclui banco de conhecimento vinculado (AC9)
				MsDocument("CND", CND->(Recno()), 2,, 3)

				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Exclui descontos                               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("CNQ")
				While !TRBCNQ->(Eof())
					dbGoto(TRBCNQ->RECNO)
					RecLock("CNQ", .F.)
						dbDelete()
					MsUnlock()
					TRBCNQ->(dbSkip())
				EndDo


				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Filtra multas / bonificacoes                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды

				cAliasCNR := GetNextAlias()

				cQuery := "SELECT CNR.R_E_C_N_O_ RECNO "
				cQuery += "  ,CNR.CNR_CONTRA "
				cQuery += "  FROM "+RetSQLName("CNR")+" CNR "
				cQuery += " WHERE CNR.CNR_FILIAL = '"+xFilial("CNR")+"'"
				cQuery += "   AND CNR.CNR_NUMMED = '"+CND->CND_NUMMED+"'"
				cQuery += "   AND CNR.D_E_L_E_T_ = ''"

				cQuery := ChangeQuery(cQuery)
				dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), cAliasCNR, .f., .t. )

				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Exclui multas / bonificacoes                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды

				While !( cAliasCNR )->(Eof())
					CNR->( dbGoto( ( cAliasCNR )->RECNO ) )
					If !Empty(( cAliasCNR )->CNR_CONTRA) .And. (( cAliasCNR )->CNR_CONTRA) <> CND->CND_CONTRA
						( cAliasCNR )->( dbSkip() )
						Loop
					EndIf
					RecLock("CNR", .F.)
					CNR->( dbDelete() )
					CNR->( MsUnlock() )
					( cAliasCNR )->(dbSkip())
				EndDo

				CNR->( FkCommit() )

				//- Exclui Planilhas
				dbSelectArea("CXN")
				While !TRBCXN->(Eof())
					dbGoto(TRBCXN->RECNO)
					RecLock("CXN", .F.)
						dbDelete()
					MsUnlock()
					TRBCXN->(dbSkip())
				EndDo

				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Exclui itens                                   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("CNE")
				While !TRBCNE->(Eof())
					dbGoto(TRBCNE->RECNO)
					//зддддддддддддддддддддддддддд©
					//Ё Executa lancamento do PCO Ё
					//юддддддддддддддддддддддддддды
					PcoDetLan("000355","02","CNTA120",.T.)
					RecLock("CNE", .F.)
						dbDelete()
					MsUnlock()
					TRBCNE->(dbSkip())
				EndDo

				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza os Descontos                          Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				CnGrvAdia(,nOpc)

				GravaCNZ(aRatGCT,iif(!lAuto,oGetDados,),nOpc,,,,aHeader,aCols,,,cFilCTR)

				//зддддддддддддддддддддддддддд©
				//Ё Executa lancamento do PCO Ё
				//юддддддддддддддддддддддддддды
				PcoDetLan("000355","01","CNTA120",.T.)

				RecLock("CND", .F.)
					dbDelete()
				MsUnLock()
			End Transaction
		Else
			Aviso( "CNTA130", STR0083, { STR0046 }, 2 )//"NЦo foi possМvel estornar o pedido de compra, as alteraГУes nЦo foram salvas"
		EndIf

		//Finaliza arquivos de trabalho
		TRBCNE->(dbCloseArea())
		TRBCNQ->(dbCloseArea())
		TRBCXN->(dbCloseArea())
		aListBox:={}
End Case

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada executado apСs a gravaГЦo da MediГЦo Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("CN130PGRV")
	ExecBlock("CN130PGRV",.F.,.F.,{nOpc})
EndIf
Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130TotMedЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Atualiza total da medicao                                   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130TotMed(nExp01)                                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nExp01 - Informa campo atual do usuario 1 - quantidade      Ё╠╠
╠╠Ё          Ё                                         2 - percentual      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                             Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130TotMed(nField,lDelete)
Local nPosVu:= 0
Local nPosDc:= 0
Local nPosDv:= 0
Local nPosQt:= 0
Local nPosQl:= 0
Local nPosQo:= 0
Local nPosVo:= 0
Local nPosTo:= 0
Local nDcTot:= TamSx3("CND_VLTOT")[2]
Local nx    := 0
Local ny    := If(lAuto,n,oGetDados:nAt)
Local nTotal:= 0
Local nTamVlTot := TamSx3("CNE_VLTOT")[2]
Local lFixo := .T.

Local cReadVar := ""
Local xConteudo:= ""
Local nArredMed:= 0

Default lDelete := .F.

If !(nField==5)
	 cReadVar := ReadVar()
	 xConteudo:= &(cReadVar)
Else
	If lDelete
		aCols[ny,Len(aCols[ny])]:=!aCols[ny,Len(aCols[ny])]
	EndIf
EndIf


lServico := Iif(Type("lServico")=='L',lServico,.F.)

If !lAuto
	aCols   := oGetDados:aCols
	aHeader := oGetDados:aHeader
EndIf

nPosVu:= aScan(aHeader,{|x| x[2] == "CNE_VLUNIT"})
nPosDc:= aScan(aHeader,{|x| x[2] == "CNE_PDESC "})
nPosDv:= aScan(aHeader,{|x| x[2] == "CNE_VLDESC"})
nPosQt:= aScan(aHeader,{|x| x[2] == "CNE_QUANT "})
nPosQl:= aScan(aHeader,{|x| x[2] == "CNE_QTDSOL"})
nPosTo:= aScan(aHeader,{|x| x[2] == "CNE_VLTOT "})

M->CND_VLTOT := 0

If lServico
	nPosVo:= aScan(aHeader,{|x| x[2] == "CNE_VUNORI"})
	nPosQo:= aScan(aHeader,{|x| x[2] == "CNE_QTDORI"})
EndIf

For nX:=1 to len(aCols)
	If !aCols[nx,len(aHeader)+1]
		If nx==ny
			Do Case
				Case nField == 1//Campo quantidade
					If cEspCtr == "2"
						aCols[nX,nPosTo] := (M->CNE_QUANT * aCols[nx,nPosVu])
						FtDescItem(0,@aCols[nX,nPosVu],@xConteudo,@aCols[nX,nPosTo],@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						M->CND_VLTOT += Round(aCols[nX,nPosTo],nTamVlTot)
					Else
						M->CND_VLTOT += Round(((M->CNE_QUANT* aCols[nx,nPosVu])-NoRound((M->CNE_QUANT*aCols[nx,nPosVu])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2])),nTamVlTot)
					EndIf

				Case nField == 2//Campo percentual
					If cEspCtr == "2"
						aCols[nX,nPosTo] := ((M->CNE_PERC*aCols[nx,nPosQl])/100) * aCols[nx,nPosVu]
						If !lServico
							FtDescItem(0,@aCols[nX,nPosVu],(aCols[nX,nPosQl]*@xConteudo)/100,@aCols[nX,nPosTo],@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						Else
							FtDescItem(0,@aCols[nX,nPosVo],(aCols[nX,nPosQl]*@xConteudo)/100,@aCols[nX,nPosTo],@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						EndIf
						M->CND_VLTOT += aCols[nX,nPosTo]
					Else
						If !lServico
							M->CND_VLTOT += Round((((M->CNE_PERC*aCols[nx,nPosQl])/100)*aCols[nx,nPosVu])-Iif((((M->CNE_PERC*aCols[nx,nPosQl])/100)*aCols[nx,nPosVu])>0,NoRound((((aCols[nx,nPosQl]*M->CNE_PERC)/100)*aCols[nx,nPosVu])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),0),nTamVlTot)
						Else
							M->CND_VLTOT += Round((((M->CNE_PERC*aCols[nx,nPosQl])/100)*aCols[nx,nPosVo])-Iif((((M->CNE_PERC*aCols[nx,nPosQl])/100)*aCols[nx,nPosVo])>0,NoRound((((aCols[nx,nPosQl]*M->CNE_PERC)/100)*aCols[nx,nPosVo])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),0),nTamVlTot)
						EndIf
					EndIf

				Case nField == 3//Campo vlunit
					If cEspCtr == "2"
						If !lServico
							FtDescItem(0,@xConteudo,@aCols[nX,nPosQt],@nTotal,@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						Else
							FtDescItem(0,@aCols[nx,nPosVo],(@xConteudo/@aCols[nX,nPosVo]),@nTotal,@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						EndIf

						M->CND_VLTOT += nTotal
					Else
						M->CND_VLTOT += Round((aCols[nx,nPosQt]*M->CNE_VLUNIT)-NoRound((M->CNE_VLUNIT*aCols[nx,nPosQt])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),nTamVlTot)
					EndIf

				Case nField == 4//Desconto
					If cEspCtr == "2"
						FtDescItem(0,@aCols[nX,nPosVu],@aCols[nX,nPosQt],@nTotal,@xConteudo,@aCols[nX,nPosDv],0,1,,)

						M->CND_VLTOT += nTotal
					Else
						M->CND_VLTOT += Round((aCols[nx,nPosQt]*aCols[nx,nPosVu])-NoRound((aCols[nx,nPosqt]*aCols[nx,nPosVu])*(M->CNE_PDESC/100),TamSx3("CNE_VLDESC")[2]),nTamVlTot)
					EndIf

				Case nField == 5//Excluido
						M->CND_VLTOT += Round((aCols[nx,nPosQt]*aCols[nx,nPosVu])-NoRound((aCols[nx,nPosqt]*aCols[nx,nPosVu])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),nTamVlTot)

				Case nField == 6//Campo vltotal
					If cEspCtr == "2"
						If !lServico
							FtDescItem(0,@aCols[nX,nPosVu],(@xConteudo/@aCols[nX,nPosVu]),@nTotal,@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						Else
							FtDescItem(0,@aCols[nX,nPosVo],(@xConteudo/@aCols[nX,nPosVo]),@nTotal,@aCols[nX,nPosDc],@aCols[nX,nPosDv],0,1,,)
						EndIf
						M->CND_VLTOT += nTotal
					Else
						M->CND_VLTOT += Round(M->CNE_VLTOT-NoRound((M->CNE_VLTOT * aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),nTamVlTot)
					EndIf

			End Case
		Else
			If cEspCtr == "2"
				If !lServico
					FtDescItem(0,@aCols[nx,nPosVu],@aCols[nX,nPosQt],0,@aCols[nX,nPosDc],@nTotal,0,1,,)
				Else
					FtDescItem(0,@aCols[nx,nPosVo],@aCols[nX,nPosQo],0,@aCols[nX,nPosDc],@nTotal,0,1,,)
				EndIf

				M->CND_VLTOT += Round((aCols[nx,nPosQt]*aCols[nx,nPosVu])-NoRound(nTotal,TamSx3("CNE_VLDESC")[2]),nTamVlTot)
			Else
				M->CND_VLTOT += Round((aCols[nx,nPosQt]*aCols[nx,nPosVu])-NoRound((aCols[nx,nPosqt]*aCols[nx,nPosVu])*(aCols[nx,nPosDc]/100),TamSx3("CNE_VLDESC")[2]),nTamVlTot)
			EndIf

		EndIf
	EndIf
Next

// Verifica se contrato e planilha И Fixo
lFixo := Cn300RetSt("FIXO",0,CNA->CNA_NUMERO,,,.F.)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Arredonda valor da mediГЦo de acordo com saldo do contrato Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
// Verifica se o valor da mediГЦo irА ultrapassar o saldo do contrato
If M->CND_VLTOT > CN9->CN9_SALDO .And. lFixo //Se o contrato for Flexivel, nЦo tem saldo... Logo, o saldo do contrato sempre serА zero
	nArredMed := Round(M->CND_VLTOT,2) - CN9->CN9_SALDO
	If nArredMed <= 0.02 // Verifica se a diferenГa И de atИ 2 centavos
		M->CND_VLTOT -= nArredMed
	Endif
Endif

//здддддддддддддддддддддддддддддд©
//Ё Atualiza valor da caucao     Ё
//юдддддддддддддддддддддддддддддды
If nCaucVlr != Nil
	If cEspCtr == "1"
		nCaucVlr:=(((M->CND_VLTOT-nTotMedDsc-nTotMedMlt+nTotMedBnf)*nCaucRet)/100)
	Else
		nCaucVlr:=(((M->CND_VLTOT-nTotMedDsc+nTotMedMlt-nTotMedBnf)*nCaucRet)/100)
	EndIf

	If !lAuto
		oGetCauc:Refresh()
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza valor total da medicao          Ё
//юдддддддддддддддддддддддддддддддддддддддддды
	If cEspCtr == "1"
		nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT - nTotMultas + nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
	Else
		nTotVlMed := M->CND_VLTOT - M->CND_DESCME - M->CND_TOTADT + nTotMultas - nTotBoni - (If(nCaucVlr!=Nil,nCaucVlr,0))
	EndIf

//здддддддддддддддддддддддддддддд©
//Ё Altera componente visual     Ё
//юдддддддддддддддддддддддддддддды
If !lAuto
	oGetTot:Refresh()
	oGetVlMed:Refresh()
EndIf

//Tratamento especifico na exclusao da Medicao de Contrato Flexivel
If (nField==5) .And. lDelete
	aCols[ny,Len(aCols[ny])]:=!aCols[ny,Len(aCols[ny])]
EndIf


Return .T.

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤ao    Ё CN130LinOkЁ Autor Ё Marcelo Custodio      Ё Data Ё 28.11.2005 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida item da planilha                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130LinOK(oExp01)                                            Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ oExp01 - Estrutura da linha                                   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA130                                                       Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130LinOk()

Local lRet       := .T.
Local lRet1      := .T.
Local lAtualiza    := .F.
Local lCN130VTOT := ExistBlock("CN130VTOT")
Local lCN130VLIN := ExistBlock("CN130VLIN")

Local uRet       := .T.

Local aCols      := If(lAuto,aCols,ogetDados:aCols)

Local nX         := 0
Local nCont      := 1
Local nPosQtde   := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_QUANT "})
Local nPosVUni   := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_VLUNIT"})
Local nPosVTot   := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_VLTOT "})
Local nPosTE     := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_TE    "})
Local nPosTS     := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_TS    "})
Local nPosProd   := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_PRODUT"})
Local nPosPedTit := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_PEDTIT"})

Local nPosConta	 := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_CONTA "})
Local nPosItemCt := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_ITEMCT"})
Local nPosCLVL   := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_CLVL  "})
Local nPosCC   	 := aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_CC    "})

Local nTamTot    := TamSX3("CNE_VLTOT")[2]
Local nDif       := 10**(-nTamTot)
Local nDiferenca := 0

//Valida AlteraГЦo de nQtde
If Altera
	M->CNE_QUANT := aCols[N,nPosQtde]
	lRet := CN130VldQtd(.T.)
EndIf

If(lRet)	
	//здддддддддддддддддддддддддд©
	//Ё Valida linha dos itens   Ё
	//юдддддддддддддддддддддддддды
	For nX := 1 to len(aCols)
	
	    If lCN130VTOT
		    uRet := ExecBlock("CN130VTOT",.F.,.F.,{})
			lRet1:=	If(Valtype(uRet)=="L",uRet,.T.)
			//Se nЦo existir o PE: CN130VLIN a validaГЦo padrЦo de total serА executada mesmo que o PE: CN130VTOT
			//esteje com .F. para nЦo validar.
			If !lRet1 .And. !lCN130VLIN
				lRet1:= .T.
			EndIf
		EndIf
	
	    //Valida amarraГЦo contАbil
	    lRet := CtbAmarra(IIF(nPosConta > 0 ,aCols[nX][nPosConta],""),IIF(nPosCC > 0 ,aCols[nX][nPosCC],""),IIF(nPosItemCt > 0 ,aCols[nX][nPosItemCt],""),IIF(nPosCLVL > 0 ,aCols[nX][nPosCLVL],""))
	    If !lRet
	    	Exit
	    EndIf
	
	    If lRet1
			nDiferenca := (Round(aCols[nX][nPosQtde]*aCols[nX][nPosVUni],nTamTot) - Round(aCols[nX][nPosVTot],nTamTot))
			If  nDiferenca <> 0 .And. (nDiferenca < nDif .Or. nDiferenca > nDif )
				Aviso("CNTA130",STR0080,{STR0046})//"Valor total invАlido!"
				lRet := .F.
				Exit
			EndIf
		EndIf
	Next
	
	//Valida a medicao de contrato Flexivel
	If !lFixo
		If !Empty(M->CND_CLIENT)
			//Valida Tipo Saida
			If lRet .And. ((nPosPedTit > 0 .And. aCols[n][nPosPedTit] == "1" .And. Empty(aCols[n][nPosTS])).Or. Empty(aCols[n][nPosProd])  )
				Help (" ",1,"CNTA130TS3")//"Os campos Produto, Quantidade, Valor UnitАrio, Un. Medida ou TES estЦo em Branco.  "
				lRet := .F.
			EndIf
		Else
			//Valida Produto
			If lRet .And. Empty(aCols[n][nPosProd])
				Help (" ",1,"CNTA130TE2")//"Os campos Produto, Quantidade, Valor UnitАrio, Un. Medida ou TES estЦo em Branco.  "
				lRet := .F.
			EndIf
		EndIF
	
	EndIf
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Chama ponto de entrada para validacoes especificas Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRet .And. ExistBlock("CN130VLIN")
		uRet := ExecBlock("CN130VLIN",.F.,.F.,{aCols[n],aHeader})
		lRet :=	If(Valtype(uRet)=="L",uRet,lRet)
	EndIf
	
	//здддддддддддддддддддддддддддддддд©
	//Ё Atualiza componente visual     Ё
	//юдддддддддддддддддддддддддддддддды
	While nCont <= Len(aCols) .And. !lAtualiza
		If aCols[nCont][Len(aHeader)+1]
			CN130TotMed(5)
			lAtualiza := .T.
		EndIf
		nCont++
	End
EndIf

Return lRet


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130TudOkЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida Getdados                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130TudOk()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                            Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.             Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                     Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                          Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130TudOk(aCols)

Local lRet		:= .T.
Local aCols		:= If(lAuto,aCols,ogetDados:aCols)
Local aRatGCT	:= Iif(Type("aAuxRat")=='A',aAuxRat,{})

Local nX
Local nPosQt	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_QUANT "})
Local nPosIt	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_ITEM  "})
Local nPosProd	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_PRODUT"})
Local nPosVUni	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_VLUNIT"})
Local nPosVTot	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_VLTOT "})
Local nPPedTit	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| x[2] == "CNE_PEDTIT"})
Local nCodNe	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| Trim(x[2]) == "CNE_CODNE"})
Local nItenNe	:= aScan(If(lAuto,aHeader,ogetDados:aHeader),{|x| Trim(x[2]) == "CNE_ITEMNE"})
Local nPosTES 	:= 0
Local lZerad  	:= (M->CND_ZERO == "1")
Local lVldPrd 	:= ( GetNewPar("MV_CNVLAMR","N") == "S" )
Local cFilSA5 	:= xFilial("SA5")
Local cFilSB1 	:= xFilial("SB1")
Local cFilSAD 	:= xFilial("SAD")
Local aMedEmp	:= {}
Local lNotaEmp	:= SuperGetMV("MV_NOTAEMP",.F.,.F.)
Local lCnnoped	:= SuperGetMV("MV_CNNOPED",.F.,.T.)

If !lAuto
	aCols := oGetDados:aCols
Endif


If cEspCtr=="2"  //Valida se for contrato do Tipo Vendas
	nPosTES := aScan(IIF(lAuto,aHeader,ogetDados:aHeader),{|x| AllTrim(x[2]) == "CNE_TS"})
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida itens de acordo com a opcao zerada da medicaoЁ
//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nX := 1 to len(aCols)
	If !aCols[nX, Len(aHeader)+ 1]

		If (lZerad .And. aCols[nX][nPosQt]>0)// .Or. (!lZerad .And. aCols[nX][nPosQt]==0)
			Aviso("CNTA130",STR0010+aCols[nX][nPosIt],{STR0046})//"Quantidade invАlida no item "
			lRet := .F.
			Exit
		EndIf

		If lRet .AND. !Cn300RetSt("FIXO",0,M->CND_NUMERO,M->CND_CONTRA,cFilCTR,.F.) .And. !lZerad .And. (aCols[nX][nPosQt] == 0 .Or. aCols[nX][nPosVUni] == 0 .Or. aCols[nX][nPosVTot] == 0)
			Aviso("CNTA130",STR0109+cValToChar(nX))
			lRet := .F.
			Exit
		EndIf

		If lRet .And. !lZerad .And. M->CND_VLTOT == 0
			lRet := .F.
			MsgAlert(STR0008)//"MediГЦo zerada!"
		EndIf

		 //Valida se for contrato do Tipo Vendas e possui TES Vinculado ao produto
		If lRet .And. nPPedTit > 0 .And. aCols[nX,nPPedTit] == '1'
			If nPosTES > 0
				If Empty(aCols[nX][nPosTES]) .AND. aCols[nX][nPosQt] > 0 //Valida se a quantidade do item И maior que 0 para inclusЦo da TES
					If lFixo
						Do Case
						Case INCLUI
							Aviso("CNTA130",STR0088,{STR0046})//Produto nЦo possui TES vinculada. Vincule o Produto a TES atravИs do Cadastro de Produtos."
							lRet := .F.
							Exit
						Case ALTERA
							Aviso("CNTA130",STR0089,{STR0046})//"Produto nЦo possui TES vinculada. Exclui essa mediГЦo, vincule o Produto a TES atravИs do Cadastro de Produtos e inclua essa mediГЦo novamente."
							lRet := .F.
							Exit
						EndCase
					Else
						Help (" ",1,"A410NOTES")
						lRet := .F.
						Exit
					EndIf
				EndIf
			Else
				If cEspCtr=="2"
					SB1->( dbSetOrder( 1 ) )
					dbSelectArea("SB1")
					If MSSeek(xFilial("SB1")+aCols[nX][nPosProd])
					   If Empty(SB1->B1_TS)
						   	Aviso("CNTA130",STR0088,{STR0046})
					   		lRet := .F.
					   		Exit
					   EndIf
					EndIf
				EndIf
			EndIf
		EndIf

		If lRet .And. lVldPrd .And. !Empty(M->CND_FORNEC+M->CND_LJFORN)
			dbSelectArea("SA5")
			dbSetOrder(1)
			dbGoTop()

			//зддддддддддддддддддддддддддддддддддддддд©
			//Ё Valida amarraГЦo Fornecedor X Produto Ё
			//юддддддддддддддддддддддддддддддддддддддды
			lProdFr := dbSeek(cFilSA5+M->CND_FORNEC+M->CND_LJFORN+aCols[nX][nPosProd])
			If !lProdFr
				dbSelectArea("SB1")
				dbSetOrder(1)
				If dbSeek(cFilSB1+aCols[nX][nPosProd])
					dbSelectArea("SAD")
					dbSetOrder(1)
					//здддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Valida amarraГЦo Fornecedor X Grupo de Produto Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддды
					lProdFr := dbSeek(cFilSAD+M->CND_FORNEC+M->CND_LJFORN+SB1->B1_GRUPO)
				EndIf
			EndIf

			If !lProdFr
				Aviso(STR0056,STR0074+AllTrim(aCols[nX][nPosIt])+STR0075,{ STR0046 },2)//"O produto informado no item "###" nЦo estА amarrado ao fornecedor informado. Verifique a amarraГЦo produto X fornecedor."
				lRet := .F.
				Exit
			EndIf
		EndIf

		If lRet
			n := nX
			//здддддддддддддддддддддддддд©
			//Ё Valida lancamento do PCO Ё
			//юдддддддддддддддддддддддддды
			If !(lRet := PcoVldLan('000355','02','CNTA120',/*lUsaLote*/,/*lDeleta*/, .T./*lVldLinGrade*/))
				Exit
			EndIf
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica se o usuario tem permissao. 				 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		If lRet
			If !(lRet := MaAvalPerm(1,{aCols[nX][nPosProd],"CNT120",If(INCLUI,3,If(ALTERA,4,5))}))
				Help(,,1,'SEMPERM')
			EndIf
		EndIf
	EndIf

	If lRet .And. !(Empty(CN9->CN9_CODED)) .And. lNotaEmp .And. (Empty(Acols[nX][nCodNe]) .Or. Empty(Acols[nX][nItenNe]))
		dbSelectArea("CX0")
		dbSetOrder(2)
		If dbseek(xFilial("CX0")+CN9->CN9_CODED+CN9->CN9_NUMPR)
			Help("",1,STR0044,,STR0108,4,1)
			lRet := .F.
			Exit
		EndIf
	EndIf

Next

If cEspCtr=="2" .And. lRet
	dbSelectArea("SE4")
	dbSetOrder(1)
	MsSeek(xFilial("SE4")+M->CND_CONDPG)

	If SE4->E4_TIPO=='9'
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica a Condicao de Pagamento Tipo 9                                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !cn130VldCnd9()
			lRet  := .F.
		EndIf
	EndIf
EndIf

If lRet .And. (!lZerad .Or. !lCnnoped)
	If Empty(M->CND_FORNEC) 
		lRet := !( (M->CND_VLTOT + nTotMultas ) <= M->CND_DESCME )//Valida total do desconto no contrato de venda
	Else 
		lRet := !( M->CND_VLTOT <= ( M->CND_DESCME + nTotMultas ) )//Valida total do desconto no contrato de compra
	EndIf

	If !lRet			
		Help("",1,"CN120VLZERO",,STR0052,1 , 0, NIL, NIL, NIL, NIL, NIL, {})//O valor resultante de descontos nao pode ultrapassar o total de medicoes !"
	EndIf
Endif

//здддддддддддддддддддддддддд©
//Ё Valida lancamento do PCO Ё
//юдддддддддддддддддддддддддды
If lRet
	lRet := PcoVldLan('000355','01','CNTA120',/*lUsaLote*/,/*lDeleta*/, .F./*lVldLinGrade*/)
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica a revisao atual do contrato se a situacao e revisao            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet
	CN9->(dbSeek(xFilial("CN9")+M->CND_CONTRA+"ZZZ",.T.))
	CN9->(dbSkip(-1))

	If CN9->CN9_SITUAC==DEF_SREVS
		lRet := .F.
		Help( " ", 1, "CNTA120_02" )//"Apenas contratos em vigЙncia podem ser medidos"
	EndIf

EndIf
//зддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa ponto de entrada para validacao Ё
//юддддддддддддддддддддддддддддддддддддддддды
If lRet .And. ExistBlock("CN130TOK")
	uRet := ExecBlock("CN130TOK",.F.,.F.,{aRatGCT})

	If valtype(uRet) == "L"
		lRet := uRet
	EndIf
EndIf

If lRet .and. lNotaEmp
	CN130MdEmp(aCols,@aMedEmp)
	lRet := ShowDivNe(aMedEmp,.F.)
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VldMedЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Manutencao de Medicoes                                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130VldMed(cExp01,cExp02,cExp03,cExp04,lExp05,aExp06)      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ cExp01 - Codigo do Contrato                                 Ё╠╠
╠╠Ё          Ё cExp02 - Codigo da Revisao                                  Ё╠╠
╠╠Ё          Ё cExp03 - Codigo da Planilha                                 Ё╠╠
╠╠Ё          Ё cExp03 - Competencia                                        Ё╠╠
╠╠Ё          Ё lExp04 - Medicao Eventual                                   Ё╠╠
╠╠Ё          Ё aExp05 - Multas                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                             Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.              Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                      Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                           Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldMed(cContra,cRevisa,cPlan,cCompet,lMedeve,aMultas)
Local nX
Local lRet       := .T.
Local lZerMed    := (M->CND_ZERO == "1")//Medicao Zerada
Local lCn130VAD  := ExistBlock("CN130VAD")
Local lRetVAD    := .T.

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada na validaГЦo no Adiantamento do Contrato Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lCn130VAD
	lRetVAD := If(Valtype(lRetVAD:=ExecBlock("CN130VAD",.F.,.F.))=="L",lRetVAD,.T.)
EndIf

If lRet .And. lRetVAD .And. (M->CND_VLTOT < (M->CND_TOTADT))
	lRet := .F.
	Aviso (STR0056, STR0072, { STR0058 }, 2 ) // "Atencao!", "Os Adiantamentos nЦo podem ultrapassar o total das mediГУes/entregas", { "Ok" }
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida se a medicao nao possui itens preenchidosЁ
//Ё quando nao for zerada                           Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
If lRet .And. lZerMed .And. M->CND_VLTOT > 0
	lRet := .F.
	MsgAlert(STR0008)//"MediГЦo zerada!"
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VldQtdЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida quantidade informada na medicao                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130VldQtd()                                               Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                             Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.              Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                      Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                           Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldQtd(lLinhaok)

Local nx       := If(lAuto,n,oGetDados:nAt)
Local nPosQSol := aScan(aHeader,{|x| x[2] == "CNE_QTDSOL"})
Local nPosQMed := aScan(aHeader,{|x| x[2] == "CNE_QTAMED"})
Local nPosMExc := aScan(aHeader,{|x| x[2] == "CNE_EXCEDE"})
Local nTotal   := 0
Local nPosItem := aScan(aHeader,{|x| Alltrim(x[2]) == "CNE_ITEM"})
Local lArrefat := (SuperGetMv("MV_ARREFAT",.F.,"S")== "S")
Local lRet := .T.
Default lLinhaok := .F.

If M->CND_ZERO == "1"
	//-- Verifica se a quantidade e igual a zero quando a medicao for zerada
	If !(lRet := (M->CNE_QUANT == 0))
		Help(" ",1,"CN130VLQTD",,STR0115,1,1) //- 'MediГУes zeradas nЦo possuem quantidade.'
	EndIf
ElseIf nPosQMed > 0 	//-- Verifica se a quantidade e menor que o saldo disponivel
	lRet := aCols[nx,nPosMExc] == '1' 
	If !lRet
		If Altera
			If CNB->(MsSeek(xFilial("CNB",cFilCTR)+M->CND_CONTRA+M->CND_REVISA+M->CND_NUMERO+aCols[nx,nPositem]) )
				If  aCols[nx,nPosQMed] > CNB->CNB_SLDMED
					aCols[nx,nPosQMed] := CNB->CNB_SLDMED
				EndIf
			EndIf
		EndIf
	
		lRet := (M->CNE_QUANT <= aCols[nx,nPosQMed])
	EndIf
	
	If !lRet
  		nTotal := M->CNE_QUANT- aCols[nx,nPosQMed]
		If nTotal <= (1/10 ^ TamSX3(aHeader[nPosQMed,2])[2]) .And. !lArrefat
		   lRet := .T.
		Else
			Help(" ",1,"CN130VLQTD",,STR0116,1,1) //- 'Quantidade informada ultrapassa o saldo disponivel.'
		EndIf
	EndIf
EndIf

If lRet .And. !lLinhaok
	CN130TotMed(1)
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VlTot Ё Autor Ё Felipe Bittar         Ё Data Ё13.01.2009Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida valor total informado na medicao                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130VlTot()                                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                             Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.              Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                      Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                           Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VlTot()

Local lRet     := .T.
Local lArrefat := (SuperGetMv("MV_ARREFAT",.F.,"S")== "S")
Local nx   := If(lAuto,n,oGetDados:nAt)
Local nPosQMed := aScan(aHeader,{|x| x[2] == "CNE_QTAMED"})
Local nPosVlUn := aScan(aHeader,{|x| x[2] == "CNE_VLUNIT"})
Local nPosQOri := aScan(aHeader,{|x| x[2] == "CNE_QTDORI"})
Local nPosVOri := aScan(aHeader,{|x| x[2] == "CNE_VUNORI"})
Local nPosVlTt := aScan(aHeader,{|x| x[2] == "CNE_VLTOT "})
Local nPosMExc := aScan(aHeader,{|x| x[2] == "CNE_EXCEDE"})

Local nQuant   := 0
Local nTotal   := 0

If M->CND_ZERO == "1"
	//-- Verifica se o total e igual a zero quando a medicao for zerada
	If !(lRet := (M->CNE_VLTOT == 0))
		Help(" ",1,"CN130VLTOT",,STR0117,1,1) //- 'MediГУes zeradas nЦo possuem valor.'
	EndIf
Else
	//-- Verifica se a quantidade e menor que o saldo disponivel                                      Ё
	If nPosQMed > 0
		If M->CND_SERVIC == '2'
			nQuant := M->CNE_VLTOT / aCols[nx,nPosVOri]
			lRet   := (nQuant <= aCols[nx,nPosQMed])
		Else
			nQuant := Round(M->CNE_VLTOT/aCols[nx,nPosVlUn],TamSX3("CNE_QUANT")[2])
			lRet   := aCols[nx,nPosMExc] == '1' .Or. (nQuant <= aCols[nx,nPosQMed])
		EndIf

		If !lRet
			nTotal := nQuant - aCols[nx,nPosQMed]

			If nTotal<=(1/10 ^ TamSX3(aHeader[nPosQMed,2])[2]) .And. !lArrefat
				lRet := .T.
			Else
				Help(" ",1,"CN130VLTOT",,STR0118,1,1) //- 'O valor informado ultrapassa o saldo disponivel.'
			EndIf
		EndIf
	EndIf

EndIf

If lRet
	CN130TotMed(6)
EndIf

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VldPercЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida percentual informado nas medicoes                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CNTA120()                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                              Ё╠╠
╠╠цддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.               Ё╠╠
╠╠цддддддддддддбддддддддбддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                       Ё╠╠
╠╠цддддддддддддеддддддддеддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                            Ё╠╠
╠╠юддддддддддддаддддддддаддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldPerc()

Local lRet     := .T.
Local lArrefat := (SuperGetMv("MV_ARREFAT",.F.,"S")== "S")

Local nPosQSol := aScan(aHeader,{|x| x[2] == "CNE_QTDSOL"})
Local nPosQMed := aScan(aHeader,{|x| x[2] == "CNE_QTAMED"})
Local nPosQExc := aScan(aHeader,{|x| x[2] == "CNE_EXCEDE"})
Local nx 	   := If(lAuto,n,oGetDados:nAt)
Local nTotal   := 0

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Calcula percentual disponivel                   Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
Local nPercMed := (aCols[nx,nPosQSol]-aCols[nx,nPosQMed])*100/aCols[nx,nPosQSol]

//-- Se excedente nЦo permite digitar percentual
If nPosQExc > 0 .And. aCols[nX,nPosQExc] == '1'
	Aviso(STR0051,STR0093,{STR0046}) //"Este campo nЦo deve ser utilizado para itens classificados como excedente."
	lRet := .F.
ElseIf M->CND_ZERO == "1"
	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Quando a medicao for zerada o percentual deve   Ё
	//Ё ser igual a zero                                Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддды
	lRet := (M->CNE_PERC == 0)
Else
	lRet := (M->CNE_PERC <= (100-nPercMed))
	If !lRet
		nTotal := M->CNE_PERC -(100-nPercMed)

		If nTotal<=0.001 .And. !lArrefat
		   lRet := .T.
		EndIf
	EndIf
EndIf

If lRet
	CN130TotMed(2)
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VldDtEntЁ Autor Ё Marcelo Custodio      Ё Data Ё09.02.2006Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida data de entrada informada nas medicoes                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130VldDtEnt()                                               Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                               Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.                Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                        Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                             Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldDtEnt()
Local lRet := .T.

lRet := (M->CNE_DTENT >= dDataBase) .And. (M->CNE_DTENT <= CN9->CN9_DTFIM)

Return lRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁCN130TrackЁ Autor Ё Sergio Silveira       Ё Data Ё22/03/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz o tratamento da chamada do System Tracker              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130Track(aHeader,aCols)

Local aEnt     := {}
Local aAreaCND := CND->(GetArea())
Local aAreaCNE := CNE->(GetArea())
Local cKey     := M->CND_FILCTR + M->CND_CONTRA + M->CND_REVISA + M->CND_NUMMED

Local nLoop    := 0
If !Empty(xFilial("CNE"))
	cKey     := M->CND_FILCTR + M->CND_CONTRA + M->CND_REVISA + M->CND_NUMMED
Else
 	cKey     := M->CND_CONTRA + M->CND_REVISA + M->CND_NUMMED
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Percorre o acols                            Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
For nLoop := 1 To Len( aCols )
	AAdd( aEnt, { "CND20", cKey} )
Next nLoop

MaTrkShow( aEnt )

RestArea( aAreaCND )
RestArea( aAreaCNE )

CN130Manut("CND",aAreaCND[3],2,,CND->CND_CONTRA,CND->CND_REVISA,,,,,,,.F.)

Return( .T. )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁCN130GrvDescЁ Autor Ё Marcelo Custodio      Ё Data Ё06/07/2006Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Executa a gravacao dos descontos informados na revisao       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                          Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130GrvDesc(aDescs,aHeadDcs,nOpc)

Local cFilCod:=xFilial("CNQ")

Local nx
Local ny
Local nPosTpD	:= aScan(aHeadDcs,{|x| x[2]=="CNQ_TPDESC"})
Local nPosVal 	:= aScan(aHeadDcs,{|x| AllTrim(x[2])=="CNQ_VALOR"})
Local nPosRec 	:= aScan(aHeadDcs,{|x| AllTrim(x[2])=="CNQ_REC_WT"})
Local nUsado 	:= len(aHeadDcs)

dbSelectArea("CNQ")
dbSetOrder(1)

For nx:=1 to len(aDescs)
	If !aDescs[nx,nUsado+1] .And. !Empty(aDescs[nx,nPosTpD]) .And. aDescs[nx,nPosVal] > 0
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se existe desconto                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If nOpc!=3 .And. nOpc!=9 .And. nOpc!=12 .And. nPosRec > 0 .And. !Empty(aDescs[nX][nPosRec])
			CNQ->(dbGoTo(aDescs[nX][nPosRec]))
			RecLock("CNQ",.F.)
		Else
			RecLock("CNQ",.T.)
		EndIf

		For ny:=1 to nUsado
			If ( aHeadDcs[ny][10] != "V" )
				FieldPut(FieldPos(aHeadDcs[ny,2]),aDescs[nx,ny])
			EndIf
		Next
		CNQ->CNQ_CONTRA := M->CND_CONTRA
		CNQ->CNQ_NUMMED := M->CND_NUMMED
		CNQ->CNQ_FILIAL := cFilCod
		CNQ->CNQ_NUMPLA := M->CND_NUMERO
		msUnlock()
	ElseIf aDescs[nx,nUsado+1]
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Apaga desconto                              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If nOpc!=3 .And. nOpc!=9 .And. nOpc!=12 .And. dbSeek(cFilCod+M->CND_NUMMED+aDescs[nx,nPosTpD])
			RecLock("CNQ",.F.)
				dbDelete()
			MsUnlock()
		EndIf
	EndIf
Next

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁCN130VldDes Ё Autor Ё Marcelo Custodio      Ё Data Ё06/07/2006Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de validacao dos descontos                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                          Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130VldDes(oGetDadDe)

Local lRet   	:= .T.
Local lRetU	 	:= .T.
Local lEmpty 	:= .F.
Local lRepetido := .F.

Local aColDE	:= oGetDadDe:aCols
Local aHeaDE 	:= oGetDadDe:aHeader

Local nPosDc 	:= aScan(aHeaDE,{|x| x[2]=="CNQ_TPDESC"})
Local nPosVl 	:= aScan(aHeaDE,{|x| x[2]=="CNQ_VALOR "})
Local nPos   	:= oGetDadDe:nAt
Local nI	 	:= 0

if !Empty(aColDE[nPos,nPosDc])
	If !aColDE[nPos][len(aHeaDE) + 1]
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Varre descontos                             Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		For nI:= 1 To Len( aColDE )
			If ( nI != nPos ) .and. !aColDE[nI][len(aHeaDE) + 1]
				If aColDE[nI,nPosDc] == aColDE[nPos,nPosDc]
					lRepetido := .T.
					Exit
				Endif
			Endif
			If Empty(aColDE[nI,nPosVl])
				lEmpty:=.T.
				Exit
			EndIf
		Next nI

		If lRepetido
			lRet:=.F.
			Help(" ",1,"JAGRAVADO")
		End
		If lEmpty
			lRet:=.F.
			Aviso("CNTA130",STR0016,{STR0046})
		EndIf
	Endif
EndIf

//-- Executa ponto de entrada para validaГЦo do usuАrio
If lRet .And. (ExistBlock("CN130USRVD"))
	lRetU := ExecBlock("CN130USRVD",.F.,.F.,{aHeaDE,aColDE})
	If ValType(lRetU) == "L"
		lRet := lRetU
	EndIf
EndIf

Return lRet


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130MultasЁ Autor Ё Sergio Silveira      Ё Data Ё11/04/2006 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁSelecao e aplicacao de multas do modulo SIGAGCT              Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁA103Multas( ExpD1, ExpC2, ExpC3, ExpA4 )                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpD1 -> Data de emissao                                    Ё╠╠
╠╠Ё          Ё ExpC2 -> Codigo do fornecedor                               Ё╠╠
╠╠Ё          Ё ExpC3 -> Loja do fornecedor                                 Ё╠╠
╠╠Ё          Ё ExpA4 -> Array de multas do documento de entrada            Ё╠╠
╠╠Ё          Ё ExpN5 -> Total de multas                                    Ё╠╠
╠╠Ё          Ё ExpN6 -> Total de bonificacoes                              Ё╠╠
╠╠Ё          Ё ExpC7 -> Numero do contrato                                 Ё╠╠
╠╠Ё          Ё ExpC8 -> Revisao do contrato                                Ё╠╠
╠╠Ё          Ё ExpO9 -> Objeto Multas		                               Ё╠╠
╠╠Ё          Ё ExpOA -> Objeto BonificaГУes                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130Multas( aMultas, aCols, aHeader, nOpc, nTotMultas, nTotBoni, cContrato, cRevisao, oGetMultas, oGetBoni, cPlan)

Local aArea      := GetArea()
Local aListBox   := {}
Local aMedicoes  := {}

Local cQuery     := ""
Local cAliasQry  := ""
Local cMulManual := CN300RETST("MULMAN",0,cPlan,,cFilCTR,.F.)

Local nOpca      := 0
Local nLoop      := 0

Local lProcessa  := .F.
Local lAltera    := ( nOpc == 3 .or. nOpc == 4 .or. nOpc == 9 .or. nOpc=12)
Local lMultaBoni := CN300RETST("TPMULT",0,cPlan,,cFilCTR,.F.) == '2'
Local lPermMulta := .F.
Local lCn130Vld  := 	ExistBlock("CN130VLDMT")
Local lRet

Local oOk        := LoadBitmap( GetResources(), "LBOK" )
Local oNOk       := LoadBitmap( GetResources(), "LBNO" )
Local oDlgMult
Local oList
Local oBold
Local oBmp
Local oBut1
Local oBut2

If lMultaBoni
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta o array de medicoes. Retira a ultima coluna do acols             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nLoop := 1 to Len( aCols )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se a linha esta excluida                                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !ATail( aCols[ nLoop ] )
			aLinhaACols := AClone( aCols[ nLoop ] )
			ASize( aLinhaACols, Len( aLinhaACols ) - 1 )
			AAdd( aMedicoes, aLinhaACols )
		EndIf
	Next nLoop

	If Empty( aMultas ) .And. lAltera
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Processa as multas                                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		CN130ProcMul( aMedicoes, @aListBox, aHeader )
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Carrega as multas do array                                             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AEval( aMultas, { |x| AAdd( aListBox,  { .T., x[1], x[2], x[3], x[4], x[5] , x[6] } ) } )
	EndIf

    If Empty( aListBox )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se estiver vazio, preenche uma linha em branco                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AAdd( aListBox, { .F., "", "", 0, "", "" , ""/* Codigo CN4 */ } )
	EndIf

	DEFINE MSDIALOG oDlgMult TITLE STR0050 FROM 0,0 TO 400, 700 OF oMainWnd PIXEL // "Selecao de multas / bonificacoes"
	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD

	@  0, -25 BITMAP oBmp RESNAME STR0114 oF oDlgMult SIZE 55, 1000 NOBORDER WHEN .F. PIXEL
	@ 03, 40 SAY STR0050 FONT oBold PIXEL // "Selecao de multas / bonificacoes"
	@ 14, 30 TO 16 ,400 LABEL '' OF oDlgMult PIXEL

	If lAltera
		@ 24, 223 BUTTON STR0023 SIZE 35,11 ACTION CN130RepMult( oList, @aListBox, aMedicoes , aHeader ) OF oDlgMult PIXEL // "Reprocessar"
		@ 24, 265 BUTTON STR0024 SIZE 35,11 ACTION CN130AltMul(  oList, @aListBox, cMulManual ) OF oDlgMult PIXEL // "Alterar"
		@ 24, 307 BUTTON STR0025 SIZE 35,11 ACTION CN130AdMult(  oList, @aListBox, cMulManual ) OF oDlgMult PIXEL // "Adicionar"
	EndIf

	oList := TWBrowse():New( If( lAltera, 43, 30 ), 40, 303, If( lAltera, 125, 138 ),,{ "", STR0026, STR0027, STR0028, STR0029, STR0073 },,oDlgMult,,,,,,,,,,,,.F.,,.T.,,.F.,,,) // "Tipo", "Descricao", "Valor", "Insercao", "Interf. Ped."
	oList:SetArray(aListBox)
	oList:bLine := { || { If( aListBox[oList:nAT,1], oOk, oNOK ), If( aListBox[oList:nAt,2] == "1", STR0030, STR0031 ), aListBox[oList:nAT,3], Transform( aListBox[oList:nAT,4],"@E 999,999,999.99" ), If( aListBox[oList:nAT,5] == "1",STR0032,If( aListBox[oList:nAT,5] == "2", STR0033 ,"" ) ),If( aListBox[oList:nAT,6] == "1", OemToAnsi(STR0048) , OemToAnsi(STR0049) ) } } // "Multa","Bonificacao","Automatica","Manual"

	If lAltera
		oList:bLDblClick := { || aListBox[oList:nAt,1] := If( Empty( aListBox[ oList:nAt,3 ]), aListBox[oList:nAt,1],!aListBox[oList:nAt,1] ) }
	EndIf

	DEFINE SBUTTON oBut2 FROM 178, If( lAltera, 280, 312 ) TYPE 1 ACTION ( nOpca := 1, If(!lCn130Vld,oDlgMult:End(),(lRet:=ExecBlock("CN130VLDMT",.F.,.F.),If((valtype(lRet)=="L" .And. lRet),oDlgMult:End(),nOpca := 0))) )  ENABLE of oDlgMult

	If lAltera
		DEFINE SBUTTON oBut3 FROM 178, 312 TYPE 2 ACTION ( nOpca := 0, oDlgMult:End() )  ENABLE of oDlgMult
	EndIf

	ACTIVATE MSDIALOG oDlgMult CENTERED

	If nOpca == 1
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Carrega as multas no array aMultas                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aMultas    := {}
		nTotMultas := 0
		nTotBoni   := 0
		nTotMedMlt := 0
		nTotMedBnf := 0

		For nLoop := 1 to Len( aListBox )
			If aListBox[ nLoop, 1 ]
				AAdd( aMultas, { aListBox[nLoop,2],aListBox[nLoop,3], aListBox[nLoop,4], aListBox[nLoop,5], aListBox[nLoop,6] , aListBox[nLoop,7] } )

				If aListBox[ nLoop, 2 ] == "1"
					nTotMultas += aListBox[ nLoop, 4 ]
					//Soma as multas que interferem pedido
					If  aListBox[ nLoop, 6 ] == "1"
						nTotMedMlt += aListBox[ nLoop, 4 ]
					EndIf
				ElseIf aListBox[ nLoop, 2 ] == "2"
					nTotBoni   += aListBox[ nLoop, 4 ]
					//Soma as bonificacoes que interferem pedido
					If  aListBox[ nLoop, 6 ] == "1"
						nTotMedBnf += aListBox[ nLoop, 4 ]
					EndIf
				EndIf
			EndIf
		Next nLoop
	EndIf
Else
	If !lMultaBoni
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Restaura a integridade dos dados de entrada                            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		Aviso( STR0056, STR0060, { STR0058 }, 2 ) // "Atencao!", "O tipo de contrato desta medicao nao permite o apontamento de multas ou bonificacoes neste momento !", { "Ok" }
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade dos dados de entrada                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RestArea( aArea )

//здддддддддддддддддддддд©
//Ё Refresh Objetos      Ё
//юдддддддддддддддддддддды
oGetMultas:Refresh()
oGetBoni:Refresh()

Return( .T. )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130AdMultЁ Autor Ё Sergio Silveira      Ё Data Ё11/04/2006 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          Ё Inclusao de multa avulsa - SIGAGCT                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1 -> Objeto listbox                                     Ё╠╠
╠╠Ё          Ё ExpA2 -> Array da listbox ( alimentado por referencia )     Ё╠╠
╠╠Ё          Ё ExpC3 -> Configuracao de acesso para multas manuais         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130AdMult( oList, aListBox, cMulManual )

Local cDescri   := Space( GetSx3Cache("CNR_DESCRI","X3_TAMANHO") )
Local cContrato := ""
Local cTipo     := ""
Local cPedFlag  := "1"
Local aPedFlag  := {STR0048, STR0049}//"Sim"##"NЦo"
Local aCN130TPC := {}

Local nValor    := 0
Local nOpca     := 0

Local oBut1
Local oBut2
Local oBmp
Local oBold
Local oDlgMult
Local oTipo
Local oPedFlag
Local oContrato

If cMulManual <> "1"

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de entrada para alterar a descriГЦo dos Tipos                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( ExistBlock("CN130TPC") )
		aCN130TPC := ExecBlock("CN130TPC",.F.,.F.,{cMulManual})
		If ( ValType(aCN130TPC) == "A" )
			aTipos := aClone(aCN130TPC)
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a permissao de acesso para manipulacao manual                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Do Case
	Case cMulManual == "2"
		aTipos := iIf(Len(aCN130TPC) > 0, aClone(aCN130TPC),{ STR0034 }) // "Multa"
	Case cMulManual == "3"
		aTipos := iIf(Len(aCN130TPC) > 0, aClone(aCN130TPC),{ STR0035 }) // "Bonificacao"
	Case cMulManual == "4"
		aTipos := iIf(Len(aCN130TPC) > 0, aClone(aCN130TPC),{ STR0034, STR0035 }) // "Multa", "Bonificacao"
	EndCase

	DEFINE MSDIALOG oDlgMult TITLE STR0036 FROM 0,0 TO 280, 550 OF oMainWnd PIXEL // "Inclusao de multas / bonificacoes"

	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD

	@  0, -25 BITMAP oBmp RESNAME STR0114 oF oDlgMult SIZE 55, 1000 NOBORDER WHEN .F. PIXEL

	@ 03, 40 SAY STR0037 FONT oBold PIXEL // "Inclusao de multas / bonificacoes avulsas"

	@ 14, 30 TO 16 ,400 LABEL '' OF oDlgMult   PIXEL

	@  30, 40 SAY STR0038 OF oDlgMult PIXEL // "Descricao"
	@  40, 40 GET cDescri SIZE 200, 11 VALID NaoVazio( cDescri ).And.cn130MulDesc(@cDescri,@nValor,@cTipo,@cPedFlag,1) PICTURE "@!" OF oDlgMult PIXEL

	@  60, 40 SAY STR0039 OF oDlgMult PIXEL // "Valor"
	@  70, 40 GET nValor SIZE 70, 11   VALID NaoVazio( nValor ) .And. Positivo( nValor ) PICTURE "@E 999,999,999.99" OF oDlgMult PIXEL

	@  90, 40 SAY STR0040 OF oDlgMult PIXEL // "Tipo"
	@ 100, 40 MSCOMBOBOX oTipo VAR cTipo ITEMS aTipos SIZE 100, 36 OF oDlgMult PIXEL

	@  90,145 SAY OemToAnsi(STR0073) OF oDlgMult PIXEL // "Interf. Ped."
	@ 100,145 MSCOMBOBOX oPedFlag VAR cPedFlag ITEMS aPedFlag SIZE 50, 36 OF oDlgMult PIXEL

	DEFINE SBUTTON oBut1 FROM 120, 207 TYPE 1 ACTION ( If( CN130VldMult( cDescri, nValor, aListBox, oList:nAt, 1 ),( nOpca := 1, cTipo := Str(oTipo:nAt,1), cPedFlag := Str(oPedFlag:nAt,1) , oDlgMult:End()), ) )  ENABLE of oDlgMult
	DEFINE SBUTTON oBut2 FROM 120, 239 TYPE 2 ACTION ( nOpca := 0, oDlgMult:End() )  ENABLE of oDlgMult

	ACTIVATE MSDIALOG oDlgMult CENTERED

	If nOpca == 1

		Do Case
		Case cMulManual == "2"
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё So permite multa                                                       Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			cTipo := "1"
		Case cMulManual == "3"
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё So permite bonificacao                                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			cTipo := "2"
		EndCase

		If Len( aListBox ) == 1 .And. Empty( aListBox[ 1, 2 ] )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Se tiver uma linha em branco, apaga                                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aListBox := {}
		EndIf
		AAdd( aListBox, { .T., cTipo, cDescri, nValor, "2", cPedFlag ,""/*Campo CN4 */} )

		bLine := oList:bLine
		oList:SetArray(aListBox)
		oList:bLine := bLine

	EndIf

Else
	Aviso( STR0056, STR0059, { STR0058 }, 2 ) // "Atencao!", "A configuracao deste contrato nao permite a manipulacao manual de multas !", { "Ok"
EndIf

Return( .T. )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130AltMulЁ Autor Ё Sergio Silveira      Ё Data Ё05/05/2006 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          Ё Alterecao de multa - SIGAGCT                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1 -> Objeto listbox                                     Ё╠╠
╠╠Ё          Ё ExpA2 -> Array da listbox ( alimentado por referencia )     Ё╠╠
╠╠Ё          Ё ExpC3 -> Configuracao de acesso para multas manuais         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130AltMul( oList, aListBox, cMulManual )

Local cDescri   := Space( 50 )
Local cContrato := ""
Local cTipo     := aListBox[ oList:nAt, 2 ]
Local aPedFlag  := {OemToAnsi(STR0048),OemToAnsi(STR0049)}//"Sim"##"NЦo"
Local cPedFlag  := ""
Local oPedFlag

Local nValor    := 0
Local nOpca     := 0

Local oBut1
Local oBut2
Local oBmp
Local oBold
Local oDlgMult
Local oContrato

If ( cMulManual == "4" ) .Or. ( cMulManual == "2" .And. cTipo == "1" ) .Or. ( cMulManual == "3" .And. cTipo == "2" )
	If !( Len( aListBox ) == 1 .And. Empty( aListBox[ 1, 2 ] ) )

		cDescri   := aListBox[ oList:nAt, 3 ]
		nValor    := aListBox[ oList:nAt, 4 ]

		DEFINE MSDIALOG oDlgMult TITLE STR0041 FROM 0,0 TO 240, 550 OF oMainWnd PIXEL // "Alteracao de multas / bonificacoes"

		DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD

		@  0, -25 BITMAP oBmp RESNAME STR0114 oF oDlgMult SIZE 55, 1000 NOBORDER WHEN .F. PIXEL

		@ 03, 40 SAY STR0041 FONT oBold PIXEL // "Alteracao de multas / bonificacoes"

		@ 14, 30 TO 16 ,400 LABEL '' OF oDlgMult   PIXEL

		@  30, 40 SAY STR0042 OF oDlgMult PIXEL // "Descricao"
		@  40, 40 GET cDescri SIZE 200, 11 VALID NaoVazio( cDescri ).And.cn130MulDesc(@cDescri,@nValor,@cTipo,@cPedFlag,2)  PICTURE "@!" OF oDlgMult PIXEL

		@  60, 40 SAY STR0043 OF oDlgMult PIXEL // "Valor"
		@  70, 40 GET nValor SIZE 70, 11   VALID NaoVazio( nValor ) .And. Positivo( nValor ) PICTURE "@E 999,999,999.99" OF oDlgMult PIXEL

		@ 60,145 SAY OemToAnsi(STR0073) OF oDlgMult PIXEL // "Afeta Pedido"
		@ 70,145 MSCOMBOBOX oPedFlag VAR cPedFlag ITEMS aPedFlag SIZE 50, 36 OF oDlgMult PIXEL

		oPedFlag:nAt := Val(aListBox[ oList:nAt, 6 ])

		DEFINE SBUTTON oBut1 FROM 100, 207 TYPE 1 ACTION ( If( CN130VldMult( cDescri, nValor, aListBox, oList:nAt, 2 ),( nOpca := 1, cPedFlag := Str(oPedFlag:nAt,1), oDlgMult:End()), ) )  ENABLE of oDlgMult
		DEFINE SBUTTON oBut2 FROM 100, 239 TYPE 2 ACTION ( nOpca := 0, oDlgMult:End() )  ENABLE of oDlgMult

		ACTIVATE MSDIALOG oDlgMult CENTERED

		If nOpca == 1
			aListBox[ oList:nAt, 3 ] := cDescri
			aListBox[ oList:nAt, 4 ] := nValor
			aListBox[ oList:nAt, 6 ] := cPedFlag

			bLine := oList:bLine
			oList:SetArray(aListBox)
			oList:bLine := bLine
		EndIf
	Else
		Aviso( STR0044, STR0045, { STR0046 } ) // "Atencao", "Este item nao pode ser alterado !", "Ok"
	EndIf
Else
	Aviso( STR0056, STR0057, { STR0058 }, 2 ) // "Atencao!", "O contrato vinculado a esta medicao nao permite a manipulacao deste movimento !", "Ok"
EndIf

Return( .T. )


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130VldMultЁ Autor Ё Sergio Silveira     Ё Data Ё11/04/2006 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁValidacao dos campos de descricao e valor - Inclusao de multaЁ╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁExpL1 :=  CN130VldMult( ExpC2, ExpN3 )                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC2 -> Descricao da multa                                 Ё╠╠
╠╠Ё          Ё ExpN3 -> Valor da multa                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130VldMult( cDescri, nValor, aListBox, nItem, nAcao )
Local uRet

lRet := !Empty( cDescri )

If lRet
	lRet := !Empty( nValor )
EndIf

If !lRet
	Help( " ", 1, "NVAZIO" )
EndIf

If lRet .And. ExistBlock("CN130MTINC")
	uRet := ExecBlock("CN130MTINC",.F.,.F.,{cDescri,nValor,aListBox,nItem,nAcao})

	If valtype(uRet) == "L"
		lRet := uRet
	EndIf
EndIf

Return( lRet )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130RepMultЁ Autor Ё Sergio Silveira     Ё Data Ё11/04/2006 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1 -> Objeto listbox                                     Ё╠╠
╠╠Ё          Ё ExpA2 -> Array de multas do listbox                         Ё╠╠
╠╠Ё          Ё ExpA3 -> Array de medicoes                                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё                                                             Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁDescri┤└o ЁEfetua o reprocessamento de multas das medicoes              Ё╠╠
╠╠Ё          Ё                                                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130RepMult( oList, aListBox, aMedicoes, aHeader )

Local bLine := { || .T. }

If Aviso( STR0044, STR0047, { STR0048, STR0049 }, 2 ) == 1 // "Atencao !", "Os dados informados serao sobrepostos. Confirma o reprocessamento das multas / bonificacoes desta medicao ?", "Sim","Nao"

	aListBox := {}
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua o reprocessamento                                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	CN130ProcMul( aMedicoes, @aListBox, aHeader )

   If Empty( aListBox )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se estiver vazio, preenche uma linha em branco                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AAdd( aListBox, { .F., "", "", 0, "", "" , ""} )
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Reinicializa o listBox                                                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	bLine := oList:bLine
	oList:SetArray(aListBox)
	oList:bLine := bLine

EndIf

Return( Nil )

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130ProcMulЁ Autor Ё Sergio Silveira     Ё Data Ё11/04/2006 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao Ё Efetua o processamento de multas                            Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё A103ProcMul( ExpA1, ExpA2 )                                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 -> Array contendo as medicoes                         Ё╠╠
╠╠Ё          Ё ExpA2 -> Array do listbox de multas a ser preenchido        Ё╠╠
╠╠Ё          Ё          ( passado por referencia )                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё GestЦo de Contratos                                         Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130ProcMul( aMedicoes, aListBox, aHeader )

Local cCronog    := ""

Local aListBoxAux:= {}
Local lFormula   := .F.

Local nLoop      := 0
Local nLoop2     := 0
Local nPos       := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Percorre os itens das medicoes                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nLoop := 1 to Len( aMedicoes )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona no contrato                                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	CN9->( dbSetOrder( 1 ) )
	CN9->( MsSeek( xFilial( "CN9" ,cFilCTR) + M->CND_CONTRA + M->CND_REVISA ) )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Cria as variaveis de memoria dos itens das medicoes                    Ё
	//Ё as variaveis vao ser utilizadas na interpretacao das multas            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nLoop2 := 1 To Len( aHeader )
		M->&( aHeader[nLoop2, 2 ] ) := aMedicoes[ nLoop, nLoop2 ]
	Next nLoop2

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona o cabecalho da planilha                                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	CNA->( dbSetOrder( 1 ) )
	CNA->( MsSeek( xFilial( "CNA" ,cFilCTR) + M->CND_CONTRA + M->CND_REVISA + M->CND_NUMERO ) )

    cCronog := CNA->CNA_CRONOG

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona no cronograma / competencia                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty(M->CND_PARCEL)
		CNF->( dbSetOrder( 3 ) )
		CNF->( MsSeek( xFilial( "CNF" ,cFilCTR) + M->CND_CONTRA + M->CND_REVISA + cCronog + M->CND_PARCEL ) )
	Else
		CNF->( dbSetOrder( 2 ) )
		CNF->( MsSeek( xFilial( "CNF" ,cFilCTR) + M->CND_CONTRA + M->CND_REVISA + cCronog + M->CND_COMPET ) )
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Percorre as multas / bonificacoes deste contrato                       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	CN130ReprMT(.F.,nLoop,@aListBox)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Reposiciona tabela de Trabalho para demais itens da mediГЦo    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea( "CN4" )

Next nLoop

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Elimina PosiГЦo de trabalho do array - aListBox                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If Len(aListBox)>0
    For nLoop:=1 to len(aListBox)
    	AAdd(aListBoxAux,{aListbox[nLoop,1],aListbox[nLoop,2],aListbox[nLoop,3],aListbox[nLoop,4],aListbox[nLoop,5],aListbox[nLoop,6],aListbox[nLoop,7]})
    Next nLoop
    aListBox:= aListBoxAux
EndIf

Return


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130AdiantЁ Autor Ё Robson Nayland       Ё Data Ё20/10/2006 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁSelecao doa adiantamenentos do modulo SIGAGCT                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   ЁCN130Adiant( ExpC1, ExpC2)                                   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Numero do contrato                                 Ё╠╠
╠╠Ё          Ё ExpC2 -> Numero do adiantamento                             Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                         Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё GestЦo de Contratos                                         Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function CN130Adiant(cContra,cNumero,aCols,aHeader,nRotina,cContra2,cRevisa,nOper,aHeadCpos,aColsCpos)

Local aArea      := GetArea()

Local cQuery     := ""
Local cAliasQry  := ""

Local nOpca      := 0
Local nLoop      := 0
Local nX

Local lProcessa  := .F.
Local oOk        := LoadBitmap( GetResources(), "LBOK" )
Local oNOk       := LoadBitmap( GetResources(), "LBNO" )
Local oBold
Local oBmp
Local oBut1
Local oDlgMult

aHeadCpos := {}
CnRetAhd('CNX_NUMERO', @aHeadCpos)
CnRetAhd('CNX_DTADT', @aHeadCpos)
CnRetAhd('CNX_VLADT', @aHeadCpos)
CnRetAhd('CNX_SALDO', @aHeadCpos)
Aadd(aHeadCpos,{ 'Vlr Compensar', 'CNX_VLCOMP', '@E 9,999,999,999,999.99', 16, 2, 'CnVlComp()', '────────────── ', 'N', 'CNX','R'} )

CN9->( dbSetOrder(1) )
CN9->( dbSeek(xFilial("CN9",cFilCTR)+M->CND_CONTRA+M->CND_REVISA ))

cEspCtr := CN9->CN9_ESPCTR


//здддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica os adiantamentos do Contrato        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддды
cAliasQry := GetNextAlias()
cQuery := "SELECT CNX_FILIAL, CNX_CONTRA, CNX_NUMERO, CNX_DTADT, CNX_VLADT, CNX_NUMMED, CNX_SALDO + ISNULL(CZY.CZY_VALOR, 0) CNX_SLDATU ,CNX_SALDO, ISNULL(CZY.CZY_VALOR, 0) CNX_VLCOMP"
cQuery += "  FROM "+RetSqlName("CNX")+" CNX "
cQuery += "  LEFT JOIN " +  RetSqlName("CZY") +  " CZY ON  CZY.CZY_FILIAL = CNX.CNX_FILIAL AND CZY.CZY_CONTRA =  CNX.CNX_CONTRA  AND CZY.CZY_NUMERO = CNX.CNX_NUMERO  AND CZY.D_E_L_E_T_=' ' "
cQuery += "  AND CZY.CZY_NUMMED = '" + M->CND_NUMMED + "'"


cQuery += " WHERE CNX_FILIAL ='"+xFilial("CNX",cFilCTR)+"'"
cQuery += "   AND CNX_CONTRA ='"+M->CND_CONTRA+ "'"
cQuery += "   AND CNX_SALDO > 0 "
If nOper == 4
	cQuery += " OR CNX_NUMMED = '" + M->CND_NUMMED + "' AND "
Else
	cQuery += " AND "
EndIf

If cEspCtr == "1"
	cQuery += "CNX_FORNEC = '"+M->CND_FORNEC+"' AND "
	cQuery += "CNX_LJFORN = '"+M->CND_LJFORN+"' AND "
Else
	cQuery += "CNX_CLIENT = '"+M->CND_CLIENT+"' AND "
	cQuery += "CNX_LOJACL = '"+M->CND_LOJACL+"' AND "
EndIf
cQuery += " CNX.D_E_L_E_T_=' ' "
cQuery += " ORDER BY CNX_DTADT "

cQuery := ChangeQuery( cQuery )
dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasQry, .F., .T. )

TCSetField(cAliasQry,"CNX_DTADT","D",08,0)
TCSetField(cAliasQry,"CNX_VLADT","N",TamSX3("CNX_VLADT")[1],TamSX3("CNX_VLADT")[2])

If  (cAliasQry )->( Eof())
     Aviso( STR0056, STR0071, { STR0058 }, 2 ) // "Atencao!", "NЦo ha Adiantamentos para esse Contrato" , { "Ok" }
     RestArea( aArea )
     Return( .T. )
Endif

If Len(aAdiants) == 0

	aColsAux := {}
	aColsCpos := {}

	While !( cAliasQry )->( Eof() )

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Avalia a aplicacao da multa                                            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		Aadd(aColsCpos, {( cAliasQRY )->CNX_NUMERO, ( cAliasQRY )->CNX_DTADT, ( cAliasQRY )->CNX_VLADT, ( cAliasQRY )->CNX_SALDO, ( cAliasQRY )->CNX_VLCOMP,.F.})
		Aadd(aColsAux, {( cAliasQRY )->CNX_SLDATU})

		( cAliasQry )->( dbSkip() )

	EndDo

EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Fecha a area de trabalho da query                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
( cAliasQRY )->( dbCloseArea() )

DEFINE MSDIALOG oDlgMult TITLE STR0066 FROM 0,0 TO 300, 700 OF oMainWnd PIXEL // "Adiantamento"
DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
oSize := FwDefSize():New(.F.,,,oDlgMult)
oSize:AddObject( "GETDADOS",  100, 90, .T., .T. ) // Totalmente dimensionavel
oSize:AddObject( "BUTTONS",  100, 10, .T., .T. ) // Totalmente dimensionavel

oSize:lProp 	:= .T. // Proporcional
oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

oSize:Process() 	   // Dispara os calculos

oGetAdia := MsNewGetDados():New(oSize:GetDimension("GETDADOS","LININI"),;
								oSize:GetDimension("GETDADOS","COLINI"),;
	     						oSize:GetDimension("GETDADOS","LINEND"),;
	     						oSize:GetDimension("GETDADOS","COLEND"),;
    							GD_UPDATE,;
    							,;
    							'CNAdTdOk',;
    							,;
    							{'CNX_VLCOMP'},;
    							,;
    							999,;
    							,;
    							,;
    							,;
    							oDlgMult,;
    							@aHeadCpos,;
    							@aColsCpos)

oDlgMult:Refresh()
DEFINE SBUTTON oBut1 FROM 135, 280  TYPE 1 ACTION ( nOpca := 1,If(oGetAdia:TudoOk(), (M->CND_TOTADT:=CnGetTotad(),oDlgMult:End()), .F.) )  ENABLE of oDlgMult
DEFINE SBUTTON oBut2 FROM 135, 310  TYPE 2 ACTION ( nOpca := 2,oDlgMult:End() )  ENABLE of oDlgMult
ACTIVATE MSDIALOG oDlgMult CENTERED

If nOpca == 1
	aAdiants 	:= aClone(CnRetTotAd())
	aColsCpos  := aClone(oGetAdia:aCols)
ElseIf nOpca == 2
	aColsCpos  := {}
	aAdiants 	:= {}
	aColsAux	:= {}
	If nOper == 3
		M->CND_TOTADT := 0
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Restaura a integridade dos dados de entrada                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
RestArea( aArea )

Return( .T. )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммммммкмммммммяммммммммммммммммммммкммммммямммммммммм╩╠╠
╠╠╨Programa  ЁCN130MkCron  ╨Autor  ЁRobson Nayland      ╨ Data Ё  23/10/06╨╠╠
╠╠лммммммммммьмммммммммммммймммммммоммммммммммммммммммммйммммммомммммммммм╧╠╠
╠╠╨Desc.     ЁMarca/Desmarca itens na TwBrowse .                          ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё GestЦo de Contratos                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function CN130MkCron()

Local nX         := 0
Local lCn130VCK  := ExistBlock("CN130VCK")
Local lRetVCK    := .T.

IF aListBox[oList:nAT,1]==.T.
   aListBox[oList:nAT,1]:=.F.
Else
   aListBox[oList:nAT,1]:=.T.
Endif

M->CND_TOTADT:=0
For nX := 1 to Len(aListBox)
	If aListBox[nX,1]
		M->CND_TOTADT += aListBox[nX,3]
	EndIf
Next nX

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada na validaГЦo no Adiantamento do Contrato Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lCn130VCK
	lRetVCK := If(Valtype(lRetVCK:=ExecBlock("CN130VCK",.F.,.F.))=="L",lRetVCK,.T.)
EndIf

If lRetVCK
	If M->CND_TOTADT>M->CND_VLTOT
	    Aviso (STR0056, STR0072, { STR0058 }, 2 ) // "Atencao!", "Os Adiantamentos nЦo podem ultrapassar o total das mediГУes/entregas", { "Ok" }

	    If (!altera,M->CND_TOTADT:=0,M->CND_TOTADT:=nTotAdia2)
	    For nX := 1 to Len(aListBox)
			aListBox[nX,1] := .F.
	    Next nX
	    oList:Refresh()
	Endif
EndIf


oGetAdia:Refresh()

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммммммкмммммммяммммммммммммммммммммкммммммямммммммммм╩╠╠
╠╠╨Programa  ЁMarcDesm     ╨Autor  ЁRobson Nayland      ╨ Data Ё  23/10/06╨╠╠
╠╠лммммммммммьмммммммммммммймммммммоммммммммммммммммммммйммммммомммммммммм╧╠╠
╠╠╨Desc.     ЁMarca/Desmarca todos os itens na TwBrowse .                 ╨╠╠
╠╠╨          Ё                                                            ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё GestЦo de Contratos                                        ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function MarcDesm( lTodos )

Local nX 		:= 0

For nX := 1 to Len(aListBox)
	If lTodos
		aListBox[nX,1] := .T.
	Else
		aListBox[nX,1] := .F.
	EndIf
Next nX

M->CND_TOTADT:=0

For nX := 1 to Len(aListBox)
	If aListBox[nX,1]
		M->CND_TOTADT += aListBox[nX,3]
	EndIf
Next nX
If M->CND_TOTADT>M->CND_VLTOT
    Aviso (STR0056, STR0072, { STR0058 }, 2 ) // "Atencao!", "Os Adiantamentos nЦo podem ultrapassar o total das mediГУes/entregas", { "Ok" }
    If (!altera,M->CND_TOTADT:=0,M->CND_TOTADT:=nTotAdia2)
    For nX := 1 to Len(aListBox)
		aListBox[nX,1] := .F.
    Next nX
    lTodos     := .F.
    oTodos:Refresh()
Endif

oGetAdia:Refresh()
oList:Refresh()

Return Nil

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130RetDcr Ё Autor Ё Sergio Silveira     Ё Data Ё27/04/2007 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao Ё Retorna descricao do produto                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130RetDcr()                                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130RetDcr()

Local cRet := ""
Local aReaCNB := CNB->(GetArea())

If Type("INCLUI")<> "L"
	INCLUI := .T. 
EndIf 

If !INCLUI

	CNB->(DbSetorder(1))
	If CNB->(DbSeek(CND->CND_FILCTR + CNE->CNE_CONTRA + CNE->CNE_REVISA + CNE->CNE_NUMERO + CNE->CNE_ITEM))
		cRet := CNB->CNB_DESCRI
	EndIf

EndIf

If Empty(cRet) .and. !INCLUI //Caso o produto nЦo tenha sido informado na inclusЦo do contrato
	cRet := Posicione("SB1",1,xFilial("SB1",CND->CND_FILCTR)+CNE->CNE_PRODUT,"B1_DESC")
Endif

RestArea(aReaCNB)

Return cRet

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130RetGrp Ё Autor Ё Marcelo Custodio    Ё Data Ё10/12/2007 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao Ё Retorna grupo de aprovacao do tipo de contrato              Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130RetGrp()                                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130RetGrp()
Local aArea		:= CN9->(GetArea())
Local cGrupo	:= Space(len(CND->CND_APROV))
Local cFilCtr	:= cFilAnt

If (GetNewPar("MV_CNMDALC","N") == "S" )
	dbSelectArea("CN9")
	CN9->(dbSetOrder(1))

	If CN9->(MsSeek(xFilial("CN9",cFilCTR)+M->CND_CONTRA+M->CND_REVISA))
		If !Empty(CN9->CN9_GRPAPR)
			cGrupo := CN9->CN9_GRPAPR
		ElseIf CN1->(dbSeek(xFilial("CN1")+alltrim(CN9->CN9_TPCTO)))
			cGrupo := CN1->CN1_GRPAPR
		EndIf
	EndIf
EndIf

RestArea(aArea)
Return cGrupo

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130Moeda  Ё Autor Ё Felipe Bittar       Ё Data Ё29/08/2008 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao Ё Atualiza os valores da medicao com base na moeda selecionadaЁ╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130Moeda()                                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё X3_VALID do campo CND_MOEDA                                 Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130Moeda()

Local cCronog	:= ""

Local nPosItem	:= 0
Local nPosVuni	:= 0
Local nPosQtde	:= 0
Local nPosVtot	:= 0
Local nPosDesc	:= 0
Local nPosPdes	:= 0
Local nDecPrv	:= 0
Local nDecVuni	:= 0
Local nX		:= 0
Local nTaxa

nPosItem := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_ITEM"})
nPosVuni := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_VLUNIT"})
nDecVuni := TamSX3("CNE_VLUNIT")[2]
nPosQtde := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_QUANT"})
nPosVtot := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_VLTOT"})
nPosDesc := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_VLDESC"})
nPosPdes := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_PDESC"})
nDecPrev := TamSX3("CND_VLPREV")[2]

dbSelectArea("CNA")
dbSetOrder(1)
If MsSeek(xFilial("CNA",cFilCTR)+CN9->CN9_NUMERO+CN9->CN9_REVISA+M->CND_NUMERO)
	cCronog := CNA_CRONOG
	dbSelectArea("CNF")
	dbSetOrder(3)
	If MsSeek(xFilial("CNF",cFilCTR)+CN9->CN9_NUMERO+CN9->CN9_REVISA+cCronog+M->CND_PARCEL)
		nTaxa := CNF_TXMOED
		M->CND_VLPREV := xMoeda(CNF_VLPREV-CNF_VLREAL,CN9->CN9_MOEDA,M->CND_MOEDA,dDataBase,nDecPrev,nTaxa)
	EndIf
EndIf

dbSelectArea("CNB")
dbSetOrder(1)
For nx:=1 to len(oGetDados:aCols)
	If MsSeek(xFilial("CNB",cFilCTR)+CN9->CN9_NUMERO+CN9->CN9_REVISA+M->CND_NUMERO+oGetDados:aCols[nx,nPosItem])
		oGetDados:aCols[nx,nPosVuni] := xMoeda(CNB_VLUNIT,CN9->CN9_MOEDA,M->CND_MOEDA,dDataBase,nDecVuni,nTaxa)
		oGetDados:aCols[nx,nPosVtot] := oGetDados:aCols[nx,nPosVuni] * oGetDados:aCols[nx,nPosQtde]
		oGetDados:aCols[nx,nPosDesc] := oGetDados:aCols[nx,nPosVtot] * oGetDados:aCols[nx,nPosPdes] / 100
	EndIf
Next

//здддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza valor total da medicao          Ё
//юдддддддддддддддддддддддддддддддддддддддддды
CN130TotMed(5)

oGetDados:Refresh()

Return .T.

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130VldFornЁ Autor Ё Felipe Bittar       Ё Data Ё11/09/2008 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁValidacao dos fornecedores do Contrato                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Logico                                                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CNTA130						                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldForn()
Local lRet		:= .T.
Local cContra	:= M->CND_CONTRA
Local cRevisa	:= M->CND_REVISA
Local cFornec	:= M->CND_FORNEC
Local cLoja	:= If(Empty(M->CND_LJFORN),"  ",M->CND_LJFORN)

lRet := ExistCpo("SA2",cFornec+cLoja)

CNC->(dbSetOrder(1))
If lRet .And. !CNC->(dbSeek(xFilial("CNC",cFilCTR)+cContra+cRevisa+cFornec+cLoja))
	Aviso(STR0056,STR0078,{STR0046})//"O fornecedor informado nЦo pertence ao Contrato."
	lRet := .F.
Endif

Return lRet

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130VldCli Ё Autor Ё Andre Anjos			Ё Data Ё02/12/2013 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁValidacao dos fornecedores do Contrato                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Logico                                                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CNTA130						                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VldCli()
Local lRet		:= .F.
Local cContra	:= M->CND_CONTRA
Local cRevisa	:= M->CND_REVISA
Local cClient	:= M->CND_CLIENT
Local cLoja	:= If(Empty(M->CND_LOJACL),"  ",M->CND_LOJACL)

lRet := ExistCpo("SA1",cClient+cLoja)

CNC->(dbSetOrder(3))
If lRet .And. !CNC->(dbSeek(xFilial("CNC",cFilCTR)+cContra+cRevisa+cClient+cLoja))
	//Aviso(STR0056,STR0106,{"OK"})//"O cliente informado nЦo pertence ao Contrato."
	lRet := .F.
Endif

Return lRet

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁCN130NumMd  Ё Autor Ё Felipe Bittar       Ё Data Ё24/09/2008 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁValidacao da numeracao da Medicao do Contrato                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Logico                                                      Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё CNTA130						                               Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130NumMd()
	Local aAreas	:= {}
	Local cNumMed   := ""
	Local lContinua	:= !(FwIsInCallStack("GCTPVGrvMD") .Or.;//Se a origem for o Pedido de Venda
						 FwIsInCallStack("TECXMEDICAO")) //Se vier do SIGATEC(GS)
						 
	If lContinua
		aAreas	:= {CND->(GetArea()), GetArea()}
		cNumMed	:= GetSxENum("CND","CND_NUMMED")
		nSaveSX8:= 1

		CND->(dbSetOrder(4))
		While CND->( msSeek(xFilial("CND")+cNumMed) )
			If ( __lSx8 )
				ConfirmSX8()
			EndIf
			cNumMed:=GetSxENum("CND","CND_NUMMED")
		EndDo

		aEval(aAreas,{|x|RestArea(x)})
		FwFreeArray(aAreas)
	EndIf

Return( cNumMed )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммяммммммммммкмммммммяммммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁCN130DesPV╨Autor  ЁAndre Anjos         ╨ Data Ё  25/11/09   ╨╠╠
╠╠лммммммммммьммммммммммймммммммоммммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao para buscar um tipo de desconte que influencie o PV.╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Retorno   Ё cRet: Codigo do primeiro tipo de desconto encontrado.	  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CNTA130                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130DesPV()
Local aArea  := GetArea()
Local cRet   := CriaVar("CNQ_TPDESC")
Local cRetPE := ""
Local cQry   := ""

cQry := "SELECT CNP_CODIGO "
cQry += "  FROM "+RetSQLName("CNP")
cQry += " WHERE D_E_L_E_T_ = ' ' "
cQry += "   AND CNP_FILIAL = '"+xFilial("CNP")+"'"
cQry += "   AND CNP_FLGPED = '1'"

cQry := ChangeQuery(cQry)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),"TMPCNP",.T.,.T.)

("TMPCNP")->(dbGoTop())
If !("TMPCNP")->(EOF())
	cRet := ("TMPCNP")->CNP_CODIGO
EndIf
("TMPCNP")->(dbCloseArea())

//-- Ponto de entrada para troca do tp. de desconto a ser usado
If ExistBlock("CN130TPD")
	cRetPE := ExecBlock("CN130TPD",.F.,.F.)
	If ValType(cRetPE) == "C" .And. !Empty(cRetPE) .And. CNP->(dbSeek(xFilial("CNP")+cRetPE))
		cRet := cRetPE
	EndIf
EndIf

RestArea(aArea)
Return cRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠иммммммммммямммммммммммкмммммммямммммммммммммммммммкммммммяммммммммммммм╩╠╠
╠╠╨Programa  ЁC130CpyCNZ ╨Autor  ЁTOTVS              ╨ Data Ё  23/11/09   ╨╠╠
╠╠лммммммммммьмммммммммммймммммммомммммммммммммммммммйммммммоммммммммммммм╧╠╠
╠╠╨Descricao Ё Funcao para copiar os registro da tabela CNZ ref. a        ╨╠╠
╠╠╨          Ё planilha selecionada para medicao. 						  ╨╠╠
╠╠╨          Ё nos registros copiados o campo CNZ_CODPLA ficarА em branco ╨╠╠
╠╠╨          Ё e o campo CNZ_NUMMED conterА o numero da medicao           ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Retorno   Ё cRet: Codigo do primeiro tipo de desconto encontrado.	  ╨╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Uso       Ё CNTA130                                                    ╨╠╠
╠╠хммммммммммомммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╪╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function C130CpyCNZ(aDadosCNB,aRatGCT)
Local aArray	:= {}
Local aAreaCNZ	:= CNZ->(GetArea())
Local aHeadCNZ	:= GCTHeadCNZ()
Local nI 		:= 0
Local cBusca	:= xFilial("CNZ",cFilCTR)+aDadosCNB[1]+aDadosCNB[2]+aDadosCNB[3]+aDadosCNB[4]

dbSelectArea("CNZ")
dbSetOrder(1)

If CNZ->(dbSeek(cBusca))
	aAdd(aRatGCT,{	AllTrim(CNZ->CNZ_CONTRA),;
					AllTrim(CNZ->CNZ_REVISA),;
					AllTrim(M->CND_NUMMED),;
					AllTrim(CNZ->CNZ_ITCONT),;
					{} } )

	While CNZ->(!EOF()) .And. cBusca == xFilial("CNZ",cFilCTR)+CNZ->(CNZ_CONTRA+CNZ_REVISA+CNZ_CODPLA+CNZ_ITCONT)		
		If Empty(CNZ->CNZ_NUMMED)			
			aAdd(aArray, Array(len(aHeadCNZ)+1))

			For nI := 1 to len(aHeadCNZ)
				aArray[len(aArray),nI] := CNZ->&(aHeadCNZ[nI,2])
			Next nI

			aArray[len(aArray),len(aHeadCNZ)+1] := .F.
		EndIf	

		CNZ->(DbSkip())
	EndDo

	If Len(aArray) > 0
		aRatGCT[len(aRatGCT),5] := aClone(aArray)
	Endif

Endif

RestArea(aAreaCNZ)
Return()


/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ёcn130MulDescЁ Autor Ё Aline Sebrian       Ё Data Ё04/07/2011 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescricao ЁValidacao do campo de descricao-Inclusao e alteracao de multaЁ╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ёcn130MulDesc(ExpC1,ExpN1,ExpC2,ExpC3,ExpN2)                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Descricao da multa                                 Ё╠╠
╠╠Ё          Ё ExpN1 -> Valor da multa                                     Ё╠╠
╠╠Ё          Ё ExpC2 -> Tipo multa/bonificacao                             Ё╠╠
╠╠Ё          Ё ExpC3 -> Interfere pedido                                   Ё╠╠
╠╠Ё          Ё ExpN2 -> 1-Adiciona; 2-Altera                               Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Materiais                                                   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function cn130MulDesc(cDescri,nValor,cTipo,cPedFlag,nOpcao)
Local aRet:= {}
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada executado na inclusao ou alteracao da multa/bonificacao      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock("CN130MLD")
	aRet := ExecBlock("CN130MLD",.f.,.f.,{cDescri,nValor,cTipo,cPedFlag,nOpcao})

	If Valtype(aRet) == "A"
		If Len(aRet)>=1 .And. Valtype(aRet[1]) == "C"
			cDescri :=  aRet[1]
		EndIf
		If Len(aRet)>=2 .And. Valtype(aRet[2]) == "N"
			nValor  := aRet[2]
		EndIf
		If Len(aRet)>=3 .And. Valtype(aRet[3]) == "C"
			cTipo   := aRet[3]
		EndIf
		If Len(aRet)>=4 .And. Valtype(aRet[4]) == "C"
			cPedFlag:= aRet[4]
		EndIf
	EndIf
EndIf
Return

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤ao    Ёcn130VldTS Ё Autor Ё Aline Sebrian         Ё Data Ё 14.01.2010 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida Tipo Saida na Planilha                                 Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA130                                                       Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function cn130VldTS(cValor)
Local aArea		:= GetArea()
Local aSaveLines:= FWSaveRows()
Local lRet      := .T.
Local oModel	:= Nil
Local cCtrNum	:= ""
Local cCtrFil   := ""
Local cReadVar  := ReadVar()
Local xConteudo := &(cReadVar)
Local lnotTEC930 := IIF( ((FindFunction("TEC930Test") .AND. TEC930Test()) .Or. FwIsInCallStack("ApurJobAux") ) , .F. , !IsInCallStack("TECA930") )

Default cValor := ""

If Empty(cValor)
	cValor := xConteudo
EndIf

If (FwIsInCallStack('CNTA120') .Or. FwIsInCallStack('CN121OldMd') ) .And. lnotTEC930
	cCtrNum	:= Iif(INCLUI,cContra,CND->CND_CONTRA)
	cCtrFil := Iif(INCLUI,cFilCtr,CND->CND_FILCTR)
Else
	oModel	:= FwModelActive()
	cCtrNum	:= oModel:GetValue("CNDMASTER","CND_CONTRA")
    cCtrFil := oModel:GetValue("CNDMASTER","CND_FILCTR")
	If Empty(cValor)
		cValor := oModel:GetValue("CNEDETAIL","CNE_TES")
	EndIf
EndIf

If !Empty(cValor)
	dbSelectArea("SF4")
	SF4->(dbSetOrder(1))
	If  SF4->(MsSeek(xFilial("SF4") + cValor))
		If cValor < "500"
			Help (" ",1,"A410NOTES")
			lRet := .F.
		EndIf
		If lRet .And. CN300RetSt("COMPRA",/*nModo*/,/*cPlan*/,cCtrNum,cCtrFil)
			Help (" ",1,"CNTA130TS1")//"Tipo Saida somente para Contratos do Tipo Vendas"
			lRet := .F.
		EndIf
	Else
		Help (" ",1,"CNTA130TS2")//"Tipo Saida nЦo cadastrada"
		lRet := .F.
	EndIf
EndIf

FWRestRows( aSaveLines )
RestArea(aArea)
Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤ao    Ёcn130VldTe Ё Autor Ё Aline Sebrian         Ё Data Ё 14.01.2010 Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida Tipo Entrada na Planilha                               Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA130                                                       Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function cn130VldTE()
Local cReadVar  := ReadVar()
Local xConteudo := &(cReadVar)
Local lRet      := .T.
Local oModel	:= Nil
Local cCtrNum	:= ""
Local cCtrFil   := ""

dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial("SF4")+xConteudo)

If (FwIsInCallStack('CNTA120') .Or. FwIsInCallStack('CN121OldMd'))
	cCtrNum	:= Iif(INCLUI,cContra,CND->CND_CONTRA)
    cCtrFil := Iif(INCLUI,cFilCtr,CND->CND_FILCTR)
Else
	oModel	:= FwModelActive()
	cCtrNum	:= oModel:GetValue("CNDMASTER","CND_CONTRA")
    cCtrFil := oModel:GetValue("CNDMASTER","CND_FILCTR")
EndIf

If  SF4->(Found())
	If xConteudo > "500"
		Help (" ",1,"A410NOTES")
		lRet := .F.
	EndIf

	If lRet
		If CN300RetSt("VENDA",/*nModo*/,/*cPlan*/,cCtrNum,cCtrFil)
			Help (" ",1,"CNTA130TE1")//"Tipo Entrada somente para Contratos do Tipo Compras"
			lRet := .F.
		EndIf
	EndIf
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤ao    ЁCN130VlUnt Ё Autor Ё Aline Sebrian         Ё Data Ё19.10.2011Ё╠╠
╠╠цддддддддддедддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida valor unitario informado na medicao                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё CN130VlUnt()                                                Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё                                                             Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё         ATUALIZACOES SOFRIDAS DESDE A CONSTRU─AO INICIAL.              Ё╠╠
╠╠цддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁProgramador Ё Data   Ё BOPS Ё  Motivo da Alteracao                      Ё╠╠
╠╠цддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё            Ё        Ё      Ё                                           Ё╠╠
╠╠юддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ*/
Function CN130VlUnt()

Local lRet := .T.
Local lArrefat := (SuperGetMv("MV_ARREFAT",.F.,"S")== "S")

Local nx       := If(lAuto,n,oGetDados:nAt)
Local nPosQMed := aScan(aHeader,{|x| x[2] == "CNE_QTAMED"})
Local nPosVlUn := aScan(aHeader,{|x| x[2] == "CNE_VLUNIT"})
Local nPosQOri := aScan(aHeader,{|x| x[2] == "CNE_QTDORI"})
Local nPosVOri := aScan(aHeader,{|x| x[2] == "CNE_VUNORI"})
Local nPosVlTt := aScan(aHeader,{|x| x[2] == "CNE_VLTOT "})
Local nQuant   := 0
Local nTotal   := 0

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se a quantidade e menor que o saldo    Ё
//Ё disponivel                                      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды
If nPosQMed > 0
	If M->CND_SERVIC == '2'
		nQuant := M->CNE_VLUNIT / aCols[nx,nPosVOri]
		lRet   := (nQuant <= aCols[nx,nPosQMed])
	EndIf
EndIf

If !lRet
	nTotal := nQuant - aCols[nx,nPosQMed]
	If nTotal<=0.001 .And. !lArrefat
	   lRet := .T.
	EndIf
EndIf

If lRet
	CN130TotMed(3)
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤ao    Ё CN130FldOk  Ё Autor Ё Allyson Freitas       Ё Data Ё 11.01.2012 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida permissao de Produto                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA130                                                         Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130FldOk()
Local lRet := .T.
Local cFieldCNE	:= ReadVar()
Local cFieldEdit	:= SubStr(cFieldCNE,4,Len(cFieldCNE))
Local cMenVar	:= &(ReadVar())
Local nPProduto	:= aScan(aHeader,{|x| AllTrim(x[2])== "CNE_PRODUT"})

If Altera
	//здддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o usuario tem permissao de alteracao. Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддды
	If cFieldEdit $ "CNE_PRODUT"
		If !(lRet := MaAvalPerm(1,{cMenVar,"CNT120",If(oGetDados:lNewLine,3,4)}))
			Help(,,1,'SEMPERM')
		EndIf
	Else
		If !(lRet := MaAvalPerm(1,{aCols[n][nPProduto],"CNT120",If(oGetDados:lNewLine,3,4)}))
			Help(,,1,'SEMPERM')
		EndIf
	EndIf
EndIf

Return lRet

/*
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤ao    Ё CN130DelOk  Ё Autor Ё Allyson Freitas       Ё Data Ё 11.01.2012 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤ao Ё Valida permissao de Produto                                     Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё CNTA130                                                         Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function CN130DelOk(lExcedente)
Local lRet		:= .T.
Local nPProduto	:= aScan(aHeader,{|x| AllTrim(x[2])== "CNE_PRODUT"})
Local nPExced	:= aScan(aHeader,{|x| AllTrim(x[2])== "CNE_EXCEDE"})
local nPPedido	:= aScan(aHeader,{|x| AllTrim(x[2])== "CNE_PEDIDO"})
Local uRet		:= .T.

DEFAULT lExcedente := .F.

//Valida a exclusЦo de itens excedentes
If lExcedente
	If INCLUI .Or. nPExced == 0 .Or. nPPedido == 0
		Return .F.
	ElseIf aCols[n][nPExced] <> '1' .Or. !Empty(aCols[n][nPPedido])
		Help(" ",1,"CN130EXCED",,STR0110,4,0)
		Return .F.
	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o usuario tem permissao de exclusao. Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддды

If !(lRet := MaAvalPerm(1,{aCols[n][nPProduto],"CNT120",5}))
	Help(,,1,'SEMPERM')
EndIf

CN130TotMed(5,.T.)

If lRet .And. ExistBlock("C130KDEL")
	uRet := ExecBlock("C130KDEL",.F.,.F.)
	lRet :=	If(Valtype(uRet)=="L",uRet,lRet)
EndIf

Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёcn130MultT Ё Autor Ё Aline S Damasceno    Ё Data Ё 03/02/12 Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Efetua a Validacao dos campos digitados quanto a quantidadeЁ╠╠
╠╠Ё          Ё,preco, desconto e quantidade liberada.                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Logico                                                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function cn130MultT()

Local aArea     := GetArea()
Local cEspecie  := ""

Local nPVlUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_VLUNIT"})
Local nPVlTot   := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_VLTOT"})
Local nPVlDesc  := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_VLDESC"})
Local nPDescont := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_PDESC"})
Local nPQuant   := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_QUANT"})
Local nPPerc    := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_PERC"})
Local nPQtdSol  := aScan(aHeader,{|x| AllTrim(x[2])=="CNE_QTDSOL"})

Local nPVlrOri	:= aScan(aHeader,{|x| AllTrim(x[2])=="CNE_VUNORI"})
Local nPQtdOri	:= aScan(aHeader,{|x| AllTrim(x[2])=="CNE_QTDORI"})

Local nValorUn  := 0

Local cReadVar  := ReadVar()
Local xConteudo := &(cReadVar)

Local nx       := If(lAuto,n,oGetDados:nAt)

If Type("cEspCtr") == "C"
	cEspecie := cEspCtr
Else
	cEspecie := CN9->CN9_ESPCTR
EndIf

//Tratamento para contrato do tipo vendas
If  !( Type("lAuto") != "U" .And. lAuto ) .And. !IsInCallStack("CN130EXCE")
  
	If cEspecie=="2"
	   If ( Type("lFixo") != "U" .And. lFixo )
			Do Case
			Case "CNE_VLUNIT"$cReadVar
				If ( Type("lServico") != "U" .And. !lServico )
					FtDescItem(0,@xConteudo,@aCols[nx,nPQuant],@aCols[nx,nPVlTot],@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
				Else
					FtDescItem(0,@aCols[nx,nPVlrOri],@xConteudo/@aCols[nx,nPVlrOri],@aCols[nx,nPVlTot],@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
				EndIf
	
			Case "CNE_QUANT"$cReadVar
				FtDescItem(0,@aCols[nx,nPVlUnit],@xConteudo,@aCols[nx,nPVlTot],@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
	
			Case "CNE_PDESC"$cReadVar
				FtDescItem(0,@aCols[nx,nPVlUnit],@aCols[nx,nPQuant],@aCols[nx,nPVlTot],@xConteudo,@aCols[nx,nPVlDesc],0,1,,)
	
			Case "CNE_VLTOT"$cReadVar
	
				If ( Type("lServico") != "U" .And. !lServico )
					FtDescItem(0,@aCols[nx,nPVlUnit],(@xConteudo/@aCols[nx,nPVlUnit]) ,@xConteudo,@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
				Else
					FtDescItem(0,@aCols[nx,nPVlrOri],(@xConteudo/@aCols[nx,nPVlrOri]),@xConteudo,@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
	
				EndIf
	
			Case "CNE_PERC"$cReadVar
				If ( Type("lServico") != "U" .And. !lServico )
					FtDescItem(0,@aCols[nx,nPVlUnit],(@aCols[nx,nPQtdSol]*@xConteudo)/100,@aCols[nx,nPVlTot],@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
				Else
					FtDescItem(0,@aCols[nx,nPVlrOri],(@aCols[nx,nPQtdSol]*@xConteudo)/100,@aCols[nx,nPVlTot],@aCols[nx,nPDescont],@aCols[nx,nPVlDesc],0,1,,)
				EndIf
	
			EndCase
	 	EndIf
	
		If ( Type("lFixo") != "U" .And. !lFixo )
			Do Case
				Case "CNE_VLUNIT"$cReadVar
					aCols[nx,nPVlDesc] := 0
					aCols[nx,nPDescont]:= 0
	
				Case "CNE_QUANT"$cReadVar
					aCols[nx,nPVlDesc] := 0
					aCols[nx,nPDescont]:= 0
	
				Case "CNE_PDESC"$cReadVar
					FtDescItem(0,@aCols[nx,nPVlUnit],@aCols[nx,nPQuant],@aCols[nx,nPVlTot],@xConteudo,@aCols[nx,nPVlDesc],0,1,,)
			EndCase
		EndIf
	EndIf
	
	If "CNE_VLUNIT"$cReadVar .And. Type("lServico") != "U" .And. lServico .And. !Empty(nPPerc) .And. !Empty(nPVlrOri) .And. !Empty(nPQtdSol)
		aCols[nX,nPPerc] := ( xConteudo / (aCols[nX,nPVlrOri] * aCols[nX,nPQtdSol]) ) * 100
	EndIf
	
EndIf

RestArea(aArea)
Return .t.
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ёcn130VldCnd9  Ё Autor Ё Aline S Damasceno Ё Data Ё 03/04/12 Ё╠╠
╠╠цддддддддддеддддддддддддддадддддддадддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Condicao de Pagamento Tipo 9                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁExpL1: Logico                                               Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function cn130VldCnd9(oModel,cTable)
	Local nx        := 0
	Local nParcelas := SuperGetMv("MV_NUMPARC")
	Local nParc     := 0
	Local nAux      := 0
	Local nTotal    := 0
	Local dParc     :=  Ctod("")
	Local lInvalida	:= .T.
	Local lRet      := .T.
	Local lPerc		:= .F.
	Local cParcela  := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"
	Local cCpoValor	:= ""
	Local cCpoData	:= ""
	Local cPrefCpo  := ""
	Local cParcPln  := ""
	Local cDataParc := ""
	Local lMVC		:= .F.
	Local lTemParc	:= .F.
	Local lTIPO9SP	:= .T.
	Local lHelp		:= .T.
	Default oModel	:= Nil
	Default cTable  := "CND"

	cPrefCpo  := PrefixoCpo(cTable)

	cParcPln  := cPrefCpo + "_PARC"

	cDataParc := cPrefCpo + "_DATA"

	If (lMVC := (ValType(oModel) == 'O'))
		lMVC := (oModel:IsActive() .And. oModel:GetId() == 'CNTA121')
	EndIf

	If (!lMVC .And. nParcelas > 4)//Essa validaГЦo sС ocorre no CNTA120, no CNTA121 И feito no Active do modelo
		cSeq := "5"	
		for nX := 5 to nParcelas
			lRet := (cTable)->(ColumnPos(cParcPln + cSeq) > 0 .And. ColumnPos(cDataParc + cSeq) > 0)
			
			If !lRet
				Help(" ",1,"TMKTIP905") //"A quantidade de parcelas nao esta compativel. Verificar junto ao administrador do sistema relacao entre parametro MV_NUMPARC e dicionario de dados"
				Exit
			EndIf
			cSeq := Soma1(cSeq)
		next nX
	EndIf

	If lRet
		lTIPO9SP := SuperGetMV("MV_TIPO9SP",,.T.)		
		For nX:=1 to nParcelas
			cCpoValor	:= cParcPln+Substr(cParcela,nx,1)
			cCpoData	:= cDataParc+Substr(cParcela,nx,1)
			If lMVC			
				nParc:= oModel:GetValue(IIF(cTable =="CXN","CXNDETAIL","CNDMASTER"), cCpoValor)
				dParc:= oModel:GetValue(IIF(cTable =="CXN","CXNDETAIL","CNDMASTER"), cCpoData)
			Else			
				nParc := &( "M->"+cCpoValor )
				dParc := &( "M->"+cCpoData  )
			EndIf
			
			If (nParc > 0 .And. Empty(dParc)) .Or. (nParc <= 0 .And. !Empty(dParc))//Avalia se И uma parcela invАlida
				lInvalida := .F.
				Exit
			ElseIf(!Empty(nParc) .And. !Empty(dParc) .And. !lTemParc)
				lTemParc := .T. //Indica que hА ao menos uma parcela vАlida
			EndIf
			nAux  += nParc
		Next nX

		If !lTemParc //Se nЦo tiver nenhuma parcela preenchida
			lTemParc := lTIPO9SP //SС valida caso <MV_TIPO9SP> estiver .F.
		EndIf

		If (!lInvalida .Or. !lTemParc) //Se tiver alguma parcela invАlida OU nЦo tiver nenhuma parcela			
			lRet := .F.
		Else
			If lMVC 
				If  cTable == "CXN"
					nTotal:= oModel:GetValue("CXNDETAIL", "CXN_VLTOT")
				Else
					nTotal	:= oModel:GetValue("CNDMASTER", "CND_VLTOT")
				EndIf
			Else
				nTotal:=M->CND_VLTOT
			EndIf

			lPerc	:= Alltrim(SE4->E4_COND) == "%"
			
			If (!lPerc .And. NoRound(nTotal,2) <> NoRound(nAux,2)) .Or. (lPerc .And. nAux <> 100)
				If (lRet := lTIPO9SP)	// Tipo 9 Sem Parcela informada
					If !IsBlind()
						If lMVC							
							aHelp := GetHlpSoluc("A410TIPO9")/*NecessАrio obter a soluГЦo do help p/ exibir no CNTA121*/
							MsgAlert(aHelp[2] + Chr(13) + Chr(10) + aHelp[1], "A410TIPO9")/*Utiliza MsgAlert pois o help sС И exibido no MVC quando a validaГЦo falha.*/
						Else
							Help(" ",1,"A410TIPO9") //Exibe help apenas p/ alertar ao usuАrio, tal qual ocorre no pedido de venda
						EndIf
						lRet := MsgYesNo(OemToAnsi(STR0104),OemToAnsi(STR0056))  //"Confirma a Inclusao da MediГЦo do Contrato ?"###"Aten┤└o"
						lHelp:= .F. //NЦo exibe o help A410TIPO9 novamente.
					EndIf
				EndIf
			EndIf
		EndIf

		If !lRet			
			If lHelp
				Help(" ",1,"A410TIPO9")
			Else /* Nesse caso, o usuАrio clicou em "NЦo" no <MsgYesNo> */
				Help("",1,"USERCANCEL") // OperaГЦo cancelada pelo Usuario!
			EndIf
		EndIf
	EndIf

Return lRet

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao	 Ё CN130ExceЁ Autor Ё Joao Goncalves de Oliveira Ё Data Ё 22/02/11 Ё╠╠
╠╠цддддддддддеддддддддддадддддддаддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Executa tela para que sejam informados dados para inclusЦo de   Ё╠╠
╠╠Ё          Ё excedente                                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe	 Ё CN130Exce()                    			        		       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Retorno  Ё Nenhum                       						   		   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function CN130Exce(nOpc)
	Local lRet 		:= .T.
	Local lAddLinha := .F.
	Local aExibCamp := {"CNE_ITEM","CNE_PRODUT","CNE_DESCRI","CNE_TABPRC","CNE_QUANT","CNE_VLUNIT","CNE_DTENT","CNE_FLGCMS","CNE_PEDTIT","CNE_CC","CNE_CONTA","CNE_ITEMCT","CNE_CLVL"}
	Local aEditCamp := {"CNE_QUANT","CNE_DTENT","CNE_FLGCMS","CNE_PEDTIT"}
	Local aCampEnch := {}
	Local aCN130CEX := {}
	Local aEntidades := CtbEntArr()
	Local aCargCamp	:= {"CNE_FLGCMS","CNE_TS","CNE_PEDTIT","CNE_CC","CNE_CONTA","CNE_ITEMCT","CNE_CLVL"} //Campos p/ inicializacao
	Local cCadastro := STR0094 //IncluisЦo de Excedente"
	Local cMaxiItem := ""
	Local cPesqTab  := ""	
	Local oDlgExced := NIL	
	Local nX 		:= 0
	Local nY		:= 0
	Local nAux	 	:= 0
	Local nTotLGetD := 0
	Local nPrcUnit  := 0
	Local nPosiItem := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_ITEM"})
	Local nPosiProd := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_PRODUT"})
	Local nPosiDesc := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_DESCRI"})
	Local nPosiVlUn := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_VLUNIT"})
	Local nPosiQtde := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_QUANT"})
	Local nPosiVlTo := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_VLTOT"})
	Local nPosiDEnt := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_DTENT"})
	Local nPosiExce := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_EXCEDE"})
	Local nPosiTPrc := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_TABPRC"})
	Local nPosEnt	:= 0
	Private aTELA[0][0]
	Private aGETS[0]
	
	For nY:=1 To Len(aEntidades)
		aAdd(aCargCamp, "CNE_EC"+aEntidades[nY]+"DB")
		aAdd(aCargCamp, "CNE_EC"+aEntidades[nY]+"CR")
		aAdd(aExibCamp, "CNE_EC"+aEntidades[nY]+"DB")
		aAdd(aExibCamp, "CNE_EC"+aEntidades[nY]+"CR")
	Next nY
	
	If ExistBlock("CN130CEX")
		aCN130CEX := ExecBlock("CN130CEX",.F.,.F.,{aEditCamp})
		If ValType(aCN130CEX) == "A"
			aEditCamp := aClone(aCN130CEX)
		EndIf
	EndIf
	
	If !lAuto .And. oGetDados:aCols[oGetDados:nAt,nPosiExce] # '1'	
		
		aCampEnch := GetCNEEnch(aExibCamp) //Carrega campos da Enchoice da CNE
	
		cPesqTab := Posicione("CNB",1,xFilial("CNB",cFilCTR)+M->CND_CONTRA+M->CND_REVISA+M->CND_NUMERO+oGetDados:aCols[oGetDados:nAt,nPosiItem],"CNB_TABPRC")
	
		M->CNE_ITEM   := oGetDados:aCols[oGetDados:nAt,nPosiItem]
		M->CNE_PRODUT := oGetDados:aCols[oGetDados:nAt,nPosiProd]
		M->CNE_DESCRI := oGetDados:aCols[oGetDados:nAt,nPosiDesc]
		M->CNE_TABPRC := cPesqTab
	
		If (nOpc == 3 .Or. nOpc == 4)
			nX := aScan(oGetDados:aCols, {|x| x[nPosiProd] == oGetDados:aCols[oGetDados:nAt,nPosiProd] .And.;
														x[nPosiTprc] == cPesqTab .And.;
													  	x[nPosiExce] == '1'})
		Else
			nX := aScan(oGetDados:aCols, {|x| x[nPosiProd] == oGetDados:aCols[oGetDados:nAt,nPosiProd] .And.;
														x[nPosiTprc] == cPesqTab .And.;
													  	x[nPosiExce] == '1' .And. Empty(x[Len(oGetDados:aHeader)])})
		EndIf
	
		If !Empty(nX)	
			M->CNE_QUANT  := oGetDados:aCols[nX,nPosiQtde]
			M->CNE_VLUNIT := oGetDados:aCols[nX,nPosiVlUn]
		Else
			nX := oGetDados:nAt
			M->CNE_ITEM   := oGetDados:aCols[oGetDados:nAt,nPosiItem]
			M->CNE_QUANT  := CriaVar("CNE_QUANT")
			M->CNE_VLUNIT := CriaVar("CNE_VLUNIT")
			lAddLinha := .T.
		EndIf
		
		M->CNE_DTENT  := oGetDados:aCols[nX,nPosiDEnt]
		
		For nY:=1 To Len(aCargCamp)
			nPosEnt := aScan(aHeader,{|x| AllTrim(x[2]) == aCargCamp[nY]})

			If nPosEnt > 0
				M->&(aCargCamp[nY]) := oGetDados:aCols[nX,nPosEnt]
			EndIf
		Next nY
		
		//-- Tratamento no aRotina pois como chamara MsMGet com nOpc precisa de tres opcoes no aRotina
		aAdd(aRotina,aClone(aRotina[1]))
		aRotina[3,4] := 3
	
		//ЁMonta a tela para digitaГЦo do excedente
		Define MsDialog oDlgExced Title cCadastro From 09,0 To 28,80
		oDlgExced:lEscClose := .F.
		oEnchExce := MsMGet():New("CNE",0,3,,,,aExibCamp,,aEditCamp,1,,,,oDlgExced,.F.,.F.,.F.,"",.F.,.F.,aCampEnch)
		oEnchExce:oBox:Align := CONTROL_ALIGN_ALLCLIENT
		Activate Msdialog oDlgExced On Init EnchoiceBar(oDlgExced, {|| If(Obrigatorio(aGets,aTela),(nAux:=1,oDlgExced:End()),NIL)}, {|| oDlgExced:End()})
	
		//-- Retorna aRotina ao formato original
		aSize(aRotina,Len(aRotina)-1)
	
		If nAux == 1
			If lAddLinha
				cMaxiItem := Soma1(oGetDados:aCols[Len(oGetDados:aCols),nPosiItem])
				aAdd(oGetDados:aCols, Array(Len(aHeader) + 1))
				nTotLGetD := Len(oGetDados:aCols)
			Else
				nTotLGetD := nX
				cMaxiItem := oGetDados:aCols[nX,nPosiItem]
			EndIf	
			
			//Inicializa campos da getdados
			For nX := 1 to Len(aHeader)
				If IsHeadRec(aHeader[nX,2])
					If Empty(oGetDados:aCols[nTotLGetD,nX])
						oGetDados:aCols[nTotLGetD,nX] := 0
					EndIf
				ElseIf IsHeadAlias(aHeader[nX,2])
					oGetDados:aCols[nTotLGetD,nX] := "CNE"
				ElseIf AllTrim(aHeader[nX,2]) # "CNE_DESCRI"
					oGetDados:aCols[nTotLGetD,nX] := CriaVar(aHeader[nX,2],.F.)
				EndIf
			Next nX
	
			oGetDados:aCols[nTotLGetD,nPosiItem] := cMaxiItem
			oGetDados:aCols[nTotLGetD,nPosiProd] := M->CNE_PRODUT
			oGetDados:aCols[nTotLGetD,nPosiDesc] := M->CNE_DESCRI
			oGetDados:aCols[nTotLGetD,nPosiVlUn] := M->CNE_VLUNIT
			oGetDados:aCols[nTotLGetD,nPosiQtde] := M->CNE_QUANT
			oGetDados:aCols[nTotLGetD,nPosiExce] := "1"
			oGetDados:aCols[nTotLGetD,nPosiTPrc] := M->CNE_TABPRC
			oGetDados:aCols[nTotLGetD,nPosiVlTo] := oGetDados:aCols[nTotLGetD,nPosiQtde] * oGetDAdos:aCols[nTotLGetD,nPosiVlUn]
			oGetDados:aCols[nTotLGetD,nPosiDEnt] := M->CNE_DTENT
	
			For nY:=1 To Len(aCargCamp)
				nPosEnt := aScan(aHeader,{|x| AllTrim(x[2]) == aCargCamp[nY]})
	
				If nPosEnt > 0
					oGetDados:aCols[nTotLGetD,nPosEnt] := M->&(aCargCamp[nY])
				EndIf
			Next nY
	
			oGetDados:aCols[nTotLGetD,Len(aHeader)+1] := .F.
	
			CN130TotMed(5)
	
		EndIf
	
		oGetDados:Refresh()
	
	ElseIf lAuto
		For nX := 1 To Len(aAutoExced)
			nPQUANT := aScan(aAutoExced[1],{|x| AllTrim(x[1]) == "CNE_QUANT"})
			If Empty(aAutoExced[nX,nPQUANT])
				Loop
			EndIf
	
			nPITEM  := aScan(aAutoExced[nX],{|x| AllTrim(x[1]) == "CNE_ITEM"})
			If (nAux := aScan(aCols,{|x| x[GDFieldPos("CNE_ITEM")] == aAutoExced[nX,nPITEM,2]})) == 0
				Aviso("CNTA120",STR0098 +aCols[nAux,nPITEM] +STR0099) //-- NЦo foi encontrado o item ### para inclusЦo de excedente.
				lRet := .F.
			ElseIf aCols[nAux,GDFieldPos("CNE_EXCEDE")] == '1'
				Aviso("CNTA120",STR0100,{STR0046}) //-- Este recurso estА disponМvel apenas para itens previstos na planilha (nЦo excedentes).
				lRet := .F.
			EndIf
	
			//-- Tratamento para receber o valor unitАrio via ExecAuto.
			If lRet .And. (nPVLUnit := aScan(aAutoExced[1],{|x| AllTrim(x[1]) == "CNE_VLUNIT"})) > 0
				If aAutoExced[nX, nPVLUnit, 2] > 0
					nPrcUnit := aAutoExced[nX, nPVLUnit, 2]
				Else
					Aviso("CNTA120",STR0101 +aCols[nAux,nPITEM] +STR0103) //-- O item ### nЦo possui preГo para a quantidade excedente.
					lRet := .F.
				EndIf
			Else
				//-- Se nao tem tabela de preco nao processa
				If lRet .And. Empty(cPesqTab := Posicione("CNB",1,xFilial("CNB",cFilCTR)+M->CND_CONTRA+M->CND_REVISA+M->CND_NUMERO+aCols[nAux,nPITEM],"CNB_TABPRC"))
					Aviso("CNTA120",STR0101 +aCols[nAux,nPITEM] +STR0102) //-- O item ### nЦo possui tabela de preГo para inclusЦo de excedente.
					lRet := .F.
				Else
					If lRet .And. Empty(nPrcUnit := MaTabPrVen(cPesqTab,aCols[nAux,GDFieldPos("CNE_PRODUT")],aAutoExced[nX,nPQUANT,2],CND->CND_CLIENT,CND->CND_LOJACL,1,dDataBase))
						Aviso("CNTA120",STR0101 +aCols[nAux,nPITEM] +STR0103) //-- O item ### nЦo possui preГo para a quantidade excedente.
						lRet := .F.
					EndIf
				EndIf
			EndIf
	
			If lRet
				If (nTotLGetD := aScan(aCols, {|x| x[GDFieldPos("CNE_PRODUT")] == aCols[nAux,GDFieldPos("CNE_PRODUT")] .And.;
				   				 	 				x[GDFieldPos("CNE_TABPRC")] == cPesqTab .And.;
												  	x[GDFieldPos("CNE_EXCEDE")] == '1'})) == 0
					aAdd(aCols,Array(Len(aHeader) + 1))
					nTotLGetD := Len(aCols)
				EndIf
	
				//-- Inicializa campos da getdados
				For nY := 1 to Len(aHeader)
					If IsHeadRec(aHeader[nY,2]) .And. Empty(aCols[nTotLGetD,nY])
						aCols[nTotLGetD,nY] := 0
					ElseIf IsHeadAlias(aHeader[nY,2])
						aCols[nTotLGetD,nY] := "CNE"
					ElseIf AllTrim(aHeader[nY,2]) $ "CNE_DTENT/CNE_FLGCMS/CNE_TS"
						aCols[nTotLGetD,ny] := aCols[nAux,nY]
					Else
						aCols[nTotLGetD,nY] := CriaVar(aHeader[nY,2],.F.)
					EndIf
				Next nY
	
				//-- Preenche campos recebidos pela execauto
				For nY := 1 To Len(aAutoExced[nX])
					If aScan(aEditCamp,{|x| x == aAutoExced[nX,nY,1]}) > 0 .And. GDFieldPos(aAutoExced[nX,nY,1]) > 0
						If aAutoExced[nX,nY,1] # "CNE_QUANT" .And. !CheckSX3(aAutoExced[nX,nY,1],aAutoExced[nX,nY,2])
							lRet := .F.
							Exit
						Else
							aCols[nTotLGetD,GDFieldPos(aAutoExced[nX,nY,1])] := aAutoExced[nX,nY,2]
						EndIf
					EndIf
				Next nY
	
				//-- Preenche campos do sistema
				aCols[nTotLGetD,GDFieldPos("CNE_ITEM")]   := Soma1(aCols[Len(aCols) - 1,GDFieldPos("CNE_ITEM")])
				aCols[nTotLGetD,GDFieldPos("CNE_PRODUT")] := aCols[nAux,GDFieldPos("CNE_PRODUT")]
				aCols[nTotLGetD,GDFieldPos("CNE_VLUNIT")] := nPrcUnit
				aCols[nTotLGetD,GDFieldPos("CNE_VLTOT")]  := aCols[nTotLGetD,GDFieldPos("CNE_QUANT")] * aCols[nTotLGetD,GDFieldPos("CNE_VLUNIT")]
				aCols[nTotLGetD,GDFieldPos("CNE_EXCEDE")] := "1"
				aCols[nTotLGetD,GDFieldPos("CNE_TABPRC")] := cPesqTab
				aCols[nTotLGetD,GDFieldPos("CNE_PEDTIT")] := aCols[nAux,GDFieldPos("CNE_PEDTIT")]
				aCols[nTotLGetD,Len(aHeader)+1] := .F.
	
				n := nTotLGetD
				CN130TotMed(5)
			Else
				Exit
			EndIf
		Next nX
	ElseIf oGetDados:aCols[oGetDados:nAt,nPosiExce] == '1'
		Aviso("CNTA120",STR0100,{STR0046}) //-- Este recurso estА disponМvel apenas para itens previstos na planilha (nЦo excedentes).
	EndIf
Return lRet

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao	 Ё CN130ExVlЁ Autor Ё Joao Goncalves de Oliveira Ё Data Ё 22/02/11 Ё╠╠
╠╠цддддддддддеддддддддддадддддддаддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valida campo tabela de preГo ou quantidade e atualiza           Ё╠╠
╠╠Ё          Ё a informaГЦo do preГo unitАrio                                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe	 Ё CN130ExVl(cTipoCamp)  			          		          	   Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 - Tipo de Campo p/ validacao: T - Tabela de PreГos ou     Ё╠╠
╠╠Ё          Ё Q - Quantidade                                                  Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Retorno  Ё ExpL1 - Validade do campo    						   		   Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function CN130ExVl(cTipoCamp)
Local lValiCamp := .F.
Local nPosiQtde := aScan(aHeader,{|x| AllTrim(x[2]) == "CNE_QUANT"})
Local nQtdeMedi := aCols[oGetDados:nAt,nPosiQtde]

If cTipoCamp == "T"
	lValiCamp := ExistCpo("DA0",M->CNE_TABPRC,1)
	If M->CNE_QUANT > 0
		M->CNE_VLUNIT := MaTabPrVen(M->CNE_TABPRC,M->CNE_PRODUT,M->CNE_QUANT,CND->CND_CLIENT,CND->CND_LOJACL,1,dDataBase)
	EndIf
ElseIf cTipoCamp == "Q"
	lValiCamp := M->CNE_QUANT > 0
	If !Empty(M->CNE_TABPRC)
		M->CNE_VLUNIT := MaTabPrVen(M->CNE_TABPRC,M->CNE_PRODUT,M->CNE_QUANT,CND->CND_CLIENT,CND->CND_LOJACL,1,dDataBase)
	EndIf
EndIf

Return(lValiCamp)


//------------------------------------------------------------------
/*/{Protheus.doc} CN130ReprMT(lAuto,nLoop,aListBox,aMultas)
FunГЦo que processa a multa vinculada ao contrato


@author taniel.silva
@since 17/07/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CN130ReprMT(lAuto,nLoop,aListBox,aMultas)

Local cAliasQry  := ""
Local cQuery     := ""
local nValor     := 0
local nPos			:= 0

Default lAuto		:= .T.
Default nLoop		:= 0
Default aListBox	:= {}
Default aMultas	:= {}

If lAuto .Or. nLoop == 1
	cAliasQry := GetNextAlias()
	cQuery := ""

	cQuery += "SELECT CN4_CODIGO, CN4_DESCRI, CN4_VLDALT, CN4_VLRALT, CN4_VALID, CN4_FORMUL, CN4_TIPO, CNH_NUMERO, CN4_FLGPED "
	cQuery +="   FROM " +RetSqlName("CNH")+" CNH,"+RetSqlName("CN4")+" CN4 "
	cQuery += "  WHERE CNH_FILIAL	= '"+xFilial("CNH",cFilCTR)+"'"
	cQuery += "  AND CNH_NUMERO     = '"+M->CND_CONTRA+"' "
	cQuery += "  AND CNH_REVISA 	= '"+M->CND_REVISA+"' "
	cQuery += "  AND CNH.D_E_L_E_T_ = ' ' "
	cQuery += "  AND CNH_CODIGO     = CN4_CODIGO "
	cQuery += "  AND CN4_FILIAL     = '" +xFilial("CN4")+"'"
	cQuery += "  AND CN4.D_E_L_E_T_ = ' '"
	cQuery += "  ORDER BY CNH_NUMERO,CN4_CODIGO"

	cQuery := ChangeQuery( cQuery )
	dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasQry, .F., .T. )

	While !( cAliasQry )->( Eof() )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Avalia a aplicacao da multa                                            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If Empty( ( cAliasQry )->CN4_VLDALT )
			lFormula := Formula(( cAliasQry )->CN4_VALID )
		Else
	   		lFormula := CNTA130Form( ( cAliasQry )->CN4_VLDALT )
	   		If ValType(lFormula) == 'U'
				lFormula := .F.
			EndIf
		EndIf

		If lFormula
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Obtem o valor da multa                                                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If Empty( ( cAliasQry )->CN4_VLRALT )
				nValor := Formula( ( cAliasQry )->CN4_FORMUL )
			Else
				nValor := CNTA130Form( ( cAliasQry )->CN4_VLRALT)
				If ValType(nValor) == 'U'
					nValor := 0
				EndIf
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Monta List Box da MediГЦo							                   Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If !lAuto
				nPos := aScan (aListBox, {|x| x[7]==(cAliasQRY)->CN4_CODIGO})
				If nPos == 0
					AAdd( aListBox, { .F., (cAliasQRY)->CN4_TIPO, (cAliasQRY)->CN4_DESCRI, nValor, "1", (cAliasQRY)->CN4_FLGPED, (cAliasQRY)->CN4_CODIGO} )
				Else
					aListBox[nPos,4]:= aListBox[nPos,4]+nValor
				EndIf
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Carrega as multas do array                                             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			aAdd(aMultas,Array(6))
			aTail(aMultas)[1] := (cAliasQRY)->CN4_TIPO
			aTail(aMultas)[2] := (cAliasQRY)->CN4_DESCRI
			aTail(aMultas)[3] := nValor
			aTail(aMultas)[4] := '1'
			aTail(aMultas)[5] := (cAliasQRY)->CN4_FLGPED
			aTail(aMultas)[6] := (cAliasQRY)->CN4_CODIGO

			//Verifica se И multa ou bonificaГЦo
			If (cAliasQRY)->CN4_TIPO == '1'
				nTotMultas += nValor
			Else
				nTotBoni += nValor
			EndIf

		EndIf
		( cAliasQry )->( dbSkip() )
	EndDo

	( cAliasQry )->( dbCloseArea() )

EndIf

Return nTotMultas


//------------------------------------------------------------------
/*/{Protheus.doc} LRDVldFormula(cExpr)
FunГЦo que retorna erro na execuГЦo da macro


@author taniel.silva
@since 17/07/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CNTA130Form(cExpr)
Local oError  := ErrorBlock( { |e| ChecErro( e ) } )
Local xRet

If !Empty(cExpr)
	Begin Sequence
		xRet := &(AllTrim(cExpr))
	End Sequence
EndIf

ErrorBlock(oError)

Return xRet

//------------------------------------------------------------------
/*/{Protheus.doc} CnRetAhd(cCampo, aHeader)
FunГЦo que retorna um array para ser acrescentado em um aheader


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CnRetAhd(cCampo, aHdr)
Local aArea 	:= GetArea()

DbSelectArea("SX3")
DbSetOrder(2)

If DbSeek(cCampo)
	AADD(aHdr,{ TRIM(X3_TITULO), X3_CAMPO, X3_PICTURE,;
			X3_TAMANHO, X3_DECIMAL,"AllwaysTrue()",;
			X3_USADO, X3_TIPO, X3_ARQUIVO, X3_CONTEXT})
EndIf
RestArea(aArea)

Return Nil

//------------------------------------------------------------------
/*/{Protheus.doc} CnRetAhd(cCampo, aHeader)
Valid do campo CNX_VLCOMP


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CnVlComp()
Local lRet := .T.
Local nPosSldAt := aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_SLDATU"})
Local nPosVlComp := aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_VLCOMP"})
Local nPosSld 	:= aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_SALDO"})

If M->CNX_VLCOMP > aColsAux[n,1]
	Help(" ",1,"CN130NVLCOMP")
	lRet := .F.
EndIf

If lRet
	oGetAdia:Acols[n,nPosSld] := aColsAux[n,1] - M->CNX_VLCOMP
	oGetAdia:Refresh()
EndIf

Return lRet

//------------------------------------------------------------------
/*/{Protheus.doc} CnRetTotAd()
FunГЦo que retorna o total dos adiantamentos


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CnRetTotAd()
Local nI 		:= 0
Local nTotal 	:= 0
Local aAdts	:= {}
Local nNumero	:= aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_NUMERO"})
Local nVlr		:= aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_VLCOMP"})

For nI := 1 To Len(oGetAdia:Acols)
	If oGetAdia:Acols[nI, nVlr] > 0
		Aadd(aAdts, {oGetAdia:Acols[nI, nNumero], oGetAdia:Acols[nI, nVlr]})
	EndIf
Next nI

Return aAdts

//------------------------------------------------------------------
/*/{Protheus.doc} CnRetTotAd()
FunГЦo que retorna o total dos adiantamentos


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CnGetTotad()
Local nI := 0
Local nTotal := 0
Local nVlr			:= aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_VLCOMP"})

For nI := 1 To Len(oGetAdia:Acols)
	If oGetAdia:Acols[nI, nVlr] > 0
		nTotal += oGetAdia:Acols[nI, aScan(oGetAdia:Aheader,{|x| AllTrim(x[2])=="CNX_VLCOMP"})]
	EndIf
Next nI

Return nTotal

//------------------------------------------------------------------
/*/{Protheus.doc} CnGrvAdia(aAdiants,nOpc, cNumAd)


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CnGrvAdia(aAdiants,nOpc, cNumAd)
Local aArea := GetArea()
Local oModel	:= Nil
Local cContra	:= ""
Local cRevisa	:= ""
Local cNumMed	:= ""
Local cNumPla	:= ""
Local cFilMed   := ""
Local aAdts		:= {}
Local cQuery	:= ""
Local cNumero := ""
Local nValor:= 0
Local nDif	:= 0
Local nI 		:= 0
Local lInc := .F.
Local lAltVlr	:= .F.
Local lCn121	:= IsInCallStacK('CNTA121') .And. !Cn121OriMd('CNTA120')
Local lFilCps := CZY->(FieldPos("CZY_FILCTR")> 0 .And. FieldPos("CZY_FILMED") > 0)

Default cNumAd := ""

If lCn121
	oModel	:= FwModelActive()
	cContra	:= oModel:GetValue("CNDMASTER", "CND_CONTRA")
	cRevisa	:= oModel:GetValue("CNDMASTER", "CND_REVISA")
	cNumMed	:= oModel:GetValue("CNDMASTER", "CND_NUMMED")
	cFilMed := oModel:GetValue("CNDMASTER", "CND_FILMED")
	cNumPla := oModel:GetValue("CXNDETAIL", "CXN_NUMPLA")
	nOpc 	:= oModel:GetOperation()
Else
	cContra := CND->CND_CONTRA
	cRevisa := CND->CND_REVISA
	cNumMed := CND->CND_NUMMED
	cNumPla := CND->CND_NUMERO
	cFilMed := cFilAnt
EndIf

If nOpc <> MODEL_OPERATION_DELETE .And. Empty(cNumAd)
	For nI := 1 To Len(aAdiants)
		lInc    := .F.
		lAltVlr    := .F.
		cNumero := aAdiants[nI,1]
		nValor  := aAdiants[nI,2]

		If nOpc == MODEL_OPERATION_INSERT
			DbSelectArea("CZY")
			CZY->(DbsetOrder(2))
			If !CZY->(MsSeek(xFilial("CZY",cFilMed)+cContra+cRevisa+cNumMed+cNumPla+cNumero))
				RecLock( "CZY", .T. )
				CZY->CZY_FILIAL := xFilial("CZY")
				CZY->CZY_CONTRA := cContra
				CZY->CZY_REVISA := cRevisa
				CZY->CZY_NUMMED := cNumMed
				CZY->CZY_NUMPLA := cNumPla
				CZY->CZY_NUMERO := cNumero
				CZY->CZY_VALOR  := nValor
				If lFilCps
						CZY->CZY_FILCTR := cFilCtr
						CZY->CZY_FILMED := cFilMed
					EndIf 
				CZY->( MsUnlock() )

				DbSelectArea("CNX")
				CNX->(DbsetOrder(1))
				If CNX->(MsSeek(xFilial("CNX",cFilCTR)+cContra+cNumero))
					Reclock("CNX",.F.)
					CNX->CNX_SALDO := CNX->CNX_SALDO - nValor
					CNX->(MsUnlock())
				Endif
			EndIf
		ElseIf nOpc == MODEL_OPERATION_UPDATE .Or. IIf(lCn121,IsInCallStack('CN121Encerr'),nOpc == 13)
			DbSelectArea("CZY")
			CZY->(DbsetOrder(2))
			If CZY->(MsSeek(xFilial("CZY",cFilMed)+cContra+cRevisa+cNumMed+cNumPla+cNumero))
				If 	nValor <> CZY->CZY_VALOR
					lAltVlr := .T.
					nDif := nValor - CZY->CZY_VALOR
					RecLock( "CZY", .F. )
					CZY->CZY_VALOR  := nValor
					CZY->( MsUnlock() )
				EndIf
			Else
				lInc := .T.
				//-- Caso seja alteraГЦo e nЦo encontrei o registro, inclui.
				RecLock( "CZY", .T. )
				CZY->CZY_FILIAL := xFilial("CZY")
				CZY->CZY_CONTRA := cContra
				CZY->CZY_REVISA := cRevisa
				CZY->CZY_NUMMED := cNumMed
				CZY->CZY_NUMPLA := cNumPla
				CZY->CZY_NUMERO := cNumero
				CZY->CZY_VALOR  := nValor
				If lFilCps
						CZY->CZY_FILCTR := cFilCtr
						CZY->CZY_FILMED := cFilMed
					EndIf 
				CZY->( MsUnlock() )
			EndIf

			DbSelectArea("CNX")
			CNX->(DbsetOrder(1))
			If CNX->(MsSeek(xFilial("CNX",cFilCtr)+cContra+cNumero))
				Reclock("CNX",.F.)
				If lInc
					CNX->CNX_SALDO := CNX->CNX_SALDO - nValor
				ElseIf lAltVlr
					CNX->CNX_SALDO := CNX->CNX_SALDO - nDif
				EndIf
				CNX->(MsUnlock())
			EndIf
		EndIf
	Next nI
Else
	cQuery := " SELECT CZY.R_E_C_N_O_ as RECNO,CZY_NUMERO, CZY.CZY_VALOR "
	cQuery += " FROM "+RetSQLName("CZY")+" CZY "
	cQuery += " WHERE CZY.CZY_FILIAL = '"+xFilial("CND",cFilMed)+"'"
	cQuery += " AND CZY.CZY_CONTRA = '"+cContra+"'"
	cQuery += " AND CZY.CZY_NUMMED = '"+cNumMed+"'"
	cQuery += " AND CZY.D_E_L_E_T_ = ''"
	If !Empty(cNumAd)
		cQuery += " AND CZY.CZY_NUMERO = '"+cNumAd+"'"
	EndIf
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCZY", .f., .t. )
	dbSelectArea("CZY")
	While !TRBCZY->(Eof())
		dbGoto(TRBCZY->RECNO)
		Aadd(aAdts, {TRBCZY->CZY_NUMERO, TRBCZY->CZY_VALOR})
		RecLock("CZY", .F.)
		dbDelete()
		CZY->(MsUnlock())
		TRBCZY->(dbSkip())
	EndDo

	//Se nЦo encontrar adiantamento na mediГЦo, nЦo И necessАrio atualizar saldos.
	If Len(aAdts) == 0
		TRBCZY->(dbCloseArea())
		Return
	EndIf

	cQuery := ""
	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Filtra Adiantamentos                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	cQuery := "SELECT CNX.R_E_C_N_O_ as RECNO "
	cQuery += "  FROM "+RetSQLName("CNX")+" CNX "
	cQuery += " WHERE CNX.CNX_FILIAL = '"+xFilial("CNX",cFilCTR)+"'"
	cQuery += "   AND CNX.CNX_CONTRA = '"+cContra+"'"
	cQuery += "   AND CNX.D_E_L_E_T_ = ''"
	If !Empty(cNumAd)
		cQuery += " AND CNX.CNX_NUMERO = '"+cNumAd+"'"
	EndIf

	cQuery := ChangeQuery(cQuery)
	dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), "TRBCNX", .f., .t. )

	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza os Descontos                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды

	dbSelectArea("CNX")
	While !TRBCNX->(Eof())
		dbGoto(TRBCNX->RECNO)
		nPos := aScan( aAdts, {|x| AllTrim(x[1]) == CNX->CNX_NUMERO  } )
		If nPos > 0
			RecLock("CNX", .F.)
			CNX->CNX_SALDO := CNX->CNX_SALDO + aAdts[nPos, 2]
			CNX->(MsUnlock())
		Endif
		TRBCNX->(dbSkip())
	EndDo

	TRBCZY->(dbCloseArea())
	TRBCNX->(dbCloseArea())
EndIf

RestArea(aArea)

Return

//------------------------------------------------------------------
/*/{Protheus.doc} CNAdTdOk()
TudoOk da GetDados de Adiantamentos


@author Matheus Lando Raimundo
@since 19/12/2014
@version P12
/*/
//-------------------------------------------------------------------
Function CNAdTdOk()
Local lRet		:= .T.
Local nNum		:= 0

nNum := CnGetTotad()

If nNum > M->CND_VLTOT
	Aviso (STR0056, STR0072, { STR0058 }, 2 ) // "Atencao!", "Os Adiantamentos nЦo podem ultrapassar o total das mediГУes/entregas", { "Ok" }
	lRet := .F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}CN130MdEmp
FunГЦo que retorna os itens vinculado a nota de empenho.

@author taniel.silva
@since 22/01/2015
@version P12
/*/
//-------------------------------------------------------------------
Function CN130MdEmp(aCols,aMedEmp)
Local nPosCodNE		:= GDFIELDPOS("CNE_CODNE")
Local nPosItemNE	:= GDFIELDPOS("CNE_ITEMNE")
Local nPosProd  	:= GDFIELDPOS("CNE_PRODUT")
Local nPosVlT   	:= GDFIELDPOS("CNE_VLTOT ")
Local nPos			:= 0
Local nX			:= 0

If !Empty(aCols)
	For nX := 1 To Len(aCols)
		If !Empty(aCols[nX][nPosCodNE]) .And. !GdDeleted(nX)
			If !Empty(aMedEmp)
				nPos := aScan(aMedEmp,{|x| AllTrim(x[1]) + AllTrim(x[2]) + AllTrim(x[3]) == AllTrim(aCols[nX][nPosProd]) + AllTrim(aCols[nX][nPosCodNE]) + AllTrim(aCols[nX][nPosItemNE])})
				If nPos > 0
					aMedEmp[nPos][4] += aCols[nX][nPosVlT]
				Else
					Aadd(aMedEmp,{aCols[nX][nPosProd],aCols[nX][nPosCodNE],aCols[nX][nPosItemNE],aCols[nX][nPosVlT]})
				EndIf
			Else
				Aadd(aMedEmp,{aCols[nX][nPosProd],aCols[nX][nPosCodNE],aCols[nX][nPosItemNE],aCols[nX][nPosVlT]})
			EndIf
		EndIf
	Next nX
EndIf

Return nil
//------------------------------------------------------------------
/*/{Protheus.doc} CN130VlNE()
Funcao para Verificar se a nota de empenho informada possui relacao com o Edital.

@author Bruno Amate Schmidt
@since 20/07/2015
@version P12
/*/
//-------------------------------------------------------------------
Function CN130VlNE()

Local aArea		:= GetArea()
Local cCodEdt		:= ""
Local cNumProc	:= ""
Local cNumAta		:= ""
Local lRet		:= .T.
Local nCodNe 		:= 0
Local nCodPrd		:= 0

CN9->(DbSetOrder(1))
CN9->(DbSeek(xFilial("CN9")+M->CND_CONTRA+M->CND_REVISA))

cCodEdt	:= CN9->CN9_CODED
cNumProc	:= CN9->CN9_NUMPR
cNumAta	:= CN9->CN9_NUMATA

nCodNe		:=	aScan(aHeader,{|x| Trim(x[2])=="CNE_CODNE"})
nCodPrd	:= aScan(aHeader,{|x| Trim(x[2])=="CNE_PRODUT"})

If !Empty(cCodEdt)
	If !Empty(M->CNE_CODNE)
		//-- Verifica se a nota de empenho informada existe
		CX0->(DbSetOrder(1))
		If !CX0->(DbSeek(xFilial("CX0")+M->CNE_CODNE))
			Help("",1,"A300SNEED",,STR0111,4,1)
			lRet := .F.
		EndIf
		//-- Verifica se a nota de empenho informada tem relacao com o Edital informado
		CX0->(DbSetOrder(2))
		If lRet .and. CX0->(DbSeek(xFilial("CX0")+cCodEdt+cNumProc+cNumAta))
			If M->CNE_CODNE = CX0->CX0_CODNE
				lRet := .T.
			Else
				Help("",1,"A300SNEED",,STR0112,4,1)
				lRet := .F.
			EndIf
		EndIf
	EndIf
	//-- Verifica se o Item informado realmente existe para o produto do aCols
	If lRet .and. !Empty(M->CNE_ITEMNE) .and. !Empty(Acols[n][nCodNe])
		CX1->(DbSetOrder(2))
		If !CX1->(DbSeek(xFilial("CX1")+Acols[n][nCodNe]+M->CNE_ITEMNE+Acols[n][nCodPrd]))
			Help("",1,"A300SLDITNE",,STR0113,4,1)
			lRet := .F.
		EndIf
	EndIf
EndIf

RestArea(aArea)

Return lRet

//FunГЦo para avaliar se o gatilho da CNE_VLUNIT serА executado ou nЦo
Function CN130GATCNE(nSec)
Local lRet := .F.

//Verifica a sequencia e se os campos utilizados estЦo preenchidos
Do Case
	Case nSec == 3
		If !Empty(M->CNE_VLTOT) .And. !Empty(M->CNE_VUNORI)
			lRet := .T.
		EndIf
	Case nSec == 4
		If !Empty(M->CNE_QTDORI) .And. !Empty(M->CNE_QTDSOL)
			lRet := .T.
		EndIf
End Case

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}CN130Liq
Calcula o valor liquido dos itens

@author jose.delmondes
@since 21/11/2017
@version P12
/*/
//-------------------------------------------------------------------
Function CN130Liq(cEspCtr, nTotItem, nDescItem, nTotMed, nTotMulta, nTotBoni, nTotDesc, nTotCauc, nTotAdi)
Local nValLiq	:= 0	//-- Valor liquido do item
Local nPercent	:= 0	//-- Percentual que o valor total do item representa sobre o valor total da medicao
Local nMulta	:= 0	//-- Valor proporcional de multa
Local nBonifi	:= 0	//-- Valor proporcional de bonificacao
Local nDescMed	:= 0	//-- Valor proporcional do desconto da medicao
Local nCaucao	:= 0	//-- Valor proporcional do caucao retido
Local nAdiant	:= 0	//-- Valor proporcional do adiantamento

nPercent	:= (nTotItem - nDescItem) / nTotMed
nMulta		:= nTotMulta * nPercent
nBonifi		:= nTotBoni * nPercent
nDescMed	:= nTotDesc * nPercent
nCaucao		:= nTotCauc * nPercent
nAdiant		:= nTotAdi * nPercent

nValLiq := nTotItem - nDescItem - nDescMed - nCaucao - nAdiant

If cEspCtr == '1'
	nValLiq +=  nBonifi - nMulta	//-- Para contratos de compra, soma a bonificacao e subtrai a multa
Else
	nValLiq +=  nMulta - nBonifi 	//-- Para contratos de venda, soma a multa e subtrai a bonificacao
EndIf

Return nValLiq

//-------------------------------------------------------------------
/*/{Protheus.doc} ChkProd
Verifica se o produto existe no Array

@author juan.felipe
@since 20/02/2019
@version P12
@return lReturn, lСgico, .T. caso encontre o produto
@param aLinha, array, contИm os produtos da planilha
@param cProduto, string, produto validado no array
@type function
/*/
//-------------------------------------------------------------------
Static Function ChkProd(aLinha As Array, cProduto As Char, cItemCNB as Char) As Logical
	Local lReturn As Logical
	Local nPosProd As Numeric
	Local nPosItCNB as Numeric  

	nPosProd := aScan(aLinha,{|x| AllTrim(x[1]) == 'CNE_PRODUT'})
	nPosItCNB:= aScan(aLinha,{|x| AllTrim(x[1]) == 'CNE_ITEM'})
	
	lReturn := .F.

	If (nPosProd > 0) .And. (nPosItCNB > 0) 
		lReturn := AllTrim(aLinha[nPosProd,2])+aLinha[nPosItCNB,2] == AllTrim(cProduto)+cItemCNB
	EndIf
	
Return lReturn

/*/{Protheus.doc} GetCNEEnch
	Dado <aExibCamp>, retorna <aCampEnch> contendo campos a serem
	exibidos na enchoice da tela de Excedentes. 
@author PHILIPE.POMPEU
@since 03/05/2019
@return aCampEnch, array, contem campos a serem exibidos na Enchoice
@param aExibCamp, array, campos a serem analisados
/*/
Static Function GetCNEEnch(aExibCamp As Array) As Array
	Local aArea := SX3->(GetArea())
	Local aCampEnch := {}
	Local cField := ""
	Local nAux := 0
	Local nX := 0
	Local cValid := ""
	Local cWhen := ""
		
	SX3->(dbSetOrder(2))  // X3_CAMPO
	For nX := 1 to Len(aExibCamp)
		cField := PadR(aExibCamp[nX], Len(SX3->X3_CAMPO))
		If SX3->(dbSeek(cField))			
			If SX3->( X3Uso(X3_USADO) .And. cNivel >= X3_NIVEL )
				cField := AllTrim(SX3->X3_CAMPO)
				aAdd(aCampEnch,{ X3Titulo()		,;
								 cField			,;
								 SX3->X3_TIPO	,;
								 SX3->X3_TAMANHO,;
								 SX3->X3_DECIMAL,;
								 SX3->X3_PICTURE,,;
								 .F.			,;
								 SX3->X3_NIVEL	,;
								 SX3->X3_RELACAO,;
								 SX3->X3_F3,,	 ;
								 .F.,			 ;
								 .F.,			 ;
								 SX3->X3_CBOX	,;
								 SX3->X3_FOLDER	,;
								 .F.			,;
								 SX3->X3_PICTVAR,;
								 SX3->X3_TRIGGER} )				
				
				nAux := Len(aCampEnch)
				
				If !Empty( SX3->X3_VALID )
					cValid := "{||" + RTrim(SX3->X3_VALID) + "}"
					aCampEnch[nAux,7] := &(cValid)
				EndIf				
				If !Empty( SX3->X3_WHEN )
					cWhen := "{||" + RTrim(SX3->X3_WHEN) + "}"
					aCampEnch[nAux,12] := &(cWhen)
				EndIf
				
				//Define a validacao do campo
				If cField == "CNE_TABPRC"
					aCampEnch[nAux][7] := &( "{ || CN130ExVl('T')}" )
				ElseIf cField == "CNE_QUANT"
					aCampEnch[nAux,7] := &( "{ || CN130ExVl('Q')}" )
					aCampEnch[nAux,19] := "N"
				EndIf
	
				//Checa se o campo eh visual
				If cField $ "CNE_ITEM,CNE_PRODUT,CNE_DESCRI"
					aCampEnch[nAux,13] := .T.
				EndIf
								
				//Checa se o campo eh obrigatorio ou eh reservado
				If ((X3Obrigat(cField)) .Or. (cField $ "CNE_TABPRC,CNE_QUANT,CNE_VLUNIT,CNE_FLGCMS"))
					aCampEnch[nAux][8] := .T.
				Endif
			Endif
		Endif
	Next nX
	RestArea(aArea)
	aSize(aArea,0)
Return aCampEnch
