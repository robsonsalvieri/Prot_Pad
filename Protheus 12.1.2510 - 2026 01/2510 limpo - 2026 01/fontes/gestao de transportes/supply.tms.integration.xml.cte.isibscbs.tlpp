#include "tlpp-core.th"
#include "tlpp-rest.th"

#define IBS_ESTADUAL        '000060'
#define IBS_MUNICIPAL       '000061'
#define CBS_FEDERAL         '000062'
#define IMPOSTO_SELETIVO    '000063'
#define IBS_EST_REGULAR     '000064'
#define IBS_MUN_REGULAR     '000065'
#define CBS_REGULAR         '000066'
#define IBS_CRED_PRESUMIDO  '000067'
#define CBS_CRED_PRESUMIDO  '000068'

static lExistCJ3_CCT := len(FWSX3Util():GetFieldStruct('CJ3_CCT')) > 0 as logical

namespace totvs.protheus.supply.tms.integration
using namespace totvs.protheus.backoffice.fiscal.tciclass

/*/{Protheus.doc} IBSCBSCTE
Classe responsável por gerar no XML para o grupo UB - Informações dos tributos IBS / CBS e Imposto Seletivo da NFe/NFCe.
@type class
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Class TaxInformation

	//Propriedades
	public  data nTotalBaseIbsCbs as numeric
    public  data aItemIdTributo as array
	private data oTaxByTotvsId as object
	public  data oTotalTaxByTotvsId as object

	//Metodos get
	public  method New() as object
	public  method getXmlIBSCBS() as character
    public  method getXmlvTPrest() as numeric 
	private method getXmlgIBSCBS() as character
    private method getXmlgIBSUF(oStateTax as json, nTotalIbs as numeric) as character
	private method getXmlgIBSMun(oMunicipalTax as json, nTotalIbs as numeric) as character
    private method getXmlgCBS(oTax as json) as character

	//metodos set
    private method setDocumentItemId()
	private method setDocumentItemTaxes()
	private method setTotalBaseIbsCbs(nBaseIbsCbs as numeric)
	public method Destroy()

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe TaxInformation, recebe o id do documento que 
será utilizado para gerar as tags de tributos.
@type method
@version  P12 V1.2410
@param cDocumentItemId, Id do documento atual
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Method New() as object class TaxInformation

	::oTaxByTotvsId      := JsonObject():New()
	::oTotalTaxByTotvsId := JsonObject():New()

Return self

/*/{Protheus.doc}
Instacia a classe que retorna as inforamações dos impostos da reforma 
tributária e seta o id do item do documento que esta sendo transmitido
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 30/05/2025
/*/
Method setDocumentItemId(aDocumentItemId) class TaxInformation
    Local oDocumentData as object
    
    //Só carrega as informações se for um novo item, caso contrario usa os dados já carregados anteriormente.
    if !::oTaxByTotvsId:HasProperty(aDocumentItemId)
        
        //Instacia classe e seta o id do item do documento
        oDocumentData := TCIWritten():New()
        oDocumentData:setId(aDocumentItemId)
        
        //Recupera e formata os dados do id informado
        self:setDocumentItemTaxes(aDocumentItemId, oDocumentData:getDataId())
        
        //Libera os recursos utilizados pela instância da classe
        oDocumentData:destroy()
    endif 

return 

/*/{Protheus.doc} IBSCBSBPE::GeraIBSCBS
Gera tags dos imposto IBS e CBS.
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Method getXmlIBSCBS() as character class TaxInformation
	Local lGeraXml as logical
	Local cXmlCbsIbs as character
	Local cCst as character
	Local cClassTrib as character
	Local nBaseIbsCbs as numeric
    Local x as integer

	Private oJsCbs as json
	Private oJsIbsEstadual  as json
	Private oJsIbsMunicipal as json
	Private oJsIbsCreditoPresumido as json
	Private oJsCbsCreditoPresumido as json
    
    self:setDocumentItemId(aClone(self:aItemIdTributo))

    for x := 1 to len(::aItemIdTributo)

        if ::oTaxByTotvsId[::aItemIdTributo[x]]:HasProperty(IBS_ESTADUAL) .or. ::oTaxByTotvsId[::aItemIdTributo[x]]:HasProperty(IBS_MUNICIPAL)

            if ::oTaxByTotvsId[::aItemIdTributo[x]]:HasProperty(IBS_ESTADUAL)
				oJsIbsEstadual  := ::oTaxByTotvsId[::aItemIdTributo[x]][IBS_ESTADUAL]
            endIf
			if ::oTaxByTotvsId[::aItemIdTributo[x]]:HasProperty(IBS_MUNICIPAL)
				oJsIbsMunicipal := ::oTaxByTotvsId[::aItemIdTributo[x]][IBS_MUNICIPAL]
			endif

            //Pego do estadual pois sempre existirão os dois tributos(Estadual e municipal)
            nBaseIbsCbs := iif(oJsIbsEstadual != nil, oJsIbsEstadual['base_tributo'], oJsIbsMunicipal['base_tributo'])
            self:setTotalBaseIbsCbs(nBaseIbsCbs)

            cCst := iif(oJsIbsEstadual != nil, oJsIbsEstadual['dados_escriturados']['cst'], oJsIbsMunicipal['dados_escriturados']['cst'])

            //Proteção para o caso de não existir o campo CJ3_CCT
            if lExistCJ3_CCT
                cClassTrib := iif(oJsIbsEstadual != nil, oJsIbsEstadual['dados_escriturados']['cclasstrib'], oJsIbsMunicipal['dados_escriturados']['cclasstrib'])
            endif

            lGeraXml := .t.
        endif

        if ::oTaxByTotvsId[::aItemIdTributo[x]]:HasProperty(CBS_FEDERAL)

            oJsCbs := ::oTaxByTotvsId[::aItemIdTributo[x]][CBS_FEDERAL]

            if nBaseIbsCbs = 0
                nBaseIbsCbs := oJsCbs['base_tributo']
                self:setTotalBaseIbsCbs(nBaseIbsCbs)
            endif

            if empty(cCst) .and. empty(cClassTrib)
                cCst := oJsCbs['dados_escriturados']['cst']

                //Proteção para o caso de não existir o campo CJ3_CCT
                if lExistCJ3_CCT
                    cClassTrib := oJsCbs['dados_escriturados']['cclasstrib']
                endif
            endif

            lGeraXml := .t.
        endif

    next

	if lGeraXml
		cXmlCbsIbs += 	'<IBSCBS>'
    	cXmlCbsIbs +=      '<CST>' + cCst + '</CST>'
		cXmlCbsIbs +=         '<cClassTrib>' + cClassTrib + '</cClassTrib>'
							   If  AllTrim(cCst) <> '410' //410 - Imunidade e não incidência. Grupo de informações específicas do IBS/CBS, este grupo NÂO DEVE estar informado (grupo: gIBSCBS)
		cXmlCbsIbs +=               self:getXmlgIBSCBS(self:nTotalBaseIbsCbs)
							   EndIf
		cXmlCbsIbs += 	'</IBSCBS>'
	endif

	//Verificar se os Jsons criados devem ser liberados da memória

Return cXmlCbsIbs

/*/{Protheus.doc}
Pega o retorno da classe TCIWritten com os dados dos impostos e formata
em um json para uso na geração do XML do CTe.
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Method setDocumentItemTaxes(aDocumentItemId as array, cDocumentItemTaxes as character ) class TaxInformation
    Local oDocumentItemId := JsonObject():New() as json
    Local aIdsTrib := {} as array
    Local cIdTotvsTrib as character
    Local cDocumentItemId as character
    Local i as integer
    Local x as integer
    
    oDocumentItemId:fromJson(cDocumentItemTaxes) 

    for x := 1 to len(aDocumentItemId)

        cDocumentItemId := aDocumentItemId[x]

        aIdsTrib := oDocumentItemId['dados_Id'][cDocumentItemId]:GetNames()

        ::oTaxByTotvsId[cDocumentItemId] := JsonObject():New()
        for i := 1 to Len(aIdsTrib)
            if upper(aIdsTrib[i]) == 'AVISO' //Id não calculado pelo configurador de tributos
                exit
            endif
            cIdTotvsTrib := oDocumentItemId['dados_Id'][cDocumentItemId][aIdsTrib[i]]['codigo_tributo_relacionado']
            ::oTaxByTotvsId[cDocumentItemId][cIdTotvsTrib] := oDocumentItemId['dados_Id'][cDocumentItemId][aIdsTrib[i]]

        next    

    next

    FwFreeArray(aIdsTrib)

return

/*/{Protheus.doc} setTotalBaseIbsCbs
	Soma a base de calculo dos impostos IBS e CBS por item
	@type method
	@author Fabio Marchiori Sampaio
	@since 25/08/2025
/*/
Method setTotalBaseIbsCbs(cValorBaseItem as numeric) class TaxInformation
    ::nTotalBaseIbsCbs += cValorBaseItem

return

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSCBS do grupo IBSCBS
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Method getXmlgIBSCBS(nBaseIbsCbs as numeric) as character class TaxInformation
	Local cXml as character
	Local nTotalIbs as numeric
	Local x as numeric

	for x := 1 to len(self:aItemIdTributo)

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_ESTADUAL) 
			nTotalIbs += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_ESTADUAL]['valor_tributo']
		EndIf

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_MUNICIPAL) 
			nTotalIbs += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_MUNICIPAL]['valor_tributo']
		EndIf

	next

	cXml += '<gIBSCBS>'
	cXml +=     '<vBC>' + ltrim(str(nBaseIbsCbs,13,2)) + '</vBC>'
	cXml +=     self:getXmlgIBSUF(oJsIbsEstadual)
	cXml +=     self:getXmlgIBSMun(oJsIbsMunicipal)
	cXml +=     '<vIBS>' + lTrim(str(nTotalIbs,13,2)) + '</vIBS>'
	cXml +=     self:getXmlgCBS(oJsCbs)
	cXml += '</gIBSCBS>'
return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSUF do grupo gIbSCBS
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 27/08/2025
/*/
Method getXmlgIBSUF(oStateTax as json) as character class TaxInformation
	Local cXml as character
	Local x as numeric
	Local nTotalIbs as numeric

	for x := 1 to len(self:aItemIdTributo)

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_ESTADUAL) 
			nTotalIbs += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_ESTADUAL]['valor_tributo']
		EndIf
	next

	if oStateTax != nil
		cXml += '<gIBSUF>'
		cXml +=     '<pIBSUF>' + ltrim(str(oStateTax['aliquota_tributo'],8,4)) + '</pIBSUF>'
		cXml +=     '<vIBSUF>' + ltrim(str(nTotalIbs,16,2)) + '</vIBSUF>'
		cXml += '</gIBSUF>'
	endif

return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSMun() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 22/05/2025
/*/
Method getXmlgIBSMun(oMunicipalTax as json) as character class TaxInformation
	Local cXml as character
	Local x as numeric
	Local nTotalIbs as numeric

	for x := 1 to len(self:aItemIdTributo)

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_MUNICIPAL) 
			nTotalIbs += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_MUNICIPAL]['valor_tributo']
		EndIf
	
	next

	if oMunicipalTax != nil
		cXml += '<gIBSMun>'
		cXml +=     '<pIBSMun>' + ltrim(str(oMunicipalTax['aliquota_tributo'],8,4)) + '</pIBSMun>'
		cXml +=     '<vIBSMun>' + ltrim(str(nTotalIbs,13,2)) + '</vIBSMun>'
		cXml += '</gIBSMun>'
	endif

return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gCBS() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 22/05/2025
/*/
Method getXmlgCBS(oTax as json) as character class TaxInformation
	Local cXml as character
	Local nTotalCbs as numeric
	Local x as numeric

	for x := 1 to len(self:aItemIdTributo)
		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(CBS_FEDERAL) 
			nTotalCbs += ::oTaxByTotvsId[self:aItemIdTributo[x]][CBS_FEDERAL]['valor_tributo']
		EndIf
	next

	if oTax != nil
		cXml += '<gCBS>'
		cXml +=     '<pCBS>' + ltrim(str(oTax['aliquota_tributo'],8,4)) + '</pCBS>'
		cXml +=     '<vCBS>' + ltrim(str(nTotalCbs,13,2)) + '</vCBS>'
		cXml += '</gCBS>
	endif

return cXml

/*/{Protheus.doc} 
Gera tags do Valor total do documento fiscal, vTPrest + total do IBS + total da CBS
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 22/05/2025
/*/
Method getXmlvTPrest() as numeric class TaxInformation
    Local nValIBSCBS as numeric
	Local x as numeric
    	
	for x := 1 to len(self:aItemIdTributo)

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_ESTADUAL) //-- '000060'
			nValIBSCBS += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_ESTADUAL]['valor_tributo']
		EndIf

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(IBS_MUNICIPAL) //-- '000061'
			nValIBSCBS += ::oTaxByTotvsId[self:aItemIdTributo[x]][IBS_MUNICIPAL]['valor_tributo']
		EndIf

		If ::oTaxByTotvsId[self:aItemIdTributo[x]]:HasProperty(CBS_FEDERAL) // -- '000062'
			nValIBSCBS += ::oTaxByTotvsId[self:aItemIdTributo[x]][CBS_FEDERAL]['valor_tributo']
		EndIf

	next
 
return nValIBSCBS

/*/{Protheus.doc}
Libera os recursos utilizados pela instância da classe
@type method
@version  P12 V1.2410
@author Fabio Marchiori Sampaio
@since 26/08/2025
/*/
Method Destroy() class TaxInformation
    FreeObj(::oTaxByTotvsId)
    FreeObj(::oTotalTaxByTotvsId)
    ::oTaxByTotvsId := nil
    ::oTotalTaxByTotvsId := nil
Return
