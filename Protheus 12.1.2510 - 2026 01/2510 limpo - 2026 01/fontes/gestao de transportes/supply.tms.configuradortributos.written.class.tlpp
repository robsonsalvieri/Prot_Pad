#include 'tlpp-core.th'
#include "protheus.ch"

#define IBS_ESTADUAL        '000060'
#define IBS_MUNICIPAL       '000061'
#define CBS_FEDERAL         '000062'
#define IMPOSTO_SELETIVO    '000063'
#define IBS_EST_REGULAR     '000064'
#define IBS_MUN_REGULAR     '000065'
#define CBS_REGULAR         '000066'
#define IBS_CRED_PRESUMIDO  '000067'
#define CBS_CRED_PRESUMIDO  '000068'

using namespace totvs.protheus.backoffice.fiscal.tciclass

/*/{Protheus.doc} TMSConfiguradorTributosWritten
    Classe responsável por processar a e retornar os dados gravados nas tabelas
	do Configurador de tributos , a partir de requisições com tipo de dado caráctere estururado
    como Json
    @author Fabio Marchiori Sampaio
    @since 26/06/2024
    @version 12.1.2410
    /*/
    
Class TMSConfiguradorTributosWritten

    Data oDados                         as object
    Data aIds                           as Array
    Data nValorTotal                    as numeric
    Public Data cError                  as Character
    Data oJsonDados                     as JSon

    Public Method New() Constructor
    Public Method getIdTrib()                        as Array
    Public Method setIdTrib()                        as Json
    Public Method processResponseSearchList()        as Array
    Public Method getItemTaxProductTransport()       as numeric
    Public Method processResponseSearchListDataSul() as Array
    Public Method destroy() //método para limpar os dados da classe

EndClass

/*/{Protheus.doc} TMSConfiguradorTributosWritten
    Classe responsável por processar a e retornar dados do Configurador de tributos no em tempo de execução na MatxFis.
    @author Fabio Marchiori Sampaio
    @since 13/11/2024
    @version 12.1.2410
    /*/
    
Class TMSConfiguradorTributosProcessing 

    Data oDados                         as object
    Data oJsonDados                     as JSon

    Public Method New() Constructor
    Public Method getDataItemProcessing()           as Character
    Public Method processResponseSearchListCst()    as Array

EndClass

/*/{Protheus.doc} new
    
    Método construtor da classe TMSConfiguradorTributosWritten

    @author Fabio Marchiori Sampaio
    @since 26/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
    /*/

METHOD New() CLASS TMSConfiguradorTributosWritten

self:oDados                := TCIWritten():New()
self:aIds                  := {}
self:nValorTotal           := 0
self:cError                := ""
self:oJsonDados            := JsonObject():New()

Return

/*/{Protheus.doc} new
    
    Método construtor da classe TMSConfiguradorTributosProcessing

    @author Fabio Marchiori Sampaio
    @since 13/11/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
    /*/

METHOD New() CLASS TMSConfiguradorTributosProcessing

self:oDados                := TCIProcessing():New()
self:oJsonDados            := JsonObject():New()

Return

/*/{Protheus.doc} getIdTrib
    
    Método para pegar o ID do configurador de Tributos

    @author Fabio Marchiori Sampaio
    @since 25/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method getIdTrib(cFilDoc as Character, cDocto as Character, cSerie as Character, cCliDev as Character, cLojDev as Character) as array Class TMSConfiguradorTributosWritten
 
    Local cAliasQry  as Character
    Local cQuery     as Character

    cAliasQry := GetNextAlias()
    cQuery := "   SELECT SD2.D2_IDTRIB "
    cQuery += "       FROM " + RetSqlName("SD2") + " SD2 "
    cQuery += "    WHERE SD2.D2_FILIAL  = '" + cFilDoc + "' "
    cQuery += "      AND SD2.D2_DOC     = '" + cDocto + "' "
    cQuery += "      AND SD2.D2_SERIE   = '" + cSerie + "' "
    cQuery += "      AND SD2.D2_CLIENTE = '" + cCliDev + "' "
    cQuery += "      AND SD2.D2_LOJA    = '" + cLojDev + "' "
    cQuery += "      AND SD2.D_E_L_E_T_ = ' ' "
    cQuery := ChangeQuery(cQuery)
    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry)
    
    While (cAliasQry)->(!Eof())
        Aadd(self:aIds, (cAliasQry)->D2_IDTRIB)
        (cAliasQry)->(DbSkip())
    EndDo
    (cAliasQry)->(DbCloseArea())

return 

/*/{Protheus.doc} setIdTrib
    
    Método para passar o ID do configurador de Tributos, para obter os impostos

    @author Fabio Marchiori Sampaio
    @since 25/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method setIdTrib() as JSon Class TMSConfiguradorTributosWritten
    
    Local cResponse  as Character

    If !Empty(self:aIds)

        self:oDados:SetId(self:aIds)
        cResponse := self:oDados:GetDataId()
        self:oJsonDados:FromJSON(cResponse)

    EndIf
  
return 

/*/{Protheus.doc} processResponseSearchList()
    
    Método que realiza o processamento da lista de pesquisa com os impostos, retornando um Json

    @author Fabio Marchiori Sampaio
    @since 28/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method processResponseSearchList as Array Class TMSConfiguradorTributosWritten

    Local nX                        as numeric
    Local nY                        as numeric
    Local aNomeRegraTributacao      as array
    Local jImpostosItem             as jSon
    Local nValorTributo             as numeric
    Local cNomeTributo              as Character
    Local nBaseTributo              as numeric
    Local nAliquotaTributo          as numeric
    Local aItensD2                  as array
    Local nValorTotal               as numeric
    Local nValorImposto             as numeric
    Local cNomeImposto              as Character
    Local cCodigoTributoRelacionado as Character
    Local nValorTotalImposto        as numeric
    Local nValorTotalItem           as numeric

    aItensD2 := {}

    For nX := 1 To Len(self:aIds)

        aNomeRegraTributacao := self:oJsonDados["dados_Id"][self:aIds[nX]]:GetNames()

        For nY := 1 To Len(aNomeRegraTributacao)

            If !Upper(aNomeRegraTributacao[nY]) == "AVISO" 
                jImpostosItem             := self:oJsonDados["dados_Id"][self:aIds[nX]][aNomeRegraTributacao[nY]]
                cNomeTributo              := jImpostosItem["tributo"]
                cCodigoTributoRelacionado := jImpostosItem["codigo_tributo_relacionado"]
                nBaseTributo              := jImpostosItem["base_tributo"]
                nAliquotaTributo          := jImpostosItem["aliquota_tributo"]
                nValorTributo             := jImpostosItem["valor_tributo"]

                // Tabela F2C | 000020 ISS - IMPOSTO SOBRE SERVIÇO
                // Tabela F2C | 000021 ICMS - Imposto sobre Circulação de Mercadorias e Serviços
                // Tabela F2C | 000056 ICMS ST - SUBSTITUIÇÃO TRIBUTÁRIA                                                                        
                
                If cCodigoTributoRelacionado == "000020" .Or. cCodigoTributoRelacionado == "000021" .Or. cCodigoTributoRelacionado == "000056"
                    nValorImposto      := nValorTributo // Impostos por Itens
                    nValorTotalItem    := nBaseTributo
                    cNomeImposto       := cNomeTributo

                    nValorTotalImposto += nValorTributo
                    nValorTotal        += nBaseTributo
                
                    aadd(aItensD2,{self:aIds[nX], cNomeImposto, cCodigoTributoRelacionado, nValorImposto, nValorTotalItem  })
                
                EndIf
                            
            Else
                self:cError := "Regra não encontrada para calcular pelo configurador de tributos, valor do imposto será zerado !!"
            EndIf

        Next        

    Next 

    If Empty(aItensD2) 
        self:cError := "Regra não encontrada para calcular pelo configurador de tributos, valor do imposto será zerado !!"        
    Else
        aadd(aItensD2,{'TF','TOTAL', nValorTotalImposto, nValorTotal  })
    EndIf

return aItensD2

/*/{Protheus.doc} getItemTaxProductTransport()
    
    Método que realiza o calculo de imposto por item de transporte

    @author Fabio Marchiori Sampaio
    @since 07/08/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method getItemTaxProductTransport(nD2ValImp as numeric, nDT8ValPas as numeric, nD2PrcVen as numeric) as numeric Class TMSConfiguradorTributosWritten

    Local nItemTax                  as numeric

    nItemTax := nD2ValImp * nDT8ValPas / nD2PrcVen

return nItemTax

/*/{Protheus.doc} clear
    
    Método para limpar os dados da classe TMSConfiguradorTributosWritten
    
    @author Fabio Marchiori Sampaio
    @since 27/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten 

/*/

Method destroy() Class TMSConfiguradorTributosWritten

    self:oDados:destroy()
    aSize(self:aIds,0)
    self:nValorTotal          := 0
    self:cError               := ""
    FreeObj(self:oJsonDados)

Return

/*/{Protheus.doc} getDataItemProcessing()
    
    O método GetDataItems() retornará os dados dos tributos por item,  em estrutura Json com o tipo de dado caractere.

    @author Fabio Marchiori Sampaio
    @since 11/11/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method getDataItemProcessing() Class TMSConfiguradorTributosProcessing
    
    Local cResponse as character
    
    self:oDados:setDataItems({"regras_base", "regras_aliquota", "regras_escrituracao", "detalhe_livro"})
     
    cResponse := self:oDados:GetDataItems()
    self:oJsonDados:FromJSON(cResponse)

Return 

/*/{Protheus.doc} processResponseSearchList()
    
    Método que realiza o processamento da lista de pesquisa com os impostos, retornando um Json 

    @author Fabio Marchiori Sampaio
    @since 28/06/2024
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosProcessing    
/*/

Method processResponseSearchListCst as Array Class TMSConfiguradorTributosProcessing

    Local nx                        as numeric
    Local nY                        as numeric
    Local cNomeRegraTributacao      as character
    Local jImpostosItem             as jSon
    Local jRegrasItem               as jSon
    Local cNomeID                   as character
    Local cCodigoCst                as character
    Local cCodigoRegra              as character
    Local cDescricaoRegra           as character
    Local aRetorno                  as Array
    Local cIdentTributo             as character
    Local cBaseTributo              as character
    Local cAliquotaTributo          as character
    Local cValorTributo             as character

    aRetorno := {}
    
    For nX := 1 To Len(self:oJsonDados["dados_itens"]:GetNames())

        cNomeRegraTributacao := self:oJsonDados["dados_itens"]:GetNames()[nX]

        For nY := 1 To Len(self:oJsonDados["dados_itens"][cNomeRegraTributacao]:GetNames())
           
            cNomeID := self:oJsonDados["dados_itens"][cNomeRegraTributacao]:GetNames()[nY]
            
            If (cNomeID <> "Aviso") //"Não há tributos calculados pelo configurador de tributos"
                jImpostosItem           := self:oJsonDados["dados_itens"][cNomeRegraTributacao][cNomeID]
                cCodigoRegra            := jImpostosItem["cod_regra"]
                cDescricaoRegra         := jImpostosItem["desc_regra"]
                cIdentTributo           := jImpostosItem["ident_trib"]
                cBaseTributo            := jImpostosItem["base_trib"]
                cAliquotaTributo        := jImpostosItem["aliq_trib"]
                cValorTributo           := jImpostosItem["val_trib"]
                
                jRegrasItem := self:oJsonDados["dados_itens"][cNomeRegraTributacao][cNomeID]["regras_escrituracao"]
                cCodigoCst              := jRegrasItem["cst"]


                // Tabela F2C | 000020 ISS - IMPOSTO SOBRE SERVIÇO
                // Tabela F2C | 000021 ICMS - Imposto sobre Circulação de Mercadorias e Serviços 
                // Tabela F2C | 000056 ICMS ST - SUBSTITUIÇÃO TRIBUTÁRIA

                If cIdentTributo == '000020' .Or. cIdentTributo == '000021' .Or. cIdentTributo == '000056'
                    aadd(aRetorno,{cCodigoRegra, cDescricaoRegra, cIdentTributo, cBaseTributo, cAliquotaTributo, cValorTributo, cCodigoCst  })
                EndIf
            EndIf

        Next nY

    Next nX

Return aRetorno

/*/{Protheus.doc} processResponseSearchListDataSul()
    
    Método que realiza o processamento da lista de pesquisa com os impostos(CBS/IBS), retornando um Json para a integração com o DataSul

    @author Fabio Marchiori Sampaio
    @since 03/11/2025
    @version 12.1.2410
    @return Objeto da classe TMSConfiguradorTributosWritten    
/*/

Method processResponseSearchListDataSul(cCodigoImposto) as Array Class TMSConfiguradorTributosWritten

    Local nX                        as numeric
    Local nY                        as numeric
    Local aNomeRegraTributacao      as array
    Local jImpostosItem             as jSon
    Local nValorTributo             as numeric
    Local cNomeTributo              as Character
    Local nBaseTributo              as numeric
    Local nAliquotaTributo          as numeric
    Local aItensD2                  as array
    Local nValorTotal               as numeric
    Local nValorImposto             as numeric
    Local cNomeImposto              as Character
    Local cCodigoTributoRelacionado as Character
    Local nValorTotalImposto        as numeric
    Local cCst                      as Character
    Local nPercReducaoAliquota      as numeric

    aItensD2 := {}

    For nX := 1 To Len(self:aIds)

        aNomeRegraTributacao := self:oJsonDados["dados_Id"][self:aIds[nX]]:GetNames()

        For nY := 1 To Len(aNomeRegraTributacao)

            If !Upper(aNomeRegraTributacao[nY]) == "AVISO" 
                jImpostosItem             := self:oJsonDados["dados_Id"][self:aIds[nX]][aNomeRegraTributacao[nY]]
                cNomeTributo              := jImpostosItem["tributo"]
                cCodigoTributoRelacionado := jImpostosItem["codigo_tributo_relacionado"]
                nBaseTributo              := jImpostosItem["base_tributo"]
                nValorTributo             := jImpostosItem["valor_tributo"]

                // Tabela F2C |  IBS_ESTADUAL        '000060'
                // Tabela F2C |  IBS_MUNICIPAL       '000061'
                // Tabela F2C |  CBS_FEDERAL         '000062'

                If cCodigoTributoRelacionado == cCodigoImposto 

                    nValorImposto      += nValorTributo // Impostos por Itens
                    cNomeImposto       := cNomeTributo
                    nValorTotalImposto += nValorTributo
                    nValorTotal        += nBaseTributo
                     
                    cCst                      := jImpostosItem["dados_escriturados"]["cst"]
                    nPercReducaoAliquota      := jImpostosItem["dados_escriturados"]["perc_red_aliquota"]
                    nAliquotaTributo          := jImpostosItem["aliquota_tributo"]

                EndIf

            EndIf

        Next        

    Next 

    aadd(aItensD2,{cNomeImposto, cCodigoImposto, nValorTotal, nAliquotaTributo, nPercReducaoAliquota, nValorImposto, cCst  })

return aItensD2


/*

Exemplo de Utilização da Classe 

supply.tms.configuradortributos.written.class.tlpp

Local oTMSConfiguradorTributosWritten  := TMSConfiguradorTributosWritten():New()

oTMSConfiguradorTributosWritten:getIdTrib(DT6->DT6_FILDOC, DT6->DT6_DOC, DT6->DT6_SERIE, DT6->DT6_CLIDEV, DT6->DT6_LOJDEV)

oTMSConfiguradorTributosWritten:setIdTrib()

oTMSConfiguradorTributosWritten:processResponseSearchList()

//nValImpClass := oTMSConfiguradorTributosWritten:nValorTotalImposto

oTMSConfiguradorTributosWritten:destroy()

*/
