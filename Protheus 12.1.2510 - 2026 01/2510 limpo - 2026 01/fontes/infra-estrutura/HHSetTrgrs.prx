#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#define VM_INSERT 08192   // Inclusao de Registro
#define VM_UPDATE 16384   // Alteracao de Registro                                                             	
#define VM_DELETE 32768   // Exclusao de Registro

Static __cCodLock  := "" // variavel usada para o bloqueio de usuario durante execucao de servico/gatilho

/*/


Ŀ
Funcao    HHSetTrgs  Rev.  Eduardo Riera           Data 06/01/2005
Ĵ
Descrio Triggers de atualizacao das aplicacoes mveis.              
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpC1: Alias da tabela de origem                            
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function HHSetTrgs(cAlias)

Local aArea      := GetArea()
Local cStatusTrg := GetSrvProfString("HHTriggerOn","0")
Local cHtrChave  := ""
Local lTriggerOn := .T.
Local cCampo     := cAlias+"->"+PrefixoCpo(cAlias) + "_FILIAL"
Local cComando   := ""
Local cBlock     := ""
Local cAliasMsg  := SuperGetMv("MV_TBLMSG",.F.,"")
Local nColigada  := GetNewPar("MV_RMCOLIG",0) // coligada igual a empr

//Ŀ
//Abre a tabela de controle do Hand Held                                           
//
If Select("HHTRG") == 0
	HHOpenTrg()
EndIf

Do Case		
	//Ŀ
	//Desabilita a executao dos triggers                                             
	//
	Case cStatusTrg == "0" .Or. ExistBlock("PDAOPEN")
		lTriggerOn := .F.
		//Ŀ
		//Executa os triggers para todas as empresas listadas no Protheus                  
		//
	Case cStatusTrg == "1"  // Sempre executa os triggers
		lTriggerOn := .T.
		//Ŀ
		//Executa os triggers somente para as empresas configuradas na tabela de gatilhos  
		//
	Case cStatusTrg == "2"   // Verifica o Trigger por Empresa
		cHtrChave := cEmpAnt
		//Ŀ
		//Executa os triggers somente para as empresas/filiais configuradas                
		//
	Case cStatusTrg == "3"   // Verifica o Trigger por Empresa / Filial
		cHtrChave := cEmpAnt + cFilAnt
		//Ŀ
		//Executa os triggers somente para as empresas/filiais e tabelas configuradas      
		//
	Case cStatusTrg == "4"  // Verifica o Trigger por Empresa / Filial / Alias
		cHtrChave := cEmpAnt + cFilAnt + cAlias
EndCase
//Ŀ
//Executa os triggers somente para as empresas/filiais e tabelas configuradas      
//
If lTriggerOn .And. !(cAlias $ SuperGetMv("MV_SFANTRG",.F.,"")+"/HMV/HCF/"+cAliasMsg)
	lTriggerOn := HHTRG->(MsSeek(cHtrChave)) .And. !Empty(HHTRG->HTR_TRG)
	If lTriggerOn
		If FindFunction("HHEXP"+cAlias) .And. !AtIsRotina("HHEXP"+cAlias)
			cBlock := "{|x,y| HHJOBTRGS('"+cAlias+"',x,y,(cAlias)->(RecNo()),.F.)} "
			cComando := "FldSettrigger("+cCampo+","+Str(VM_INSERT+VM_UPDATE+VM_DELETE,18)+","+cBlock+")"
			&(cComando)
		ElseIf FindFunction("U_HHEXP"+cAlias) .And. !AtIsRotina("U_HHEXP"+cAlias)
			cBlock := "{|x,y| HHJOBTRGS('"+cAlias+"',x,y,(cAlias)->(RecNo()),.T.)} "
			cComando := "FldSettrigger("+cCampo+","+Str(VM_INSERT+VM_UPDATE+VM_DELETE,18)+","+cBlock+")"
			&(cComando)
		EndIf
	EndIf
EndIf          


//Ŀ
// Criacao das triggers para integracao com o RM SOLUM                             
//
//nColigada := GetNewPar("MV_RMCOLIG",0) // coligada igual a empr
If nColigada > 0 
//FindFunction( "PMSINTSOLUM" ) .AND. PMSINTSOLUM()
	PMSSOLUM( cAlias )
	//PMSVIEWSOLUM()
EndIf

RestArea(aArea)
Return(.T.)

/*/


Ŀ
Funcao    HHjobTrgs Autor  Eduardo Riera           Data 16/01/2005
Ĵ
Descrio Funcao de chamada dos triggers de aplicaes moveis.        
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpC1: Alias da tabela de origem                            
          ExpC2: Campo atualizado                                     
          ExpC3: Tipo de atualizacao                                  
          ExpC4: Recno da tabela a ser atualizada                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function HHJobTrgs(cAlias,cCpo,nOper,nRecAlias,lUserF)

Local cBlock
Local bBlock
Local cID := ""

If lUserF
	cBlock := "{|x,y| U_HHEXP"+cAlias+"(x,y)} "
Else
	cBlock := "{|x,y| HHEXP"+cAlias+"(x,y)} "
EndIf

bBlock := &(cBlock)

//Ŀ
//Verifica o ID a ser executado                                           
//
cID := Eval(bBlock,cCpo,-1)
//Ŀ
//Inicializa o Job                                                        
//
If GetSrvProfString("HHTriggerDebug","0")<>"ON"
	StartJob("HHExecTrg",GetEnvServer(),.F.,cEmpAnt,cFilAnt,cAlias,cCpo,nOper,nRecAlias,cID,lUserF)
Else
	HHExecTrg(cEmpAnt,cFilAnt,cAlias,cCpo,nOper,nRecAlias,cID,lUserF)
EndIf
Return(.T.)


/*/


Ŀ
Funcao    HHExecTrg Autor  Eduardo Riera           Data 16/01/2005
Ĵ
Descrio Funcao de execucao de gatilho das aplicaes moveis.        
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpC1: Codigo da empresa para execucao do gatilho           
          ExpC2: Codigo da filial para execucao do gatiho             
          ExpC3: Alias da tabela de execucao                          
          ExpC4: Campo atualizado                                     
          ExpN5: Tipo de atualizacao                                  
          ExpN6: Recno da tabela a ser atualizada                     
          ExpC7: Id de atualizacao                                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function HHExecTrg(cCodEmp,cCodFil,cAlias,cCpo,nOper,nRecAlias,cID,lUserF)

Local cBlock
Local bBlock
Local lDebugOn
Local cSerie    := ""

If lUserF
	cBlock    := "{|x,y| U_HHEXP"+cAlias+"(x,y)} "
Else
	cBlock    := "{|x,y| HHEXP"+cAlias+"(x,y)} "
EndIf

bBlock    := &(cBlock)

lDebugOn  := GetSrvProfString("HHTriggerDebug","0")=="ON"

If HHLockTrg(cAlias,cID,@cSerie,cCodEmp,cCodFil,.F.)
	//Ŀ
	//Preparando o ambiente para execucao                                     
	//
	If !lDebugOn
		RpcSetType ( 3 )
		PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil
	EndIf
	//Ŀ
	//Executando a trigger                                                    
	//
	(cAlias)->(MsGoto(nRecAlias))
	Eval(bBlock,cCpo,nOper)
	HHLockTrg(cAlias,cID,cSerie,cCodEmp,cCodFil,.T.)
	//Ŀ
	//Finalizando o ambiente                                                  
	//
	If !lDebugOn
		RESET ENVIRONMENT
	EndIf
EndIf
Return(.T.)

/*/


Ŀ
Funcao    HHLockTrg  Autor Eduardo Riera           Data 11/01/2007
Ĵ
Descrio Trava de gatilhos durante a importacao dos dados            
Ĵ
Retorno   ExpL1: Indica se a execucao do trigger pode ser realizada   
Ĵ
ParametrosExpC1: Alias da tabela acionada                             
          ExpC2: Id do usuario                                        
          ExpC3: Serie do Hand Held                         (OPC)(REF)
          ExpC3: Codigo da Empresa                                    
          ExpC4: Codigo da Filial                                     
          ExpL5: .F. - Trava o trigger impedindo a execucao           
                 .T. - Destrava o trigger                             
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function HHLockTrg(cAlias,cID,cSerie,cCodEmp,cCodFil,lUnLock)

Local nX        := 0
Local nY        := 0
Local nMinutos  := Val(GetSrvProfString("HHTriggerInterval","60"))
Local lWaiting  := .T.
Local lExecute  := .F.
Local cLockName := "HHEXECTRG_"+cCodEmp+"_"+cCodFil+"_"+cAlias+"_"
Local cRaiz     := "Emp: "+cCodEmp+"/"+cCodFil+" Logged: Mobile Trigger "+" Tabela: "+cAlias+" Time: "+Time()
Local lIsGeneric:= .F.
Local lConnect  := .F.

DEFAULT lUnLock := .F.

If !lUnLock
	//Ŀ
	//Atualiza a identificacao no monitor do Protheus                         
	//
	PtInternal(1,cRaiz+" ID: "+cID+" Status: "+" OPEN ")
	//Ŀ
	//Analisa se a execucao  genrica ou por ID                              
	//
	If Empty(cID)
		cID := "GENERIC"
		lIsGeneric:= .T.
	EndIf
	If cID == "0" 
		lWaiting := .F.
	EndIf
	cLockName += cID
	//Ŀ
	//Verificando se a trigger ja esta em execucao e se tem espera            
	//
	While lWaiting .And. !KillApp()
		If !LockByName(cLockName+"1",.F.,.F.,.T.)
			lWaiting := .F.
			lExecute := .F.
		Else
			lConnect := CheckUsr(cId,cCodEmp,cCodFil,@cSerie)
			If !LockByName(cLockName+"0",.F.,.F.,.T.) .Or. lConnect
				//Ŀ
				//Atualiza a identificacao no monitor do Protheus                         
				//
				PtInternal(1,cRaiz+" ID: "+cID+" Status: "+" WAIT ")
				lWaiting := .T.
				lExecute := .F.
				For nX := nMinutos To 1 Step -1
					PtInternal(1,cRaiz+" ID: "+cID+" Status: "+" WAIT "+"("+AllTrim(Str(nX,10))+"'')")
					For nY := 1 To 6
						If KillApp()
							Exit
						Else
							Sleep(10000)
						EndIf
					Next nY
					If KillApp()
						Exit
					EndIf
					//Ŀ
					//Validacao de Usuario Conectado                                          
					//
					If nX == 1
						If !lIsGeneric
							If CheckUsr(cId,cCodEmp,cCodFil,@cSerie)
								nX := nMinutos + 1
							EndIf
						EndIf
					EndIf
				Next nX
			Else				
				__cCodLock := HHLock(cSerie,"J")
				lConnect := CheckUsr(cId,cCodEmp,cCodFil,@cSerie)
				If lConnect
					lWaiting := .T.
					lExecute := .F.
					If __cCodLock=="0"
						HHUnlock(cSerie)
					EndIf
				Else
					lWaiting := .F.
					lExecute := .T.
				Endif
				UnLockByName(cLockName+"1",.F.,.F.,.T.)				
			EndIf
		EndIf
		Sleep(1000)
	EndDo
Else
	If __cCodLock=="0"
		HHUnlock(cSerie)
	EndIf
	UnLockByName(cLockName+"0",.F.,.F.,.T.)
	lExecute := .F.
EndIf
//Ŀ
//Atualiza a identificacao no monitor do Protheus                         
//
If !lExecute
	PtInternal(1,cRaiz+" ID: "+cID+" Status: "+" OFF ")
Else
	PtInternal(1,cRaiz+" ID: "+cID+" Status: "+" ON ")
EndIf
Return(lExecute)








