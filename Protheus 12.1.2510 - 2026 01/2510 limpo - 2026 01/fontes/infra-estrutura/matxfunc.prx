#INCLUDE "MATXFUNC.CH"
#INCLUDE "AP5MAIL.CH"
#INCLUDE "protheus.ch"
#INCLUDE "dbstruct.ch"
#INCLUDE "APWIZARD.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWADAPTEREAI.CH"
#INCLUDE "FWLIBVERSION.CH"

#define _CRLF CHR(13)+CHR(10)

Static __nMV1CENT, __nMV9CENT, __nMV0CENT, __cUltFilMVCent, __cUltEmpMVCent
Static __lDefTop	:= NIL
Static __oVerPad := NIL
Static _oJsAllUsers
Static __oPrepSA  := Nil
Static __oPrepSB1 := Nil
Static __oPrepSA4 := Nil
Static __lLibVer  As Logical

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ DATA   ³ BOPS ³Program.³ALTERACAO							            ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³16.03.99³20092A³Julio   ³Melhoria na Extenso() p/ Argentina.             ³±±
±±³22.03.99³      ³Julio   ³Criacao da ABrowse3() - Browse de Vetor Multi.D ³±±
±±³08.04.99³      ³Gilson  ³Criacao da VerProtheus()                        ³±±
±±³08.04.99³xxxxxx³Wagner  ³Transf da funcao a100segum()                    ³±±
±±³08.04.99³xxxxxx³Sandro  ³ajuste nas funcoes de  codigo de barras         ³±±
±±³09.04.99³xxxxxx³Fernando³Inclusao da funcao QA_ULTRVQ                    ³±±
±±³30.04.99³xxxxxx³Sandro  ³Inclusao das funcoes para codigo de barras para ³±±
±±³        ³      ³        ³o padrao Allegro                                ³±±
±±³28.05.99³xxxxxx³Julio   ³Inclusao das funcoes graficas de compatibilizac.³±±
±±³        ³      ³        ³entre o Advanced e Protheus usado em finc020.prw³±±
±±³        ³      ³        ³par uso gen‚rico.                               ³±±
±±³01.06.99³PROTH.³Julio   ³Implementacoes nas Funcoes gr ficas...          ³±±
±±³17.06.99³xxxxxx³Wagner  ³Substituicao da Serial.Dll                      ³±±
±±³15.07.99³PROTH.³Gilson  ³Inclusao da Funcao ProcLote()                   ³±±
±±³15.07.99³PROTH.³Gilson  ³Transferencia das FuncoesChkMoeda() e NewMoeda()³±±
±±³26.07.99³21069A³Julio   ³Melhoria Extenso (ARG) Dezenas 21..29 juntas.   ³±±
±±³02.08.99³META  ³Julio   ³Intrerpretar MV_CRNEG                           ³±±
±±³10.08.99³PROTH.³Pilar   ³Mover adatainicio/adatafim para Sigacon.prw     ³±±
±±³16.08.99³PROTH.³Julio   ³Cria‡„o da Fun‡„o LISDIR()                      ³±±
±±³13.08.99³PROTH.³Julio W.³Funcao ConValor transf. do Fina470.prx          ³±±
±±³09.09.99³PROTH.³Julio W.³Transferidas funcoes AbreCta/Criacta            ³±±
±±³18.10.99³XXXXXX³Julio W ³Cria‡„o das fun‡”es SOMASTR e MULTSTR           ³±±
±±³09.11.99³XXXXXX³Aldo    ³Colocado a funcao VldHora() para ficar generica ³±±
±±³16.11.99³Melhor³Julio W.³Implementada fun‡„o Tira1()                     ³±±
±±³23.11.99³Melhor³Julio W.³Implementada fun‡„o Fonetica()                  ³±±
±±³15.12.99³XXXXXX³Julio W.³Transferida funcao NaoAltera() do CONA100A.PRX  ³±±
±±³15.12.99³23390a³Mauricio³Corrigida selecao de baixa de RA por Dacao para ³±±
±±³        ³      ³        ³cancelamento da baixa (SEL070Baixa())           ³±±
±±³16.12.99³XXXXXX³Julio W.³Transferidas funcoes de Validacao SI3 (SigaCTB) ³±±
±±³03.02.00³XXXXXX³Pilar   ³Transferidas funcoes Mascara (SigaCTB)          ³±±
±±³07.06.00³XXXXXX³Fabio R.³Implementado novo conceito de segmentos         ³±±
±±³26/07/00³xxxxxx³Mauro   ³Colocada Funcoes Filtro Browse estava Sigacusa  ³±±
±±³30.08.00³XXXXXX³Fabio R.³Alterado a TmkSeg para contemplar prospect      ³±±
±±³11.09.00³Melhor³Lucas   ³Inclusao da funcao MsDecimais()					³±±
±±³30.10.00³Melhor³Ronny C.³Localizacao da funcao extenso para outros paises³±±
±±³02.08.01³Melhor³Naldo   ³Inclusao da funcao MyInternalPack() e  Alteracao³±±
±±³        ³      ³        ³na Chk_Pack() substituindo PACK por esta    nova³±±
±±³        ³      ³        ³funcao.											³±±
±±³03.08.01³Meta  ³Natie   ³Inclusao Evento "015" a "019" - Eventos RH      ³±±
±±³28.08.01³Meta  ³Natie   ³Ajustes no texto do evento 14-funcao MEnviaMail ³±±
±±³22.10.01³Melhor³Naldo   ³Fechar a Area em Chk_Pack() para que possa   ser³±±
±±³        ³      ³        ³reaberta em ChkFile()							³±±
±±³30.10.01³Melhor³Naldo   ³Alterada Chk_Pack() para so Fechar/Reabrir os ar³±±
±±³        ³      ³        ³quivos travados com Ma280fLock.					³±±
±±³23.11.01³Melhor³Naldo   ³Passagem de Dois Novos Parametros para Chk_Pack ³±±
±±³27.06.02³Melhor³Emerson ³Inclusao de Evento "020" para M-Messenger.		³±±
±±³19.09.02³Melhor³Naldo   ³Passagem de Novo Parametro para EndFilBrw() para³±±
±±³        ³      ³        ³forcar um RetIndex()           					³±±
±±³04.12.02³Melhor³Naldo   ³Inclusao das Funcoes  GdNoEmpty()/GdPosDeleted()³±±
±±³        ³      ³        ³Otimizacao x Amarracao das Funcoes GdFieldPos(),³±±
±±³        ³      ³        ³GdFieldGet(), GdFieldPut() e GdDeleted()        ³±±
±±³23.12.02³Acerto³Naldo   ³Alterada Chk_Pack() para utilizar RetIndex() ape³±±
±±³        ³      ³        ³nas para Arquivos que estejam no SX2            ³±±
±±³14.05.03³Melhor³Cleber  ³Alterada QA_ULTRVQ adicionando o parametro      ³±±
±±³        ³      ³        ³lUltRv para que a rotina traga a ultima revisao ³±±
±±³        ³      ³        ³independente da data de inicio de vigencia.     ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³27.11.03³Melhor³Naldo   ³Alterada GDCheckKey() para que possa estar  veri³±±
±±³        ³      ³        ³ficando os campos de preenchimento Obrigatorio  ³±±
±±³08.01.04³Melhor³Naldo   ³Passagem de Novos parametros para GdSeek(),  tra³±±
±±³        ³      ³        ³tamento para MsNewGetDados() e Opcao de pesquisa³±±
±±³        ³      ³        ³parcial, exata e que contenha a expressao ( quan³±±
±±³        ³      ³        ³do campos tipo caracter )                       ³±±
±±³23.03.04³Melhor³Naldo   ³Permitir a Pesquisa de Campos Memo na GdSeek()  ³±±
±±³06.04.04³Melhor³Naldo   ³Carregar as Opcoes do X3_CBOX na GDSeek()       ³±±
±±³16.04.04³ 70605³Nilton  ³Salvar e restaurar area na funcao extenso()     ³±±
±±³16.04.04³Melhor³Naldo   ³Executar o bChange da GetDados quando efetuar  o³±±
±±³        ³      ³        ³posicionamento                                  ³±±
±±³01.05.04³Melhor³Naldo   ³Utilizar o Metodo :Goto() da GetDados para efetu³±±
±±³        ³      ³        ³tuar o Posicionamento na GdSeek					³±±
±±³02.06.04³Melhor³Naldo   ³Exclusao das Funcoes GetKeys() e RestKeys() e da³±±
±±³        ³      ³        ³constante manifesta __aKeysSets__				³±±
±±³04.06.04³Melhor³Naldo   ³Passagem de novo parametro para PesqBrw()		³±±
±±³13.02.09³003472³Marcelo ³Ajuste na FilBrowse() para manter o filtro da   ³±±
±±³        ³ /2009³        ³tabela apos alterar o indice - Ambiente Codebase³±±
±±³14.07.11³017083³Tiago   ³Ajustes de tela usando msadvsize na funcao      ³±±
±±³        ³ /2011³Malta   ³GdSeek.                                         ³±±
±±³09.10.18³DMINA-³A       ³Funcion Extenso(), considerar pais Colombia en  ³±±
±±³        ³  4582³Rodriguez³retorno de centavos.                           ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³    DATA    ³ BOPS / ISSUE     ³ PROGRAMA     ³ ALTERACAO			                         ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 26.11.2018 ³ DMANMAT01-12278  ³ Marcos Gomes ³ Correção da Funcao MEnviaMail()               ³±±
±±³            ³ DMANMAT01-12339  ³              ³ Caso nao tenha sido informado o parametro     ³±±
±±³            ³                  ³              ³ MV_RELAUSR o sistema assume como conteudo a   ³±±
±±³            ³                  ³              ³ mesma Senha do parâmetro MV_EMSENHA           ³±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

static aCtrlCB := { nil, nil, nil, nil } // funcoes de codigo de barras
Static __aSXG  := {}
Static nContMoeda
Static cFilMoed

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Extenso	³ Autor ³ Julio Wittwer         ³ Data ³ 23/02/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera extenso de um valor Numerico.						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Extenso(nValor[,lQuantid][,nMoeda][,cPrefixo]              ³±±
±±³          ³         [,cIdioma][,lCent][,lFrac][,lUsaCon][,cPosMoed])   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nValor     -Valor a ser gerado o extenso.				  ³±±
±±³			 ³ lQuantid   -Determina se a impressao do Extenso sera para  ³±±
±±³			 ³ 			   Valores ou apenas quantidade. (Default = .F.)  ³±±
±±³			 ³ nMoeda     -Identifica a moeda (1-5)                       ³±±
±±³			 ³ cPrefixo   -Prefixo alternativo. Caso especificado, prefixa³±±
±±³			 ³             o retorno do extenso, fazendo com que a unidade³±±
±±³			 ³             monet ria n„o seja impressa. (Default = "")    ³±±
±±³			 ³ cIdioma    -Especifica em que idioma devera ser retornado  ³±±
±±³			 ³             o valor do extenso. (1=Port.2=Espa.3=Ingl      ³±±
±±³			 ³             O padrao e o idioma do repositorio             ³±±
±±³			 ³ lCent      -Especifica se a funcao devera retornar os cent.³±±
±±³			 ³             o padrao e .t.                                 ³±±
±±³			 ³ lFrac      -Especifica se os centavos deverao ser retornado³±±
±±³			 ³             em modo fracionado.                            ³±±
±±³			 ³             (Somente funciona com cIdioma sendo Ingles)    ³±±
±±³			 ³ lUsaCon     -Especifica se sera utilizado "y" ou "con"     ³±±
±±³			 ³             (Somente utilizado no idioma espanhol)         ³±±f
±±³			 ³ cPosMoed    -Especifica a Posicao da Descricao da Moeda    ³±±
±±³			 ³             (1=Antes do Texto;2=Entre os valores Inteiros  ³±±
±±³			 ³             e os Centavos;3=No final do texto              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION Extenso(nNumToExt,lQuantid,nMoeda,cPrefixo,cIdioma,lCent,lFrac,lUsaCon,cPosMoed)
Local nInt := 0
Local nDec := 0
Local cExt := ""
Local aM1  				// {"Milh„o","Milh”es"}
Local aM2  				// {"Bilh„o","Bilh”es"}
Local cTheNum
Local cIdMoedaS
Local cIdMoedaP

Local aArea	:=	GetArea()  //Salva area posicionada
Local cLanguage	:= ""
Local cRetId	:= ""
DEFAULT nMoeda	:=	1

If ValType(nNumtoExt) != "N" .or. nNumToExt <= 0
	Return("")
EndIf

cRetId := FwRetIdiom()
If cRetId == 'en' 
	cLanguage := 'ENGLISH'
ElseIf cRetId == 'es'
	cLanguage := 'SPANISH'
Else
	cLanguage := 'PORTUGUESE'
EndIf

if ValType(cIdioma) == "U"
	cIdioma := iif(cLanguage == "PORTUGUESE", "1",iif(cLanguage == "SPANISH", "2", "3"))
else
	cIdioma := iif(!(cIdioma $ "123"),iif(cLanguage == "PORTUGUESE", "1",iif(cLanguage == "SPANISH", "2", "3")), cIdioma)
endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso o parametro nao exista, sugere-se o     ³
//³default de acordo com a lingua do repositorio³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(GetMV("MV_MOEDA"+AllTrim(Str(nMoeda))))
	cIdMoedaS := GetMV("MV_MOEDA" + AllTrim(Str(nMoeda)))
Else
	cIdMoedaS := iif(cLanguage == "PORTUGUESE","REAL",iif(cLanguage == "SPANISH","PESO","DOLLAR"))
endif

If !Empty(GetMV(IIF(nMoeda < 10,"MV_MOEDAP", "MV_MOEDP") + AllTrim(Str(nMoeda))))
	cIdMoedaP := GetMV(IIF(nMoeda < 10,"MV_MOEDAP", "MV_MOEDP") + AllTrim(Str(nMoeda)))
Else
	cIdMoedaP := iif(cLanguage == "PORTUGUESE","REAIS",iif(cLanguage == "SPANISH","PESOS","DOLLARS"))
endif

lCent := iif(lCent = NIL,.t.,lCent)
lFrac := iif(lFrac = NIL,.f.,lFrac)

aM1 := {DevStr("STR0001",cIdioma),DevStr("STR0002",cIdioma)}
aM2 := {DevStr("STR0003",cIdioma),DevStr("STR0004",cIdioma)}

lQuantid		:= if(lQuantid=NIL,.f.,lQuantid)
cPrefixo    := if(valtype(cPrefixo)="C",cPrefixo,"")

cTheNum 	:= strzero(nNumToExt,20,4)
nInt 		:= val(left(cTheNum,15))
nDec 		:= val(substr(cTheNum,17,2))

If nInt >= 1000000000000
	Return(DevStr("STR0005",cIdioma))	//	"NUMERO MAIOR QUE 1 TRILHAO"
Endif

If cPaisLoc $ "BOL"
	lFrac:=.T.
EndIf

Do While nInt >0

	if nInt>=1 .and. nInt<1000
		cExt += extens_(nInt, cIdioma)
		nInt :=0
	elseif nInt>=1000 .and. nInt <1000000
		If cPaisLoc == "PTG" .And. int(nInt/1000) == 1
			cExt += DevStr("STR0006",cIdioma) // "Mil"
		Else
			cExt += extens_(int(nInt/1000),cIdioma) + " "+ DevStr("STR0006",cIdioma) // "Mil"
		Endif
		nInt -= (1000*int(nInt/1000))
		If cPaisLoc $ "CHI|PAR" .And. nMoeda == 1
			nInt := Round(nInt,0)
		Endif
		If cIdioma == "1"  // Espanhol/Ingles nao tem nenhum tipo de separacao, nem "E"/"AND", nem ","
			cExt += if(nInt>0,if(nInt<99," "+DevStr("STR0009",cIdioma), "," ),"")	// "E"
		EndIf
	elseif nInt >=1000000 .and. nInt<1000000000
		If cIdioma == "3" // Ingles nao tem plural para Million
			cExt += extens_(int(nInt/1000000),cIdioma) + " "+ aM1[1]
		Else
			cExt += extens_(int(nInt/1000000),cIdioma) + " "+if(int(nInt/1000000)=1,aM1[1],aM1[2] )
		EndIf
		nInt -= (1000000*int(nInt/1000000))
		If cPaisLoc $ "CHI|PAR" .And. nMoeda == 1
			nInt := Round(nInt,0)
		Endif
		If cIdioma == "1"  // Espanhol/ Ingles nao tem nenhum tipo de separacao, nem "E"/"AND", nem ","
			cExt += if(nInt>0,if(nInt<99," "+DevStr("STR0009",cIdioma), "," ),"")	// "E"
		EndIf
	elseif nInt >=1000000000 .and. nInt<1000000000000
		cExt += extens_(int(nInt/1000000000),cIdioma) + " "+if(int(nInt/1000000000)=1,aM2[1],aM2[2] )
		nInt -= (1000000000*int(nInt/1000000000))
		If cPaisLoc $ "CHI|PAR" .And. nMoeda == 1
			nInt := Round(nInt,0)
		Endif
		If cIdioma == "1"  // Espanhol/Ingles nao tem nenhum tipo de separacao, nem "E"/"AND", nem ","
			cExt += if(nInt>0,if(nInt<99," "+DevStr("STR0009",cIdioma), "," ),"")	// "E"
		ElseIf cIdioma == "2"
			If nInt > 999999
				cExt := Substr(cExt,1,Len(cExt)-9)
			EndIf
		EndIf
	endif
	if nInt >0
		if Int(nInt * 1000) > 0
			cExt += " "
		else
			nInt := 0
		endif
	endif
Enddo

// Ajuste da ultima virgula
If cIdioma == "1" 
	If At( ',', cExt ) > 0 .AND. At( " " + DevStr( "STR0009",cIdioma) + " ", cExt ) == 0
		cExt := Stuff( cExt, RAt( "," , cExt ), 1, ' ' +  DevStr( "STR0009",cIdioma) )
	Endif
Endif

// NOME DA MOEDA
If !lFrac .or. cIdioma <> "3"
	If !lQuantid .and. nNumToExt >= 1 .and. empty(cPrefixo) .And. !(lFrac .And. (cPaisLoc!="MEX" .Or. nMoeda != 1 ) );
	   .And. (cPosMoed == Nil .Or. cPosMoed <> "1" )
		cExt += IIf(GetRestEx(nNumToExt), " de "," ")+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP) 	// Real ### REais
	Endif
Endif
// CENTAVOS
if !lQuantid .and. (nDec > 0 .or. cIdioma=="3" .Or. cPaisLoc$"MEX|BOL|PER|ARG|COL")  // Mostrar como moeda
	if lCent // Mostrar os centavos
		if cIdioma == "1" // Portugues
			if lFrac  // deve-se informar centavos em formato fracionario mesmo quando for zero.
				If cPaisLoc == "MEX" .And. nMoeda == 1
					cExt += if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 M. N."  // Moneda Nacional
				ElseIf cPaisLoc == "BOL"
					cExt += " "+if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 "
				Else
					If cPosMoed == "1"
						cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "	// " e "
					ElseIf cPosMoed == "2"
						cExt += " "+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+;
						if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "
					ElseIf cPosMoed == Nil .Or. cPosMoed == "3"
						cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "+;	// " e "
							if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP) 	// Real ### REais
					Endif
				EndIf
			elseIf nDec > 0
				cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+extens_(nDec,cIdioma)+" "+;	// " e "
					if(nDec=1,DevStr("STR0010",cIdioma),DevStr("STR0011",cIdioma))	 // "Centavo","Centavos"
				If cPaisLoc = "PAR" .and. nNumToExt< 1 .and. !lQuantid
		      		cExt += " "+DevStr("STR0054",cIdioma)+ " "+cIdMoedaS//TRATAMENTO PARA CENTAVOS EM REAIS
		      	EndIf
			endif
		elseif cIdioma == "2" // Espanhol
			// Em espanhol, usar separador "COM" entre inteiros e centavos, caso
			// seja especificado um prefixo. Cao contrario, usar "e"
			if lFrac  // deve-se informar centavos em formato fracionario mesmo quando for zero.
				If cPaisLoc == "MEX" .And. nMoeda == 1
					cExt += if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 M. N."
				ElseIf cPaisLoc == "BOL"
					cExt += " "+if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 "
				ElseIf cPaisLoc == "ARG" .And. nMoeda == 1
					cExt += if(nDec >0,(if(int(nNumToExt)>0," ","")+ DevStr("STR0053",cIdioma)+ " " + StrZero(nDec,2)+"/100")," ")	+" Pesos"
				Else
					If cPosMoed == "1"
						If lUsaCon == Nil
					 		cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "	// " e "
					   	Else
							cExt += if(int(nNumToExt)>0," "+If(lUsaCon,DevStr("STR0053",cIdioma),DevStr("STR0009",cIdioma))+" ","")+StrZero(nDec,2)+"/100 "	// "com"##" e "
					   	Endif
					ElseIf cPosMoed == "2"
						If lUsaCon == Nil
							cExt += " "+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+;
								if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "
                  		Else
							cExt += " "+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+; 	// Real ### REais
								if(int(nNumToExt)>0," "+If(lUsaCon,DevStr("STR0053",cIdioma),DevStr("STR0009",cIdioma))+" ","")+StrZero(nDec,2)+"/100 "	// "com"##" e "
                  		Endif
					ElseIf cPosMoed == Nil .Or. cPosMoed == "3"
						If lUsaCon == Nil
							cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "+;	// " e "
								if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP) 	// Real ### REais
					   	Else
							If cPaisLoc == "PER" .And. nDec == 0
								cExt += if(int(nNumToExt)=1," "+cIdMoedaS," "+cIdMoedaP) 	// Real ### REais
							ElseIf cPaisLoc == "ARG" .And. nDec == 0
								cExt += if(int(nNumToExt)=1," "+cIdMoedaS," "+cIdMoedaP)
							Else
								cExt += if(int(nNumToExt)>0," "+If(lUsaCon,DevStr("STR0053",cIdioma),DevStr("STR0009",cIdioma))+" ","")+StrZero(nDec,2)+"/100 "+;	// "com"##" e "
									if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP) 	// Real ### REais
							EndIf
					   	Endif
					Endif
				EndIf
			elseIf nDec > 0
				If lUsaCon == Nil
					cExt += if(int(nNumToExt)>0," "+if(empty(cPrefixo),DevStr("STR0009",cIdioma),DevStr("STR0053",cIdioma))+" ","")+extens_(nDec,cIdioma)+" "+;	// " e " ### "com"
						if(nDec=1,DevStr("STR0010",cIdioma),DevStr("STR0011",cIdioma))	 // "Centavo","Centavos"
		      	Else
					cExt += if(int(nNumToExt)>0," "+if(lUsaCon,DevStr("STR0053",cIdioma),DevStr("STR0009",cIdioma))+" ","")+extens_(nDec,cIdioma)+" "+;	// " con ### "e"
						if(nDec=1,DevStr("STR0010",cIdioma),DevStr("STR0011",cIdioma))	 // "Centavo","Centavos"
		      	Endif
		      	If cPaisLoc = "PAR" .and. nNumToExt< 1 .and. !lQuantid
		      	   cExt += " "+DevStr("STR0054",cIdioma)+ " "+cIdMoedaS//TRATAMENTO PARA CENTAVOS EM PESOS
		      	EndIf
			endif
		elseif cIdioma == "3" // Ingles
			if lFrac  // em ingles, deve-se informar centavos em formato fracionario mesmo quando for zero.
				If cPaisLoc == "MEX" .And. nMoeda == 1
					cExt += if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 M. N."
				ElseIf cPaisLoc == "BOL"
					cExt += " "+if(int(nNumToExt)>0," ","")+StrZero(nDec,2)+"/100 "
				Else
					If cPosMoed == "1"
						cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "	// " e "
					ElseIf cPosMoed == "2"
						cExt += " "+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+;
						if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "
					ElseIf cPosMoed == Nil .Or. cPosMoed == "3"
						cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+StrZero(nDec,2)+"/100 "+;	// " e "
							if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP) 	// Real ### REais
					Endif
				EndIf
			elseIf nDec > 0
				cExt += if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+extens_(nDec,cIdioma)+" "+;	// " e "
					if(nDec=1,DevStr("STR0010",cIdioma),DevStr("STR0011",cIdioma))	 // "Centavo","Centavos"
				If cPaisLoc = "PAR" .and. nNumToExt< 1 .and. !lQuantid
     				cExt := ""
		      	 	cExt := if(int(nNumToExt)>0," "+DevStr("STR0009",cIdioma)+" ","")+extens_(nDec,cIdioma)+" "+cIdMoedaS+" "+DevStr("STR0054",cIdioma)+" "+if(nDec=1,DevStr("STR0010",cIdioma),DevStr("STR0011",cIdioma))//TRATAMENTO PARA CENTAVOS EM DOLAR
		      	EndIf
			endif
		endif
	endif
endif
if !Empty(cPrefixo) .And. cPosMoed == Nil
	cExt := cPrefixo+" " +upper(cExt)
ElseIf !Empty(cPrefixo) .And. cPosMoed == "1"
   cExt := cPrefixo+" "+if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+ upper(cExt)
ElseIf Empty(cPrefixo) .And. cPosMoed == "1"
   cExt := if(int(nNumToExt)=1,cIdMoedaS,cIdMoedaP)+" "+upper(cExt)
Else
   cExt := " "+upper(cExt)
Endif

//Restaura a area posicionada anteriormente
RestArea(aArea)

Return cExt

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Extens_	³ Autor ³ Julio Wittwer         ³ Data ³ 23/02/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gera extenso de um valor Numerico de 1 a 999				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ _Extens(nNumero,cIdioma)                  				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nNumero    -Valor a ser gerado o extenso.				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Usado Internamente pela Funcao EXTENSO()					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          | ALTERACOES                           					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Ronny C .³ 01/11/00 - Adicionado novo parametro: cIdioma     		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function extens_(nNumero, cIdioma)

Local aNum:={DevStr("STR0012",cIdioma),DevStr("STR0013",cIdioma),;
	DevStr("STR0014",cIdioma),DevStr("STR0015",cIdioma),;
	DevStr("STR0016",cIdioma),DevStr("STR0017",cIdioma),;
	DevStr("STR0018",cIdioma),DevStr("STR0019",cIdioma),;
	DevStr("STR0020",cIdioma)}
// Um#Dois#Tres#Quatro#Cinco#Seis#Sete#Oito#Nove

Local aDez:={DevStr("STR0021",cIdioma),DevStr("STR0022",cIdioma),;
	DevStr("STR0023",cIdioma),DevStr("STR0024",cIdioma),;
	DevStr("STR0025",cIdioma),DevStr("STR0026",cIdioma),;
	DevStr("STR0027",cIdioma),DevStr("STR0028",cIdioma),;
	DevStr("STR0029",cIdioma)}
// Onze#Doze#Treze#Quatorze#Quinze#Desesseis#desessete#dezoito#dezenove

Local aDec:={DevStr("STR0030",cIdioma),DevStr("STR0031",cIdioma),;
	DevStr("STR0032",cIdioma),DevStr("STR0033",cIdioma),;
	DevStr("STR0034",cIdioma),DevStr("STR0035",cIdioma),;
	DevStr("STR0036",cIdioma),DevStr("STR0037",cIdioma),;
	DevStr("STR0038",cIdioma)}
// Dez#Vinte#Trinta#Quarenta#Cinquenta#Sessenta#setenta#Oitenta#Noventa

Local aCen:={DevStr("STR0039",cIdioma),DevStr("STR0040",cIdioma),;
	DevStr("STR0041",cIdioma),DevStr("STR0042",cIdioma),;
	DevStr("STR0043",cIdioma),DevStr("STR0044",cIdioma),;
	DevStr("STR0045",cIdioma),DevStr("STR0046",cIdioma),;
	DevStr("STR0047",cIdioma)}
// Cento#Duzentos#Trezentos#Quatrocentos#quinhentos#Seiscentos#Setecentos#Oitocentos#Novecentos

if nNumero >=1 .and. nNumero<10
	Return aNum[nNumero]
elseif nNumero >=10 .and. nNumero<100
	if nNumero = 10
		Return aDec[1]
	elseif nNumero>=11 .and. nNumero<=19
		Return aDez[nNumero-10]
	else
		if cIdioma == "3" // ENGLISH
			// Montagem da String em Ingles n„o usa <E> entre dezena e unidade
			Return aDec[int(nNumero/10)] + if(nNumero/10 != int(nNumero/10),;
				"-"+ aNum[nNumero-(10*int(nNumero/10))],"")
		elseif cIdioma == "2" // SPANISH
			If nNumero > 20 .and. nNumero < 30
				// Em espanhol, as dezenas com vinte e ... s„o juntas.
				Return "VEINTI"+aNum[nNumero-20]
			Else
				Return aDec[int(nNumero/10)] + if(nNumero/10 != int(nNumero/10),;
					" "+DevStr("STR0009",cIdioma) +" " + aNum[nNumero-(10*int(nNumero/10))],"")
			Endif
		elseif cIdioma == "1" // Portugues
			Return aDec[int(nNumero/10)] + if(nNumero/10 != int(nNumero/10),;
				" "+DevStr("STR0009",cIdioma) +" " + aNum[nNumero-(10*int(nNumero/10))],"")
		endif
	endif
elseif nNumero >=100 .and. nNumero <1000
	If nNumero = 100
		Return(DevStr("STR0048",cIdioma)) // "Cem"
	Else
		if cIdioma == "2" .Or. cIdioma == "3"  // SPANISH e ENGLISH
			// Montagem em espanhol e ingles n„o usa "e" entre centena e dezena
			Return aCen[int(nNumero/100)] +  if(nNumero/100 != int(nNumero/100),;
				" "+extens_(nNumero-(100*int(nNumero/100)),cIdioma),"")
		else
			Return aCen[int(nNumero/100)] +  if(nNumero/100 != int(nNumero/100),;
				" "+DevStr("STR0009",cIdioma)+" "+extens_(nNumero-(100*int(nNumero/100)),cIdioma),"")
		endif
	Endif
Endif
Return ""

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ DevStr	³ Autor ³ RONNY CTVRTNIK        ³ Data ³ 30/10/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna texto dependendo do parametro cIdioma            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ DevStr(cStrTexto, cIdioma)					              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cTexto     -Codigo do String a ser retornado				  ³±±
±±³			 ³ cIdioma  -Idioma para retorno da string                  ³±±
±±³			 ³ 			   sendo 1 = portugues, 2 = Espanhol 3 = Ingles   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Extenso   												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function DevStr(cTexto,cIdioma)
Local cRet := ""
if cIdioma == "1" // Portugues
	Do Case
	case cTexto == "STR0001";cRet := "Milhao"
	case cTexto == "STR0002";cRet := "Milhoes"
	case cTexto == "STR0003";cRet := Iif(cPaisLoc == "PTG","Milhar de Milhão","Bilhao")
	case cTexto == "STR0004";cRet := Iif(cPaisLoc == "PTG","Milhares de Milhão","Bilhoes")
	case cTexto == "STR0005";cRet := Iif(cPaisLoc == "PTG","Numero Maior Que 1 Bilião","NUMERO MAIOR QUE 1 TRILHAO")
	case cTexto == "STR0006";cRet := "Mil"
	case cTexto == "STR0007";cRet := "Sem Uso"
	case cTexto == "STR0008";cRet := "Sem Uso"
	case cTexto == "STR0009";cRet := "e"
	case cTexto == "STR0010";cRet := Iif(cPaisLoc == "PTG","Cêntimo","Centavo")
	case cTexto == "STR0011";cRet := Iif(cPaisLoc == "PTG","Cêntimos","Centavos")
	case cTexto == "STR0012";cRet := "Um"
	case cTexto == "STR0013";cRet := "Dois"
	case cTexto == "STR0014";cRet := "Tres"
	case cTexto == "STR0015";cRet := "Quatro"
	case cTexto == "STR0016";cRet := "Cinco"
	case cTexto == "STR0017";cRet := "Seis"
	case cTexto == "STR0018";cRet := "Sete"
	case cTexto == "STR0019";cRet := "Oito"
	case cTexto == "STR0020";cRet := "Nove"
	case cTexto == "STR0021";cRet := "Onze"
	case cTexto == "STR0022";cRet := "Doze"
	case cTexto == "STR0023";cRet := "Treze"
	case cTexto == "STR0024";cRet := "Quatorze"
	case cTexto == "STR0025";cRet := "Quinze"
	case cTexto == "STR0026";cRet := Iif(cPaisLoc == "PTG","Dezasseis","Dezesseis")
	case cTexto == "STR0027";cRet := Iif(cPaisLoc == "PTG","Dezassete","Dezessete")
	case cTexto == "STR0028";cRet := "Dezoito"
	case cTexto == "STR0029";cRet := Iif(cPaisLoc == "PTG","Dezanove","Dezenove")
	case cTexto == "STR0030";cRet := "Dez"
	case cTexto == "STR0031";cRet := "Vinte"
	case cTexto == "STR0032";cRet := "Trinta"
	case cTexto == "STR0033";cRet := "Quarenta"
	case cTexto == "STR0034";cRet := "Cinquenta"
	case cTexto == "STR0035";cRet := "Sessenta"
	case cTexto == "STR0036";cRet := "Setenta"
	case cTexto == "STR0037";cRet := "Oitenta"
	case cTexto == "STR0038";cRet := "Noventa"
	case cTexto == "STR0039";cRet := "Cento"
	case cTexto == "STR0040";cRet := "Duzentos"
	case cTexto == "STR0041";cRet := "Trezentos"
	case cTexto == "STR0042";cRet := "Quatrocentos"
	case cTexto == "STR0043";cRet := "Quinhentos"
	case cTexto == "STR0044";cRet := "Seiscentos"
	case cTexto == "STR0045";cRet := "Setecentos"
	case cTexto == "STR0046";cRet := "Oitocentos"
	case cTexto == "STR0047";cRet := "Novecentos"
	case cTexto == "STR0048";cRet := "Cem"
	case cTexto == "STR0053";cRet := "com"
	case cTexto == "STR0054";cRet := "de"
	end case
elseif cIdioma == "2" // Espanhol
	do case
	case cTexto == "STR0001";cRet := "Millon"
	case cTexto == "STR0002";cRet := "Millones"
	case cTexto == "STR0003";cRet := "Mil Millones"
	case cTexto == "STR0004";cRet := "Mil Millones"
	case cTexto == "STR0005";cRet := "NUMERO MAYOR QUE 1 BILLON"
	case cTexto == "STR0006";cRet := "Mil"
	case cTexto == "STR0007";cRet := "Sem Uso"
	case cTexto == "STR0008";cRet := "Sem Uso"
	case cTexto == "STR0009";cRet := "y"
	case cTexto == "STR0010";cRet := "Centavo"
	case cTexto == "STR0011";cRet := "Centavos"
	case cTexto == "STR0012";cRet := "Un"
	case cTexto == "STR0013";cRet := "Dos"
	case cTexto == "STR0014";cRet := "Tres"
	case cTexto == "STR0015";cRet := "Cuatro"
	case cTexto == "STR0016";cRet := "Cinco"
	case cTexto == "STR0017";cRet := "Seis"
	case cTexto == "STR0018";cRet := "Siete"
	case cTexto == "STR0019";cRet := "Ocho"
	case cTexto == "STR0020";cRet := "Nueve"
	case cTexto == "STR0021";cRet := "Once"
	case cTexto == "STR0022";cRet := "Doce"
	case cTexto == "STR0023";cRet := "Trece"
	case cTexto == "STR0024";cRet := "Catorce"
	case cTexto == "STR0025";cRet := "Quince"
	case cTexto == "STR0026";cRet := "Dieciseis"
	case cTexto == "STR0027";cRet := "Diecisiete"
	case cTexto == "STR0028";cRet := "Dieciocho"
	case cTexto == "STR0029";cRet := "Diecinueve"
	case cTexto == "STR0030";cRet := "Diez"
	case cTexto == "STR0031";cRet := "Veinte"
	case cTexto == "STR0032";cRet := "Treinta"
	case cTexto == "STR0033";cRet := "Cuarenta"
	case cTexto == "STR0034";cRet := "Cincuenta"
	case cTexto == "STR0035";cRet := "Sesenta"
	case cTexto == "STR0036";cRet := "Setenta"
	case cTexto == "STR0037";cRet := "Ochenta"
	case cTexto == "STR0038";cRet := "Noventa"
	case cTexto == "STR0039";cRet := "Ciento"
	case cTexto == "STR0040";cRet := "Doscientos"
	case cTexto == "STR0041";cRet := "Trescientos"
	case cTexto == "STR0042";cRet := "Cuatrocientos"
	case cTexto == "STR0043";cRet := "Quinientos"
	case cTexto == "STR0044";cRet := "Seiscientos"
	case cTexto == "STR0045";cRet := "Setecientos"
	case cTexto == "STR0046";cRet := "Ochocientos"
	case cTexto == "STR0047";cRet := "Novecientos"
	case cTexto == "STR0048";cRet := "Cien"
	case cTexto == "STR0053";cRet := "con"
	case cTexto == "STR0054";cRet := "de"
	end case
elseif cIdioma == "3" // Ingles
	do case
	case cTexto == "STR0001";cRet := "Million"
	case cTexto == "STR0002";cRet := "Millions"
	case cTexto == "STR0003";cRet := "Billion"
	case cTexto == "STR0004";cRet := "Billions"
	case cTexto == "STR0005";cRet := "NUMBER GREATER THAN 1000 BILLION"
	case cTexto == "STR0006";cRet := "Thousand"
	case cTexto == "STR0007";cRet := "Sem Uso"
	case cTexto == "STR0008";cRet := "Sem Uso"
	case cTexto == "STR0009";cRet := "and"
	case cTexto == "STR0010";cRet := "Cents"
	case cTexto == "STR0011";cRet := "Cents"
	case cTexto == "STR0012";cRet := "One"
	case cTexto == "STR0013";cRet := "Two"
	case cTexto == "STR0014";cRet := "Three"
	case cTexto == "STR0015";cRet := "Four"
	case cTexto == "STR0016";cRet := "Five"
	case cTexto == "STR0017";cRet := "Six"
	case cTexto == "STR0018";cRet := "Seven"
	case cTexto == "STR0019";cRet := "Eight"
	case cTexto == "STR0020";cRet := "Nine"
	case cTexto == "STR0021";cRet := "Eleven"
	case cTexto == "STR0022";cRet := "Twelve"
	case cTexto == "STR0023";cRet := "Thirteen"
	case cTexto == "STR0024";cRet := "Fourteen"
	case cTexto == "STR0025";cRet := "Fifteen"
	case cTexto == "STR0026";cRet := "Sixteen"
	case cTexto == "STR0027";cRet := "Seventeen"
	case cTexto == "STR0028";cRet := "Eighteen"
	case cTexto == "STR0029";cRet := "Nineteen"
	case cTexto == "STR0030";cRet := "Ten"
	case cTexto == "STR0031";cRet := "Twenty"
	case cTexto == "STR0032";cRet := "Thirty"
	case cTexto == "STR0033";cRet := "Forty"
	case cTexto == "STR0034";cRet := "Fifty"
	case cTexto == "STR0035";cRet := "Sixty"
	case cTexto == "STR0036";cRet := "Seventy"
	case cTexto == "STR0037";cRet := "Eighty"
	case cTexto == "STR0038";cRet := "Ninety"
	case cTexto == "STR0039";cRet := "One Hundred"
	case cTexto == "STR0040";cRet := "Two Hundred"
	case cTexto == "STR0041";cRet := "Three Hundred"
	case cTexto == "STR0042";cRet := "Four Hundred"
	case cTexto == "STR0043";cRet := "Five Hundred"
	case cTexto == "STR0044";cRet := "Six Hundred"
	case cTexto == "STR0045";cRet := "Seven Hundred"
	case cTexto == "STR0046";cRet := "Eight Hundred"
	case cTexto == "STR0047";cRet := "Nine Hundred"
	case cTexto == "STR0048";cRet := "One Hundred"
	case cTexto == "STR0053";cRet := "with"
	case cTexto == "STR0054";cRet := "of"
	end case
endif
Return(cRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MaTravaReg³ Autor ³Eduardo Riera          ³ Data ³23.03.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica de Travamento de Registros para evitar Dead-Lock   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Registro Travado                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array para adicionar registro                        ³±±
±±³          ³ExpL2: .T.-SoftLock,.F.-RecLock                             ³±±
±±³          ³ExpL3: Append                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MaTravas(aTravas,lSoft,lAppend)

Local lTravas	:= .F.

lSoft		:= If( lSoft == Nil , .T. , lSoft )
lAppend	:= If( lAppend==Nil , .F. , lAppend )

lTravas 	:= If(lSoft,SoftLock(Alias()),RecLock(Alias(),lAppend))
If ( lTravas )
	If ( aScan(aTravas,{|x| x[1]==Alias() .And. x[2]==RecNo()}) == 0 )
		aadd(aTravas,{ Alias() , RecNo() })
	EndIf
EndIf
Return(lTravas)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MaDesTrava³ Autor ³Eduardo Riera          ³ Data ³23.03.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Destravamento de Multiplos Registros                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array [1] Alias()                                    ³±±
±±³          ³             [2] RecNo(0                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MaDesTrava(aTravas)

Local aArea 	:= GetArea()
Local nCntFor	:= 0

If ( aTravas!=Nil )
	For nCntFor := 1 To Len(aTravas)
		dbSelectArea(aTravas[nCntFor,1])
		dbGoto(aTravas[nCntFor,2])
		MsUnLock()
	Next nCntFor
EndIf

RestArea(aArea)

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SGetArea  ³ Autor ³ Eduardo Riera         ³ Data ³23.03.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Salva a WorkArea de Alias                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpA1: Array atualizado                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array para guardar as workareas                      ³±±
±±³          ³ExpC2: WorkArea a ser guardada                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SGetArea(aArea,cAlias)

Local cSavAlias	:= Alias()

cAlias := If(cAlias==Nil,Alias(),cAlias)
aArea	 := If(aArea==Nil, {} , aArea )

dbSelectArea(cAlias)

aadd(aArea,{ Alias() , IndexOrd() , RecNo() })

dbSelectArea(cSavAlias)

Return(aArea)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SRestArea ³ Autor ³Eduardo Riera          ³ Data ³ 23.03.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Restaura as Areas Salvas pela funcao SGetArea              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array com as WorkAreas                               ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function SRestArea(aArea)

Local cSavAlias := Alias()
Local nCntFor	 := 0

For nCntFor := Len(aArea) To 1 Step -1
	dbSelectArea(aArea[nCntFor,1])
	dbSetOrder(aArea[nCntFor,2])
	dbGoto(aArea[nCntFor,3])
Next nCntFor

dbSelectArea(cSavAlias)

Return(aArea)

Function VerProtheus()
Return .T.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ QtdComp() ³ Autor ³ Fernando Joly Siquini ³ Data ³ 31/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Arredonda um numero em N casas decimais.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QtdComp(ExpN1,ExpL1)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Variavel Num‚rica                                   ³±±
±±³          ³ ExpL1 = Indica se esta comparando com qtd zero              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna um numero com quandidade fixa de casas decimais.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rica !!! UTILIZAR SOMENTE PARA COMPARACOES !!!          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function QtdComp(nQtdOrig,lCompZero)

Local nDecimais  := 8
Local nQtdDest   := Round(0,nDecimais)
Local cQtdDest   := ""

DEFAULT lCompZero := .F.

If nQtdOrig # Nil .And. ValType(nQtdOrig) == 'N'
	// Multiplica por 1000 para resolver problemas com valores somente na oitava decimal
	If lCompZero
		nQtdOrig:=nQtdOrig*1000
	EndIf
	nQtdDest := Round(nQtdOrig, nDecimais)
	cQtdDest := Str(nQtdDest,30,nDecimais)
	nQtdDest := Val(cQtdDest)
EndIf

Return nQtdDest

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³QA_CvKey	³ Autor ³ Vera / Wanderley 	  ³ Data ³ 02.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Converte o codigo passado como param. para um cod. chave   ³±±
±±³			 ³ de ligacao de arquivos. 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_CvKey(ExpC1,ExpC2,ExpN1)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Campos chaves									  ³±±
±±³          ³ ExpC2 = Alias do arquivo que contem o campo onde ser  gra- ³±±
±±³          ³         vada a chave gerada.                               ³±±
±±³          ³ ExpN1 = Order do campo onde ser  gravada a chave gerada.   ³±±
±±³          ³         (Dever  existir indice por este campo, para consis-³±±
±±³          ³          tir se a chave gerada j  existe).                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Siga Quality  		    								  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_CvKey( cCampos, cAliKey, nOrdKey, cAliKey2, nOrdKey2, lTexto, aTextos )

Local nSoma		:=0
Local nA        :=0
Local lLoopTxt  := .F.
Local k
Local nSec		:= Aleatorio(9999999,0)
Local cChave	:= ""
Local cChvKey	:= ""
Local nOrdOld	:= &cAliKey.->(IndexOrd())
Local nRecOld	:= &cAliKey.->(Recno())
Local nOrdOld2	:= If( cAliKey2 == NIL, 0, &cAliKey2.->(IndexOrd()))
Local nRecOld2	:= If( cAliKey2 == NIL, 0, &cAliKey2.->(Recno()))

Default lTexto  := .F.
Default aTextos := {}

// Seta Order pelo campo que guarda a chave gerada
&cAliKey.->(dbSetOrder(nOrdKey))

If cAliKey2 <> NIL
	nOrdKey2 := If( nOrdKey2 == NIL,1,nOrdKey2 )
	&cAliKey2.->(DbSetOrder(nOrdKey2))
End
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Converte os campos para um codigo chave  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For k:= 1 to len(cCampos)
	nSoma+=asc(Substr(cCampos,k,1))*k
Next k
cChvKey := StrZero(nSoma,8)

While .T.
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma os segundos do momento, na chave gerada, para evitar repeticoes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cChave := StrZero(Val(cChvKey) + nSec,8)

	// Verifica se a chave gerada j  existe
	If lTexto
		lLoopTxt := .F.
		For nA :=1 to Len(aTextos)
			If QA2->(dbSeek(xFilial("QA2")+Padr(aTextos[nA],8)+cChave) )
				lLoopTxt := .T.
				Exit
			Endif
		Next
		If lLoopTxt
			If nSec < 0
				nSec := Aleatorio(9999999,0)
			EndIf
			nSec++
			Loop
		Endif
	Endif
	If &cAliKey.->(dbSeek(xFilial(cAliKey)+cChave))
		If nSec < 0
			nSec := Aleatorio(9999999,0)
		EndIf
		nSec++
		Loop
	Else
		If cAliKey2 <> NIL
			If &cAliKey2.->(dbSeek(xFilial(cAliKey)+cChave))
				If nSec < 0
					nSec := Aleatorio(9999999,0)
				EndIf
				nSec++
				Loop
			Else
				Exit
			Endif
		Else
			Exit
		Endif
	EndIf
EndDo

// Volta order e registro originais
&cAliKey.->(dbSetOrder(nOrdOld))
&cAliKey.->(dbGoTo(nRecOld))

If cAliKey2 <> NIL
	&cAliKey2.->(DbSetOrder(nOrdOld2))
	&cAliKey2.->(DbGoTo(nRecOld2))
End
Return(cChave)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ QA_JUST	³ Autor ³ Vera Lucia S. Simoes  ³ Data ³ 08.04.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta janela p/ digitacao da Justificativa e a grava no QA3³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ QA_JUST(ExpC1,ExpC2,ExpC3,ExpL1,ExpC4,ExpL2) 			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Titulo da janela									  ³±±
±±³			 ³ ExpC2 = Especie da Justificativa (Identifica a justif.)	  ³±±
±±³			 ³ ExpC3 = Chave j  convertida								  ³±±
±±³			 ³ ExpL1 = Gera historico das justificativas 				  ³±±
±±³			 ³ ExpC4 = Valor anterior									  ³±±
±±³			 ³ ExpL2 = Verifica se Justificativa deve ser recuperada 	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Siga Quality      										  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function QA_JUST(cTit,cEspecie,cChave,lHist,cAnt,lJust)
Local cJust := Space(TamSX3("QA3_TEXTO")[1])
Local nOpca := 0
Local oDlgJust
Local oGetJust

If lJust == NIL
	lJust := .f.
EndIf

If lHist == NIL
	lHist := .t.
EndIf

While .t.
	DEFINE MSDIALOG oDlgJust FROM 123,164 TO 218,575;
		TITLE cTit PIXEL OF GetWndDefault()

	@ 3, 4 TO 31, 199 LABEL OemToAnsi(STR0056) OF oDlgJust  PIXEL  // "Justificativa"
	@ 14, 10 MSGET oGetJust VAR cJust PICTURE "@S55" ;
		SIZE 180, 10 OF oDlgJust PIXEL VALID !Empty( cJust )

	DEFINE SBUTTON FROM 35, 170 TYPE 1 ENABLE OF oDlgJust ;
		Action (oDlgJust:End())

	ACTIVATE MSDIALOG oDlgJust Centered VALID !Empty( cJust )
	nOpca := 1
	Exit

EndDo

If nOpcA == 1
	If !lHist
		If QA3->(dbSeek(xFilial("QA3")+cEspecie+cChave))
			RecLock("QA3",.F.)
		Else
			RecLock("QA3",.T.)
		EndIf
	Else
		RecLock("QA3",.T.)
	EndIf
	QA3->QA3_FILIAL	:= xFilial("QA3")
	QA3->QA3_ESPEC 	:= cEspecie
	QA3->QA3_CHAVE 	:= cChave
	QA3->QA3_TEXTO 	:= cJust
	QA3->QA3_DATA		:= dDataBase
	QA3->QA3_DATINV	:= Inverte(dDataBase)
	If cAnt <> NIL
		QA3->QA3_ANT	:= cAnt
	EndIf
EndIf


Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A100ReDV  ³ Autor ³ Vera Lucia S. Simoes  ³ Data ³17/08/1998³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Inicializa campo Data Validade do IQS do Fornecedor         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Data de Validade do IQS                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³SigaQIE ->  ( Cad. Fornecedores) - SX3(X3_RELACAO) A2_DTVAL ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A100ReDV()
Local dRet  := CtoD("  /  /  ")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Sugere 1 ano a partir da Data da Avaliacao  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(M->A2_DTAVA)
	dRet := M->A2_DTAVA + 365
EndIf
Return dRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CpoRetF3 ³ Autor ³Edson Maricate         ³ Data ³ 31.03.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna a consulta padrao do campo no SX3.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ CpoRetF3(ExpC1,ExpC2)                                      ³±±
±±³          ³   ExpC1 : Nome do campo.                                   ³±±
±±³          ³   ExpC2 : Consulta default do campo.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CpoRetF3(cCampo,cDefault)
Local aArea	:= GetArea()
Local cF3 	:= ""
Local cSavRec
cDefault 	:= IIF(cDefault==Nil,"",cDefault)

dbSelectArea("SX3")
cSavRec	:= RecNo()
dbSetOrder(2)
If dbSeek(cCampo)
	If !Empty(X3_F3)
		cF3 := X3_F3
	Else
		cF3 := cDefault
	EndIf
EndIf
dbSetOrder(1)
dbGoto(cSavRec)
RestArea(aArea)

Return cF3


Function IWLenNum(nNum)
Local nPart1, nPart2,cStr, ni, cByte
nPart2 := Round(nNum-Int(nNum),12)
cStr := Str(nPart2,14,12)
For ni := Len(cStr) to 1 Step -1
	cByte := Subs(cStr,ni,1)
	If cByte != "0"
		Exit
	EndIf
Next
IF ni > 2
	nPart2 := ni - 2
Else
	nPart2 := 0
Endif
ni := 0
While .t.
	nPart1 := Int(nNum) / (10 ** ni)
	IF nPart1 >= 1
		ni++
		Loop
	Endif
	Exit
End
nPart1 := nI
Return( nPart1 + nPart2 + If ( nPart2 != 0, 1, 0 ))


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ FA290COND  ³ Autor ³ J£lio Wittwer     	  ³ Data ³ 05.04.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida Condicao de Pagamento da Fatura        	 		    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Fina290													    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Fa290Cond(cCondPg)

SE4->(DbSeek(xFilial("SE4")+cCondPg))
If Se4->E4_TIPO=="9"
	Help(" ",1,"A290COND")
	Return .F.
ElseIf SE4->E4_TIPO == "A"
	// As condicoes de pagamento do tipo A são exclusivas dos modulos SIGAVEI e SIGAOFI.
	Help(" ",1,"FA290CONA")
	Return .F.
Endif
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³TmkSeg    ³ Autor ³Luis Marcelo Kotaki    ³ Data ³28/06/98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Verifica se os segmentos do cliente foram preenchidos       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso	     ³SA1 (Clientes) e SUS	(Prospect)         					  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TmkSeg(nOpcao)
Local lRet := .T.

IIf(nOpcao == NIL,"1",nOpcao)

If (nOpcao == "1") // Cadastro de Clientes
	If Empty(M->A1_SATIV1) .And. Empty(M->A1_SATIV2) .And. Empty(M->A1_SATIV3) .And.;
			Empty(M->A1_SATIV4) .And. Empty(M->A1_SATIV5) .And. Empty(M->A1_SATIV6) .And.;
			Empty(M->A1_SATIV7) .And. Empty(M->A1_SATIV8)
		lRet := .F.
		MsgStop(OemToAnsi(STR0059),OemToAnsi(STR0058)) //"Informe um Segmento para esse cliente","Aten‡„o"
	EndIf

ElseIf (nOpcao == "2") //Cadastro de Prospect
	If Empty(M->US_SATIV)  .And. Empty(M->US_SATIV2) .And. Empty(M->US_SATIV3) .And.;
			Empty(M->US_SATIV4) .And. Empty(M->US_SATIV5) .And. Empty(M->US_SATIV6) .And.;
			Empty(M->US_SATIV7) .And. Empty(M->US_SATIV8)
		lRet := .F.
		MsgStop(OemToAnsi(STR0059),OemToAnsi(STR0086)) //"Informe um Segmento para esse prospect","Aten‡„o"
	EndIf
EndIf


Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ProcLote  ³ Autor ³Gilson Nascimento      ³ Data ³15/07/99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Dispara funcao para verificar a data de Validade dos Lotes  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso 	     ³TmkA010			          								  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ProcLote()
Local lRet := .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se lotes estao com validade vencida						 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMV("MV_LOTVENC") == "N" .And. GetMV("MV_RASTRO") == "S" .And. !FWIsInCallStack('ENGSETCOMPANY')
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ PROCLOTE - Ponto de Entrada utilizada para verificar se executa o    |
	//|            bloqueio de lotes na inicializacao do sistema.            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("MBLQLOTE")
		lRet := ExecBlock("MBLQLOTE",.F.,.F.)
	EndIf
	If lRet
		Processa({|lEnd| BloqData()},OemToAnsi(STR0060),OemToAnsi(STR0061),.F.) //"Verificando Data de Validade dos Lotes"###"Verificando Lotes com data de validade vencida ..."
	EndIf
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ ChkMoeda ³ Autor ³ Wagner Xavier 		³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe nova moeda, se sim cria novas contas	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CHKMOEDA()

*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
*³Verifica se ha'moeda nova, caso haja, devera' ser criada no plano de  ³
*³contas em outras moedas as contas para a nova moeda.					³
*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SI1")
IF Reccount() != 0
	NewMoeda()
EndIF
MsUnlockAll()

dbSelectArea("SI1")
dbSeek(cFilial)

dbSelectArea("SI7")
dbSeek(cFilial)

return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ NewMoeda ³ Autor ³ Wagner Xavier 	    ³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se ha' moeda nova                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FuncTion NewMoeda()
Local k,cMoeda,cCodigo

dbSelectArea("SI1")
dbSeek(cFilial)
cCodigo:=I1_CODIGO

For k:=2 To 5
	cMoeda:=Str(k,1)
	IF Empty(GetMV("MV_MOEDA"+cMoeda))
		Exit
	EndIF
	dbSelectArea("SI7")
	dbSeek(cFilial+cCodigo+cMoeda)
	IF !Found()
		AbreCta(cMoeda)
	EndIF
Next k
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ F_ULTDIA ³ Autor ³ EQUIPE DE RH -Mauro   ³ Data ³ 29.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para Calcular Ultimo dia do mes                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ F_ULTDIA(Data)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Data = Data de referencia                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION F_ULTDIA(dDat)
Local fmes , fdia := {31,28,31,30,31,30,31,31,30,31,30,31}
fmes := Month(dDat)
Return If(Mod(Year(dDat),4)=0 .And. fmes = 2 , 29, fdia[fmes] )


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ fHist    ³ Autor ³ Mauro Sergio          ³ Data ³ 22/04/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao para Testar se Grava o Historico  sem efeito        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ fHist                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ gpa010 - X3_VALID e  X3_VLDUSER                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function FHIST
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ lIsDir 	³ Autor ³ Julio Wittwer         ³ Data ³ 16.08.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna .T. se o diret¢rio passado como parƒmetro existe.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lIsDir(cCaminho)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cCaminho   - Path a ser verificada a existˆncia. 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observa‡„o³ Fun‡„o implementada para compatibiliza‡„o c/ Protheus.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function lIsDir(cCaminho)
Local lIsDir := .F.
If !empty(cCaminho) .and. !("\\"$cCaminho)
	cCaminho := alltrim(cCaminho)
	if Right(cCaminho,1) == "\"
		cCaminho := SubStr(cCaminho,1,len(cCaminho)-1)
	Endif
	lIsDir :=(Ascan( Directory(cCaminho,"D"),{|_Vet | "D" $ _Vet[5] } ) > 0)
EndIf
Return lIsDir



/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ConValor	³ Autor ³ Wagner Xavier 		³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Converte valor do cheque de String em Numerico			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ConValor()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³FINA470													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function ConValor( cString, nLen )
Local cValor:=cString
Local cValorA:=""
Local k

For k:=1 To Len(cValor)
	IF !SubStr(cValor,k,1) $".,"
		cValorA+=SubStr(cValor,k,1)
	End
	IF SubStr(cValor,k,1) == ","
		cValorA+="."
	End
Next k
If Len(cValorA) <= nLen
	cValorA:=Space(nLen-Len(cValorA))+cValorA
Else
	cValorA:=Right(cValorA,nLen)
End
Return cValorA

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ AbreCta	³ Autor ³ Wagner Xavier 		³ Data ³	      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Abre conta para nova moeda 								  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AbreCta(cMoeda)

MsgRun(OemToAnsi(OemToAnsi(STR0003)+cMoeda+CHR(166)+OemToAnsi(STR0004)),,{|| Criacta(cMoeda)})	//"Criando Contas na "###" Moeda, Aguarde..."
Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ CriaCta	³ Autor ³ Wagner Xavier 		³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria Conta para nova moeda 								  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static FuncTion CRIACta(cMoeda)

dbSelectArea("SI1")
dbSeek(cFilial)
While !EOF() .and. I1_FILIAL == cFilial
	dbSelectArea("SI7")
	dbSeek(cFilial+SI1->I1_CODIGO+cMoeda)
	IF !Found()
		Reclock("SI7",.T.)
		Replace I7_FILIAL With cFilial,I7_CODIGO With SI1->I1_CODIGO,I7_MOEDA With cMoeda
		MsUnlock()
	EndIF
	dbSelectArea("SI1")
	dbSkip()
EndDO

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ SOMASTR   ³ Autor ³ J£lio Wittwer         ³ Data ³ 18.10.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cRetorno := SOMASTR( <cValor1>,<cValor2> )                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³ cValor1 : String num‚rica a ser somada                      ³±±
±±³          ³ cValor2 : String num‚rica a ser somada                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna o valor correspondente … soma das Strings           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SOMASTR(cValor1,cValor2)
Local cRetSoma := ""
Local nMaxTam  := max(len(cValor1),len(cValor2))
Local nPos :=0 , nVai     := 0 , nSoma    := 0

cValor1:=padl(alltrim(cValor1),nMaxTam,"0")
cValor2:=padl(alltrim(cValor2),nMaxTam,"0")
For nPos := nMaxTam to 1 STEP -1
	nSoma := val(substr(cValor1,nPos,1)) + val(substr(cValor2,nPos,1))
	nSoma += nVai
	cRetSoma := right(str(nSoma,2),1) + cRetSoma
	nVai     := val( left(str(nSoma,2),1) )
Next
IF nVai>0
	cRetSoma := right(str(nVai,2),1) + cRetSoma
Endif
Do while left(cRetSoma,1) == "0"
	cRetSoma := Substr(cRetSoma,2)
Enddo
Return if(len(cRetSoma)=0,"0",cRetSoma)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ MULTSTR   ³ Autor ³ J£lio Wittwer         ³ Data ³ 18.10.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ cRetorno := MULTSTR( <cValor1>,<cValor2> )                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³ cValor1 : String num‚rica a ser multiplicada                ³±±
±±³          ³ cValor2 : String num‚rica a ser multiplicada                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna o valor correspondente … multiplica‡„o das Strings  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MultStr(cValor1,cValor2)
Local cRetMult := "" , cRetSoma := ""
Local nMaxTam  := max(len(cValor1),len(cValor2))
Local nPos1    := 0  , nPos2    := 0 , nVai     := 0
Local aSomar   := {} , nSoma    := 0 , nMult    := 0

cValor1:=padl(alltrim(cValor1),nMaxTam,"0")
cValor2:=padl(alltrim(cValor2),nMaxTam,"0")
For nPos2 := nMaxTam to 1 STEP -1
	nMult := val(substr(cValor2,nPos2,1))
	IF nMult = 0
		aadd(aSomar,"0")
	Else
		nVai:=0
		cRetSoma := replicate("0",nMaxTam-nPos2)
		For nPos1 := nMaxTam to 1 STEP -1
			nSoma  := val(substr(cValor1,nPos1,1)) * nMult
			nSoma  += nVai
			cRetSoma := right(str(nSoma,2),1) + cRetSoma
			nVai     := val( left(str(nSoma,2),1) )
		Next
		IF nVai>0
			cRetSoma := right(str(nVai,2),1) + cRetSoma
		Endif
		aadd(aSomar,cRetSoma)
	Endif
Next
cRetMult  := aSomar[1]
IF len(aSomar)>1
	For nMult := 2 to len(aSomar)
		cRetMult := SomaStr(cRetMult,aSomar[nMult])
	Next
Endif
Do while left(cRetMult,1) == "0"
	cRetMult := Substr(cRetMult,2)
Enddo
Return if(len(cRetMult)=0,"0",cRetMult)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³VldHora	³ Autor ³ Wiliam R. Bertoncini  ³ Data ³ 12.02.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consiste se a hora digitada ‚ v lida.					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ VldHora(ExpN1) 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Hora no formato 99.99 						      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function VldHora( nCampo )

//-- Variaveis Locais.
Local lRet		:= .T.
Local nHoras	:= 0
Local nMinutos := 0

nHoras	:= Val(Left (StrZero(nCampo,5,2),2))
nMinutos := Val(Right(StrZero(nCampo,5,2),2))

If nHoras < 0 .Or. nHoras > 23 .Or. nMinutos < 0 .Or. nMinutos > 59
	lRet := .F.
EndIf

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ StrHora()  ³ Autor ³ Marcelo Iuspa       ³ Data ³ 01/08/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao generica para campos hora                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function StrHora(cHora)
cHora := If(cHora = Nil, &(ReadVar()), cHora)

If Val(Left(cHora, 2)) > 23 .Or. Val(Right(cHora, 2)) > 59 .Or. "-" $ cHora
	Help(" ",1,"VldHora")
	Return(.F.)
EndIf
Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Tira1       ³ Autor ³ J£lio Wittwer      ³ Data ³ 16.11.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Faz o contr rio da Soma1()                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Tira1(cDado)
Local lTemAlpha := .F. , nPos
Local cRetStr:= "" , lMenos := .T.
Local cSequenc := "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"
cDado:=upper(cDado)
For nPos:=1 to len(cDado)
	IF !substr(cDado,nPos,1)$"0123456789"
		lTemAlpha := .T.
	Endif
Next
IF lTemAlpha
	For nPos := len(cDado) to 1 STEP -1
		nAt := at(substr(cDado,nPos,1),cSequenc)
		IF lMenos
			If nAt <= 1
				cRetStr := "9" + cRetStr
			Else
				cRetStr := substr(cSequenc,nAt-1,1) + cRetStr
				lMenos:=.F.
			Endif
		Else
			cRetStr := substr(cDado,nPos,1) + cRetStr
		Endif
	Next
Else
	cRetStr := strzero(if(val(cDado)-1<=0,0,val(cDado)-1),len(cDado))
Endif
Return cRetStr

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Fonetica ³ Autor ³ Julio Wittwer 		³ Data ³ 13.11.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o que realiza convers„o fon‚tica de String            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Fonetica(cTexto)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION FONETICA(cTxtIn)

Local nPos, cTxtOut:=""
Local nTamTxt := len(cTxtIn)

If !empty(cTxtIn)
	cTxtIn := Upper(Alltrim(cTxtIn))
	For nPos:=1 to len(cTxtIn)-1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Looping para eliminar letras repetidas   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cTxtOut += If(substr(cTxtIn,nPos,1) != SubStr(cTxtIn,nPos+1,1),;
			Substr(cTxtIn,nPos,1),"")
	Next
	If right(cTxtIn,1) != right(cTxtOut,1)
		cTxtOut += Right(cTxtIn,1)
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Rotina de transforma‡„o fon‚tica            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTxtOut:=StrTran(cTxtOut,"SCH","X")
	cTxtOut:=StrTran(cTxtOut,"CH","X")
	cTxtOut:=StrTran(cTxtOut,"SH","X")
	cTxtOut:=StrTran(cTxtOut,"QUE","K")
	cTxtOut:=StrTran(cTxtOut,"QU","K")
	cTxtOut:=StrTran(cTxtOut,"PH","F")
	cTxtOut:=StrTran(cTxtOut,"CK","K")
	cTxtOut:=StrTran(cTxtOut,"LH","LI")
	cTxtOut:=StrTran(cTxtOut,"W","V")
	cTxtOut:=StrTran(cTxtOut,"S","Z")
	cTxtOut:=StrTran(cTxtOut,"Y","I")
	cTxtOut:=StrTran(cTxtOut,"€","C")
	cTxtOut:=StrTran(cTxtOut,"‡","C")
	cTxtOut:=StrTran(cTxtOut,"U","O")
	cTxtOut:=StrTran(cTxtOut,"H","")
	cTxtOut:=padr(cTxtOut,nTamTxt," ")
Endif
Return cTxtOut

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ TamSxg   ³ Autor ³ Julio Wittwer 	    ³ Data ³ 13.12.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Fun‡„o que devolve caracter¡sticas do SXG                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ TamSxg(cGrupo)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TamSxg(cGrupo)
LOCAL cAlias := ALIAS()
LOCAL __aRet   := {}, nAT


IF (nAT := ASCAN(__aSXG,{|x| x[1] == cGrupo} ) ) > 0
	__aRet := aClone(__aSXG[nAT,2])
Else
	dbSelectArea("SXG")
	dbSetOrder(1)
	If dbSeek(cGrupo)
		__aRet := { SXG->XG_SIZE, SXG->XG_PICTURE, SXG->XG_SIZEMIN, SXG->XG_SIZEMAX }
	Else
		IF cGrupo == "001"                 //Default Cod.Cliente/Fornecedor
			__aRet := {6,"@!",6,20}
		ElseIF cGrupo == "002"             //Default Loj.Cliente/Fornecedor
			__aRet := {2,"@!",2,4}
		ElseIF cGrupo == "003"
			__aRet := {20,"@!",20,50}      //Default Conta COntabil
		ElseIF cGrupo == "004"
			__aRet := {9,"@!",9,15}        //Default Centro de Custos
		Else
			Final("Grupo SXG Inexistente" + cGrupo)
		Endif
	Endif
	AADD(__aSXG,{cGrupo,aClone(__aRet)})
	dbSelectArea(cAlias)
ENdif

Return __aRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡…o    ³ NaoAltera                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Autor     ³ Wagner Xavier                            ³ Data ³ 25.10.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡…o ³ Retorna uma matriz com os campos dispon¡veis para altera‡„o³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Par„metros³ cArea ú Arquivo utilizado                                  ³±±
±±³           ³ aCampos ú Matriz com os campos que n„o est„o dispon¡veis   ³±±
±±³           ³           para altera‡„o ( GET ).                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  Uso      ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function NaoAltera ( cArea, aCampos )

Local aEntradas  := {}
Local nCampo     := 0
Local nElemento  := 0
Local nExcluidos := 0
Local nOrdem     := 0
Local nRegistro  := 0

nRegistro := SX3 -> ( Recno () )
nOrdem    := SX3 -> ( IndexOrd () )
SX3 -> ( dbSetOrder ( 1 ) )
SX3 -> ( dbSeek ( cArea ) )

SX3 -> ( dbEval (                                                         ;
	{ || aAdd ( aEntradas, AllTrim ( SX3 -> X3_CAMPO ) ) }, ;
	{ || x3Uso( SX3 -> X3_USADO ) },                     ;
	{ || SX3 -> X3_ARQUIVO == cArea }                       ;
	,,,.T.)) // <--- o sexto parâmetro precisou receber .T. para o código funcionar

SX3 -> ( dbSetOrder ( nOrdem ) )
SX3 -> ( dbGoTo ( nRegistro ) )
For nCampo := 1 to Len ( aCampos )
	nElemento := aScan ( aEntradas, aCampos [nCampo] )
	If ( nElemento > 0 )
		nExcluidos ++
		aDel ( aEntradas, nElemento )
	End
Next nCampo
aSize ( aEntradas, Len ( aEntradas ) - nExcluidos )

Return ( aEntradas )


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Ctba030Cus³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 10/11/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se C.Custo ja cadastrado                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ctba030Cus(cCusto)

Local lRet := .T.

If nModulo == 34
	dbSelectArea("SX3")
	dbSetOrder(2)
	dbSeek("I3_CONTA")
	dbSetOrder(1)
	If !X3USO(X3_USADO)
		dbSelectArea("SI3")
		dbSetOrder(1)
		If dbSeek(xFilial()+cCusto)
			Help(" ",1,"JAGRAVADO")
			lRet := .F.
		EndIf
	EndIf
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ctba030Cta³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 10/11/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se C.Custo + Conta ja cadastrados                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ctba030Cta(cCusto,cConta)

Local lRet := .t.

If nModulo == 34
	dbSelectArea("SI3")
	dbSetOrder(1)
	If dbSeek(xFilial()+cCusto+cConta)
		Help(" ",1,"JAGRAVADO")
		lRet := .F.
	EndIf
EndIf

Return lRet


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ctba070His³ Autor ³ J£lio Wittwer         ³ Data ³ 16.12.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica validade do hist¢rico padronizado.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Ctba070Hist()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Valida‡„o do Campo CT8->CT8_DESC no SX3                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ctba070Hist()
LOCAL i,j,k,l
LOCAL nBytes:=0
LOCAL Z := 0
LOCAL cHist := &(ReadVar())
LOCAL nTamCpo := Len(CriaVar("CT8_DESC"))

IF Empty(cHist)
	Help(" ",1,"HISTVAZIO")
	Return(.F.)
EndIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula o Tamanho do hist¢rico com uso do @ ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For i:=1 TO Len(cHist)
	IF SubStr( cHist,i,1 ) == "@"
		z++	 ; j := i ; k := ""
		While SubStr( cHist,j,1 ) != " " .and. j < Len( cHist )
			j++
			k += SubStr(cHist,j,1)
		EndDo
		l := Len(Trim(k)) ; 	k := Val(k)
		nBytes := nBytes + k - l
	EndIf
Next
IF (nBytes + Len(Trim(cHist)))-z > nTamCpo
	Help(" ",1,"MUIGRD",,;
		STR0062 + Str((nBytes + Len(Trim(cHist)))-z,3,0) + "     "+;	// Tamanho :
		STR0063 + Str(nTamCpo,3,0),5,0)	// Limite
	Return .F.
EndIF
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AllToChar³ Autor ³ Wilson de Godoy       ³ Data ³ 11/04/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Converte tipos de variaveis Num,Char,Date e Logic para CHAR³±±
±±³          ³ conforme a picture.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGA Advanced for Windows (Planilha)                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function AllToChar( xPar , cPict , lDtos )

Local cRet		:= ""
Local cType		:= ""

DEFAULT lDtos	:= .F.

IF ( ( cType := ValType( xPar ) ) == "N" )
	cPict := IF( cPict == NIL , "9999999999" , cPict )
	cPict := IF( Empty( cPict ) , "9999999999" , cPict )
	cRet  := Transform( xPar , ALLTRIM( cPict ) )
ElseIF ( cType == "C" )
	cPict := IF( cPict == NIL , "" , cPict )
	IF !Empty( cPict )
		cRet  := Transform( xPar , AllTrim( cPict ) )
	Else
		cRet  := xPar
	EndIF
ElseIF ( cType == "L" )
	cRet := IF( xPar , ".T." , ".F." )
ElseIF ( cType == "D" )
	IF ( lDtos )
		cRet := Dtos( xPar )
	Else
		cRet := DToC( xPar )
	EndIF
Else
	cRet := " "
EndIF

Return( cRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o	  ³ Mascara ( cCodigo, cMascara, cLado, nTamanho ) 				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Autor	  ³ Lu¡s C. Cunha 									³ Data ³ 25.08.93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o ³ Retorna uma conta com a formata‡„o de n¡veis.					³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Par„metros³ cCodigo ú C¢digo formatado.											³±±
±±³			  ³ cMascara ú M scara de formata‡„o.									³±±
±±³			  ³ cLado ú Orienta‡„o do c¢digo formatado : 						³±±
±±³			  ³			"E" - … esquerda                                   ³±±
±±³			  ³			"C" - centralizado                                 ³±±
±±³			  ³			"D" - … direita                                    ³±±
±±³			  ³ nTamanho ú Tamanho m ximo do c¢digo retornado. 				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  Uso 	  ³ SigaCon ú Advanced														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mascara ( cCodigo, cMascara, cLado, nTamanho )

Local nNivel	:= 0
Local nPosicao := 0

cCodigo	:= AllTrim ( cCodigo )
cMascara := AllTrim(Iif( cMascara == NIL, GetMV("MV_MASCARA"), cMascara))
cLado 		:= IIF( cLado == NIL, "E", Upper ( cLado ) )
nTamanho 	:= IIF( nTamanho == NIL,;
					 Len ( cCodigo ) + IIF(Len ( cMascara ) == 0, 0, Len ( cMascara )), ;
					 nTamanho)

For nNivel = 1 to Len ( cMascara )
	nPosicao += Val ( Substr ( cMascara, nNivel, 1 ) ) + 1
	If ( nPosicao <= Len ( cCodigo ) )
		cCodigo := Stuff ( cCodigo, nPosicao, 0, "." )
	End
Next

Do Case
Case ( cLado == "E" )
	cCodigo := PadR ( cCodigo, nTamanho )
Case ( cLado == "C" )
	cCodigo := PadC ( cCodigo, nTamanho )
Case ( cLado == "D" )
	cCodigo := PadL ( cCodigo, nTamanho )
End

Return ( cCodigo )
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ NoAcento ³ Autor ³ Julio Wittwer         ³ Data ³15.04.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Remove os acentos de String Acentuada.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION NoAcento(Arg1)
Return FwNoAccent(Arg1)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ VerPadrao³ Autor ³ Wagner Xavier         ³ Data ³ 06/05/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se o codigo Padronizado existe                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ VerPadrao(ExpC1)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Codigo do historico Padronizado a ser pesquisado           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function VerPadrao(cPadrao)
Local aArea  := GetArea()
Local lRet   :=.F.
Local cChave := ""
Local nRecno := 0
DEFAULT cPadrao := ""

If __oVerPad == Nil
	__oVerPad := tHashMap():New()
EndIf

cChave := xFilial("CT5")+cPadrao

If __oVerPad:Get(cChave, @nRecno)     	
	CT5->(DbGoTo(nRecno)) //Garante o posicionamento correto na CT5
	lRet := .T.
Else
	CT5->(dbSetOrder(1))
	If CT5->(MsSeek(cChave))
		While CT5->(!Eof()) .And. CT5->(CT5_FILIAL+CT5_LANPAD) == cChave
			If CT5->CT5_STATUS <> '2'								
				lRet := .T.
				HMSet(__oVerPad,cChave, CT5->(RECNO()))
				Exit
			EndIf
			CT5->(dbSkip())
		EndDo
	EndIf
EndIf

RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MaRateio  ³ Autor ³Eduardo Riera          ³ Data ³05.09.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rateio das diferencas existentes nos calculos de imposto.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Retorno a valor truncado + nDiferenca                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Valor                                                ³±±
±±³          ³ExpN2: Decimal de calculo a ser retornada                   ³±±
±±³          ³ExpA3: Array de controle para as diferencas                 ³±±
±±³          ³ExpN4: Linha do Array                                       ³±±
±±³          ³ExpN5: Coluna do Array                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Procedure MaRateio(nValor,nDecimal,aDiferenca,nLinha,nColuna)

Local nCompara    := (10**(-1*(nDecimal)))
Local nDiferenca  := 0
Local nRetorno    := NoRound(nValor,nDecimal,@nDiferenca,10)

aDiferenca[nLinha][nColuna] += nDiferenca

If ( aDiferenca[nLinha][nColuna] >= nCompara )
	nRetorno    += NoRound(aDiferenca[nLinha][nColuna],nDecimal)
	aDiferenca[nLinha][nColuna] -= NoRound(aDiferenca[nLinha][nColuna],nDecimal)
EndIf
Return(nRetorno)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetSESNew ºAutor  ³Bruno Sobieski      º Data ³  06/13/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Passando como parametro o tipo "interno" de titulo, a funcaoº±±
±±º          ³ retorna todos os titulos variaveis correspondentes.        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ cTipoOrig = Tipo Interno (Tipo original)                   º±±
±±º          ³ cCarteira = Carteira a consultar (Default e ambas).        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAFIN                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GetSESNew(cTipoOrig,cCarteira)
Local aArea,  cRet

DEFAULT cCarteira :=	"3"

cRet	:=	Space(0)

If cTipoOrig == Nil
	Return cRet
Endif

//Salva a area de trabalho
aArea	:=	GetArea()

dbSelectArea("SES")
dbSetOrder(2)
If DbSeek(xFilial()+cTipoOrig)
	While !EOF() .And. cTipoOrig == ES_TIPORIG.And. xFilial() == ES_FILIAL
		If cCarteira == "3" .Or. ES_CARTEIR == "3" .Or. ES_CARTEIR == cCarteira
			cRet	+=	IIf(!Empty(cRet),"|","") + ES_TIPO
		Endif
		DbSkip()
	Enddo
Endif

//Restaura a area de trabalho
RestArea(aArea)
cRet	:=	IIf(Empty(cRet),cTipoOrig,cRet)
Return cRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetSESTiposºAutor  ³Bruno Sobieski      º Data ³ 06/13/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a os tipos de acordo com o bloco de codigo.         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ bProps    = Bloco de codigo com as propriedades a consultarº±±
±±º          ³ cCarteira = Carteira a consultar (Default ‚ ambas).        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAFIN                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GetSESTipos(bProps,cCarteira)
Local aArea, cRet
DEFAULT cCarteira 	:=	"3"

cRet	:=	Space(0)

aArea	:=	GetArea()

DbSelectArea("SES")
DbSetOrder(1)
DbSeek(xFilial())
While !EOF() .And. xFilial() == ES_FILIAL
	If EVAL(bProps) 	.And.( cCarteira $ "3" .Or. ES_CARTEIR $ "3" ;
			.Or. ES_CARTEIR $ cCarteira)
		cRet	+=	ES_TIPO + "|"
	Endif
	dbSkip()
Enddo

cRet	:=	IIf(!Empty(cRet),Substr(cRet,1,Len(cRet)-1),cRet)
RestArea(aArea)
Return	cRet



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ AMIIn    ³ Autor ³ Wilson Godoy          ³ Data ³ 09/06/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se a rotina selecionada esta sendo chamada de 	  ³±±
±±³          ³ maquina com licenca de uso para modulo origem.         	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AMIIn(xMd01,...xMd20)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ xMdN - Numero do Modulo a ser verificado                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Modulos   ³ Numero do Modulo                                           ³±±
±±³          ³                                                            ³±±
±±³SIGAATF   ³ 1                                                          ³±±
±±³SIGACOM   ³ 2                                                          ³±±
±±³SIGAESP   ³ 2                                                          ³±±
±±³SIGACON   ³ 3                                                          ³±±
±±³SIGAEST   ³ 4                                                          ³±±
±±³SIGAFAS   ³ 8                                                          ³±±
±±³SIGAFAT   ³ 5                                                          ³±±
±±³SIGAFIN   ³ 6                                                          ³±±
±±³SIGAFIS   ³ 9                                                          ³±±
±±³SIGAGPE   ³ 7                                                          ³±±
±±³SIGATCF   ³ 7                                                          ³±±
±±³SIGAPCP   ³ 10                                                         ³±±
±±³SIGALOJA  ³ 12                                                         ³±±
±±³SIGATMK   ³ 13                                                         ³±±
±±³SIGAPON   ³ 16                                                         ³±±
±±³SIGAEIC   ³ 17                                                         ³±±
±±³SIGAMNT   ³ 19                                                         ³±±
±±³SIGAGRH   ³                                                            ³±±
±±³SIGAOFI   ³                                                            ³±±
±±³SIGARSP   ³ 20                                                         ³±±
±±³SIGAQMT   ³ 22                                                         ³±±
±±³SIGAQDO   ³ 24                                                         ³±±
±±³SIGAQIP   ³ 25                                                         ³±±
±±³SIGATRM   ³ 26                                                         ³±±
±±³SIGATEC   ³ 28                                                         ³±±
±±³SIGACFG   ³ 99                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AMIIn(xMd01,xMd02,xMd03,xMd04,xMd05,xMd06,xMd07,xMd08,xMd09,xMd10,xMd11,xMd12,xMd13,xMd14,xMd15,xMd16,xMd17,xMd18,xMd19,xMd20,xMd21,xMd22,xMd23,xMd24,xMd25)
Local aT := Array(26), nI, cType, lLight := .f., nLast
Local cFunc := AllTrim(ProcName(1))
Local cFunName := FunName()

DEFAULT cFunName := Space(8)

If FindFunction("FWLSModLight") .And. FWLSVersion() >= 2014
	Return(FWLSModLight(xMd01,xMd02,xMd03,xMd04,xMd05,xMd06,xMd07,xMd08,xMd09,xMd10,xMd11,xMd12,xMd13,xMd14,xMd15,xMd16,xMd17,xMd18,xMd19,xMd20,xMd21,xMd22,xMd23,xMd24,xMd25))
EndIf

IF IsTelNet()
	Return .t.
Endif

If cFunc <> cFunName .and. !IsRdmake(cFunName)
	Return .T.
EndIf

aT[1]  := IIF(xMd01 ==Nil,0,xMd01)
aT[2]  := IIF(xMd02 ==Nil,0,xMd02)
aT[3]  := IIF(xMd03 ==Nil,0,xMd03)
aT[4]  := IIF(xMd04 ==Nil,0,xMd04)
aT[5]  := IIF(xMd05 ==Nil,0,xMd05)
aT[6]  := IIF(xMd06 ==Nil,0,xMd06)
aT[7]  := IIF(xMd07 ==Nil,0,xMd07)
aT[8]  := IIF(xMd08 ==Nil,0,xMd08)
aT[9]  := IIF(xMd09 ==Nil,0,xMd09)
aT[10] := IIF(xMd10 ==Nil,0,xMd10)
aT[11] := IIF(xMd11 ==Nil,0,xMd11)
aT[12] := IIF(xMd12 ==Nil,0,xMd12)
aT[13] := IIF(xMd13 ==Nil,0,xMd13)
aT[14] := IIF(xMd14 ==Nil,0,xMd14)
aT[15] := IIF(xMd15 ==Nil,0,xMd15)
aT[16] := IIF(xMd16 ==Nil,0,xMd16)
aT[17] := IIF(xMd17 ==Nil,0,xMd17)
aT[18] := IIF(xMd18 ==Nil,0,xMd18)
aT[19] := IIF(xMd19 ==Nil,0,xMd19)
aT[20] := IIF(xMd20 ==Nil,0,xMd20)
aT[21] := IIF(xMd21 ==Nil,0,xMd21)
aT[22] := IIF(xMd22 ==Nil,0,xMd22)
aT[23] := IIF(xMd23 ==Nil,0,xMd23)
aT[24] := IIF(xMd24 ==Nil,0,xMd24)
aT[25] := IIF(xMd25 ==Nil,0,xMd25)
aT[26] := 0

If ( GetCountLicense() )
	nLast := LS_GETLICENSE(10001)    // Volta Ultimo Modulo Usado
Else
	nLast := 0
EndIf

If FWLSVersion() < 2009
	If ( nModulo == 54 ) .And. ( IsInListGSP(aT) )
		aT[26] := 54 // Inlui GSP na LISTA se qq um dos módulos acima
	EndIf
	IF ( nLast > 0 ) .And. ( nLast <= 1000 .or. (nLast >= 3501 .and. nLast <= 4000) )
		lLight := .t.
	EndIf
	If lLight
	   nI := aScan(aT, nModulo)
	Else
		For nI:= 1 to 26
			aT[nI] := IIF(aT[nI] > 0,ModType(aT[nI]),"XXX")
		Next nI
		cType := ModType(nModulo)
		nI    := aScan(aT,cType)
	EndIf
Else
	If nLast > 0 .And. !(nLast >= 4000 .And. nLast < 4100) //Faixa do SLOT FULL
		nI := aScan(aT, nModulo)
		If nI == 0 .And. !ModType(nModulo) $ "ALZT" //Parceiros
			nI := aScan(aT, ModType(nModulo,.T.))
		EndIf
	Else
		For nI:= 1 to 26
			aT[nI] := IIF(aT[nI] > 0,ModType(aT[nI]),"XXX")
		Next nI
		cType := ModType(nModulo)
		nI    := aScan(aT,cType)
	EndIf
EndIf
If ( nI == 0 )
	Alert("O Objeto selecionado nao pertence ao modulo escolhido",OemToAnsi("Aten‡„o"))
	Return( .F. )
Else
	Return( .T. )
Endif


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ModType   ºAutor  ³Wilson              º Data ³  06/13/00   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ModType retorna A Para Classic e uma letra diferente para ca-±±
±±º          ³ da parceiro                                                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/


Function ModType(nMod,lNum)
Local uVar, cModulo := StrZero(nMod,2,0)

DEFAULT lNum := .F.

IF IsTelNet() .or. cModulo == "46"
	uVar := If(lNum,46,"T")
ElseIF cModulo $ "12/23/72"   // Loja
	uVar := If(lNum,12,"L")
ElseIf __lPyme
	uVar := If(lNum,0,"A")
ElseIF cModulo == "99"   // Configurador
	uVar := If(lNum,99,"Z")
ElseIF cModulo $ "17/27/29/30/31/50"   //Average
	uVar := If(lNum,17,"I")
ElseIF cModulo $ "19/35/56"  //NG
	uVar := If(lNum,19,"M")
ElseIF cModulo $ "33"  //APSOFT
	uVar := If(lNum,33,"K")
ElseIF cModulo$"11/14/41"   //MIL
	uVar := If(lNum,11,"V")
ElseIf cModulo == '51'   // Gestao hospitalar
	uVar := If(lNum,51, 'R' )
ElseIf cModulo == '54'   // Gestao Serviços Publicos
	uVar := If(lNum,54, 'P' )
Else
	uVar := If(lNum,0,"A")
Endif
Return uVar

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FilBrowse ³ Autor ³Eduardo Riera          ³ Data ³18.05.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de tratamento de filtragem para MBrowse              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias : Alias do Arquivo da MBrowse                        ³±±
±±³          ³aQuery : [1] Nome do Arquivo Fisico                         ³±±
±±³          ³         [2] Ordem Correspondente ao Sindex                 ³±±
±±³          ³cFiltro: Expressao de Filtro para IndRegua                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Ver Utilizacao no Mata130                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FilBrowse(cAlias,aQuery,cFiltro,lShowProc)

Local cArqTmp	:= ""
Local nIndex	:= 0
Local nScan		:= 0
Local nOrdem   := 0
Local cKey		:= ""
Local nCntFor	:= 0

dbSelectArea(cAlias)
If Empty(IndexKey())
	dbSetOrder(1)
EndIf
If ( IndexOrd() != 0 )
	cKey	:= &(IndexKey())
EndIf

If ( !Empty(cFiltro) )

	dbSelectArea(cAlias)
	nOrdem := IndexOrd()

	nScan := aScan(aQuery,{|x| x[2] == nOrdem })


	If ( Len(dbFilter()) == 0 )

		cArqTmp := CriaTrab(,.F.)
		Set Filter To &cFiltro
		nIndex := IndexOrd()-1

		aadd(aQuery,{ cArqTmp , nOrdem })

	Else
		nIndex := IndexOrd()-1

	EndIf

	dbSetOrder(nIndex+1)

	If ( Len(aQuery) > 0 )
		dbSeek(cKey,.T.)
	EndIf

EndIf

Return(nIndex)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ PesqBrw  ³ Autor ³ Edson Maricate        ³ Data ³08.07.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Substitui a chamada da AxPesqui na utlizacao da FilBrowse. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias : Alias do Arquivo da MBrowse                        ³±±
±±³          ³nReg   : Numero do registro                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³04/06/2004³Marinaldo      ³Passagem de Novo parametro bBrwFilter		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function PesqBrw( cAlias , nReg , bBrwFilter )

AxPesqui()
IF ( ValType( bBrwFilter ) == "B" )
	Eval( bBrwFilter )
ElseIF ( Type( "bFiltraBrw" ) == "B" )
	Eval( bFiltraBrw )
EndIF

Return( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ EndFilBrw³ Autor ³ Edson Maricate        ³ Data ³08.07.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna os indices padroes e apaga os arquivos temporarios.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cAlias : Alias do Arquivo da MBrowse                        ³±±
±±³          ³aQuery : [1] Nome do Arquivo Fisico                         ³±±
±±³          ³         [2] Ordem Correspondente ao Sindex                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function EndFilBrw( cAlias , aQuery )

Local aNotErase		:= {}
Local cOrdBagExt	:= OrdBagExt()
Local nQuerys		:= 0.00

DEFAULT cAlias	:= Alias()
DEFAULT aQuery	:= {}

IF ( cAlias )->( !InTransact() )
	( cAlias )->( RetIndex( cAlias ) )
EndIF

( cAlias )->( dbClearFilter() )
IF ( ( nQuerys := Len( aQuery ) ) > 0.00 )
	aEval( aQuery , { |x| IF( Ferase( x[1]+cOrdBagExt ) == -1 , aAdd( aNotErase , aClone( x ) ) , NIL ) } )
	aQuery := aClone( aNotErase )
EndIF

Return( NIL )

//--------------------------------------------------------------------
/*/{Protheus.doc} MatMailEv()
Rotina que retorna eventos disponiveis para envio de e-mail

@author Israel Escorizza
@since 08/12/2016
@version 2.0
@return aEventos
/*/
//--------------------------------------------------------------------
Function MatMailEv(cEvento)
Local aArea	:= GetArea()
Local aEventos:= {}

Default cEvento := ""

dbSelectArea("SX5")
dbSetOrder(1)

If TcSrvType() <> "AS/400"
		lQuery    := .T.
		cAliasSX5 := "MATMAILEV"

		cQuery := "SELECT * "
		cQuery += "FROM "+RetSqlName("SX5")+" SX5 "
		cQuery += "WHERE SX5.X5_FILIAL='"+xFilial("SX5")+"' AND "
		cQuery += "SX5.X5_TABELA	='E3' AND "
		If !Empty(cEvento)
			cQuery += "SX5.X5_CHAVE = '"+cEvento+"' AND "
		Endif
		cQuery += "SX5.D_E_L_E_T_=' ' "
		cQuery += "ORDER BY "+SqlOrder(SX5->(IndexKey()))
		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSX5)
Else
	dbSeek(xFilial("SX5")+"E3"+cEvento)
EndIf


While !Eof() .And. (cAliasSX5)->X5_FILIAL == xFilial("SX5") .And. (cAliasSX5)->X5_TABELA == "E3"

	SX5->(dbSeek( (cAliasSX5)->X5_FILIAL+(cAliasSX5)->X5_TABELA+(cAliasSX5)->X5_CHAVE ))
	AADD(aEventos,{Substr((cAliasSX5)->X5_CHAVE,1,3),X5Descri()})

	dbSelectArea(cAliasSX5)
	dbSkip()

EndDo

If lQuery
	dbSelectArea(cAliasSX5)
	dbCloseArea()
	dbSelectArea("SX5")
EndIf
RestArea(aArea)
Return aEventos

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MExistMail³ Autor ³ Edson Maricate        ³ Data ³ 03/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a existencia de e-mails para um determinado evento³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1:=MExistMail(ExpC1)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do Evento                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MExistMail(cEvento)

Local lRet 		:= .F.
Local aArea		:= GetArea()
Local aAreaSAN	:= SAN->(GetArea())

dbSelectArea("SAN")
dbSetOrder(3)
lRet := DbSeek(xFilial("SAN")+cEvento)

RestArea(aAreaSAN)
RestArea(aArea)

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MEnviaMail³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 03/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina que envia e-mail de acordo com evento pre-cadastrado³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MEnviaMail(ExpC1,ExpA1,ExpC2,ExpC3,ExpC4)                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do evento a ser disparado                   ³±±
±±³          ³ ExpA1 = Array com os dados relativos ao evento             ³±±
±±³          ³ ExpC2 = String com usuarios a serem considerados           ³±±
±±³          ³ ExpC3 = String com grupos de usuarios a serem considerados ³±±
±±³          ³ ExpC4 = String com e-mails a serem considerados            ³±±
±±³          ³ ExpL1 = .T. Se Formato mensagem for HTML                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MEnviaMail(cEvento,aDados,cParUsuario,cParGrUsuario,cParEmails,lEvRH)

Static cMailConta  := NIL
Static cMailServer := NIL
Static cMailSenha  := NIL
Static cMailCtaAut := NIL
Static cMailUsrAut := NIL
Static lAvalMMail  :=NIL
Static lAvalFiltr  :=NIL

Local aArea      := GetArea()
Local aUsers     := {}
Local aUserAlt	 := {}
Local aEventos   := MatMailEv(cEvento)
Local aMTENVML   := {}
Local aMTENVCC 	:= {}
Local cAliasSAN  := "SAN"
Local cAliasSAL  := "SAL"
Local cUsuario   := ""
Local cMensagem  := ""
Local cBackMens  := ""
Local cBlockUser := ""
Local cError     := ""
Local cCodEmp	 := FWGrpCompany()
Local cMailCC		:= ""
Local cMailCCO	:= ""
Local nAux, cUser

Local nMsgGrpApr := SuperGetMV("MV_CNGRMSG",.F.,0) // 0=usuários x eventos, 1=Grupo de Aprovação, 2=Todos, 3=Entidades Contábeis X Grp.Apr.
Local nX         := 0
Local nY         := 0
Local nAcho      := ASCAN(aEventos,{|x| x[1] == cEvento})
Local nAcho2     := 0
Local nLoop      := 0
Local lEnvia     := .F.
Local lOk        := .F.
Local lSendOK    := .F.
Local lBlockUser := ExistBlock("MMENS"+Substr(cEvento,1,3))
Local lSmtpAuth  := GetMv("MV_RELAUTH",,.F.)
Local lAutOk     := .F.
Local aRetorMail:={}
Local i
Local aKeyValues:= {"USR_CODIGO","USR_NOME","USR_EMAIL","USR_MSBLQL"}
Local lNaoEnvia := .F. 
Local aFwAllUsers 

DEFAULT lEvRh         := .F.
DEFAULT cParUsuario   := ""
DEFAULT cParGrUsuario := ""
DEFAULT cParEmails    := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variavel PRIVATE com codigo do evento - NAO DEVE SER ALTERADO           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cFormEvent := cEvento
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Array PRIVATE com dados para formula - NAO DEVE SER ALTERADO            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE aFormDados := ACLONE(aDados)

DEFAULT _oJsAllUsers := JsonObject():New()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se existem dados consistentes para chamada da funcao que envia ³
//³o(s) e-mail(s)                                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lEnvia := nAcho > 0 .And. ValType(aDados) == "A" .And. Len(aDados) > 0

If ValType(lAvalMMail) # "L"
	lAvalMail:=ExistBlock("MAVALMMAIL")
EndIf

If ValType(lAvalFiltr) # "L"
	lAvalFiltr:=Existblock("MFILTRMAIL")
EndIf

// Executa ponto de entrada para filtrar os emails, usuarios e grupos  que devem ser considerados
// bops 00000066533
If	lAvalFiltr
	aRetorMail:=Execblock("MFILTRMAIL",.f.,.f.,{cParUsuario,cParGrUsuario,cParEmails})
	If ValType(aRetorMail) == "A" .And. Len(aRetorMail) == 3
		For i:=1 to Len(aRetorMail)
			If Valtype(aRetorMail[i]) == "C"
				If i == 1
					cParUsuario  :=aRetorMail[i]
				ElseIf i == 2
					cParGrUsuario:=aRetorMail[i]
				ElseIf i == 3
					cParEmails   :=aRetorMail[i]
				EndIf
			EndIf
		Next i
	EndIf
EndIf

// Avalia se manda email
If lAvalMail .And. lEnvia
	lEnvia:=ExecBlock("MAVALMMAIL",.f.,.f.,{cEvento,aDados,cParUsuario,cParGrUsuario,cParEmails,lEvRH})
	If ValType(lEnvia) # "L"
		lEnvia:=.T.
	EndIf
EndIf

If lEnvia
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Obtem usuarios para envio de e-mail(s)                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If !(cEvento $ '038#039') .Or. nMsgGrpApr == 0 .Or. nMsgGrpApr == 2
		cAliasSAN := "MEnviaMail"
		aStruSAN  := SAN->(dbStruct())

		cQuery   := "SELECT * "
		cQuery   += "FROM "+RetSqlName("SAN")+" SAN "
		cQuery   += "WHERE SAN.AN_FILIAL='"+xFilial("SAN")+"' AND "
		cQuery   += "SAN.AN_EVENTO='"+cEvento+"' AND "
			If SAN->(FieldPos("AN_MSBLQL")) > 0
				cQuery   += "SAN.AN_MSBLQL != '1' AND "
			Endif
		cQuery   += "SAN.D_E_L_E_T_=' ' "
		cQuery   += "ORDER BY "+SqlOrder(SAN->(IndexKey()))

		cQuery   := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSAN)

		For nX := 1 To Len(aStruSAN)
			If aStruSAN[nX][2] <> "C" .And. (cAliasSAN)->(FieldPos(aStruSAN[nX][1]))<>0
				TcSetField(cAliasSAN,aStruSAN[nX][1],aStruSAN[nX][2],aStruSAN[nX][3],aStruSAN[nX][4])
			EndIf
		Next nX

		If lAvalFiltr .And. Len(aRetorMail) == 0 //Se o ponto de entrada estiver compilado e retornar vazio, não envia o e-mail
			lNaoEnvia := .T. 
		EndIf  

		While !Eof()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Obtem e-mail do usuario                                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty((cAliasSAN)->AN_USER)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso utilize parametro para filtragem, valida o usuario                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ((cAliasSAN)->AN_BFILTRO == "2" .And. lNaoEnvia)
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif ((cAliasSAN)->AN_BFILTRO == "2" .And. !lNaoEnvia)  .And. (!Empty(cParUsuario) .And. !(Alltrim((cAliasSAN)->AN_USER) $ cParUsuario))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif (cAliasSAN)->AN_BFILTRO == "1" .And. (!Empty(cParUsuario) .And. !(Alltrim((cAliasSAN)->AN_USER) $ cParUsuario))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				EndIf
				//- Validação via cache json, para melhorar a performance
				If (lAllUsers := _oJsAllUsers[(cAliasSAN)->AN_USER]) == nil 
					aFwAllUsers := FWSFAllUsers({(cAliasSAN)->AN_USER},aKeyValues)
					lAllUsers   := Len(aFwAllUsers) > 0 .And. aFwAllUsers[1][6] == "2"
					_oJsAllUsers[(cAliasSAN)->AN_USER] := lAllUsers
					aSize(aFwAllUsers,0)
					aFwAllUsers := nil 
				EndIf 

				If lAllUsers 
					cUsuario := UsrRetMail((cAliasSAN)->AN_USER)
					If !Empty(cUsuario)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Caso utilize parametro para filtragem, valida o e-mail                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If (!Empty(cParEmails) .And. (Alltrim(cUsuario) $ cParEmails)) .Or. Empty(cParEmails)
							nAcho2 := ASCAN(aUsers,{|x| x[1] == (cAliasSAN)->AN_FORMULA})
							If nAcho2 > 0
								aUsers[nAcho2,2]:=aUsers[nAcho2,2]+";"+cUsuario
							Else
								AADD(aUsers,{(cAliasSAN)->AN_FORMULA,cUsuario})
							EndIf
						EndIf
					EndIf
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Obtem e-mail do grupo do usuario  			³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ElseIf !Empty((cAliasSAN)->AN_GRUSER)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso utilize parametro para filtragem, valida o grupo de usuarios       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ((cAliasSAN)->AN_BFILTRO == "2" .And. lNaoEnvia)
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif ((cAliasSAN)->AN_BFILTRO == "2" .And. !lNaoEnvia)  .And. (!Empty(cParGrUsuario) .And. !(Alltrim((cAliasSAN)->AN_GRUSER) $ cParGrUsuario))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif (cAliasSAN)->AN_BFILTRO == "1" .And. (!Empty(cParGrUsuario) .And. !(Alltrim((cAliasSAN)->AN_GRUSER) $ cParGrUsuario))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				EndIf

				aUsGrpAux := FWSFGrpUsers((cAliasSAN)->AN_GRUSER)
				For nX := 1 to Len(aUsGrpAux)

					//- Validação via cache json, para melhorar a performance
					If (lAllUsers := _oJsAllUsers[aUsGrpAux[nX]]) == nil 
						aFwAllUsers := FWSFAllUsers({aUsGrpAux[nX]},aKeyValues)
						lAllUsers   := Len(aFwAllUsers) > 0 .And. aFwAllUsers[1][6] == "2"
						_oJsAllUsers[aUsGrpAux[nX]] := lAllUsers
						aSize(aFwAllUsers,0)
						aFwAllUsers := nil 
					EndIf 

					If lAllUsers 
						cUsuario := UsrRetMail(aUsGrpAux[nX])
						If !Empty(cUsuario)
							//Caso utilize parametro para filtragem, valida o e-mail
							If (!Empty(cParEmails) .And. (Alltrim(cUsuario) $ cParEmails)) .Or. Empty(cParEmails)
								nAcho2 := ASCAN(aUsers,{|x| x[1] == (cAliasSAN)->AN_FORMULA})
								If nAcho2 > 0
									aUsers[nAcho2,2]:=aUsers[nAcho2,2]+";"+cUsuario
								Else
									AADD(aUsers,{(cAliasSAN)->AN_FORMULA,cUsuario})
								EndIf
							EndIf
						EndIf
					EndIf
				Next nX

			ElseIf !Empty((cAliasSAN)->AN_EMAIL)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso utilize parametro para filtragem, valida o e-mail                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ((cAliasSAN)->AN_BFILTRO == "2" .And. lNaoEnvia)
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif ((cAliasSAN)->AN_BFILTRO == "2" .And. !lNaoEnvia)  .And. (!Empty(cParEmails) .And. !(Alltrim((cAliasSAN)->AN_EMAIL) $ cParEmails))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				Elseif (cAliasSAN)->AN_BFILTRO == "1" .And. (!Empty(cParEmails) .And. !(Alltrim((cAliasSAN)->AN_EMAIL) $ cParEmails))
					dbSelectArea(cAliasSAN)
					dbSkip()
					Loop
				EndIf
				
				nAcho2 := ASCAN(aUsers,{|x| x[1] == (cAliasSAN)->AN_FORMULA})
				If nAcho2 > 0
					aUsers[nAcho2,2]:=aUsers[nAcho2,2]+";"+(cAliasSAN)->AN_EMAIL
				Else
					AADD(aUsers,{(cAliasSAN)->AN_FORMULA,(cAliasSAN)->AN_EMAIL})
				EndIf
			EndIf
			dbSelectArea(cAliasSAN)
			dbSkip()
		EndDo

		dbSelectArea(cAliasSAN)
		dbCloseArea()
	EndIf

	If (cEvento $ '038#039') .And. FindFunction('CNMsgGrpApr')
		CNMsgGrpApr(aDados, aUsers)
	EndIf

	//- Ponto de entrada genérico para alteração de lista de usuários que receberão e-mail.
	If ExistBlock("MMENTO"+Substr(cEvento,1,3))
		aUserAlt := ExecBlock("MMENTO"+Substr(cEvento,1,3),.F.,.F.,{aUsers})
		If ValType(aUserAlt) == "A" .And. Len(aUserAlt) >= 1
			aUsers := aUserAlt
		EndIf
	EndIf

	If ExistBlock("MENTO"+Substr(cEvento,1,3)) 
		aUserAlt := ExecBlock("MENTO"+Substr(cEvento,1,3),.F.,.F.,{aUsers})
		If ValType(aUserAlt) == "A" .And. Len(aUserAlt) >= 1
			aUsers := aUserAlt
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existem usuarios para receber esta mensagem                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len(aUsers)
		Do Case
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"001" - "Produto atingiu ponto de pedido."                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "001"
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0065)	//"O produto "###" atingiu a quantidade de "
			cMensagem+=Str(aDados[4])+OemToAnsi(STR0066)+aDados[3]+OemToAnsi(STR0067)+Str(aDados[5])	//" (almoxarifado "###"), abaixo do Ponto de Pedido de "
			If Len(aDados)>5 .and. Valtype(aDados[6]) == "C"
				cMensagem+=aDados[6]
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"002" - "Produto atingiu estoque negativo."                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "002"
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0068)	//"O produto "###" atingiu o saldo negativo de "
			cMensagem+=Str(aDados[4])+" "+RetTitle("B1_FILIAL")+" "+cFilAnt+" "+OemToAnsi(STR0066)+aDados[3]+If(aDados[5],")",OemToAnsi(STR0069))	//" (almoxarifado "###"), mesmo com o parametro de estoque negativo igual a Nao"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"003" - "Solicitacao de Compra com cotacao pendente."                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "003"
			cMensagem:= OemToAnsi(STR0104)+aDados[1]+_CRLF	// "Numero da Cotacao : "
			cMensagem+= OemToAnsi(STR0105)+_CRLF 				//"Solicitacoes de Compras atendidas : "
			If Len(aDados[2]) > 0
				For nY:=1 to Len(aDados[2])
					cMensagem+=_CRLF+aDados[2,nY]
				Next nY
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"004" - "Cotacao com analise pendente."                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "004"
			cMensagem := OemToAnsi(STR0106)+aDados[1]+OemToAnsi(STR0107)+_CRLF //"A Cotacao Num.: "###" possui analise pendente."
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"005" - "Nota Fiscal de Entrada com classificacao fiscal pendente."     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "005"
			cMensagem:=OemToAnsi(STR0074)+aDados[1]+" / "+aDados[2]+OemToAnsi(STR0075)+aDados[3]+" / "+aDados[4]+" - "+aDados[5] //"A nota fiscal / serie "###" do fornecedor / loja "
			cMensagem+=OemToAnsi(STR0076) //" aguarda classifica‡„o fiscal."
			If Len(aDados[6]) > 0
				cMensagem+=_CRLF+OemToAnsi(STR0077) // "Pedido(s) de Compra correspondente(s): "
				For nY:=1 to Len(aDados[6])
					cMensagem+=_CRLF+aDados[6,nY]
				Next nY
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"006" - "Controle de Qualidade aguardando tratamento."                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "006"
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0078)	//"O produto "###" aguarda avaliacao no C.Q. do documento/serie "
			cMensagem+=aDados[3]+"/"+aDados[4]+OemToAnsi(STR0066)+aDados[5]+")" //" (almoxarifado "
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"007" - "Distribuicao de produto aguardando tratamento."                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "007"
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0071)	//"O produto "###" aguarda distribuicao do documento/serie"
			cMensagem+=aDados[3]+"/"+aDados[4]+OemToAnsi(STR0066)+aDados[5]+")" //" (almoxarifado "
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"008" - "Lote de produto Vencido."                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "008"
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0073)	//"O produto "###" teve o(s) seguinte(s) lote(s) bloqueado(s) por vencimento:"
			For nY:=1 to Len(aDados[3])
				cMensagem+=_CRLF
				cMensagem+=aDados[3,nY,1]+If(!Empty(aDados[3,nY,2]),"/"+aDados[3,nY,2],"")
			Next nY
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"009" - "Alocacao de tecnicos."                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "009"
			If aDados[ 1 ] == 1
				cMensagem := OemToAnsi(STR0087) + aDados[2] + OemToAnsi(STR0088) + ;
					aDados[3]+ OemToAnsi(STR0089) + aDados[4]+ OemToAnsi(STR0090)+aDados[5] + OemToAnsi(STR0091) +;
					aDados[6]+ OemToAnsi(STR0092) +aDados[7]+ OemToAnsi(STR0093) +aDados[8] + "." + _CRLF +;
					_CRLF + aDados[9]
				// "O tecnico " ," foi alocado para atender a O.S. numero "," do cliente "," do dia "," e hora "," ate o dia "," e hora "
			Else
				cMensagem := OemToAnsi(STR0094) + aDados[2]+OemToAnsi(STR0095)+ ;
					aDados[3]+OemToAnsi(STR0089)+ aDados[4]+OemToAnsi(STR0090)+aDados[5] + OemToAnsi(STR0091)+;
					aDados[6]+OemToAnsi(STR0092) +aDados[7]+OemToAnsi(STR0093)+aDados[8] + OemToAnsi(STR0096)+;
					_CRLF +	_CRLF + aDados[9]
				// "A alocacao do tecnico ", " para a O.S. numero ", " do cliente ", " do dia ", " e hora "
				// " ate o dia ", " e hora ", ", foi CANCELADA."
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"010" - "Geracao de pedidos de venda - sigatec"                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "010"
			cMensagem := OemToAnsi(STR0097) + aDados[2] + If( aDados[1]==2, OemToAnsi(STR0098), OemToAnsi(STR0099) ) + ;
				aDados[3] + OemToAnsi(STR0100) + If( aDados[1]==3, OemToAnsi(STR0101) , OemToAnsi(STR0102) )+aDados[4] + "."
			// "O pedido de venda numero ", " do fabricante ", " do cliente ", " foi gerado a partir ", " do Contrato de Manutenção " , " da Ordem de Servico ", "."
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³011 ou 012 - Acao para a pontuacao minima e maxima do script dinamico   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "011" .Or. cEvento == "012"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³O cliente XXXXXX participou da campanha YYYYYY                   ³
			//³ que tem a pontuacao minima de ZZ pontos e a pontuacao maxima de ³
			//³WW pontos, e obteve KK pontos                                    ³
			//³Atendimento : 123456                                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem:= OemToAnsi(STR0079) + aDados[1][2] + OemToAnsi(STR0080) + aDados[1][3] +;
				OemToAnsi(STR0081) + aDados[1][5] + OemToAnsi(STR0082) +;
				aDados[1][6] + OemToAnsi(STR0083) + aDados[1][4] + OemToAnsi(STR0084) + _CRLF
			cMensagem+= OemToAnsi(STR0085) + aDados[1][1]
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"013" - "Aviso de Solicitacao de Treinamento"                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "013"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  " Foi gerado necessidade de Treinamento para o(s) Funcionario(s): " ³
			//³  " Funcionario / Treinamento "										 ³
			//³	   999999 - XXXXXXXXXXXXXXXXXX / 9999 - XXXXXXXXXXXXXX "			 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0132)+ _CRLF + _CRLF + OemToAnsi(STR0133) + _CRLF
			For nY := 1 to Len(aDados)
				cMensagem += aDados[nY][1] +" / "+ aDados[nY][2] + _CRLF
			Next nY
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"014" - "Pedido de Venda - Contrato de Servicos"                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "014"

			cMensagem := OemToAnsi(STR0097) + aDados[2] + OemToAnsi(STR0158) + aDados[3] + ;
				OemToAnsi(STR0100) + OemToAnsi(STR0159) + aDados[4]
			cMensagem += _CRLF + OemToAnsi(STR0160) + ":" + DToC( aDados[5] ) + _CRLF + _CRLF
			cMensagem += _CRLF + OemToAnsi(STR0161) + _CRLF + _CRLF
			For nLoop := 1 To Len( aDados[6] )
				cMensagem += aDados[6,nLoop,1] + " / " + Transform( aDados[6,nLoop,2], "@E 999,999,999,999.99" ) + ;
					" / " + Transform( aDados[6,nLoop,3], "@E 999,999,999,999.99" ) + ;
					" / " + Transform( aDados[6,nLoop,4], "@E 999,999,999,999.99" )
			Next nLoop
			// "O pedido de venda numero ", " do cliente ", " foi gerado a partir ", " do Contrato de servico"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Aviso de Ferias                                                      |
			//³"Foram programadas ferias para o funcionario:                         |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "015"
			cMensagem	:= OemToAnsi(STR0145) + _CRLF + _CRLF 							//Aviso de Ferias
			cMensagem 	+= OemToAnsi(STR0146) + _CRLF + _CRLF
			cMensagem   += OemToAnsi(STR0135) + aDados[1][1] + _CRLF							//Filial
			cMensagem   += OemToAnsi(STR0134) + aDados[1][2] + "  " + aDados[1][3] + _CRLF 	//Funcionario
			cMensagem 	+= OemToAnsi(STR0141) + aDados[1][4] + _CRLF   						//Periodo Aq.
			cMensagem  	+= OemToAnsi(STR0142) + aDados[1][5] +  " -> " + aDados[1][6] + _CRLF	//Periodo Gozo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Admissao de Funcionario                                                     |
			//³"A partir dessa data, estara integrando ao nosso quadro de funcionarios      |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "016"
			cMensagem 	:= OemToAnsi(STR0147) + _CRLF + _CRLF				//Admissao de Funcionarios
			cMensagem 	+= OemToAnsi(STR0148) + _CRLF + _CRLF
			cMensagem	+= OemToAnsi(STR0135) + aDados[1][1] + _CRLF						//Filial
			cMensagem   += OemToAnsi(STR0134) + aDados[1][2] + "-" + aDados[1][3] + _CRLF	//Funcionario
			cMensagem   += OemToAnsi(STR0139) + aDados[1][4] + _CRLF						//dt Admissao
			cMensagem 	+= OemToAnsi(STR0137) + aDados[1][6] + _CRLF       				//Centro de custo
			cMensagem   += OemToAnsi(STR0138) + aDados[1][5] + _CRLF 						//Cargo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Desligamento de funcionario                                                       |
			//³"Foi efetuada Rescisao Contratual do seguinte funcionario :                        |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento =="017"
			cMensagem 	:= OemToAnsi(STR0149) + _CRLF + _CRLF
			cMensagem 	+= OemToansi(STR0150) + _CRLF
			cMensagem	+= OemToAnsi(STR0135) + aDados[1][1] + _CRLF						//Filial
			cMensagem   += OemToAnsi(STR0134) + aDados[1][2] + "-" + aDados[1][3] + _CRLF	//Funcionario
			cMensagem   += OemToAnsi(STR0140) + aDados[1][4] + _CRLF						//Dt Admissao
			cMensagem 	+= OemToAnsi(STR0137) + aDados[1][6] + _CRLF       				//Centro de custo
			cMensagem   += OemToAnsi(STR0138) + aDados[1][5] + _CRLF //Cargo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Aviso de Afastamentos                                                |
			//³"O funcionario abaixo estara em periodo de afastamento                |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento =="018"
			cMensagem 	:= OemToAnsi(STR0151) + _CRLF + _CRLF
			cMensagem 	+= OemToansi(STR0152) + _CRLF
			cMensagem	+= OemToAnsi(STR0135) + aDados[1][1] + _CRLF						//Filial
			cMensagem   += OemToAnsi(STR0134) + aDados[1][2] + "-" + aDados[1][3] + _CRLF	//Funcionario
			cMensagem   += OemToAnsi(STR0143) + aDados[1][4] +_CRLF	//Data Afastam
			cMensagem   += aDados[1][5] +_CRLF	//Motivo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Aviso de Transferencia de Funcionarios                                           |
			//³"Houve alteracoes no    dado    do funcionario :                                  |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "019"
			cMensagem 	:= OemToAnsi(STR0153) + _CRLF + _CRLF
			cMensagem 	+= OemToansi(STR0154) + _CRLF
			cMensagem   += OemToAnsi(STR0134) +  aDados[1][5] + "-" + aDados[1][1] + " - " + aDados[1][2] + _CRLF + _CRLF //Funcionario
			cMensagem   +=  repl("-",100) + _CRLF
			cMensagem   += OemToAnsi(STR0156) + _CRLF + _CRLF					//Transf. De:
			cMensagem	+= OemToAnsi(STR0136) + aDados[1][3] + _CRLF	//Empresa
			cMensagem	+= OemToAnsi(STR0135) + aDados[1][4] + _CRLF	//Filial
			cMensagem   += OemToAnsi(STR0137) + aDados[1][6] + "-" + aDados[1][7] + _CRLF + _CRLF    //Centro de Custo
			cMensagem   += OemToAnsi(STR0157) + _CRLF                   //Transf. Para
			cMensagem   += OemToAnsi(STR0136) + aDados[1][8] + _CRLF	//Empresa
			cMensagem   += OemToAnsi(STR0135) + aDados[1][9] + _CRLF	//Filial
			cMensagem 	+= OemToAnsi(STR0137) + aDados[1][11]+ "-" + aDados[1][12] + _CRLF + _CRLF  //Centro Custo
			cMensagem   += OemToAnsi(STR0144) + aDados[1][13]+ _CRLF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³  "Funcionario foi aprovado no processo de Recrutamento Interno, "    ³
			//³  "favor realizar suas alteracoes cadastrais: "						 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "020"
			cMensagem := OemToAnsi(STR0169)+ _CRLF + OemToAnsi(STR0170)+ _CRLF + _CRLF

			For nY := 1 To Len(aDados[1])
				cMensagem += aDados[1][nY] + _CRLF
			Next nY
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"021" - "Log de Processamento - Refaz Acumulados"                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "021"
			cMensagem:= STR0171+aDados[1]  //"A rotina de Refaz Acumulados acaba de ser processada pelo usuario "
			cMensagem+= STR0172+aDados[2]+" / "+aDados[3]+"."+ _CRLF //" na empresa / filial "
			cMensagem+= STR0173+aDados[4]+STR0174+ _CRLF  //"Seu processamento durou "###" minuto(s)."
			cMensagem+= STR0178+Time()+ _CRLF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"022" - "Log de Processamento - Saldo Atual"                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "022"
			cMensagem:= STR0175+aDados[1]  //"A rotina de Saldo Atual acaba de ser processada pelo usuario "
			cMensagem+= STR0172+aDados[2]+" / "+aDados[3]+"."+ _CRLF //" na empresa / filial "
			cMensagem+= STR0173+aDados[4]+STR0174+ _CRLF  //"Seu processamento durou "###" minuto(s)."
			cMensagem+= STR0178+Time()+ _CRLF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"023" - "Log de Processamento - Recalculo do Custo Medio"               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "023"
			cMensagem:= STR0176+IIf(!IsBlind(), STR0184+Trim(aDados[1]), '') //"A rotina de Recalculo do Custo Medio acaba de ser processada "
			cMensagem+= STR0172+aDados[2]+" / "+aDados[3]+"."+ _CRLF         //" na empresa / filial "
			cMensagem+= STR0173+aDados[4]+STR0174+ _CRLF                     //"Seu processamento durou "###" minuto(s)."
			cMensagem+= STR0178+Time()+ _CRLF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"024" - "Log de Processamento - Carga Maquina   "                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "024"
			cMensagem:= STR0177+aDados[1]  //"A rotina de Carga Maquina acaba de ser processada pelo usuario "
			cMensagem+= STR0172+aDados[2]+" / "+aDados[3]+"."+ _CRLF //" na empresa / filial "
			cMensagem+= STR0173+aDados[4]+STR0174+ _CRLF  //"Seu processamento durou "###" minuto(s)."
			cMensagem+= STR0178+Time()+ _CRLF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"025" - "Aviso de Geracao de Planejamento de Projetos"                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Case cEvento == "025"
			//cMensagem:= STR0182+aDados[1]+_CRLF //"O Projeto : "
			//cMensagem+= STR0183+aDados[3]+STR0184+aDados[2]+"."+_CRLF  //"foi planejado com o codigo : "###" pelo usuario "
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Evento criado pelo usuario                                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			cMensagem:="<head>"
		    cMensagem+="<title>" + STR0188 + "</title>"
			cMensagem+="</head>"
			cMensagem+="<body>"
			cMensagem+='<center><table border="0" width="85%">'

			// titulo
			cMensagem+="<tr><td><center><h3>"+STR0188+"</h3></center></td></tr>"

			//cMensagem+= STR0175+aDados[1]+_CRLF //"O Projeto : "
			//cMensagem+= STR0176+aDados[3]+STR0177+aDados[2]+"."+_CRLF  //"foi planejado com o codigo : "###" pelo usuario "

			// cabecalho
			cMensagem+= "<tr><td>"
			cMensagem+= '<table border="0" width="100%">'

			// cabecalho - Planejamento
			cMensagem+= "<tr>"
			cMensagem+= '<td width="25%">'
			cMensagem+= "<b>" + STR0189 + "</b>"
			cMensagem+= "</td>"
			cMensagem+= "<td>"
			cMensagem+= HTMLEnc(aDados[3])
			cMensagem+= "</td>"
			cMensagem+= "</tr>"

			// cabecalho - Projeto
			cMensagem+= "<tr>"
			cMensagem+= '<td width="25%">'
			cMensagem+= "<b>" + STR0190 + "</b>"
			cMensagem+= "</td>"
			cMensagem+= "<td>"
			cMensagem+= HTMLEnc(aDados[1])
			cMensagem+= "</td>"
			cMensagem+= "</tr>"

			// cabecalho - Usuario
			cMensagem+= "<tr>"
			cMensagem+= '<td width="25%">'
			cMensagem+= "<b>" + STR0191 + "</b>"
			cMensagem+= "</td>"
			cMensagem+= "<td>"
			cMensagem+= HTMLEnc(aDados[2])
			cMensagem+= "</td>"
			cMensagem+= "</tr>"

			cMensagem+= "</table>"
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

			// regua horizontal
			cMensagem+= "<tr><td>"
			cMensagem+= '<hr width="100%">'
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

		    //////////////////////////////
			// ordens de producao
	        //////////////////////////////
			cMensagem+= "<tr><td>"
			cMensagem+= "<h4>" + STR0192 + "</h4>"
			cMensagem+= "</td></tr>"

			cMensagem+= "<tr><td>"

			If Len(aDados) < 3
				cMensagem+= STR0193
			Else
				If Len(aDados[4]) > 0
					cMensagem+= MontaTabelaHTML(aDados[4], .T., "100%")
				Else
					cMensagem+= STR0193
				EndIf

			EndIf

			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

			// regua horizontal
			cMensagem+= "<tr><td>"
			cMensagem+= '<hr width="100%">'
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

            //////////////////////////////
			// solicitacao de compras
            //////////////////////////////
			cMensagem+= "<tr><td>"
			cMensagem+= "<h4>" + STR0194 + "</h4>"
			cMensagem+= "</td></tr>"

			cMensagem+= "<tr><td>"

			If Len(aDados) < 3
				cMensagem+= STR0193
			Else
				If Len(aDados[5]) > 0
					cMensagem+= MontaTabelaHTML(aDados[5], .T., "100%")
				Else
					cMensagem+= STR0193
				EndIf

			EndIf

			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

			// regua horizontal
			cMensagem+= "<tr><td>"
			cMensagem+= '<hr width="100%">'
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

            //////////////////////////////
			// empenhos
            //////////////////////////////
			cMensagem+= "<tr><td>"
			cMensagem+= "<h4>" + STR0195 + "</h4>"
			cMensagem+= "</td></tr>"

			cMensagem+= "<tr><td>"

			If Len(aDados) < 3
				cMensagem+= STR0193
			Else
				If Len(aDados[6]) > 0
					cMensagem+= MontaTabelaHTML(aDados[6], .T., "100%")
				Else
					cMensagem+= STR0193
				EndIf
			EndIf
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

			// regua horizontal
			cMensagem+= "<tr><td>"
			cMensagem+= '<hr width="100%">'
			cMensagem+= "</td></tr>"

			// espaco
			cMensagem+= "<tr><td>&nbsp;</td></tr>"

			//////////////////////////////
			// contratos de parceria
			//////////////////////////////
			cMensagem+= "<tr><td>"
			cMensagem+= "<h4>" + STR0196 + "</h4>"
			cMensagem+= "</td></tr>"

			cMensagem+= "<tr><td>"

			If Len(aDados) < 3
				cMensagem+= STR0193
			Else
				If Len(aDados[7]) > 0
					cMensagem+= MontaTabelaHTML(aDados[7], .T., "100%")
				Else
					cMensagem+= STR0193
				EndIf
			EndIf
			cMensagem+= "</td></tr>"

			// fim
			cMensagem+="</table></center>"
			cMensagem+="</body>"
			cMensagem+="</html>"
		Case cEvento == "026"
			//	"026" - "Aprovacao de orcamento"
			cMensagem:= OemToAnsi(STR0197)+_CRLF+_CRLF    // "Aprovacao de Venda"
			cMensagem+= OemToAnsi(STR0198)+_CRLF+_CRLF    //"Orcamento a ser efetivado : "
			cMensagem+= OemToAnsi(STR0199)+aDados[1]+_CRLF  //"Numero : "
			cMensagem+= OemToAnsi(STR0200)+aDados[2]+" - "+aDados[3]+" - "+aDados[4]+_CRLF  //"Cliente  : "

		Case cEvento = '027'
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"027" - "Liberacao/Rejeicao de Movimentacao de Veiculo (SigaWMS)."      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Conteudo do Array aDados:                   ³
			//³ aDados[01]= Usuario                         ³
			//³ aDados[02]= Empresa                         ³
			//³ aDados[03]= Filial                          ³
			//³ aDados[04]= Sequencia                       ³
			//³ aDados[05]= Placa                           ³
			//³ aDados[06]= Ident. Veiculo                  ³
			//³ aDados[07]= Ident. Motorista                ³
			//³ aDados[08]= Documento Motorista             ³
			//³ aDados[09]= Tara do Veiculo                 ³
			//³ aDados[10]= Tipo de Carga                   ³
			//³ aDados[11]= Estrutura Fisica                ³
			//³ aDados[12]= Local Externo                   ³
			//³ aDados[13]= Peso                            ³
			//³ aDados[14]= Hora de Entrada                 ³
			//³ aDados[15]= Data de Entrada                 ³
			//³ aDados[16]= Hora de Saida                   ³
			//³ aDados[17]= Data de Saida                   ³
			//³ aDados[18]= Status (2=Liberado/3=Rejeitado) ³
			//³ aDados[19]= Motivo da Rejeicao              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
         If aDados[18] == '2'
				cMensagem := STR0202 + _CRLF + _CRLF			 //'Liberacao de Movimentacao de Veiculo'
				cMensagem += STR0203+aDados[04]+STR0204+aDados[01]+STR0205+aDados[2]+STR0206+aDados[3]+').' + _CRLF //'A Sequencia de Movimentacao '###' foi LIBERADA pelo Usuario "'###'" (Empresa '###', Filial '
				cMensagem += STR0207 + _CRLF //'A seguir os dados referentes a Liberacao:'
				cMensagem += '	  >> '+STR0208+': '+aDados[07] + _CRLF //'Motorista'
				cMensagem += '	  >> '+STR0209+': '+aDados[08] + _CRLF //'Documento do Motorista'
				cMensagem += '	  >> '+STR0210+': '+aDados[06] + _CRLF //'Veiculo'
				cMensagem += '	  >> '+STR0211+': '+aDados[05] + _CRLF //'Placa do Veiculo'
				cMensagem += '	  >> '+STR0212+': '+aDados[09] + _CRLF //'Tara do Veiculo'
				cMensagem += '	  >> '+STR0213+': '+aDados[10] + _CRLF //'Tipo de Carga'
				cMensagem += '	  >> '+STR0214+': '+aDados[13] + _CRLF //'Peso da Carga'
				cMensagem += '	  >> '+STR0215+': '+aDados[11] + _CRLF //'Estrutura Fisica'
				cMensagem += '	  >> '+STR0216+': '+aDados[12] + _CRLF //'Local Externo'
				cMensagem += '	  >> '+STR0217+': '+aDados[15] + ', ' + aDados[14] + _CRLF //'Data e Hora de Entrada'
			ElseIf aDados[18]	 == '3'
				cMensagem := STR0218 + _CRLF + _CRLF			 //'Rejeicao de Movimentacao de Veiculo'
				cMensagem += STR0203+aDados[04]+STR0219+aDados[01]+STR0205+aDados[2]+STR0206+aDados[3]+').' + _CRLF //'A Sequencia de Movimentacao '###' foi REJEITADA pelo Usuario "'###'" (Empresa '###', Filial '
				cMensagem += STR0220 + _CRLF //'A seguir os dados referentes a Rejeicao:'
				cMensagem += '	  >> '+STR0208+': '+aDados[07] + _CRLF //'Motorista'
				cMensagem += '	  >> '+STR0209+': '+aDados[08] + _CRLF //'Documento do Motorista'
				cMensagem += '	  >> '+STR0210+': '+aDados[06] + _CRLF //'Veiculo'
				cMensagem += '	  >> '+STR0211+': '+aDados[05] + _CRLF //'Placa do Veiculo'
				cMensagem += '	  >> '+STR0212+': '+aDados[09] + _CRLF //'Tara do Veiculo'
				cMensagem += '	  >> '+STR0213+': '+aDados[10] + _CRLF //'Tipo de Carga'
				cMensagem += '	  >> '+STR0214+': '+aDados[13] + _CRLF //'Peso da Carga'
				cMensagem += '	  >> '+STR0215+': '+aDados[11] + _CRLF //'Estrutura Fisica'
				cMensagem += '	  >> '+STR0216+': '+aDados[12] + _CRLF //'Local Externo'
				cMensagem += '	  >> '+STR0217+': '+aDados[15] + ', ' + aDados[14] + _CRLF //'Data e Hora de Entrada'
				cMensagem += STR0221 + _CRLF //' A seguir o Motivo da Rejeicao:'
				cMensagem += aDados[19] + _CRLF
			EndIf
			cMensagem += STR0222+DtoC(dDataBase)+STR0223+SubStr(Time(),1,5)+'.' //'Enviado em '###', as '
		Case cEvento = '028'
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"028" - "Finalizacao de Movimentacao de Veiculo (SigaWMS)."             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Conteudo do Array aDados:                   ³
			//³ aDados[01]= Usuario                         ³
			//³ aDados[02]= Empresa                         ³
			//³ aDados[03]= Filial                          ³
			//³ aDados[04]= Sequencia                       ³
			//³ aDados[05]= Placa                           ³
			//³ aDados[06]= Ident. Veiculo                  ³
			//³ aDados[07]= Ident. Motorista                ³
			//³ aDados[08]= Documento Motorista             ³
			//³ aDados[09]= Tara do Veiculo                 ³
			//³ aDados[10]= Tipo de Carga                   ³
			//³ aDados[11]= Estrutura Fisica                ³
			//³ aDados[12]= Local Externo                   ³
			//³ aDados[13]= Peso                            ³
			//³ aDados[14]= Hora de Entrada                 ³
			//³ aDados[15]= Data de Entrada                 ³
			//³ aDados[16]= Hora de Saida                   ³
			//³ aDados[17]= Data de Saida                   ³
			//³ aDados[18]= Status (2=Liberado/3=Rejeitado) ³
			//³ aDados[19]= Motivo da Rejeicao              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := STR0224 + _CRLF + _CRLF //'Conclusao da Movimentacao de Veiculo'
			cMensagem += STR0203+aDados[04]+STR0225+aDados[01]+STR0205+aDados[2]+STR0206+aDados[3]+').' + _CRLF //'A Sequencia de Movimentacao '###' foi CONCLUIDA pelo Usuario "'###'" (Empresa '###', Filial '
			cMensagem += STR0226 + _CRLF //'A seguir os dados referentes a Conclusao:'
			cMensagem += '	  >> '+STR0208+': '+aDados[07] + _CRLF //'Motorista'
			cMensagem += '	  >> '+STR0209+': '+aDados[08] + _CRLF //'Documento do Motorista'
			cMensagem += '	  >> '+STR0210+': '+aDados[06] + _CRLF //'Veiculo'
			cMensagem += '	  >> '+STR0211+': '+aDados[05] + _CRLF //'Placa do Veiculo'
			cMensagem += '	  >> '+STR0212+': '+aDados[09] + _CRLF //'Tara do Veiculo'
			cMensagem += '	  >> '+STR0213+': '+aDados[10] + _CRLF //'Tipo de Carga'
			cMensagem += '	  >> '+STR0214+': '+aDados[13] + _CRLF //'Peso da Carga'
			cMensagem += '	  >> '+STR0215+': '+aDados[11] + _CRLF //'Estrutura Fisica'
			cMensagem += '	  >> '+STR0216+': '+aDados[12] + _CRLF //'Local Externo'
			cMensagem += '	  >> '+STR0217+': '+aDados[15] + ', ' + aDados[14] + _CRLF //'Data e Hora de Entrada'
			cMensagem += '	  >> '+STR0227+': '+aDados[17] + ', ' + aDados[16] + _CRLF //'Data e Hora de Saida'
			cMensagem += STR0222+DtoC(dDataBase)+STR0223+SubStr(Time(),1,5)+'.' //'Enviado em '###', as '
		Case cEvento == "029"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"029" - "Produto atingiu/ultrapassou estoque maximo (B1_EMAX)           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0065)	//"O produto "###" atingiu a quantidade de "
			cMensagem+=Str(aDados[4])+OemToAnsi(STR0066)+aDados[3]+OemToAnsi(STR0228)+Str(aDados[5])	//" (almoxarifado "###") Acima do Estoque Maximo do Produto"
		Case cEvento == "030"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"030" - "Entrada de Documento Fiscal                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem:=OemToAnsi(STR0074)+aDados[1]+" / "+aDados[2]+OemToAnsi(STR0075)+aDados[3]+" / "+aDados[4]+" - "+aDados[5]+OemToAnsi(STR0294)+aDados[8] //"A nota fiscal / serie "###" do fornecedor / loja  - Natureza /"
			cMensagem+=OemToAnsi(STR0237) //" foi "
			// Verifica qual a opcao
			If aDados[6] == 3 // Inclusao
				cMenSagem+=OemToAnsi(STR0238)
			ElseIf aDados[6] == 4 // Classificacao
				cMenSagem+=OemToAnsi(STR0239)
			ElseIf aDados[6] == 5 // Exclusao
				cMenSagem+=OemToAnsi(STR0240)
			EndIf
			// Salta linha
			cMensagem+=_CRLF;cMensagem+=_CRLF
			// Inclui itens das NFs
			If Len(aDados[7]) > 0
				cMensagem+=_CRLF+OemToAnsi(STR0241) //"Item  Produto          Quantidade      Valor Total"
				cMensagem+=_CRLF                   // 1234xx123456789012345xx12345678901234xx12345678901234
				For nY:=1 to Len(aDados[7])
					cMensagem+=_CRLF+Substr(aDados[7,NY,1],1,4) // Item
					cMensagem+=Space(2)
					cMensagem+=Substr(aDados[7,NY,2],1,15) // Produto
					cMensagem+=Space(2)
					cMensagem+=Str(aDados[7,NY,3],14,2) // Quantidade
					cMensagem+=Space(2)
					cMensagem+=Str(aDados[7,NY,4],14,2) // Valor total
				Next nY
			EndIf
		Case cEvento == "031"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"031" - "Exclusao de pedidos de venda - mata410"                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0097) + aDados[1] + OemToAnsi(STR0099) + aDados[2]+"/"+aDados[3] + OemToAnsi(STR0237) + OemToAnsi(STR0240)
			// "O pedido de venda numero ", " do cliente/loja ", " foi excluido ", "."
		Case cEvento == "032"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"032" - "Inclusao de Cliente - mata030"                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		    cMensagem+= OemToAnsi(STR0289)+aDados[4]+_CRLF //"Evento 032 - O Cliente abaixo acaba de ser incluido no sistema pelo usuario: "
		    cMensagem+= _CRLF
			cMensagem+= ' 	  >> '+OemToAnsi(STR0242)+ aDados[1] + "/" + aDados[2] + _CRLF // Cod.Client. #### Loja
			cMensagem+= '	  >> '+OemToAnsi(STR0270)+ aDados[3]+_CRLF  // Razao Social
		Case cEvento = '033'
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"033" - "Inclusao de Produtos - MATA010" 		                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		    cMensagem+= OemToAnsi(STR0244)+aDados[7]+_CRLF //"Evento 033 - O Produto abaixo acaba de ser incluido no sistema pelo usuario"
		    cMensagem+= _CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0245)+ aDados[1]+_CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0246)+ aDados[2]+ " - " + aDados[3]+_CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0247)+ aDados[4]+_CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0248)+ aDados[5]+_CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0249)+ aDados[6]+_CRLF
		Case cEvento == "034"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"034" - "Liberacao de pedido para compra - Mata097"                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If aDados[2] $ 'PC|IP'
				cMensagem := OemToAnsi(STR0250)+ aDados[1] + OemToAnsi(STR0251)
				//"O pedido numero "###" foi liberado para compra."
			ElseIf aDados[2] == "AE"
				cMensagem := OemToAnsi(STR0252)+ aDados[1] + OemToAnsi(STR0253)
				 //"A autorizacao de entrega numero "###" foi liberada."
			EndIf
		Case cEvento == "035"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"035" - "Inclusao de Solicitacao de Compras - Mata110"                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0254)+ aDados[1] + OemToAnsi(STR0255)
			//"A Solicitacao de Compras numero "### acaba de ser incluida.
		Case cEvento = '036'
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"036" - "Inclusao de Fornecedor - Mata020" 		                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		    cMensagem+= OemToAnsi(STR0267)+aDados[6]+_CRLF //"Evento 036 - O Fornecedor abaixo acaba de ser incluido no sistema pelo usuario: "
		    cMensagem+= _CRLF
			cMensagem+= '	  >> '+OemToAnsi(STR0268)+ aDados[1]+_CRLF  //Filial
			cMensagem+= ' 	  >> '+OemToAnsi(STR0269)+ aDados[2]+ OemToAnsi(STR0272) + aDados[3]+_CRLF // Cod.Fornec. #### Loja
			cMensagem+= '	  >> '+OemToAnsi(STR0270)+ aDados[4]+_CRLF  // Razao Social
			cMensagem+= '	  >> '+OemToAnsi(STR0271)+ aDados[5]+_CRLF  // Nome Reduzido
		Case cEvento == "037"

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"037" - "Inclusao do Pedido de Compras - Mata120"                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0273)+ aDados[1] + OemToAnsi(STR0274) + aDados[5] +_CRLF
			//"O Pedido de Compras numero "### acaba de ser incluido no sistema pelo usuario
            //"Este pedido foi originado do processo de cotação numero "
			If !Empty(aDados[2])
				cMensagem+= OemToAnsi(STR0275)+aDados[2] +_CRLF
            EndIf
            If !Empty(aDados[3]) .And. aDados[4] == "B"
	            cMensagem+= OemToAnsi(STR0276)+aDados[3] +_CRLF //" Aguardando liberacao do grupo de aprovacao numero "
	        Endif
      Case cEvento == "038"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"038" - Vencimento do Contrato - (SIGAGCT)                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
     		cMensagem := OemToAnsi(STR0277) + _CRLF//"Aviso de vencimento do contrato:"
     		cMensagem += OemToAnsi(STR0278) + aDados[1]+"\"+aDados[2] + _CRLF//"Contrato\Revisao: "
			cMensagem += OemToAnsi(STR0245) + aDados[5]+ _CRLF//"Filial : "
     		cMensagem += OemToAnsi(STR0279) + DTOC(aDados[3]) + _CRLF//"Data de inicio: "
     		cMensagem += OemToAnsi(STR0280) + DTOC(aDados[4]) + _CRLF//"Data de Termino: "
		Case cEvento == "039"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"039" - Termino do Contrato - (SIGAGCT)                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
     		cMensagem := OemToAnsi(STR0281) + _CRLF//"Aviso de termino do contrato:"
     		cMensagem += OemToAnsi(STR0278) + aDados[1]+"\"+aDados[2] + _CRLF//"Contrato\Revisao: "
			cMensagem += OemToAnsi(STR0245) + aDados[5]+ _CRLF//"Filial : "
     		cMensagem += OemToAnsi(STR0279) + DTOC(aDados[3]) + _CRLF//"Data de inicio: "
     		cMensagem += OemToAnsi(STR0280) + DTOC(aDados[4]) + _CRLF//"Data de Termino: "
		Case cEvento == "040"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"040" - Limite de saldo das planilhas de contrato - (SIGAGCT)           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0282) + _CRLF//"O saldo da planilha de contrato abaixo, atingiu o percentual minimo estabelecido. Informações: "
			cMensagem += OemToAnsi(STR0278) + aDados[2] + "/" + aDados[3] + _CRLF//"Contrato\Revisao: "
			cMensagem += OemToAnsi(STR0245) + aDados[6]+ _CRLF//"Filial : "
			cMensagem += OemToAnsi(STR0283) + aDados[1] + _CRLF//"Planilha: "
			cMensagem += OemToAnsi(STR0284) + Transform(aDados[4],PesqPict("CNA","CNA_VLTOT")) + _CRLF//"Valor Total: "
			cMensagem += OemToAnsi(STR0285) + Transform(aDados[5],PesqPict("CNA","CNA_SALDO")) + _CRLF//"Saldo: "
		Case cEvento == "041"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"041" - Log de execucao das medicoes automaticas - (SIGAGCT)            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := aDados[1] + _CRLF

		Case cEvento == "042"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"042" - "Inclusao de Solicitacao ao Almoxarifado - (SIGAEST)            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0286)+ aDados[1] + OemToAnsi(STR0255)
			//"A Solicitacao ao Almoxarifado numero "### acaba de ser incluida.

		Case cEvento == "043"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"043" - "Exclusao do Pedido de Compras - Mata120"                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem := OemToAnsi(STR0273)+ aDados[1] + OemToAnsi(STR0287) +_CRLF
			//"O Pedido de Compras numero "### acaba de ser excluido.

		Case cEvento == "044"
			cMensagem:= STR0288+aDados[1]  //"A rotina de Refaz Acumulados (SIGAFIN) acaba de ser processada pelo usuario "
			cMensagem+= STR0172+aDados[2]+" / "+aDados[3]+"."+ _CRLF //" na empresa / filial "
			cMensagem+= STR0173+aDados[4]+STR0174+ _CRLF  //"Seu processamento durou "###" minuto(s)."
			cMensagem+= STR0178+Time()+ _CRLF

		Case cEvento == "045"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"045" - "Produto atingiu/ultrapassou lote minimo/qtde mebalagem (B1_LM/B1_QE)    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SG1->(DbSetOrder(1))
			If SG1->(DbSeek(xFilial('SG1')+SB1->B1_COD))
				cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0065)	//"O produto "###" atingiu a quantidade de "
				cMensagem+=Str(aDados[4])+OemToAnsi(STR0066)+aDados[3]+OemToAnsi(STR0290)+Str(aDados[5])	//" (almoxarifado "###") Abaixo do Lote Minimo do Produto"
			Else
				cMensagem:=OemToAnsi(STR0064)+aDados[1]+" - "+aDados[2]+OemToAnsi(STR0065)	//"O produto "###" atingiu a quantidade de "
				cMensagem+=Str(aDados[4])+OemToAnsi(STR0066)+aDados[3]+OemToAnsi(STR0291)+Str(aDados[5])	//" (almoxarifado "###") Abaixo da Quantidade por Embalagem do Produto"
			Endif

		Case cEvento == "046"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"046" - "A rotina de Recalculo do Custo Medio foi iniciada em DD/MM/YY hh:mm:ss pelo usuario XXX na empresa / filial YY/ZZ. ³
			//³        "Horario do Envio :hh:mm:ss                                                                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem:= STR0293+aDados[4]+' '+aDados[5]              //"A rotina de Recalculo do Custo Medio foi iniciada em "
			cMensagem+= IIf(!IsBlind(), STR0184+Trim(aDados[1]), '') //" pelo usuario " ou ""
			cMensagem+= STR0172+aDados[2]+' / '+aDados[3]+'.'+ _CRLF //" na empresa / filial "
			cMensagem+= STR0178+Time()+ _CRLF                        //"Horario do Envio :"

		Case cEvento == "048"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"048" - "Documento XXX nao apresentado, nao impediu a (liberacao / movimentacao) do titulo					   			   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem:= STR0295+aDados[2]+' - '+aDados[3]		//"Documento "XXX - XXX
			cMensagem+= ' - '+STR0296		//"não apresentado, não impediu a "
			cMensagem+= IIF(aDados[1]$'01|',STR0297,STR0298)		//"liberacao "/"movimentacao "
			cMensagem+= STR0299+_CRLF		//"do titulo abaixo."
			cMensagem+= _CRLF+STR0300+aDados[4]+_CRLF		//"Filial: "
        	cMensagem+=	STR0301+aDados[5]+_CRLF		//"Prefixo: "
        	cMensagem+=	STR0302+aDados[6]+_CRLF		//"Numero: "
        	cMensagem+=	STR0303+aDados[7]+_CRLF		//"Parcela: "
   			cMensagem+=	STR0304+aDados[8]+_CRLF		//"Tipo: "
   			cMensagem+=	STR0305+aDados[9]+'/'+aDados[10]+_CRLF		//"Fornecedor: "

   		Case cEvento == "052"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"052" - Aviso de inconsistencias na importacao NF-e [TOTVS Colaboracao]									   			   	   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem += '<html><body><font face="Arial"><big style="font-weight: bold; color: gray;">'
			cMensagem += STR0308 //Aviso de inconsistencias da importacao NF-e [TOTVS Colaboracao]
			cMensagem += '</big><br><small>' + FWEmpName(cCodEmp) + '-[M-Messenger]<br>'
			cMensagem += FWFilialName() + '<br><br>'
			cMensagem += STR0309 +DToC(Date()) +'<br>' +STR0310 +Time() +'<br>'
			cMensagem += '<hr style="width: 100%; height: 2px; font-family: Arial;"><br>'
			cMensagem += STR0311 //Um ou mais arquivos de NF-e recebidos via TOTVS Colaboracao apresentaram inconsistancias durante o processamento.
			cMensagem += STR0312 //Tais arquivos foram movidos automaticamente para o diretório XMLNFE\ERR na raiz do servidor Microsiga.
			cMensagem += '<br><br>'
			cMensagem += Iif(IsInCallStack("MATA140I"),STR0324,STR0313) //Corrija as ocorrencias listadas abaixo e providencie o reprocessamento destes arquivos no monitor TOTVS Colaboração.
			cMensagem += '<br><br></small>'
			cMensagem += '<table style="text-align: left; width: 100%;" border="0" cellpadding="2" cellspacing="1"><tbody>'
			cMensagem += '<tr style="font-family: Arial;"><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0314 //Arquivo
			cMensagem += '</small></th><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0315 //Ocorrencia
			cMensagem += '</small></th><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0316 //Solucao
			cMensagem += '</small></th></tr>

			For nY := 1 To Len(aDados)
				cMensagem += '<tr><td valign="center">	<small>'
				cMensagem += aDados[nY,1] //Nome do arquivo
				cMensagem += '</small></td><td valign="center"><small>'
				cMensagem += aDados[nY,2] //Descricao da ocorrencia
				cMensagem += '</small></td><td valign="center"><small>'
				cMensagem += aDados[nY,3] //Solucao
				cMensagem += '</small></td></tr>
			Next nY

			cMensagem += '</tbody></table><br><font size=1>'
			cMensagem += STR0317 // * Estes arquivos não serão reprocessados automaticamente.
			cMensagem += '</font></font></body></html>'

		Case cEvento == "053"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³"053" - NF-e disponiveis [TOTVS Colaboracao]									   			   	   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cMensagem += '<html><body><font face="Arial"><big style="font-weight: bold; color: gray;">'
			cMensagem += STR0318 //NF-e disponíveis [TOTVS Colaboração]
			cMensagem += '</big><br><small>' + FWEmpName(cCodEmp) + '-[M-Messenger]<br>'
			cMensagem += FWFilialName() + '<br><br>'
			cMensagem += STR0309 +DToC(Date()) +'<br>' +STR0310 +Time() +'<br>'
			cMensagem += '<hr style="width: 100%; height: 2px; font-family: Arial;"><br>'
			cMensagem += Iif(IsInCallStack("MATA140I"),STR0319,STR0325) //Um ou mais arquivos de NF-e foram recebidos via TOTVS Colaboração e estão disponíveis para geração de documento fiscal no monitor TOTVS Colaboração.
			cMensagem += '<br><br></small>'
			cMensagem += '<table style="text-align: left; width: 100%;" border="0" cellpadding="2" cellspacing="1"><tbody>'
			cMensagem += '<tr style="font-family: Arial;"><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0320 //Documento
			cMensagem += '</small></th><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0321 //Serie
			cMensagem += '</small></th><th valign="center" style="background-color: gray; font-weight: bold; color: white;"><small>'
			cMensagem += STR0322 //Fornecedor
			cMensagem += '</small></th></tr>'

			For nY := 1 To Len(aDados)
				cMensagem += '<tr><td valign="center"><small>'
				cMensagem += aDados[nY,1] //numero da nota
				cMensagem += '</small></td><td valign="center"><small>'
				cMensagem += aDados[nY,2] //serie da nota
				cMensagem += '</small></td><td valign="center"><small>'
				cMensagem += aDados[nY,3] //nome do fornecedor
				cMensagem += '</small></td></tr>'
			Next nY

			cMensagem += '</tbody></table><br><font size=1>'
			cMensagem += STR0323 //* Estes documentos não serão gerados automaticamente.
			cMensagem += '</font></font></body></html>'

		EndCase

		If lBlockUser
			cBlockUser:= "MMENS"+Substr(cEvento,1,3)
			cMensagem:=ExecBlock(cBlockUser,.F.,.F.,{aDados,cMensagem})
			If Valtype(cMensagem) # "C"
				cMensagem:=""
			EndIf
		EndIf

		cMensagem += _CRLF+OemToAnsi(STR0072)+cModulo	// "E-Mail automatico enviado pelo modulo SIGA"
		cBackMens := cMensagem
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa Formula com conteudo do texto                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aUsers[nX,1])
			cMensagem:=Formula(aUsers[nX,1])
			If Valtype(cMensagem) != "C"
				cMensagem:=cBackMens
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Obtem dados necessarios a conexao                                                |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cMailConta  := If(cMailConta == NIL,GETMV("MV_EMCONTA"),cMailConta)
		cMailServer := If(cMailServer== NIL,GETMV("MV_RELSERV"),cMailServer)
		cMailSenha  := If(cMailSenha == NIL,GETMV("MV_EMSENHA"),cMailSenha)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Se o parametro nao estiver definido pega o parametro dos relatorios              |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( cMailConta )
			cMailConta := GETMV("MV_RELFROM",,"")
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Se o parametro nao estiver definido pega o parametro dos Contratos            |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( cMailConta )
			cMailConta := GETMV("MV_RELACNT",,"")
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Se o parametro nao estiver definido pega o parametro dos relatorios              |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( cMailSenha )
			cMailSenha := GETMV("MV_RELPSW")
		EndIf

		cMailCtaAut := If(cMailCtaAut == NIL,GETMV("MV_RELACNT"),cMailCtaAut)
		cMailUsrAut := If(cMailUsrAut == NIL,GETMV("MV_RELAUSR"),cMailUsrAut)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Caso nao exista conta definida, pega o proprio e-mail origem                       |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( cMailCtaAut )
			cMailCtaAut := cMailConta
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Caso nao exista senha da conta definida, pega a senha do proprio e-mail origem   |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( cMailUsrAut )
			cMailUsrAut	:= cMailSenha
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Ponto de entrada para envio de e-mail com os dados customizados         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock("MTENVML")
		    aMTENVML := ExecBlock("MTENVML",.F.,.F.,{cMailCtaAut,cMailConta,cMailSenha,cMailServer})
			If ValType(aMTENVML) == "A"
				If Len(aMTENVML)>=1 .And. Valtype(aMTENVML[1]) == "C"
			    	cMailCtaAut := aMTENVML[1]
			 	EndIf
			 	If Len(aMTENVML)>=2 .And. Valtype(aMTENVML[2]) == "C"
			  		cMailConta  := aMTENVML[2]
			  	EndIf
			  	If Len(aMTENVML)>=3 .And. Valtype(aMTENVML[3]) == "C"
			   		cMailSenha  := aMTENVML[3]
			   EndIf
   			  	If Len(aMTENVML)>=4 .And. Valtype(aMTENVML[4]) == "C"
			   		cMailServer  := aMTENVML[4]
			   EndIf
			EndIf
		EndIf

		//- Ponto de entrada genérico para inclusão de destinatário em cópia e/ou cópia oculta
		If ExistBlock("MMENCC"+Substr(cEvento,1,3))
			aMtEnvCC:= ExecBlock("MMENCC"+Substr(cEvento,1,3))
			If ValType(aMtEnvCC) == "A"
				If Len(aMtEnvCC) >= 1 .And. Valtype(aMtEnvCC[1]) == "C"
					cMailCC 	:= aMtEnvCC[1]
				EndIf
				If Len(aMtEnvCC) >= 2 .And. Valtype(aMtEnvCC[2]) == "C"
					cMailCCO 	:= aMtEnvCC[2]
				EndIf
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Envia e-mail com os dados necessarios                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cMailServer) .And. !Empty(cMailConta) .And. !Empty(cMailSenha) .And. !Empty(cMailCtaAut)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Conecta uma vez com o servidor de e-mails                                        |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lOk
				CONNECT SMTP SERVER cMailServer ACCOUNT cMailCtaAut PASSWORD cMailSenha RESULT lOk
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Se configurado, efetua a autenticacao                                            |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lAutOk
				If ( lSmtpAuth )
					lAutOk := MailAuth(cMailUsrAut,cMailSenha)

					If !lAutOk
						nAux	:= At("@",cMailUsrAut)
               			cUser	:= IF(nAux>0,SUBS(cMailUsrAut,1,nAux-1),cMailUsrAut)
			       		lAutOk	:= MailAuth(cUser,cMailSenha)
			       EndIf
				Else
					lAutOk := .T.
				EndIf
			EndIf

			If lOk .And. lAutOk
					SEND MAIL FROM AllTrim(cMailConta);
					TO	 AllTrim(aUsers[nX,2]);
					CC  cMailCC;
					BCC cMailCCO;
					SUBJECT AllTrim(aEventos[nAcho,2]);
					BODY cMensagem;
					RESULT lSendOk

				If !lSendOk
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Erro no Envio do e-mail                                                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					GET MAIL ERROR cError
					MsgInfo(cError,OemToAnsi(STR0070)) // "Erro no envio de e-Mail"
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Erro na conexao com o SMTP Server ou na autenticacao da conta          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				GET MAIL ERROR cError
				MsgInfo(cError,OemToAnsi(STR0070)) // "Erro no envio de e-Mail"
			EndIf
		EndIf
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Desconecta com o servidor de e-mails                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lOk
		DISCONNECT SMTP SERVER
	EndIf
EndIf
RestArea(aArea)
aSize(aArea,0)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MsSalPend ³ Autor ³ Valdir                ³ Data ³21.08.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna o Saldo de Pedidos pendentes do Produto e/ou Fornec ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Quantidade de pecas pendentes do produto e/ou Fornecedor    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cProduto    :Produto a ser processado						  ³±±
±±³			 ³cFornec     :Fornecedor a ser processado 					  ³±±
±±³			 ³cLoja       :Loja do Fornecedor a ser processado 	          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MsSalPend(cProduto,cFornecedor)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³Generico				                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MsSalPend(cProduto,cFornec,cLoja)
Local cFornLoja:=""
Local nPend    :=0
Local aAreaSC7 :=SC7->(GetArea())

DEFAULT cProduto:= ""
DEFAULT cFornec := ""
DEFAULT cLoja   := ""

cFornLoja:=Padr(cFornec,6)+Padr(cLoja,2)

IF (!Empty(cProduto )) .and. (!Empty(cFornLoja))
	SC7->(dbSetOrder(2))
	SC7->(dbSeek(xFilial("SC7")+cProduto+cFornLoja))
	While (SC7->(!Eof())) .and. SC7->C7_FILIAL == xFilial("SC7") .and. SC7->C7_PRODUTO == cProduto .and.;
			SC7->C7_FORNECE+SC7->C7_LOJA == cFornLoja
		IF !Empty(SC7->C7_RESIDUO)
			SC7->(dbSkip())
			Loop
		EndIf
		nPend+=(SC7->C7_QUANT-SC7->C7_QUJE)
		SC7->(dbSkip())
	End
ElseIF (!Empty(cProduto )) .and. (Empty(cFornLoja))
	SC7->(dbSetOrder(2))
	SC7->(dbSeek(xFilial("SC7")+cProduto))
	While (SC7->(!Eof())) .and. SC7->C7_FILIAL == xFilial("SC7") .and. SC7->C7_PRODUTO == cProduto
		If !Empty(SC7->C7_RESIDUO)
			SC7->(dbSkip())
			Loop
		EndIF
		nPend+=(SC7->C7_QUANT-SC7->C7_QUJE)
		SC7->(dbSkip())
	End
ElseIF (Empty(cProduto )) .and. (!Empty(cFornLoja))
	SC7->(dbSetOrder(3))
	SC7->(dbSeek(xFilial("SC7")+cFornLoja))
	While (SC7->(!Eof())) .and. SC7->C7_FILIAL == xFilial("SC7") .and. SC7->C7_FORNECE+SC7->C7_LOJA == cFornLoja
		If !Empty(SC7->C7_RESIDUO)
			SC7->(dbSkip())
			Loop
		EndIf
		nPend+=(SC7->C7_QUANT-SC7->C7_QUJE)
		SC7->(dbSkip())
	End
EndIF
SC7->(RestArea(aAreaSC7))
Return (nPend)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ MsDecimais  ³ Autor ³ Lucas     	  		³ Data ³ 22/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Arrendodar valores baseando se no parametro MV_RNDLOC que  ³±±
±±³          ³ indica o nuemro de casas para cada moeda.				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := MsRound(ExpN2)									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN2 := Numero da moeda da operacao aritimetica   		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 := Numero de casas para arredondamento				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Localizacoes - Todos os Paises							  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MsDecimais( nMoeda )
DEFAULT nMOEDA := 1

If ( __cUltFilMVCent == Nil ) .Or. !(__cUltEmpMVCent+__cUltFilMVCent == cEmpAnt+cFilAnt ) .Or. ( __nMV9CENT == Nil ) .Or. ( __nMV1CENT == Nil ) .Or. ( __nMV0CENT == Nil )
	__cUltEmpMVCent := cEmpAnt
	__cUltFilMVCent := cFilAnt
	If nMoeda > 9
		__nMV9CENT := SuperGetMv("MV_CENT"+Str(nMoeda,2))
		Return __nMV9CENT
	ElseIf nMoeda > 1
		__nMV1CENT := SuperGetMv("MV_CENT"+Str(nMoeda,1))
		Return __nMV1CENT
	Else
		__nMV0CENT := SuperGetMv("MV_CENT")
		Return __nMV0CENT
	Endif
EndIf

If nMoeda > 9
	Return __nMV9CENT
ElseIf nMoeda > 1
	Return __nMV1CENT
Else
	Return __nMV0CENT
Endif

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³VLDAuxCod2³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Verifica se existe B1_GRUPO+B1_CODITE						  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Veiculos                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function VLDAuxCod2(cCampoBase,cCampo1)
Local nReg, nOrd, cAlias := Alias()
Local cValid := '.t.', lRet
Local uVar := &(ReadVar()), lEof, cCod, nCol, cAnt, cReadAnt
Local nPos
Local lGetDados
Local oModel   := FWModelActive()
Local lIsInMVC	:= ValType(oModel) <> "U" .And. oModel:IsActive()
DEFAULT lGetDados := (Type('aHeader') == 'A') .and.  (Type('N') == 'N')

IF Empty(uVar)
	If lGetDados
		nPos	:= aScan(aHeader,{|x|x[2]==SubStr(ReadVar(),4)})
		IF nPos > 0
			aCols[n,nPos] := SB1->B1_CODITE
		Endif
	EndIf
	uVar := SB1->B1_CODITE
EndIF
IF lGetDados
	nPos	:= aScan(aHeader,{|x|Trim(x[2])==Trim(cCampo1)})
	IF nPos == 0
		Help(' ',1, 'NAOEXISTE')
		Return .f.
	Endif
	uVar=aCols[n,nPos]+uVar
Elseif lIsInMVC
	If !empty(FWFldGet(cCampo1))
		FWFldGet(cCampo1) := Space(len( FWFldGet(cCampo1)))
	Else
		FWFldGet(cCampo1) := Space(len(SB1->B1_GRUPO))
	EndIf
	uVar=FWFldGet(cCampo1)+uVar
Else
	If !empty(m->&cCampo1)
		m->&cCampo1 := Space(len( m->&cCampo1))
	Else
		m->&cCampo1 := Space(len(SB1->B1_GRUPO))
	EndIf
	uVar=m->&cCampo1+uVar
EndIf


dbSelectArea('SB1')
nOrd := IndexOrd()
nReg := Recno()
dbSetOrder(7)
lEof := !dbSeek(xFilial()+uVar)
cCod := SB1->B1_COD
dbSetOrder(nOrd)
dbGoto(nReg)

IF lEof
	dbSelectArea(cAlias)
	Help(' ',1, 'NAOEXISTE')
	Return .f.
Endif

dbSelectArea('SX3')
nReg := Recno()
nOrd := IndexOrd()
dbSetOrder(2)
dbSeek(cCampoBase)
IF !Empty(SX3->X3_VALID)
	cValid := SX3->X3_VALID
Endif
IF !Empty(SX3->X3_VLDUSER)
	cValid := '('+cValid+') .and. ('+SX3->X3_VLDUSER+')'
Endif

dbSetOrder(nOrd)
dbGoto(nReg)
dbSelectArea(cAlias)
cReadAnt := __ReadVar
IF lGetDados
	nCol := ASCAN(aHeader, {|x| Alltrim(x[2]) == cCampobase })
	cAnt := aCols[n,nCol]
	&('m->'+cCampoBase) := cCod
	aCols[n,nCol] := cCod
	__ReadVar := 'm->'+cCampoBase
Else
	cAnt := &('M->'+cCampoBase)
	&('M->'+cCampoBase) := cCod
	__ReadVar 				:= 'M->'+cCampoBase   //Enchoice
	&('M->'+SubStr(cCampo1,1,9)+'2') := SB1->B1_CODITE
Endif

lRet := &(cValid)
IF !lRet
	&('m->'+cCampoBase) := cAnt
	If lGetDados
		aCols[n,nCol] := cAnt
	EndIF
Endif
dbSelectArea(cAlias)
__ReadVar := cReadAnt
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³VLDAuxCod1³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Valida o B1_GRUPO e Limpa o _CODAUX2                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Veiculos                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function VLDAuxCod1(cCampo1,cCampo2)
Local lRet := .F.

If SuperGetMV("MV_VEICULO",.F.,"N")=="S"
	If ExistCpo('SB1',M->&cCampo1,7)
		If (Type('aHeader') == 'A') .And. (Type('N') == 'N')
			nPos := aScan(aHeader,{|x|Trim(x[2])==Trim(cCampo2)})
			aCols[n,nPos]:=Space(aHeader[nPos,4])
		Else
	   		If !empty(m->&cCampo2)
	   			m->&cCampo2 := Space(len( m->&cCampo2))
			Else
				m->&cCampo2 := Space(len(SB1->B1_CODITE))
			EndIf	
		EndIf
	lRet := .T.
	Endif
ElseIf empty(&cCampo1)
	lRet :=.T.
	Else
	lRet := ExistCpo('SBM',M->&cCampo1,1)
Endif

SB1->(DbSetOrder(1))
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³INIAuxCod ³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Retorna o SB1->B1_GRUPO ou SB1->B1_CODITE                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Veiculos                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function INIAuxCod(cCod,cCodAux)

Local aOldArea	:= {}
Local aAreaSB1	:= {}
Local cReturn	:= ''
Local cOldArea	:= ""
Local nPosUnder	:= 0
Local cKeySeek  
Static oCodRet := JsonObject():New()

If Type('Inclui') == 'L' .and. Inclui
	Return (cReturn)
EndIf
If (Type('aCols') == 'A') .and. (Type('N') == 'N') .and. (n > 0 ) .and. (n <= Len(aCols)) .and. (Len(aCols[1]) <> Len(aCols[n]))
	Return (cReturn)
EndIf

cKeySeek  := cEmpAnt+cFilAnt+cCod+cCodAux

If (cReturn := oCodRet[cKeySeek] )== nil 
	cOldArea := Alias()

	If Empty(cOldArea)
		nPosUnder := At("_",cCodAux)

		If nPosUnder == 3
			cOldArea := "S"+SubStr(cCodAux,1,nPosUnder-1)
		Elseif nPosUnder == 4
			cOldArea := SubStr(cCodAux,1,nPosUnder-1)
		Endif
	Endif

	aOldArea	:= (cOldArea)->(GetArea())
	aAreaSB1	:= SB1->(GetArea())
	SB1->(dbSetOrder(1))
	SB1->(MsSeek(xFilial("SB1")+cCod))
	cReturn	:= If(Substr(cCodAux,3) == '_CODGRP', SB1->B1_GRUPO, SB1->B1_CODITE)

	oCodRet[cKeySeek] := cReturn 

	RestArea(aAreaSB1)
	RestArea(aOldArea)
	aSize(aOldArea,0)
	aSize(aAreaSB1,0)
	aOldArea	:= NIL
	aAreaSB1	:= NIL
EndIf 
Return (cReturn)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RtPict    ³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Retorna a Picture do CODITE de acordo c/ o GRUPO            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Veiculos                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function RtPict(cCampo)
Local cPict :='@!%C'
Local nPos
If (Type('aHeader') == 'A') .and.  (Type('N') == 'N') .and. ((nPos	:= aScan(aHeader,{|x| Upper(Alltrim(x[2]))==Upper(Alltrim(cCampo))})) > 0 )
	cPict := Trim(POSICIONE('SBM',1,xFilial('SBM')+aCols[n,nPos],'BM_PICPAD'))+'%C'
Else
	If !EMPTY(&('m->'+cCampo))
		cPict := Trim(POSICIONE('SBM',1,xFilial('SBM')+&('m->'+cCampo),'BM_PICPAD'))+'%C'
	EndIf
EndIf
Return RTrim(cPict)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RetGrupo  ³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Filtra os itens de um Grupo                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Veiculos                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function RetGrupo()
Local cReturn :='.T.'
Local nPos
Local cVar
Local aGets
Local oModel := FWModelActive()
Local lIsInMVC	:= ValType(oModel) <> "U" .And. oModel:IsActive()

If (Type('aHeader') == 'A') .and.  (Type('N') == 'N')
	nPos	:= aScan(aHeader,{|x|SubStr(x[2],3,7)== '_CODGRP' })
	If nPos == 0
		nPos	:= aScan(aHeader,{|x|SubStr(x[2],3,7)== '_CODITE' })
	EndIF
	IF nPos > 0
		cReturn:=aCols[n,nPos]
	Endif
Elseif lIsInMVC
	Do case
		Case oModel:GetId() == "MATA061"
			cReturn:= (FWFldGet('A5_CODGRP'))
		Case oModel:GetId() == "MATA370"
			cReturn:= (FWFldGet('A7_CODGRP'))
		Case oModel:GetId() == "MATA180"
			cReturn:= (FWFldGet('B5_CODGRP'))
	End case
Else
	nPos	:= aScan(aGets,{|x|SubStr(x,11,7)== '_CODGRP' })
	If nPos == 0
		nPos	:= aScan(aGets,{|x|SubStr(x,11,7)== '_CODITE' })
	EndIF
	IF nPos > 0
		cVar := Subst(aGets[nPos],9,10)
		cReturn:=m->&cVar
	Endif
EndIf
Return (cReturn)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsGetPos   ³ Autor ³ Edson Maricate       ³ Data ³24/08/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calculo automatico de dimensoes de objetos GET             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsObjGetPos(nSizeAtu,nSizeDef,aPosDef)
Local aRet	:= aClone(aPosDef)
Local nX
Local nY

For nx := 1 to Len(aPosDef)
	For ny := 1 to (Len(aPosDef[nx]))
		aRet[nx,ny] := aPosDef[nx,ny]*(nSizeAtu/nSizeDef)
	Next
Next

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GDFieldPos³ Autor ³ Sergio Silveira       ³ Data ³ 05/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve a posicao de um campo na GetDados                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := GDFieldPos( ExpC1 , [ ExpA2 ] )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Campo                                             ³±±
±±³          ³ ExpA2 -> aHeader Opcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 -> Posicao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GdFieldPos( cCampo , aHeadOpc )

Local nRet := 0

DEFAULT aHeadOpc := aHeader

cCampo	 := Upper( AllTrim( cCampo ) )
IF !( cCampo == "GDDELETED" )
	nRet := aScan( aHeadOpc , { |x| Upper( AllTrim( x[2] ) ) == cCampo } )
Else
	nRet := GdPosDeleted( aHeadOpc )
EndIF

Return( nRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GDFieldGet³ Autor ³ Sergio Silveira       ³ Data ³ 05/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve o conteudo de um campo na GetDados                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpX1 := GDFieldGet( ExpC1, [ ExpL1 ] )                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Campo / ExpL1 -> Linha do aCols / lReadVar        ³±±
±±³          ³ lReadVar -> Se .T. e cCampo = ReadVar() retorna conteudo de³±±
±±³          ³ ReadVar(), Se .F. (Default) retorna conteudo do Acols      ³±±
±±³          ³ aHeaderPastPar -> aHeader opicional					      ³±±
±±³          ³ aColsPastPar	  -> aCols opicional					      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpX1 -> Conteudo                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GdFieldGet( cCampo , nLine , lReadVar , aHeaderPastPar , aColsPastPar )

Local nPos      		:= 0
Local xConteudo 		:= NIL

cCampo 					:= Upper( AllTrim( cCampo ) )
DEFAULT nLine			:= n
DEFAULT lReadVar		:= .F.
DEFAULT aHeaderPastPar	:= aHeader
DEFAULT aColsPastPar	:= aCols

IF ( lReadVar .and. ( nLine == n ) .and. ( ValType( ReadVar() ) == "C" ) .and. ( ReadVar() == "M->" + cCampo ) )
	xConteudo := &( ReadVar() )
Else
	IF !Empty( ( nPos := GdFieldPos( cCampo , aHeaderPastPar ) ) )
		xConteudo := aColsPastPar[ nLine , nPos ]
	EndIF
EndIF

Return( xConteudo )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GdFieldPut³ Autor ³ Sergio Silveira       ³ Data ³ 05/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Coloca o conteudo de um campo na GetDados                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ GDFieldPut( ExpC1, ExpX1, [ ExpN1 ] )                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Campo/ExpX1 -> Conteudo/ExpN1 -> Linha do Acols   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GdFieldPut( cCampo , xConteudo , nLine , aHeaderPastPar , aColsPastPar , lReadVar )

Local nPos		:= 0
Local uLastCnt	:= NIL

DEFAULT nLine		   	:= n
DEFAULT aHeaderPastPar	:= aHeader
DEFAULT aColsPastPar	:= aCols
DEFAULT lReadVar		:= .F.

IF !Empty( nPos := GdFieldPos( cCampo , aHeaderPastPar ) )
	uLastCnt := GdFieldGet( cCampo , nLine , lReadVar , aHeaderPastPar , aColsPastPar )
	aColsPastPar[ nLine , nPos ] := xConteudo
EndIF

Return( uLastCnt )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GdDeleted ³ Autor ³ Sergio Silveira       ³ Data ³ 05/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Identifica Linha excluida na Getdados                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := GDDeleted( [ ExpN1] )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> ExpN1 -> Linha do Acols                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1 -> Item excluido ou nao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function GdDeleted( nLine , aHeaderPastPar , aColsPastPar )

Local lDeleted	:= .F.
Local nPos		:= 0

DEFAULT nLine			:= n
DEFAULT aHeaderPastPar	:= aHeader
DEFAULT aColsPastPar	:= aCols

IF !Empty( nPos := GdFieldPos( "GDDELETED" , aHeaderPastPar ) )
	lDeleted := aColsPastPar[ nLine, nPos ]
EndIF

Return( lDeleted )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³FtContato ³ Autor ³ Sergio Silveira       ³ Data ³06/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Amarracao entidades x Contatos                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Entidade                                            ³±±
±±³          ³ ExpN1 -> Registro                                            ³±±
±±³          ³ ExpN2 -> Opcao                                               ³±±
±±³          ³ ExpX1 -> Sem Funcao                                          ³±±
±±³          ³ ExpN3 -> Tipo de Operacao                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo Kotaki³01/05/01³ 609  ³O cadastro de CONTATOS pode ser associado ³±±
±±³              ³        ³      ³a qualquer entidade.(Clientes, Fornecedor)³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FtContato( cAlias, nReg, nOpc, xVar, nOper )

Local aArea        := GetArea()

Local lExcNotView	:= .F.
Local nOperation := 4									// Alterar no MVC

PRIVATE aCols      := {}
PRIVATE aHeader    := {}
PRIVATE n := 1 // variavel para o model "CRMA060"

DEFAULT nOper      := 1

DbSelectArea("AD2")
If nOper == 3
	lExcNotView := .T.
	nOperation  := 5
EndIf

CRMA060(cAlias, nReg, nOperation, lExcNotView)

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocument³ Autor ³ Sergio Silveira       ³ Data ³06/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Amarracao entidades x documentos                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Entidade                                            ³±±
±±³          ³ ExpN1 -> Registro                                            ³±±
±±³          ³ ExpN2 -> Opcao                                               ³±±
±±³          ³ ExpX1 -> Sem Funcao                                          ³±±
±±³          ³ ExpN5 -> Tipo de Operacao                                    ³±±
±±³          ³ ExpA6 -> Array de referencia retorno dos anexos ( Recno )    ³±±
±±³          ³ ExpL7 -> Flag que indica se abre as planilhas Excel          ³±±
±±³          ³          conectadas ao Protheus.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocument( cAlias, nReg, nOpc, xVar, nOper, aRecACB , lExcelConnect)

//Conforme instruções do Sergio Silveira
Return MPDocument( cAlias, nReg, nOpc, xVar, nOper, aRecACB , lExcelConnect )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ MsDocGrv ³ Autor ³ Sergio Silveira       ³ Data ³06/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gravacao / Exclusao da amarracao                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := FtContGrv( ExpC1, ExpC2, ExpA1, [ExpL2] )           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se gravou                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Entidade                                            ³±±
±±³          ³ ExpC2 -> Codigo entidade                                     ³±±
±±³          ³ ExpA1 -> Array de registros                                  ³±±
±±³          ³ ExpL2 -> Indica se e exclusao                                ³±±
±±³          ³ ExpL3 ->   								                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Fernando  	 ³08/01/06³116015³alteração feita para que seja gravados os ³±±
±±³            	 ³        ³      ³itens no banco de conhecimento quando o 	³±±
±±³            	 ³        ³      ³ambiente for Linux						³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocGrv( cEntidade, cCodEnt, aRecAC9, lExclui, lFilAcols )

//Conforme instruções do Sergio Silveira
Return MPDocGrv( cEntidade, cCodEnt, aRecAC9, lExclui, lFilAcols )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ MsDocLok ³ Autor ³ Sergio Silveira       ³ Data ³08/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da linha da amarracao.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := FtContLOK()                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocLOK()

//Conforme instruções do Sergio Silveira
Return MPDocLOK()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsFlPreview³ Autor ³ Sergio Silveira      ³ Data ³08/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua o preview do objeto                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MsFlPreview( ExpO1, ExpA1, ExpL1 )                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto OLE                                          ³±±
±±³          ³ ExpA1 -> Array de arquivos a excluir                         ³±±
±±³          ³ ExpL1 -> Indica se foi efetuado o Preview                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsFlPreview( oOle, aExclui, lPreview )

//Conforme instruções do Sergio Silveira
Return MPFlPreview( aExclui )


/*---------------------------------------------------------
{Protheus.doc} MsTraDoc
Função criada para transferir documento para diretório Temp
@author thiago.rover
@since 10/09/2019
----------------------------------------------------------*/
Function MsTraDoc()

LOCAL	lRemotLin:= GetRemoteType() == 2
LOCAL   lCopied  := .F.
LOCAL   cDirDocs := ""
LOCAL   cFile    := AllTrim( GDFieldGet( "AC9_OBJETO" ) )

If MsMultDir()
   	cDirDocs := MsRetPath( cFile )
Else
	cDirDocs := MsDocPath()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retira a ultima barra invertida ( se houver )                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cDirDocs  := MsDocRmvBar( cDirDocs )
cPathFile := cDirDocs + "\" + cFile

cTempPath := GetTempPath()

cPathTerm := cTempPath + cFile

lAbre := .T.

If File( cPathTerm )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua o tratamento caso o arquivo ja exista no temp do Windows        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lAbre := .F.

	If GDFieldGet( "AC9_PRVIEW" ) <> "1"
		nOpc := Aviso( STR0114, STR0115 + Capital( cFile ) + STR0116, { STR0117, STR0118 }, 2 )  //"Atencao!"###"O arquivo '"###"' ja existe em sua area de trabalho. Qual a acao a ser efetuada ?"###"Sobrepor"###"Cancelar"

		If nOpc == 1
			lAbre := .T.
			fErase(cPathTerm)
		EndIf
	Else
		lAbre   := .T.
		lCopied := .T.
	EndIf

EndIf

If lAbre
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Transfere o arquivo para o diretorio de trabalho do Windows            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lCopied
		cPathFile := Lower(cPathFile)
		cPathTerm := Lower(cPathTerm)
		Processa( { || lCopied := __CopyFile( cPathFile, If(lRemotLin,"l:","")+cPathTerm ) }, STR0119, STR0120, .F. )	//"Transferindo objeto"###"Aguarde..."
	EndIf

	If lCopied .Or. File( cPathTerm )
		GDFieldPut( "AC9_PRVIEW", "1" )
	Else
		Aviso( STR0121, STR0122 + cFile + STR0123, { STR0124 }, 2 )   //"Atencao !"###"Nao foi possivel efetuar a transferencia do arquivo '"###"' do banco de conhecimento para a area de trabalho !"###"Ok"
	EndIf
EndIf


Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ MsDocExis³ Autor ³ Sergio Silveira       ³ Data ³08/12/2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a existencia do documento                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocExis()                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocExis()

LOCAL cDirDocs := ""
LOCAL cFile    := AllTrim( &( ReadVar() ) )

LOCAL lRet     := .T.

If MsMultDir()
	cDirDocs := MsRetPath( cFile )
Else
	cDirDocs := MsDocPath()
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retira a ultima barra invertida ( se houver )                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cDirDocs := If( Right( cDirDocs, 1 ) == "\", Left( cDirDocs, Len( cDirDocs ) -1 ), cDirDocs )

lRet     := File( cDirDocs + "\" + cFile )

If !lRet
	Help( " ", 1, "OBJNEXIS" )
EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsRelation³ Autor ³ Sergio Silveira       ³ Data ³31/02/2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Devolve array contendo a chave unica de relacionamento de    ³±±
±±³          ³ cada entidade e o campo descricao.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := MsRelation()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsRelation()

//Conforme instruções do Sergio Silveira
Return MPRelation()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsDocSize ³ Autor ³ Sergio Silveira       ³ Data ³12/03/2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Muda o tamanho da janela para a vizualizacao do objeto       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocSize( ExpO1, ExpO2, ExpA1, ExpA2, ExpA3 )               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto Scroll                                       ³±±
±±³          ³ ExpO2 -> Objeto Ole                                          ³±±
±±³          ³ ExpA1 -> Coordenadas de oScroll                              ³±±
±±³          ³ ExoA2 -> Nao usado                                           ³±±
±±³          ³ ExpA3 -> Lista de objetos a esconder / mostrar               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocSize( oScroll, oOle, aPosObjMain, aDimen, aHide )

nStatus := oScroll:Cargo

If nStatus == 1

	oScroll:nTop    := aPosObjMain[ 1,1 ] * 2
	oScroll:nLeft   := aPosObjMain[ 1,2 ] * 2

	oScroll:nBottom := aPosObjMain[ 1,3 ] * 2
	oScroll:nRight  := aPosObjMain[ 1,4 ] * 2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Esconde os demais objetos  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AEval( aHide, { |x| x:Hide() } )

	oScroll:Cargo := 2

Else

	oScroll:nTop    := aDimen[1] * 2
	oScroll:nLeft   := aDimen[2] * 2

	oScroll:nBottom := ( aDimen[1] + aDimen[4] ) * 2
	oScroll:nRight  := ( aDimen[2] + aDimen[3] ) * 2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Exibe os demais objetos    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AEval( aHide, { |x| x:Show() } )

	oScroll:Cargo := 1

EndIf

oOle:Refresh()

Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsDocOpen ³ Autor ³ Sergio Silveira       ³ Data ³12/03/2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a abertura do documento posicionado                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocOpen( ExpO1, ExpA1 )                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto ole ( Preview ) / ExpA1 -> Array de arquivos ³±±
±±³          ³ a excluir.                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocOpen( oOle, aExclui )

//Conforme instruções do Sergio Silveira
Return MPDocOpen( aExclui )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsDocCall ³ Autor ³ Sergio Silveira       ³ Data ³04/04/2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a chamada do banco de conhecimento                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocCall( )                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocCall()

//Conforme instruções do Sergio Silveira
Return MPDocCall()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³MsDocArray³ Autor ³ Sergio Silveira       ³ Data ³18/04/2001  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta acols ou array contendo recnos do ACB                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MsDocArray( )                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocArray( cEntidade, cCodEnt, aCols, aTravas, aRecAC9, nTipo, aRecACB )

//Conforme instruções do Sergio Silveira
Return MPDocArray( cEntidade, cCodEnt, aCols, aTravas, aRecAC9, nTipo, aRecACB )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsDocExclui³ Autor ³Sergio Silveira       ³ Data ³29/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exclui arquivos do diretorio temporario do windows         ³±±
±±³          ³ ( Banco de conhecimento )                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocExclui( ExpA1 )                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se excluiu todos os arquivos               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 -> Array com os arquivos a exluir ( com path )       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MsDocExclui( aExclui, lPergunta )

//Conforme instruções do Sergio Silveira
Return MPDocExclui( aExclui, lPergunta )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsDocRmvBar³ Autor ³Sergio Silveira       ³ Data ³05/04/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Remove a ultima barra invertida do Path ( se houver )      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocRmvBar( @ExpC1 )                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Path                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MsDocRmvBar( cDirDocs )

//Conforme instruções do Sergio Silveira
Return MPDocRmvBar( cDirDocs )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsDocLoclz ³ Autor ³Sergio Silveira       ³ Data ³09/04/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Engine de busca do banco de conhecimentos                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocLoclz(ExpC1,ExpC2,ExpC3,@ExpA1,ExpL1,ExpL2)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> .T.                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> String com palavras-chave                         ³±±
±±³          ³ ExpC2 -> Descricao para pesquisa                           ³±±
±±³          ³ ExpC3 -> Objeto para pesquisa                              ³±±
±±³          ³ ExpA1 -> Array contendo os resultados da busca             ³±±
±±³          ³ ExpL1 -> Indica pesquisa exata                             ³±±
±±³          ³ ExpL2 -> Indica se havia keyword                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocLoclz( cKeyWord, cDescri, cObjeto, aResult, lExata, lKeyWord )

LOCAL aKeyWord    := {}
LOCAL aListaACB   := {}
LOCAL aStruct     := {}
LOCAL aItem       := {}

LOCAL cArqTrab    := ""
LOCAL cSeekACC    := ""
LOCAL cSeekACB    := ""

LOCAL lRet        := .T.
LOCAL lAdicObj    := .F.

LOCAL nLoop       := 0
LOCAL nKeyCount   := 0
LOCAL nSizeDesc   := 0
LOCAL nSizeObj    := 0
LOCAL nSizeKey    := 0

LOCAL cQuery      := ""
LOCAL cAliasQry   := ""

DEFAULT lKeyWord  := .F.
DEFAULT lExata    := .F.

If !Empty( cKeyWord ) .Or. !Empty( cDescri ) .Or. !Empty( cObjeto )

	aResult  := {}
	aKeyWord := Ft340ExtKey( cKeyWord )

	cDescri := AllTrim( cDescri )
	cObjeto := AllTrim( cObjeto )

	lKeyWord := !Empty( aKeyWord )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Rotina para recuperacao de dados em SQL                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Identifica os conhecimentos que possuem pelo menos uma palavra chave   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAliasQry := GetNextAlias()

	cQuery := ""
	cQuery += "SELECT ACB_CODOBJ, ACB_DESCRI, ACB.R_E_C_N_O_ ACBRECNO "
	If lKeyWord
		cQuery += ",ACC_CODOBJ,ACC_KEYWRD "
	EndIf

	cQuery += " FROM " + RetSqlName( "ACB" ) + " ACB "

	If lKeyWord
		cQuery += "," + RetSqlName( "ACC" ) + " ACC "
	EndIf

	cQuery += "WHERE "
	cQuery += "ACB_FILIAL='" + xFilial( "ACB" ) + "' AND "

	If lKeyWord

		cQuery += "ACC_FILIAL='" + xFilial( "ACB" ) + "' AND "
		cQuery += "ACB_CODOBJ=ACC_CODOBJ AND ( "

		For nLoop := 1 To Len( aKeyWord )

			If nLoop > 1
				cQuery += " OR "
			EndIf

			If lExata
				cQuery += "ACC_KEYWRD='" + aKeyWord[ nLoop, 1 ] + "'"
			Else
				cQuery += "ACC_KEYWRD LIKE '%" + AllTrim( aKeyWord[ nLoop, 1 ] ) + "%'"
			EndIf
		Next nLoop

		cQuery += " ) AND "
		cQuery += "ACC.D_E_L_E_T_=' ' AND "

	EndIf

	If !Empty( cObjeto )
		If lExata
			cQuery += "ACB_OBJETO='" + cObjeto + "' AND "
		Else
			cQuery += "ACB_OBJETO LIKE '%" + AllTrim( cObjeto ) + "%' AND "
		EndIf
	EndIf

	If !Empty( cDescri )
		If lExata
			cQuery += "ACB_DESCRI='" + cDescri + "' AND "
		Else
			cQuery += "ACB_DESCRI LIKE '%" + AllTrim( cDescri ) + "%' AND "
		EndIf
	EndIf

	cQuery += "ACB.D_E_L_E_T_=' ' ORDER BY ACB_CODOBJ"

	cQuery := ChangeQuery( cQuery )

	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasQry, .F., .T. )

	If Alias() == cAliasQry

		While !Eof()
			nKeyCount := 0
			If lKeyWord
				cCodObj := ACB_CODOBJ
				AEval( aKeyWord, { |x| x[2] := .F. } )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se este conhecimento possui todas as palavras chave           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nKeyCount := 0

				While !Eof() .And. ACB_CODOBJ == cCodObj
					If !Empty( nScan := AScan( aKeyWord, { |x| x[1] $ ACC_KEYWRD } ) )
						aKeyWord[ nScan, 2 ] := .T.
						nKeyCount++
					EndIf

					aItem := { Capital( ACB_DESCRI ), ACBRECNO, nKeyCount }
					dbSkip()
				EndDo

				lAdicObj := .T.

				For nLoop := 1 To Len( aKeyWord )
					If !aKeyWord[ nLoop, 2 ]
						lAdicObj := .F.
						Exit
					EndIf
				Next nLoop
			Else
				lAdicObj := .T.
				aItem := { Capital( ACB_DESCRI ), ACBRECNO, nKeyCount }
				dbSkip()
			EndIf

			If lAdicObj
				AAdd( aResult, aItem )
			EndIf

		EndDo

	EndIf
EndIf

Return( lRet  )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsDocView  ³ Autor ³Sergio Silveira       ³ Data ³28/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Abre o objeto efetuando a Shell Execute                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocView( ExpC1, [ @ExpA1 ], [ ExpC2 ], [ExpC3]) ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Abertura bem sucedida                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Arquivo a abrir ( sem path )                      ³±±
±±³          ³ ExpA1 -> Array de arquivos a excluir                       ³±±
±±³          ³ ExpC2 -> Acao a ser executada ( verb )                     ³±±
±±³          ³ ExpC3 -> Diretorio onde esta localizado o arquivo          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocView( cFile, aExclui, cVerb, cDirDocs )

LOCAL cDrive     := ""
LOCAL cDir       := ""
LOCAL cPathFile  := ""
LOCAL cTempPath  := ""
LOCAL cPathTerm  := ""
LOCAL cCompl     := ""
Local lMacOS 	 := .F.
Local cLibCli	 := ""
Local lCopied    := .F.
Local lRet       := .F.
LOCAL nRet       := 0
Local lLinuxCli  := .F.

DEFAULT cVerb    := ""

GetRemoteType( @cLibCli )
lMacOS := Iif('MAC' $ cLibCli,.T.,.F.)
lLinuxCli := ("linux" $ lower(cLibCli))

If !Empty(cFile)

	If MsMultDir()
		DEFAULT cDirDocs := MsRetPath( cFile )
	Else
		DEFAULT cDirDocs := MsDocPath()
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Retira a ultima barra invertida ( se houver )                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MsDocRmvBar( @cDirDocs )

	cFile := Alltrim(cFile)
	cPathFile := cDirDocs + "\" + cFile

	cTempPath := GetTempPath()
	cPathTerm := cTempPath + cFile

	If lLinuxCli
		cPathFile := Lower(cPathFile)
		cPathTerm := StrTran(cPathTerm, "/", "\")
		cPathTerm := "l:"+Lower(cPathTerm)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Copia o arquivo do servidor para o diretorio temporario do terminal    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMacOS
		lCopied := CpyS2T(cPathFile, "C:\Library\Caches", .F. ) //Se Client é MAC copia para o local identico ao TEMP
	Else
		Processa( { || lCopied := __CopyFile( cPathFile, cPathTerm ) }, STR0119, STR0120, .F. )	//"Transferindo objeto"###"Aguarde..."
    EndIf
	If lCopied

		SplitPath(cPathTerm, @cDrive, @cDir )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento para ambiente Linux ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If IsSrvUnix() .And. GetRemoteType() == 1
			cDir := StrTran(cDir,"/","\")
		Endif

		cDir := Alltrim(cDrive) + Alltrim(cDir)

		If !lMacOS
			If ExistBlock( "MTADOCOPEN" )
				nRet := ExecBlock( "MTADOCOPEN", .F., .F.,{cVerb,AllTrim(cFile),"",cDir})
			Else
				If lLinuxCli
					// Monta como abrir com o aplicativo padrão do SO para Linux
					cVerb := If( Empty( cVerb ), "xdg-open", AllTrim( cVerb ) )

					cFile := cVerb + ' "' + cDir + Lower(AllTrim(cFile)) + '"'
					nRet := WaitRun( cFile )
				Else
					// Faz a chamada do aplicativo associado para Windows
					cVerb := If( Empty( cVerb ), "Open", AllTrim( cVerb ) )
					nRet := ShellExecute( cVerb, AllTrim(cFile),"",cDir, 1 )
				EndIf
			EndIf

			If nRet <= 32
				cCompl := ""
				If nRet == 31
					cCompl := STR0131 //" Nao existe aplicativo associado a este tipo de arquivo !"
				EndIf
				Aviso( STR0121, STR0125 + AllTrim(cFile) + STR0126 + cCompl, { STR0124 }, 2 ) 	 //"Atencao !"###"Nao foi possivel abrir o objeto '"###"'!"###"Ok"
			Else
				lRet := .T.

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Adiciona ao array de arquivos a excluir                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ValType( aExclui ) == "A"
					If Empty( AScan( aExclui, cPathTerm ) )
						AAdd( aExclui, cPathTerm )
					EndIf
				EndIf
			EndIf
		Else
			Waitrun("open "+"/Library/Caches/"+Lower(cFile))
		EndIF
	EndIf
EndIf

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³CtbMoedas ³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 24.07.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Conta quantas moedas estao em uso no SIGACTB               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nro de Moedas em uso                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CtbMoedas()
Local aSaveArea
Local lCache := ( GetNewPar( "MV_CTBCHMO" , "0" ) == "1" )   //"0"=Nao Trabalha com Cache "1"=Trabalha com Cache
Local cQuery := ""

#IFDEF TOP
	Local lQuery := ( TCGetDb() <> "POSTGRES"	.And. TcSrvType() != "AS/400" )

#ELSE
	Local lQuery := .F.

#ENDIF

If cFilMoed == NIL .OR. xFilial("CTO") != cFilMoed
	cFilMoed := xFilial("CTO")
	//se for primeira vez  ou  se mudou de filial e tabela CTO-MOEDAS for exclusivo em algum segmento deve carregar novamente qtd moedas
	nContMoeda := NIL   //atribui NIL para executar query novamente
EndIf

If !lCache .Or. nContMoeda == NIL
	aSaveArea := GetArea()
	nContMoeda := 0
	If lQuery
		cQuery := " SELECT COUNT(CTO_FILIAL) NCONT FROM "+RetSqlName("CTO")
		cQuery += " WHERE CTO_FILIAL = '"+xFilial("CTO")+"' "
		cQuery += " AND D_E_L_E_T_ = ' ' "

		//RETIRADO PARA PERFORMANCE - ANSI NAO HA NECESSIDADE DE PASSAR PELA CHANGEQUERY
		If ! ( Alltrim(Upper(TCGetDB())) $ "MSSQL|MSSQL7|ORACLE" )
			cQuery := ChangeQuery(cQuery)
		EndIf

		dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), 'CTDMOEDA', .F., .T.)
		TcSetField('CTDMOEDA',"NCONT","N",10,0)
		nContMoeda := CTDMOEDA->NCONT
		CTDMOEDA->( dbCloseArea() )
	Else

		If ChkFile("CTO")
			dbSelectArea("CTO")
			dbSetOrder(1)
			dbSeek(xFilial())
			While !Eof() .And. xFilial() == CTO->CTO_FILIAL
				nContMoeda++
				dbSkip()
			EndDo
		EndIf

	Endif

	RestArea(aSaveArea)
EndIf

Return nContMoeda

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsDocPath  ³ Autor ³Sergio Silveira       ³ Data ³29/05/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Devolve o Path do banco de conhecimento e cria o path caso  ³±±
±±³          ³ nao exista.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MsDocPath()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Path do arquivo                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocPath( lMultDir )

//Conforme instruções do Sergio Silveira
Return MPDocPath( lMultDir )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Chk_Pack	³ Autor ³ Equipe de RH			³ Data ³28/03/1995³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Executa um pack no Arquivo se o n. de registros deletados  ³±±
±±³			 ³ for maior que: (tolerancia(%) / RecCount())				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³Chk_Pack(cArquivo,nPerc,nRegMinimo,lShowHelp,lQuitProgram)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cArquivo		= Arquivo a executar o Pack					  ³±±
±±³			 ³ nPerc        =  Percentual de Tolerancia para Deletados	  ³±±
±±³			 ³ nRegMinimo   =  Quantidade Minima de Registros no Banco    ³±±
±±³			 ³ lShowHelp    =  Mostrar Help em Ma280FLock                 ³±±
±±³			 ³ lQuitProgram =  Sair do Programa se Nao Conseguiu FLocK    ³±±
±±³			 ³ lChkFile		=  Se Deve Executar ChkFile()                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³Generico													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function Chk_Pack( cArquivo , nPerc , nRegMinimo , lShowHelp , lQuitProgram , lChkFile )

Local aArea				:= GetArea()
Local aAreaSX2			:= {}
Local lLocked			:= .F.
Local lSX2				:= .F.

DEFAULT cArquivo		:= Alias()
DEFAULT nPerc			:= -1
DEFAULT nRegMinimo		:= 1
DEFAULT lShowHelp		:= .F.
DEFAULT lQuitProgram	:= .F.
DEFAULT lChkFile		:= .T.

IF ( Select("SX2") > 0.00 )
	aAreaSX2 := SX2->( GetArea() )
	lSX2 := SX2->( DbSeek( cArquivo ) )
EndIF

dbSelectArea(cArquivo)
IF ( ( cArquivo )->( RecCount() ) > nRegMinimo )
	IF ( ( lSX2 .and. ( SX2->X2_DELET / RecCount() ) > ( nPerc / 100 ) ) .or. !lSX2 )
		IF ( lLocked := Ma280Flock( cArquivo , lShowHelp , lQuitProgram ) )
			IF ( lSX2 )
				RetIndex( cArquivo )
			EndIF
			MyInternalPack( cArquivo )
			IF ( lSX2 )
				RecLock( "SX2" , .F. , .T. )
				SX2->X2_DELET := 0
				SX2->( MsUnlock() )
			EndIF
		EndIF
		IF ( lLocked .and. lSX2 .and. lChkFile )
			( cArquivo )->( dbCloseArea() )
			ChkFile( cArquivo , .F. )
		EndIF
	EndIF
EndIF

IF ( lSX2 )
	RestArea( aAreaSX2 )
EndIF

RestArea( aArea )

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma³MyInternalPack()ºAutor³Marinaldo de Jesusº Data ³02/08/2001   º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.   ³                                                              º±±
±±º        ³                                                              º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso     ³Chk_Pack()                                  	              º±±
±±ÈÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function MyInternalPack(cAlias)

Local cQuery		:= ""
Local cRetSqlName	:= RetSqlName(cAlias)
Local nRecno		:= 0
Local nMax			:= (cAlias)->(RecCount())

While nRecno < nMax
	nRecno += 1024
	cQuery := "DELETE FROM "+cRetSqlName
	cQuery += " WHERE R_E_C_N_O_ > "+AllTrim(Str(nRecno-1024,18,0))+" AND R_E_C_N_O_ <= "+AllTrim(Str(nRecno,18,0))
	cQuery += " AND D_E_L_E_T_ = '*'"
	TCSqlExec(cQuery)
	(cAlias)->( dbGotop() )
End While

Return( NIL )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³TipoAPS    ³ Autor ³ Marcelo Iuspa        ³ Data ³10/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se sera usado APS (PREACTOR/DRUMMER) ou nao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ lHelp, cAPS                                                ³±±
±±³          ³ lHelp: Se sera apresentado (ou nao) o help                 ³±±
±±³          ³ cAPS : O nome do APS a ser verificado em MV_APS            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAPCP       ³ Integracao PREACTOR/DRUMMER                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³14/09/2006³Erike Yuri     ³ Inclusao de validacao para integracao com  ³±±
±±³          ³               ³ Aps DRUMMER, mantendo legado do PREACTOR   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TipoAPS(lHelp, cAPS)
Local cAlias	:= Alias()
Local lTipoAPS	:= .F.
Local aCampos
Local x
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis Defaults³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Default lHelp	:= .F.
Default cAPS	:= "PREACTOR"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Variaveis Staticas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Static lCampos

cAPS := Upper( AllTrim(cAPS) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual e' o APS a ser analisado na Integracao e ³
//³realiza sua validacao.                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DO CASE
	CASE cAPS == "PREACTOR"
		lTipoAPS := SuperGetMv("MV_APS",.F.,"") == "PREACTOR"

		If lTipoAPS .And. lCampos = Nil
			lCampos := .F.
			aCampos := {	"B1_ATRIB1"  , "B1_ATRIB2"  , "B1_ATRIB3"  , "B1_CORPRI"  , "B1_CORSEC"  , "B1_NICONE"  , "B1_REGSEQ"  , "G2_CHAVMON" , "G2_FOLMIN"  , "G2_GRSETUP" , "G2_GRUPREC" , "G2_HOROTIM" ,  ;
					"G2_MAXINCR" , "G2_NIVMONT" , "G2_TMAXPRO" , "G2_TPINTER" , "G6_CAB"     , "G6_DESC"    , "G6_DESREC"  , "G6_GRUPO"   , "G6_RECURSO" , "G7_FERRAM"  , "G7_OPERAC"  , "G7_PRODUTO" ,  ;
					"G7_QUANT"   , "G7_RECURSO" , "G7_ROTEIRO" , "G7_USO"     , "G8_CRITERIO", "G8_RECURSO" , "G8_TIPO"    , "G9_CODIGO"  , "G9_DESCR"   , "GB_DATA"    , "GB_EFICIEN" , "GB_FERRAM"  ,  ;
					"GB_GRUPREC" , "GB_HORAFIM" , "GB_HORAINI" , "GB_QUANT"   , "GB_RECURSO" , "GB_SEMANA"  , "GB_TODOS"   , "GC_AUTO"    , "GC_FERRAM"  , "GC_FIXAR"   , "GC_LIBERAR" , "GC_LIMTEM"  ,  ;
					"GC_OPERAC"  , "GC_PRODUTO" , "GC_QUANRES" , "GC_RECURSO" , "GC_ROTEIRO" , "GC_SETUP"   , "GC_TEMPO"   , "GC_USOREST" , "GD_DE"      , "GD_PARA"    , "GD_RECURSO" , "GD_TEMPO"   ,  ;
					"GE_ATRIB"   , "GE_DESCR"   , "GE_RANKING" , "H1_CONF"    , "H1_INTERV"  , "H4_CGRAF"   , "H4_CORMAX"  , "H4_CORMIN"  , "H4_EFEITO"  , "H4_RESTR"   , "H4_TURNO"   }
			dbSelectArea("SX3")
			dbSetOrder(2)
			For x := 1 to Len(aCampos)
				If ! dbSeek(aCampos[x]) .Or. ! ( X3Uso(x3_usado) .And. cNivel >= x3_nivel )
					* Alert("O campo demonstrado devera ser colocado em uso quando integracao APS ativado")
					Help(" ",1,"APSCAMPO",,RetTitle(aCampos[x]) + " (" + aCampos[x] + ")",3,1)
					lCampos  := .T.
					lTipoAPS := .F.
					Exit
				EndIf
			Next
			dbSetOrder(1) // Retorno SX3 para ordem 1 (padrao)
		EndIf

		If lCampos
			* Alert("APS desativado pois existem campos necessarios que estao fora de uso")
			Help(" ",1,"APSDESATIV")
		Endif

	CASE cAPS == "DRUMMER"
		lTipoAPS := SuperGetMv("MV_APS",.F.,"") == "DRUMMER" .And. SX2->( DbSeek("SHJ") )

ENDCASE

If ! lTipoAPS .And. lHelp
	Help(" ",1,"NAOAPS")
EndIf


dbSelectArea(cAlias)
Return(lTipoAPS)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³CtRelation³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 27.09.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna Chave de Busca - para Drill Down                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ cChaveBusca                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Lancamento Padrao                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CtRelation(cPadrao,lSeek,aSTRTRAN)

Local aSaveArea	:= GetArea()	
Local c_Alias
Local c_AliasAux := "AUX"
Local c_ChvAux	 := ""
Local c_Chave
Local n_Ordem
Local cChaveBusca := ""
Local aArea
Local nX := 0

Static __lExistCTL

DEFAULT lSeek 		:= .F.				/// RETIRADO O SEEK POIS TODOS OS PROCESSOS DEVEM ESTAR POSICIONADOS NO MOMENTO DA DETPROVA.
DEFAULT aSTRTRAN	:= {}				/// CRIADO TRATAMENTO PARA CTBANFE/CTBANFS PARA UTILIZAR O ALIAS DAS QUERIES
DEFAULT __lExistCTL := ChkFile("CTL")

If	__lExistCTL
	cChaveBusca := ""//- inicia a variavel 
	dbSelectArea("CTL")
	dbSetOrder(1)
	If DbSeek(xFilial("CTL")+cPadrao)
		c_Alias 		:= CTL->CTL_ALIAS
		c_Chave 		:= Alltrim(CTL->CTL_KEY)
		For nX := 1 To Len(aStrTran)
			If c_Alias == aStrTran[nX][2] .and. c_Alias <> aStrTran[nX][1]
				c_Alias := StrTran(c_Alias,aStrTran[nX][2],aStrTran[nX][1])
				c_Chave := StrTran(c_Chave,aStrTran[nX][2]+"->",aStrTran[nX][1]+"->")
			EndIf
		Next nX
		If lSeek
			aArea 	  	:= (c_Alias)->(GetArea())
			n_Ordem   	:= Val(CTL->CTL_ORDER)

			If c_Alias	= "SN3"
				c_ChvAux  	:= (c_Alias)->(&(c_Chave))
				c_AliasAux 	+= AllTrim(c_Alias)
				If ChkFile(c_Alias,.F.,c_AliasAux)
					(c_AliasAux)->(DbGoTop())
					(c_AliasAux)->(dbSetOrder(n_Ordem))
					If (c_AliasAux)->(DbSeek(c_ChvAux))
						cChaveBusca	:= c_ChvAux
					EndIf
					if Empty(cChaveBusca) .And. (c_Alias)->(DbSeek(c_ChvAux))
						cChaveBusca	:= c_ChvAux
					EndIf
					(c_AliasAux)->(dbCloseArea())
				EndIf
			Else
				If ChkFile(c_Alias)
					(c_Alias)->(dbSetOrder(n_Ordem))
					If (c_Alias)->(DbSeek(&(c_Chave)))
						cChaveBusca	:= (c_Alias)->(&(c_Chave))
					EndIf
				EndIf
			Endif
			(c_Alias)->(RestArea(aArea))

		Else
			cChaveBusca 	:= (c_Alias)->(&(c_Chave))
		Endif
	EndIf
EndIf
RestArea(aSaveArea)
aSize(aSaveArea,0)
aSaveArea := nil

Return cChaveBusca

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LerStr   ³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 29/11/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ler uma String de arquivo de importacao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LerStr()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SigaCon Advanced.                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LerStr(x,y)
Return Substr(xBuffer,x,y)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LerVal   ³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 29/11/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ler um Valor de arquivo de importacao                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LerVal()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SigaCon Advanced.                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LerVal(x,y)
Return Val(LerStr(x,y))/100

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ LerData  ³ Autor ³ Wagner                ³ Data ³ 03/09/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ler um Data de arquivo de importacao                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ LerData(Posicao,Tamanho)	                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SigaCon Advanced.                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LerData(x,y)
Local cData
Local dDatBasOld := dDataBase  //salva database atual

If y == 6
	cData := Substr(xBuffer,x,2)+"/"+Substr(xBuffer,x+2,2)+"/"+Substr(xBuffer,x+4,2)
Elseif y == 8
	cData := Substr(xBuffer,x,2)+"/"+Substr(xBuffer,x+2,2)+"/"+Substr(xBuffer,x+4,4)
Endif
dDataBase := CtoD(cData,"ddmmyy")

If Empty(dDataBase) //se apos leitura data estiver em branco recupera database salva
	dDataBase := dDatBasOld  //pois nao pode contabilizar com data em branco
EndIf

Return " "


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³PutSx1    ³ Autor ³Wagner                 ³ Data ³ 14/02/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria uma pergunta usando rotina padrao                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function PutSx1(cGrupo,cOrdem,cPergunt,cPerSpa,cPerEng,cVar,;
	cTipo ,nTamanho,nDecimal,nPresel,cGSC,cValid,;
	cF3, cGrpSxg,cPyme,;
	cVar01,cDef01,cDefSpa1,cDefEng1,cCnt01,;
	cDef02,cDefSpa2,cDefEng2,;
	cDef03,cDefSpa3,cDefEng3,;
	cDef04,cDefSpa4,cDefEng4,;
	cDef05,cDefSpa5,cDefEng5,;
	aHelpPor,aHelpEng,aHelpSpa,cHelp)

LOCAL aArea := GetArea()
Local cKey
Local lPort := .f.
Local lSpa  := .f.
Local lIngl := .f.

If GetVersao(.F.) < "12"

	cKey  := "P." + AllTrim( cGrupo ) + AllTrim( cOrdem ) + "."

	cPyme    := Iif( cPyme 		== Nil, " ", cPyme		)
	cF3      := Iif( cF3 		== NIl, " ", cF3		)
	cGrpSxg  := Iif( cGrpSxg	== Nil, " ", cGrpSxg	)
	cCnt01   := Iif( cCnt01		== Nil, "" , cCnt01 	)
	cHelp	 := Iif( cHelp		== Nil, "" , cHelp		)

	dbSelectArea( "SX1" )
	dbSetOrder( 1 )

	// Ajusta o tamanho do grupo. Ajuste emergencial para validação dos fontes.
	// RFC - 15/03/2007
	cGrupo := PadR( cGrupo , Len( SX1->X1_GRUPO ) , " " )

	If !( DbSeek( cGrupo + cOrdem ))

	    cPergunt:= If(! "?" $ cPergunt .And. ! Empty(cPergunt),Alltrim(cPergunt)+" ?",cPergunt)
		cPerSpa	:= If(! "?" $ cPerSpa  .And. ! Empty(cPerSpa) ,Alltrim(cPerSpa) +" ?",cPerSpa)
		cPerEng	:= If(! "?" $ cPerEng  .And. ! Empty(cPerEng) ,Alltrim(cPerEng) +" ?",cPerEng)

		Reclock( "SX1" , .T. )

		Replace X1_GRUPO   With cGrupo
		Replace X1_ORDEM   With cOrdem
		Replace X1_PERGUNT With cPergunt
		Replace X1_PERSPA  With cPerSpa
		Replace X1_PERENG  With cPerEng
		Replace X1_VARIAVL With cVar
		Replace X1_TIPO    With cTipo
		Replace X1_TAMANHO With nTamanho
		Replace X1_DECIMAL With nDecimal
		Replace X1_PRESEL  With nPresel
		Replace X1_GSC     With cGSC
		Replace X1_VALID   With cValid

		Replace X1_VAR01   With cVar01

		Replace X1_F3      With cF3
		Replace X1_GRPSXG  With cGrpSxg

		If Fieldpos("X1_PYME") > 0
			If cPyme != Nil
				Replace X1_PYME With cPyme
			Endif
		Endif

		Replace X1_CNT01   With cCnt01
		If cGSC == "C"			// Mult Escolha
			Replace X1_DEF01   With cDef01
			Replace X1_DEFSPA1 With cDefSpa1
			Replace X1_DEFENG1 With cDefEng1

			Replace X1_DEF02   With cDef02
			Replace X1_DEFSPA2 With cDefSpa2
			Replace X1_DEFENG2 With cDefEng2

			Replace X1_DEF03   With cDef03
			Replace X1_DEFSPA3 With cDefSpa3
			Replace X1_DEFENG3 With cDefEng3

			Replace X1_DEF04   With cDef04
			Replace X1_DEFSPA4 With cDefSpa4
			Replace X1_DEFENG4 With cDefEng4

			Replace X1_DEF05   With cDef05
			Replace X1_DEFSPA5 With cDefSpa5
			Replace X1_DEFENG5 With cDefEng5
		Endif

		Replace X1_HELP  With cHelp

		PutSX1Help(cKey,aHelpPor,aHelpEng,aHelpSpa)

		MsUnlock()
	Else

	   lPort := ! "?" $ X1_PERGUNT .And. ! Empty(SX1->X1_PERGUNT)
	   lSpa  := ! "?" $ X1_PERSPA  .And. ! Empty(SX1->X1_PERSPA)
	   lIngl := ! "?" $ X1_PERENG  .And. ! Empty(SX1->X1_PERENG)

	   If lPort .Or. lSpa .Or. lIngl
			RecLock("SX1",.F.)
			If lPort
	         SX1->X1_PERGUNT:= Alltrim(SX1->X1_PERGUNT)+" ?"
			EndIf
			If lSpa
				SX1->X1_PERSPA := Alltrim(SX1->X1_PERSPA) +" ?"
			EndIf
			If lIngl
				SX1->X1_PERENG := Alltrim(SX1->X1_PERENG) +" ?"
			EndIf
			SX1->(MsUnLock())
		EndIf
	Endif

	RestArea( aArea )
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GDCheckKey³ Autor ³ Alex Egydio / Sergio  ³ Data ³05/05/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a existencia de linhas duplicadas na getdados       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := GDCheckKey( ExpA1, ExpN1 )                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 -> Vetor com campos para compor a chave de pesquisa  ³±±
±±³          ³ ExpN2 -> Indica o modelo de validacao:                     ³±±
±±³          ³   1 - Apenas valida                                        ³±±
±±³          ³   2 - Valida e exibe mensagem de erro                      ³±±
±±³          ³   3 - Valida e exibe mensagem de erro informando os campos ³±±
±±³          ³   4 - Valida e exibe mensagem de erro informando os campos ³±±
±±³          ³       e as linhas com duplicidade                          ³±±
±±³          ³ ExpA3 -> Vetor com campos de preenchimento obrigatorio     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Retorno  ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marinaldo   ³27/11/03³Melhor³Permitir a Verificacao de Campos de  Preen³±±
±±³            ³        ³      ³cimento obrigatorio                       ³±±
±±³Marinaldo   ³07/04/04³Melhor³Retornar Aviso Por Referencia e verificar ³±±
±±³            ³        ³      ³se Deve Mostrar Aviso                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±*/
Function GDCheckKey( aCpo , nModelo , aNoEmpty , cMsgAviso , lShowAviso )

Local aAux      	:= {}
Local aLinhas   	:= {}
Local aChkNoEmpty	:= {}
Local aEmptys		:= {}

Local cString   	:= ""
Local cEmptyString	:= ""
Local cCrLf     	:= Chr(13) + Chr(10)

Local lRet		 	:= .T.
Local lDuplic		:= .F.
Local lEmpty		:= .F.

Local nLoop     	:= 0
Local nLoop2    	:= 0
Local nPosCampo 	:= 0
Local nPosAtu   	:= 0

DEFAULT nModelo 	:= 1
DEFAULT aNoEmpty	:= {}
DEFAULT lShowAviso	:= .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array auxiliar com os campos a serem validados                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nLoop := 1 To Len( aCpo )
	IF ( ( nPosCampo := GDFieldPos( aCpo[ nLoop ] ) ) == 0 )
		Loop
	EndIF
	AAdd( aAux, { aCpo[ nLoop ] , nPosCampo , .F. } )
Next nLoop

For nLoop := 1 To Len( aNoEmpty )
	IF ( ( nPosCampo := GDFieldPos( aNoEmpty[ nLoop ] ) ) == 0 )
		Loop
	EndIF
	aAdd( aChkNoEmpty , { aNoEmpty[ nLoop ] , nPosCampo, .F. } )
Next nLoop

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordena por posicao no acols                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ASort( aAux, , , { |x,y| y[2] > x[2] } )
aSort( aChkNoEmpty , , , { |x,y| y[2] > x[2] } )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percorre o acols para verificar as linhas duplicadas                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

For nLoop := 1 To Len( aCols )

	If !GDDeleted( nLoop )

		IF ( n <> nLoop )

			For nLoop2 := 1 To Len( aAux )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Marca no array caso o campo esteja duplicado 		                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nPosAtu := aAux[ nLoop2, 2 ]
				aAux[ nLoop2, 3 ] := ( aCols[ nLoop, nPosAtu ] == aCols[ N , nPosAtu ] )

			Next nLoop2

		EndIF

		For nLoop2 := 1 To Len( aChkNoEmpty )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Marca no array caso o campo esteja Vazio	 		                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nPosAtu := aChkNoEmpty[ nLoop2 , 2 ]
			aChkNoEmpty[ nLoop2 , 3 ] := Empty( aCols[ nLoop , nPosAtu ] )

		Next nLoop2

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pesquisa algum campo que nao esteja duplicado						   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lDuplic := ( ( n <> nLoop ) .and. Empty( AScan( aAux, { |x| !x[3] } ) ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pesquisa algum campo que esteja vazio                 				   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lEmpty	:= !Empty( aScan( aChkNoEmpty , { |x| x[3] } ) )

		If ( ( lDuplic ) .or. ( lEmpty ) )

			lRet := .F.
			If nModelo == 4
				IF ( lDuplic )
					AAdd( aLinhas , nLoop )
				EndIF
				IF ( lEmpty )
					aAdd( aEmptys , nLoop )
				EndIF
			Else
				Exit
			EndIf

		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Marca todos os campos como nao duplicados novamente                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF ( lDuplic )
			AEval( aAux, { |x| x[3] := .F. } )
		EndIF

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Marca todos os campos como nao vazios novamente                    	   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF ( lEmpty )
			aEval( aChkNoEmpty , { |x| x[3] := .F. } )
		EndIF

	EndIf

Next nLoop

If !lRet .And. nModelo <> 1

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a mensagem conforme o modelo                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF ( lDuplic := !Empty( aLinhas ) )
		cString := STR0166	// "A linha atual possui uma chave duplicada no browse."
	EndIF

	IF ( lEmpty := !Empty( aEmptys ) )
		cEmptyString := STR0229	//"A linha atual possui campo de Preenchimento Obrigat¢rio."
	EndIF

	If nModelo == 3 .Or. nModelo == 4

		IF ( lDuplic )

			cString += cCrLf + STR0167 //  "Campo(s):  "

			For nLoop := 1 To Len( aAux )
				cString += aHeader[ aAux[nLoop,2], 1 ] + ", "
			Next nLoop

			cString := Left( cString, Len( cString ) - 2 ) + "."

		EndIF

		IF ( lEmpty )

			cEmptyString += cCrLf + STR0167 //  "Campo(s):  "

			For nLoop := 1 To Len( aChkNoEmpty )
				cEmptyString += aHeader[ aChkNoEmpty[ nLoop , 2 ] , 1 ] + ", "
			Next nLoop

			cEmptyString := Left( cEmptyString , Len( cEmptyString ) - 2 ) + "."

		EndIF

		If nModelo == 4

			IF ( lDuplic )

				cString += cCrLf + STR0168 // "Linha(s):  "

				For nLoop := 1 to Len( aLinhas )
					cString += AllTrim( Str( aLinhas[ nLoop ] ) ) + ", "
				Next nLoop

				cString := Left( cString, Len( cString ) - 2 ) + "."

			EndIF

			IF ( lEmpty )

				cEmptyString += cCrLf + STR0168 // "Linha(s):  "

				For nLoop := 1 To Len( aEmptys )
					cEmptyString += AllTrim( Str( aEmptys[ nLoop ] ) ) + ", "
				Next nLoop

				cEmptyString := Left( cEmptyString, Len( cEmptyString ) - 2 ) + "."

			EndIF

		EndIf

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Exibe a mensagem                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF ( ( lDuplic ) .and. ( lEmpty ) )
		cMsgAviso := cString
		cMsgAviso += cCrLf
		cMsgAviso += cCrLf
		cMsgAviso += cEmptyString
	ElseIF ( lDuplic )
		cMsgAviso := cString
	ElseIF ( lEmpty )
		cMsgAviso := cEmptyString
	EndIF
	IF ( lShowAviso )
		Aviso( STR0164, OemToAnsi( cMsgAviso ) , { "&"+STR0124 }, 2 ) // "Atencao!" , "Ok"
	EndIF

EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetSAETiposºAutor  ³Julio Cesar         º Data ³ 12/06/02   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna os tipos de Adm. existentes no arquivo SAE          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nenhum                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGAFIN                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GetSAETipos()

Local aArea	:=	GetArea()
Local cRet	:=	Space(0)

DbSelectArea("SAE")
DbSetOrder(1)
DbSeek(xFilial())
While !EOF() .And. xFilial() == AE_FILIAL
	If !(AE_TIPO $ cRet)
		cRet +=	AE_TIPO + "|"
	Endif
	dbSkip()
Enddo

cRet	:=	IIf(!Empty(cRet),Substr(cRet,1,Len(cRet)-1),cRet)
RestArea(aArea)

Return(cRet)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³EICATUSB2 ³ Rev.  ³ Rodrigo de A. Sartorio³ Data ³13.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a gravacao do Arquivo SB2 nas operacoes da PRE-Nota ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : 1 - Inclusao                                       ³±±
±±³          ³         2 - Exclusao                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA140                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION EicAtuSB2(nOpc)
Local aArea:=GetArea()
Local aAreaSB1:=SB1->(GetArea())
Local aAreaSB2:=SB2->(GetArea())

// Posiciona SB1 caso necessario
If SB1->B1_FILIAL+SB1->B1_COD # xFilial("SB1")+SWN->WN_PRODUTO
	SB1->(DbSeek(xFilial("SB1")+SWN->WN_PRODUTO))
EndIf
dbSelectArea("SB2")
If DbSeek(xFilial()+SWN->WN_PRODUTO+RetFldProd(SB1->B1_COD,"B1_LOCPAD"))
	RecLock("SB2",.F.)
Else
	CriaSB2(SWN->WN_PRODUTO,RetFldProd(SB1->B1_COD,"B1_LOCPAD"))
EndIf
If nOpc == 1
	SB2->B2_NAOCLAS := SB2->B2_NAOCLAS + SWN->WN_QUANT
Else
	SB2->B2_NAOCLAS := SB2->B2_NAOCLAS - SWN->WN_QUANT
EndIf
SB2->(MsUnlock())
RestArea(aAreaSB1)
RestArea(aAreaSB2)
RestArea(aArea)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GdSeek    ³ Autor ³ Henry Fila  1a. Ver   ³ Data ³17/07/2002 ³±±
±±³          ³          ³ Autor ³ Marinaldo   2a. Ver   ³ Data ³09/01/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Seek em uma determinada getdados                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1 : Objeto corrente da getdados                          ³±±
±±³          ³ExpC2 : Texto da janela                                      ³±±
±±³          ³ExpA3 : aHeader      ( Opcional )                            ³±±
±±³          ³ExpA4 : aCols        ( Opcional )                            ³±±
±±³          ³ExpL1 : Ordena aCols ( Opcional )                            ³±±
±±³          ³ExpL2 : ModelGrid    ( Opcional )              			   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo abrir uma janela de pesquisa   ³±±
±±³          ³em browses de getdados poisicionando na llinha caso encontre ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Function GdSeek( oGetDados , cTitulo , aHeaderPar , aColsPar , lOrdena , oMdlGrid )

Local aSvKeys		:= GetKeys()
Local aArea			:= GetArea()
Local aAreaSX3		:= SX3->( GetArea() )
Local aCpoBusca 	:= {}
Local aClassData	:= {}
Local aComboBox		:= { AllTrim( STR0232 ) , AllTrim( STR0233 ) , AllTrim( STR0234 ) } //"Exata"###"Parcial"###"Contem"

Local bAscan		:= { || .F. }
Local bGdChgPict
Local bSet15
Local bSet24
Local bDialogInit

Local cPesq
Local cBusca
Local cAscan
Local cOpcAsc		:= aComboBox[2]	//"Parcial"

Local nOpca			:= 0
Local nPos     		:= 0
Local nTipo    		:= 1
Local nBegin
Local nEnd
Local nLoop
Local nLoops
Local nX := 0

Local lTpBusca 		:= .T.
Local lAcima		:= .F.
Local lNewGd   		:= .F.
Local lGridMvc		:= IIF(((ValType(oMdlGrid) == "O" .AND. ValType(oGetDados) == "U") .AND. oMdlGrid:ClassName() == "FWFORMGRID"),.T.,.F.)

Local oDlg
Local oPesq
Local oBusca
Local oTpBusca
Local oComboBox
Local oAcima

Local aAdvSize		:= {}
Local aInfoAdvSize	:= {}
Local aObjSize		:= {}
Local aObjCoords	:= {}

DEFAULT cTitulo := OemtoAnsi(STR0181) //"Pesquisa"
DEFAULT lOrdena := .F.

Begin Sequence


    IF !(lGridMvc)
		aClassData := ClassDataArr( oGetDados )
		IF ( ( nPos := aScan( aClassData , { |eData| ( Upper( AllTrim( eData[1] ) ) == "CCLASSNAME" ) } ) ) > 0 )
			lNewGd := ( aClassData[ nPos , 2 ] == "MSNEWGETDADOS" )
		EndIF

		IF ( ( ValType( aHeaderPar ) == "A" ) .or. ( lNewGd ) )
			Private aHeader
			IF ( ValType( aHeaderPar ) == "A" )
				aHeader	:= aHeaderPar
			ElseIF ( lNewGd )
				aHeader	:= oGetDados:aHeader
			EndIF
		EndIF

		IF ( ( ValType( aColsPar ) == "A" ) .or. ( lNewGd ) )
			Private aCols
			IF ( ValType( aColsPar ) == "A" )
				aCols	:= aColsPar
			ElseIF ( lNewGd )
				aCols	:= oGetDados:aCols
			EndIF
		EndIF

		IF ( ( Type( "n" ) <> "N" ) .or. ( lNewGd ) )
			Private n
			IF ( Type( "n" ) <> "N" )
				IF ( lNewGd )
					n := oGetDados:oBrowse:nAt
				Else
					n := oGetDados:nAt
				EndIF
			ElseIF ( lNewGd )
				n := oGetDados:oBrowse:nAt
			EndIF
		EndIF
    Else
        aCols   := oMdlGrid:aCols
        aData   := oMdlGrid:aDataModel
        aHeader	:= oMdlGrid:aHeader
        n       := oMdlGrid:GetLine()

		For nX := 1 To Len(aHeader)
			aHeader[nX][3] := GetSx3Cache(aHeader[nX][2], "X3_PICTURE")
			aHeader[nX][6] := GetSx3Cache(aHeader[nX][2], "X3_VALID")
			aHeader[nX][7] := GetSx3Cache(aHeader[nX][2], "X3_USADO")
			aHeader[nX][9] := GetSx3Cache(aHeader[nX][2], "X3_ARQUIVO")
		Next nX
	EndIf

	IF Empty( aHeader )
		Break
	EndIF

	nLoops := Len( aHeader )
	For nLoop := 1 TO nLoops
		AAdd( aCpoBusca , aHeader[nLoop][1] )
	Next nLoop

	aAdvSize			:= MsAdvSize(.T. , .T. , 380)
	aInfoAdvSize	:= { aAdvSize[1] , aAdvSize[2] , aAdvSize[3] , aAdvSize[4] , 1 , 1 }
	aAdd( aObjCoords , { 000 , 000 , .T. , .T.  } )
	aObjSize		:= MsObjSize( aInfoAdvSize , aObjCoords )

	cBusca	:= aCpoBusca[1]
	bGdChgPict := { || GdChgPict( @oDlg , nTipo , @oPesq , @cPesq , @oComboBox , aObjSize ) }
	DEFINE MSDIALOG oDlg TITLE OemtoAnsi(cTitulo)  FROM aAdvSize[7],0 TO aAdvSize[6]-200,aAdvSize[5] OF oMainWnd PIXEL

		@ aObjSize[1][1]+5,aObjSize[1][2]+5 MSCOMBOBOX oBusca VAR cBusca ITEMS aCpoBusca SIZE 256, 36 OF oDlg PIXEL ON CHANGE ( nTipo := oBusca:nAt , Eval( bGdChgPict ) )

		@ aObjSize[1][1]+40,aObjSize[1][2]+5 SAY OemToAnsi( STR0235 ) + AllTrim( Str( n ) ) SIZE 050,10 OF oDlg PIXEL

		@ aObjSize[1][1]+40,aObjSize[1][2]+55 CHECKBOX oTpBusca Var lTpBusca Prompt OemtoAnsi( STR0201 )	SIZE 150,10 OF oDlg PIXEL

		@ aObjSize[1][1]+40,aObjSize[1][2]+171 CHECKBOX oAcima Var lAcima Prompt OemtoAnsi( STR0236 )		SIZE 050,10 OF oDlg PIXEL WHEN ( IF( !( lTpBusca ) ,lAcima := .F. , lAcima ) , ( lTpBusca ) .and. ( n > 1 ) )

		@ aObjSize[1][1]+40,aObjSize[1][2]+210 MSCOMBOBOX oComboBox VAR cOpcAsc ITEMS aComboBox SIZE 050,10 OF oDlg PIXEL

		bSet15 := { || nOpca := 1 , RestKeys( aSvKeys , .T. ) , oDlg:End() }
		oDlg:bSet15 := bSet15
		bSet24 := { || nOpca := 0 , RestKeys( aSvKeys , .T. ) , oDlg:End() }
		oDlg:bSet24 := bSet24

		bDialogInit := { || EnchoiceBar( oDlg , bSet15 , bSet24 ) ,;
							SetKey( 15 , bSet15 ) , SetKey( 24 , bSet24 ) , Eval( bGdChgPict ) }

	ACTIVATE MSDIALOG oDlg CENTERED ON INIT Eval( bDialogInit )

	IF ( nOpca == 1 )

		IF ( lTpBusca )
			IF ( lAcima )
				nBegin	:= 1
				nEnd	:= Max( n - 1 , 1 )
			Else
				nBegin	:= Min( n , Len( aCols ) )
				nEnd	:= Len( aCols )
			EndIF
        Else
			nBegin	:= 1
			nEnd	:= Len( aCols )
		EndIF

		//-- Ordena aCols
		If lOrdena
			aSort( aCols , , , { |x,y| x[nTipo] < y[nTipo] } )
			If lGridMvc
				aSort( aData , , , { |x,y| x[1][1][nTipo] < y[1][1][nTipo] } )
				aSort( oMdlGrid:aCols , , , { |x,y| x[nTipo] < y[nTipo] } )
			EndIf
		EndIf

		Do Case

			Case aHeader[nTipo][8] $ "CM"
				IF ( cOpcAsc == aComboBox[1] )	//Exata
					cAscan := Padr( Upper( cPesq ) , TamSx3(aHeader[nTipo][2])[1] )
					bAscan := { |x| cAscan == Upper( x[ nTipo ] ) }
				ElseIF ( cOpcAsc == aComboBox[2] )	//Parcial
					cAscan := Upper( AllTrim( cPesq ) )
					bAscan := { |x| cAscan == Upper( SubStr( Alltrim( x[nTipo] ) , 1 , Len( cAscan ) ) ) }
				ElseIF ( cOpcAsc == aComboBox[3] )	//Contem
					cAscan := Upper( AllTrim( cPesq ) )
					bAscan := { |x| cAscan $ Upper( Alltrim( x[nTipo] ) ) }
				EndIF
			Case aHeader[nTipo][8] == "N"
				bAscan	:= {|x| cPesq == x[nTipo]}
			Case aHeader[nTipo][8] == "D"
				bAscan	:= {|x| Dtos(cPesq) == Dtos(x[nTipo])}
			OtherWise
				Break
		EndCase

		nPos := Ascan( aCols , bAscan , nBegin , nEnd )

		IF ( nPos > 0 )

			If !(lGridMvc)
				If lOrdena .and. lNewGd
					oGetDados:aCols := aCols
				EndIf
				If ( oGetDados:LinhaOk() )
					oGetDados:Goto( nPos )
					oGetDados:oBrowse:Refresh()
					oGetDados:oBrowse:SetFocus()
					If ( ValType( oGetDados:oBrowse:bChange ) == "B" )
						Eval( oGetDados:oBrowse:bChange )
					EndIF
				EndIf
			Else
				If oMdlGrid:VldLineData()
					oMdlGrid:GoLine(nPos)
				EndIf
			EndIf
		Else
			Help(" ",1,"REGNOIS")
		EndIF

	EndIF

End Sequence

RestArea( aAreaSX3 )
RestArea( aArea )
RestKeys( aSvKeys , .T. )

Return( NIL )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GdChgPict ³ Autor ³ Henry Fila  1a. Ver   ³ Data ³17/07/2002 ³±±
±±³          ³          ³ Autor ³ Marinaldo   2a. Ver   ³ Data ³09/01/2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Atualiza picture na funcao GdSeek                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³oDlg  	-> Dialog                                          ³±±
±±³          ³nTipo 	-> Indice em Header                                ³±±
±±³          ³oPesq 	-> Objeto da Pesquisa                              ³±±
±±³          ³cPesq		-> Variavel da Pesquisa                            ³±±
±±³          ³oComboBox -> Objeto Combo 								   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³lRet                                                         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo tratar a picture do campo sele ³±±
±±³          ³cionado na funcao GdSeek                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³GdSeek													   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß/*/
Static Function GdChgPict(oDlg,nTipo,oPesq,cPesq,oComboBox,aObjSize)

Local aCombo
Local aX3cBox
Local cF3
Local cBox
Local cPicture
Local cAlias
Local cSxbRet
Local lGdChgPict
Local nLoop
Local nLoops
Local nAt

Begin Sequence

	IF ( ValType( oPesq ) == "O" )
		oPesq:Disable()
		oPesq:Hide()
		oPesq:End()
		oPesq := NIL
		oDlg:Refresh()
	EndIF

	oComboBox:Disable()
	oComboBox:Hide()

	IF !( lGdChgPict := ( nTipo > 0 ) )
		Break
	EndIF

	cF3 := GetSx3Cache( aHeader[nTipo][2] , "X3_F3" )
	IF !( lGdChgPict := ( cF3 <> NIL ) )
		Break
	EndIF

	IF ( Empty( cF3 ) )
		nAt := At( "_" , aHeader[nTipo][2] )
		IF ( Len( SubStr( aHeader[nTipo][2] , 1 , nAt ) ) == 3 )
			cAlias := "S" + SubStr( aHeader[nTipo][2] , 1 , nAt -1 )
		Else
			cAlias := SubStr( aHeader[nTipo][2] , 1 , nAt -1 )
		EndIF
		cBox := AllTrim( fDesc( "SX3" , aHeader[nTipo][2] , "X3cBox()" , Len( SX3->X3_CBOX ) , "" , 2 , .F. ) )
		IF ( !Empty( cBox ) )
			aX3cBox	:= RetSx3Box( cBox , NIL , NIL , GetSx3Cache( aHeader[nTipo][2] , "X3_TAMANHO" ) )
			nLoops	:= Len( aX3cBox )
			aCombo	:= Array( nLoops )
			For nLoop := 1 To nLoops
				aCombo[ nLoop ] := aX3cBox[ nLoop , 1 ]
			Next nLoop
		ElseIF ( SXB->( dbSeek( cAlias , .F. ) ) )
			cAlias := Upper( AllTrim( cAlias ) )
			While SXB->( !Eof() .and. Upper( SubStr( AllTrim( XB_ALIAS ) , 1 , 3 ) ) == cAlias )
				IF ( SXB->XB_TIPO == "5" )
					cSxbRet := Upper( AllTrim( SXB->XB_CONTEM ) )
					Exit
				EndIF
				SXB->( dbSkip() )
			End While
			IF ( Upper( AllTrim( aHeader[nTipo][2] ) ) $ cSxbRet )
				cF3 := cAlias
			EndIF
		ElseIF ( "_FILIAL" $ Upper( AllTrim( aHeader[nTipo][2] ) ) )
			IF ( SXB->( dbSeek( "XM0" , .F. ) ) )
				cF3 := "XM0"
			EndIF
		EndIF
	EndIF

	IF ( aHeader[nTipo][8] <> "M" )
		cPesq	:= CriaVar( aHeader[nTipo][2] , .F. )
	Else
		cPesq	:= Space( 80 )
	EndIF
	cPicture := aHeader[nTipo][3]

	IF !Empty( cBox )
		@ aObjSize[1][1]+22,aObjSize[1][2]+5 COMBOBOX oPesq VAR cPesq ITEMS aCombo SIZE 256, 10 OF oDlg PIXEL
	Else
		@ aObjSize[1][1]+22,aObjSize[1][2]+5 MSGET oPesq VAR cPesq SIZE 256, 10 PICTURE cPicture OF oDlg PIXEL HASBUTTON
		IF !Empty( cF3 )
			oPesq:cF3 := cF3
		EndIF
	EndIF

	IF ( aHeader[nTipo][8] $ "CM" )
		oComboBox:Show()
		oComboBox:Enable()
	EndIF

	oDlg:Refresh()

End Sequence

Return( lGdChgPict )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GdNoEmpty		 ³Autor³Marinaldo de Jesus³ Data ³02/12/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Verificar se Determinado Campo da GetDados esta Vazio		³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Generico 													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function GdNoEmpty( aCpos , nLine , aHeaderPastPar , aColsPastPar , lReadVar , cMsg , lShowMsg )

Local cCpo		:= ""
Local lNoEmpty	:= .T.
Local nCpo		:= 0
Local nCpos		:= 0

DEFAULT aCpos			:= {}
DEFAULT nLine			:= n
DEFAULT aHeaderPastPar	:= aHeader
DEFAULT aColsPastPar	:= aCols
DEFAULT lReadVar		:= .F.
DEFAULT lShowMsg		:= .T.

Begin Sequence

	IF !( GdDeleted( nLine , aHeaderPastPar , aColsPastPar ) )
		nCpos := Len( aCpos )
		For nCpo := 1 To nCpos
			cCpo := aCpos[ nCpo ]
			IF ( GdFieldPos( cCpo , aHeaderPastPar ) == 0 )
				Loop
			EndIF
			IF !( lNoEmpty := !Empty( GdFieldGet( cCpo , nLine , lReadVar , aHeaderPastPar , aColsPastPar ) ) )
				cMsg := ( STR0186 + " " )	// 'O Campo:'
			   	cMsg += ( aHeaderPastPar[ GdFieldPos( cCpo , aHeaderPastPar ) , 01 ] + " " )
			   	cMsg += STR0187 //' de Preenchimento obrigat¢rio'
				IF ( lShowMsg )
					MsgInfo( OemToAnsi( cMsg ) , OemToAnsi( STR0185 ) )//'Aviso de Inconsitˆncia!'
				EndIF
				Break
			EndIF
		Next nCpo
	EndIF

End Sequence

Return( lNoEmpty )

/*
ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿
³Fun‡…o    ³GdPosDeleted	 ³Autor³Marinaldo de Jesus³ Data ³04/12/2002³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´
³Descri‡…o ³Retorna a Posicao Correspondente ao Elemento Delecao da Gd	³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Sintaxe   ³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³Parametros³<Vide Parametros Formais>									³
ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
³ Uso      ³Generico 													³
ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ*/
Function GdPosDeleted( aHeaderPastPar )

aHeaderPastPar := IF( ( ValType( aHeaderPastPar ) == "A" ) .and. !Empty( aHeaderPastPar ) , aHeaderPastPar , aHeader )

Return( ( Len( aHeaderPastPar ) + 1 ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍ»±±
±±º Programa   º GDFILL  A  º Autor º        Nava        º Data º 18/10/01 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍ¹±±
±±º   Preenche aHeader e Acols da GetDados retornando Array de Gravacao    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Sintaxe    º GDFill         ( cAlias, nOrder, cSeekKey, bSeekWhile, ;  º±±
±±º            º                  bSeekFor, aNoFields, aYesFields,lOnlyYes)º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Parametros º                                                           º±±
±±º         01 º nOpcx      - Opcao PADRAO do Mbrowse                      º±±
±±º         02 º cAlias     - Alias                                        º±±
±±º         03 º nOrder     - Ordem                                        º±±
±±º         04 º cSeekKey   - Chave de Seek para montar aCols              º±±
±±º         05 º bSeekWhile - Condicao While                               º±±
±±º         06 º bSeekFor   - Condicao For                                 º±±
±±º         07 º aNoFields  - Campos a serem excluidos                     º±±
±±º         08 º aYesFields - Campos a serem incluidos                     º±±
±±º         09 º lOnlyYes   - Flag indicando se considera somente os camposº±±
±±º            º declarados no aYesFields + campos do usuario              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Retorno    º aColsRecno - Contem os Recno()'s que serao gravados       º±±
±±ºÍÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso        º Generico                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Comentario º Supoe aHeader e Acols declarados como Private PELO progr. º±±
±±º            º e inicializadas como um array vazio.                      º±±
±±º            º O array do retorno contem todos os 'recno()' do arquivo   º±±
±±º            º que foi alterado/incluido. Portanto BASTA chamar a funcao º±±
±±º            º TMSRecGetD com este mesmo Array.                          º±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION GDFill( nOpcx, cAlias, nOrder, cSeekKey, bSeekWhile, bSeekFor, aNoFields, aYesFields, lOnlyYes )
LOCAL aArea 		:= GetArea()
LOCAL aAreaAlias  := ( cAlias )->( GetArea() )
LOCAL lNoFields	:= ( aNoFields <> NIL )
LOCAL lYesFields	:= ( aYesFields <> NIL )
LOCAL aColsRecno  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ aHeader e Acols devem vir definidos OBRIGATORIAMENTE ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL nUsado      := Len( aHeader )

DEFAULT nOrder	  := 1
DEFAULT bSeekFor  := { || .T. }
DEFAULT lOnlyYes  := .F.

// aHeader e aCols jah VEM PREECHIDOS OU VAZIO

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array aHeader para a GetDados()                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SX3->( DbSetOrder( 1 ) )
SX3->( DbSeek( cAlias ) )

SX3->( DbEval( {||	++nUsado, ;
		 					AADD(	aHeader, {	;
												Rtrim( X3Titulo()), ;
												Rtrim( X3_CAMPO ),;
												X3_PICTURE, ;
												X3_TAMANHO, ;
												X3_DECIMAL, ;
												X3_VALID, 	;
												X3_USADO,	;
												X3_TIPO, 	;
												X3_ARQUIVO,	;
												X3_CONTEXT  ;
		                           } ) },;
					{ || 	( lYesFields .AND. Ascan( aYesFields, Rtrim( X3_CAMPO ) ) > 0 ) .OR. ;
							If(lOnlyYes,X3_PROPRI == "U" .And. X3USO(X3_USADO) .AND. cNivel >= X3_NIVEL,;
							( X3USO(X3_USADO) .AND. cNivel >= X3_NIVEL .AND. ;
							( !lNoFields .OR. Ascan( aNoFields, Rtrim( X3_CAMPO ) ) = 0 ) )) },;
					{ || 	! Eof() .AND. X3_ARQUIVO == cAlias } )	)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta o array aCols para a GetDados()                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

( cAlias )->( DbSetOrder( nOrder ) )
( cAlias )->( DbSeek( cSeekKey ) )

( cAlias )->( DbEval( {||	Aadd( aColsRecno, Recno() ), ;
									Aadd( aCols, Array( nUsado + 1 ) ), ;
									Aeval( aHeader, {|aCampo, nI|	aCols[Len(aCols),nI] := IF( aCampo[10] != 'V'.AND.nOpcx<>3,;
																				FieldGet(FieldPos(aHeader[nI,2])), ;
																				CriaVar(aHeader[nI,2],.T.) )  } ),;
									aCols[Len(aCols),nUsado+1]:=.F. },;
									bSeekFor,;
								{||!Eof() .AND. Eval( bSeekWhile ) == cSeekKey } )	)

If ( Empty(aCols) ) // Coloca ao menos 1 em branco para a Inclusao ...
	Aadd(aCols,Array(nUsado+1))
	Aeval( aHeader, {|aCampo, nI| aCols[1][nI] := ( cAlias )->( CriaVar(aHeader[nI,2],.T.) ) } )
	aCols[Len(aCols)][nUsado+1] := .F.
EndIf

RestArea( aAreaAlias )
RestArea( aArea )

RETURN ( aColsRecno )

Static Function IsRdmake(cName)

If FunType() == 3 .or. Subs(cName,1,2) == "U_"
	Return .T.
EndIf
Return .F.

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MConnect  ³ Autor ³ Henry Fila            ³ Data ³ 03/07/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina que envia e-mail de acordo com evento pre-cadastrado³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MEnviaMail(cMensagem,cModulo,aUsers,lEvRh                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Mensagem a ser enviada                             ³±±
±±³          ³ ExpC2 = Modulo                                             ³±±
±±³          ³ ExpA3 = Usuarios                                           ³±±
±±³          ³ ExpL3 = Se gera HTML                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MConnect(cMensagem,aUsers,cSubject,lHtml,lOk)

Local cMailConta  := NIL
Local cMailServer := NIL
Local cMailSenha  := NIL
Local cMailCtaAut := NIL
Local cMailUsrAut := NIL
Local cBackMens   := ""
Local cError      := ""
Local lAutOk     := .F.
Local lSmtpAuth  := GetMv("MV_RELAUTH",,.F.)
Local cUser, nAux

If !lHtml
	cMensagem += _CRLF+OemToAnsi(STR0072)+cModulo	// "E-Mail automatico enviado pelo modulo SIGA"
EndIf
cBackMens := cMensagem
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa Formula com conteudo do texto                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(aUsers[1,1])
	cMensagem:=Formula(aUsers[1,1])
	If Valtype(cMensagem) != "C"
		cMensagem:=cBackMens
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Obtem dados necessarios a conexao                                                |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMailConta  := If(cMailConta == NIL,GETMV("MV_EMCONTA"),cMailConta)
cMailServer := If(cMailServer== NIL,GETMV("MV_RELSERV"),cMailServer)
cMailSenha  := If(cMailSenha == NIL,GETMV("MV_EMSENHA"),cMailSenha)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Se o parametro nao estiver definido pega o parametro dos relatorios              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( cMailConta )
	cMailConta := GETMV("MV_RELFROM",,"")
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Se o parametro nao estiver definido pega o parametro dos relatorios              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( cMailSenha )
	cMailSenha := GETMV("MV_RELPSW")
EndIf

cMailCtaAut := If(cMailCtaAut == NIL,GETMV("MV_RELACNT"),cMailCtaAut)
cMailUsrAut := If(cMailUsrAut == NIL,GETMV("MV_RELAUSR"),cMailUsrAut)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Caso nao exista conta definida, pega o proprio e-mail origem                     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( cMailCtaAut )
	cMailCtaAut := cMailConta
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia e-mail com os dados necessarios                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cMailServer) .And. !Empty(cMailConta) .And. !Empty(cMailSenha) .And. !Empty(cMailCtaAut)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Conecta uma vez com o servidor de e-mails                                        |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lOk
		CONNECT SMTP SERVER cMailServer ACCOUNT cMailCtaAut PASSWORD cMailSenha RESULT lOk
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Se configurado, efetua a autenticacao                                            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAutOk
		If ( lSmtpAuth )
			lAutOk := MailAuth(cMailUsrAut,cMailSenha)
			If !lAutOk
				nAux	:= At("@",cMailUsrAut)
              	cUser	:= IF(nAux>0,SUBS(cMailUsrAut,1,nAux-1),cMailUsrAut)
	       		lAutOk	:= MailAuth(cUser,cMailSenha)
	       EndIf
		Else
			lAutOk := .T.
		EndIf
	EndIf

	If lOk .And. lAutOk

		If lHtml
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Envio de e-mail HTML                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SEND MAIL FROM cMailConta to aUsers[1,2] SUBJECT cSubject BODY cMensagem RESULT lSendOk
		Else
			SEND MAIL FROM cMailConta to aUsers[1,2] SUBJECT cSubject BODY cMensagem FORMAT TEXT RESULT lSendOk
		EndIf
		If !lSendOk
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Erro no Envio do e-mail                                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			GET MAIL ERROR cError
			MsgInfo(cError,OemToAnsi(STR0070)) // "Erro no envio de e-Mail"
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Erro na conexao com o SMTP Server ou na autenticacao da conta          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		GET MAIL ERROR cError
		MsgInfo(cError,OemToAnsi(STR0070)) // "Erro no envio de e-Mail"
	EndIf
EndIf

Return


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MDisconnect³ Autor ³ Henry Fila            ³ Data ³ 03/07/00³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina que desconecta o servidor de email                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MDisconnect()                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MDisconnect()

DISCONNECT SMTP SERVER

Return

Static ;
Function IsInListGSP(aT)
Local nI := 0
Local lRet := .F.
Local aX := {6, 2, 3, 34, 7, 16, 40, 26, 1, 4, 28, 24}

For nI := 1 To Len(aX)
	If ( AScan(aT, aX[nI]) > 0 )
		lRet := .T.
        Exit
	EndIf
Next
Return(lRet)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MaBuildKey³ Autor ³ Sergio Silveira       ³ Data ³15/07/2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Monta uma string baseada em uma lista de campos              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 : Alias da entidade                                    ³±±
±±³          ³ExpA2 : Array contendo o nome dos campos                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpC3 -> Chave montada                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo tratar a picture do campo sele ³±±
±±³          ³cionado na funcao GdSeek                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MaBuildKey( cEntidade, aChave )

//Conforme instruções do Sergio Silveira
Return MPBuildKey( cEntidade, aChave )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocWizard³ Autor ³ Sergio Silveira       ³ Data ³03/03/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Wizard para inclusao de conhecimentos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Getdados                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocWizard( oGetDad )

//Conforme instruções do Sergio Silveira
Return MPDocWizard( oGetDad )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocWzV1  ³ Autor ³ Sergio Silveira       ³ Data ³03/03/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Validacao do campo objeto                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocWzV1( ExpC1 )                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Nome do objeto                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocWzV1( cObj )

//Conforme instruções do Sergio Silveira
Return MPDocWzV1( cObj )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocWzGet ³ Autor ³ Sergio Silveira       ³ Data ³03/03/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao para selecao do objeto                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocWzGet( ExpC1, ExpC2 )                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Nome do objeto / ExpC2 -> Descricao                 ³±±
±±³          ³ ambos por referencia                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocWzGet( cObj, cDescri )

//Conforme instruções do Sergio Silveira
Return MPDocWzGet( cObj, cDescri )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocWzEnd ³ Autor ³ Sergio Silveira       ³ Data ³03/03/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao de encerramento do Wizard                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsDocWzGrv( ExpC1, ExpC2, ExpA3, ExpA4, ExpL5, ExpO6 )       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Nome do objeto / ExpC2 -> Descricao                 ³±±
±±³          ³ ExpA3 -> aHeader getdados palavra chave / ExpA4 -> aCols     ³±±
±±³          ³ ExpL5 -> Indica se ja associa a entidade                     ³±±
±±³          ³ ExpO6 -> objeto getdados                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocWzEnd( cObj, cDescri, aHeaderWiz, aColsWiz, lAssocia, oGetDad )

//Conforme instruções do Sergio Silveira
Return MPDocWzEnd( cObj, cDescri, aHeaderWiz, aColsWiz, lAssocia, oGetDad )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MsDocWzGrv ³ Autor ³ Sergio Silveira       ³ Data ³03/03/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Funcao de gravacao do Wizard                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := MsDocWzGrv( ExpC1, ExpC2, ExpA3, ExpA4 )            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se efetuou a gravacao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Nome do objeto / ExpC2 -> Descricao                 ³±±
±±³          ³ ExpA3 -> aHeader getdados palavra chave / ExpA4 -> aCols     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function MsDocWzGrv( cObj, cDescri, aHeaderWiz, aColsWiz )

//Conforme instruções do Sergio Silveira
Return MPDocWzGrv( cObj, cDescri, aHeaderWiz, aColsWiz, lCopyObj, cCodObj )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³FtVerAC9  ³ Autor ³ Marco Bianchi         ³ Data ³02/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao disparada para validar cada registro da tabela      ³±±
±±³          ³ AC9, adicionar recno no array aRecAC9 utilizado na gravacao³±±
±±³          ³ cao da tabela AC9 e verificar se conseguiu travar AC9.     ³±±
±±³          ³ Se retornar .T. considera o registro.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Logico                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: Array com numero dos registros da tabela AC8         ³±±
±±³          ³ExpA2: Array coim registros travados do AC8                 ³±±
±±³          ³ExpL3: .T. se conseguiu travar AC8                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MsVerAC9(aTravas,aRecAC9,aRecACB,lTravas,nOper,nOpc,lPE)

Local nTipo 	:= IIf(nOper == 1,2,1)
Local lRet 		:= .T.
Local lMsDocFil := Existblock("MSDOCFIL") .And. lPE
Local nRecNoAC9
DEFAULT nOpc 	:= 2

If Valtype("AC9RECNO") == 'N'
	nRecNoAC9 := AC9RECNO
	AC9->( dbGoto( nRecNoAC9 ) )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria o Acols para exibicao                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTipo == 2 .AND. nOpc <> 2
	If ( SoftLock("AC9" ) )
		AAdd(aTravas,{ Alias() , RecNo() })
	Else
		lTravas := .F.
	EndIf
EndIf
AAdd(aRecAC9, AC9->( Recno() ) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria o Array de recnos do banco de conhecimento                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTipo == 1
	ACB->( dbSetOrder( 1 ) )
	If ACB->( dbSeek( xFilial( "ACB" ) + AC9->AC9_CODOBJ ) )
		AAdd( aRecACB, ACB->( RecNo() ) )
	EndIf
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada pata filtro do usuario. Se retornar .T. considera o   ³
//³ registro do AC9, senao pula o registro.                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMsDocFil
	lRet := ExecBlock("MSDOCFIL",.F.,.F.,{AC9->(Recno())})
	If ValType(lRet) <> "L"
		lRet := .T.
	EndIf
EndIf

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsRetPath  ³ Autor ³ Sergio S. Fuzinaka   ³ Data ³ 05.11.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Retorna o path do arquivo                                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpC1 := MsRetPath( cFile )                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpC1 -> Path do arquivo                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MsRetPath( cFile )

Local aArea		:= GetArea()
Local aAreaACB	:= ACB->( GetArea() )
Local cDirDoc	:= MsDocPath( .T. )

Default cFile	:= ""

If !Empty( cFile )
	ACB->( dbSetOrder( 2 ) )
	If ACB->( dbSeek( xFilial( "ACB" ) + Upper( cFile ) ) )
		If !Empty( Alltrim( ACB->ACB_PATH ) )
		   	cDirDoc := Alltrim( ACB->ACB_PATH )
		Else
		   	cDirDoc := MsDocPath()
		Endif
	Endif
Endif

RestArea( aAreaACB )
RestArea( aArea )

Return( cDirDoc )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsMultDir  ³ Autor ³ Sergio S. Fuzinaka   ³ Data ³ 05.11.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Verifica se foi ativado a gravacao em varios niveis de      ³±±
±±³          ³de diretorio.                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpC1 := MsMultDir()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1 -> Logico                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   Data   ³ Programador   ³ Manutencao Efetuada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MsMultDir()
Return( ( GetNewPar("MV_MULTDIR",.F.) .And. ACB->(FieldPos("ACB_PATH")) > 0 ) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MsConHist  ³ Autor ³ Marco Bianchi        ³ Data ³ 11/11/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Retorna historico de alteracoes do cadastro de clientes,    ³±±
±±³          ³fornecedores, produto e transportadora.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpA1 := MsConHist(cPar1,cPar2,cPar3,dPar4,dPar5,cPar6,     ³±±
±±³			 ³					 cPar7)						              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cPar1: Alias da tabela (SA1 ou SA2)                         ³±±
±±³          ³cPar2: Codigo do cliente/fornecedor                         ³±±
±±³          ³cPar3: Loja do cliente/fornecedor                           ³±±
±±³          ³dPar4: Data da alteracao inicial                            ³±±
±±³          ³dPar5: Data da alteracao final                              ³±±
±±³          ³cPar6: Codigo do produto                                    ³±±
±±³          ³cPar7: Codigo da transportadora                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpA1 -> {cPos1,cPos2,cPos3,cPos4}                          ³±±
±±³          ³[1]: Campo que sofreu alteracao                             ³±±
±±³          ³[2]: Conteudo do campo antes da alteracao                   ³±±
±±³          ³[3]: Data da alteracao                                      ³±±
±±³          ³[4]: Hora da alteracao                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   Data   ³ Programador   ³ Manutencao Efetuada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MsConHist(cAliasTab,cCliFor,cLoja,dDataIni,dDataFim,cProduto,cTransp)

Local aRet			:= {}
Local aAreaAIF		:= AIF->(GetArea())
Local cAliasAIF		:= "AIF"
Local cQuery		:= ""
Local cFilAlias 	:= FwxFilial(cAliasTab)
Local cFilAIF   	:= FwxFilial("AIF")

Default cCliFor	 	:= ""
Default cLoja	 	:= ""
Default dDataIni 	:= CTOD("01/01/00")
Default dDataFim 	:= CTOD("31/12/49")
Default cProduto 	:= ""
Default cTransp  	:= ""

If (cAliasTab $ "SA1/SA2")		//Cadastro de Clientes/Fornecedores
	dbSelectArea("AIF")
	dbSetOrder(1)

	cAliasAIF := "HISTTAB"
	cQuery := "SELECT AIF_FILIAL, AIF_FILTAB, AIF_TABELA, AIF_CODIGO, AIF_LOJA, AIF_DATA, AIF_CAMPO, AIF_CONTEU, AIF_HORA "
	cQuery += "FROM "+RetSqlName("AIF")+" AIF "
	cQuery += "WHERE AIF.AIF_FILIAL= ? AND "
	cQuery += "AIF.AIF_FILTAB= ? AND "
	cQuery += "AIF.AIF_TABELA= ? AND "
	cQuery += "AIF.AIF_CODIGO= ? AND "
	cQuery += "AIF.AIF_LOJA= ? AND "
	cQuery += "AIF.AIF_DATA>= ? AND "
	cQuery += "AIF.AIF_DATA<= ? AND "
	cQuery += "AIF.D_E_L_E_T_= ? "
	cQuery += "ORDER BY "+SqlOrder(AIF->(IndexKey()))

	If Empty(__oPrepSA)
		cQuery := ChangeQuery(cQuery)
		__oPrepSA := FwExecStatement():New(cQuery)
	Endif 

	__oPrepSA:SetString(1,cFilAIF)
	__oPrepSA:SetString(2,cFilAlias)
	__oPrepSA:SetString(3,cAliasTab)
	__oPrepSA:SetString(4,cCliFor)
	__oPrepSA:SetString(5,cLoja)
	__oPrepSA:SetString(6,DTOS(dDataIni))
	__oPrepSA:SetString(7,DTOS(dDataFim))
	__oPrepSA:SetString(8," ")

	__oPrepSA:OpenAlias(cAliasAIF)
	dbselectarea(cAliasAIF)
	
	TcSetField(cAliasAIF,"AIF_DATA","D",8,0)

	While !(cAliasAIF)->(Eof()) .And. (cAliasAIF)->AIF_FILIAL == cFilAIF .And. (cAliasAIF)->AIF_FILTAB == cFilAlias .And. (cAliasAIF)->AIF_TABELA == cAliasTab .And.;
		(cAliasAIF)->AIF_CODIGO == cCliFor .And. (cAliasAIF)->AIF_LOJA == cLoja .And. (cAliasAIF)->AIF_DATA >= dDataIni .And. (cAliasAIF)->AIF_DATA <= dDataFim

		AADD(aRet,{(cAliasAIF)->AIF_CAMPO,AllTrim((cAliasAIF)->AIF_CONTEU),(cAliasAIF)->AIF_DATA,(cAliasAIF)->AIF_HORA})

		dbSkip()
	EndDo

ElseIf (cAliasTab $ "SB1")		//Cadastro de Produtos
	dbSelectArea("AIF")
	If AIF->(FieldPos("AIF_CODPRO")) > 0
		dbSetOrder(2)

		cAliasAIF := "HISTTAB"
		cQuery := "SELECT AIF_FILIAL, AIF_FILTAB, AIF_TABELA, AIF_CODPRO, AIF_DATA, AIF_CAMPO, AIF_CONTEU, AIF_HORA "
		cQuery += "FROM "+RetSqlName("AIF")+" AIF "
		cQuery += "WHERE AIF.AIF_FILIAL= ? AND "
		cQuery += "AIF.AIF_FILTAB= ? AND "
		cQuery += "AIF.AIF_TABELA= ? AND "
		cQuery += "AIF.AIF_CODPRO= ? AND "
		cQuery += "AIF.AIF_DATA>= ? AND "
		cQuery += "AIF.AIF_DATA<= ? AND "
		cQuery += "AIF.D_E_L_E_T_= ? "
		cQuery += "ORDER BY "+SqlOrder(AIF->(IndexKey()))

		If Empty(__oPrepSB1)
			cQuery := ChangeQuery(cQuery)
			__oPrepSB1 := FwExecStatement():New(cQuery)
		Endif 

		__oPrepSB1:SetString(1,cFilAIF)
		__oPrepSB1:SetString(2,cFilAlias)
		__oPrepSB1:SetString(3,cAliasTab)
		__oPrepSB1:SetString(4,cProduto)
		__oPrepSB1:SetString(5,DTOS(dDataIni))
		__oPrepSB1:SetString(6,DTOS(dDataFim))
		__oPrepSB1:SetString(7," ")

		__oPrepSB1:OpenAlias(cAliasAIF)
		dbselectarea(cAliasAIF)
		
		TcSetField(cAliasAIF,"AIF_DATA","D",8,0)

		While !Eof() .And. (cAliasAIF)->AIF_FILIAL == cFilAIF .And. (cAliasAIF)->AIF_FILTAB == cFilAlias .And. (cAliasAIF)->AIF_TABELA == cAliasTab .And.;
			(cAliasAIF)->AIF_CODPRO == cProduto .And. (cAliasAIF)->AIF_DATA >= dDataIni .And. (cAliasAIF)->AIF_DATA <= dDataFim

			AADD(aRet,{(cAliasAIF)->AIF_CAMPO,AllTrim((cAliasAIF)->AIF_CONTEU),(cAliasAIF)->AIF_DATA,(cAliasAIF)->AIF_HORA})

			dbSkip()
		EndDo
	EndIf
ElseIf (cAliasTab == "SA4")		//Cadastro de Transportadora
	dbSelectArea("AIF")
	If AIF->(FieldPos("AIF_TRANSP")) > 0
		dbSetOrder(3)

		cAliasAIF := "HISTTAB"
		cQuery := "SELECT AIF_FILIAL, AIF_FILTAB, AIF_TABELA, AIF_TRANSP, AIF_DATA, AIF_CAMPO, AIF_CONTEU, AIF_HORA "
		cQuery += "FROM "+RetSqlName("AIF")+" AIF "
		cQuery += "WHERE AIF.AIF_FILIAL= ? AND "
		cQuery += "AIF.AIF_FILTAB= ? AND "
		cQuery += "AIF.AIF_TABELA= ? AND "
		cQuery += "AIF.AIF_TRANSP= ? AND "
		cQuery += "AIF.AIF_DATA>= ? AND "
		cQuery += "AIF.AIF_DATA<= ? AND "
		cQuery += "AIF.D_E_L_E_T_= ? "
		cQuery += "ORDER BY "+SqlOrder(AIF->(IndexKey()))

		If Empty(__oPrepSA4)
			cQuery := ChangeQuery(cQuery)
			__oPrepSA4 := FwExecStatement():New(cQuery)
		Endif 

		__oPrepSA4:SetString(1,cFilAIF)
		__oPrepSA4:SetString(2,cFilAlias)
		__oPrepSA4:SetString(3,cAliasTab)
		__oPrepSA4:SetString(4,cTransp)
		__oPrepSA4:SetString(5,DTOS(dDataIni))
		__oPrepSA4:SetString(6,DTOS(dDataFim))
		__oPrepSA4:SetString(7," ")

		__oPrepSA4:OpenAlias(cAliasAIF)
		dbselectarea(cAliasAIF)
		
		TcSetField(cAliasAIF,"AIF_DATA","D",8,0)

		While !Eof() .And. (cAliasAIF)->AIF_FILIAL == cFilAIF .And. (cAliasAIF)->AIF_FILTAB == cFilAlias .And. (cAliasAIF)->AIF_TABELA == cAliasTab .And.;
				(cAliasAIF)->AIF_TRANSP == cTransp .And. (cAliasAIF)->AIF_DATA >= dDataIni .And. (cAliasAIF)->AIF_DATA <= dDataFim

			AADD(aRet,{(cAliasAIF)->AIF_CAMPO,AllTrim((cAliasAIF)->AIF_CONTEU),(cAliasAIF)->AIF_DATA,(cAliasAIF)->AIF_HORA})

			dbSkip()
		EndDo
	EndIf
EndIf

dbSelectArea(cAliasAIF)
dbCloseArea()

RestArea(aAreaAIF)

Return( aRet )

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MSGrvHist ³ Autor ³ Emerson Rony Oliveira ³ Data ³10/11/2008              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Grava historico de alteracoes das tabelas SA1/SA2/SB1/SA4/SA5            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MsGrvHist(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6,ExpU7,Expd8,ExpC9,ExpC10, ³±±
±±³          ³ 			 ExpC11)												              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1  = Filial da tabela AIF                                           ³±±
±±³          ³ ExpC2  = Filial da tabela SA1 ou SA2                                    ³±±
±±³          ³ ExpC3  = Nomda da tabela ("SA1" ou "SA2")                               ³±±
±±³          ³ ExpC4  = Codigo do cliente / fornecedor                                 ³±±
±±³          ³ ExpC5  = Loja do cliente / fornecedor                                   ³±±
±±³          ³ ExpC6  = Nome do campo que sofreu alteracao                             ³±±
±±³          ³ ExpU7  = Conteudo do campo antes da alteracao (indefinido)              ³±±
±±³          ³ ExpD8  = Data da alteracao (DD/MM/YYYY)                                 ³±±
±±³          ³ ExpC9  = Hora da alteracao (HH:MM)                                      ³±±
±±³          ³ ExpC10 = Codigo do produto                                              ³±±
±±³          ³ ExpC11 = Codigo da transportadora                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA020() / MATA030 / MATA010 / MATA050 / MATA060                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MSGrvHist(cFilAIF, cFilTab, cTabela, cCodigo, cLoja, cCampo, uConteudo, dData, cHora, cProduto, cTransp)
Local aArea      := GetArea()
Local cConteudo  := ""
Local cTipo      := TamSX3(cCampo)[3]
Local nPosCodPro := 0

Default cCodigo  := ""
Default cLoja    := ""
Default cProduto := ""
Default cTransp  := ""

dbSelectArea("AIF")
nPosCodPro := AIF->(FieldPos("AIF_CODPRO"))
nPosTransp := AIF->(FieldPos("AIF_TRANSP"))

If (cTabela $ "SA1/SA2/SA5" .Or. (cTabela == "SB1" .And. nPosCodPro > 0) .Or. (cTabela == "SA4" .And. nPosTransp > 0)) .And.;
	(cTipo $ "C|N|D|L") // Se o tipo do campo for "M"emo, abandona a gravacao

	If cTipo == "C" .And. ValType(uConteudo) == cTipo
		cConteudo := uConteudo
	ElseIf cTipo == "N" .And. ValType(uConteudo) == cTipo
		cConteudo := AllTrim(Str(uConteudo))
	ElseIf cTipo == "D" .And. ValType(uConteudo) == cTipo
		cConteudo := DtoC(uConteudo)
	ElseIf cTipo == "L" .And. ValType(uConteudo) == cTipo
		cConteudo := IIf(uConteudo, ".T.", ".F.")
	EndIf

	RecLock("AIF", .T.)
	AIF->AIF_FILIAL := cFilAIF
	AIF->AIF_FILTAB := cFilTab
	AIF->AIF_TABELA := cTabela
	AIF->AIF_CODIGO := cCodigo
	AIF->AIF_LOJA   := cLoja
	If nPosCodPro > 0
		AIF->AIF_CODPRO := cProduto
	EndIf
	If nPosTransp > 0
		AIF->AIF_TRANSP := cTransp
	EndIf
	AIF->AIF_CAMPO  := cCampo
	AIF->AIF_CONTEU := cConteudo
	AIF->AIF_DATA   := dData
	AIF->AIF_HORA   := cHora
	MsUnlock()

	If FindFunction('TAFGrvTabC1i')
		TAFGrvTabC1i(cFilTab, cCodigo, cLoja, cTabela, cCampo, cConteudo, dData, cHora)
	Endif	
	
EndIf

RestArea(aArea)
Return Nil


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MaEnvEAI  ³ Autor ³ Richard A./Jefferson T. Data ³ 05/04/2011           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Envia informacoes de tabelas para o EAI por rotinas que nao foram       ³±±
±±³          ³ convertidas para MVC                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MaEnvEAI(ExpN1,ExpC1,ExpA1,ExpL1)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1  = Opcao de operacao (3=Inclusao,4=Alteracao,5=Exclusao)          ³±±
±±³          ³ ExpC1  = Nome do model principal                                        ³±±
±±³          ³ ExpA1  = Models de estrutura das tabelas                                ³±±
±±³          ³ //-- [1]   Alias                                                        ³±±
±±³          ³ //-- [2]   Model da Estrutura                                           ³±±
±±³          ³ //-- [3]   bSeek                                                        ³±±
±±³          ³ //-- [4]   nOrdem                                                       ³±±
±±³          ³ //-- [5]   bWhile                                                       ³±±
±±³          ³ //-- [6]   aFieldValue                                                  ³±±
±±³          ³ //-- [6,1] Nome do Campo                                                ³±±
±±³          ³ //-- [6,2] Bloco de execucao para o valor                               ³±±
±±³          ³ //--       a ser atribuido ao campo
±±³          ³ ExpL1  = Exibe Help                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Protheus                                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MaEnvEAI(cEmpX,cFilX,nOpcx,cMainModel,aStruModel,lHelp,lEnvia,lLoadModel,oMdl,cXml,cFunName)
Local   aArea       := GetArea()
Local   nA,nI       := 0
Local   cMsgError   := ''
Local   oMainModel  := NIL
Local   aFields     := {}
Local   cIDField    := ''
Local   xValue      := NIL
Local   nOrdem      := 0
Local   nItem       := 1
Local   aFieldValue := {}
Local   lRet        := .T.
Local   lVerifySend := .T.
Local   lRetMsgPrc  := .F.
Local   oFWEAI		  := NIL
Local   lNoMsgUn	  := .T.

Private N	//Necessária declaração da variável N, para que a Lib do MVC trate-a conforme modelo antigo, aCols, aHeader e variavel

Default cFunName    := FunName()
Default nOpcx       := 3
Default aStruModel  := {}
Default lHelp       := .T.
Default lEnvia		  := .T.
Default lLoadModel  := .T.
Default cEmpX		  := ""
Default cFilX		  := ""
Default cXml		  := ""
Default oMdl          := NIL

//-- aStruModel
//-- [1] - Alias
//-- [2] - Model da Estrutura
//-- [3] - bSeek
//-- [4] - nOrdem
//-- [5] - bWhile
//-- [6] - aFieldValue
//-- [6,1] Nome do Campo
//-- [6,2] Bloco de execucao para o valor
//--       a ser atribuido ao campo

If !Empty(cEmpX) .And. !Empty(cFilX)
	//Seta job para nao consumir licensas
	RpcSetType(3)
	// Seta job para empresa filial desejada
	RpcSetEnv( cEmpX, cFilX,,,'OMS')
	lRetMsgPrc := .T.
	SetFunName("RETURNMESSAGE")
Else
	If XX4->(FieldPos("XX4_UNMESS")) > 0
		XX4->( dbSetOrder( 1 ) )
		If FWXX4Seek(cFunName)
			If XX4->XX4_UNMESS == '1' //Se for Mensagem Única
		   	lNoMsgUn := .F.
		   EndIf
		EndIf
	EndIf
EndIf

If lNoMsgUn
	If FWHasEAI(cMainModel,lVerifySend)

		If lLoadModel
			oMainModel:= FWLoadModel(cMainModel)
			oMainModel:SetOperation(nOpcx)
			oMainModel:Activate()
			If !Empty(cXml)
				oMainModel:LoadXmlData(cXml, .T.)
			EndIf
		Else
			oMainModel := oMdl
		EndIf
		If nOpcx == 3

			For nA := 1 To Len(aStruModel)
				cAlias := aStruModel[nA,1]
				aFields:= oMainModel:GetModel(aStruModel[nA,2]):GetStruct():aFields
				bSeek  := aStruModel[nA,3]
				nOrdem := Iif(aStruModel[nA,4] == NIL,1,aStruModel[nA,4])
				bWhile := aStruModel[nA,5]
				aFieldValue := aStruModel[nA,6]
				If aFieldValue == NIL
					aFieldValue := {}
				EndIf
				For nI := 1 To Len(aFields)
					If bSeek == NIL .Or. bWhile == NIL
						If ValType(bSeek) == 'B'
							(cAlias)->(dbSetOrder(nOrdem))
							(cAlias)->(dbSeek(Eval(bSeek)))
						EndIf
						cIDField := aFields[nI][MODEL_FIELD_IDFIELD]
						If (nPos := Ascan(aFieldValue,{ | e | e[1] == cIDField })) > 0
							xValue:= Eval(aFieldValue[nPos,2])
						Else
							xValue:= (cAlias)->(FieldGet(FieldPos(cIDField)))
						EndIf
						If ValType(xValue) != "U"
							If !oMainModel:SetValue( aStruModel[nA,2], cIDField, xValue )
								cMsgError += "Campo "+cIDField+": "+oMainModel:GetErrorMessage()[6]
								lRet := .F.
								Exit
							EndIf
						EndIf
					Else
						(cAlias)->(dbSetOrder(nOrdem))
						(cAlias)->(dbSeek(Eval(bSeek)))
						nItem := 1
						While (cAlias)->(!Eof()) .And. (cAlias)->(Eval(bWhile))
							If nItem > 1
								oMainModel:GetModel(aStruModel[nA,2]):AddLine(.T.)
							EndIf
							For nI := 1 To Len(aFields)
								cIDField := aFields[nI][MODEL_FIELD_IDFIELD]
								If (nPos := Ascan(aFieldValue,{ | e | e[1] == cIDField })) > 0
									xValue:= Eval(aFieldValue[nPos,2])
								Else
									xValue:= (cAlias)->(FieldGet(FieldPos(cIDField)))
								EndIf
								If ValType(xValue) != "U"
									If !oMainModel:SetValue( aStruModel[nA,2], cIDField, xValue )
										cMsgError += "Campo "+cIDField+": "+oMainModel:GetErrorMessage()[6]
										lRet := .F.
										Exit
									EndIf
								EndIf
							Next nI
						   nItem += 1
							(cAlias)->(dbSkip())
						EndDo
					EndIf
					If !lRet
						Exit
					EndIf
				Next nI
				If !lRet
					Exit
				EndIf
			Next nA
		EndIf
		If lEnvia
			If Empty(cMsgError)
				If !lRetMsgPrc
					FWEAICreate(oMainModel,.T.)
				Else
					oFWEAI:= FWEAI():New()
					oFWEAI:SetFuncCode( oMainModel:GetID() )
					oFWEAI:SetFuncDescription( oMainModel:GetDescription() )
					oFWEAI:SetDocType( XX4->XX4_METODO )
					oFWEAI:AddLayout(  oMainModel:GetID() , "1.0", "FWFORMEAI." + XX4->XX4_ROTINA, cXml  )
					oFWEAI:SetTypeMessage( EAI_MESSAGE_MVC )
					oFWEAI:SetSendChannel( XX4->XX4_CHANEL )
					oFWEAI:Activate()

					If !oFWEAI:Save()
						If InTransaction()
					   	DisarmTransaction()
					   EndIf
					EndIf

				EndIf
			ElseIf lHelp
				cTitulo := "Registro não enviado!"
				MsgStop(cMsgError,cTitulo)
			EndIf
			If lLoadModel .And. oMdl == NIL
				FreeObj(oMainModel)
			EndIf
		EndIf
	EndIf

	If !Empty(cEmpX) .And. !Empty(cFilX)
		RpcClearEnv()
	EndIf

EndIf
RestArea(aArea)
Return oMainModel


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MaRecEAI  ³ Autor ³ Richard A./Jefferson T. Data ³ 05/04/2011           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡ao ³ Recebe informacoes do EAI e executa MsExecAuto para as rotinas que nao  ³±±
±±³          ³ convertidas para MVC                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MaRecEAI(ExpN1,ExpC1,ExpA1,ExpL1)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1  = Objeto Model                                                   ³±±
±±³          ³ ExpC1  = Nome do Model principal                                        ³±±
±±³          ³ ExpA1  = Array contendo as estruturas do Model                          ³±±
±±³          ³ //-- [1] Model da Estrutura                                             ³±±
±±³          ³          Maximo de 5 estruturas                                         ³±±
±±³          ³ ExpA2  = Parametros para MsExecAuto                                     ³±±
±±³          ³ //-- [1] Nome dos parametros (formato caracter)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Protheus                                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function MaRecEAI(oModel, cMainModel, aIDStruct, aParRot, aNewField, aMsgRet, bOption )
Local   aArea          := GetArea()
Local   nA             := 0
Local   nI             := 0
Local   nY             := 0
Local   nZ             := 0
Local   aFields        := {}
Local   cIDField       := ""
Local   aErroAuto      := {}
Local   cLogErro       := ""
Local   cExec          := ""
Local   cParExec       := ""
Local   cClasse        := ""
Local   lVerifyRec     := .T.
Local   xValue         := ""
Local   nPos           := 0
Local   cAlias         := ""
Local   nOrder         := 0
Local   aCampos        := {}
Local   oMdlStru		  := Nil
Local   aCpoKey		  := {}

Private nOpcx          := oModel:GetOperation()
Private aRotAuto1      := {}
Private aRotAuto2      := {}
Private aRotAuto3      := {}
Private aRotAuto4      := {}
Private aRotAuto5      := {}
Private lMsErroAuto    := .F.
Private lMsHelpAuto    := .T.
Private lAutoErrNoFile := .T.

Default aNewField      := {}
Default aMsgRet        := {}


If FWHasEAI(cMainModel,,lVerifyRec)
	For nA:= 1 To Len(aIDStruct)

		oMdlStru := oModel:GetModel(aIDStruct[nA])
		aFields  := oMdlStru:GetStruct():aFields
		cClasse  := oMdlStru:classname()
		aCpoKey  := oMdlStru:GetStruct():GetTable()[2]

		If cClasse == 'FWFORMFIELDS'

			For nI := 1 To Len(aFields)
				nPos     := 0
				cIDField := aFields[nI][MODEL_FIELD_IDFIELD]
				xValue   := oMdlStru:GetValue(cIDField)
				aCampos  := {}

				If ( nPos := aScan(aNewField,{|x| x[1] == AllTrim(cIDField) }) ) > 0
					cAlias := aNewField[nPos,2]
					nOrder := aNewField[nPos,3]
					If nOrder == NIL
						nOrder := 0
					EndIf
					aCampos:= aNewField[nPos,4]
					If nOrder > 0
						(cAlias)->(dbSetOrder(nOrder))
						(cAlias)->(dbSeek(xFilial(cAlias)+xValue))
					EndIf
				EndIf
				If Len(aCampos) > 0
					For nY := 1 To Len( aCampos )
						If ValType(aCampos[nY,2]) == "B"
							Aadd( &('aRotAuto'+AllTrim(Str(nA))) ,{ (aCampos[nY,1]), Eval(aCampos[nY,2]), NIL })
						Else
							Aadd( &('aRotAuto'+AllTrim(Str(nA))) ,{ (aCampos[nY,1]), &( cAlias+"->"+(aCampos[nY,2])), NIL })
						EndIf
					Next nY
				Else
					Aadd( &('aRotAuto'+AllTrim(Str(nA))) ,{ cIDField, xValue, NIL })
				EndIf
			Next nI

		ElseIf cClasse == 'FWFORMGRID'
			If !oMdlStru:IsEmpty()

				For nZ := 1 To oMdlStru:Length()

					Aadd( &('aRotAuto'+AllTrim(Str(na))) ,{ })
					oMdlStru:GoLine(nZ)
					For nI:= 1 To Len(aFields)
						nPos     := 0
						cIDField := aFields[nI][MODEL_FIELD_IDFIELD]
						xValue   := oMdlStru:GetValue(cIDField)
						aCampos  := {}

						If ( nPos := aScan(aNewField,{|x| x[1] == AllTrim(cIDField) }) ) > 0
							cAlias := aNewField[nPos,2]
							nOrder := aNewField[nPos,3]
							If nOrder == NIL
								nOrder := 0
							EndIf
							aCampos:= aNewField[nPos,4]
							If nOrder > 0
								(cAlias)->(dbSetOrder(nOrder))
								(cAlias)->(dbSeek(xFilial(cAlias)+xValue))
							EndIf
						EndIf

						If !Empty(xValue)
							If Len(aCampos) > 0
								For nY := 1 To Len( aCampos )
									If ValType(aCampos[nY,2]) == "B"
										Aadd( &('aRotAuto'+AllTrim(Str(na)))[Len(&('aRotAuto'+AllTrim(Str(na))))] ,{ (aCampos[nY,1]), Eval(aCampos[nY,2]), NIL })
									Else
										Aadd( &('aRotAuto'+AllTrim(Str(na)))[Len(&('aRotAuto'+AllTrim(Str(na))))] ,{ (aCampos[nY,1]), &( cAlias+"->"+aCampos[nY,2]), NIL })
									EndIf
								Next nY
							Else
								Aadd( &('aRotAuto'+AllTrim(Str(na)))[Len(&('aRotAuto'+AllTrim(Str(na))))] ,{cIDField, xValue, NIL })
							EndIf
						ElseIf aScan(aCpoKey, { |x| x == cIDField } ) > 0
							Aadd( &('aRotAuto'+AllTrim(Str(na)))[Len(&('aRotAuto'+AllTrim(Str(na))))] ,{cIDField, xValue, NIL })
						EndIf

					Next nI

				Next nZ
			EndIf
		EndIf
	Next nA

	For nI := 1 To Len(aParRot)
		If !Empty(cParExec)
			cParExec += ','
		EndIf
		cParExec += Chr(96+nI)
	Next nI

	For nI := 1 To Len(aParRot)
		If nI == 1
			cExec := 'MsExecAuto({|'+cParExec+'|'+cMainModel+'('+cParExec+')}'
		EndIf
		cExec += ","+aParRot[nI]
	Next nI
	cExec += ')'

	If bOption <> NIL
		nOpcx := Eval(bOption)
	EndIf

	&(cExec)

	If lMsErroAuto
		aErroAuto := GetAutoGRLog()
		For nI := 1 To Len(aErroAuto)
			cLogErro += NoAcentoCte(aErroAuto[nI])
		Next nI
		//-- Forca Help para o Model retornar mensagem de erro para o EAI
		Conout ("Erro: " + clogerro)
		/*@param cIdForm Id do formulário em validação
		@param cIdField Id do campo do formulário em validação
		@param cIdFormErr Id do formulário em que ocorreu o erro
		@param cIdFieldErr Id do campo do formulário em que ocorreu o erro
		@param cId Id da mensagem de help
		@param cMessage Mensagem de erro
		@param cSoluction Mensagem de solução
		@param xValue Valor especificado do campo
		@param xOldValue Valor anterior do campo
		*/
		oModel:SetErrorMessage (,,,,,cLogErro)

	EndIf
	If Len(aMsgRet) > 0
		cXml := oModel:GetXmlData()
		If lMsErroAuto
			For nI:= 1 To Len(aMsgRet)
				cXml:= STRTRAN ( cXml , "<value>Processado</value></"+aMsgRet[nI,2]+">" , "<value>"+SubStr(cLogErro,1,120)+"</value></"+aMsgRet[nI,2]+">"  )
			Next nI
		EndIf
		StartJob("MaEnvEAI",GetEnvServer(),.T.,cEmpAnt,cFilAnt,3,cMainModel,,,,,,cXml)
	EndIf
EndIf


RestArea(aArea)
Return !lMsErroAuto

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetRestEx ºAutor  ³ Pedro Pereira Lima º Data ³  29/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que retorna .T. caso o valor a ser convertido para  º±±
±±º          ³ extenso seja um milhar redondo (ex. 1000000,50000000)      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function GetRestEx(nValor)
Local nX		:= 0
Local lRet	:= .F.

Default nValor := 0

If nValor > 0
	For nX := 1000000 To 1000000000 Step 1000000 //Incremento de um em um milhão pois somente esses valores
		If nX == nValor                           //possuem o "de" entre o valor e a moeda quando por extenso
			lRet := .T.
			Exit
		EndIf
	Next nX
EndIf

Return lRet

/*/{Protheus.doc} A410ChSGO
Valida a existencia de campos na SGO
@return lRet
/*/

Function A410ChSGO()
Local aCpoSGO   := FWSX3Util():GetAllFields('SGO',.f.)
Local lRet		:= .T.

If Ascan (aCpoSGO,{|x|AllTrim(x) == "GO_LOTECTL"}) = 0 .or. ;
   Ascan (aCpoSGO,{|x|AllTrim(x) == "GO_NUMLOTE"}) = 0 .or. ;
   Ascan (aCpoSGO,{|x|AllTrim(x) == "GO_ORDEM"}) = 0 .or. ;
   Ascan (aCpoSGO,{|x|AllTrim(x) == "GO_OPORIG"}) = 0 .or. ;
   Ascan (aCpoSGO,{|x|AllTrim(x) == "GO_SEQ"}) = 0 	
   lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} FinAjusAC9
Ajusta a amarração da base de conhecimento com a nova tabela FK7, trocando a amarração do TITPAI com
o IDDOC.
@type function
@version 12.1.2410
@author tp.ciro.pedreira
@since 02/10/2025
@param cEntidade, character, código da entidade
@param cChvBusca, character, chave antiga do título para a busca
@param cNewChave, character, nova chave que irá substituir a antiga
@return logical, .T. = se encontrou a amarração antiga e fez o ajuste; .F. = não encontrou a amarração antiga
/*/
Function FinAjusAC9(cEntidade As Character, cChvBusca As Character, cNewChave As Character) As Logical

	Local cQuery As Character
	Local oQryAC9 As Object
    Local nParam As Numeric
    Local cAliasAC9 As Character
	Local lRet As Logical

	Default cEntidade := ""
	Default cChvBusca := ""
	Default cNewChave := ""

	cQuery    := ""
	oQryAC9   := Nil
	nParam    := 1
    cAliasAC9 := GetNextAlias()
	lRet      := .F.

	If __lLibVer == Nil
        __lLibVer := FWLibVersion() >= "20211116"
    EndIf

	cQuery := "SELECT AC9.R_E_C_N_O_ RECAC9 "
	cQuery += "FROM " + RetSQLName("AC9") + " AC9 "
	cQuery += "WHERE AC9.AC9_FILIAL = ? " // WHERE seguindo a ordem do indice 2 da tabela AC9 = AC9_FILIAL+AC9_ENTIDA+AC9_FILENT+AC9_CODENT+AC9_CODOBJ
	cQuery += "AND AC9.AC9_ENTIDA = ? "
	cQuery += "AND AC9.AC9_FILENT = ? "
	cQuery += "AND AC9.AC9_CODENT = ? "
	cQuery += "AND AC9.D_E_L_E_T_ = ? "
	cQuery := ChangeQuery(cQuery)

	oQryAC9 := IIf(__lLibVer, FwExecStatement():New(cQuery), FWPreparedStatement():New(cQuery))
	
	oQryAC9:SetString(nParam++, FWxFilial("AC9"))
	oQryAC9:SetString(nParam++, cEntidade)
	oQryAC9:SetString(nParam++, FWxFilial(cEntidade))
	oQryAC9:SetString(nParam++, cChvBusca)
	oQryAC9:SetString(nParam++, " ")

	IIf(__lLibVer, oQryAC9:OpenAlias(cAliasAC9), MpSysOpenQuery(oQryAC9:GetFixQuery(), cAliasAC9))

	If (cAliasAC9)->(!EOF())
		While (cAliasAC9)->(!EOF())
			AC9->(DBGoTo((cAliasAC9)->RECAC9))

			RecLock("AC9", .F.)
			AC9->AC9_FILENT := FWxFilial("FK7")
			AC9->AC9_ENTIDA := "FK7"
			AC9->AC9_CODENT := cNewChave
			AC9->(MSUnlock())
			
			(cAliasAC9)->(DBSkip())
		EndDo

		lRet := .T.
	EndIf

	(cAliasAC9)->(DBCloseArea())
	oQryAC9:Destroy()
	FWFreeObj(oQryAC9)
	
Return lRet
