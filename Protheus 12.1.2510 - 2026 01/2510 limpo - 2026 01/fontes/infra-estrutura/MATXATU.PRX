#INCLUDE "ERROR.CH"
#INCLUDE "FWADAPTEREAI.CH"

#DEFINE PESQ_SAY  10
#DEFINE PESQ_GET  70
#DEFINE PESQ_TOP  5
#DEFINE PESQ_SKIP 15
#DEFINE PDABOTTOM 234
#DEFINE PDARIGHT  234

Static __lPointSB6
Static oGets,__cLineOK, oGetDados, nCount, __nOpcx
//Statics da AtuSalDup
Static nMCusto
Static cTiposLC
Static __HasFunType

Static __HasSNPrvt	:= NIL
Static dLastPcc  := CTOD("22/06/2015")
Static lIsIssBx := FindFunction("IsIssBx")
Static lPLSTITPF	:= findFunction("PLSTITPF")

// Motor de retenção
Static __lTemMR := NIL

//-- F100Top
Static lSe2MsFil 	:= Nil
Static lSe5MsFil 	:= Nil
Static lSeiMsFil 	:= Nil
Static lDescSe2 	:= Nil
Static lEdF100		:= Nil
Static lEdCprb		:= Nil
Static lF100TQry	:= Nil

Static __aSE1Stru	:= Nil 
Static __aSE2Stru	:= Nil 
Static __aSEIStru	:= Nil 
Static __aSE5Stru	:= Nil 
Static __lAjstSx	:= FindFunction("AjstSxSPD")

#include "protheus.ch"
#include "MATXATU.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ AxVisual ³ Autor ³ Jorge Queiroz         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa generico para visualizacao 		                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ AxVisual(ExpC1,ExpN1,ExpN2,ExpA1,ExpN3,ExpC1)              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³			 ³ ExpN2 = Numero do registro                                 ³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        ³±±
±±³			 ³ ExpA4 = Array com os campos a serem mostrados              ³±±
±±³			 ³ ExpN5 = Coluna a ser impressa uma determinada mensagem	  ³±±
±±³			 ³ ExpC6 = Mensagem a ser impressa                            ³±±
±±³			 ³ ExpC7 = Funcao a ser executada antes de entrar na tela	  ³±±
±±³			 ³ ExpA8 = Array com os botoes da EnchoiceBar            	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION AxVisual(cAlias,nReg,nOpc,aAcho,nColMens,cMensagem,cFunc,aButtons,lMaximized,cTela,;
						lPanelFin,oFather,oEnc01,lCriaBut,aDim,cStack,aCpos)

Local aArea    := GetArea()
Local aPosEnch := {}
Local nOpcA    := 3
Local cCpoFil  := PrefixoCpo(cAlias)+"_FILIAL"
Local cMemo    := ""
LOcal nX       := 0
Local oDlg
Local lVirtual:=.F. // Qdo .F. carrega inicializador padrao nos campos virtuais
Local nTop
Local nLeft
Local nBottom
Local nRight
Local oSize
Local lProperty := .F.

Private Altera :=.F.
Private Inclui :=.F.
Private aTELA[0][0]
Private aGETS[0]

//verifica a criacao ou nao da barra de botoes (enchoicebar) quando chamado do Gestor Financeiro
DEFAULT lCriaBut := .F.
DEFAULT lPanelFin := .F.
DEFAULT oFather:= If(lPanelFin,FinWindow:GetVisPanel(),NIL)
DEFAULT aCpos :={}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento efetuado na variavel "cFilDep", utilizada na consulta padrao "QDD". ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("cFilDep") ==  "U"
   cFilDep := xFilial("QAD") // Utilizada no SXB
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VerIfica se esta' vizualizando um registro da mesma filial            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea(cAlias)

If (If(!lPanelFin,(cAlias)->(FieldGet(FieldPos(cCpoFil))) == xFilial(cAlias),.T.)) .OR. FunName() = 'EDAPP'
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo						     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RegToMemory(cAlias, .F., If(lPanelFin,.T., .F.),,cStack )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa variaveis para campos Memos Virtuais						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Type("aMemos")=="A"
		For nX := 1 To Len(aMemos)
			cMemo := aMemos[nX][2]
			If ExistIni(cMemo)
				&cMemo := InitPad(SX3->X3_RELACAO)
			Else
				&cMemo := ""
			EndIf
		Next nX
	EndIf
	If cFunc != NIL
		lVirtual:=.T.
		&cFunc.()
	EndIf

	If nOpc == Nil
		nOpc := 2 //Visualizar
	EndIf

	If !lPanelFin

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula as dimensoes dos objetos                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize := FwDefSize():New( .T. ) // Com enchoicebar

		oSize:lLateral     := .F.  // Calculo vertical

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria Enchoice                                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize:AddObject( "ENCHOICE", 100, 60, .T., .T. ) // Adiciona enchoice

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Dispara o calculo                                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize:Process()

		nTop    := oSize:aWindSize[1]
		nLeft   := oSize:aWindSize[2]
		nBottom := oSize:aWindSize[3]
		nRight  := oSize:aWindSize[4]

		If IsPDA()
			nTop := 0
			nLeft := 0
			nBottom := PDABOTTOM
			nRight  := PDARIGHT
		EndIf

		DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd STYLE nOr(WS_VISIBLE,WS_POPUP)

		If lMaximized <> NIL
			oDlg:lMaximized := lMaximized
		EndIf

		If IsPDA()
			aPosEnch := {,,(oDlg:nClientHeight - 4)/2,}  // ocupa todo o  espaço da janela
			oEnc01:= MsMGet():New( cAlias, nReg, nOpc ,,,,aAcho,aPosEnch,,,nColMens,cMensagem,,,,lVirtual,.t.,,,,,,,, cTela )
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
		Else

			aPosEnch := {oSize:GetDimension("ENCHOICE","LININI"),;
				 oSize:GetDimension("ENCHOICE","COLINI"),;
				 oSize:GetDimension("ENCHOICE","LINEND"),;
				 oSize:GetDimension("ENCHOICE","COLEND")}
			if Type("aRotina") == 'A'
				If nOpc > len(aRotina)
					lProperty := .T.
				EndIf
			endif
			If nColMens != NIL
				oEnc01:= MsMGet():New( cAlias, nReg, nOpc ,,,,aAcho,aPosEnch,aCpos,,nColMens,cMensagem,,,,lVirtual,,,,lProperty,,,,, cTela)
				oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
			Else
				oEnc01:= MsMGet():New( cAlias, nReg, nOpc ,,,,aAcho,aPosEnch,aCpos,,,,,,,lVirtual,,,,lProperty,,,,, cTela)
				oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
			EndIf
		EndIf
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpcA := 1,oDlg:End()},{|| nOpcA := 2,oDlg:End()},,aButtons)

	Else

		If lCriaBut

			//dimensoes do painel visual contido no Gestor Financeiro
			//aDim := FinWindow:DimObj("panel_visual")

			//Visualizacao no Gestor Financeiro com enchoicebar
			DEFINE MSDIALOG ___oDlg OF oFather:oWnd FROM 0, 0 TO 0, 0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )

			aPosEnch := {,,,}
			oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,aAcho,aPosEnch,,,,,,___oDlg,,lVirtual,,,,,,,,,cTela)
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT

			// posiciona dialogo sobre a celula
			___oDlg:nWidth := aDim[4]-aDim[2]

			ACTIVATE MSDIALOG ___oDlg ON INIT (FaMyBar(___oDlg,{|| nOpcA := 1,___oDlg:End(),},	{|| nOpcA := 2,___oDlg:End()},aButtons), ___oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]) )

			If Type("FinWindow") <> "U"
				FinVisual(cAlias,FinWindow,(cAlias)->(Recno()))
			EndIf
		Else
			//Visualizacao no Gestor Financeiro sem enchoicebar
			aPosEnch := {,,,}
			oEnc01:= MsMGet():New( cAlias, nReg, 1,,,,aAcho,aPosEnch,,,,,,oFather,,lVirtual,,,,,,,,,cTela)
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
		Endif
	Endif

Else
	Help(" ",1,"A000FI")
	nOpcA := 3
EndIf

RestArea(aArea)
Return(nOpcA)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ AxAltera ³ Autor ³ Jorge Queiroz 	     ³ Data ³	        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa generico para alteracao 						  		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN1 := AxAltera(ExpC1,ExpN2,ExpN3,ExpA1,ExpA2)			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³			 ³ ExpN2 = Numero do registro                                 ³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        ³±±
±±³			 ³ ExpA4 = Array com os campos a serem mostrados              ³±±
±±³			 ³ ExpN5 = Coluna a ser impressa uma determinada mensagem	  ³±±
±±³			 ³ ExpC6 = Mensagem a ser impressa                            ³±±
±±³			 ³ ExpC7 = Funcao de validacao para confirmacao dos dados	  ³±±
±±³			 ³ ExpC8 = Funcao a ser chamada durante a transacao           ³±±
±±³			 ³ ExpC9 = Funcao a ser chamada antes da interface            ³±±
±±³			 ³ ExpAA = Array com os botoes da EnchoiceBar            	  ³±±
±±³			 ³ ExpAB = Array de codeblock de execucao                	  ³±±
±±³			 ³ ExpAC = Array da rotina automatica                    	  ³±±
±±³			 ³ ExpLD = Carrega os campos virtuais? (.T.=Sim/.F.=Nao		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION AxAltera(cAlias,nReg,nOpc,aAcho,aCpos,nColMens,cMensagem,cTudoOk,cTransact,cFunc,;
				aButtons,aParam,aAuto,lVirtual,lMaximized,cTela,lPanelFin,oFather,aDim,uArea,lFlat)

Local aArea    := GetArea(cAlias)
Local aPosEnch := {}
Local bCampo   := {|nCPO| Field(nCPO) }
Local bOk      := Nil
Local bOk2     := {|| .T.}
Local cCpoFil  := PrefixoCpo(cAlias)+"_FILIAL"
Local cMemo    := ""
Local nOpcA    := 0
Local nX       := 0
Local oDlg
Local nTop
Local nLeft
Local nBottom
Local nRight
Local cAliasMemo
Local bEndDlg := {|lOk| lOk:=oDlg:End(), nOpcA:=1, lOk}
Local oEnc01
Local oSize

Private aTELA[0][0]
Private aGETS[0]

DEFAULT lVirtual:= .F.
DEFAULT cTudoOk := ".T."
DEFAULT nReg    := (cAlias)->(RecNO())
DEFAULT bOk := &("{|| "+cTudoOk+"}")
DEFAULT lPanelFin := .F.
DEFAULT lFlat := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento de codeblock de validacao de confirmacao            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(aParam)
	bOk2 := aParam[2]
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³VerIfica se esta' alterando um registro da mesma filial               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea(cAlias)
If (cAlias)->(FieldGet(FieldPos(cCpoFil))) == xFilial(cAlias)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a entrada de dados do arquivo						     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SoftLock(cAlias)
		RegToMemory(cAlias,.F.,lVirtual)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa variaveis para campos Memos Virtuais		 			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Type("aMemos")=="A"
			For nX:=1 to Len(aMemos)
				cMemo := aMemos[nX][2]
				If ExistIni(cMemo)
					&cMemo := InitPad(SX3->X3_RELACAO)
				Else
					&cMemo := ""
				EndIf
			Next nX
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa variaveis para campos Memos Virtuais		 			  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( ValType( cFunc ) == 'C' )
		    If ( !("(" $ cFunc) )
			   cFunc+= "()"
			EndIf
			&cFunc
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processamento de codeblock de antes da interface                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aParam)
			Eval(aParam[1],nOpc)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Envia para processamento dos Gets				   	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aAuto == Nil
		   If !lPanelFin .AND. !lFlat

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula as dimensoes dos objetos                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSize := FwDefSize():New( .T. ) // Com enchoicebar

				oSize:lLateral     := .F.  // Calculo vertical

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria Enchoice                                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSize:AddObject( "ENCHOICE", 100, 60, .T., .T. ) // Adiciona enchoice

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Dispara o calculo                                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oSize:Process()

				nTop    := oSize:aWindSize[1]
				nLeft   := oSize:aWindSize[2]
				nBottom := oSize:aWindSize[3]
				nRight  := oSize:aWindSize[4]

				If IsPDA()
					nTop := 0
					nLeft := 0
					nBottom := PDABOTTOM
					nRight  := PDARIGHT
				EndIf
				// Build com correção no tratamento dos controles pendentes na dialog ao executar o método End()
				bEndDlg := {|lOk| If(lOk:=oDlg:End(),nOpcA:=1,nOpcA:=3), lOk}

				DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd STYLE nOr(WS_VISIBLE,WS_POPUP)

				If lMaximized <> NIL
					oDlg:lMaximized := lMaximized
				EndIf

				If IsPDA()
					oEnc01:= MsMGet():New( cAlias, nReg, nOpc,     ,"CRA",oemtoansi(STR0004),aAcho,  aPosEnch,aCpos, ,nColMens,If(nColMens != Nil,cMensagem,NIL),cTudoOk,,lVirtual,.t.,,,,,,,,, cTela) //"Quanto …s altera‡”es?"
					oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
				Else

					aPosEnch := {oSize:GetDimension("ENCHOICE","LININI"),;
						 oSize:GetDimension("ENCHOICE","COLINI"),;
						 oSize:GetDimension("ENCHOICE","LINEND"),;
						 oSize:GetDimension("ENCHOICE","COLEND")}

					If nColMens != Nil
						oEnc01:= MsMGet():New( cAlias, nReg, nOpc, ,"CRA",oemtoansi(STR0004),aAcho,aPosEnch,aCpos,,nColMens,cMensagem,cTudoOk,,,lVirtual,,,,,,,,, cTela) //"Quanto …s altera‡”es?"
					Else
						oEnc01:= MsMGet():New( cAlias, nReg, nOpc, ,"CRA",oemtoansi(STR0004),aAcho,aPosEnch,aCpos,,,,cTudoOk,,,lVirtual,,,,,,,,, cTela) //"Quanto …s altera‡”es?"
					EndIf
					oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
				EndIf
				ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| IIf(Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc),Eval(bEndDlg),(nOpcA:=3,.F.))},{|| nOpcA := 3,oDlg:End()},,aButtons,nReg,cAlias)
			Else

				DEFINE MSDIALOG ___oDlg OF oFather:oWnd  FROM 0, 0 TO 0, 0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )

				aPosEnch := {,,,}
				oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,"CRA",oemtoansi(STR0004),aAcho,aPosEnch,aCpos,,,,cTudoOk,___oDlg  ,,lVirtual,.F.,,,,,,,,cTela) //"Quanto …s altera‡”es?"
				oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT

				bEndDlg := {|lOk| If(lOk:=___oDlg:End(),nOpcA:=1,nOpcA:=3), lOk}

				// posiciona dialogo sobre a celula
				___oDlg:nWidth := aDim[4]-aDim[2]
				ACTIVATE MSDIALOG ___oDlg  ON INIT (FaMyBar(___oDlg,{|| If(Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc),Eval(bEndDlg),(nOpcA:=3,.f.))},{|| nOpcA := 3,___oDlg:End()},aButtons), ___oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]) )

			Endif
		Else
			If EnchAuto(cAlias,aAuto,{|| Obrigatorio(aGets,aTela) .And. Eval(bOk).And.Eval(bOk2,nOpc)},nOpc,aCpos)
				nOpcA := 1
			EndIf
		EndIf
		(cAlias)->(MsGoTo(nReg))
		If nOpcA == 1
			Begin Transaction
				RecLock(cAlias,.F.)
				For nX := 1 TO FCount()
					FieldPut(nX,M->&(EVAL(bCampo,nX)))
				Next nX
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Grava os campos Memos Virtuais					  				  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Type("aMemos") == "A"
					For nX := 1 to Len(aMemos)
						cVar := aMemos[nX][2]
						cVar1:= aMemos[nX][1]
						//Incluído parametro com o nome da tabela de memos => para módulo APT
						cAliasMemo := If(len(aMemos[nX]) == 3,aMemos[nX][3],Nil)
						MSMM(&cVar1,TamSx3(aMemos[nX][2])[1],,&cVar,1,,,cAlias,aMemos[nX][1],cAliasMemo)
					Next nX
				EndIf
				If cTransact != Nil
					If !("("$cTransact)
						cTransact+="()"
					EndIf
					&cTransact
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Processamento de codeblock dentro da transacao                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty(aParam)
					Eval(aParam[3],nOpc)
				EndIf
			End Transaction
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Processamento de codeblock fora da transacao                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(aParam)
				Eval(aParam[4],nOpc)
			EndIf
		EndIf
	Else
		nOpcA := 3
	EndIf
Else
	Help(" ",1,"A000FI")
	nOpcA := 3
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade dos dados                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MsUnLockAll()
RestArea(aArea)

If lPanelFin
	FinVisual(cAlias,uArea,(cAlias)->(Recno()))
Endif

Return(nOpcA)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄ¿±±
±±³Fun‡…o	 ³ AxInclui ³ Autor ³ Jorge Queiroz			  ³ Data ³			  				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa generico para iclusao									  			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AxInclui(ExpC1,ExpN1,ExpN2,ExpA1,ExpC2) 				  				³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   					³±±
±±³			 ³ ExpN2 = Numero do registro                                 					³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        					³±±
±±³			 ³ ExpA4 = Array com os campos a serem mostrados              					³±±
±±³			 ³ ExpC5 = Funcao de validacao para confirmacao dos dados	  					³±±
±±³			 ³ ExpL6 = Controle da CondPad                                					³±±
±±³			 ³ ExpC7 = Funcao a ser chamada durante a transacao           					³±±
±±³			 ³ ExpA8 = Array com os botoes da EnchoiceBar            	  					³±±
±±³			 ³ ExpA9 = Array de codeblock de execucao                	  					³±±
±±³			 ³ ExpAA = Array da rotina automatica                    	 					³±±
±±³			 ³ ExpAB = Inicializa os campos virtuais?                     					³±±
±±³			 ³ Exp21 = Mantido para compatibilidade, não é mais utilizado em fontes padrões ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄ
±±³ Uso		 ³ Generico 												  					³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxInclui(cAlias,nReg,nOpc,aAcho,cFunc,aCpos,cTudoOk,lF3,cTransact,aButtons,;
						aParam,aAuto,lVirtual,lMaximized,cTela,lPanelFin,oFather,aDim,uArea,lFlat,lSubst)

Local aArea    := GetArea(cAlias)
Local aCRA     := { oemtoansi(STR0001),oemtoansi(STR0002),oemtoansi(STR0003) }//"Confirma" ### "Redigita" ### "Abandona"
Local aSvRot   := Nil
Local aPosEnch := {}
Local cMemo    := ""
Local nX       := 0
Local nOpcA    := 0
Local nLenSX8  := GetSX8Len()
Local bCampo   := {|nCPO| Field(nCPO) }
Local bOk      := Nil
Local bOk2     := {|| .T.}
Local oDlg
Local nTop
Local nLeft
Local nBottom
Local nRight
Local cAliasMemo
Local bEndDlg := {|lOk| lOk:=oDlg:End(), nOpcA:=1, lOk}
Local oEnc01
Local oSize

Private aTELA[0][0]
Private aGETS[0]

DEFAULT cTudoOk := ".T."
DEFAULT bOk     := &("{|| "+cTudoOk+"}")
DEFAULT lF3     := .F.
DEFAULT lVirtual:= .F.
DEFAULT lPanelFin := .F.
DEFAULT lFlat	  := .F.
DEFAULT lSubst	  := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento de codeblock de validacao de confirmacao            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(aParam)
	bOk2 := aParam[2]
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo							 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpc == Nil
	nOpc := 3
	If Type("aRotina") == "A"
		aSvRot := aClone(aRotina)
	EndIf
	Private aRotina := { { " "," ",0,1 } ,{ " "," ",0,2 },{ " "," ",0,3 } }
EndIf
RegToMemory(cAlias, .T., lVirtual )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa variaveis para campos Memos Virtuais (GILSON)			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("aMemos")=="A"
	For nX :=1 To Len(aMemos)
		cMemo := aMemos[nX][2]
		If ExistIni(cMemo)
			&cMemo := InitPad(SX3->X3_RELACAO)
		Else
			&cMemo := ""
		EndIf
	Next nX
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcoes executadas antes da chamada da Enchoice      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cFunc != NIL
	&cFunc.()
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Processamento de codeblock de antes da interface                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(aParam)
	Eval(aParam[1],nOpc)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia para processamento dos Gets		 			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aAuto == Nil
	If !lPanelFin .AND. !lFlat

		// Verifica se eh substituicao de titulo e define emissao e vencimento conforme titulo de origem
		// guilhermed.santos - 10/01/2025 - Trecho movido para a função fa050IniS(FINA050.PRX) era utilizado pela função de Substituição do FINA050,
		// mantido trecho para compatibilidade entre diferentes versões dos fontes, e caso esse parâmetro seja utilizado em customizações por clientes.
		If lSubst
			M->E2_EMISSAO  := SE2->E2_EMISSAO
			M->E2_VENCTO   := SE2->E2_VENCTO
			M->E2_VENCREA  := SE2->E2_VENCREA
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula as dimensoes dos objetos                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize := FwDefSize():New( .T. ) // Com enchoicebar

		oSize:lLateral     := .F.  // Calculo vertical

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria Enchoice                                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize:AddObject( "ENCHOICE", 100, 60, .T., .T. ) // Adiciona enchoice

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Dispara o calculo                                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize:Process()

		nTop    := oSize:aWindSize[1]
		nLeft   := oSize:aWindSize[2]
		nBottom := oSize:aWindSize[3]
		nRight  := oSize:aWindSize[4]

		If IsPDA()
			nTop := 0
			nLeft := 0
			nBottom := PDABOTTOM
			nRight  := PDARIGHT
		EndIf

		// Build com correção no tratamento dos controles pendentes na dialog ao executar o método End()
		bEndDlg := {|lOk| If(lOk:=oDlg:End(),nOpcA:=1,nOpcA:=3), lOk}

		DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd STYLE nOr(WS_VISIBLE,WS_POPUP)
		If lMaximized <> NIL
			oDlg:lMaximized := lMaximized
		EndIf

		If IsPDA()
			aPosEnch := {,,,}
			oEnc01:= MsMGet():New( cAlias, nReg, nOpc, aCRA,"CRA",oemtoansi(STR0005),aAcho, aPosEnch , aCpos, , , ,cTudoOk,,lF3,lVirtual,.t.,,,,,,,,cTela) //"Quanto … inclus„o?"
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
		Else
			aPosEnch := {oSize:GetDimension("ENCHOICE","LININI"),;
				 oSize:GetDimension("ENCHOICE","COLINI"),;
				 oSize:GetDimension("ENCHOICE","LINEND"),;
				 oSize:GetDimension("ENCHOICE","COLEND")}

			oEnc01:= MsMGet():New( cAlias, nReg, nOpc, aCRA,"CRA",oemtoansi(STR0005),aAcho, aPosEnch , aCpos, , , ,cTudoOk,,lF3,lVirtual,,,,,,,,,cTela) //"Quanto … inclus„o?"
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
		Endif

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc),Eval(bEndDlg),(nOpcA:=3,.f.))},{|| nOpcA := 3,oDlg:End()},,aButtons)

	ElseIf lPanelFin .OR. lFlat

		DEFINE MSDIALOG ___oDlg OF oFather:oWnd FROM 0, 0 TO 0, 0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )

		aPosEnch := {,,,}
		oEnc01:= MsMGet():New( cAlias, nReg, nOpc, aCRA,"CRA",oemtoansi(STR0005),aAcho,aPosEnch,aCpos,,,,cTudoOk,___oDlg,lF3,lVirtual,.F.,,,,,,,,cTela) //"Quanto … inclus„o?"
		oEnc01:oBox:Align := CONTROL_ALIGN_ALLCLIENT

		bEndDlg := {|lOk| If(lOk:=___oDlg:End(),nOpcA:=1,nOpcA:=3), lOk}

      // define dimenção da dialog
		___oDlg:nWidth := aDim[4]-aDim[2]

		ACTIVATE MSDIALOG ___oDlg  ON INIT (FaMyBar(___oDlg,{|| If(Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc),Eval(bEndDlg),(nOpcA:=3,.f.))},{|| nOpcA := 3,___oDlg:End()},aButtons), ___oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]) )

		(cAlias)->(MsGoto( nReg ) )

	EndIf

	If lF3  // Esta na conpad, desabilita o trigger por execblock
		SetEntryPoint(.f.)
	EndIf
Else
	If EnchAuto(cAlias,aAuto,{|| Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc)},nOpc,aCpos)
		nOpcA := 1
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravacao da enchoice                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcA == 1
	Begin Transaction
		RecLock(cAlias,.T.)
		For nX := 1 TO FCount()
			If "_FILIAL"$FieldName(nX)
				FieldPut(nX,xFilial(cAlias))
			Else
				FieldPut(nX,M->&(EVAL(bCampo,nX)))
			EndIf
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava os campos Memos Virtuais         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Type("aMemos") == "A"
			For nX := 1 to Len(aMemos)
				cVar := aMemos[nX][2]
				//Incluído parametro com o nome da tabela de memos => para módulo APT
				cAliasMemo := If(len(aMemos[nX]) == 3,aMemos[nX][3],Nil)
				MSMM(,TamSx3(aMemos[nX][2])[1],,&cVar,1,,,cAlias,aMemos[nX][1],cAliasMemo)
			Next nX
		EndIf
		// Desbloqueio do registro inserido - COMMIT
		If cModulo == "FIN"
			(cAlias)->(MsUnLock())
		EndIf
		While ( GetSX8Len() > nLenSX8 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Confirma a numeracao com a verificacao do numero gravado ativado  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ConfirmSx8()
		EndDo
		If cTransact != Nil
			If !("("$cTransact)
				cTransact+="()"
			EndIf
			&cTransact
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processamento de codeblock dentro da transacao                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aParam)
			Eval(aParam[3],nOpc)
		EndIf
	End Transaction
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento de codeblock fora da transacao                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(aParam)
		Eval(aParam[4],nOpc)
	EndIf
Else
	While ( GetSX8Len() > nLenSX8 )
		RollBackSX8()
	EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade dos dados                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aSvRot != Nil
	aRotina := aClone(aSvRot)
EndIf
RestArea(aArea)
lRefresh := .T.

If lPanelFin
	FinVisual(cAlias,uArea,(cAlias)->(Recno()))
Endif

Return(nOpcA)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Obrigatori³ Autor ³ Jorge Queiroz 		³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ VerIfica se todos os campos obrigatorios foram digitados.  ³±±
±±³			 ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpL1 := Obrigatori(ExpA1,ExpA2) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Retorna .F. se algum campo obrigatorio esta vazio  ³±±
±±³			 ³ ExpA1 = Array com os campos a serem verIficados 			  ³±±
±±³			 ³ ExpA2 = Array com os titulos da tela						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION Obrigatorio(aGets,aTela,tObg,lShow)
Local i,bCampo,x,y, cText := "", cByte
Local aPos := {STR0051, STR0052, STR0053, STR0054, STR0055, STR0056, STR0057, STR0058, STR0059, STR0060} //"Primeira","Segunda","Terceira","Quarta","Quinta","Sexta","Setima","Oitava","Nona","Ultima"
Local cFold := STR0061 //"Pasta"

DEFAULT lShow := .T.

bCampo := {|nCPO| Field(nCPO) }
tObg:=""
FOR i:=1 TO LEN(aGETS)
	If SubStr(aGETS[i],25,1) == "T" .AND. Empty(M->&(SubStr(aGETS[i],9,10)))
		AchaATELA(@x,@y,i,aTela)
		If x*y > 0
			tObg:=tObg+aTELA[x][y]
			If Empty(ctext)
				cByte := Subs(aGets[i],27,1)
				If cByte == " "
					cText := Subs(aTela[x,y],1,18)
				ElseIf cByte$"123456789"
					cText := Subs(aTela[x,y],1,18)+" -> "+aPos[Val(cByte)]+" "+cFold
				Else
					cText := Subs(aTela[x,y],1,18)+" -> "+aPos[10]+" "+cFold
				EndIf
			EndIf
		Else
			tObg := "VV"
		EndIf
	EndIf
NEXT i

cText := cText + Space(50-Len(cText))

If LEN(tObg) > 0
	If lShow
		Help(1," ","OBRIGAT",,cText,3,0)
	EndIf
	Return .F.
EndIf
Return .T.

Static Function AchaAtela(x,y,i,aTELA)
Local ni, nJ:= 0
For ni:=1 to Len(aTela)
	If !Empty(aTela[ni,1])
		nJ++
	EndIf
	If nJ == i
		x := ni
		y := 1
		Return Nil
	EndIf
	If !Empty(aTela[ni,3])
		nJ++
	EndIf
	If nJ == i
		x := ni
		y := 3
		Return Nil
	EndIf
Next
Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxPesqui	³ Autor ³ Jorge Queiroz 		³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisas genericas										  ³±±
±±³			 ³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ AxPesqui()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ 															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxPesqui()
Local oDlg, oCbx, cOrd, oBigGet
Local nSavReg, cAlias, ni, nj
Local cCpofil, dCampo
Local nOrd    := 1
Local lSeek   := .F.
Local aLista  := {}
Local bSav12  := SetKey(VK_F12)
Local cCampo  := Space(60)

Local lDetail := .F.
Local lUseDetail := .F.
Local aAllLista
Local oDetail
Local aMyOrd	:= {}
Local aScroll	:= {}
Local lSeeAll   := GetBrwSeeMode()
Local aPesqVar  := {}
Local cVar
Local bBloco
Local cMsg := ""
Local oPPreview
Local oList
Local aList    := {}
Local lMenuDef := ( ProcName(1) == "MBRBLIND" ) .Or. RunInMenuDef()
Local lPreview := .F.
Local nRet     := 0

Private aOrd     := {}

/*/
aLista
[1] := F3
[2] := tipo
[3] := tamanho
[4] := decimais
[5] := titulo
/*/

SetKey(VK_F12,{|| NIL})

cAlias := Alias()
DbSelectArea(cAlias)
cCpofil := PrefixoCpo(cAlias)+"_FILIAL"
nSavReg := Recno()

If At(cCpoFil,Indexkey())==1 .And. !lSeeAll
	If &cCpofil!=cFilial
		DbSeek(cFilial)
	Endif
Else
	DbGoTop()
EndIf

If Eof()
	If lMenuDef
		Help(" ",1,"ARQVAZIO")
	Else
		Help(" ",1,"A000FI")
	EndIf
	SetKey(VK_F12,bSav12)
	Return 3
EndIf

If nSavReg!=Recno()
	DbGoTo(nSavReg)
Endif

AxPesqOrd(cAlias,@aMyOrd,@lUseDetail,lSeeAll)

nOrd := 1
cOrd := aOrd[1]
For nI := 1 To Len(aOrd)
	aOrd[nI] := OemToAnsi(aOrd[nI])
Next

If IndexOrd() > Len(aOrd)
	cOrd := 1 //aOrd[Len(aOrd)]
	nOrd := 1 //Len(aOrd)
ElseIf IndexOrd() <= 1
	cOrd := aOrd[1]
	nOrd := 1
Else
	cOrd := aOrd[IndexOrd()]
	nOrd := IndexOrd()
EndIf

If lUseDetail .and. !PesqList(cAlias,lSeeAll,@aPesqVar,@aAllLista,@cMsg)
	Help(,,"PESQLIST",,STR0048+cMsg+STR0049,1,1)	//"O campo "###" não foi encontrado no Didionário de Campos (SX3)"
	Return 0
EndIf

DEFINE MSDIALOG oDlg FROM 00,00 TO 100,490 PIXEL TITLE OemToAnsi(STR0010) //"Pesquisa"

@05,05 COMBOBOX oCBX VAR cOrd ITEMS aOrd SIZE 206,36 PIXEL OF oDlg FONT oDlg:oFont

@22,05 MSGET oBigGet VAR cCampo SIZE 206,10 PIXEL

If lMenuDef
	DEFINE SBUTTON FROM 05,215 TYPE 1 OF oDlg ENABLE ;
		ACTION If(lPreview,( DbGoto(aList[oList:nAt,Len(aList[oList:nAt])]), nRet := 1, oDlg:End() ),;
					(lPreview := AxPreview(cAlias,lDetail,cCampo,aLista,aMyOrd,nOrd,lSeeAll,aPesqVar,@oPPreview,oBigGet,aScroll,nOrd,@oList,@aList), ;
					If(!lPreview,oDlg:End(),(PesqDetail(.F.,oDlg,{},oBigGet),oCBX:Disable(),oDetail:Disable()))))

	DEFINE SBUTTON FROM 20,215 TYPE 2 OF oDlg ENABLE ACTION If(lPreview,(lPreview:= .F.,oPPreview:Hide(),PesqDetail(!lDetail,oDlg,aScroll,oBigGet,nOrd),;
															oCBX:Enable(),oDetail:Enable()),oDlg:End())
Else
	DEFINE SBUTTON FROM 05,215 TYPE 1 OF oDlg ENABLE ACTION (lSeek := .T.,oDlg:End())
	DEFINE SBUTTON FROM 20,215 TYPE 2 OF oDlg ENABLE ACTION oDlg:End()
EndIf

If ( lUseDetail )
	@22,05 MSPANEL oPPreview SIZE 205,84 OF oDlg
	oPPreview:Hide()
	DEFINE SBUTTON oDetail FROM 35,215 TYPE 5 OF oDlg ENABLE ONSTOP STR0032 ACTION (lDetail := PesqDetail(lDetail,@oDlg,@aScroll,@oBigGet,nOrd,oPPreview),;
																					If(lMenuDef,(lPreview:= .F.,oPPreview:Hide()),)) //"Detalhes"

	For ni := 1 To Len(aAllLista)
		Aadd(aScroll,NIL)
		@22,05 SCROLLBOX aScroll[ni] VERTICAL SIZE 84,205 BORDER
		aScroll[ni]:Hide()

		For nj := 1 To Len(aAllLista[ni])
			cVar := "aPesqVar["+StrZero(ni,2)+"]["+StrZero(nj,2)+"]"
			bBloco  := &("{ | u | If( PCount() == 0, "+cVar+","+cVar+" := u)}")
			PesqInit(aAllLista[ni],aScroll[ni],nj,bBloco,cVar)
		Next
	Next

	oCbx:bChange := {|| PesqChange(@nOrd,oCbx:nAt,@aLista,cAlias,@aAllLista,@aScroll,@lDetail,@oDetail,@oDlg,@oBigGet) }
	aLista := Aclone(aAllLista[nOrd])
Else
	oCbx:bChange := {|| nOrd := oCbx:nAt}
EndIf

ACTIVATE MSDIALOG oDlg CENTERED

If ( lSeek )
	nRet := 1
	AxPesqSeek(cAlias,lDetail,cCampo,aLista,aMyOrd,nOrd,lSeeAll,aPesqVar)
	SetKey(VK_F12,bSav12)
Else
	SetKey(VK_F12,bSav12)
EndIf

Return nRet

Function AxPesqSeek(cAlias,lDetail,cCampo,aLista,aMyOrd,nOrd,lSeeAll,aPesqVar,lPreview)
Local nReg
Local nChave  := 1
Local cFil    := ""
Local cCpofil := PrefixoCpo(cAlias)+"_FILIAL"
Local aFils	  := {}
Local nI
Local aRet    := {}
Local lRet    := .T.
Local nSizeFil := 2
Local nJ	  := 0
Local lEOF	  := .F.
Local nPosFil := 0
Default lPreview := .F.

//-- Atualiza o conteúdo da filial
nSizeFil := FWSizeFilial()

DbSelectArea(cAlias)
DbSetOrder(aMyOrd[nOrd, 1])
If At(cCpoFil,Indexkey())==1  //Procura pela filial
	cFil := cFilial
	nChave := 11
	//--------------------------------
	//Localiza posição do campo filial
	//--------------------------------
	nPosFil	:=	AScan( aLista, {|x| AllTrim(x[7]) == AllTrim(cCpoFil)	} )

EndIf

If ( lDetail )
	cCampo := ""
	For nI := 1 To Len(aPesqVar[nOrd])
		If ( nPosFil == nI)
			//--------------------------------------------------------------------------
			//Se é o campo filial, deve trazer o valor de acordo com o compartilhamento
			//--------------------------------------------------------------------------
			cCampo	+=	FWxFilial(cAlias, aPesqVar[nOrd][nI])
		ElseIf ( aLista[nI][2] == "C" .And. (Len(cCampo) <> aLista[nI][3]) )
			cCampo += Subs(aPesqVar[nOrd][nI],1,aLista[nI][3])
		ElseIf ( aLista[nI][2] == "D" )
			cCampo += Dtos(aPesqVar[nOrd][nI])
		ElseIf ( aLista[nI][2] == "N" )
			cCampo += Str(aPesqVar[nOrd][nI],aLista[nI][3],aLista[nI][4])
		Else
			cCampo += aPesqVar[nOrd][nI]
		EndIf
	Next
EndIf
cCampo := Trim(cCampo)
nReg := Recno()
SET SOFTSEEK ON

If !lDetail .and. (("DTOS" $ Upper(IndexKey())) .Or. ("DTOC" $ Upper(IndexKey()))) .And. cAlias != "SM2"
	If ( lSeeAll )
		If !EMPTY(cFil)
			cCampo := Subs(cCampo,1,nSizeFil) + ConvData(IndexKey(), Subs(cCampo,nSizeFil+1))
		Else
			cCampo := ConvData(IndexKey(), cCampo)
		EndIf
	Else
		cCampo := ConvData(IndexKey(), cCampo)
	EndIf
EndIf

If Subs(cAlias,1,3) == "SM2"
	dCampo:=Ctod(AllTrim(cCampo))
	DbSeek(dCampo)
ElseIf !(At(cCpoFil,Indexkey())==1) .or. !aMyOrd[nOrd, 2] .or. (lSeeAll .And. !Empty(cCampo))
	DbSeek(cCampo)
	If lPreview
		cIndexKey := IndexKey()
		Aadd( aRet, Recno() )
		DbSkip()
		While !Eof() .And. lRet .And. Len(aRet) <= 10
			If cCampo $ &(cIndexKey)
				Aadd( aRet, Recno() )
			Else
				lRet := .F.
			EndIf
			DbSkip()
		EndDo
		DbGoTo(aRet[1])
	ElseIf ( lSeeAll .And. At(cCpoFil,Indexkey())==1)
		aFils	:= GetBrwFils()
		cFil	:= &cCpoFil
		// Testa se o usuario pode ver a filial encontrada.
		// Se não puder posiciona na proxima filial valida para o usuário, se não puder vai para EOF
		If ( AScan(aFils, cFil ) == 0 )
			If ( (nI:=AScan(aFils, {|x| x > cFil} )) == 0 )
				DbSeek(chr(255))
				cFil	:= &cCpoFil
				If ( AScan(aFils, cFil ) == 0 )
					lEOF := .T.
				EndIf
			Else
				DbSeek(aFils[nI])
			EndIf
		EndIf
	EndIf
Else
	DbSeek(cFilial+cCampo)
	If Subs(&(IndexKey()),1,nSizeFil) != cFilial	 // IR Para EOF
		DbSeek(chr(255))
	ElseIf lPreview
		Aadd( aRet, Recno() )
		DbSkip()
		While !Eof() .And. lRet .And. Len(aRet) <= 10
			If cFilial+cCampo $ &(Indexkey())
				Aadd( aRet, Recno() )
			Else
				lRet := .F.
			EndIf
			DbSkip()
		EndDo
		DbGoTo(aRet[1])
	EndIf
EndIf

If Eof() .Or. lEof
	DbGoTo(nReg)
	Help(" ",1,"PESQ01")
EndIf
SET SOFTSEEK OFF
lRefresh := .t.
Return aRet

Function AxPesqOrd(cAlias,aMyOrd,lUseDetail,lSeeAll,aRetOrd)
Local aTemp := {}
Local cIndice
Local nI

If Type("aOrd") == "U"
	Private aOrd := {}
EndIf

If !(cAlias $ "SX6/SX9/SX7")
	PesqOrd(cAlias,@aMyOrd, lSeeAll)

	If ( !Empty(aMyOrd) .And. ValType(aMyOrd[1]) != 'A' )

		aTemp	:= AClone(aMyOrd)
		aMyOrd	:= {}

		For nI := 1 To Len(aTemp)
			Aadd( aMyOrd, { nI, aTemp [nI] } )
		Next

	EndIf

	lUseDetail := .T.
ElseIf cAlias == "SX7"
	cIndice	:= OemToAnsi(STR0043) // " Campo+Sequencia"
	Aadd(aOrd," "+cIndice)
	AADD(aMyOrd, { 1, .T. } )
	lUseDetail := .F.
ElseIf cAlias == "SX6"
	cIndice	:= OemToAnsi(STR0006) // " Filial+Parametro"
	Aadd(aOrd," "+cIndice)
	AADD(aMyOrd, { 1, .T. } )
	lUseDetail := .F.
Else
	cIndice	 := OemToAnsi(STR0007) //" Dominio+C.Dominio"
	Aadd(aOrd," "+cIndice)
	AADD(aMyOrd, { 1, .T. } )
	lUseDetail := .F.
EndIf

aRetOrd := aOrd
Return

Function PesqChange(nOrd,nAt,aLista,cAlias,aAllLista,aScroll,lDetail,oDetail,oDlg,oBigGet)

If nOrd <> nAt .and. aAllLista <> NIL
	aLista := Aclone(aAllLista[nAt])
	aScroll[nOrd]:Hide()

	If Empty(aLista)
			lDetail := .T.
			lDetail := PesqDetail(lDetail,@oDlg,@oScroll,@oBigGet)
			oDetail:Disable()
	Else
		oDetail:Enable()
		EndIf

	If lDetail
		aScroll[nAt]:Show()
	EndIf
	nOrd := nAt
EndIf
Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ ExistIni ³ Autor ³ Ary Medeiros			³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ VerIfica se campo tem inicializador padrao				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ ExpL1 := ExistIni(ExpC2)									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Retorna .t. se existir							  ³±±
±±³			 ³ ExpC2 = Campo a ser verIficado							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ExistIni(cCpo,lPosSX3)

DEFAULT lPosSX3 := .T.

If lPOSSX3
	SX3->(dbSetOrder(2))
	SX3->(MsSeek(cCpo))
	SX3->(dbSetOrder(1))
EndIf

Return(!Empty(GetSx3Cache(cCpo,"X3_RELACAO")))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ InitPad	³ Autor ³ Ary Medeiros		    ³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inicializa campo de acordo com formula pre-definida		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ InitPad(ExpC1) 											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Formula a ser executada							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function InitPad(xForm,cTitulo)

Local bBlock:=ErrorBlock()
Local bErro := errorBlock( { |e| InitErr(e) } )
Local xRet

DEFAULT cTitulo := X3Titulo()

If STR0011$xForm //"FORMULA"
	If !('"'$xForm)
		xForm := Substr(xForm,1,8)+'"'+Substr(xForm,9,3)+'")'
	EndIf
EndIf

BEGIN SEQUENCE
xRet := &xForm
End SEQUENCE
ErrorBlock(bBlock)
//If "FORMULA"$xForm
If xRet == nil
	HELP(" ",1,"INITERR",, cTitulo,4,2)
EndIf
//EndIf
Return xRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ InitErr	³ Autor ³ Ary Medeiros			  ³ Data ³			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Controle de Erro no Inicializador padrao						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ InitErr(ExpO1) 														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 = Objeto Error 												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function InitErr(e)
If ( e:genCode == EG_ZERODIV )
	Return (0)
EndIf
If (e:GenCode == EG_NOALIAS)
	Chkfile(e:Operation)
	Return
EndIf
If e:gencode > 0
	Tone(3000,1)
	HELP(" ",1,"INITERR",, X3Titulo(),4,2)
	BREAK
EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ ProxReg	³ Autor ³ Ary Medeiros			  ³ Data ³		     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna ultimo registro incrementado						     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpC1 := ProxReg(ExpN2,ExpN3,ExpN4) 						     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Retorna Registro incrementado							  ³±±
±±³			 ³ ExpN2 = Incremento													  ³±±
±±³			 ³ ExpN3 = Posicoes utilizadas										  ³±±
±±³			 ³ ExpN4 = Indice Utilizado											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ProxReg(nInc,nPos,nIndice)
Local nSavRec := 0
Local cRet := ""
Local nOrdem:=IndexOrd()
Local nSizeFil := 2
nIndice := Iif(nIndice==NIL,1,nIndice)
//-- Atualiza o conteúdo da filial
nSizeFil := FWSizeFilial()

nSavRec := RecNo()
DbSetOrder(nIndice)
Set SoftSeek On
DbSeek(SubStr(cFilial,1,nSizeFil-1)+Chr(ASC(Substr(cFilial,nSizeFil-1,1))+1))
Set SoftSeek Off
DbSkip(-1)
cRet := &(SX3->X3_CAMPO)
If SX3->X3_TIPO == "N" .or. SX3->X3_TIPO == "D"
	cRet += nInc
ElseIf SX3->X3_TIPO == "C"
	cRet := StrZero(Val(cRet)+nInc,If(nPos > SX3->X3_TAMANHO,SX3->X3_TAMANHO,nPos))
	cRet := cRet+Space(SX3->X3_TAMANHO-Len(cRet))
EndIf
DbSetOrder(nOrdem)
DbGoTo(nSavRec)
Return cRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FuncAMoeda³ Autor ³ Wagner Xavier 	     ³ Data ³ 16/04/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Devolve um array com o valor do titulo em ate 5 moedas.	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := FuncAMoeda(ExpD1,ExpN1,ExpN2) 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array que sera devolvido com os 5 valores			  ³±±
±±³			 ³ ExpD1 = Data utilizada como referencia 						  ³±±
±±³			 ³ ExpN1 = Valor utilizado como referencia					     ³±±
±±³			 ³ ExpN2 = Moeda em que o titulo se encontra 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA040																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FuncTion FuncAMoeda(dData,nValor,nMoeda)
Local cAlias:=ALIAS(),aValores[5],nBase:=0,cCampo
nMoeda:=Iif((nMoeda=NIL .Or. nMoeda==0),1,nMoeda)

DbSelectArea("SM2")
Set SoftSeek On
DbSeek(dData)
Set SoftSeek Off
AFILL(aValores,0)

If nMoeda==1
	aValores[1]:=nValor
	aValores[2]:=nValor/SM2->M2_MOEDA2
	aValores[3]:=nValor/SM2->M2_MOEDA3
	aValores[4]:=nValor/SM2->M2_MOEDA4
	aValores[5]:=nValor/SM2->M2_MOEDA5
Else
	cSuf :=Str(nMoeda,1)
	cCampo:= "M2_MOEDA"+cSuf
	//  nBase:=nValor*SM2->M2_MOEDA&cSuf
	nBase:= nValor * FieldGet( FieldPos( cCampo ) )
	aValores[1]:=nBase/1
	aValores[2]:=nBase/SM2->M2_MOEDA2
	aValores[3]:=nBase/SM2->M2_MOEDA3
	aValores[4]:=nBase/SM2->M2_MOEDA4
	aValores[5]:=nBase/SM2->M2_MOEDA5
EndIf
DbSelectArea(cAlias)
Return aValores

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³RunTrigger³ Autor ³ Ary Medeiros			  ³ Data ³ 31/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa Gatilhos Primarios e Armazena Estrangeiros 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ RunTrigger(ExpN1) 													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 := Tipo (1-Enchoice 2-GetDados 3-F3						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

/*/
Function RunTrigger(nTipo,nLin,cMacro,oObj,cField)
Local cSavAlias := Alias()
Local nSavRec
Local cCpo := If(cField == NIL,SX3->X3_CAMPO,cField)
Local cResult, nElem, nSavOrd, cSeek
Local bErro
Local nPos := 0,cCompVal:=""
Local aGetsTrigger
If Type("lNoDetect") == "L"
	bErro := ErrorBlock({ |e| TriggerErr(e,SX7->X7_CAMPO,SX7->X7_SEQUENC,lNoDetect)})
Else
	bErro := ErrorBlock({ |e| TriggerErr(e,SX7->X7_CAMPO,SX7->X7_SEQUENC,.f.)})
EndIf
DbSelectArea("SX7")
nSavRec := Recno()
DbSeek(cCpo)
BEGIN SEQUENCE
While SX7->(!EOF()) .And. Alltrim(SX7->X7_CAMPO) == Alltrim(cCpo)
	If !Empty(SX7->X7_CONDIC)
		If !(&(SX7->X7_CONDIC))
			SX7->(DbSkip())
			Loop
		EndIf
	EndIf
	If SX7->X7_Tipo ==	"P"
		If SX7->X7_SEEK=="S"
			DbSelectArea(SX7->X7_ALIAS)
			nSavOrd := IndexOrd()
			DbSetOrder(SX7->X7_ORDEM)
			DbSeek(&(If(nTipo == 2,TriggerClear(SX7->X7_CHAVE,nLin,cSavAlias),SX7->X7_CHAVE)))
			DbSetOrder(nSavOrd)
			DbSelectArea("SX7")
		EndIf
		If nTipo == 1 .or. nTipo ==	3
			cResult := &(SX7->X7_REGRA)
			cMacro := "M->"+SX7->X7_CDOMIN
			If ValType(cResult) == "C"
				cResult	:= TriggerSize(SX7->X7_CDOMIN,cResult)
				&cMacro := cResult
			Else
				&cMacro := cResult
				cResult := TriggerPict(SX7->X7_CDOMIN,cResult)
			EndIf
			If nTipo == 1
				If Type("__cInternet") == "C" .and. __cInternet == "AUTOMATICO" .or. ValType(oObj) <> "O"
					If Type("aGets") <> "U"
						aGetsTrigger := aGets
					EndIf
					If Type("aTela") <> "U"
						aTelaTrigger := aTela
					EndIf
				Else
					aGetsTrigger := oObj:aGets
					aTelaTrigger := oObj:aTela
				EndIf
				If ((nElem := ASCAN(aGetsTrigger,{ |x| Substr(x,9,10) == Substr(cMacro,4)})) # 0)
					If ASC(SubStr(aGetsTrigger[nElem],1,1)) > 64
						cCompVal := Str(ASC(SubStr(aGetsTrigger[nElem],1,1))-55,2)+SubStr(aGetsTrigger[nElem],2,1)
						nPos := Val(cCompVal)
					Else
						nPos := Val(SubStr(aGetsTrigger[nElem],1,2))
					EndIf
					If nPos <= Len(aTelaTrigger)
						aTelaTrigger[nPos][Val(SubStr(aGetsTrigger[nElem],3,1))+If(Substr(aGetsTrigger[nElem],3,1)=="1",1,2)] := cResult
					End
				EndIf
			Else
				If ((nElem := ASCAN(aGets2,{|x| Substr(x,9,10) == Substr(cMacro,4)})) # 0)
					aTela2[nElem,2] := cResult
				EndIf
			EndIf
		ElseIf nTipo == 2
			cResult := &(TriggerClear(SX7->X7_REGRA,nLin,cSavAlias))
			nElem := ASCAN(aHeader,{|x| PADR(x[2],10) == PADR(SX7->X7_CDOMIN,10)})
			If nElem > 0
				If ValType(cResult) == "C"
					If Type("aCols") # "U"
						aCols[nLin,nElem] := TriggerSize(SX7->X7_CDOMIN,cResult)
					Else
						// p/ GetDadDB
						nElem := (cSavAlias)->(FieldPos(SX7->X7_CDOMIN))
						If nElem > 0
							(cSavAlias)->( FieldPut( nElem,TriggerSize(SX7->X7_CDOMIN,cResult) ) )
						EndIf
					EndIf
				Else
					If Type("aCols") # "U"
						aCols[nLin,nElem] := cResult //TriggerPict(X7_CDOMIN,cResult)
					Else
						// p/ GetDadDB
						nElem := (cSavAlias)->(FieldPos(SX7->X7_CDOMIN))
						If nElem > 0
							(cSavAlias)->(FieldPut(nElem,cResult))
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
		lRefresh := .t.
	ElseIf SX7->X7_TIPO == "X"
		DbSelectArea(SX7->X7_ALIAS)
		nSavOrd := IndexOrd()
		DbSetOrder(SX7->X7_ORDEM)
		DbSeek(&(SX7->X7_CHAVE))
		DbSetOrder(nSavOrd)
		DbSelectArea("SX7")
	Else
		nElem := ASCAN(aTrigger,{ |aX| aX[1] == SX7->X7_CAMPO .and. aX[2]==SX7->X7_SEQUENC .and.	aX[5] == nLin })
		If SX7->X7_SEEK=="S"
			DbSelectArea(SX7->X7_ALIAS)
			cSeek := &(If(nTipo == 2,TriggerClear(SX7->X7_CHAVE,nLin,cSavAlias),SX7->X7_CHAVE))
			DbSeek(cSeek)
			DbSelectArea("SX7")
		Else
			cSeek :=""
		EndIf
		If nTipo == 1
			cResult := &(SX7->X7_REGRA)
		Else
			cResult := &(TriggerClear(SX7->X7_REGRA,nLin,cSavAlias))
		EndIf
		If nElem == 0
			AADD(aTrigger,{SX7->X7_CAMPO,SX7->X7_SEQUENC,cResult,cSeek,nLin})
		Else
			aTrigger[nElem] := {SX7->X7_CAMPO,SX7->X7_SEQUENC,cResult,cSeek,nLin}
		EndIf
	EndIf
	SX7->(DbSkip())
End
End SEQUENCE
DbGoTo(nSavRec)
If !Empty(cSavAlias)
	DbSelectArea(cSavAlias)
Endif
ErrorBlock(bErro)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ TriggerSize ³ Autor ³ Ary Medeiros		  ³ Data ³ 31/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna Conteudo do campo com tamanho correto				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ TriggerSize(ExpC1,ExpC2)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Campo															  ³±±
±±³			 ³ ExpC2 = Conteudo														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TriggerSize(cCpo,cContem)
Local cSavAlias := Alias(), nSavRec,nSavOrd

DbSelectArea("SX3")
nSavRec := Recno()
nSavOrd := IndexOrd()
DbSetOrder(2)
DbSeek(cCpo)
cContem := RTrim(cContem) + Space(X3_TAMANHO-Len(RTrim(cContem)))
DbSetOrder(nSavOrd)
DbGoTo(nSavRec)
DbSelectArea(cSavAlias)
Return cContem

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ExistTrigger³ Autor ³ Ary Medeiros	     ³ Data ³ 01/09/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ VerIfica a existencia de gatilho para um campo				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExistTrigger(ExpC1)													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nome do Campo a VerIficar											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ExistTrigger(cCpo)
Local aArea := getArea()
Local nSavOrd , lRet := .f.
IF Select("SX3") > 0
	DbSelectArea("SX3")
	nSavOrd := IndexOrd()
	DbSetOrder(2)
	DbSeek(cCpo)
	If X3_TRIGGER == "S"
		lRet := .t.
	EndIf
	DbSetOrder(nSavOrd)
	RestArea(aArea)
Endif
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ EvalTrigger³ Autor ³ Ary Medeiros		  ³ Data ³ 02/09/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Avalia Gatilhos Estrangeiros										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ EvalTrigger()															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function EvalTrigger()
Local cSavAlias   := Alias()
Local aAreaSX2    := GetArea()
Local i           := 0
Local nSavOrd     := 0
Local bErro       := ErrorBlock({ |e| TriggerErr(e,SX7->X7_CAMPO,SX7->X7_SEQUENC)})
Local nInstanceId := 0
Local cFilSeek    := ""
//Default lDoEAI    := .T.
EvalGeneric()
DbSelectArea("SX7")
For i := 1 to Len(aTrigger)
	DbSeek(aTrigger[i,1]+aTrigger[i,2])
	BEGIN SEQUENCE
	If SX7->X7_TIPO = "E"
		DbSelectArea(SX7->X7_ALIAS)
		If SX7->X7_SEEK=="S"
			nSavOrd := IndexOrd()
			DbSetOrder(SX7->X7_ORDEM)
			DbSeek(aTrigger[i,4])
			DbSetOrder(nSavOrd)
		EndIf
		RecLock(SX7->X7_ALIAS,.F.)
		Replace &(SX7->X7_CDOMIN) with aTrigger[i,3]
		//dbUnLock()
		DbSelectArea("SX7")
	EndIf
	End SEQUENCE
Next i
aTrigger := {}
// ------------------------------------------------------
// Verifica se há gatilho de WF a ser disparado
// ------------------------------------------------------
If Select("XXB") == 0
	FWOpenXXB()
EndIf
If Select("XXB") <> 0
	dbSelectArea("XXB")
	dbSetOrder(1)

	// Se estrutura de filial da XXB corrigida, não usa FwSizeFilial
	If FindFunction('FwTcUnique')
		cFilSeek := Space( Len( XXB->XXB_FILIAL ) )
		MsSeek(cFilSeek+PadR(FunName(),Len(XXB->XXB_ROTINA)))
	ElseIf !MsSeek(cFilAnt+PadR(FunName(),Len(XXB->XXB_ROTINA)))
		cFilSeek := Space( FWSizeFilial() )
		MsSeek(cFilSeek+PadR(FunName(),Len(XXB->XXB_ROTINA)))
	Else
		cFilSeek := cFilAnt
	EndIf
	While !Eof() .And. ;
			XXB->XXB_FILIAL == cFilSeek .And.;
			XXB->XXB_ROTINA == PadR(FunName(),Len(XXB->XXB_ROTINA))
		If Empty(XXB->XXB_RULE) .Or. &(XXB->XXB_RULE)
			DbSelectARea("SX2")
			DbSetOrder(1)
			MsSeek(XXB->XXB_ALIAS)
			If !Empty(SX2->X2_UNICO)
				If XXB->XXB_START=="1"
					// ------------------------------------------------------
					// Verifica se o processo já existe para esta entidade
					// ------------------------------------------------------
					DbSelectArea("WFE")
					DbSetOrder(1)
					If !MsSeek(xFilial("WFE")+PadR("",Len(WFE->WFE_TPPROC))+(XXB->XXB_ALIAS)->(&(SX2->X2_UNICO)))
						nInstanceId := FWECMStartProcess(AllTrim(XXB->XXB_PROCID)/*cProcId*/,XXB->XXB_TASKID/*nNextTask*/,"It started by trigger ECM Microsiga Protheus"/*cComments*/,IIf(XXB->XXB_TYPE=="1",FWEcmXmlData(XXB->XXB_ALIAS),Nil)/*cXMLData*/,/*aAttach*/,/*cUserId*/,/*aColleagueIds*/,/*Complete*/)
						If nInstanceId <> 0
							Begin Transaction
							RecLock( "WFE" , .T. )
							WFE->WFE_FILIAL	:= xFilial("WFE")
							WFE->WFE_TPPROC	:= ""
							WFE->WFE_PRTID	:= (XXB->XXB_ALIAS)->(&(SX2->X2_UNICO))
							WFE->WFE_ECMID  := AllTrim(Str(nInstanceId,Len(WFE->WFE_ECMID)))
							MsUnLock()
							End Transaction
						EndIf
					Else
						// ------------------------------------------------------
						// Se o processo existir reinicia o fluxo
						// ------------------------------------------------------
						FWECMCancelProcess(Val(WFE->WFE_ECMID),,"Cancel by trigger ECM Microsiga Protheus")
						nInstanceId := FWECMStartProcess(AllTrim(XXB->XXB_PROCID)/*cProcId*/,XXB->XXB_TASKID/*nNextTask*/,"It started by trigger ECM Microsiga Protheus"/*cComments*/,IIf(XXB->XXB_TYPE=="1",FWEcmXmlData(XXB->XXB_ALIAS),Nil)/*cXMLData*/,/*aAttach*/,/*cUserId*/,/*aColleagueIds*/,/*Complete*/)
						If nInstanceId <> 0
							Begin Transaction
							RecLock("WFE")
							WFE->WFE_ECMID  := AllTrim(Str(nInstanceId,Len(WFE->WFE_ECMID)))
							MsUnLock()
							End Transaction
						EndIf
					EndIf
					If FWWFIsError()
						EventInsert( FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_TOTVSECM, FW_EV_FAILED, FW_EV_LEVEL_ERROR, "", "TOTVS ECM" ,FWWFGetError()[2],.T.)
					EndIf
				Else
					// ------------------------------------------------------
					// Movimentação de processo
					// ------------------------------------------------------
					DbSelectArea("SX2")
					DbSetOrder(1)
					MsSeek(XXB->XXB_SEEK)

					DbSelectArea("WFE")
					DbSetOrder(1)
					If MsSeek(xFilial("WFE")+PadR("",Len(WFE->WFE_TPPROC))+(XXB->XXB_SEEK)->(&(SX2->X2_UNICO)))
						FWECMMoveProcess(Val(WFE->WFE_ECMID),XXB->XXB_TASKID,"It moved by trigger ECM Microsiga Protheus"/*cComments*/,IIf(XXB->XXB_TYPE=="1",FWEcmXmlData(XXB->XXB_ALIAS),Nil)/*cXMLData*/,/*aAttach*/,/*cUserId*/,/*aColleagueIds*/,/*lComplete*/)
						If FWWFIsError()
							EventInsert( FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_TOTVSECM, FW_EV_FAILED, FW_EV_LEVEL_ERROR, "", "TOTVS ECM" ,FWWFGetError()[2],.T.)
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
		dbSelectArea("XXB")
		dbSkip()
	EndDo
EndIf

ErrorBlock(bErro)

//If lDoEAI
//	DoEAI()
//EndIf

RestArea(aAreaSX2)
If !Empty(cSavAlias)
	DbSelectArea(cSavAlias)
Endif
Return nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ TriggerErr ³ Autor ³ Ary Medeiros		  ³ Data ³ 02/09/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento de Erro para os Gatilhos 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ TriggerErr()								 						     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gatilhos 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TriggerErr(e,cCampo,cSeq,lReturn)
If e:gencode > 0
	If ( Type("__cInternet") == "C" .and. __cInternet == "AUTOMATICO" )
		AutoGRLog(oemtoansi(STR0030)+cCampo+chr(10)+chr(13)+e:Description,OemToAnsi(STR0013)) //"Erro no Gatilho : " ### "Inconsistˆncia"
	Else
		MSGSTOP(oemtoansi(STR0030)+cCampo+" "+OemToAnsi(STR0047)+cSeq+chr(10)+chr(13)+e:Description,OemToAnsi(STR0013)) //"Erro no Gatilho : " ### "Inconsistˆncia"
	EndIf
	BREAK
EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ TriggerPict ³ Autor ³ Ary Medeiros	     ³ Data ³ 31/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna Conteudo do campo transformado pela picure 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ TriggerPict(ExpC1)													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Campo															  ³±±
±±³			 ³ ExpC2 = Conteudo														  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 										  	  					  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TriggerPict(cCpo,cContem)
Local cSavAlias := Alias(), nSavRec,nSavOrd

DbSelectArea("SX3")
nSavRec := Recno()
nSavOrd := IndexOrd()
DbSetOrder(2)
DbSeek(cCpo)
cContem := Transform(cContem,Trim(X3_PICTURE))
DbSetOrder(nSavOrd)
DbGoTo(nSavRec)
DbSelectArea(cSavAlias)
Return cContem

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ TriggerClear³ Autor ³ Ary Medeiros	     ³ Data ³ 31/08/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Substitui "M->" por conteudo do array aTela                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function TriggerClear(cExpr,nLin,cAliasOld)

Local aPos
Local nIni
Local nEnd
Local nElem
Local nElemTBL
Local cSub

cExpr := UPPER(cExpr)
While "M->"$cExpr
	nIni := At("M->",cExpr)+3
	cSub := Substr(cExpr,nIni,Len(cExpr))
	aPos := {}
	If ( "!" $ cSub )
		AADD(aPos,At("!",cSub))
	EndIf
	If ( "$" $ cSub )
		AADD(aPos,At("$",cSub))
	EndIf
	If ( "%" $ cSub )
		AADD(aPos,At("%",cSub))
	EndIf
	If ( "^" $ cSub )
		AADD(aPos,At("^",cSub))
	EndIf
	If ( ")" $ cSub )
		AADD(aPos,At(")",cSub))
	EndIf
	If ( "-" $ cSub )
		AADD(aPos,At("-",cSub))
	EndIf
	If ( "+" $ cSub )
		AADD(aPos,At("+",cSub))
	EndIf
	If ( "*" $ cSub )
		AADD(aPos,At("*",cSub))
	EndIf
	If ( "/" $ cSub )
		AADD(aPos,At("/",cSub))
	EndIf
	If ( " " $ cSub )
		AADD(aPos,At(" ",cSub))
	EndIf
	If ( "," $ cSub )
		AADD(aPos,At(",",cSub))
	EndIf
	If ( "<" $ cSub )
		AADD(aPos,At("<",cSub))
	EndIf
	If ( ">" $ cSub )
		AADD(aPos,At(">",cSub))
	EndIf
	If ( "=" $ cSub )
		AADD(aPos,At("=",cSub))
	EndIf
	If ( Len(aPos) == 0 )
		nEnd := Len(cSub)
	Else
		aPos := aSort(aPos)
		nEnd := aPos[1]
	EndIf
	cCpo	:= Substr(cExpr,nIni,nEnd-1)
	nElem := ASCAN(aHeader,{|x| AllTrim(x[2]) == AllTrim(Upper(cCpo))})
	If ( nElem == 0 )
		cExpr := Substr(cExpr,1,nIni-4)+"m"+Subs(cExpr,nIni-2)
	Else
		If ( Type("aCols") # "U" )
			cExpr := Substr(cExpr,1,nIni-4)+"aCols["+AllTrim(Str(nLin))+","+AllTrim(Str(nElem))+"]"+Substr(cSub,nEnd,Len(cSub))
		Else
			nElemTBL := (cAliasOld)->(FieldPos(AllTrim(Upper(cCpo))))
			cExpr := Substr(cExpr,1,nIni-4)+cAliasOld+"->(FieldGet("+AllTrim(Str(If(ValType(nElemTBL)=="N" .And. nElemTBL > 0,nElemTBL,nElem)))+"))"+Substr(cSub,nEnd,Len(cSub))
		EndIf
	EndIf
End
Return( cExpr )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ CriaVar	³ Autor ³ Wagner Xavier         ³ Data ³ 03.02.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Cria uma variavel de acordo com Dicion rio de Dados.		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ CriaVar( ExpC1,ExpC2 )					                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Nome do campo dicion rio 						  ³±±
±±³			 ³ ExpL1 : Se ser  considerado inicializador padr„o			  ³±±
±±³			 ³ ExpC2 : Lado para inicializa‡„o padr„o                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Gen‚rico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CriaVar( cCampo, lInitPad, cLado, lCriaPub )

Local aArea     := GetArea()
Local aAreaSX3  := SX3->(GetArea())
Local cTipo
Local nTamanho
Local xConteudo
Local lInicializador

DEFAULT lCriaPub := .T.
DEFAULT cLado    := "L"
DEFAULT lInitPad := .T.

If At( ">", cCampo ) != 0
	cCampo := AllTrim(SubStr( cCampo, 1+At( ">", cCampo) , 10 ) )
EndIf
cCampo := PadR(cCampo,10)
cTipo     := GetSx3Cache(cCampo,"X3_TIPO")
nTamanho  := GetSx3Cache(cCampo,"X3_TAMANHO")

If cTipo == Nil
	Help(" ", 1, "NOCPOSX3",,"Field -> "+cCampo,5,0)
	RestArea(aAreaSX3)
	RestArea(aArea)
	Return("")
EndIf

Do Case
	Case cTipo == "C"
		xConteudo := Space( nTamanho )
	Case cTipo == "N"
		xConteudo :=	0
	Case cTipo == "D"
		xConteudo := CtoD("  /  /  ")
	Case cTipo == "L"
		xConteudo := .F.
	Case cTipo == "M"
		xConteudo := ""
EndCase
If lInitPad
	lInicializador := ExistIni( cCampo , .F.)
	If lInicializador
		xConteudo := InitPad( GetSx3Cache(cCampo,"X3_RELACAO") , cCampo )

		If cTipo == "C" .And. Empty(xConteudo)
			xConteudo := " "
		EndIf

		If cTipo == "C"
			xConteudo += Space( nTamanho - Len ( xConteudo ) )
			Do Case
				Case cLado == "L"
					xConteudo := PadL( xConteudo, nTamanho )
				Case cLado == "C"
					xConteudo := PadC( xConteudo, nTamanho )
				OtherWise
					xConteudo := PadR( xConteudo, nTamanho )
			EndCase
		EndIf
	EndIf
EndIf
If GetSx3Cache(cCampo,"X3_CONTEXT") == "V"
	cVar := 'M->'+cCampo
	If lCriaPub
		Public &cVar := xConteudo
	EndIf
EndIf
RestArea(aAreaSX3)
RestArea(aArea)
Return(xConteudo)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AtuaSB6	³ Autor ³ Gilson do Nascimento  ³ Data ³ 20/04/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza arquivo de Saldos De/Em Poder de Terceiros (SB6)  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aArray := AtuaSB6(ExprC1,ExprC2,ExprA1,ExprA2) 		        ³±±
±±³			 ³ ExprC1 := TES											              ³±±
±±³			 ³ ExprC2 := Chave de Pesquisa no SB6						        ³±±
±±³			 ³ ExprA1 := Array dos Campos Ref. ao SD1 ou SD2			     ³±±
±±³			 ³ ExprA2 := Array dos Custos para Este Item 				     ³±±
±±³			 ³ ExprC3 := Tipo da Nota									           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtuaSB6(cTes,cChave,aCpoSD,aCustoSB6,cTipo)
Local cAliasAnt:=alias(),nQTD,nTotQtd
Local nSaldo := 0, aSaldo:= Array(2)
Afill(aSaldo,0)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no arquivo de TES para este item				 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SF4")
DbSeek(xFilial()+cTes)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava arquivo de Saldo Em/De Poder de Terceiros			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SF4->F4_PODER3 != "N"

	DbSelectArea("SB6")
	DbSetOrder(3)
	DbSeek(xFilial()+cChave)

	If SF4->F4_PODER3=="R"
		AGravaSB6(cTes,aCustoSB6,cTipo,aCpoSD)
		Replace B6_IDENT	With aCpoSD[17]
		Replace B6_SALDO	With aCpoSD[12]
		Replace B6_ESTOQUE	With SF4->F4_ESTOQUE
		If B6_SALDO <=0
			Replace B6_ATEnd With "S"
		EndIf
		cSB6Ant := B6_IDENT
		__lPointSB6 := Iif(__lPointSB6==Nil,(ExistBlock("SB6GRAVA")),__lPointSB6)
		If __lPointSB6
			ExecBlock("SB6GRAVA",.f.,.f.)
		EndIf
		MsUnlock()

	ElseIf SF4->F4_PODER3=="D"

		nQtd	  := 0
		nTotQtd := 0
		While !Eof() .And. ;
			xFilial()+cChave == B6_FILIAL+B6_IDENT+B6_PRODUTO
			nSaldo := B6_SALDO
			If nSaldo <= 0
				DbSkip()
				Loop
			EndIf

			If (aCpoSD[12] - nTotQtd) <= 0
				Exit
			EndIf

			If nSaldo <= (aCpoSD[12] - nTotQtd)
				nQtd := nSaldo
			Else
				nQtd := aCpoSD[12] - nQtd
			EndIf

			nTotQtd := nTotQtd +  nQtd

			RecLock("SB6",.F.)

			If B6_QULIB > 0				  // A Entrada nao pode estornar o B6_QULIB
				Replace		B6_QULIB 	With B6_QULIB - nQtd
			EndIf
			Replace B6_SALDO With B6_SALDO - nQtd
			Replace B6_UENT	With dDataBase

			If B6_SALDO <= 0
				Replace B6_ATEnd With "S"
			EndIf

			MsUnlock()

			If SF4->F4_ESTOQUE=="S"
				aCustoSB6[1] := B6_CUSTO1	* (nQtd / B6_QUANT)
				aCustoSB6[2] := B6_CUSTO2	* (nQtd / B6_QUANT)
				aCustoSB6[3] := B6_CUSTO3	* (nQtd / B6_QUANT)
				aCustoSB6[4] := B6_CUSTO4	* (nQtd / B6_QUANT)
				aCustoSB6[5] := B6_CUSTO5	* (nQtd / B6_QUANT)
			EndIf

			nRegSB6:= Recno()
			cSB6Ant	 := B6_IDENT
			aCpoSD[6] := B6_TIPO

			AGravaSB6(cTes,aCustoSB6,cTipo,aCpoSD)						// Grava Item Devolucao no SB6
			Replace B6_IDENT	With cSB6Ant
			__lPointSB6 := Iif(__lPointSB6==Nil,(ExistBlock("SB6GRAVA")),__lPointSB6)
			If __lPointSB6
				ExecBlock("SB6GRAVA",.f.,.f.)
			EndIf
			MsUnlock()

			DbGoTo(nRegSB6)

			DbSkip()
			Loop
		End
	EndIf
EndIf
DbSelectArea(cAliasAnt)
Return(aCustoSB6)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxCadastro³Autor  ³ Ary Medeiros	        ³ Data ³ 28.02.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Geracao de modelo 1                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³AxCadastro(ExpC1,ExpC2,ExpC3,ExpC4,[ExpA1])                 ³±±
±±³ ExpC1	 ³Alias do arquivo                                            ³±±
±±³ ExpC2	 ³Titulo da Janela                                            ³±±
±±³ ExpC3	 ³Nome de funcao para validacao da exclusao                   ³±±
±±³ ExpC4	 ³Nome de funcao para validacao da tela	                      ³±±
±±³ ExpA5	 ³Array de funcoes adicionais          	                      ³±±
±±³ ExpB6	 ³CodeBlock Executado antes da Interface                      ³±±
±±³ ExpB7	 ³CodeBlock Executado na validacao da Interface               ³±±
±±³ ExpB8	 ³CodeBlock Executado na validacao da Interface               ³±±
±±³ ExpB9	 ³Logico que informa se o menu padrão será criado             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxCadastro(cAlias,cTitle,cDel,cOk,aRotAdic,bPre,bOK,bTTS,bNoTTS,aAuto,nOpcAuto,aButtons,aACS,cTela,lMenudef)

Local nX := 1
DEFAULT cTitle   := "AX_CADASTRO()"
DEFAULT bPre     := {|| .T.}
DEFAULT bOk      := {|| .T.}
DEFAULT bTTS     := {|| .T.}
DEFAULT bNoTTS   := {|| .T.}
DEFAULT nOpcAuto := 3
DEFAULT aACS     := {}
DEFAULT lMenudef := .T.

Private cOkFunc := cOk
Private cDelFunc:= cDel
Private aParam  := {bPre,bOK,bTTS,bNoTTS}
Private xAuto   := aAuto
Private aNewBtn := aButtons

// Verifica se o menu padrão da AxCadastro será criado.
If lMenudef
	Private aRotina := { { oemtoansi(STR0014),"AxPesqui", 0 , 1},; // "Pesquisar"
		{ oemtoansi(STR0015),"AxCadVis", 0 , 2},; // "Visualizar"
		{ oemtoansi(STR0016),"AxCadInc", 0 , 3},; //"Incluir"
		{ oemtoansi(STR0017),"AxCadAlt", 0 , 4},; //"Alterar"
		{ oemtoansi(STR0018),"AxCadDel", 0 , 5}}  //"Excluir"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona o array de rotinas adicioniais   				     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ValType( aRotAdic ) == "A"
	AEval( aRotAdic, { |x| AAdd( aRotina, x ) } )
EndIf
For nX := 1 To Len(aACS)
	If aACS[nX] <> Nil
		aadd(aRotina[nX],aACS[nX])
	EndIf
Next nX
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes				     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := OemToAnsi(cTitle)

If aAuto==Nil
	mBrowse( 6, 1,22,75,cAlias,,,,,,,,,,,,,,,,, cTela)
Else
	MBrowseAuto(nOpcAuto,Aclone(aAuto),cAlias)
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxCadInc	³Autor  ³ Ary Medeiros		    ³ Data ³ 28.02.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada da funcao AxInclui pela AxCadastro				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AxCadastro												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AxCadInc(cAlias,nReg,nOpc,aAcho,cFunc,aCpos)

Return(AxInclui(cAlias,nReg,nOpc,aAcho,cFunc,aCpos,cOkFunc,/*lF3*/,/*cTransact*/,aNewBtn,aParam,xAuto))
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxCadAlt	³Autor  ³ Ary Medeiros			³ Data ³ 28.02.94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada da funcao AxAltera pela AxCadastro		          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AxCadastro												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxCadAlt(cAlias,nReg,nOpc,aAcho,aCpos,nColMens,cMensagem,cTudoOk)

AxAltera(cAlias,nReg,nOpc,aAcho,aCpos,nColMens,cMensagem,cOkFunc,/*cTransact*/,/*cFunc*/,aNewBtn,aParam,xAuto)

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxCadDel	³Autor  ³ Eduardo Riera         ³ Data ³10.09.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada da funcao AxDeleta pela AxCadastro		          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AxCadastro												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxCadDel(cAlias,nReg,nOpc,aCpos)

AxDeleta(cAlias,nReg,nOpc,/*cTransact*/,aCpos,aNewBtn,aParam,xAuto)

Return nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxCadVis	³Autor  ³ Eduardo Riera         ³ Data ³25.02.2004³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Chamada da funcao AxVisual pela AxCadastro		          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AxCadastro												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxCadVis(cAlias,nReg,nOpc,aCpos)

AxVisual(cAlias,nReg,nOpc,/*aAcho*/,/*nColMens*/,/*cMensagem*/,/*cFunc*/,aNewBtn,/*lMaximized*/)

Return nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ AxDeleta ³ Autor ³ Jorge Queiroz         ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa generico para exclusao     		                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ AxDeleta(ExpC1,ExpN1,ExpN2,ExpA1,ExpN3,ExpC1)              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³			 ³ ExpN2 = Numero do registro                                 ³±±
±±³			 ³ ExpN3 = Numero da opcao selecionada                        ³±±
±±³			 ³ ExpC4 = Funcao a ser executada durante a transacao    	  ³±±
±±³			 ³ ExpA5 = Array com os campos a serem mostrados              ³±±
±±³			 ³ ExpA6 = Array com os botoes da EnchoiceBar            	  ³±±
±±³			 ³ ExpA7 = Array de codeblock de execucao                	  ³±±
±±³			 ³ ExpA8 = Array da rotina automatica                    	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AxDeleta(cAlias,nReg,nOpc,cTransact,aCpos,aButtons,aParam,aAuto,lMaximized,cTela,aAcho,lPanelFin,oFather,aDim, lFlat)

Local aArea    := GetArea()
Local aPosEnch := {}
Local nOpcA    := 0
Local nX       := 0
Local oDlg
Local nTop
Local nLeft
Local nBottom
Local nRight
Local cMsgError:= ""
Local oEnc01
Local lVirtual:=.F. // Qdo .F. carrega inicializador padrao nos campos virtuais
Local oSize

Private aTELA[0][0]
Private aGETS[0]

DEFAULT lPanelFin := .F.
DEFAULT lFlat		:= .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a entrada de dados do arquivo                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea(cAlias)
If SoftLock( cAlias )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento de codeblock de antes da interface                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(aParam)
		Eval(aParam[1],nOpc)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Controle de Interface                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case
		Case ( Type("__cInternet") # "C" .Or. __cInternet # 'AUTOMATICO' ) .And. aAuto == Nil

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula as dimensoes dos objetos                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSize := FwDefSize():New( .T. ) // Com enchoicebar

			oSize:lLateral     := .F.  // Calculo vertical

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cria Enchoice                                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSize:AddObject( "ENCHOICE", 100, 60, .T., .T. ) // Adiciona enchoice

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Dispara o calculo                                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oSize:Process()

			nTop    := oSize:aWindSize[1]
			nLeft   := oSize:aWindSize[2]
			nBottom := oSize:aWindSize[3]
			nRight  := oSize:aWindSize[4]

			If IsPDA()
				nTop := 0
				nLeft := 0
				nBottom := PDABOTTOM
				nRight  := PDARIGHT
			EndIf

         If !lPanelFin .AND. !lFlat
				DEFINE MSDIALOG oDlg TITLE cCadastro FROM nTop,nLeft TO nBottom,nRight PIXEL OF oMainWnd STYLE nOr(WS_VISIBLE,WS_POPUP)

				If lMaximized <> NIL
					oDlg:lMaximized := lMaximized
				EndIf
				If IsPDA()
					aPosEnch := {,,,}  // ocupa todo o  espaço da janela
					oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,aCpos,aPosEnch,,,,,,,,.t.,,,,,,,,,cTela )
					oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
				Else

					aPosEnch := {oSize:GetDimension("ENCHOICE","LININI"),;
						 oSize:GetDimension("ENCHOICE","COLINI"),;
						 oSize:GetDimension("ENCHOICE","LINEND"),;
						 oSize:GetDimension("ENCHOICE","COLEND")}

					oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,aCpos,aPosEnch,,,,,,,,,,,,,,,,,cTela)
					oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
				EndIf
				ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},,aButtons)
		   Else

		   	DEFINE MSDIALOG ___oDlg OF oFather:oWnd FROM 0, 0 TO 0, 0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )

				aPosEnch := {,,,}
				oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,aAcho,aPosEnch,,,,,,___oDlg,,lVirtual,,,,,,,,,cTela)
				oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT

				// posiciona dialogo sobre a celula
				___oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1])

				ACTIVATE MSDIALOG ___oDlg  ON INIT (FaMyBar(___oDlg,{|| If(Obrigatorio(aGets,aTela).And.Eval(bOk).And.Eval(bOk2,nOpc),Eval(bEndDlg),(nOpcA:=3,.f.))},{|| nOpcA := 3,___oDlg:End()},aButtons), ___oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]) )

				If Type("FinWindow") <> "U"
					FinVisual(cAlias,FinWindow,(cAlias)->(Recno()))
				EndIf
		   Endif

		Case aAuto <> Nil
			RegToMemory(cAlias,.F.,.F.)
			If EnchAuto(cAlias,aAuto,{|| Obrigatorio(aGets,aTela)},nOpc,aCpos)
				nOpcA := 2
			EndIf
		OtherWise
			nOpcA := 2
	EndCase
	If nOpcA == 2
		dbSelectArea(cAlias)
		If Type("cDelFunc") != "U" .and. cDelFunc != Nil
			If !&(cDelFunc)
				nOpcA := 0
			EndIf
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento de codeblock de validacao de confirmacao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpcA == 2 .And. !Empty(aParam)
		If !Eval(aParam[2],nOpc)
			nOpcA := 0
		EndIf
	EndIf
	If nOpcA == 2
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualizacao da tabela                                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Begin Transaction
			DbSelectArea(cAlias)
			If cTransact != Nil .And. ValType(cTransact) == "C"
				If !("("$cTransact)
					cTransact+="()"
				EndIf
				&cTransact
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Processamento de codeblock dentro da transacao                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty(aParam)
				Eval(aParam[3],nOpc)
			EndIf

			If Type("aMemos")=="A"
				For nX := 1 To Len(aMemos)
//Incluído parametro com o nome da tabela de memos => para módulo APT
					cAliasMemo := If(len(aMemos[nX]) == 3,aMemos[nX][3],Nil)
					MSMM(&(aMemos[nX][1]),,,,2,,,,,cAliasMemo)
				Next nX
			EndIf

			RecLock(cAlias,.F.,.T.)
			IF !FKDelete(@cMsgError)
			   RollBackDelTran(cMsgError)
			EndIf
			MsUnLock()

		End Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Processamento de codeblock fora da transacao                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aParam)
			Eval(aParam[4],nOpc)
		EndIf
	EndIf
EndIf
MsUnLockAll()
RestArea(aArea)
Return(nOpcA)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ClearTitle³Autor  ³ Ary Medeiros			³ Data ³		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Limpa string com titulo do campo 						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AS400 													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ClearTitle(cTitle)
Local i,cChar, cOut := ""

For i:= 1 to Len(cTitle)
	cChar := Substr(cTitle,i,1)
	cOut  += If(LetterOrNum(cChar),cChar," ")
Next
Return cChar


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³LetterOrNum³Autor	³ Ary Medeiros 		  ³ Data ³			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ VerIfica se caracter e uma letra ou um numero				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AS400 													 				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LetterOrNum(cChar)
Local nChar := ASC(cChar)

Return ((nChar >= 48 .and. nChar <= 57) .or. (nChar >= 65 .and. nChar <= 90) .or. (nChar >= 97 .and. nChar <= 122 ))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ADupCred	³Autor  ³ Rosane L. Chene		³ Data ³01.06.95  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gera titulo de Credito para fornecedor no caso de nota 	  ³±±
±±³			 ³fiscal de devolucao de compras 							  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ADupCred()												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Valor do Titulo 									  ³±±
±±³			 ³ ExpC2 = Codigo do TES									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ADupCred(nValtot,cTes,nMoeda,cNatureza,nTaxa,dVencNCC,cOrigem)
Local cSaveAlias := Alias()
Local nSaveOrd   := IndexOrd()
Local cSerie     := ""
Local nRecCred   := 0
Local cPrefixo   := ""
Local cParcela   := ""
Local nTamParc   := 0
Local lTpDesc    := If(SE1->(FieldPos("E1_TPDESC")) > 0, .T., .F.)
Local lUsaNewKey := TamSX3("F2_SERIE")[1] == 14 // Verifica se o novo formato de gravacao do Id nos campos _SERIE esta em uso
Local cAliasSE1  := ""
Local cAliasSE2  := ""
Local cQuery     := ""
Local cFilE2	 := ""
Local cFilE1	 := ""
Local cCarteira  := PadR("0",TamSX3("E1_SITUACA")[1])
Local lTechfin   := .F.
Local cCartTecF  := ""
Local lIntGC	 := IIf((SuperGetMV("MV_VEICULO",,"N")) == "S",.T.,.F.)
Local cE1Cliente := "" // Quando integrado com DMS, o cliente pode ser outro se utilizado condicao de pagamento TIPO A na venda.
Local cE1Loja    := "" // Quando integrado com DMS, o cliente pode ser outro se utilizado condicao de pagamento TIPO A na venda.
Local cE1NReduz  := "" // Quando integrado com DMS, o cliente pode ser outro se utilizado condicao de pagamento TIPO A na venda.
Local lUseRISK   := FindFunction( "RskIsActive" ) .And. RskIsActive()
Local lDtEmDtbs	 := SuperGetMV("MV_DTEMIBS",.F.,.F.)

Local cTipo		 As Character
Local cMV1Dup    As Character
Local cChaveTit	 As Character

#ifdef SHELL
	Local nSaveOrdSF2, nSaveRecSF2
	Local nPosSer, nPosNfOr, nElem
#endif

DEFAULT nMoeda     	:= 1
DEFAULT nTaxa   	:= 0
DEFAULT dVencNCC	:= dDataBase
DEFAULT cOrigem		:= "MATA460"

If Empty(dVencNCC)
	dVencNCC	:= dDataBase
EndIf

cTipo		:= ""
cChaveTit	:= ""

If cTes > "500"
	cArqSA := "SA2"
	cSeek  := SF2->F2_CLIENTE+SF2->F2_LOJA
	cArqSE := "SE2"
	cSerie := SF2->F2_SERIE
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Desposiciona o SA1                                           ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SA1->(MsGoto(0))
Else
	cArqSA := "SA1"
	cSeek  := SF1->F1_FORNECE+SF1->F1_LOJA
	cArqSE := "SE1"
	cSerie := SF1->F1_SERIE
	*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	*³ Desposiciona o SA2                                           ³
	*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SA2->(MsGoto(0))
	#ifdef SHELL
		*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		*³ Posiciona a Capa de Nota de Saida para obter o VEndedor      ³
		*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SF2")
		nSvOrdSF2 :=IndexOrd()
		nSvRecSF2 := Recno()
		nPosSer := Ascan(aHeader,{|x|Trim(x[2])=="D1_SERIORI"})
		nPosNfOr := Ascan(aHeader,{|x|Trim(x[2])=="D1_NFORI"})
		DbSetOrder(1)
		For nElem :=1 to Len(Acols)
			If ! aCols[nElem][Len(aHeader)+1]
				dbSeek(cFilial+aCols[nElem][nPosNfOr]+aCols[nElem][nPosSer])
				Exit
			EndIf
		Next
	#endif
EndIf

DbSelectArea(cArqSA) 	 // Posiciona o arquivo de cliente/fornecedor
dbSetOrder(1)
DbSeek(cFilial+cSeek)

If cArqSE == "SE2"
	DEFAULT cNatureza := IIF("D" $ SF2->F2_TIPO,&(GetMV("mv_2dupnat")),&(GetMV("mv_1dupnat")))

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Projeto Chave Unica                                                                ³
	//³Caso estiver sendo utilizado o recurso de gravar o ID de controle no campo F2_SERIE³
	//³( lUsaNewKey := .T.),  os documentos poderão repetir o mesmo numero e serie, neste ³
	//³caso a query abaixo consulta os titulos anteriores para evitar a duplicidade no SE2³
	//³Caso encontre titulos anteriores com o mesmo numero ascrecenta soma1() ao prefixo. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lUsaNewKey
		SE2->(dbSetOrder(1))

		cAliasSE2 := "ADUPPRFX"
		cQuery := " SELECT MAX(E2_PREFIXO) E2PRFXMAX FROM " + RetSqlName("SE2")
		cQuery += " WHERE E2_FILIAL = '" + xFilial("SE2") + "'"
		cQuery += " AND E2_NUM      = '" + SF2->F2_DOC + "'"
		cQuery += " AND E2_FORNECE  = '" + SF2->F2_CLIENTE + "'"
		cQuery += " AND E2_LOJA     = '" + SF2->F2_LOJA + "'"
		cQuery += " AND D_E_L_E_T_  = ''"

		cQuery	  := ChangeQuery(cQuery)
		cAliasSE2 := CriaTrab(Nil,.F.)
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasSE2, .T., .T. )
		cFilE2	  := xFilial("SE2")

		dbSelectArea(cAliasSE2)
		If !Empty( (cAliasSE2)->E2PRFXMAX )
 			cPrefixo := Soma1( (cAliasSE2)->E2PRFXMAX , TamSX3("E2_PREFIXO")[1] )
 			/*
			Devido ao projeto CHAVE ÚNICA, a consistência abaixo é necessária para
			garantir que o novo título a ser gerado não irá colidir com um título
			da base histórica do cliente criada antes do projeto chave única.
			*/
			While .T.
			   	If (SE2->(dbSeek(cFilE2+cPrefixo+SF2->F2_DOC)))
					cPrefixo := Soma1( (cAliasSE2)->E2PRFXMAX , TamSX3("E2_PREFIXO")[1] )
			   	Else
					EXIT
				EndIf
			EndDo
		Else
			cPrefixo := &(GetMV("mv_1dupref"))
		EndIf

		(cAliasSE2)->(dbCloseArea())
	Else
		cPrefixo := &(GetMV("mv_1dupref"))
	EndIf

	dbSelectArea("SE2")
	RecLock(cArqSE,.T.)
	Replace E2_NOMFOR	With SA2->A2_NREDUZ
	Replace E2_EMIS1	With dDataBase
	Replace E2_BAIXA	With CtoD("  /  /  ")
	Replace E2_SALDO	With nValTot
	Replace E2_FILIAL	With cFilial
	Replace E2_PREFIXO	With cPrefixo
	Replace E2_NUM		With SF2->F2_DOC
	Replace E2_PARCELA	With GetMV("mv_1dup")
	Replace E2_NATUREZ	With cNatureza
	If cPaisLoc$"ARG|POR|EUA"
		Replace E2_TIPO	With SC5->C5_TIPOTIT
		Replace E2_VALOR	With If(SC5->C5_TIPOTIT $ MVABATIM,nValTot*-1,nValTot )
	Else
		Replace E2_TIPO	With left(MV_CPNEG,3)
		Replace E2_VALOR	With nValTot
	EndIf
	Replace E2_EMISSAO	With dDataBase
	Replace E2_VALOR	With nValTot
	Replace E2_VENCTO	With dDataBase
	Replace E2_VENCREA	With dDatabase
	Replace E2_VENCORI	With dDatabase
	Replace E2_FORNECE	With SF2->F2_CLIENTE
	Replace E2_LOJA		With SF2->F2_LOJA
	Replace E2_NOMFOR	With SA2->A2_NREDUZ
	Replace E2_MOEDA	With nMoeda
	Replace E2_VLCRUZ	With Round(NoRound(xMoeda(SE2->E2_VALOR,nMoeda,1,SE2->E2_EMISSAO,MsDecimais(1)+1,nTaxa),MsDecimais(1)+1),MsDecimais(1))
	Replace E2_ORIGEM	With cOrigem
	Replace E2_LA		With "S"
	Replace E2_FILORIG	With cFilAnt
	If nMoeda > 1
		Replace E2_TXMOEDA	With nTaxa
	EndIf
	fkCommit(.T.)

	cChaveTit := xFilial("SE2") + "|" + SE2->E2_PREFIXO + "|" + SE2->E2_NUM + "|" + SE2->E2_PARCELA + "|" +;
				 SE2->E2_TIPO   + "|" + SE2->E2_FORNECE + "|" + SE2->E2_LOJA
	
    FINGRVFK7('SE2', cChaveTit,)

	nRecCred := Recno()
	DbSelectArea( "SA2" )
	RecLock("SA2")
	Replace A2_SALDUP With A2_SALDUP	- Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,SE2->E2_EMISSAO,3),3),2)
	Replace A2_SALDUPM With A2_SALDUPM - Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,Val(GetMv("MV_MCUSTO")),SE2->E2_EMISSAO,3),3),2)

	If SA2->A2_SALDUPM > SA2->A2_MSALDO
		Replace SA2->A2_MSALDO With SA2->A2_SALDUPM
	EndIf
ElseIf cArqSe == "SE1"
	DEFAULT cNatureza := IIF("D" $ SF1->F1_TIPO,&(GetMV("mv_1dupnat")),&(GetMV("mv_2dupnat")))

	cE1Cliente := SF1->F1_FORNECE
 	cE1Loja    := SF1->F1_LOJA
	cE1NReduz  := SA1->A1_NREDUZ

	If SF1->F1_TIPO == "D" .and. lIntGC .and. ExistFunc("FMX_NCCCliente")
		FMX_NCCCliente(SF1->F1_DOC, SF1->F1_SERIE, SF1->F1_FORNECE, SF1->F1_LOJA, @cE1Cliente, @cE1Loja, @cE1NReduz)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Projeto Chave Unica                                                                ³
	//³Caso estiver sendo utilizado o recurso de gravar o ID de controle no campo F1_SERIE³
	//³( lUsaNewKey := .T.),  os documentos poderão repetir o mesmo numero e serie, neste ³
	//³caso a query abaixo consulta os titulos anteriores para evitar a duplicidade no SE1³
	//³Caso encontre titulos anteriores com o mesmo numero ascrecenta soma1() ao prefixo. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lUsaNewKey
		SE1->(dbSetOrder(1))

		cAliasSE1 := "ADUPPRFX"
		cQuery := " SELECT MAX(E1_PREFIXO) E1PRFXMAX FROM " + RetSqlName("SE1")
		cQuery += " WHERE E1_FILIAL = '" + xFilial("SE1") + "'"
		cQuery += " AND E1_NUM      = '" + SF1->F1_DOC + "'"
		cQuery += " AND E1_CLIENTE  = '" + SF1->F1_FORNECE + "'"
		cQuery += " AND E1_LOJA     = '" + SF1->F1_LOJA + "'"
		cQuery += " AND D_E_L_E_T_  = ''"

		cQuery	  := ChangeQuery(cQuery)
		cAliasSE1 := CriaTrab(Nil,.F.)
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasSE1, .T., .T. )
		cFilE1	  := xFilial("SE1")

		dbSelectArea(cAliasSE1)
		If !Empty( (cAliasSE1)->E1PRFXMAX )
			cPrefixo := Soma1( (cAliasSE1)->E1PRFXMAX , TamSX3("E1_PREFIXO")[1] )
 			/*
			Devido ao projeto CHAVE ÚNICA, a consistência abaixo é necessária para
			garantir que o novo título a ser gerado não irá colidir com um título
			da base histórica do cliente criada antes do projeto chave única.
			*/
			While .T.
				If (SE1->(dbSeek(cFilE1+cPrefixo+SF1->F1_DOC)))
		    		cPrefixo := Soma1( (cAliasSE1)->E1PRFXMAX , TamSX3("E1_PREFIXO")[1] )
				Else
					EXIT
				EndIf
			EndDo
		Else
			cPrefixo := PadR(&(SuperGetMV("MV_2DUPREF")), Len( SE1->E1_PREFIXO ) )
		EndIf

		(cAliasSE1)->(dbCloseArea())
	Else
		cPrefixo := PadR(&(SuperGetMV("MV_2DUPREF")), Len( SE1->E1_PREFIXO ) )
	EndIf

	cCartTecF:= Padr(SuperGetMv("MV_CARTECF",.F.,""),TamSX3("E1_SITUACA")[1])
	/* Tratamento para integração com TOTVS Antecipa para mudar a carteira da NCC criada no processo de devolução. */
	If !Empty(cCartTecF)
		If FindFunction('F136ANFAnt')
			cCarteira := F136ANFAnt(cSerie, SF1->F1_DOC, SF1->F1_FORNECE, SF1->F1_LOJA, cCartTecF)
			lTechfin  := cCarteira <> "0"
		EndIf
	EndIf

	/* Integração RISK - TOTVS Mais Negócios
	Apenas títulos para carteira APÓS tratamento do Antecipa podem ser bloqueados*/
	If lUseRISK
		If cCarteira == "0"
			cCarteira := RSKNCCAnalyze(cSerie, SF1->F1_DOC, SF1->F1_FORNECE, SF1->F1_LOJA)
		EndIf
	EndIf

	dbSelectArea("SE1")
	dbSetOrder(1) //E1_FILIAL+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
	cParcela := (GetMV("mv_1dup"))
	nTamParc := Len(SE1->E1_PARCELA)
	cTipo	 := LEFT(MV_CRNEG,TamSX3("E1_TIPO")[1])
	cMV1Dup  := Replicate("9",nTamParc)

	If cParcela $ "0000|0001"
		cMV1Dup := Replicate("Z",nTamParc)
	EndIf

	While MSSeek(xFilial("SE1") + cPrefixo + SF1->F1_DOC + cParcela + cTipo)
		cParcela := Soma1(cParcela,nTamParc)

		If cParcela == cMV1Dup .And. MSSeek(xFilial("SE1") + cPrefixo + SF1->F1_DOC + cParcela + cTipo)
			Help( ,,"LIMITETAMPARC",, STR0062, 1, 0,,,,,, { STR0063 }  )
			//"O sequencial para o campo E1_PARCELA chegou ao seu limite de tamanho. Esta movimentação será abortada."
			//"Necessário aumentar o tamanho do grupo de campo parcela (011)."
			DisarmTransaction()
			Break
		Endif
	EndDo

	RecLock(cArqSE,.T.)
	Replace E1_FILIAL 	with cFilial
	Replace E1_PREFIXO	with cPrefixo
	Replace E1_SERIE   	with cSerie
	Replace E1_NUM	  	with SF1->F1_DOC
	Replace E1_PARCELA 	with cParcela
	Replace E1_NATUREZ 	with cNatureza
	Replace E1_TIPO	 	with cTipo
	Replace E1_EMISSAO 	with If(lDtEmDtbs,SF1->F1_EMISSAO,dDataBase)
	Replace E1_VALOR   	with nValTot

	If FwIsInCallStack("MATA103")
		Replace E1_VENCREA with DataValida(dVencNCC,.T.)
	Else
		Replace E1_VENCREA with dVencNCC
	EndIf
	Replace E1_SALDO   with nValTot
	Replace E1_VENCTO  with dVencNCC
	Replace E1_VENCORI with ddataBase
	Replace E1_EMIS1   with dDataBase
	Replace E1_CLIENTE with cE1Cliente
	Replace E1_LOJA	   with cE1Loja
	Replace E1_NOMCLI  with cE1NReduz
	Replace E1_MOEDA   with nMoeda
	Replace E1_VLCRUZ  with Round(NoRound(xMoeda(SE1->E1_VALOR,nMoeda,1,SE1->E1_EMISSAO,3,nTaxa),MsDecimais(1)+1),MsDecimais(1))
	Replace E1_STATUS  with Iif(SE1->E1_SALDO>0.01,"A","B")
	Replace E1_SITUACA With cCarteira
	Replace E1_ORIGEM  With "MATA100"
	Replace E1_LA      With "S"
	Replace E1_RELATO  With "2"
	SE1->E1_FILORIG := cFilAnt

	If lTpDesc
		Replace E1_TPDESC With "C"
	Endif
	If nMoeda > 1
		Replace E1_TXMOEDA	With nTaxa
	EndIf
	#ifdef SHELL
		Replace E1_VEnd1   With SF2->F2_VEnd1
	#endif
	fkCommit(.T.)

	cChaveTit := xFilial("SE1") + "|" + SE1->E1_PREFIXO + "|" + SE1->E1_NUM + "|" + SE1->E1_PARCELA + "|" +;
				 SE1->E1_TIPO   + "|" + SE1->E1_CLIENTE + "|" + SE1->E1_LOJA
	
    FINGRVFK7('SE1', cChaveTit,)

	nRecCred := Recno()
	DbSelectArea( "SA1" )
	AtuSalDup("-",SE1->E1_VALOR,SE1->E1_MOEDA,SE1->E1_TIPO,,SE1->E1_EMISSAO)

	If lTechfin .And. SF1->F1_FORMUL <> "S" .And. tlpp.ffunc("totvs.protheus.backoffice.techfin.antecipa.postbilling.processReturnDoc")
		If tlpp.ffunc("totvs.protheus.backoffice.techfin.util.IsActive") .And. totvs.protheus.backoffice.techfin.util.IsActive('1',.F.)
			totvs.protheus.backoffice.techfin.antecipa.postbilling.processReturnDoc(SF1->F1_DOC, cSerie, SF1->F1_FORNECE, SF1->F1_LOJA)
		EndIf
	EndIf

	/* Integração RISK - TOTVS Mais Negócios
	Atualizar o movimento de bloqueio de NCC na AR2*/
	If lUseRISK
		RSKBlockNCC(SE1->E1_FILIAL + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO)
	EndIf

	If FWHasEAI("FINA040",.T.,,.T.) .AND. FwIsInCallStack('LOJA720')		//Para geração de NCC
		aRetInteg := FwIntegDef( 'FINA040', , , , 'FINA040' )
		//Se der erro no envio da integração, apresenta mensagem em tela para o usuário
		If ValType(aRetInteg) == "A" .AND. Len(aRetInteg) >= 2 .AND. !aRetInteg[1]
			If ! IsBlind()
				Help( ,, "FINA040INTEG",, "O registro não será gravado, pois ocorreu um erro na integração: " +;		//"O registro não será gravado, pois ocorreu um erro na integração: "
					 AllTrim( aRetInteg[2] ), 1, 0,,,,,, {"Verifique se a integração está configurada corretamente."} ) //"Verifique se a integração está configurada corretamente."
			Endif
			DisarmTransaction()
			Break
		Endif
	Endif

EndIf
#ifdef SHELL
	If Val(cTes) < 500
		DbSelectArea("SF2")
		DbSetOrder(nSvOrdSF2)
		DbGoTo(nSvRecSF2)
	EndIf
#endif

If ExistBlock( "M461DUPCR" )
	ExecBlock( "M461DUPCR", .f., .f. )
EndIf

DbSelectArea(cSaveAlias)
DbSetOrder(nSaveOrd)
Return nRecCred


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AGravaSB6 ³Autor  ³ Rosane / Cristina	  ³ Data ³ 01.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava item de poder de terceiros 								  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ AGravaSB6(ExprC1,ExprA1,ExprC2)									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExprC1 := Codigo da Tes 											  ³±±
±±³			 ³ ExprA2 := Array dos Campos com os valores dos Custos		  ³±±
±±³			 ³ ExprC2 := Tipo da Nota												  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AGRAVASb6(cTes,aCustoSB6,cTipo,aCpoSD)

DbSelectArea("SF4")
DbSetOrder(1)
DbSeek(xFilial()+cTes)

RecLock("SB6",.T.)

Replace B6_FILIAL 	With xFilial()
Replace B6_CLIfOR 	With aCpoSD[1]
Replace B6_LOJA		With aCpoSD[2]
Replace B6_PRODUTO	With aCpoSD[3]
Replace B6_Local		With aCpoSD[4]
Replace B6_PRUNIT 	With aCpoSD[5]
Replace B6_TIPO		With aCpoSd[6]
Replace B6_DOC 		With aCpoSD[7]
Replace B6_SERIE		With aCpoSD[8]
Replace B6_EMISSAO	With aCpoSD[9]
Replace B6_DTDIGIT	With aCpoSD[10]
Replace B6_TES 		With cTes
Replace B6_QUANT		With aCpoSD[12]
Replace B6_UM			With aCpoSD[13]
Replace B6_QTSEGUM	With aCpoSD[14]
Replace B6_SEGUM		With aCpoSD[15]
Replace B6_PODER3 	With SF4->F4_PODER3

If cTes <= "500"
	If cTipo == "B"
		Replace B6_TPCF with "C"
	ElseIf cTipo == "N"
		Replace B6_TPCF with "F"
	EndIf
Else
	If cTipo == "B"
		Replace B6_TPCF with "F"
	ElseIf cTipo == "N"
		Replace B6_TPCF with "C"
	EndIf
EndIf

If SF4->F4_ESTOQUE=="S"
	Replace B6_CUSTO1 With aCustoSB6[1]
	Replace B6_CUSTO2 With aCustoSB6[2]
	Replace B6_CUSTO3 With aCustoSB6[3]
	Replace B6_CUSTO4 With aCustoSB6[4]
	Replace B6_CUSTO5 With aCustoSB6[5]
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ CalcTerc ³ Autor ³ Rosane L Chene        ³ Data ³ 23.05.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna um Array onde:                                     ³±±
±±³          ³ aSaldo[1] := Saldo de Poder Terceiro                       ³±±
±±³          ³ aSaldo[2] := Quantidade Poder Terceiro Liberada(ainda nao  ³±±
±±³          ³                               faturada)                    ³±±
±±³          ³ aSaldo[3] := Saldo total do poder de terceiro ( Valor)     ³±±
±±³          ³ aSaldo[4] := Soma do total de devolucoes do Poder Terceiros³±±
±±³          ³ aSaldo[5] := Valor Total em Poder Terceiros				  ³±±
±±³          ³ aSaldo[6] := Quantidade Total em Poder Terceiros			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do Produto                                  ³±±
±±³          ³ ExpC2 = Codigo do Cliente/Fornecedor                       ³±±
±±³          ³ ExpC3 = Codigo da Loja                                     ³±±
±±³          ³ ExpC4 = Codigo do identIficador do SB6                     ³±±
±±³          ³ ExpC5 = Codigo da Tes                                      ³±±
±±³          ³ ExpC6 = Tipo da Nota                                       ³±±
±±³			 ³ ExpD1 = Dt Inicial a ser Considerada na Composi‡Æo do Saldo³±±
±±³			 ³ ExpD2 = Dt Final a ser Considedara na Composi‡Æo do Saldo  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function CalcTerc(cCod,cClIfor,cLoja,cIdentB6,cTesB6,cTipo,dDtIni,dDtFim)

Local aArq
Local aArqSF4
Local aArqSB6
Local aStruSB6
Local aSaldo
Local lConsIni
Local lConsFim
Local lQuery
Local cAliasSB6
Local cAliasSF4
Local aNF
Local cNF
Local l103

Local cQuery
Local nX

If FindFunction("EstCalcTerc")
	// re-direcionamento para a nova função.
	// Onde foi refatorada para uma melhor performance
	aSaldo := EstCalcTerc(cCod,cClIfor,cLoja,cIdentB6,cTesB6,cTipo,dDtIni,dDtFim)

Else

	aArq       := GetArea()
	aArqSF4    := SF4->(GetArea())
	aArqSB6    := SB6->(GetArea())
	aStruSB6   := {}
	aSaldo     := {0,0,0,0,0,0}
	lConsIni   := (ValType(dDtIni)=='D'.And.!Empty(dDtIni))
	lConsFim   := (ValType(dDtFim)=='D'.And.!Empty(dDtFim))
	lQuery     := .F.
	cAliasSB6  := "SB6"
	cAliasSF4  := "SF4"
	aNF        := {}
	cNF        := ""
	l103       := IsInCallStack("MATA103")

	cQuery     := ""
	nX         := 0

	dbSelectArea("SB6")
	If !Empty(cIdentB6)
		DbSetOrder(3)

		lQuery     := .T.
		aStruSB6   := SB6->(dbStruct())
		cAliasSB6  := "CALCTERC"
		cAliasSF4  := "CALCTERC"

		cQuery := "SELECT "+SqlOrder(SB6->(IndexKey()))+","
		For nX := 1 To Len(aStruSB6)
			If !aStruSB6[nX][1]$cQuery
				cQuery += aStruSB6[nX][1]+","
			EndIf
		Next nX
		cQuery += "SF4.F4_PODER3 "
		cQuery += " FROM "+RetSqlName("SB6")+" SB6,"+RetSqlName("SF4")+" SF4 "
		cQuery += "WHERE SB6.B6_FILIAL='"+xFilial("SB6")+"' AND "
		cQuery += "SB6.B6_IDENT='"+cIdentB6+"' AND "
		cQuery += "SB6.B6_PRODUTO='"+cCod+"' AND "
		cQuery += "SB6.D_E_L_E_T_=' ' AND "
		If cTesB6 <= "500"
			If cTipo == "B"
				cQuery += "SB6.B6_TPCF='C' AND "
			ElseIf cTipo == "N"
				cQuery += "SB6.B6_TPCF='F' AND "
			EndIf
		Else
			If cTipo == "B"
				cQuery += "SB6.B6_TPCF='F' AND "
			ElseIf cTipo == "N"
				cQuery += "SB6.B6_TPCF='C' AND "
			EndIf
		EndIf
		If ( lConsIni )
			cQuery += "SB6.B6_DTDIGIT>='"+Dtos(dDtIni)+"' AND "
		EndIf
		If ( lConsFim )
			cQuery += "SB6.B6_DTDIGIT<='"+Dtos(dDtFim)+"' AND "
		EndIf
		cQuery += "SF4.F4_FILIAL='"+xFilial("SF4")+"' AND "
		cQuery += "SF4.F4_CODIGO=SB6.B6_TES AND "
		cQuery += "SF4.D_E_L_E_T_=' ' "
		cQuery += "UNION ALL "+StrTran(cQuery,"B6_IDENT=","B6_IDENTB6=")+" "
		cQuery += "ORDER BY 1,2,3,4"

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB6,.T.,.T.)

		For nX := 1 To Len(aStruSB6)
			If ( aStruSB6[nX][2] <> "C" )
				TcSetField(cAliasSB6,aStruSB6[nX][1],aStruSB6[nX][2],aStruSB6[nX][3],aStruSB6[nX][4])
			EndIf
		Next nX

		While !Eof() .And. xFilial("SB6") == (cAliasSB6)->B6_FILIAL .And.;
			(cIdentB6==(cAliasSB6)->B6_IDENTB6 .Or.;
			cIdentB6==(cAliasSB6)->B6_IDENT ) .And.;
			cCod == (cAliasSB6)->B6_PRODUTO

			If  !lQuery
				If !Empty((cAliasSB6)->B6_IDENTB6)
					DbSelectArea(cAliasSB6)
					DbSkip()
					Loop
				EndIf
				DbSelectArea("SF4")
				dbSetOrder(1)
				DbSeek(xFilial()+(cAliasSB6)->B6_TES)
				//-- Condicao para DBFCDX deve ser o inverso do SQL, ja que indica registros a serem
				//-- desconsiderados, ao contrario do SQL que indica registros a serem considerados
				If cTesB6 <= "500"
					If (cTipo == "B" .And. (cAliasSB6)->B6_TPCF != "C") .Or. (cTipo == "N" .And. (cAliasSB6)->B6_TPCF != "F")
						DbSelectArea(cAliasSB6)
						DbSkip()
						Loop
					EndIf
				Else
					If (cTipo == "B" .And. (cAliasSB6)->B6_TPCF != "F") .Or. (cTipo == "N" .And. (cAliasSB6)->B6_TPCF != "C")
						DbSelectArea(cAliasSB6)
						DbSkip()
						Loop
					EndIf
				EndIf
				//-- Condicao para DBF deve ser o inverso do SQL, ja que indica registros a serem
				//-- desconsiderados, ao contrario do SQL que indica registros a serem considerados
				//-- Consiste Data Inicial e Final na Composi??o do Saldo
				If (lConsIni.And.(cAliasSB6)->B6_DTDIGIT<dDtIni).Or.(lConsFim.And.(cAliasSB6)->B6_DTDIGIT>dDtFim)
					DbSelectArea(cAliasSB6)
					DbSkip()
					Loop
				EndIf
			EndIf
			If (cAliasSF4)->F4_PODER3 == "R"
				aSaldo[1]+= (cAliasSB6)->B6_QUANT
				aSaldo[2]+= (cAliasSB6)->B6_QULIB

				If l103
					aSaldo[3]+= ((cAliasSB6)->B6_PRUNIT*(cAliasSB6)->B6_QUANT)-aSaldo[4]
				Else
					aSaldo[3]+= ((cAliasSB6)->B6_PRUNIT*IIF((cAliasSB6)->B6_QUANT==0,1,aSaldo[1]))
				EndIf

				If (cAliasSB6)->B6_QUANT == 0
					aSaldo[5]+= (cAliasSB6)->B6_PRUNIT
				Else
					aSaldo[5]+= (cAliasSB6)->B6_PRUNIT * (cAliasSB6)->B6_QUANT
					aSaldo[6]= (cAliasSB6)->B6_QUANT
				EndIf
			ElseIf (cAliasSF4)->F4_PODER3 == "D"
				aSaldo[1]-= (cAliasSB6)->B6_QUANT

				SD1->(dbSetOrder(1))
				SD1->(dbSeek(xFilial("SD1")+(cAliasSB6)->B6_DOC+(cAliasSB6)->B6_SERIE+(cAliasSB6)->B6_CLIFOR+(cAliasSB6)->B6_LOJA+(cAliasSB6)->B6_PRODUTO))
				While !Eof() .And. xFilial("SD1") == SD1->D1_FILIAL .And.;
					(cAliasSB6)->B6_DOC == SD1->D1_DOC .And.;
					(cAliasSB6)->B6_SERIE == SD1->D1_SERIE .And.;
					(cAliasSB6)->B6_CLIFOR == SD1->D1_FORNECE .And.;
					(cAliasSB6)->B6_LOJA == SD1->D1_LOJA .And.;
					(cAliasSB6)->B6_PRODUTO == SD1->D1_COD

					// Verifica se o Item já n?o está somado para n?o duplicar a soma //
					cNF:= SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA+SD1->D1_COD+SD1->D1_ITEM
					If (cAliasSB6)->B6_IDENT==SD1->D1_IDENTB6
						If Ascan(aNF,cNF) = 0
							If l103
								aSaldo[4]+= ((SD1->D1_QUANT * SD1->D1_VUNIT) - SD1->D1_VALDESC)
							Else
								aSaldo[4]+= (SD1->D1_TOTAL - SD1->D1_VALDESC)
							EndIf
							aadd(aNF,cNF)
						EndIf
					EndIf

					SD1->(dbSkip())
				EndDo
			EndIf
			DbSelectArea(cAliasSB6)
			DbSkip()
		EndDo

		dbSelectArea(cAliasSB6)
		dbCloseArea()
		dbSelectArea("SB6")

	EndIf
	//
	// Calculo do valor unitario
	//
	aSaldo[3] := A410Arred(If(aSaldo[1]==0,aSaldo[3],Round(aSaldo[3]/aSaldo[1],TamSX3("D2_PRCVEN")[2])),"D2_PRCVEN")

	RestArea(aArqSF4)
	RestArea(aArqSB6)
	RestArea(aArq)

EndIf

Return(aSaldo)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ ConvData ³ Autor ³ Cristiane Maed	     ³ Data ³ 19.06.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Transforma chave lida em formato caracter (	/	/ ),		  ³±±
±±³			 ³ para data e em seguida para formato string "aaaa/mm/dd"    ³±±
±±³			 ³ em casos em que o indice do arquivo utiliza a funcao DTOS()³±±
±±³			 ³ ou DTOC().																  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1(ExpC1)															  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Indice do arquivo											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ AXPESQ																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ConvData(cKey,cConteudo)
Local nPos,cStr,nTam
Local aKey:= {},i,cConteudo1:=""

cKey := Upper(AllTrim(cKey))
cStr := Iif("DTOS" $ Upper(cKey),"DTOS","DTOC")
If "_FILIAL" == Subs(cKey,3,7)
	cKey := AllTrim(Subs(cKey,10))
ElseIf  "_FILIAL" == Subs(cKey,4,7)
	cKey := AllTrim(Subs(cKey,11))
EndIf
If Subs(cKey,1,1) == "+"
	cKey := Subs(cKey,2)
EndIf

While !Empty(cKey)
	nPos :=	AT("+",cKey)
	AADD(aKey,SubStr(cKey,1,Iif(nPos>0,nPos-1,Len(cKey))))
	cKey := Iif(nPos>0,SubStr(cKey,nPos+1),"")
End

For i:= 1 to Len(aKey)
	nTam := Len(&(aKey[i]))
	If cStr $ aKey[i]
		nTam := Len( Dtoc( MsDate() ) )
		cConteudo1 += DTOS(CTOD(SubStr(cConteudo,1,nTam)))
	Else
		cConteudo1 += Substr(cConteudo,1,nTam)
	End
	cConteudo	:= Substr(cConteudo,nTam+1)
Next i

Return cConteudo1

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Aleatorio ³ Autor ³ Jorge Queiroz  	     ³ Data ³			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera um numero aleatorio de acordo com a semente passada   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpN1:=Aleatorio(ExpN2,ExpN3) 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Numero aleatorio (entre 0 e ExpN2)					  ³±±
±±³			 ³ ExpN2 = Numero maximo a ser devolvido							  ³±±
±±³			 ³ ExpN2 = Semente de geracao do numero aleatorio. E' alta-   ³±±
±±³			 ³ mente recomEndavel que seja o proprio numero devolvido	  ³±±
±±³			 ³ anteriormente pela funcao. 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
function aleatorio(nMax,nSeed)
Local nRand:=0
While .T.
	nRand := (nSeed+(seconds()-int(seconds())))/.717171717
	nRand := nRand*log(ABS(nRand))
	nRand := ABS(nRand)+sqrt(nRand)
	nRand := nRand-Int(nRand)
	nRand := INT(nRand*(nMax+1))
	If (nRand!=nSeed)   //.AND.at(str(nRand,4),cString)==0
		exit
	EndIf
End
Return nRand

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ EvalGeneric³ Autor ³ Wagner Xavier	     ³ Data ³ 16/04/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ 																			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ EvalGeneric()						 				 				     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ SIGAFIN																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function EvalGeneric()


If GetStartMod1()
	If FunType() <> 3
		If ExistBlock(FunName())
			BEGIN SEQUENCE
			ExecBlock(FunName(),.F.,.F.)
			END SEQUENCE
			GetStartMod1(.f.)
		EndIf
	EndIf
EndIf
Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	  ³ Modelo2  ³ Autor ³ Juan Jose Pereira	  ³ Data ³ 18/09/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o  ³ Exibe Formulario Modelo 2 										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros ³ cTitulo=Titulo da Janela								 			  ³±±
±±³			  ³ aC=Array com campos do cabecalho								  ³±±
±±³			  ³ aR=Array com campos do rodape							  		  ³±±
±±³			  ³ aCGD=Array com coordenadas da GetDados	 			   	  ³±±
±±³			  ³ nOpcx=Modo de operacao 									        ³±±
±±³			  ³ cLineOk=Validacao de linha da GetDados					     ³±±
±±³			  ³ cAllOkk=Validacao de toda GetDados 						 	  ³±±
±±³			  ³ aGetsD=Array com gets editaveis 								  ³±±
±±³			  ³ bF4=bloco de codigo para tecla F4								  ³±±
±±³			  ³ cIniCpos=string com nome dos campos que devem ser inicia- ³±±
±±³			  ³			 lizados ao teclar seta para baixo. 				  ³±±
±±³			  ³ lDelGetD=determina se as linhas da Getdados podem ser de- ³±±
±±³			  ³			 letadas ou nao.											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		  ³ Generico																  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

FUNCTION Modelo2(cTitulo,aC,aR,aCGD,nOpcx,cLineOk,cAllOk,aGetsGD,bF4,cIniCpos,nMax,aCordW,lDelGetD,lMaximized, aButtons)

Local nOpca:=0, i,lAllOk
Local oDlg, cCampo, nX, nY, cCaption, cPict, cValid, cF3, cWhen, nLargSay, uConteudo, oSay, oGet
Local cBlkGet,cBlkWhen,cBlkVld, oSaveGetdad := Nil, aSvRot := Nil
Local oTop
Local oBottom
Local aObject := {}
Local aSize := {}
Local nObject
Local nAuxWidth := 0
Local lVentImp:= ( FindFunction("fvenImp"))
Local aAux:={}
Local nAux:=0

DEFAULT cLineOk := "AllwaysTrue()"
DEFAULT cAllOk  := "AllwaysTrue()"
DEFAULT nCOunt := 0
DEFAULT nOpcx	:= 3
DEFAULT aCGD	:= {}
DEFAULT lDelGetD := .T.
DEFAULT aButtons := {}

nOpcX := Iif(nOpcX<1,3,nOpcX)
cIniCpos := Iif(cIniCpos==Nil,"",cIniCpos)
nCount++
__cLineOk := cLineOK
__nOpcx	 := nOpcx
If nCount > 1
	oSaveGetdad := oGetDados
	oSaveGetdad:oBrowse:lDisablePaint := .t.
EndIf
oGets := {}
If Type("aRotina") == "A"
	aSvRot := aClone(aRotina)
EndIf
aRotina := {}
For nX := 1 to nOpcX
	AADD(aRotina,{"","",0,nOpcx})
Next

aCGD:=Iif(Len(aCGD)==0,{34,5,128,315},aCGD)

If !Empty(bF4)
	SetKey(VK_F4,bF4)
EndIf

aCordW :=Iif(aCordW==Nil,{125,0,400,635},aCordW)
IF lVentImp

 	aAux:=	fvenImp(oMainWnd,@nAux)

	IF Len(aAux)>0
		aCordW:=ACLONE( aAux )
	ENDIF

ENDIF

DEFINE MSDIALOG oDlg TITLE OemToAnsi(cTitulo) FROM aCordW[1],aCordW[2] TO aCordW[3],aCordW[4] PIXEL OF oMainWnd

If ValType(lMaximized) == "L"
	oDlg:lMaximized := lMaximized
EndIf

If Len(aC) > 0

	@00,00 SCROLLBOX oTop
	Aadd(aSize,aCGD[1]+13)
	Aadd(aObject,oTop)
	nObject := 2

	For i:=1 to Len(aC)
		If Len(aC[i])==7 .Or. Len(aC[i])==8
			cCampo:=aC[i,1]
			nX:=aC[i,2,1]-13
			nY:=aC[i,2,2]
			cCaption:=Iif(Empty(aC[i,3])," ",aC[i,3])
			cPict:=Iif(Empty(aC[i,4]),Nil,aC[i,4])
			cValid:=Iif(Empty(aC[i,5]),".t.",aC[i,5])
			cF3:=Iif(Empty(aC[i,6]),NIL,aC[i,6])
			cWhen:=Iif(aC[i,7]==NIL,".t.",Iif(aC[i,7],".t.",".f."))
			cWhen := Iif(!(Str(nOpcx,1,0)$"346"),".f.",cWhen)
			cBlKSay := "{|| OemToAnsi('"+cCaption+"')}"
			oSay := TSay():New( nX+1, nY, &cBlkSay,oTop,,, .F., .F., .F., .T.,,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8  // estava 2.2
			cCaption := oSay:cCaption

			cBlkGet := "{ | u | If( PCount() == 0, "+cCampo+","+cCampo+":= u ) }"
			cBlKVld := "{|| "+cValid+"}"
			cBlKWhen := "{|| "+cWhen+"}"

			If Len(aC[i])==8 .And. ValType(aC[i,8]) == "L" .And. aC[i,8]
				nAuxWidth  := CalcMemoSize(80)
				oGet := TMultiGet():New(nX,nY+nLargSay,&cBlKGet,oTop,nAuxWidth,27,,.F.,,,,.T.,,.F.,&(cBlkWhen),.F.,.F.,.F.,&(cBlkVld),,.F.,.F.,.T.)
			Else
				oGet := TGet():New( nX, nY+nLargSay,&cBlKGet,oTop,,,cPict, &(cBlkVld),,,, .F.,, .T.,, .F., &(cBlkWhen), .F., .F.,, .F., .F. ,cF3,(cCampo))
			EndIf
			AADD(oGets,oGet)
		EndIf
	Next
EndIf

// 	  oGetDados:=MSGetDados():New(aCGD[1],aCGD[2],aCGD[3],aCGD[4],1,"__MOD2LineOK()","AllwaysTrue()","",.T.,aGetsGD, , , ,"__FieldOk()")
oGetDados:=MSGetDados():New(aCGD[1],aCGD[2],aCGD[3],aCGD[4],nOpcX,"__MOD2LineOK()","AllwaysTrue()",cIniCpos,lDelGetD,aGetsGD, , ,nMax ,"__FieldOk()")
Aadd(aObject,oGetDados:oBrowse)
Aadd(aSize,NIL)

If Len(aR) > 0

	@00,00 SCROLLBOX oBottom
	Aadd(aSize,(aCordW[3]/2-aCGD[1]-aCGD[3])-nAux)
	Aadd(aObject,oBottom)
	nObject := 2

	For i:=1 to Len(aR)
		If Len(aR[i])==7 .Or. Len(aR[i])==8
			cCampo:=aR[i,1]
			nX:=aR[i,2,1]-aCGD[3]
			nY:=aR[i,2,2]
			cCaption:=Iif(Empty(aR[i,3])," ",aR[i,3])
			cPict:=Iif(Empty(aR[i,4]),NIL,aR[i,4])
			cValid:=Iif(Empty(aR[i,5]),".t.",aR[i,5])
			cF3:=Iif(Empty(aR[i,6]),NIL,aR[i,6])
			cWhen:=Iif(aR[i,7]==NIL,".t.",Iif(aR[i,7],".t.",".f."))
			cWhen := Iif(!(Str(nOpcx,1,0)$"346"),".f.",cWhen)
			cBlKSay := "{|| OemToAnsi('"+cCaption+"')}"
			oSay := TSay():New( nX+1, nY, &cBlkSay,oBottom,,, .F., .F., .F., .T.,,,,, .F., .F., .F., .F., .F. )
			nLargSay := GetTextWidth(0,cCaption) / 1.8
			cCaption := oSay:cCaption

			cBlkGet := "{ | u | If( PCount() == 0, "+cCampo+","+cCampo+":= u ) }"
			cBlKVld := "{|| "+cValid+"}"
			cBlKWhen := "{|| "+cWhen+"}"

			If Len(aR[i])==8 .And. ValType(aR[i,8]) == "L" .And. aR[i,8]
				nAuxWidth  := CalcMemoSize(80)
				oGet := TMultiGet():New(nX,nY+nLargSay,&cBlKGet,oBottom,nAuxWidth,27,,.F.,,,,.T.,,.F.,&(cBlkWhen),.F.,.F.,.F.,&(cBlkVld),,.F.,.F.,.T.)
			Else
				oGet := TGet():New( nX, nY+nLargSay,&cBlKGet,oBottom,,,cPict, &(cBlkVld),,,, .F.,, .T.,, .F., &(cBlkWhen), .F., .F.,, .F., .F. ,cF3,(cCampo))
			EndIf
			AADD(oGets,oGet)
		EndIf
	Next
EndIf

ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{||nOpca:=1,lAllOk:=__Mod2OK(cAllOk),Iif(lAllOk,oDlg:End(),nOpca:=0)},{||oDlg:End()},,aButtons),;
AlignObject(oDlg,aObject,1,nObject,aSize),oGetDados:oBrowse:Refresh())

nCount--
If nCount > 0
	oGetDados := oSaveGetDad
	oGetDados:oBrowse:lDisablePaint := .f.
EndIf
If ValType(aSvRot) == "A"
	aRotina := aClone(aSvRot)
EndIf

SetKey(VK_F4,{||Nil})

Return (nOpca==1)

Function __Mod2LineOk()
Local lRet:=.t. , ni
If Str(__nOpcx,1,0)$"346"
	lRet := &__cLineOK
	For ni:= 1 to Len(oGets)
		oGets[ni]:Refresh()
	Next
EndIf
Return lRet

Static Function __Mod2Ok(cAllOK)
Local lRet
lRet := __Mod2LineOk()
If lRet
	lRet := &cAllOk
EndIf
Return lRet

Function CallMod2Obj()
Return oGetDados

Function __FieldOk()
Local ni
For ni:= 1 to Len(oGets)
	oGets[ni]:Refresh()
Next
Return .t.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³Modelo3	  ³ Autor ³ Wilson				  ³ Data ³ 17/03/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Enchoice e GetDados													  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³lRet:=Modelo3(cTitulo,cAlias1,cAlias2,aMyEncho,cLinOk, 	  ³±±
±±³			 ³ cTudoOk,nOpcE,nOpcG,cFieldOk,lVirtual,nLinhas,aAltEnchoice)³±±
±±³			 ³lRet=Retorno .T. Confirma / .F. Abandona						  ³±±
±±³			 ³cTitulo=Titulo da Janela 											  ³±±
±±³			 ³cAlias1=Alias da Enchoice									 		  ³±±
±±³			 ³cAlias2=Alias da GetDados											  ³±±
±±³			 ³aMyEncho=Array com campos da Enchoice							  ³±±
±±³			 ³cLinOk=LinOk 															  ³±±
±±³			 ³cTudOk=TudOk 															  ³±±
±±³			 ³nOpcE=nOpc da Enchoice												  ³±±
±±³			 ³nOpcG=nOpc da GetDados												  ³±±
±±³			 ³cFieldOk=validacao para todos os campos da GetDados 		  ³±±
±±³			 ³lVirtual=Permite visualizar campos virtuais na enchoice	  ³±±
±±³			 ³nLinhas=Numero Maximo de linhas na getdados				  	  ³±±
±±³			 ³aAltEnchoice=Array com campos da Enchoice Alteraveis		  ³±±
±±³			 ³nFreeze=Congelamento das colunas.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³RdMake 																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Modelo3(cTitulo,cAlias1,cAlias2,aMyEncho,cLinOk,cTudoOk,nOpcE,nOpcG,cFieldOk,lVirtual,nLinhas,aAltEnchoice,nFreeze,aButtons,aCordW,nSizeHeader)
Local lRet, nOpca := 0,nReg:=(cAlias1)->(Recno()),oDlg
Local oEnchoice
Local nDlgHeight
Local nDlgWidth
Local nDiffWidth := 0
Local nDiffHeight := 0
Local lMDI := .F.
Local lPlugin := .F.
Local nTop := 32
Local aSize := {}

Private Altera:=.t.,Inclui:=.t.,lRefresh:=.t.,aTELA:=Array(0,0),aGets:=Array(0),;
bCampo:={|nCPO|Field(nCPO)},nPosAnt:=9999,nColAnt:=9999
Private cSavScrVT,cSavScrVP,cSavScrHT,cSavScrHP,CurLen,nPosAtu:=0

If IsPlugin()
	lPlugin := .T.
EndIf

nOpcE := If(nOpcE==Nil,3,nOpcE)
nOpcG := If(nOpcG==Nil,3,nOpcG)
lVirtual := Iif(lVirtual==Nil,.F.,lVirtual)
nLinhas:=Iif(nLinhas==Nil,99,nLinhas)

If SetMDIChild()
	oMainWnd:ReadClientCoors()
	nDlgHeight := oMainWnd:nHeight
	nDlgWidth := oMainWnd:nWidth
	lMdi := .T.
	nDiffWidth := 2
	If lPlugin
		nDiffHeight := 25
	EndIf
Else
	If lPlugin
		nDlgHeight := oMainWnd:nHeight-55
		nDlgWidth	:= oMainWnd:nWidth-12
		nDiffHeight := 80
		nTop := 10
	Else
		nDlgHeight := oMainWnd:nHeight-50
		nDlgWidth	:= oMainWnd:nWidth-27
	Endif
	nDiffWidth := 7
EndIf

Default aCordW := {nTop,000,nDlgHeight,nDlgWidth}
Default nSizeHeader := 110

Aadd(aSize,nSizeHeader)

DEFINE MSDIALOG oDlg TITLE cTitulo From aCordW[1],aCordW[2] to aCordW[3],aCordW[4] Pixel of oMainWnd
If lMdi
	oDlg:lMaximized := .T.
EndIf

oEnchoice := Msmget():New(cAlias1,nReg,nOpcE,,,,aMyEncho,{13,1,(nSizeHeader/2)+13,If(lMdi, (oMainWnd:nWidth/2)-2,__DlgWidth(oDlg)-nDiffWidth)},aAltEnchoice,3,,,,oDlg,,lVirtual,,,,,,,,.F.)

oGetDados := MsGetDados():New((nSizeHeader/2)+13+2,1,(oMainWnd:nHeight/2)-nDiffHeight,oMainWnd:nWidth/2-nDiffWidth,nOpcG,cLinOk,cTudoOk,"",.T.,,nFreeze,,nLinhas,cFieldOk,,,,oDlg)

ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{||nOpca:=1,If(oGetDados:TudoOk(),If(!obrigatorio(aGets,aTela),nOpca := 0,oDlg:End()),nOpca := 0)},{||oDlg:End()},,aButtons),AlignObject(oDlg,{oEnchoice:oBox,oGetDados:oBrowse},1,,aSize))

lRet:=(nOpca==1)
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³RegToMemory ³ Autor ³ Wilson			     ³ Data ³ 17/03/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria variaveis M-> para uso na modelo3()						  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³Modelo3																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function RegToMemory(cAlias,lInc,lDic,lInitPad, cStack)

Local aArea    := GetArea()
Local aAreaSX3 := SX3->(GetArea())
Local nX    := 0
Local cCpo  := ""

DEFAULT lInc := .F.
DEFAULT lDic := .T.
DEFAULT lInitPad := .T.
DEFAULT __HasSNPrvt := FindFunction('_SETNAMEDPRVT')

If ( cStack != NIL ) .And. ( ! __HasSNPrvt )
	UserException( 'Cannot find function _SetNamedPrvt' )
EndIf

If lDic
	DbSelectArea("SX3")
	DbSetOrder(1)
	DbSeek(cAlias)
	While !Eof() .and. SX3->X3_ARQUIVO == cAlias
		DbSelectArea(cAlias)
		If SX3->X3_CONTEXT == "V" .or. lInc
			If ( cStack == NIL )
				_SetOwnerPrvt(Trim(SX3->X3_CAMPO),CriaVar(Trim(SX3->X3_CAMPO),lInitPad))
			Else
				_SetNamedPrvt(Trim(SX3->X3_CAMPO),CriaVar(Trim(SX3->X3_CAMPO),lInitPad), cStack)
			EndIf
		Else
			cCpo := (cAlias+"->"+Trim(SX3->X3_CAMPO))
			If ( cStack == NIL )
				_SetOwnerPrvt(Trim(SX3->X3_CAMPO),&cCpo)
			Else
				_SetNamedPrvt(Trim(SX3->X3_CAMPO),&cCpo, cStack)
			EndIf
		EndIf
		DbSelectArea("SX3")
		DbSkip()
	EndDo
Else
	dbSelectArea(cAlias)
	For nX := 1 To FCount()
		If ( lInc )
			cCpo := CriaVar(Trim(FieldName(nX)),lInitPad)
		Else
			cCpo := &(cAlias+"->"+Trim(FieldName(nX)))
		EndIf

		If ( cStack == NIL )
			_SetOwnerPrvt(Trim(FieldName(nX)),cCpo)
		Else
			_SetNamedPrvt(Trim(FieldName(nX)),cCpo, cStack)
		EndIf
	Next nX
EndIf
RestArea(aAreaSX3)
RestArea(aArea)
Return(Nil)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ Fun‡„o	  ³ xCxLoja  ³ Autor ³ Fernando Godoy		   ³Data  ³ 27/02/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Descri‡„o ³ Retorna o Caixa/Agencia/Conta geral da Loja 	 			   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Sintaxe   ³ xCxLoja()																   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function xCxLoja()
Local cCaixa
Local aCaixaRet := {}
Local cAlias 	:= Alias()
Local nReg	 	:= Recno()
Local cOrder 	:= IndexOrd()
Local nTam1		:= TamSX3("A6_COD")[1]
Local nTam2		:= TamSX3("A6_AGENCIA")[1]
Local nTam3		:= TamSX3("A6_NUMCON")[1]
Local nSizeFil  := 2
Local cBcoCx 	:= ""
Local cAgeCx 	:= ""
Local cCtaCx 	:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VeIfica a existencia do parametro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !GetMv("MV_CXLOJA", .T.)
	GetMv("MV_CXLOJA")
	cCaixa	 := " "
Else
	cCaixa	 := GetMv("MV_CXLOJA")
EndIf

//-- Atualiza o conteúdo da filial
nSizeFil := FWSizeFilial()

If Substr(cCaixa,nTam1 + 1,1) != "/" .or.;
	Substr(cCaixa,((nTam1 + 1) + (nTam2 + 1)) ,1) != "/" .or.;
	Empty(Substr(cCaixa,1,nTam1)) .or.;
	Empty(Substr(cCaixa,nTam1 + 2 , nTam2)) .or.;
	Empty(Substr(cCaixa,((nTam1 + 1) + (nTam2 + 1) + 1),(nTam1 + 1) + (nTam2 + 1)))
	If GetMv("MV_CXLOJA", .T.)
		RecLock("SX6",.F.)
	Else
		RecLock("SX6", .T.)
		SX6->X6_FIL      := Space(nSizeFil)
		SX6->X6_VAR      := "MV_CXLOJA"
		SX6->X6_TIPO     := "C"
	EndIf
	SX6->X6_CONTEUD := Padr("CL1",nTam1) + "/" + PadL("1",nTam2,"0") + "/" + PadL("1",IIF(nTam3>10,10,nTam3),"0")
	SX6->(MsUnlock())
EndIf

cCaixa := GetMv("MV_CXLOJA")

cBcoCx := Substr(cCaixa,1,At("/",cCaixa) - 1)

cAgeCx := Substr(cCaixa,At("/",cCaixa) +1 ,(Rat("/",cCaixa)-At("/",cCaixa)-1))
cAgeCx := StrZero(Val(cAgeCx),nTam2)

// Alterado para gerar o cCtaCx exatamente igual o retorno do parametro MV_CXLOJA
cCtaCx := Substr(cCaixa,Rat("/",cCaixa) +1,Len(SE1->E1_CONTA))
cCtaCx := Alltrim(cCtaCx)

Aadd(aCaixaRet, cBcoCx)
Aadd(aCaixaRet, cAgeCx)
Aadd(aCaixaRet, cCtaCx)

DbSelectArea("SA6")
DbSetOrder(1)
If !DbSeek(xFilial()+aCaixaRet[1]+aCaixaRet[2]+aCaixaRet[3])
	RecLock("SA6",.T.)
	SA6->A6_FILIAL	:= xFilial()
	SA6->A6_COD		:= aCaixaRet[1]
	SA6->A6_AGENCIA	:= aCaixaRet[2]
	SA6->A6_NUMCON	:= aCaixaRet[3]
	SA6->A6_NOME	:= STR0041 // CAIXA GERAL DA LOJA
	SA6->A6_NREDUZ	:= STR0042 // CAIXA GERAL LJ
	IF SA6 -> (FIELDPOS("A6_DV")) > 0 //Campo existente apenas nos países ANG, EQU e HAI
		SA6 -> A6_DV := FINMod9710( aCaixaRet[1] + aCaixaRet[2] + aCaixaRet[3] )
	ENDIF
	MsUnLock()
EndIf

DbSelectArea("SX5")
DbSetOrder(1)
If ! DbSeek(xFilial()+"23"+aCaixaRet[1])
	RecLock("SX5",.T.)
	SX5->X5_FILIAL	:= 	xFilial("SX5")
	SX5->X5_TABELA	:= "23"
	SX5->X5_CHAVE	:= aCaixaRet[1]
	SX5->X5_DESCRI	:= STR0041 // CAIXA GERAL DA LOJA
	SX5->X5_DESCSPA	:= STR0041 // CAIXA GERAL DA LOJA
	SX5->X5_DESCENG	:= STR0041 // CAIXA GERAL DA LOJA
	MsUnLock()
EndIf
DbSelectArea(cAlias)
DbSetOrder(cOrder)
DbGoTo(nReg)

Return aCaixaRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³NetCancel   ³ Autor ³ Alessandro B. Freire³ Data ³ 19/05/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Envia mensagem ao usuario									 		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³Generico													  				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION NetCancel()
Return (APMsgYesNo(OemToAnsi(STR0022),OemToAnsi(STR0023))) //"H  outro usu rio utilizando o SX8. Deseja Cancelar ?" ### "Aten‡„o"


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ ArrayTP8 ³ Autor ³ Rogerio F. Guimaraes  ³ Data ³ 31.10.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina para trazer array com os itens da cond pagto tipo 9  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ExpA1 := ArrayTP8(ExpN1)									  			³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array contEndo os dias e percentuais de cada parcel ³±±
±±³			 ³ ExpN1 = E4_COND														   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 																   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 						   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO						   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Rogerio F.G. ³24/11/97³B.HUR ³Alt Cond Pagto Tipo 8 - validacao		   ³±±
±±³ Rogerio/Edson³09/12/97³12551 ³Alt Validacao para o Percent. Digitado   ³±±
±±³ Edson  M.	  ³25/02/98³XXXXX ³Acerto da validacao da cond. Tipo 8.	   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION ArrayTP8(cCond)
Local cString,cPercent,cDias
Local nPos1:=nPos2:=0
Local aTp8:={},aDias:={},aPercent:={},i:=0
Local nPerc:=0

cString:=cCond
nPos1:=Rat("[",cString)
nPos2:=Rat("]",cString)

cPercent := Substr(cString,nPos1+1,nPos2-(nPos1+1))
cString:=Stuff(cString,nPos1-1,(nPos2+1)-(nPos1-1),"")

nPos1:=Rat("[",cString)
nPos2:=Rat("]",cString)
cDias := Substr(cString,nPos1+1,nPos2-(nPos1+1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VeIfica nro de parcelas e percentuais ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While Len(cDias) > 0
	AAdd(aDias,Val(Parse(@cDias)))			// Adiciona os dias para pagamento
End

While Len(cPercent) > 0
	AAdd(aPercent,Val(Parse(@cPercent)))	// Adiciona os percentuais de pagamento
	If aPercent[Len(aPercent)] == 0
		Return aTp8 := {}
	EndIf
End

If Len(aDias) == 0 .Or. Len(aPercent) == 0
	Return aTp8:={}
EndIf

If Len(aDias) != Len(aPercent)					// VerIfica discrepancia
	Return aTP8:={}
EndIf

For i := 1 to Len(aDias)
	AAdd(aTp8,{aDias[i],aPercent[i]})			// Atualiza Retorno, acumula percentuais
	nPerc += aTp8[i][2]
Next

If nPerc < 100 .Or. nPerc > 100
	aTp8 := {}
EndIf
Return aTp8

Function x2Path(cAlias)
Local cSavAlias := Alias(),nRecSx2,cRet:=""
Local cFilter

DbSelectArea("SX2")
cFilter := dbFilter()
nRecSx2 := Recno()
DbClearFilter()
If DbSeek(cAlias)
	cRet := PADR(AllTrim(x2_path)+AllTrim(x2_arquivo),50)
EndIf
Set Filter to &cFilter
DbGoTo(nRecSx2)

If ( ! Empty(cSavAlias) )
	DbSelectArea(cSavAlias)
EndIf
Return cRet

Function SuperVal(cVar)
Local xVar:="",ni, cByte

If ValType( cVar ) == "N"
	Return( cVar )
EndIf

For ni:= 1 to Len(cVar)
	cByte := Subs(cVar,ni,1)
	If cByte == ","
		cByte := "."
	endif
	If cByte $"1234567890.-"
		xVar+= cByte
	EndIf
Next
Return (Val(xVar))

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ Final 	³ Autor ³ Ary Medeiros	  	     ³ Data ³		     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Finaliza Aplicativo com janela propria 					     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 												              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

//REMOVIDA PARA LIB
Function Final(cStr,cStr1)
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ As fun‡”es Posicione e RetField       ³
//³ fazem exatamente a mesma coisa        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
function RetField(cAlias,nOrder,cChave,cCampo)
Return Posicione(cAlias,nOrder,cChave,cCampo)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³AxPesqInd	³ Autor ³ Bruno Sobieski		  ³ Data ³ 23.03.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisas genericas em arquivos com Indice cond. ativo.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ AxPesqInd(cTxtInd,cArqInd)											  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cTxtInd = Texto exibido no ComboBox com a chave do indice. ³±±
±±³          ³ cArqInd = Nome de arquivo de indice temporario.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Generico 											      			  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
FUNCTION axPesqInd(cTxtInd,cArqInd)
Local cAlias,cCampo,nReg,oDlg,nOpca:=0,bSav12 := SetKey(VK_F12,Nil)
Local cFil:="",nChave:=1,dCampo,cOrd,oCbx,nSavReg
Local	nOrder	:=	IndexOrd(), aOrd	:=	{}
Local nSizeFil := 2
*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
*³Salva a Integridade dos Dados														³
*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cTxtInd==Nil .Or. cArqInd==Nil
	Return
EndIf
cAlias  := ALIAS()
DbSelectArea(cAlias)
cCpofil := PrefixoCpo(cAlias)+"_FILIAL"
nSavReg := Recno()
If At(cCpoFil,Indexkey())==1 //TYPE(cCpoFil) != "U"
	DbSeek(cFilial)
Else
	DbGoTop()
EndIf
If Eof()
	Help(" ",1,"A000FI")
	SetKey(VK_F12,bSav12)
	Return 3
EndIf
DbGoTo(nSavReg)

Aadd(aOrd,OemToAnsi(" "+cTxtInd))
cOrd := cTxtInd
cCampo:=SPACE(40)
DEFINE MSDIALOG oDlg FROM 5, 5 TO 14, 50 TITLE OemToAnsi(STR0010) //"Pesquisa"

@ 0.6,1.3 COMBOBOX oCBX VAR cOrd ITEMS aOrd  SIZE 165,44 OF oDlg FONT oDlg:oFont
@ 2.1,1.3 MSGET cCampo SIZE 165,10
DEFINE SBUTTON FROM 055,122	TYPE 1 ACTION (nOpca := 1,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 055,149.1 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg CENTERED
If nOpca == 0
	SetKey(VK_F12,bSav12)
	Return 0
EndIf
nReg := RecNo()
Set Softseek On
DbSetOrder(0)

If At(PrefixoCpo(cAlias)+"_FILIAL",Indexkey())==1 //Procura pela filial
	cFil:=cFilial
	nChave:=11
EndIf

If ("DTOS" $ Upper(IndexKey(nOrder))) .or. ("DTOC" $ Upper(IndexKey(nOrder)))
	cCampo := ConvData(IndexKey(nOrder),cCampo)
EndIf

If SubStr(cAlias,1,3) == "SM2"
	dCampo:=Ctod(AllTrim(cCampo))
	DbSeek(dCampo)
EndIf
If !(At(PrefixoCpo(cAlias)+"_FILIAL",Indexkey())==1) .And. !("SM2" $ cAlias)  //cAlias $ "SX6/SX9"
	DbSeek(Trim(ccampo))
ElseIf cAlias != "SM2"
	//-- Atualiza o conteúdo da filial
	nSizeFil := FWSizeFilial()
	DbSeek(cFilial+Trim(cCampo))
	If Subs(&(IndexKey()),1,nSizeFil) != cFilial	 // IR Para EOF
		DbSeek(chr(255))
	EndIf
EndIf
Set SoftSeek Off
lRefresh := .t.
SetKey(VK_F12,bSav12)
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ MaParcela  ³ Autor ³ Ben-Hur M Castilho  ³ Data ³ 14/02/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calcula o Numero da Proxima Parcela 			 		 	     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ MATA460													  				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MaParcela( cParcela )
Local cBack    := ""
Local nTamParc := Len(SE1->E1_PARCELA)
If nTamParc == 1
	Do Case
		Case (cParcela >= "0" .And. cParcela <= "8")
			cBack := Str( (Val( cParcela )+1),1 )
		Case (cParcela >= "A" .And. cParcela <= "Y")
			cBack := Chr( (Asc( cParcela )+1) )
		Case (cParcela >= "a" .And. cParcela <= "y")
			cBack := Chr( (Asc( cParcela )+1) )
		Case cParcela = "Z"
			cBack := "a"
		OtherWise
			cBack := "A"
	EndCase
Else
	cBack := Soma1(cParcela,nTamParc)
EndIf
Return( cBack )

Function PesqInit(aLista,oFather,nAtu,bGet,cVar)
Local oGet
Local nWidth
Local nTop
Local cPict
Local cF3
Local bSay

nTop := PESQ_TOP+((nAtu-1)*PESQ_SKIP)
If aLista[nAtu][2] == "D"
	cPict := "@D"
	nWidth := 40
Else
	cPict := If(Empty(aLista[nAtu][6]),NIL,AllTrim(aLista[nAtu][6]))
	nWidth := CalcFieldSize(aLista[nAtu][2],aLista[nAtu][3],0,," ")
EndIf

If '"'$aLista[nAtu][5]
	bSay := &("{|| '"+OemToAnsi(aLista[nAtu][5])+"'}")
Else
	bSay := &('{|| "'+OemToAnsi(aLista[nAtu][5])+'"}')
EndIf

nWidth += 10
nWidth := Iif(nWidth > 130,130,nWidth)
cF3 := If(Empty(aLista[nAtu][1]),NIL,aLista[nAtu][1])
If Empty(cF3) .And. "_FILIAL" $ aLista[nAtu][7]
	cF3 := "SM0"
EndIf
TSay():New(nTop,10,bSay,oFather,,,.F.,.F.,.F.,.T.,,,(Len(Trim(aLista [nAtu][5]))*8),15,.F.,.F.,.F.,.F.,.F.)
oGet := TGet():New(nTop,70,bGet,oFather,nWidth,10,cPict,,,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,cF3,cVar,,,,.T.)
oGet:cSX1Hlp := aLista[nAtu][7]
Return

Function PesqList(cAlias,lSeeAll,aPesqVar,aReturn,cMsg)
Local cSvAlias := Alias()
Local nI := 1
Local cChave, nAt1
Local cF3 := ""
Local nPos := 0
Local nSx3Ord := SX3->(IndexOrd())
Local nJ	:= 1
Local aKey	:= {}
Local lOK := .T.
Local lFilial

DEFAULT lSeeALL := .f.

aReturn := {}

DbSelectArea("SIX")
If DbSeek(cAlias)
	While !Eof() .And. SIX->INDICE == cAlias

		If  ( SIX->SHOWPESQ == 'N' )
			DbSkip()
			Loop
		EndIf

		nPos++
		Aadd(aReturn,{})
		Aadd(aPesqVar,{})

		nI 	:= 0
		aKey:= StrTokArr( Upper(SIX->CHAVE), '+' )
		nAt1:= 1
		lFilial := ( '_FILIAL+' $ SIX->CHAVE )

		For nJ := 1 To Len( aKey )

			cChave := AllTrim(aKey [nJ])

			If ( '(' $ cChave )

				nAt1 := At('(', cChave )
				While ( nAt1 > 0 )
					cChave	:= Subs( cChave, nAt1 + 1 )
					nAt1	:= At('(', cChave )
				End

				nAt1 := At(',', cChave )

				If ( nAt1 > 0 )
					cChave := Subs( cChave, 1, nAt1 - 1 )
				EndIf

				nAt1 := At(')', cChave )

				If ( nAt1 > 0 )
					cChave := Subs( cChave, 1, nAt1 - 1 )
				EndIf

			EndIf

			If ( '->' $ cChave )
				cChave := Subs(cChave,At('->',cChave)+2)
			EndIf

			If !( 'FILIAL' $ cChave) .or. lSeeALL
				nI++
				Aadd(aPesqVar[nPos],NIL)

				If ('_FILIAL|' $ cChave+"|" )
					cF3 := ""
				Else
					cF3 := PesqF3(SIX->F3,nI-If(lFilial .And. lSeeAll,1,0))
				EndIf
				DbSelectArea("SX3")
				DbSetOrder(2)
				If DbSeek(cChave)
					Aadd(aReturn[nPos],{cF3,SX3->X3_TIPO,SX3->X3_TAMANHO,SX3->X3_DECIMAL,X3Titulo(),SX3->X3_PICTURE,SX3->X3_CAMPO})
					If SX3->X3_TIPO == "D"
						aPesqVar[nPos][ni] := Ctod("")
					ElseIf SX3->X3_TIPO == "C"
						aPesqVar[nPos][ni] := Space(SX3->X3_TAMANHO)
					ElseIf SX3->X3_TIPO == "N"
						aPesqVar[nPos][ni] := 0
					EndIf
				Else
					lOK := .F.
					Exit
				EndIf
			EndIf
		Next

		If !lOK
			Exit
		EndIf

		DbSelectArea("SIX")
		DbSkip()
	End
EndIf

SX3->(DbSetOrder(nSx3Ord))
DbSelectArea(cSvAlias)
Return lOK

Static Function PesqF3(cString,nPos)
Local cReturn := ""
Local i := 0, nAt

If !Empty(cString)
	While ( i <= nPos )
		i++
		nAt := At("+",cString)
		If ( nAt == 0 )
			cReturn := Trim(cString)
			cString := ""
		Else
			cReturn := Trim(Subs(cString,1,nAt-1))
			cString := Subs(cString,nAt+1)
		EndIf

		If i == nPos
			If cReturn == "XXX"
				cReturn := ""
			EndIf
			Exit
		EndIf
	End
EndIf
Return cReturn

Function PesqDetail(lDetail,oDlg,aScroll,oBigGet,nOrd,oPPreview)

lDetail := !lDetail
If lDetail
	If oPPreview <> NIL
		oPPreview:Hide()
	EndIf
	oBigGet:Hide()
	If !Empty(aScroll)
		aScroll[nOrd]:Show()
	EndIf
	If oDlg <> NIL
		oDlg:ReadClientCoors(.T.)
		oDlg:Move(oDlg:nTop,oDlg:nLeft,498,250,,.T.)
	EndIf
Else
	If oPPreview <> NIL
		oPPreview:Hide()
	EndIf
	aScroll[nOrd]:Hide()
	oBigGet:Show()
	If oDlg <> NIL
		oDlg:ReadClientCoors(.T.)
		oDlg:Move(oDlg:nTop,oDlg:nLeft,498,127,,.T.)
	EndIf
EndIf
Return lDetail
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ MoedFin   ³Autor  ³ Bruno Sobieski       ³ Data 17.07.2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Retorna a quantidade de moedas usadas pelo modulo financeiro³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³                   											   	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MoedFin
Static  nCnt

If ( nCnt == Nil )
	nCnt := 1
	While SM2->(FieldPos("M2_MOEDA"+Alltrim(Str(nCnt)))) <> 0
		nCnt++
	EndDo
EndIf
Return( nCnt - 1)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ap5TObtv()³Autor  ³ Marcelo Abe          ³ Data 16.08.2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Funcao que transforma o File.AP5(Btrieve) para File.DDF.
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Programa chamado pelo menu do configurador
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³
±±³          ³
±±³          ³
±±³          ³
±±³          ³
±±³          ³
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³                   											   	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ap5TObtv()
Local aRet:={},i,oDlg,oPath,cPath:=Space(120)
Local oTables,aTables:={},oPathTab,cPathTab:=SPACE(120),cDest:=SPACE(120)
Local oOk := LoadBitmap( GetResources(), "LBOK" )
Local oNo := LoadBitmap( GetResources(), "LBNO" )
Private cDir := ""

//Pego as tabelas contidas no File.AP5
aRet := BTVTables()
aSort(aRet)

DEFINE MSDIALOG oDlg TITLE STR0034 From 9,0 To 30,62  //"Gerar DDF Padrao""AgEndamento"
@ 0.5, 0.9  SAY STR0035 of oDlg   //"Informe o diretorio Default dos Arquivos(*.dat)"
@ 1.1, 0.9  MSGET oPath VAR cPath Picture "@!"  SIZE 150,10 OF oDlg
For i:=1 To Len(aRet)
	aadd(aTables,{.T.,aRet[i],""})
Next
oPath:bLostFocus := { || cPathTab:=Ap5ToDDFDir(aTables,cPath,oTables),oPathTab:Refresh() }
@ 2.5, 0.9  SAY STR0036 of oDlg   //"Escolha os Arquivos:"
@ 42,07 LISTBOX oTables FIELDS HEADER " ",STR0037;  //"Arquivo"
FIELDSIZES 14, 50 SIZE 70, 70 PIXEL OF oDlg
oTables:bChange := { || cPathTab:=Ap5ToDDFBO(oTables,aTables),oPathTab:Refresh() }
oTables:SetArray(aTables)
oTables:bLine := {|| {Iif(aTables[oTables:nAt,1],oOK,oNo),aTables[oTables:nAt,2]} }
oTables:bLDblClick := { |nRowPix, nColPix, nKeyFlags| If(oTables:nAtCol(nColPix)==1,(aTables[oTables:nAt,1] := !aTables[oTables:nAt,1],oTables:DrawSelect()),)}
oTables:lhScroll := .F.

@ 3.1, 11  SAY STR0038 of oDlg   //"Diretorio do Arquivo Selecionado"
@ 3.7, 11  MSGET oPathTab VAR cPathTab SIZE 100,10 Picture "@!" OF oDlg
oPathTab:bLostFocus := { || Ap5ToDDFTb(aTables,cPathTab,oTables) }

@ 9.0, 0.9  SAY STR0039 of oDlg   //"Diretorio da Geracao deste DDF(Abaixo do RootPath)"
@ 9.6, 0.9  MSGET oDest VAR cDest SIZE 150,10 Picture "@!" OF oDlg

DEFINE SBUTTON FROM 145,90 TYPE 1 ENABLE OF oDlg ACTION ap5toddfOK(aTables,cDest) //Iif((lInclui==.T. .or. lAltera==.T.),Grava(lInclui),Iif(lDeleta,Deleta(),oDlg:End()))
DEFINE SBUTTON FROM 145,120 TYPE 2 ENABLE OF oDlg ACTION (oDlg:End())

ACTIVATE MSDIALOG oDlg CENTERED
Return .T.

Static Function ap5toddfOK(aTables,cDest)
Local aRet1:={}
Local i
For i:=1 to Len(aTables)
	If aTables[i][1] = .T.
		aadd(aRet1,{aTables[i][2],aTables[i][3]})
	EndIf
Next
BTVCreateDDFs(aRet1,cDest)
MessageBox(STR0040,"AP5",0) //"DDF Gerado"
Return .T.

Static Function Ap5ToDDFDir(aTables,cPath,oTables)
Local i
If cDir <> cPath
	For i:=1 to Len(aTables)
		If aTables[i][3] = cDir
			aTables[i][3] := cPath
		EndIf
	Next
	cDir := cPath
EndIf
Return AP5TODDFBO(oTables,aTables)

Static Function AP5TODDFBO(oTables,aTables)
Return aTables[oTables:nAt,3]

Static Function Ap5ToDDFTb(aTables,cPathTab,oTables)
aTables[oTables:nAt,3] := cPathTab
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtuSalDup()³Autor  ³ Gilson da Silva      ³ Data 09.04.2001     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Funcao que grava os campos no SA1 para avaliaçao de credito     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtuSalDup(cOper,nValor,nMoeda,cTipoDoc,nTaxa,dData)            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cOper = Operando para calcular os campos de avliaçao do credito ³±±
±±³          ³nValor = Valor do campo que será calculado                      ³±±
±±³          ³nMoeda = Moeda Forte do Cliente                                 ³±±
±±³          ³cTipoDoc = Tipo de Titulo(Docto)                                ³±±
±±³          ³nTaxa  = Taxa de conversão do Valor do campo para a Moeda Forte ³±±
±±³          ³dData  = Data de conversão do Valor do cmapo para a Moeda Forte ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³  Generico           										   	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtuSalDup(cOper,nValor,nMoeda,cTipoDoc,nTaxa,dData,nCm,lBloqSa1,lGrvSa1)

Local cCampo	:= ""									// Campo que terá o saldo atualizado
Local cCampo1	:= ""									// Campo que terá o saldo atualizado
Local cTpNaoAtu	:= Nil									// Tipos que nao atualizam saldo algum no cadastro de clientes
Local aAreaSA1	:= SA1->(GetArea()) 					// Salva a area do SA1
Local aAreaSF2	:= SF2->(GetArea())						// Salva a area do SF2
Local lAtuSalDup:= .T.
Local lAtuSaldo	:= .T.

nTaxa	:=  If(nTaxa	==	Nil,0,nTaxa)
nMoeda	:=	If(nMoeda	==	Nil,1,nMoeda)
dData	:=	If(dData	==	Nil,dDataBase,dData)

Default nCm := 0
Default lBloqSa1 :=  SuperGetMv("MV_ATUSA1",.F.,.T.) 
Default lGrvSa1  := .T.

If !cOper $ "+-="
	MsgAlert(STR0044)  //"Operando incorreto, Verifique se é uma atribuição, soma ou subtração"
	Return
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³No loja quando for cliente ADM Finan e o parametro ³
//³estiver habilitado nao atualiza o registro do cliente ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SuperGetMv("MV_LJATUSA",,.F.)
	aAreaSAE := SAE->(GetArea())
	SAE->(DbSetOrder(2))
	If SAE->(MsSeek(xFilial("SAE")+SA1->A1_COD))
		lAtuSalDup 	:= .F.
	EndIf
	SAE->(RestArea(aAreaSAE))
Endif

If lAtuSalDup
	If cTiposLC == Nil
		cTiposLC := ""
		cTiposLC := GetSESTipos({ || ES_SALDUP == "2"},"1")
		nMCusto		:=	Val(GetMV("MV_MCUSTO"))
	Endif

	// Tipos que nao atualizam nenhum dos saldos
	If cTpNaoAtu == Nil
		cTpNaoAtu := ""
		cTpNaoAtu := GetSESTipos({ || ES_SALDUP == "3"},"1")
	Endif

	If !(cTipoDoc $ cTpNaoAtu)
		If cTipoDoc $ cTiposLC
			cCampo	:=	"SA1->A1_SALFIN"
			cCampo1	:=	"SA1->A1_SALFINM"
		Else
			cCampo	:=	"SA1->A1_SALDUP"
			cCampo1	:=	"SA1->A1_SALDUPM"
		Endif
	Else
		Return

	Endif

	If SA1->(Eof())		// garantir que estah posicionado em algum registro valido
		SF2->(RestArea(aAreaSF2))
		SA1->(RestArea(aAreaSA1))
		Return
	Endif

	nMoedaF		:= If(SA1->A1_MOEDALC > 0,SA1->A1_MOEDALC,nMCusto)

	nValorMoe1	:= Round(xMoeda(nValor-nCm,nMoeda,1,dData,MsDecimais(1)+1,nTaxa),MsDecimais(1))
	nValorMoeF  := Round(xMoeda(nValor,nMoeda,nMoedaF,dData,MsDecimais(nMoedaF)+1,nTaxa),MsDecimais(nMoedaF))
	IF ExistBlock("MATATUSL")
		lAtuSaldo := ExecBlock("MATATUSL",.F.,.F.)
	Endif

	If lAtuSaldo .and. lBloqSa1 .and. lGrvSa1
			RecLock("SA1",.F.)
			If cOper=="+"
				&cCampo. 	+= nValorMoe1
				&cCampo1. 	+= nValorMoeF
			ElseIf cOper=="-"
				&cCampo. 	-= nValorMoe1
				&cCampo1. 	-= nValorMoeF
			Elseif cOper="="
				&cCampo. 	:= nValorMoe1
				&cCampo1.	:= nValorMoeF
			EndIf
			SA1->(MsUnlock())
	EndIf
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SAPVALID()ºAutor  ³Denis Martins       º Data ³  06/29/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida conversao de unidades de medidas.                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ X3_VALID do campo AP_PARA                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function SapValid()

Local aArea		:=GetArea()
Local lRet		:=.T.
Local nRec		:=SAP->(RecNo())

If M->AP_DE == M->AP_PARA
	MsgInfo(OemToAnsi(STR0045)) //"Unidades de medidas nao poderao ser iguais !"
	lRet:=.F.
EndIf

If lRet
	If !empty(M->AP_PARA)
		If SAP->(DbSeek(xFilial("SAP")+M->AP_DE+M->AP_PARA))
			If Inclui .Or. nRec <> SAP->(RecNo())
				SAP->(DbGoTo(nRec))
				Help(" ",1,"JAGRAVADO")
				lRet:=.F.
			EndIf
		EndIf
	EndIf
Endif

SAP->(DbGoTo(nRec))
RestArea(aArea)

Return lRet


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FGrvImpPcc³ Autor ³ Mauricio Pequim Junior³ Data ³ 18/10/04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a gravacao dos titulos de impostos PIS, COFINS      ³±±
±±³          ³ e CSLL                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ GrvImpPcc(cPrefixo,cNum,cTipoE2)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINANCEIRO/COMPRAS  												     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FGrvImpPcc(nPis,nCofins,nCsll,nRegSe2,lInclusao,lRtPICFCS,cSeq,cOrigem,nMoeda,cGeraDirf,nIrrf,nIss,;
					lRatImp,aRatSev,aRatSez,_lPccMR,_lIrfMR,_lInsMR,_lIssMR,_lCidMR,_lSesMR, aRatIRF)

Local lRet       := .T.
Local lPCCBaixa  := SuperGetMv("MV_BX10925",.T.,"2") == "1"
Local cFornUni	 := ""
Local cLojaUni	 := ""
Local cNomeUni 	 := ""
Local cPrefixo	 := ""
Local cNum		 := ""
Local cTipoE2	 := ""
Local cParcela	 := ""
Local dEmissao	 := CTOD("//")
Local dVencto	 := CTOD("//")
Local dVencRea	 := CTOD("//")
Local dEmis1	 := CTOD("//")
Local cCodRetPis := ""
Local cCodRetCof := ""
Local cCodRetCsl := ""
Local cLojaImpos := PadR( "00", Len( SE2->E2_LOJA ), "0" )
Local lIRPFBaixa := Iif(cPaisLoc == "BRA", SA2->A2_CALCIRF == "2", .F.)
Local cTpPessoa  := SA2->A2_TIPO
Local cCodRetIr  := SE2->E2_CODRET
Local lCalcIssBx :=	IIF(lIsIssBx, IsIssBx("P"), SuperGetMv("MV_MRETISS",.F.,"1") == "2" )
Local cMunIss    := GetMv("MV_MUNIC")
Local cNome		 := "MUNICIPIO"
Local nDia       := 0
Local nDiaUtil   := 0
Local aTamParc   := TamSx3("E2_PARCELA")
Local cForLojISS := ""
Local dVenISS    := CtoD("")
Local nDiaISS    := SuperGetMv("MV_DIAISS",.F.,10,)
Local cDiaISS    := StrZero(nDiaISS,2)
Local cTitPai    := SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
Local cCodAprov  := SE2->E2_CODAPRO
Local cFilOrig   := SE2->E2_FILORIG

Local lF050GER 	:= ExistBlock("F050GER")
Local lF050Irf 	:= ExistBlock("F050IRF")
Local aRecImpos := {}

Local nRefCof := 0
Local nRefCsl := 0
Local aCRets  := {}
Local lE2Iss  := .F.
Local lFina050 := IsInCallStack("FINA050")
Local lVcAntIss 	:= (SuperGetMV("MV_ANTVISS",.T.,"2") == "1")  //Antecipa ou nao o vencimento do ISS em caso de vencimento em dia nao util
Local lBdVcImp	:= SuperGetMV("MV_BDVCIMP",.T.,.F.)

Local nDiaUtIss	:=	SuperGetMv("MV_DIAUISS",.T.,0) //Nro de dias uteis que deve ser gerado o vencimento do titulo do ISS.
Local nI		:=	0
Local aAreaFor := {}
Local cFornSe2 := SE2->E2_FORNECE
Local cLojaSe2	:= SE2->E2_LOJA
Local nVretPis	:= SuperGetMV("MV_VRETPIS")
Local nVretCof	:= SuperGetMV("MV_VRETCOF")
Local nVretCsl	:= SuperGetMV("MV_VRETCSL")
Local cUniao	:= If(! EMPTY(GetMV("MV_UNIAO")),GetMV("MV_UNIAO"),'UNIAO')

Local lGerPis	:= .T. //controla a retenção do Pis, na contribuição individual
Local lGerCof	:= .T. //controla a retenção do Cofins, na contribuição individual
Local lGerCsl	:= .T. //controla a retenção do Csll, na contribuição individual
Local lContInd	:= AllTrim(cGeraDirf) == "1" .And. (nPis <= 0 .Or. nCofins <= 0 .Or. nCsll <= 0)
Local dDtRec	:= CtoD("")
Local nTamData	:= 0
Local nTamForn	:= TamSX3("A2_COD")[1]

DEFAULT cOrigem 	:= Space(8)
DEFAULT lRtPICFCS:= .T.
DEFAULT nMoeda := 1
DEFAULT lInclusao := .T.
DEFAULT cGeraDirf := "1"
DEFAULT nIrrf := 0
DEFAULT nIss := 0
DEFAULT aRatSev := {}
DEFAULT aRatSez := {}
DEFAULT _lPccMR := .F.
DEFAULT _lIrfMR := .F.
DEFAULT _lInsMR := .F.
DEFAULT _lIssMR := .F.
DEFAULT _lCidMR := .F.
DEFAULT _lSesMR := .F.
DEFAULT aRatIRF	:= {}

dBaixa := If(Type("dBaixa") != "D",dDataBase,dBaixa)

If Empty(aRatIRF)

	/*	[1]  = Codigo do Fornecedor
		[2]  = Loja do Fornecedor
		[3]  = CPF do Fornecedor
		[4]  = Percentual de Rateio
		[5]  = Base do Imposto
		[6]  = Imposto Calculado
		[7]  = Imposto Retido
		[8]  = Nome do Fornecedor*/

	aAdd( aRatIRF, { "", "", "", 100, 0, nIrrf, 0, ""} )

EndIf

If lPccBaixa .and. !lInclusao
	dEmissao	:= dBaixa
ElseIf !lPccBaixa .and. lInclusao
	dEmissao := SE2->E2_EMISSAO
Endif

If dEmissao >= dLastPcc
	nVlMinImp	:= 0
	nVretPis	:= 0
	nVretCof	:= 0
	nVretCsl	:= 0
EndIf

If IsInCallStack("fa530Processa")	 .And. !Empty(cCodRetIr)
	cGeraDirf := "1"
Endif

cForLojISS := PadR(cMunIss, nTamForn )+cLojaImpos
nRecnoFor := SA2->(RECNO())

//Transfere titulo de ISS para fornecedor de origem ³
If !Empty(SE2->E2_FORNISS) .And. !Empty(SE2->E2_LOJAISS)
	cForLojISS := SE2->E2_FORNISS+SE2->E2_LOJAISS

	aArea:=GetArea()
	DbSelectArea("SA2")
	DbSetOrder(1)
	If DbSeek(xFilial("SA2")+SE2->E2_FORNISS+SE2->E2_LOJAISS)
		cNome := SA2->A2_NREDUZ
	EndIF
	lE2Iss := .T.
EndIf

If !Empty(SE2->E2_VENCISS)
	dVenISS := SE2->E2_VENCISS
EndIf

SE2->(dbGoto(nRegSe2))

If !_lPccMR .And. lPccBaixa
	// Caso nao exista os tres impostos, o codigo de retencao sera diferenciado
	// para cada imposto
	If	(nPis <= 0 .Or. nCofins <= 0 .Or. nCsll <= 0 )
		//cCodRetPis := If (!Empty(SE2->E2_CODRPIS),SE2->E2_CODRPIS,"5979")
		cCodRetPis := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRPIS), SE2->E2_CODRPIS, "5979"), "5979")
		cCodRetCof := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCOF), SE2->E2_CODRCOF, "5960"), "5960")
		cCodRetCsl := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCSL), SE2->E2_CODRCSL, "5987"), "5987")
	Else
		// Se os 3 impostos juntos for maior que a media de retencao, o codigo sera o mesmo
		// para os tres.
		If nPis+nCofins+nCsll > ((nVretPis+nVretCof+nVretCsl) / 3)
				If lIRPFBaixa .and. cTpPessoa == "F" .And. !Empty(SE2->E2_CODRET)
					cCodRetPis := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRPIS), SE2->E2_CODRPIS, SE2->E2_CODRET), SE2->E2_CODRET)
					cCodRetCof := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCOF), SE2->E2_CODRCOF, SE2->E2_CODRET), SE2->E2_CODRET)
					cCodRetCsl := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCSL), SE2->E2_CODRCSL, SE2->E2_CODRET), SE2->E2_CODRET)
				Else
					cCodRetPis := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRPIS), SE2->E2_CODRPIS, "5952"), "5952")
					cCodRetCof := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCOF), SE2->E2_CODRCOF, "5952"), "5952")
					cCodRetCsl := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCSL), SE2->E2_CODRCSL, "5952"), "5952")
				Endif
		Endif
		If ExistBlock("FINCDRET")
			aCRets :=ExecBlock("FINCDRET")
			If aScan(aCRets,cCodRetIr) > 0
				cCodRetPis := cCodRetCof := cCodRetCsl := cCodRetIr
			EndIf
		End
	Endif
Else
	// Caso nao exista os tres impostos, o codigo de retencao sera diferenciado
	// para cada imposto
	If !_lPccMR
		If	(SE2->E2_PIS <= 0 .Or. SE2->E2_COFINS <= 0 .Or. SE2->E2_CSLL <= 0 )
			cCodRetPis := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRPIS), SE2->E2_CODRPIS, "5979"), "5979")
			cCodRetCof := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCOF), SE2->E2_CODRCOF, "5960"), "5960")
			cCodRetCsl := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCSL), SE2->E2_CODRCSL, "5987"), "5987")
		Else
			// Se os 3 impostos juntos for maior que a media de retencao, o codigo sera o mesmo
			// para os tres.
			If SE2->(E2_PIS+E2_COFINS+E2_CSLL) > ((nVretPis+nVretCof+nVretCsl) / 3)
				cCodRetPis := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRPIS), SE2->E2_CODRPIS, "5952"), "5952")
				cCodRetCof := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCOF), SE2->E2_CODRCOF, "5952"), "5952")
				cCodRetCsl := IIF(cPaisLoc $ "BRA|MEX", IIF(!Empty(SE2->E2_CODRCSL), SE2->E2_CODRCSL, "5952"), "5952")
			Endif
		Endif
	EndIf

	If ExistBlock("FINCDRET")
		aCRets :=ExecBlock("FINCDRET")
		If aScan(aCRets,cCodRetIr) > 0
			cCodRetPis := cCodRetCof := cCodRetCsl := cCodRetIr
		EndIf
	Endif
Endif
//Retenção Individual, caso o fornecedor tenha isenção, supensação ou beneficio amparado pela lei para
//não reter um dos impostos. Nesse caso codigo de retencao sera diferenciado para cada imposto
If !_lPccMR .And. lContInd
	lGerPis := If( nPis > 0,	.T., .F. )
	lGerCof := If( nCofins > 0,	.T., .F. )
	lGerCsl := If( nCsll > 0,	.T., .F. )
EndIf

If !( SE2->E2_TIPO $ MVABATIM )
	If cPaisLoc=="BRA".And.;
		(!SE2->E2_NATUREZ $ SuperGetMV("MV_CSLL")	 .And. ;
		!SE2->E2_NATUREZ $ SuperGetMV("MV_PISNAT")  	 .And. ;
		!SE2->E2_NATUREZ $ SuperGetMV("MV_COFINS"))

		//Cria o Fornecedor, caso nao exista
		DbSelectArea("SA2")
		DbSetOrder(1)	//A2_FILIAL, A2_COD, A2_LOJA
		If !MsSeek(xFilial("SA2")+ PadR(cUniao, nTamForn) + cLojaImpos)
			Reclock("SA2",.T.)
			SA2->A2_FILIAL	:= xFilial("SA2")
			SA2->A2_COD 	:= cUniao
			SA2->A2_LOJA	:= cLojaImpos
			SA2->A2_NOME	:= "UNIAO"
			SA2->A2_NREDUZ	:= "UNIAO"
			SA2->A2_BAIRRO	:= "."
			SA2->A2_MUN 	:= "."
			SA2->A2_EST 	:= SuperGetMv("MV_ESTADO")
			SA2->A2_END		:= "."
			SA2->A2_TIPO	:= "J"
			MsUnlock()
			FKCOMMIT()
		EndIf

		cFornUni := SA2->A2_COD
		cLojaUni := SA2->A2_LOJA
		cNomeUni := SA2->A2_NREDUZ
		cPrefixo  := SE2->E2_PREFIXO
		cNum	  := SE2->E2_NUM
		cTipoE2   := SE2->E2_TIPO

		DbSelectArea( "SE2" )
		DbGoTo( nRegSe2 )

		If !_lPccMR .And. lInclusao
			// Se existirem os 3 impostos e o valor dos 3 for menor que a media dos valores
			// minimos de retencao, zera o imposto gerado.
			If SE2->E2_PIS > 0 .And. SE2->E2_COFINS > 0 .And. SE2->E2_CSLL > 0 .And.;
				(SE2->(E2_PIS+E2_COFINS+E2_CSLL) <= ((nVretPis+nVretCof+nVretCsl)/3) .OR. !lRtPICFCS)
				DbSelectArea("SE2")
				RecLock("SE2",.F.)
				SE2->E2_VALOR	+= SE2->E2_PIS
				SE2->E2_SALDO	+= SE2->E2_PIS
				SE2->E2_PIS		:= 0
				nPis				:= 0

				SE2->E2_VALOR	+= SE2->E2_COFINS
				SE2->E2_SALDO	+= SE2->E2_COFINS
				SE2->E2_COFINS	:= 0
				nCofins			:= 0

				SE2->E2_VALOR	+= SE2->E2_CSLL
				SE2->E2_SALDO	+= SE2->E2_CSLL
				SE2->E2_VLCRUZ	:= Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,SE2->E2_EMISSAO,MsDecimais(1)+1,SE2->E2_TXMOEDA),MsDecimais(1)+1),MsDecimais(1))
				SE2->E2_CSLL	:= 0
				nCSLL				:= 0
			ElseIf (SE2->E2_PIS <= 0 .Or. SE2->E2_COFINS <= 0 .Or. SE2->E2_CSLL <= 0)
				// Caso nao exista um dos impostos, o codigo de retencao e separado para
				//	cada um deles, entao zera o imposto caso ele seja menor que o valor minimo.
				If (IIF(lContInd, SE2->E2_PIS < nVretPis, SE2->E2_PIS <= nVretPis))
					DbSelectArea("SE2")
					RecLock("SE2",.F.)
					SE2->E2_VALOR	+= SE2->E2_PIS
					SE2->E2_SALDO	+= SE2->E2_PIS
					SE2->E2_VLCRUZ := Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,SE2->E2_EMISSAO,MsDecimais(1)+1,SE2->E2_TXMOEDA),MsDecimais(1)+1),MsDecimais(1))
					SE2->E2_PIS	:= 0
					nPis			:= 0
				EndIf
				If (IIF(lContInd, SE2->E2_COFINS < nVretCof, SE2->E2_COFINS <= nVretCof))
					DbSelectArea("SE2")
					RecLock("SE2",.F.)
					SE2->E2_VALOR	+= SE2->E2_COFINS
					SE2->E2_SALDO	+= SE2->E2_COFINS
					SE2->E2_VLCRUZ := Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,SE2->E2_EMISSAO,MsDecimais(1)+1,SE2->E2_TXMOEDA),MsDecimais(1)+1),MsDecimais(1))
					SE2->E2_COFINS	:= 0
					nCofins			:= 0
				EndIf
				If (IIF(lContInd, SE2->E2_CSLL < nVretCsl, SE2->E2_CSLL <= nVretCsl))
					DbSelectArea("SE2")
					RecLock("SE2",.F.)
					SE2->E2_VALOR	+= SE2->E2_CSLL
					SE2->E2_SALDO	+= SE2->E2_CSLL
					SE2->E2_VLCRUZ := Round(NoRound(xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,SE2->E2_EMISSAO,MsDecimais(1)+1,SE2->E2_TXMOEDA),MsDecimais(1)+1),MsDecimais(1))
					SE2->E2_CSLL	:= 0
					nCsll				:= 0
				EndIf
			EndIf
		Endif

		cParcSe2 := SE2->E2_PARCELA
		dEmissao := SE2->E2_EMISSAO
		dEmis1	:= SE2->E2_EMIS1
		dVencto	:= SE2->E2_VENCTO
		dVctoReal:= SE2->E2_VENCREA
		cLa		:= SE2->E2_LA

		//Se for tratado o imposto pela baixa
		If ( lPccBaixa .AND. !lInclusao .AND. !lBdVcImp ) .OR. ( lPccBaixa .AND. !lInclusao .AND. lBdVcImp .AND. Empty(SE2->E2_NUMBOR) )
			dEmissao	:= dBaixa
			dEmis1		:= dDataBase
			dVctoReal	:= dBaixa
		EndIf

		//Verifica se aglutina os impostos em apenas um titulo
		If lRtPICFCS .and. SuperGetMv("MV_AG10925",.F.,"2") == "1" .and. cCodRetPis == "5952" .And. (nCsll > 0 .And. nCofins > 0 .And. nPis > 0)
			nRefCof := nCofins //Armazena o valor do Cofins, para recompor os valores apos a geracao dos titulos
			nRefCsl := nCsll   //Armazena o valor do Csll, para recompor os valores apos a geracao dos titulos

			nPis += nCofins + nCsll
			nCsll := 0
			nCofins := 0
		Endif

		//Gera titulo de IRRF
		If nIrrf > 0 .And. lIRPFBaixa .And. !lFina050 .And. cOrigem<>"MATA100"
			//Cria o Fornecedor, caso nao exista
			DbSelectArea("SA2")
			DbSetOrder(1)	//A2_FILIAL, A2_COD, A2_LOJA
			
			If !MsSeek(xFilial("SA2")+ PadR(cUniao, nTamForn) + cLojaImpos)
				Reclock("SA2",.T.)
				SA2->A2_FILIAL	:= xFilial("SA2")
				SA2->A2_COD 	:= cUniao
				SA2->A2_LOJA	:= cLojaImpos
				SA2->A2_NOME	:= "UNIAO"
				SA2->A2_NREDUZ	:= "UNIAO"
				SA2->A2_BAIRRO	:= "."
				SA2->A2_MUN 	:= "."
				SA2->A2_EST 	:= SuperGetMv("MV_ESTADO")
				SA2->A2_END 	:= "."
				SA2->A2_TIPO	:= "J"
				MsUnlock()
				FKCOMMIT()
			EndIf

			cNatureza		:= &(GetMv("MV_IRF"))
			cNatureza		:= cNatureza+Space(10-Len(cNatureza))

			//Cria a natureza IRF caso nao exista
			DbSelectArea("SED")
			If ( !DbSeek(cFilial+cNatureza) )
				RecLock("SED",.T.)
				SED->ED_FILIAL  := cFilial
				SED->ED_CODIGO  := cNatureza
				SED->ED_CALCIRF := "N"
				SED->ED_CALCISS := "N"
				SED->ED_CALCINS := "N"
				SED->ED_CALCCSL := "N"
				SED->ED_CALCCOF := "N"
				SED->ED_CALCPIS := "N"
				SED->ED_DESCRIC := "IMPOSTO RENDA RETIDO NA FONTE"
				SED->ED_TIPO	:= "2"
				MsUnlock()

				Iif(FindFunction("JurCompSED"), JurCompSED(SED->(Recno())), Nil) //Integração SIGAPFS - Complemento da Natureza

				FKCOMMIT()
			EndIf
			
			SA2->(DBGOTO(nRecnoFor))
			//Se for tratado o imposto pela baixa
			If !lInclusao
				dEmissao	:= dBaixa
				dEmis1		:= dDataBase
			Endif

			// Rateio p/ CPF
			For nI := 1 To Len(aRatIRF)
				If !Empty(aRatIRF[nI][6])
					If !_lIrfMR
						dVencRea := F050VIMP("IRRF",dEmissao,dEmis1,dVctoReal,cCodRetIr,cTpPessoa,lIRPFBaixa) // Calcula o vencto do imposto
						cParcela := ParcImposto(cPrefixo,cNum,cTipoE2,,,.F.)

						DbSelectArea("SA2")
						MsSeek(xFilial("SA2")+ PadR(cUniao, nTamForn) + cLojaImpos)
						RecLock("SE2",.T.)
						SE2->E2_FILIAL  := cFilial
						SE2->E2_PREFIXO := cPrefixo
						SE2->E2_NUM 	 := cNum
						SE2->E2_PARCELA := cParcela
						SE2->E2_TIPO    := Iif(cTipoE2 $ MVPAGANT+"/"+MV_CPNEG  .And. !lPCCBaixa,MVTXA,MVTAXA)
						SE2->E2_EMISSAO := dEmissao
						SE2->E2_VALOR	 := aRatIRF[nI][6]
						SE2->E2_VENCREA := dVencrea
						SE2->E2_SALDO	 := aRatIRF[nI][6]
						SE2->E2_VENCTO  := dVencRea
						SE2->E2_VENCORI := dVencRea
						SE2->E2_MOEDA	 := If(cPaisLoc=="BRA",1,nMoeda)
						SE2->E2_EMIS1	 := dDataBase
						SE2->E2_FORNECE := cUniao
						SE2->E2_VLCRUZ  := Round( SE2->E2_VALOR, MsDecimais(1) )
						SE2->E2_LOJA	 := SA2->A2_LOJA
						SE2->E2_NOMFOR  := SA2->A2_NREDUZ
						SE2->E2_ORIGEM  := UPPER(cOrigem)
						SE2->E2_NATUREZ := cNatureza
						SE2->E2_LA      := cLA			// Herda do principal
						SE2->E2_SEQBX   := cSeq
						
						If cPaisLoc == "BRA"
							SE2->E2_DIRF    := cGeraDirf
							SE2->E2_CODRET  := cCodRetIr
							SE2->E2_CNPJRET := aRatIRF[nI][3]
							SE2->E2_NOMERET := aRatIRF[nI][8]
						Endif
						
						If lRatImp
							SE2->E2_MULTNAT  := "1"
						Endif

						SE2->E2_FILORIG := cFilorig //filial origem do titulo pai
						//Grava os dados do titulo de origem do imposto se existir o campo no SE2
						SE2->E2_TITPAI := cTitPai
						SE2->E2_CODAPRO := cCodAprov
						SE2->E2_FORMPAG := SA2->A2_FORMPAG
						MsUnlock()
						FKCOMMIT()

						FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)

						if lPLSTITPF
							PLSTITPF('SE2',cTitPai)
						endIf

						AADD(aRecImpos,{"SE2",Recno()})
					EndIf

					If lF050Irf
						Execblock("F050IRF",.F.,.F.,nRegSE2)
					EndIf

					//Grava lançamento do imposto IRRF no SIGAPCO se a rotina foi chamada do contas a pagar ³
					If "FINA050" $ Upper(cOrigem)
						PCODetLan("000002","06","FINA050")
					ElseIf Upper(cOrigem) == "FINA240"
						PCODetLan("000023","06",Upper(cOrigem))
					EndIf

					// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
					If lRatImp
						// Grava SEV e SEZ dos impostos, baseado nos percentuais do rateio do titulo principal
						GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR)
					Else
						AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, If(SE2->E2_TIPO $ MVABATIM,"-","+"))
					Endif

				EndIf
				DbGoTo( nRegSe2 )
			Next nI

			//Grava parcela do IRF na parcela do titulo
			If !_lIrfMR
				Reclock( "SE2" , .F. )
					SE2->E2_PARCIR := cParcela
					If cPaisLoc=="BRA"
						SE2->E2_DIRF    := "2"	 // Desmarca titulo principal, pois apenas o titulo de imposto var para DIRF
					Endif
				MsUnlock()
			EndIf
		EndIf

		//Gera titulo de PIS
		If nPis > 0 .And. lRtPICFCS .And. lGerPis
			//Cria a natureza PIS caso nao exista
			DbSelectArea("SED")
			cNatureza := Pad(SuperGetMV("MV_PISNAT"),10)

			If ( ! DbSeek( cFilial + cNatureza ) )
				RecLock("SED",.T.)
				SED->ED_FILIAL  := cFilial
				SED->ED_CODIGO  := cNatureza
				SED->ED_CALCIRF := "N"
				SED->ED_CALCISS := "N"
				SED->ED_CALCINS := "N"
				SED->ED_CALCCSL := "N"
				SED->ED_CALCCOF := "N"
				SED->ED_CALCPIS := "N"
				SED->ED_DESCRIC := "PIS"
				SED->ED_TIPO	:= "2"
				MsUnlock()

				Iif(FindFunction("JurCompSED"), JurCompSED(SED->(Recno())), Nil) //Integração SIGAPFS - Complemento da Natureza

				FKCOMMIT()
			EndIf

			If !_lPccMR
				dVencRea := F050VIMP("PIS",dEmissao,dEmis1,dVctoReal,cCodRetPis) // Calcula o vencto do imposto
				cParcela := ParcImposto(cPrefixo,cNum,cTipoE2,,,.F.)
				RecLock("SE2",.T.)
				SE2->E2_FILIAL  := cFilial
				SE2->E2_PREFIXO := cPrefixo
				SE2->E2_NUM     := cNum
				SE2->E2_PARCELA := cParcela
				SE2->E2_TIPO    := Iif(cTipoE2 $ MVPAGANT+"/"+MV_CPNEG  .And. !lPCCBaixa,MVTXA,MVTAXA)
				SE2->E2_EMISSAO := dEmissao
				SE2->E2_EMIS1   := dDataBase
				SE2->E2_VALOR   := nPis
				SE2->E2_VENCTO  := dVencRea
				SE2->E2_SALDO   := nPis
				SE2->E2_VENCREA := dVencRea
				SE2->E2_VENCORI := dVencRea
				SE2->E2_FORNECE := cFornUni
				SE2->E2_LOJA    := cLojaUni
				SE2->E2_NOMFOR  := SA2->A2_NREDUZ
				SE2->E2_MOEDA   := If(cPaisLoc == "BRA", 1, nMoeda)
				SE2->E2_VLCRUZ  := Round(SE2->E2_VALOR, MsDecimais(1))
				SE2->E2_ORIGEM  := Upper(cOrigem)
				SE2->E2_NATUREZ := cNatureza
				SE2->E2_LA      := cLa

				If cPaisLoc == "BRA"
					SE2->E2_DIRF    := cGeraDirf
					SE2->E2_CODRET  := cCodRetPis
				Endif

				If lPccBaixa
					SE2->E2_SEQBX  := cSeq
				Endif

				If lRatImp
					SE2->E2_MULTNAT  := "1"
				Endif

				// Grava a filial de origem quando existir o campo no SE2
				SE2->E2_FILORIG := cFilorig //filial origem do titulo pai

				//Grava os dados do titulo de origem do imposto se existir o campo no SE2
				SE2->E2_TITPAI := cTitPai
				SE2->E2_CODAPRO := cCodAprov
				SE2->E2_FORMPAG := SA2->A2_FORMPAG
				MsUnlock()
				FKCOMMIT()

				FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)

				if lPLSTITPF
					PLSTITPF('SE2',cTitPai)
				endIf

				AADD(aRecImpos,{"SE2",Recno()})
			EndIf

			If ExistBlock("F050PIS")
				Execblock("F050PIS",.F.,.F.,nRegSE2)
			EndIf

			//Grava lançamento do imposto PIS no SIGAPCO se a rotina foi chamada do contas a pagar
			If Upper(ALLTRIM(cOrigem)) $ "FINA050#FINA080"
				PCODetLan("000002","10","FINA050")
			ElseIf Upper(cOrigem) == "FINA240"
    			PCODetLan("000023","03",Upper(cOrigem))
			EndIf

			// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
			If lRatImp
				// Grava SEV e SEZ dos impostos, baseado nos percentuais do rateio do titulo principal
				GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR)
			Else
				AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, "+")
			Endif

			//Grava parcela do PIS na parcela do titulo
			DbSelectArea( "SE2" )
			DbGoTo( nRegSe2 )
			If !_lPccMR
				Reclock( "SE2" , .F. )
				SE2->E2_PARCPIS := cParcela
				If cPaisLoc=="BRA"
					SE2->E2_DIRF    := "2"	 // Desmarca titulo principal, pois apenas o titulo de imposto var para DIRF
				Endif
				MsUnlock()
			EndIf
		EndIf

		//Gera titulo de COFINS
		If nCofins > 0 .And. lRtPICFCS .And. lGerCof
			//Cria a natureza COFINS caso nao exista
			DbSelectArea("SED")
			cNatureza := Pad(SuperGetMV("MV_COFINS"),10)
			If ( ! DbSeek( cFilial + cNatureza ) )
				RecLock("SED",.T.)
				SED->ED_FILIAL  := xFilial()
				SED->ED_CODIGO  := cNatureza
				SED->ED_CALCIRF := "N"
				SED->ED_CALCISS := "N"
				SED->ED_CALCINS := "N"
				SED->ED_CALCCSL := "N"
				SED->ED_CALCCOF := "N"
				SED->ED_CALCPIS := "N"
				SED->ED_DESCRIC := "COFINS"
				SED->ED_TIPO	:= "2"
				MsUnlock()

				Iif(FindFunction("JurCompSED"), JurCompSED(SED->(Recno())), Nil) //Integração SIGAPFS - Complemento da Natureza

				FKCOMMIT()
			EndIf

			If !_lPccMR
				dVencRea := F050VIMP("COFINS",dEmissao,dEmis1,dVctoReal,cCodRetCof) // Calcula o vencto do imposto
				cParcela := ParcImposto(cPrefixo,cNum,cTipoE2,,,.F.)
				RecLock("SE2",.T.)
				SE2->E2_FILIAL  := cFilial
				SE2->E2_PREFIXO := cPrefixo
				SE2->E2_NUM     := cNum
				SE2->E2_PARCELA := cParcela
				SE2->E2_TIPO    := Iif(cTipoE2 $ MVPAGANT+"/"+MV_CPNEG  .And. !lPCCBaixa,MVTXA,MVTAXA)
				SE2->E2_EMISSAO := dEmissao
				SE2->E2_EMIS1   := dDataBase
				SE2->E2_VALOR   := nCofins
				SE2->E2_VENCTO  := dVencRea
				SE2->E2_SALDO   := nCofins
				SE2->E2_VENCREA := dVencRea
				SE2->E2_VENCORI := dVencRea
				SE2->E2_FORNECE := cFornUni
				SE2->E2_LOJA    := cLojaUni
				SE2->E2_NOMFOR  := SA2->A2_NREDUZ
				SE2->E2_MOEDA   := If(cPaisLoc == "BRA", 1, nMoeda)
				SE2->E2_VLCRUZ  := Round(SE2->E2_VALOR, MsDecimais(1))
				SE2->E2_ORIGEM  := Upper(cOrigem)
				SE2->E2_NATUREZ := cNatureza
				SE2->E2_LA      := cLa

				If cPaisLoc == "BRA"
					SE2->E2_DIRF    := cGeraDirf
					SE2->E2_CODRET  := cCodRetCof
				Endif

				If lPccBaixa
					SE2->E2_SEQBX  := cSeq
				Endif

				If lRatImp
					SE2->E2_MULTNAT  := "1"
				Endif

				SE2->E2_FILORIG := cFilorig //filial origem do titulo pai
				SE2->E2_TITPAI := cTitPai
				SE2->E2_CODAPRO := cCodAprov
				SE2->E2_FORMPAG := SA2->A2_FORMPAG
				MsUnlock()
				FKCOMMIT()
			
				FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)

				if lPLSTITPF
					PLSTITPF('SE2',cTitPai)
				endIf

				AADD(aRecImpos,{"SE2",Recno()})
			EndIf

			If ExistBlock("F050COF")
				Execblock("F050COF",.F.,.F.,nRegSE2)
			EndIf

			//Grava lançamento do imposto COFINS no SIGAPCO se a rotina foi chamada do contas a pagar ³
			If Upper(ALLTRIM(cOrigem)) $ "FINA050#FINA080"
				PCODetLan("000002","11","FINA050")
			ElseIf Upper(cOrigem) == "FINA240"
    			PCODetLan("000023","04",Upper(cOrigem))
			EndIf

			// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
			If lRatImp
				// Grava SEV e SEZ dos impostos, baseado nos percentuais do rateio do titulo principal
				GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR)
			Else
				AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, "+")
			Endif

			//Grava parcela do COFINS na parcela do titulo
			DbSelectArea( "SE2" )
			DbGoTo( nRegSe2 )
			If !_lPccMR
				Reclock( "SE2" , .F. )
				SE2->E2_PARCCOF := cParcela

				If cPaisLoc=="BRA"
					SE2->E2_DIRF    := "2"	 // Desmarca titulo principal, pois apenas o titulo de imposto var para DIRF
				Endif
				MsUnlock()
			EndIf
		EndIf

		//Gera titulo de CSLL
		If nCsll > 0 .And. lRtPICFCS .And. lGerCsl
			//Cria a natureza CSLL caso nao exista
			DbSelectArea("SED")
			cNatureza  := Pad(SuperGetMV("MV_CSLL"),10)
			If ( ! DbSeek( cFilial + cNatureza ) )
				RecLock("SED",.T.)
				SED->ED_FILIAL  := cFilial
				SED->ED_CODIGO  := cNatureza
				SED->ED_CALCIRF := "N"
				SED->ED_CALCISS := "N"
				SED->ED_CALCINS := "N"
				SED->ED_CALCCSL := "N"
				SED->ED_CALCCOF := "N"
				SED->ED_CALCPIS := "N"
				SED->ED_DESCRIC := "CONTRIB.S/LUCRO LIQUIDO"
				SED->ED_TIPO	:= "2"
				MsUnlock()

				Iif(FindFunction("JurCompSED"), JurCompSED(SED->(Recno())), Nil) //Integração SIGAPFS - Complemento da Natureza

				FKCOMMIT()
			EndIf

			If !_lPccMR
				dVencRea := F050VIMP("CSLL",dEmissao,dEmis1,dVctoReal,cCodRetCsl) // Calcula o vencto do imposto
				cParcela := ParcImposto(cPrefixo,cNum,cTipoE2,,,.F.)
				RecLock("SE2",.T.)
				SE2->E2_FILIAL  := cFilial
				SE2->E2_PREFIXO := cPrefixo
				SE2->E2_NUM     := cNum
				SE2->E2_PARCELA := cParcela
				SE2->E2_TIPO    := Iif(cTipoE2 $ MVPAGANT+"/"+MV_CPNEG  .And. !lPCCBaixa,MVTXA,MVTAXA)
				SE2->E2_EMISSAO := dEmissao
				SE2->E2_EMIS1   := dDataBase
				SE2->E2_VALOR   := nCsll
				SE2->E2_VENCTO  := dVencRea
				SE2->E2_SALDO   := nCsll
				SE2->E2_VENCREA := dVencRea
				SE2->E2_VENCORI := dVencRea
				SE2->E2_FORNECE := cFornUni
				SE2->E2_LOJA    := cLojaUni
				SE2->E2_NOMFOR  := SA2->A2_NREDUZ
				SE2->E2_MOEDA   := If(cPaisLoc == "BRA", 1, nMoeda)
				SE2->E2_VLCRUZ  := Round(SE2->E2_VALOR, MsDecimais(1))
				SE2->E2_ORIGEM  := Upper(cOrigem)
				SE2->E2_NATUREZ := cNatureza
				SE2->E2_LA      := cLa

				If cPaisLoc == "BRA"
					SE2->E2_DIRF    := cGeraDirf
					SE2->E2_CODRET  := cCodRetCsl
				Endif

				If lPccBaixa
					SE2->E2_SEQBX  := cSeq
				Endif

				If lRatImp
					SE2->E2_MULTNAT  := "1"
				Endif

				SE2->E2_FILORIG := cFilorig //filial origem do titulo pai
				SE2->E2_TITPAI := cTitPai
				SE2->E2_CODAPRO := cCodAprov
				SE2->E2_FORMPAG := SA2->A2_FORMPAG
				MsUnlock()
				FKCOMMIT()

				FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)
				
				if lPLSTITPF
					PLSTITPF('SE2',cTitPai)
				endIf

				AADD(aRecImpos,{"SE2",Recno()})
			EndIf

			If ExistBlock("F050CSL")
				Execblock("F050CSL",.F.,.F.,nRegSE2)
			EndIf

			//Grava lançamento do imposto CSLL no SIGAPCO se a rotina foi chamada do contas a pagar
			If Upper(ALLTRIM(cOrigem)) $ "FINA050#FINA080"
				PCODetLan("000002","12","FINA050")
			ElseIf Upper(cOrigem) == "FINA240"
    			PCODetLan("000023","05",Upper(cOrigem))
			EndIf

			// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
			If lRatImp
				// Grava SEV e SEZ dos impostos, baseado nos percentuais do rateio do titulo principal
				GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR)
			Else
				AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, "+")
			Endif

			//Grava parcela do CSLL na parcela do titulo
			DbSelectArea( "SE2" )
			DbGoTo( nRegSe2 )
			If !_lPccMR
				Reclock( "SE2" , .F. )
				SE2->E2_PARCSLL := cParcela

				If cPaisLoc=="BRA"
					SE2->E2_DIRF    := "2"	 // Desmarca titulo principal, pois apenas o titulo de imposto var para DIRF
				Endif
				MsUnlock()
			EndIf
		EndIf  // cPaisLoc

		//Refaz os valores de PIS/COFINS/CSLL, quando aglutinados em um unico TX.
		If !_lPccMR .And. SuperGetMv("MV_AG10925",.F.,"2") == "1" .and. cCodRetPis == "5952"
			nPis -= nRefCof + nRefCsl
			nCsll := nRefCsl
			nCofins := nRefCof
		Endif
	EndIf
EndIf

//Gera titulo de ISS - Baixa
If ( nIss > 0 ) .and. lCalcIssBx
	DbSelectArea( "SE2" )
	DbGoTo( nRegSe2 )

	DbSelectArea("SA2")
	DbSetOrder(1)
	DbSeek(xFilial(SE2->E2_FORNECE+SE2->E2_LOJA))

	If SA2->( FieldPos( "A2_MUNIC" ) ) > 0 .And. !Empty( SA2->A2_MUNIC )
		cMunISS := SA2->A2_MUNIC
	Else
		cMunIss := SuperGetMV("MV_MUNIC")
		If !Empty( SE2->E2_CODISS )
			DbSelectArea( "FIM" )
			FIM->( DbSetOrder( 1 ) )
			If FIM->( DbSeek( xFilial( "FIM" ) + SE2->E2_CODISS ) )
				cMunIss 	:= FIM->FIM_CODFOR
				cLojaImpos	:= FIM->FIM_FORLOJ
				cNome	+= "-" + AllTrim( FIM->FIM_MUN )
			EndIf

			If AliasInDic("CC2")
				CC2->(DbSetOrder( 1 ))
				If CC2->(DbSeek(xFilial("CC2") + FIM->FIM_EST + FIM->FIM_CODMUN))
					dDtRec := CC2->CC2_DTRECO
				EndIf
			EndIf
		EndIf

		If !lE2Iss
			cForLojISS := PadR(cMunIss, nTamForn) + cLojaImpos
		Endif

	EndIf

	DbSelectArea( "SE2" )

	cParcSe2 := SE2->E2_PARCELA
	dEmissao := dBaixa
	dEmis1	 := dBaixa
	dVencto	 := dBaixa
	dVctoReal := dBaixa
	cLa		 := SE2->E2_LA

	DbSelectArea("SA2")
	DbSetOrder(1)	//A2_FILIAL, A2_COD, A2_LOJA
	If !MsSeek(xFilial("SA2")+ PadR(cMunIss, nTamForn) + cLojaImpos)
		Reclock("SA2",.T.)
		SA2->A2_FILIAL	:= xFilial("SA2")
		SA2->A2_COD 	:= cMunIss
		SA2->A2_LOJA	:= cLojaImpos
		SA2->A2_NOME	:= "MUNICIPIO"
		SA2->A2_NREDUZ	:= "MUNICIPIO"
		SA2->A2_BAIRRO	:= "."
		SA2->A2_MUN 	:= "."
		SA2->A2_EST 	:= SuperGetMv("MV_ESTADO")
		SA2->A2_END 	:= "."
		MsUnlock()
		FKCOMMIT()
	EndIf

	//Cria a natureza ISS caso nao exista
	cNatureza	:= &(SuperGetMV("MV_ISS"))
	cNatureza	:= cNatureza+Space(10-Len(cNatureza))
	DbSelectArea("SED")
	If ( ! DbSeek( xFilial("SED") + cNatureza ) )
		RecLock("SED",.T.)
		SED->ED_FILIAL  := xFilial("SED")
		SED->ED_CODIGO  := cNatureza
		SED->ED_CALCIRF := "N"
		SED->ED_CALCISS := "N"
		SED->ED_CALCINS := "N"
		SED->ED_CALCCSL := "N"
		SED->ED_CALCCOF := "N"
		SED->ED_CALCPIS := "N"
		SED->ED_DESCRIC := "IMPOSTO SOBRE SERVICOS"
		SED->ED_TIPO	:= "2"
		MsUnlock()

		Iif(FindFunction("JurCompSED"), JurCompSED(SED->(Recno())), Nil) //Integração SIGAPFS - Complemento da Natureza

		FKCOMMIT()
	EndIf

	//Calcula parcela do imposto
	cParcela := STRZERO(1,aTamParc[1])
	cParcela := ParcImposto(cPrefixo,cNum,"ISS",lIRPFBaixa,cForLojISS,.F.)

	If Empty(dVenISS)
		Do Case
			Case nDiaUtIss > 0 //Vencimento do ISS deve ser gerado por dias uteis.

				If lCalcIssBx // Mes subsequente de acordo com o momento de retenção do imposto.
					dVenISS	:= LastDay(dVencto)
				Else
					dVenISS	:=	LastDay(dEmissao)
				Endif

				For nI:= 1 To nDiaUtIss
					dVenISS ++
					dVenISS := DataValida(dVenISS)
				Next

			Case GetNewPar("MV_VENCISS","E")=="E"
				dVenISS := dEmissao
				dVenISS += 28
				If ( Month(dVenISS) == Month(dEmissao) )
					dVenISS := dVenISS+28
				EndIf
				nTamData := Iif(Len(Dtoc(dVenISS)) == 10, 7, 5)
				dVenISS	:= Ctod(cDiaISS+"/"+Subs(Dtoc(dVenISS),4,nTamData)) 
				dVenISS := DataValida(dVenISS,!lVcAntIss)
			Case GetNewPar("MV_VENCISS","E")=="Q" //Ultimo dia util da quinzena subsequente a dEmissao
				If Day(dEmissao) <= 15
					dVenISS	:= LastDay(dEmissao)
					dVenISS := DataValida(dVenISS,!lVcAntIss)
				Else
					dVenISS := DataValida((LastDay(dEmissao)+1)+14,!lVcAntIss)
				EndIf
			Case GetNewPar("MV_VENCISS","E")=="U" //Ultimo dia util do mes subsequente da dEmissao
				dVenISS := DataValida(LastDay(LastDay(dEmissao)+1),!lVcAntIss)
			Case GetNewPar("MV_VENCISS","E")=="D"
				dVenISS := (LastDay(dEmissao)+1)
				nDiaUtil:= nDiaISS
				For nDia := 1 To nDiaUtil-1
					If !(dVenISS == DataValida(dVenISS))
						dVenISS := DataValida(dVenISS,!lVcAntIss)
						Exit
					EndIf
					dVenISS+=1
				Next nDia
			Case GetNewPar("MV_VENCISS","E")=="F" //Qtd de dia do parametro MV_DIAISS apos o fechamento da quinzena.
				If Day(dEmissao) <= 15
					dVenISS := CtoD("15"+SUBSTR(DtoC(dEmissao),3,Len(DtoC(dEmissao)))) + nDiaISS
				Else
					dVenISS := LastDay(dEmissao) + nDiaISS
				EndIf
				dVenISS := DataValida(dVenISS,!lVcAntIss)
			OtherWise
				dVenISS := dVencto
				dVenISS += 28
				If ( Month(dVenISS) == Month(dVencto) )
					dVenISS := dVenISS+28
				EndIf
				nTamData := Iif(Len(Dtoc(dVenISS)) == 10, 7, 5)
				dVenISS	:= Ctod( cDiaISS + "/"+Subs(Dtoc(dVenISS),4,nTamData))
				dVenISS := DataValida(dVenISS,!lVcAntIss)
		EndCase
	EndIf

	If Alltrim(SM0->M0_ESTENT) == "SC" .And. Alltrim(SM0->M0_CODMUN) == "09102"
		aAreaFor:= getArea("SA2")
		DbSelectArea("SA2")
		DbSetOrder(1)
		If SA2->(DbSeek(xFilial("SA2")+cFornSe2+cLojaSe2)) .And. Alltrim(SA2->A2_EST) == "SC"
			If Alltrim(SA2->A2_COD_MUN) == "09102"
				dVenISS := fCRetCal(4,dEmissao)
			Else
				dVenISS := fCRetCal(6,dEmissao)
			EndIf
		EndIf
		restArea(aAreaFor)
	ElseIf !Empty(dDtRec) .And. nTamData > 0
		dVenISS := Ctod(StrZero(dDtRec,2)+"/"+Subs(Dtoc(dVenISS),4,nTamData))
	EndIf

	If !_lIssMR
		RecLock("SE2",.T.)
		SE2->E2_FILIAL  := cFilial
		SE2->E2_PREFIXO := cPrefixo
		SE2->E2_NUM 	 := cNum
		SE2->E2_PARCELA := cParcela
		SE2->E2_TIPO	 := MVISS
		SE2->E2_EMISSAO := dEmissao
		SE2->E2_EMIS1	 := dDataBase
		SE2->E2_VALOR	 := nIss
		SE2->E2_VENCTO  := dVenISS
		SE2->E2_SALDO	 := nIss
		SE2->E2_VENCREA := dVenISS
		SE2->E2_VENCORI := dVenISS
		SE2->E2_FORNECE := Left(cForLojISS,Len(SE2->E2_FORNECE))
		SE2->E2_LOJA    := Right(cForLojISS,Len(SE2->E2_LOJA))
		SE2->E2_NOMFOR  := cNome
		SE2->E2_MOEDA	 := If(cPaisLoc == "BRA", 1, nMoeda)
		SE2->E2_VLCRUZ  := Round(SE2->E2_VALOR, MsDecimais(1))
		SE2->E2_ORIGEM  := cOrigem
		SE2->E2_NATUREZ := cNatureza
		SE2->E2_LA      := cLA			// Herda do principal
		SE2->E2_SEQBX	 := cSeq

		If lRatImp
			SE2->E2_MULTNAT  := "1"
		Endif

		SE2->E2_FILORIG := cFilorig //filial origem do titulo pai
		SE2->E2_TITPAI := cTitPai
		SE2->E2_CODAPRO := cCodAprov
		SE2->E2_FORMPAG := SA2->A2_FORMPAG
		MsUnlock()
		FKCOMMIT()

		FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)

		if lPLSTITPF
			PLSTITPF('SE2',cTitPai)
		endIf

		AADD(aRecImpos,{"SE2",Recno()})
	EndIf

	If ExistBlock("F050ISS")
		Execblock("F050ISS",.F.,.F.,nRegSE2)
	EndIf
	// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
	If lRatImp
		// Grava SEV e SEZ dos impostos, baseado nos percentuais do rateio do titulo principal
		GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR)
	Else
		AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, "+")
	Endif

	//Grava parcela do IRF na parcela do titulo
	DbSelectArea( "SE2" )
	DbGoTo( nRegSe2 )
	If !_lIssMR
		Reclock( "SE2" , .F. )
		SE2->E2_PARCISS := cParcela
		MsUnlock()
	EndIf
EndIf

//Ponto se entrada para gravacoes genericas a todos os titulos de impostos
If lF050GER
	ExecBlock("F050GER",.F.,.F.,aRecImpos)
Endif

//Reposiciona o fornecedor
If SA2->(Recno()) <> nRecnoFor
	SA2->(dbGoto(nRecnoFor))
EndIf

// Integração SIGAPFS x SIGAFIN Inclui os desdobramentos para os titulos filhos, quando estes forem gerados na baixa do titulo Pai.
Iif(FindFunction("JDesdFilho"), lRet := JDesdFilho(3, nRegSe2, aRecImpos), Nil)

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³GrvSevSezImp³ Autor ³ Claudio Donizete Souza³ Data ³ 15/03/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a gravacao das multiplas naturezas/centro de custo    ³±±
±±³          ³ dos titulos de impostos												    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³GrvSevSezImp(aRatSev,aRatSez,cParcela,cTipo,cFornece,cLoja,   ³±±
±±³          ³         		nValor)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINANCEIRO/COMPRAS  												       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GrvSevSezImp(aRatSev,aRatSez,cParcela, cTipo, cFornece, cLoja, nValor)
Local nX
Local nY
Local nLenSev := Len(aRatSev)
Local nLenSez := Len(aRatSez)
Local aArea := GetArea()
Local aAreaSev := SEV->(GetArea())
Local aAreaSez := SEZ->(GetArea())
Local nPosPrefixo
Local nPosNum
Local nPosNatureza
Local nPosPerc
Local nPosCCusto

SEV->(DbSetOrder(1))
SEZ->(DbSetOrder(1))

For nX := 1 To nLenSev
	nPosPrefixo := Ascan(aRatSev[1],{|e| Alltrim(e[1]) == "EV_PREFIXO" } )
	nPosNum		:= Ascan(aRatSev[1],{|e| Alltrim(e[1]) == "EV_NUM" } )
	nPosNatureza:= Ascan(aRatSev[1],{|e| Alltrim(e[1]) == "EV_NATUREZ" } )
	nPosPerc		:= Ascan(aRatSev[1],{|e| Alltrim(e[1]) == "EV_PERC" } )
	If !SEV->(MsSeek(xFilial("SEV")+aRatSev[nX][nPosPrefixo][2]+aRatSev[nX][nPosNum][2]+cParcela+cTipo+cFornece+cLoja+aRatSev[nX][nPosNatureza][2]))
		SEV->(RecLock("SEV", .T.))
		Aeval(aRatSev[nX], {|e| SEV->(FieldPut(FieldPos(e[1]),e[2])) })  // Grava os campos comuns ao rateio do titulo principal
		// Altera os campos que nao sao comuns
		SEV->EV_PARCELA	:= cParcela
		SEV->EV_CLIFOR		:= cFornece
		SEV->EV_LOJA		:= cLoja
		SEV->EV_TIPO		:= cTipo
		SEV->EV_VALOR		:= nValor * aRatSev[nX][nPosPerc][2]	// Proporcionaliza o valor do impostos pelo percentual rateado
		SEV->EV_RECPAG		:= "P" // Grava a Carteira
		SEV->(MsUnlock())
		AtuSldNat(SEV->EV_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SEV->EV_VALOR, SE2->E2_VLCRUZ * SEV->EV_PERC, If(SE2->E2_TIPO $ MVABATIM,"-","+"))
	Endif
Next
For nY := 1 To nLenSez
	nPosPrefixo := Ascan(aRatSez[1],{|e| e[1] == "EZ_PREFIXO" } )
	nPosNum		:= Ascan(aRatSez[1],{|e| e[1] == "EZ_NUM" } )
	nPosNatureza:= Ascan(aRatSez[1],{|e| e[1] == "EZ_NATUREZ" } )
	nPosPerc	:= Ascan(aRatSez[1],{|e| e[1] == "EZ_PERC" } )
	nPosCcusto	:= Ascan(aRatSez[1],{|e| e[1] == "EZ_CCUSTO" } )
	// Posiciona no SEV para obter o valor do imposto rateado por centro de custo
	If SEV->(MsSeek(xFilial("SEV")+aRatSez[nY][nPosPrefixo][2]+aRatSez[nY][nPosNum][2]+cParcela+cTipo+cFornece+cLoja+aRatSez[nY][nPosNatureza][2]))
		SEZ->(RecLock("SEZ", .T.))
		Aeval(aRatSez[nY], {|e| SEZ->(FieldPut(FieldPos(e[1]),e[2])) })  // Grava os campos comuns ao rateio do titulo principal
		// Altera os campos que nao sao comuns
		SEZ->EZ_PARCELA	:= cParcela
		SEZ->EZ_CLIFOR		:= cFornece
		SEZ->EZ_LOJA		:= cLoja
		SEZ->EZ_TIPO		:= cTipo
		SEZ->EZ_VALOR		:= SEV->EV_VALOR * aRatSez[nY][nPosPerc][2]
		SEZ->EZ_RECPAG		:= "P"  // Grava a Carteira
		SEV->(MsUnlock())
	Endif
Next
SEV->(RestArea(aAreaSev))
SEZ->(RestArea(aAreaSez))
RestArea(aArea)
Return Nil

Static Function MenuDef()
Local aRotina

aRotina := { { oemtoansi(STR0014),"AxPesqui", 0 , 1,,.F.},; // "Pesquisar"
	{ oemtoansi(STR0015),"AxCadVis", 0 , 2},; // "Visualizar"
	{ oemtoansi(STR0016),"AxCadInc", 0 , 3},; //"Incluir"
	{ oemtoansi(STR0017),"AxCadAlt", 0 , 4},; //"Alterar"
	{ oemtoansi(STR0018),"AxCadDel", 0 , 5}}  //"Excluir"
Return aRotina
//-----------------------------------------------

Static Function AxPesqPreview(aRet,oPPreview,oList,aList)

Local nI
Local nJ
Local aArray := {}
Local aHList := {}
Local aIndex := StrTokArr(IndexKey(),"+")
aList := {}

//-- Cria cabecalho do list
For nI := 1 To Len(aIndex)
	Aadd( aHList, RetTitle(aIndex[nI]) )
Next nI

//-- Carrega as informacoes
For nI := 1 To Len(aRet)
	DbGoto(aRet[nI])
	aArray := Array(Len(aIndex)+1)

	For nJ := 1 To Len(aIndex)
		aArray[nj] := &(aIndex[nJ])
	Next nJ
	aArray[Len(aArray)] := aRet[nI]

	Aadd( aList, aClone(aArray) )
Next nI

//-- List
oList := TCBrowse():New(0,0,0,0,,,,oPPreview,,,,,,,,,,,,.T.,,.T.,,.F.,,)
oList:SetArray(aList)
oList:Align := CONTROL_ALIGN_ALLCLIENT

For nI := 1 To Len(aHList)
	oList:AddColumn(TCColumn():New(aHList[nI],&("{ || aList[oList:nAt,"+Str(nI)+"] }"),,,,"LEFT",,.F.,.F.,,,,.F.,))
Next nI

oList:nAt := 1
oList:Refresh()

Return

//-----------------------------------------------

Static Function AxPreview(cAlias,lDetail,cCampo,aLista,aMyOrd,nOrd,lSeeAll,aPesqVar,oPPreview,oBigGet,aScroll,nOrd,oList,aList)

Local lRet := .F.
Local aRet := AxPesqSeek(cAlias,lDetail,cCampo,aLista,aMyOrd,nOrd,lSeeAll,aPesqVar,.T.)

If Len(aRet) > 0
	lRet := .T.
	oPPreview:FreeChildren()

	oBigGet:Hide()
	aScroll[nOrd]:Hide()
	AxPesqPreview(aRet,@oPPreview,@oList,@aList)

	oPPreview:Refresh()
	oPPreview:Show()
EndIf

Return lRet

//---------------------
Static Function CalcMemoSize(nTam)
Local nRet

// Mantem o tamanho Default em char.
nRet := If(nTam == 0 .or. nTam == 10,56,nTam)
nRet := If(nRet > 80,80,nRet)
If nRet > 50
	nRet := (308 * nRet / 100) +7
Else
	nRet := (310 * nRet / 100)+8
EndIf
Return nRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FGrvIRRF   ³Autor  ³João Gonçalves de Oliveira ³ Data ³18/08/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Efetua gravação do titulo de imposto de renda e dados            ³±±
±±³          ³relacionados a operacao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 - Função que originou a gravação                           ³±±
±±³          ³ExpD2 - Vencimento Real                                          ³±±
±±³          ³ExpD3 - Emissão do título                                        ³±±
±±³          ³ExpC4 - Prefixo do título                                        ³±±
±±³          ³ExpC5 - Número do título                                         ³±±
±±³          ³ExpC6 - Tipo do título                                           ³±±
±±³          ³ExpN7 - Valor IRRF                                               ³±±
±±³          ³ExpN8 - Moeda                                                    ³±±
±±³          ³ExpC9 - Flag Lançamento Contábil                                 ³±±
±±³          ³ExpCA - Flag Geração DIRF                                        ³±±
±±³          ³ExpCB - Código de Retenção                                       ³±±
±±³          ³ExpLC - Define se existe rateio                                  ³±±
±±³          ³ExpAD - Vetor com rateio multiplas naturezas                     ³±±
±±³          ³ExpAE - Vetor com rateio centros de custo                        ³±±
±±³          ³ExpNF - Número de registro do documento que originou o imposto   ³±±
±±³          ³ExpCG - Apelido do arquivo do documento que originou o imposto   ³±±
±±³          ³ExpLH - Define se trata-se de IRPF na Baixa                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function FGrvIRRF(cOrigem,dVencRea,dEmissao,cPrefixo,cNum,cTipoE2,nIRRF,nMoeda,cLa,cGeraDIRF,cCodRetIr,lRatImp,;
	aRatSEV,aRatSEZ,nRegiOrig,cAlias,lIRPFBaixa,aRecImpos, aRatIRF, aImpostos)

Local aArea		:= GetArea()
Local aAreaSE2	:= SE2->( GetArea() )
Local aAreaSA2	:= SA2->( GetArea() )
Local aEmpDados	:= {}

Local cNatureza	 := &(GetMv("MV_IRF"))
Local cLojaImpos := PadR( "00", Len( SE2->E2_LOJA ), "0" )
Local cTitPai    := SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
Local cCodAprov  := SE2->E2_CODAPRO
Local cFilorig   := SE2->E2_FILORIG
Local cCNPJRet	 := ""
Local cNomeCom	 := ""
Local nTpInsc	:= 0
Local nSM0Recno	:= ""
Local lCallMT103 := IsInCallStack("MATA103")
Local cUniao	 := If(! EMPTY(GetMV("MV_UNIAO")),GetMV("MV_UNIAO"),'UNIAO')
Local lCampos	:= ColumnPos("E2_NOMERET") > 0 .and. ColumnPos("E2_CNPJRET") > 0 .and. ColumnPos("E2_TPINSC") > 0
Local cParcela	:= ""
Local nTamForn	:= TamSX3("A2_COD")[1]
Local nX		:= 0
Local lMultNatP := SuperGetMV("MV_MULNATP",.F.,.F.)
Local lF050Irf 	:= ExistBlock("F050IRF")

Local lDedSimp	As Logical
Local cCGC		As Char

Default aRecImpos := {}
Default cGeraDirf := "1"
Default aRatIRF := {}
Default aImpostos	:= {}

lDedSimp	:= .F.
cCGC		:= ""

DbSelectArea("SA2")
DbSetOrder(1)	//A2_FILIAL, A2_COD, A2_LOJA
If SA2->(MsSeek(xFilial("SA2")+SE2->E2_FORNECE+SE2->E2_LOJA))
	If SA2->A2_TIPO == 'F'
		cCGC := SA2->A2_CGC
	ElseIf SA2->A2_TIPO == 'J' .And. SA2->A2_IRPROG == "1"
		cCGC := SA2->A2_CPFIRP
	EndIf
EndIf
If nIRRF == 0
	Return
EndIf

If IsInCallStack("fa530Processa")	 .And. !Empty(cCodRetIr)
	cGeraDirf := "1"
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria o Fornecedor, caso nao exista 		  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !MsSeek(xFilial("SA2")+ PadR(cUniao, nTamForn) + cLojaImpos)
	Reclock("SA2",.T.)
	SA2->A2_FILIAL	:= xFilial("SA2")
	SA2->A2_COD 	:= cUniao
	SA2->A2_LOJA	:= cLojaImpos
	SA2->A2_NOME	:= "UNIAO"
	SA2->A2_NREDUZ	:= "UNIAO"
	SA2->A2_BAIRRO	:= "."
	SA2->A2_MUN 	:= "."
	SA2->A2_EST 	:= SuperGetMv("MV_ESTADO")
	SA2->A2_END 	:= "."
	SA2->A2_TIPO	:= "J"
	MsUnlock()
	FKCOMMIT()
EndIf

cNatureza := cNatureza + Space(10-Len(cNatureza))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria a natureza IRF caso nao exista		   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SED")
DbSetOrder(1)		//ED_FILIAL, ED_CODIGO
If ( !DbSeek(cFilial+cNatureza) )
	RecLock("SED",.T.)
	SED->ED_FILIAL  := cFilial
	SED->ED_CODIGO  := cNatureza
	SED->ED_CALCIRF := "N"
	SED->ED_CALCISS := "N"
	SED->ED_CALCINS := "N"
	SED->ED_CALCCSL := "N"
	SED->ED_CALCCOF := "N"
	SED->ED_CALCPIS := "N"
	SED->ED_DESCRIC := "IMPOSTO RENDA RETIDO NA FONTE"
	SED->ED_TIPO	:= "2"
	MsUnlock()
	FKCOMMIT()
EndIf

If Empty(aRatIRF)

	/*	[1]  = Codigo do Fornecedor
		[2]  = Loja do Fornecedor
		[3]  = CPF do Fornecedor
		[4]  = Percentual de Rateio
		[5]  = Base do Imposto
		[6]  = Imposto Calculado
		[7]  = Imposto Retido
		[8]  = Nome do Fornecedor*/

	aAdd( aRatIRF, { "", "", cCGC, 100, SE2->E2_BASEIRF, nIrrf, nIrrf, ""} )

EndIf

For nX := 1 to Len(aRatIRF)

	If !Empty(aRatIRF[nX][6])
		nIrrf := aRatIRF[nX][6]
		cParcela := ParcImposto(cPrefixo,cNum,cTipoE2,lIRPFBaixa,,.T.)

		DbSelectArea("SE2")
		DbSetOrder(1)	//E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA

		If !SE2->(Dbseek(xFilial("SE2")+cPrefixo+cNum+cParcela+Iif(cTipoE2 $ MVPAGANT+"#"+MV_CPNEG .And. (!lIRPFBaixa),MVTXA,MVTAXA)+PadR(cUniao,6)))
			RecLock("SE2",.T.)
			SE2->E2_FILIAL  := cFilial
			SE2->E2_PREFIXO := cPrefixo
			SE2->E2_NUM 	:= cNum
			SE2->E2_PARCELA := cParcela
			SE2->E2_TIPO	:= Iif(cTipoE2 $ MVPAGANT+"/"+MV_CPNEG .And. ! lIRPFBaixa,MVTXA,MVTAXA)
			SE2->E2_EMISSAO := dEmissao
			SE2->E2_VALOR	:= nIrrf
			SE2->E2_VENCREA := dVencrea
			SE2->E2_SALDO	:= nIrrf
			SE2->E2_VENCTO  := dVencRea
			SE2->E2_VENCORI := dVencRea
			SE2->E2_MOEDA	:= If(cPaisLoc=="BRA",1,nMoeda)
			SE2->E2_EMIS1	:= dDataBase
			SE2->E2_FORNECE := cUniao
			SE2->E2_VLCRUZ  := Round( SE2->E2_VALOR, MsDecimais(1) )
			SE2->E2_LOJA	:= SA2->A2_LOJA
			SE2->E2_NOMFOR  := SA2->A2_NREDUZ
			SE2->E2_ORIGEM  := cOrigem
			SE2->E2_NATUREZ := cNatureza
			SE2->E2_LA      := cLA			// Herda do principal

			//PCDEF-80569 - campos novos para pagamento do cnab tributo via cnab.
			If lCampos .and. Empty(SE2->E2_CNPJRET)
				If Empty(aRatIRF[nX][3])
					If cFilorig == FWCodFil()
						cCNPJRet	:= SM0->M0_CGC
						cNomeCom	:= SM0->M0_NOMECOM
						nTpInsc		:= SM0->M0_TPINSC
					Else
						aEmpDados := FWArrFilAtu(,SE2->E2_FILIAL)

						If Len(aEmpDados) >= 18
							cNomeCom  := aEmpDados[17]// Nome da Empresa que originou o Título
							cCNPJRet  := aEmpDados[18]// CNJP da Filial que originou o Título
							nSM0Recno := SM0->(RECNO())
							SM0->(DbSetOrder(1))
							SM0->(DbSeek( aEmpDados[1] + aEmpDados[2] ))
							nTpInsc := SM0->M0_TPINSC
							SM0->(DbGoTo(nSM0Recno))
						EndIf
					EndIf
					SE2->E2_TPINSC	:= IIf( nTpInsc == 3, "1", "2" )
				Else
					cNomeCom := aRatIRF[nX][8]
					cCNPJRet := aRatIRF[nX][3]
				EndIf
				SE2->E2_NOMERET	:= cNomeCom
				SE2->E2_CNPJRET	:= cCNPJRet
			EndIf

			If cPaisLoc == "BRA"
				SE2->E2_DIRF    := cGeraDirf
				SE2->E2_CODRET  := cCodRetIr
			Endif

			// PCREQ-3746-Retenção de IR
			If ColumnPos("E2_DTAPUR") > 0 .and. SE2->E2_DIRF == "1" .AND. !Empty(SE2->E2_CODRET)
				SE2->E2_DTAPUR	:= LastDay(MonthSub(dVencrea, 1))
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o campo E2_MULTNAT = '1' para que seja possivel a        ³
			//³ visualizacao do roteio atraves da rotina FINA050 (FA050Rateio)    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRatImp .And. lCallMT103 .And. lMultNatP
				SE2->E2_MULTNAT := "1"
			EndIf

			SE2->E2_FILORIG := cFilorig //filial origem do titulo pai
			SE2->E2_TITPAI := cTitPai
			SE2->E2_CODAPRO := cCodAprov
			SE2->E2_FORMPAG := SA2->A2_FORMPAG
			MsUnlock()

			FKCOMMIT()

			FINGRVFK7("SE2", SE2->E2_FILIAL +"|"+ SE2->E2_PREFIXO +"|"+ SE2->E2_NUM +"|"+ SE2->E2_PARCELA +"|"+ SE2->E2_TIPO +"|"+ SE2->E2_FORNECE +"|"+ SE2->E2_LOJA,,cTitPai)

			if lPLSTITPF
				PLSTITPF('SE2',cTitPai)
			endIf

		EnDIf
		AADD(aRecImpos,{"SE2",Recno()})

		If lF050Irf
			Execblock("F050IRF",.F.,.F.,nRegiOrig)
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava lançamento do imposto IRRF no SIGAPCO se a rotina foi chamada do contas a pagar ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If "FINA050" $ cOrigem

			PCODetLan("000002","06","FINA050")

		EndIf

		// Se rateia os impostos, grava o rateio multipla natureza/centro de custo
		If lRatImp .And. cAlias == "SE2"
			GrvSevSezImp(aRatSev,aRatSez,SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_FORNECE, SE2->E2_LOJA, SE2->E2_VALOR) // Grava SEV e SEZ dos impostos
		ElseIf cAlias == "SE2"
			AtuSldNat(SE2->E2_NATUREZ, SE2->E2_VENCREA, SE2->E2_MOEDA, "2", "P", SE2->E2_VALOR, SE2->E2_VLCRUZ, "+")
		Endif

		/*-------------------------------------------
			Estrutura da aImpos
			[1]  = Codigo do tipo de imposto (FKK_CODIGO)
			[2]  = Base do imposto
			[3]  = Valor calculado do imposto
			[4]  = Base de retenção do imposto
			[5]  = Valor a reter do imposto
			[6]  = IDRET FK4
			[7]  = Array contendo os Recnos FK3 das pendências de retenção
			[8]  = Tipo do Imposto (FOO)
			[9]  = Regime (1 = Competência ou 2 = Baixa)
			[10] = Natureza do imposto
			[11] = Tabela onde foi gerado o imposto
			[12] = Recno do titulo de imposto gerado
			[13] = Ação aplicada no valor da nota (1 = subtrai, 2 = soma, 3 = sem ação)
			[14] = Carteira de movimento do imposto (1 = Pagar, 2 = Receber)
			[15] = Tipo de movimento (1 = Abtimento, 2 = Impostos)
			[16] = Flag que valida se houve retenção no período para o imposto
			[17] = Variável de contabilização
			[18] = Chave única da tabela Id de retenção
			[19] = Ação sobre títulos de antecipação pagamento/recebimento: 1 = Retém, 3 = sem ação
			[20] = Flag que valida se houve retenção no período para o imposto
			[21] = Recno da FKK
			[22] = Valor retido previamente para determinado imposto
			[23] = CPF do Rateio ou do fornecedor
			[24] = Codigo de retenção
			[25] = Calculo de IRPF simplificado

		--------------------------------------------*/
		If ! (lIRPFBaixa .and. cTipoE2 $ MVPAGANT)
			lDedSimp := If(len(aRatIRF[nX])>10, aRatIRF[nX][11], .F.)
			Aadd(aImpostos, { "", aRatIRF[nX][5], aRatIRF[nX][6], aRatIRF[nX][5], aRatIRF[nX][6],"", ;
				{}, "IRF", "1", cNatureza,"",SE2->(Recno()),"","1", "2", "","","", "2",.T.,0, 0,aRatIRF[nX][3], cCodRetIr, lDedSimp})
		EndIf
	EndIf
Next Nx
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava parcela do IRF na parcela do titulo  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cAlias == "SE2"

	DbSelectArea( cAlias )
	DbGoTo( nRegiOrig )
	Reclock( "SE2" , .F. )
	SE2->E2_PARCIR := cParcela
	If cPaisLoc=="BRA"

		SE2->E2_DIRF    := "2"	 // Desmarca titulo principal, pois apenas o titulo de
									 // imposto vai para DIRF
	Endif
	MsUnlock()

EndIf

RestArea( aAreaSA2 )
RestArea( aAreaSE2 )
RestArea( aArea )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FGrvSFQ    ³Autor  ³Adrianne Furtado Andrade   ³ Data ³12/01/2009³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Efetua gravação do relacionamento de impostos no SFQ             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1 - Tabela de relacionamento                                 ³±±
±±³          ³ExpN2 - RECNO do registro que gerou o(s) imposto(s)              ³±±
±±³          ³ExpA3 - Array com a lista de titulos relacionados ao impostos    ³±±
±±³          		  retido.												   ³±±
±±³          ³ExpC4 - Tipo do Imposto referente ao relacionamento, informar:   ³±±
±±³                   "INS" : parar relacionamentos de INSS                    ³±±
±±³                   "   " : parar relacionamentos de PIS COFINS e CSLL       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FGrvSFQ(cTabela,nRegOri,aListTit, cTipImp)
Local cPrefOri
Local cNumOri
Local cParcOri
Local cTipoOri
Local cCfOri
Local cLojaOri
Local nX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava parcela do INSS na parcela do titulo ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea( "SE2" )
DbGoTo( nRegOri )
cPrefOri  := SE2->E2_PREFIXO
cNumOri   := SE2->E2_NUM
cParcOri  := SE2->E2_PARCELA
cTipoOri  := SE2->E2_TIPO
cCfOri    := SE2->E2_FORNECE
cLojaOri  := SE2->E2_LOJA

For nX := 1 to Len(aListTit)

	SE2->(DbGoTo( aListTit[nX] ))
	Reclock( cTabela , .F. )
	SE2->E2_PRETINS := "2" // PRET = "2" - Retido na emissão em outro título.
	MsUnlock()

	If nRegOri <> aListTit[nX]
		dbSelectArea("SFQ")
		RecLock("SFQ",.T.)
		SFQ->FQ_FILIAL  := xFilial("SFQ")
		SFQ->FQ_ENTORI  := cTabela
		SFQ->FQ_PREFORI := cPrefOri
		SFQ->FQ_NUMORI  := cNumOri
		SFQ->FQ_PARCORI := cParcOri
		SFQ->FQ_TIPOORI := cTipoOri
		SFQ->FQ_CFORI   := cCfOri
		SFQ->FQ_LOJAORI := cLojaOri
		SFQ->FQ_FILDES			:=    SE2->E2_FILIAL
		SFQ->FQ_ENTDES  := cTabela
		SFQ->FQ_PREFDES := SE2->E2_PREFIXO
		SFQ->FQ_NUMDES  := SE2->E2_NUM
		SFQ->FQ_PARCDES := SE2->E2_PARCELA
		SFQ->FQ_TIPODES := SE2->E2_TIPO
		SFQ->FQ_CFDES   := SE2->E2_FORNECE
		SFQ->FQ_LOJADES := SE2->E2_LOJA

		SFQ->FQ_TPIMP	:= cTipImp //sempre deve gravar "INS" para INSS e "   " para PCC
		MsUnlock()
	Endif
Next nX

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³ParcImpRec³ Autor ³ Claudio D. de Souza   ³ Data ³ 11/12/03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula a parcela de titulos de impostos, IR, PIS, COFINS  ³±±
±±³          ³ e CSLL                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ ParcImpRec(cPrefixo,cNum,cTipoE1,lIRPFBaixa)               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINANCEIRO/COMPRAS  												     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ParcImpRec(cPrefixo,cNum,cTipoE1)
Local aArea	:= GetArea()
Local aAreaSE1 := SE1->(GetArea())
Local cParcela	:= ""
Local cAlias 	:= ""
Local cChave := ""
Local aTamParc := TamSx3("E1_PARCELA")

Local cSavFilter // Declarar variavel para evitar problemas quando existir filtro

If Select("__SE1") == 0
	dbSelectArea("SE1")
	cSavFilter := dbFilter()
	dbClearFilter()
	cAlias := "SE1"
Else
	cAlias := "__SE1"
EndIf
cParcela := STRZERO(1,aTamParc[1])
While ( .T. )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ VerIfica se ja' ha' titulo de IMPOSTO com esta numera‡„o ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea(cAlias)
	DbSetOrder(1)
	cChave := xFilial("SE1")+cPrefixo+cNum+cParcela+cTipoE1

	If DbSeek(cChave)
		cParcela := Soma1( cParcela,,.T.)
		Loop
	EndIf
	Exit
Enddo

If cAlias=="SE1" .And. !Empty( cSavFilter )
	dbSelectArea("SE1")
	DbSetFilter({||&(cSavFilter)},cSavFilter)
EndIf

RestArea(aArea)
RestArea(aAreaSE1)
Return cParcela

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³FPccBxCr	ºAutor  ³Mauricio Pequim Jr. º Data ³  03/09/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para verificar o tratamento de PCC na baixa CR      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Generico - PCC Baixa CR                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function FPccBxCr(lVerMotor)

Local lPccBxCr   := .F.
Local lPccMr     := .F.
Local aRetPcc    := {}


Default lVerMotor := .F.

If __lTemMR == NIL
	__lTemMR := (FindFunction("FTemMotor") .and. FTemMotor())
Endif

If lVerMotor .and. __lTemMR
	aRetPcc := F070VerImp("2",cFilAnt,SE1->E1_CLIENTE,SE1->E1_LOJA,,.T.,,@lPccBxCr,,,@lPccMr)
	If Len(aRetPcc) == 0 .Or. !lPccMr
		lPccBxCr := SuperGetMv("MV_BR10925",.T.,"2") == "1"
	Endif
Else //Se não estiver configurado pelo motor de retenções, então verifica se o PCC é na emissão ou baixa pelo parâmetro do legado
	//Controla o Pis Cofins e Csll na baixa (1-Retem PCC na Baixa ou 2-Retem PCC na Emissão(default))
	lPccBxCr := SuperGetMv("MV_BR10925",.T.,"2") == "1"
Endif

Return lPccBxCr

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FTotF100  ³ Autor ³ Totvs          	     ³ Data ³ 06/05/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Func„o para a soma dos registros enviados no bloco F100    ³±±
±±³          ³ do SPED PIS/COFINS                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FTotF100() 					      		                  	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Modulo de RH        													  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FTotF100(nMesRef,nAnoRef,cDataIni,cDataFim)

Return FinSpdF100(nMesRef,nAnoRef,.T.,cDataIni,cDataFim)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³   DoEAI  ³ Autor ³ Totvs          	    ³ Data ³ 13/11/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Func„o para gerar mensagem do EAI                          ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ GERAL     												  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function DoEAI()
Local aArea       := GetArea()
Local aAreaXX4    := XX4->( GetArea() )
Local cFunName    := FunName()
Local nTamFunName := Len( cFunName )
Local aRetorno    := {}


XX4->( dbSetOrder( 1 ) )
//XX4->( MsSeek(PadR(xFilial('XX4'),Len(XX4->XX4_FILIAL) ) + cFunName ) )
FWXX4Seek( cFunName )

If  XX4->XX4_UNMESS == '1'
	While !XX4->( EOF() ) .AND. cFunName == SubStr( XX4->XX4_ROTINA, 1, nTamFunName )
		If FWEAICanSend( EAI_MESSAGE_BUSINESS,, cFunName )
			aRetorno := &( "STATICCALL(" + cFunName + ", IntegDef, NIL, '" + TRANS_SEND + "', '" + EAI_MESSAGE_BUSINESS + "')" )
			If ValType( aRetorno ) == 'A' .AND. Len( aRetorno ) > 1 .AND. aRetorno[1]
				FWEAIMake( EAI_MESSAGE_BUSINESS, aRetorno[2] )
			EndIf
		EndIf
		XX4->( dbSkip() )
	End
EndIf

RestArea( aAreaXX4 )
RestArea( aArea )
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³F100Top   ºAutor  ³Clovis Magenta      º Data ³  29/06/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao de filtros dos registros do bloco F100 - SPED PIS   º±±
±±º          ³ COFINS - para TOP CONNECT                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function F100Top(cAlias,cAliasQry,cDataIni,cDataFim,cTpSe5,cTpApl,lSpdDtEm,cTpSel,lSEI_,cNOrigem,lSpdDtEm2,lUnidNeg,cNatSe2,lDescDoc,cNatResg,lNRastDSD,lIntPFS)

Local aStruct	:= {}
Local cQuery	:= ""
Local cDtEmis	:= ""
Local nX		:= 0
Local nLen		:= 0
Local cFilSe	:= ""
Local cTipoTit	:=	""
Local cTpNat	:=	cNatDesp	:=	""
Local lSe5Excl	:= FWModeAccess("SE5",3) == "E"
Local nTamDoc	:= ""
Local nTamFil    := 0
Local nTamEsc    := 0
Local cTamFilial := 0
Local cIniEscr   := 0
Local cTamEscr   := 0
Local cIniFatur  := 0
Local cTamFatur  := 0

Default lUnidNeg:= FWSizeFilial() > 2
Default cNatSe2	:= SuperGetMv("MV_NATCP",,"")
Default lDescDoc:= SuperGetMv("MV_DESCDOC",,.F.) 
Default cNatResg:= &(GetMv("MV_NATRAPL"))
Default lNRastDSD:=SuperGetMV("MV_NRASDSD",.T.,.F.)
Default lIntPFS  := SuperGetMv("MV_JESCJUR",,.F.) 

If __lTemMR == NIL
	__lTemMR := (FindFunction("FTemMotor") .and. FTemMotor())
Endif

If lSe2MsFil == NIL
	lSe2MsFil := SE2->(FieldPos("E2_MSFIL")) > 0
	lSe5MsFil := SE5->(FieldPos("E5_MSFIL")) > 0
	lSeiMsFil := SEI->(FieldPos("EI_MSFIL")) > 0

	lDescSe2:= SE2->(ColumnPos("E2_TPDESC"))> 0 //Verifica campo TPDESC na tabela SE5 (<C>ondicional ou <I>ncondicional)
	lEdF100	:= SED->(ColumnPos("ED_F100"))	> 0
	lEdCprb	:= SED->(ColumnPos("ED_CPRB"))	> 0	

	lF100TQry	:= ExistBlock("SPDF100Q")	
EndIf

If lDescSe2 .And. __lAjstSx
	AjstSxSPD("E2_TPDESC")
EndIf

 If !Empty(cNatSe2) // Tratamento para o parametro MV_NATDESP.
	cNatDesp	:="'"	// Inserindo aspas e virgulas para o select da query.
	For nX:=1 To Len(cNatSe2)
		If Subst(cNatSe2,nX,1) $ ";,-_|./"
  	   	cNatDesp += "','"
  	   Else
  	   	cNatDesp += Subst(cNatSe2,nX,1)
  	   Endif
  	Next

	cNatDesp	+=	"'"
	cTpNat	:= ""
	cNatSe2	:= ""
	For nX:=1 To Len(cNatDesp)
		If Subst(cNatDesp,nX,1) $ "'"
	  	   If Len(cNatSe2) > 1 .And. Len(cNatSe2) < 10
  	       	cTpNat += Space(1)
  	      Endif
	  	   cTpNat  +=Subst(cNatDesp,nX,1)
  	      cNatSe2 :=""
	   ElseIf Subst(cNatDesp,nX,1) $ ","
		   cTpNat+=Subst(cNatDesp,nX,1)
  	   Else
  	   	cTpNat	+=Subst(cNatDesp,nX,1)
  			cNatSe2	+=Subst(cNatDesp,nX,1)
	   Endif
	Next
Endif

If lUnidNeg
	cFilSe	:= SM0->M0_CODFIL
Else
	cFilSe	:= cFilAnt
Endif

If cAlias == "SE5"

	aStruct 	:= Iif( __aSE5Stru==Nil , (__aSE5Stru:=SE5->(dbStruct())) , __aSE5Stru )
	cQuery	 	:= "SELECT "
	cQuery	 	+= "SE5.E5_FILIAL FILIAL, SE5.E5_PREFIXO PREFIXO, SE5.E5_NUMERO NUM, SE5.E5_PARCELA PARCELA, SE5.E5_TIPO TIPO, SE5.E5_CLIFOR CLIFOR, SE5.E5_LOJA LOJA, "
	cQuery	 	+= "SE5.E5_TIPODOC TIPODOC, SE5.E5_SEQ SEQBX, SE5.E5_MOEDA MOEDA, SE5.E5_TXMOEDA TXMOEDA, "
	cQuery	 	+= "SE5.E5_DATA DATAM, SE5.E5_VALOR VALOR, SE5.E5_VRETPIS PIS, SE5.E5_VRETCOF COFINS, SE5.E5_RECPAG RECPAG, "
	cQuery		+= "SED.ED_CONTA EDCONTA, SED.ED_CCD EDCCD, SED.ED_CCC EDCCC, "

	If lEdF100
		cQuery	+= "SED.ED_F100 ED_F100, "
	EndIf

	If lEdCprb
		cQuery	+= "SED.ED_CPRB CPRB, "
	Else
		cQuery	+= "'1' CPRB, "
	EndIf
	
	cQuery   	+= "SE5.E5_TPDESC TPDESC, "
	cQuery		+= "SE5.E5_VLDESCO DESCONTO, "
	cQuery	 	+= "SE5.R_E_C_N_O_ RECNO, "
	cQuery	 	+= " ( SELECT SUM(SE50.E5_VALOR) "
	cQuery	 	+= " FROM " + RetSqlName("SE5") + " SE50 "
	cQuery	 	+= " WHERE SE50.E5_FILIAL = SE5.E5_FILIAL AND SE50.E5_PREFIXO = SE5.E5_PREFIXO AND SE50.E5_NUMERO = SE5.E5_NUMERO AND SE50.E5_PARCELA = SE5.E5_PARCELA AND "
	cQuery	 	+= " SE50.E5_TIPODOC = 'VM' AND SE50.E5_DATA <= SE5.E5_DATA ) AS TOT_VM "
	cQuery	 	+= "FROM "
	cQuery 		+= RetSqlName("SE5") + " SE5, "
	cQuery 		+= RetSqlName("SED") + " SED "
	cQuery	 	+= "WHERE "

	If lSe5Excl
		cQuery 	+= "SE5.E5_FILIAL = '"  + xFilial("SE5") + "' AND "
	Else
		If lSe5MsFil
			cQuery 	+= "SE5.E5_MSFIL = '" + cFilSe + "' AND "
		Else
			cQuery 	+= "SE5.E5_FILORIG = '" + cFilSe + "' AND "
		Endif
	EndIf

	cQuery 		+=	" SED.ED_FILIAL='" + xFilial("SED") + "' AND "
	cQuery	 	+= " SE5.E5_NATUREZ = SED.ED_CODIGO AND "
	cQuery 	+= " SED.D_E_L_E_T_ ='' AND "
	cQuery	 	+= "(SE5.E5_DATA >= '"+cDataIni+"' AND SE5.E5_DATA <= '"+cDataFim+"') AND "

	//Exclui os titulos que possuem estorno
	cQuery	 	+= "SE5.E5_SEQ NOT IN "
	cQuery 		+= "(SELECT SE5AUX.E5_SEQ FROM "+RetSqlName("SE5")+" SE5AUX WHERE "
	cQuery		+= 		" SE5AUX.E5_FILIAL = SE5.E5_FILIAL AND "
	cQuery		+= 		" SE5AUX.E5_PREFIXO = SE5.E5_PREFIXO AND "
	cQuery		+= 		" SE5AUX.E5_NUMERO = SE5.E5_NUMERO AND  "
	cQuery		+= 		" SE5AUX.E5_PARCELA = SE5.E5_PARCELA AND "
	cQuery		+= 		" SE5AUX.E5_TIPO = SE5.E5_TIPO AND "
	cQuery		+= 		" SE5AUX.E5_CLIFOR = SE5.E5_CLIFOR AND "
	cQuery		+= 		" SE5AUX.E5_LOJA = SE5.E5_LOJA AND "
	cQuery		+= 		" SE5AUX.E5_TIPODOC = 'ES' AND "
	cQuery		+= 		" SE5AUX.D_E_L_E_T_ = '' "
	cQuery 		+= ") AND "

	cTipoTit		:=	""
	cTipoTit		:=	MVABATIM + "|" + MV_CRNEG + "|" + MVPROVIS
	cQuery 		+= "SE5.E5_TIPO NOT IN " + FormatIn(cTipoTit,If("|"$cTipoTit,"|",","))  + " AND "

	cQuery		+= "((SE5.E5_MOTBX = 'FAT' AND SE5.E5_TIPODOC IN ('DC','JR','MT')) OR SE5.E5_MOTBX <> 'FAT') AND "

	cQuery		+= "SE5.E5_MOTBX <> 'LIQ' AND "

	If !Empty(cTpSe5)
		cQuery 	+= "((SE5.E5_TIPODOC IN (" + cTpSe5 + ")) OR SE5.E5_TIPODOC = 'CP' AND E5_VLDECRE >0) AND "
	Endif

	cQuery		+= "SE5.E5_SITUACA <> 'C' AND "
	cQuery		+= "SE5.D_E_L_E_T_ = '' "

	cQuery 		+= " ORDER BY FILIAL, PREFIXO, NUM, PARCELA, TIPO, CLIFOR, LOJA, SE5.R_E_C_N_O_, TIPODOC"

ElseIf cAlias == "SEI"

	IF lSEI_
		aStruct 	:= Iif( __aSEIStru==Nil , (__aSEIStru:=SEI->(dbStruct())) , __aSEIStru )
		cQuery	 	:= "SELECT "
		cQuery	 	+= "SEI.EI_FILIAL FILIAL, SEI.EI_APLEMP APLEMP, SEI.EI_NUMERO NUM, SEI.EI_REVISAO REVISAO, SEI.EI_SEQ SEQ, "
		cQuery	 	+= "SEI.EI_TIPO TIPO, SEI.EI_DATA DATAM,SEI.EI_VALOR VALOR, SEI.EI_STATUS STATUS, "
		cQuery	 	+= "SEI.EI_BANCO BANCO, SEI.EI_AGENCIA AGENCIA, SEI.EI_CONTA CONTA, SEI.EI_TIPODOC TIPODOC, "
		cQuery   	+= "SEI.EI_VLMOED2 VLCRUZ, SEI.EI_NATUREZ NATUREI, SEI.EI_TXMOEDA TXMOEDA, "
		cQuery   	+= "SEH.EH_MOEDA MOEDA, SEH.EH_NATUREZ NATUREZ, "

		cQuery	 	+= "SED.ED_FILIAL FILSED, SED.ED_CODIGO CODNAT, "

		If lEdF100
			cQuery	+= "SED.ED_F100 ED_F100, "
		EndIf

		If lEdCprb
			cQuery	+= "SED.ED_CPRB CPRB, "
		Else
			cQuery	+= "'1' CPRB, "
		EndIf

		cQuery		+= "SED.ED_CONTA EDCONTA, SED.ED_CCD EDCCD, SED.ED_CCC EDCCC, "
		cQuery	 	+= "SA6.A6_FILIAL FILBCO, SA6.A6_COD CODBCO, SA6.A6_AGENCIA AGBCO, SA6.A6_NUMCON CTABCO, SA6.A6_CGC CLIFOR, "
		cQuery	 	+= "SEI.R_E_C_N_O_ RECNO "
		cQuery	 	+= "FROM "
		cQuery 		+= RetSqlName("SEI") + " SEI,  "
		cQuery 		+= RetSqlName("SEH") + " SEH,  "
		cQuery 		+= RetSqlName("SA6") + " SA6,  "
		cQuery 		+= RetSqlName("SED") + " SED   "

		cQuery	 	+= "WHERE "

		cQuery	 	+= "(SEI.EI_DATA >= '"+cDataIni+"' AND SEI.EI_DATA <= '"+cDataFim+"') AND "

		If !Empty( Iif( lUnidNeg, FWFilial("SEI") , xFilial("SEI") ) )
			cQuery 	+= "SEI.EI_FILIAL = '"  + xFilial("SEI") + "' AND "
		Else
			If lSeiMsFil
				cQuery 	+= "SEI.EI_MSFIL = '" + cFilSe + "' AND "
			Else
				cQuery 	+= "SEI.EI_FILIAL = '"  + cFilSe + "' AND "
			Endif
		EndIf

		cQuery 		+=	"SEH.EH_FILIAL='" + xFilial("SEH") + "' AND "
		cQuery 		+=	"SA6.A6_FILIAL='" + xFilial("SA6") + "' AND "
		cQuery 		+=	"SED.ED_FILIAL='" + xFilial("SED") + "' AND "

		cQuery		+= "SEI.EI_NUMERO = SEH.EH_NUMERO AND "
		cQuery		+= "SEI.EI_REVISAO = SEH.EH_REVISAO AND "
		cQuery		+= "SEI.EI_APLEMP = SEH.EH_APLEMP AND "

		cQuery 		+= "SEH.EH_NATUREZ IN (" + cTpApl + ") AND "
		cQuery 		+= "SEI.EI_NATUREZ IN ('" + cNatResg + "') AND "

		cQuery 		+=  "SEI.EI_TIPODOC IN ( 'JR','I7')  AND "
		cQuery 		+=  "SEI.EI_MOTBX = 'APR' AND "

		cQuery 	+=  "SEI.EI_STATUS NOT IN ( 'C')   AND "

		cQuery 	+=  "SEI.EI_BANCO  != ' '  AND "
		cQuery 	+=  "SEI.EI_AGENCIA  != ' '  AND "
		cQuery 	+=  "SEI.EI_CONTA  != ' '   AND "

		cQuery		+= "SA6.A6_COD = SEH.EH_BANCO AND "
		cQuery		+= "SA6.A6_AGENCIA = SEH.EH_AGENCIA AND "
		cQuery		+= "SA6.A6_NUMCON = SEH.EH_CONTA AND "
		cQuery		+= "SA6.D_E_L_E_T_ = '' AND "

		cQuery		+= "SED.ED_CODIGO = SEH.EH_NATUREZ AND "
		cQuery 		+= "SED.D_E_L_E_T_ = '' AND "
		cQuery		+= "SEI.D_E_L_E_T_ = '' AND "
		cQuery		+= "SEH.D_E_L_E_T_ = '' "

	Else

		aStruct 	:= Iif( __aSE5Stru==Nil , (__aSE5Stru:=SE5->(dbStruct())) , __aSE5Stru )
		cQuery	 	:= "SELECT "
		cQuery	 	+= "SE5.E5_FILIAL FILIAL, SE5.E5_NUMERO NUM, "
		cQuery	 	+= "SE5.E5_TIPO TIPO, SE5.E5_NATUREZ NATUREZ, SE5.E5_DATA DATAM,SE5.E5_VALOR VALOR, "
		cQuery	 	+= "SE5.E5_BANCO BANCO, SE5.E5_AGENCIA AGENCIA, SE5.E5_CONTA CONTA, SE5.E5_RECPAG RECPAG, "
		cQuery   	+= "SE5.E5_VLMOED2 VLCRUZ, SE5.E5_MOEDA MOEDA, SE5.E5_TXMOEDA TXMOEDA, SE5.E5_ORIGEM, E5_CODORCA, E5_IDMOVI, "
		cQuery	 	+= "SED.ED_FILIAL FILSED, SED.ED_CODIGO CODNAT, "
	
		If lDescDoc
			cQuery   += "SE5.E5_HISTOR HIST, "
		Endif
		If lEdF100
			cQuery	+= "SED.ED_F100 ED_F100, "
		EndIf

		If lEdCprb
			cQuery	+= "SED.ED_CPRB CPRB, "
		Else
			cQuery	+= "'1' CPRB, "
		EndIf

		cQuery		+= "SED.ED_CONTA EDCONTA, SED.ED_CCD EDCCD, SED.ED_CCC EDCCC, "
		cQuery	 	+= "SA6.A6_FILIAL FILBCO, SA6.A6_COD CODBCO, SA6.A6_AGENCIA AGBCO, SA6.A6_NUMCON CTABCO, SA6.A6_CGC CLIFOR, "
		cQuery   	+= "SE5.E5_TPDESC TPDESC, "
		cQuery	 	+= "SE5.R_E_C_N_O_ RECNO, "
		cQuery	 	+= "SE5.E5_TIPODOC TIPODOC, ( SELECT SUM(SE50.E5_VALOR) FROM " + RetSqlName("SE5") + " SE50 "
		cQuery	 	+= " WHERE SE50.E5_PREFIXO = SE5.E5_PREFIXO AND SE50.E5_NUMERO = SE5.E5_NUMERO AND SE50.E5_PARCELA = SE5.E5_PARCELA AND "
		cQuery	 	+= " SE50.E5_TIPODOC = 'VM' AND SE50.E5_DATA <= SE5.E5_DATA ) AS TOT_VM "

		cQuery	 	+= "FROM "
		cQuery 		+= RetSqlName("SE5") + " SE5,  "
		cQuery 		+= RetSqlName("SA6") + " SA6,  "
		cQuery 		+= RetSqlName("SED") + " SED   "

		cQuery	 	+= "WHERE "
		cQuery	 	+= "(SE5.E5_DATA >= '"+cDataIni+"' AND SE5.E5_DATA <= '"+cDataFim+"') AND "
		cQuery 		+= "SE5.E5_NATUREZ IN (" + cTpApl + ") AND "

		If lSe5Excl
			cQuery 	+= "SE5.E5_FILIAL = '"  + xFilial("SE5") + "' AND "
		Else
			If lSe5MsFil
				cQuery 	+= "SE5.E5_MSFIL = '" + cFilSe + "' AND "
			Else
				cQuery 	+= "SE5.E5_FILORIG = '" + cFilSe + "' AND "
			Endif
		EndIf

		cQuery 		+=	"SA6.A6_FILIAL='" + xFilial("SA6") + "' AND "
		cQuery 		+=	"SED.ED_FILIAL='" + xFilial("SED") + "' AND "
		cQuery		+= "SE5.E5_NATUREZ = SED.ED_CODIGO AND "
		cQuery		+= "SA6.A6_COD = SE5.E5_BANCO AND "
		cQuery		+= "SA6.A6_AGENCIA = SE5.E5_AGENCIA AND "
		cQuery		+= "SA6.A6_NUMCON = SE5.E5_CONTA AND "
		cQuery		+= "(SE5.E5_ORIGEM IN ('FINA171 ', 'FINA181 ') OR "
		cQuery		+= "(SE5.E5_CODORCA <> '' AND SE5.E5_IDMOVI <> '' AND SE5.E5_PREFIXO = '' AND SE5.E5_NUMERO = '' )) AND " // ALTERAÇÃ PARA FILTRAR OS MOVIMENTOS BANCARIOS FINA100
		cQuery		+= "SE5.E5_SITUACA NOT IN ('C' , 'X', 'E') AND "
						//Exclui os titulos que possuem estorno
		cQuery	 	+= "SE5.E5_SEQ NOT IN "
		cQuery 		+= "(SELECT SE5AUX.E5_SEQ FROM "+RetSqlName("SE5")+" SE5AUX WHERE "
		cQuery		+= 		" SE5AUX.E5_DOCUMEN = SE5.E5_DOCUMEN AND "
		cQuery		+= 		" SE5AUX.E5_TIPO = SE5.E5_TIPO AND "
		cQuery		+= 		" SE5AUX.E5_CLIFOR = SE5.E5_CLIFOR AND "
		cQuery		+= 		" SE5AUX.E5_LOJA = SE5.E5_LOJA AND "
		cQuery		+= 		" ((SE5AUX.E5_TIPODOC = 'AP' AND "
		cQuery		+= 		" SE5AUX.E5_RECPAG = 'R') OR(SE5AUX.E5_TIPODOC = 'EP' AND "
		cQuery		+= 		" SE5AUX.E5_RECPAG = 'P')) AND"
		cQuery		+= 		" SE5AUX.D_E_L_E_T_ = '' "
		cQuery 		+= ") AND "
		cQuery		+= "SE5.D_E_L_E_T_ = '' AND SED.D_E_L_E_T_ = '' AND SA6.D_E_L_E_T_ = '' "

	Endif

Else
	If cAlias == "SE1"

		If lSpdDtEm2
			cDtEmis := ExecBlock ("SPDF1006",.F.,.F.)
		Endif

		If !(ValType(cDtEmis) == "C") .Or. Empty(cDtEmis)
    	    cDtEmis	:=	"E1_EMIS1"
        Endif

		cAliasQry	:= "SE1QRY"
		aStruct 	:= Iif( __aSE1Stru==Nil , (__aSE1Stru:=SE1->(dbStruct())) , __aSE1Stru )

		If lIntPFS  //Integração PFS Juridico
			nTamDoc	:= SF2->(TamSx3("F2_DOC")[1])
			cQuery := " SELECT X.* From ("
			nTamFil    := TamSX3("NXA_FILIAL")[1]
			nTamEsc    := TamSX3("NXA_CESCR")[1]
			cTamFilial := cValToChar(nTamFil)
			cIniEscr   := cValToChar(nTamFil+2)
			cTamEscr   := cValToChar(nTamEsc)
			cIniFatur  := cValToChar(nTamFil+1+nTamEsc+2)
			cTamFatur  := cValToChar(TamSX3("NXA_COD")[1])

		Else
			cQuery := ""
		EndIf
		cQuery	+= "SELECT SE1." + Alltrim(cDtEmis) +  " EMISSAO, SE1.E1_VALOR VALOR, SE1.E1_ORIGEM ORIGEM, SE1.E1_CLIENTE CLIFOR, SE1.E1_TIPO TIPO, "
		cQuery   += "SE1.E1_NUM NUM, SE1.E1_PREFIXO PREFIXO, SE1.E1_LOJA LOJA, SE1.E1_PARCELA PARCELA, SE1.E1_MULTNAT MULTNAT, "
		cQuery   += "SE1.E1_DESDOBR DESDOBR, SE1.E1_FILIAL FILIAL, SE1.E1_FILORIG FILORIG, SE1.E1_NATUREZ NATUREZ, "
		cQuery   += "SE1.E1_VLCRUZ VLCRUZ, SE1.E1_MOEDA MOEDA, SE1.E1_BASEIRF BASEPIS, SE1.E1_TXMOEDA TXMOEDA, SE1.E1_DECRESC DECRESC, SE1.E1_SERIE SERIE, "

		If !lNRastDSD  //	.And. !lEnvFatCr
			cQuery   += "SE1.E1_FATPREF FATPREF, SE1.E1_FATURA FATURA, SE1.E1_TIPOFAT TIPOFAT,  "
		Endif

		cQuery   += "SE1.E1_NUMPRO NUMPRO, SE1.E1_INDPRO INDPRO,"
		cQuery   += "SE1.E1_SCORGP SCORGP, "

		If lDescDoc
			cQuery   += "SE1.E1_HIST HIST, "
		Endif

		cQuery   += "SE1.E1_TPDESC TPDESC, "

		cQuery   += "SA1.A1_TIPO TIPOA1, "

	   If lIntPFS  //Integração PFS Juridico
			cQuery += "SF2.F2_DOC, NXA.NXA_SITUAC, "
	   EndIf

	ElseIf cAlias == "SE2"

		If lSpdDtEm
			cDtEmis := ExecBlock ("SPDF1001",.F.,.F.)
		Endif

		If !(ValType(cDtEmis) == "C") .Or. Empty(cDtEmis)
    	    cDtEmis	:=	"E2_EMIS1"
      	EndIf

		cAliasQry	:= "SE2QRY"
		aStruct 	:= Iif( __aSE2Stru==Nil , (__aSE2Stru:=SE2->(dbStruct())) , __aSE2Stru )

		cQuery	:= "SELECT SE2." + Alltrim(cDtEmis) + " EMISSAO, SE2.E2_VALOR VALOR, SE2.E2_ORIGEM ORIGEM, SE2.E2_FORNECE CLIFOR, SE2.E2_TIPO TIPO, "
		cQuery   += "SE2.E2_NUM NUM, SE2.E2_PREFIXO PREFIXO, SE2.E2_LOJA LOJA, SE2.E2_PARCELA PARCELA, SE2.E2_MULTNAT MULTNAT, SE2.E2_VENCTO VENCTO, "
		cQuery   += "SE2.E2_DESDOBR DESDOBR, SE2.E2_FILIAL FILIAL, SE2.E2_FILORIG FILORIG, SE2.E2_NATUREZ NATUREZ, SE2.E2_TXMOEDA TXMOEDA, "
		cQuery   += "SE2.E2_VLCRUZ VLCRUZ, SE2.E2_MOEDA MOEDA, SE2.E2_BASEPIS BASEPIS, SE2.E2_STATUS STATUS, SE2.E2_BASEIRF BASEIRF, "
		cQuery   += "SE2.E2_VRETPIS VRETPIS, SE2.E2_VRETCOF VRETCOF, SE2.E2_VRETCSL VRETCSL, SE2.E2_VRETINS VRETINS, SE2.E2_VRETISS VRETISS, SE2.E2_VRETIRF VRETIRF,SE2.E2_FATURA FATURA, "
		cQuery   += "SE2.E2_FATFOR FATFOR, SE2.E2_FATLOJ FATLOJ, SE2.E2_TIPOFAT TIPOFAT, SE2.E2_FATPREF FATPREF, SE2.E2_DECRESC DECRESC, "
		cQuery   += "SE2.E2_NUMPRO NUMPRO, SE2.E2_INDPRO INDPRO, SE2.E2_SALDO SALDO, SE2.E2_BAIXA BAIXA,"
   	   	If lDescDoc
			cQuery   += "SE2.E2_HIST HIST, "
		Endif
		If lDescSe2
			cQuery   += "SE2.E2_TPDESC TPDESC, "
		EndIf

	EndIf

	cQuery 		+= "SED.ED_APURPIS APURPIS, SED.ED_APURCOF APURCOF, SED.ED_PCAPPIS PCAPPIS,SED.ED_PCAPCOF PCAPCOF, "
	cQuery 		+= "SED.ED_CONTA EDCONTA, SED.ED_CCD EDCCD, SED.ED_CCC EDCCC, "

	If lEdF100
		cQuery	+= "SED.ED_F100 ED_F100, "
	EndIf

	If lEdCprb
		cQuery	+= "SED.ED_CPRB CPRB, "
	Else
		cQuery	+= "'1' CPRB, "
	EndIf

	cQuery 		+= "SED.ED_TABCCZ TABCCZ, SED.ED_CODCCZ CODCCZ, SED.ED_GRUCCZ GRUCCZ,SED.ED_DTFCCZ DTFCCZ, "
	cQuery		+= "SED.ED_CSTPIS CSTPIS, " //CST PIS
	cQuery		+= "SED.ED_CSTCOF CSTCOF, " //CST COFINS
	cQuery	+= "SED.ED_CLASFIS CLASFIS, " //Classificação Financeira
	cQuery	+= "SED.ED_INDCMLT INDCMLT, " //Indicador de Cumulatividade

	If cAlias == "SE1"
		cQuery		+= "SE1.R_E_C_N_O_ RECNO FROM "
		cQuery 		+= RetSqlName(cAlias) + " SE1 "

		If lIntPFS  //Integração PFS Juridico
			//Busca as Faturas com vínculo de NF
			cQuery +=      " LEFT JOIN " + RetSqlName( "NXA" ) + " NXA "
			cQuery +=           " ON ( NXA.NXA_FILIAL = '" + xFilial( "NXA" ) + "' "
			cQuery +=            " AND NXA.NXA_FILIAL = SUBSTRING(E1_JURFAT, 1, " + cTamFilial + ") "
			cQuery +=            " AND NXA.NXA_CESCR = SUBSTRING(E1_JURFAT, " + cIniEscr + ", " + cTamEscr + ") "
			cQuery +=            " AND NXA.NXA_COD = SUBSTRING(E1_JURFAT, " + cIniFatur + ", " + cTamFatur + ") "
			cQuery +=            " AND NXA.NXA_SITUAC = '1' "
			cQuery +=            " AND NXA.D_E_L_E_T_ = ' ' ) "

			cQuery +=      " LEFT JOIN " + RetSqlName( "SF2" ) + " SF2 "
			cQuery +=           " ON ( SF2.F2_FILIAL = '" + xFilial( "SF2" ) + "' "
  			cQuery 		+=     " AND NXA.NXA_DOC = SF2.F2_DOC "
			cQuery 		+=      "AND NXA.NXA_SERIE = SF2.F2_SERIE	" 
			cQuery 		+=      "AND SF2.D_E_L_E_T_  = ' ' )			
		EndIf
		cQuery 		+= ", " + RetSqlName("SA1") + " SA1, "
	ElseIf cAlias == "SE2"
		cQuery		+= "SE2.R_E_C_N_O_ RECNO FROM "
		cQuery 		+= RetSqlName(cAlias) + " SE2, "
		cQuery 		+= RetSqlName("SA2") + " SA2, "
	EndIf

	cQuery 		+= RetSqlName("SED") + " SED  "
	cQuery 		+= "WHERE "

	If __lTemMR
		If cAlias == "SE1"
			cQuery += " SE1.E1_TIPO NOT IN ("
		ElseIf cAlias == "SE2"
			cQuery += " SE2.E2_TIPO NOT IN ("
		EndIf

		cQuery += "SELECT DISTINCT CASE "
		cQuery += "WHEN FKM.FKM_FATGER = '1' "
		cQuery += "THEN FKM.FKM_TIPOEM "
		cQuery += "ELSE "
		cQuery += "FKM.FKM_TIPOBX "
		cQuery += "END FKM_TIPOS "
		cQuery += "FROM " + RetSqlName("FKM") + " FKM "
		cQuery += "JOIN " + RetSqlName("FOI") + " FOI ON FOI.FOI_FILIAL = FKM.FKM_FILIAL AND FOI.FOI_CODIGO = FKM.FKM_CODIGO "
		cQuery += "WHERE FOI.D_E_L_E_T_ = ' ' AND FKM.D_E_L_E_T_ = ' ' "
		cQuery += ") AND "
	EndIf

	If cAlias <> "SE2"
		cQuery 		+= "(SED.ED_APURCOF <> '' OR SED.ED_APURPIS <> '') AND "
	EndIf

	If cAlias == "SE1"

		cTipoTit		:=	""
		cTipoTit		:=		MVTAXA + "|" + MVPROVIS + "|" + MVABATIM + "|" + "PR|PRE"
		cQuery 		+= "SE1.E1_TIPO NOT IN " + FormatIn(cTipoTit,If("|"$cTipoTit,"|",",") ) + " AND "

		If !Empty(cTpSel)
			cQuery 	+= "SE1.E1_TIPO IN (" + cTpSel + ") AND " //Tipos para selecao e envio no bloco F100
		Endif

		cQuery 		+= "SE1.E1_ORIGEM NOT IN ('FINA460 ','FINA280 ') AND " //Fatura ou Liquidacao

		cQuery 	+= "SED.ED_CODIGO = SE1.E1_NATUREZ AND "
		cQuery 	+=	"SED.ED_FILIAL='" + xFilial("SED") + "' AND "
		cQuery 	+=	"SA1.A1_FILIAL='" + xFilial("SA1") + "' AND "

		If !Empty( Iif( lUnidNeg, FWFilial("SE1") , xFilial("SE1") ) )
			cQuery 	+= "SE1.E1_FILIAL = '"  + xFilial("SE1") + "' AND "
		Else
			cQuery 	+= "SE1.E1_FILORIG = '" + cFilSe + "' AND "
		EndIf

		If !lNRastDSD
			cQuery	+= "((SE1.E1_DESDOBR = '1' AND SE1.E1_PARCELA <> '' ) OR "
			cQuery	+= " SE1.E1_DESDOBR <> '1') AND  "
		Endif

		If !Empty(cNOrigem)
			cQuery 	+= "SE1.E1_ORIGEM NOT IN ("  + cNOrigem + ") AND " //Origens que nao devem ser selecionadas - ponto entrada SPDF1005.
		Endif

		cQuery	+= "(SE1." + Alltrim(cDtEmis) + " BETWEEN '"+cDataIni+"' AND '"+cDataFim+ "') AND "
		cQuery 	+= "SA1.A1_COD = SE1.E1_CLIENTE AND "
		cQuery 	+= "SA1.A1_LOJA = SE1.E1_LOJA AND "
		cQuery 	+= "SA1.D_E_L_E_T_ = '' AND "
		cQuery 	+= "SE1.D_E_L_E_T_ = '' AND "
		cQuery 	+= "SED.D_E_L_E_T_ = '' "

		If lIntPFS  //Integração PFS Juridico
			cQuery += " )X WHERE (X.F2_DOC IS NULL OR X.F2_DOC = '"+space(nTamDoc) + "') AND "
			cQuery += " (X.NXA_SITUAC = '1' OR X.NXA_SITUAC IS NOT NULL) "
		EndIf

	ElseIf cAlias == "SE2"

		cTipoTit		:=	""
		cTipoTit		:=	MVTAXA + "|" + MVPROVIS + "|" + MVABATIM + "|" + "PR|PRE"
		cQuery 		+= "SE2.E2_TIPO NOT IN " + FormatIn(cTipoTit,If("|"$cTipoTit,"|",",") ) + " AND "

		If !Empty(cTpSel)
      		cQuery 	+= "SE2.E2_TIPO IN (" + cTpSel+ ") AND " //Tipos para selecao e envio do bloco F100
		Endif

		cQuery 	+= "SED.ED_CODIGO = SE2.E2_NATUREZ AND "
		cQuery 	+=	"SED.ED_FILIAL='" + xFilial("SED") + "' AND "
		cQuery 	+=	"SA2.A2_FILIAL='" + xFilial("SA2") + "' AND "

		If !Empty( Iif( lUnidNeg, FWFilial("SE2") , xFilial("SE2") ) )
			cQuery 	+= "SE2.E2_FILIAL = '"  + xFilial("SE2") + "' AND "
		Else
			If lSe2MsFil
				cQuery 	+= "SE2.E2_MSFIL = '" + cFilSe + "' AND "
			Else
				cQuery 	+= "SE2.E2_FILORIG = '" + cFilSe + "' AND "
			Endif
		EndIf

		cQuery		+= " ( SE2.E2_DESDOBR <> 'S' OR "
		cQuery	+= " (SE2.E2_DESDOBR = 'S' AND SE2.E2_STATUS = '')) AND "

		If !Empty(cNOrigem)
			cQuery 	+= "SE2.E2_ORIGEM NOT IN ("  + cNOrigem + ") AND " //Origens que nao devem ser selecionadas - ponto entrada SPDF1005.
		Endif

		cQuery 		+= " SE2.E2_ORIGEM NOT IN ('FINA565 ','FINA290 ') AND " 	//Fatura ou Liquidacao

		If !Empty(cTpNat) .And. cDtEmis	=	"E2_BAIXA"
			cQuery	+= "(((SE2." + cDtEmis + " BETWEEN '" + cDataIni + "' AND '" +cDataFim+ "') AND SE2.E2_NATUREZ NOT IN (" + cTpNat + "))  OR "
			cQuery	+= "((SE2.E2_VENCTO  BETWEEN '" + cDataIni + "' AND '"+cDataFim+ "') AND SE2.E2_NATUREZ IN (" + cTpNat + ") ))  AND "
		Else
			cQuery	+= "((SE2." + cDtEmis + " BETWEEN '"+cDataIni+"' AND '" +cDataFim+ "' AND SED.ED_CLASFIS <> '08' ) OR "
			cQuery	+= "(SE2.E2_BAIXA BETWEEN '"+cDataIni+"' AND '" +cDataFim+ "' AND SE2.E2_SALDO = 0 AND SED.ED_CLASFIS = '08')) AND "
		Endif

		cQuery 	+= "SA2.A2_COD = SE2.E2_FORNECE AND "
		cQuery 	+= "SA2.A2_LOJA = SE2.E2_LOJA AND "
		cQuery 	+= "SA2.D_E_L_E_T_ = '' AND "
		cQuery 	+= "SE2.D_E_L_E_T_ = '' AND "
		cQuery 	+= "SED.D_E_L_E_T_ = ''"
	Endif
Endif

If lF100TQry
	cQuery := ExecBlock("SPDF100Q", .F. , .F. , {cQuery, cAlias})
Endif

cQuery 		:= ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry)

nLen:=(Len(aStruct))
For nX := 1 To nLen
	If aStruct[nX][2] <> "C"
		TcSetField(cAliasQry,aStruct[nX][1],aStruct[nX][2],aStruct[nX][3],aStruct[nX][4])
	EndIf
Next nX

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DTVenIcms ºAutor  ³Marcio Nunes        º Data ³  24/08/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao que retorna a data de vencimento dos impostos       º±±
±±º          ³ FETHAB / FABOB / FACS                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function DTVenIcms(lVctReal)

Local cICMVenc	:= SubStr(SuperGetMV("MV_ICMVENC",,""),1,2)
Local dUltDia	:= UltimoDia(dDataBase)
Local dDatValid := dUltDia+Val(cICMVenc)
Local dDifdt  	:= CTOD("  /  /    ")

DEFAULT lVctReal := .F.

If cICMVenc == ""
	dDtVenc := dDataBase

ElseIf cICMVenc == "06"
	dDtVenc := DataValida(dDatValid,.T.)

ElseIf cICMVenc <> "06" // retorna o dia util anterior a data de vencimento
	If DataValida(dDatValid,.T.) <> dDatValid
		dDifdt	:= DataValida(dDatValid,.T.) - dDatValid
		dDtVenc := DataValida(dDatValid,.T.)-(dDifdt+1)
	Else
		dDtVenc := dDatValid
	EndIf
EndIf

If lVctReal
	dDtVenc := dDatValid
EndIf

Return (dDtVenc)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³VldFilOrigºAutor  ³Caio Cesar Felipe   º Data ³  26/02/2013 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida se a filial informada é válida                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function VldFilOrig(cFilAux)
Local lRet 		:= .F.
Local aArea		:= GetArea()

lRet := FWFilExist(cEmpAnt,cFilAux)

If !lRet
	MsgAlert("A Filial informada no campo Filial Orig. não é válida.")
EndIf

RestArea( aArea )

Return( lRet )
