#INCLUDE "protheus.ch"
#INCLUDE "atfa110.ch"

#DEFINE APTO_SIMP	31		//Apontamentos simples
#DEFINE APTO_MULTI	32		//Apontamentos multiplos
#DEFINE EST_MULTI	41		//Estornos multiplos
#DEFINE HIST_APTO	90		//HistÛrico

Static lFWCodFil := .T.
Static lMultMoed := .T.
Static _oATFA1101

STATIC lIsRussia	:= If(cPaisLoc$"RUS",.T.,.F.) // CAZARINI - Flag to indicate if is Russia location

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥ATFA110   ∫Autor  ≥Renan Guedes        ∫ Data ≥  05/19/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥DepreciaÁ„o por exaust„o (agotamiento)                      ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                         ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function ATFA110(nPosRot,lAutomato)

Local cTypes10	:= IIF(lIsRussia,"|" + AtfNValMod({1}, "|"),"") // CAZARINI - 24/03/2017 - If is Russia, add new valuations models - main models
Local cTypesNM	:= IIF(lIsRussia,"|" + AtfNValMod({3,4}, "|"),"") // CAZARINI - 24/03/2017 - If is Russia, add new valuations models - 17 and 16 models
Local lAtfctap	:= IIF(GetNewPar("MV_ATFCTAP","0")=="0",.F.,.T.)
Local cFiltra		:= ""
Local aCposRot	:= {}

Private aRotina		:= MenuDef("SN3")
Private cCadastro	:= STR0007		//"Apontamentos de produÁ„o"
Private aIndexSN3 	:= {}
Private bFiltraBrw	:= {|| Nil}
Private lMostra
Private lAglutina
Private cMoedaAtf	:= GetMV("MV_ATFMOEDA")

Default lAutomato := .F.
Default nPosRot   := 0

//Ativa tecla F12 de par‚metros de lanÁamento cont·bil
SetKey( VK_F12, { || Pergunte("AFA110CONT",.T.) } )

Pergunte("AFA110CONT",.F.)
lMostra := IIF(MV_PAR01 == 1, .T., .F.)
lAglutina := IIF(MV_PAR02 == 1, .T., .F.)

If lIsRussia
	cFiltra := "N3_FILIAL == '"+xFilial('SN3')+"' .And. N3_TIPO $ ('01|10|16|17'+cTypes10+cTypesNM) .And. N3_BAIXA == '0' .And. N3_TPDEPR $ '4|5|8|9|'"
ElseIf cPaisLoc == "BOL"
	cFiltra := "N3_FILIAL == '"+xFilial('SN3')+"' .And. N3_TIPO $ '01|02|05|10|16|17' .And. N3_BAIXA == '0' .And. N3_TPDEPR $ '4|5|8|9|'"
Else
	cFiltra := "N3_FILIAL == '"+xFilial('SN3')+"' .And. N3_TIPO $ '01|10|16|17' .And. N3_BAIXA == '0' .And. N3_TPDEPR $ '4|5|8|9|'"
EndIf

bFiltraBrw := { || FilBrowse("SN3",@aIndexSN3,@cFiltra) }

//Verifica se a rotina est· parametrizada para ser utilizada
If !lAtfctap
	Help("",1,"AF110CTAP")		//"Esta rotina n„o poder· ser utilizada, pois o sistema est· parametrizado para n„o controlar apontamentos de produÁ„o."##"Configure o par‚metro MV_ATFCTAP."
	Return
EndIf

//Inclui os novos lanÁamentos padrıes da depreciaÁ„o por exaust„o
ChkCVA()

If Select("TRB") != 0
	dbSelectArea("TRB")
	dbCloseArea()
EndIf

dbSelectArea("SN3")
dbSetOrder(1)		//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ

//Aplica o filtro no alias SN3
Eval(bFiltraBrw)
//Ativa o filtro
SN3->(dbGoTop())

If !lAutomato
	mBrowse(6, 1, 22, 75, "SN3")
Endif

EndFilBrw("SN3",aIndexSN3)

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥MenuDef   ∫Autor  ≥Renan Guedes        ∫ Data ≥  05/19/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥UtilizaÁ„o de menu funcional.                               ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function MenuDef(cAlias)

Local aRotina	:= {}

Default cAlias	:= "SN3"

//Botıes da rotina de apontamentos
If cAlias == "SN3"
	aRotina	:= {{STR0001	, "AxPesqui"	,0 , 1, 			,.F.},;		//"Pesquisar"
				{STR0002	, "AxVisual"	,0 , 2					},;		//"Visualizar"
				{STR0003	, "AFA110Apt"	,0 , 3, APTO_SIMP		},;		//"Apontamento"
				{STR0005	, "AFA110Apt"	,0 , 3, APTO_MULTI		},;		//"Apt. multiplos"
				{STR0016	, "AFA110Hist"	,0 , 2, HIST_APTO		}}		//"HistÛrico"
EndIf

//Botıes da rotina de histÛrico de apontamentos
If cAlias == "FNA"
	aRotina	:= {{STR0001	, "AxPesqui"	,0 , 1, 			,.F.},;		//"Pesquisar"
				{STR0002	, "AxVisual"	,0 , 2					},;		//"Visualizar"
				{STR0017	, "AF110Leg"	,0 , 5					}}		//"Legenda"
EndIf

Return(aRotina)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AFA110Apt ∫Autor  ≥Renan Guedes        ∫ Data ≥  05/19/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Apontamentos de produÁ„o                                    ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AFA110Apt(cAlias,nReg,nOpc)

Local nOpcX 		:= aRotina[nOpc,5]
Local dDataBloq 	:= GetNewPar("MV_ATFBLQM", CTOD(""))
Local dUltDepr		:= GetNewPar("MV_ULTDEPR", STOD("19800101"))
Local nOpcA			:= 0
Local nOpcB			:= 0
Local nRecno		:= 1
Local cN3Ok			:= "N3_OK"
Local cMarca		:= ""
Local aCpos			:= {}
Local lRet			:= .T.
Local lGravou		:= .F.
Local aAcho			:= {}
Local aPosObj   	:= {}
Local aObjects  	:= {}
Local aSize     	:= MsAdvSize()
Local cOcor			:= ""
Local lEstorno		:= .F.
Local aRetP1		:= {}
Local lDefTop		:= IfDefTopCTB() // verificar se pode executar query (TOPCONN)
Local oEncSimp		:= Nil
Local oEncMult		:= Nil

Private oDlgApt
Private nQtdMark	:= 0

If SN3->(EoF()) .And. SN3->(BoF())
	Help("",1,"AF110NOREC")		//"N„o h· registros na tabela de dados."##"Os apontamentos sÛ podem ser realizados para bens cadastrados."
	Return
EndIf

//Apontamento simples
If nOpcX == APTO_SIMP
	aObjects := {}
	AADD(aObjects, {100,100, .T., .T.})
	AADD(aObjects, {100,100, .T., .T.})

	aInfo := {aSize[1], aSize[2], aSize[3]*2, aSize[4]*2, 3, 3}
	aPosObj := MsObjSize(aInfo, aObjects)

	//Monta o array com os campos a serem exibidos no apontamento simples
	aAcho := {"FNA_IDMOV","FNA_ITMOV","FNA_CBASE","FNA_ITEM","FNA_DESCRI","FNA_DATA","FNA_OCORR","FNA_DTPERI","FNA_DTPERF","FNA_QUANTD"}

	//Montagem da tela de apontamentos simples
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[1],aSize[1] To aSize[6],aSize[5] of oMainWnd PIXEL
		oDlg:lMaximized := .T.

		RegToMemory("FNA", .T., .T., .T.)

		//Inicializa os campos do cadastro
		AF110IniBrw()

		//Enchoice("FNA", nReg, nOpc,,,, aAcho, aPosObj[1], aAcho, 3,,, /*cTudoOk*/, oDlg)
		oEncSimp:= MsMGet():New("FNA",nReg,nOpc,,,,aAcho,{0,0,0,0},,,,,,oDlg)
		oEncSimp:oBox:Align	:= CONTROL_ALIGN_ALLCLIENT
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| IIF(AF110TudOk(.F.),(nOpcA := 1, oDlg:End()),) },{|| nOpcA := 2,oDlg:End() }) CENTERED

	If nOpcA == 1
		ConfirmSX8()
		AF110Grv(,,,.F.)
	Else
		RollBackSX8()
	EndIf

//Apontamentos multiplos
ElseIf nOpcX == APTO_MULTI
	If !lDefTop
		Help("",1,"AF110NOTOP")		//"Rotina disponÌvel apenas para ambientes TopConnect / byYou DBAccess."
		Return
	EndIf

	aObjects := {}
	AADD(aObjects, {100,100, .T., .T.})
	AADD(aObjects, {100,100, .T., .T.})

	aInfo := {aSize[1], aSize[2], aSize[3]*2, aSize[4]*2, 3, 3}
	aPosObj := MsObjSize(aInfo, aObjects)

	//Monta o array com os campos a serem exibidos no apontamento multiplos
	aAcho := {"FNA_DATA","FNA_OCORR","FNA_DTPERI","FNA_DTPERF","FNA_QUANTD"}

	//Montagem da tela de apontamentos multiplos
	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[1],aSize[1] To aSize[6],aSize[5] of oMainWnd PIXEL
		oDlg:lMaximized := .T.

		RegToMemory("FNA", .T., .T., .T.)

		//Enchoice("FNA", nReg, nOpc,,,, aAcho, aPosObj[1], aAcho, 3,,, /*cTudoOk*/, oDlg)
		oEncMult := MsMGet():New("FNA",nReg,nOpc,,,,aAcho,{0,0,0,0},,,,,,oDlg)
		oEncMult:oBox:Align	:= CONTROL_ALIGN_ALLCLIENT
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| IIF(AF110TudOk(.T.),(nOpcA := 1, oDlg:End()),) },{|| nOpcA := 2,oDlg:End() }) CENTERED

	If nOpcA == 1
		cOcor := M->FNA_OCORR
		aRetP1 := {}

		If cOcor $ "P8|P9|"
			lRetPar := AFA110ParBox(@aRetP1,DTOS(M->FNA_DATA))
			If !lRetPar
				Return
			EndIf
			lEstorno := .T.
		Else
			Pergunte("AFA110", .T.)
			lEstorno := .F.
		EndIf

		//Cria arquivo tempor·rio com os registros a partir do range dos par‚metros
		aCpos := AF110Trb(cOcor,lEstorno,aRetP1)

		If Select("TRB") > 0
			dbSelectArea("TRB")
			dbGoTop()
		Else
			Help("",1,"AF110NOTRB")		//"N„o foram encontrados bens de acordo com os par‚metros informados."##"Preencha corretamente as perguntas para filtrar os bens desejados."
			Return
		EndIf

		//Captura a marca corrente
		cMarca := GetMark( , "TRB", cN3Ok)

		//Browse de marcaÁ„o para inclus„o dos apontamentos multiplos
		DEFINE MSDIALOG oDlgApt TITLE cCadastro From aSize[1],aSize[1] To aSize[6],aSize[5] OF oMainWnd PIXEL
			oDlgApt:lMaximized := .T.

			oMark := MsSelect():New("TRB",cN3Ok,, aCpos,.F.,@cMarca,{50,oDlgApt:nLeft,oDlgApt:nBottom,oDlgApt:nRight})
			oMark:bMark := {|| AF110Inv(cMarca,.F.,oMark)}
			oMark:bAval	:= {|| AF110Inv(cMarca,.F.,oMark)}
			oMark:oBrowse:lHasMark := .T.
			//oMark:oBrowse:lCanAllmark := .T.
			oMark:oBrowse:bAllMark := {|| AF110Inv(cMarca,.T.,oMark)}
			oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT
		ACTIVATE MSDIALOG oDlgApt ON INIT EnchoiceBar( oDlgApt, {|| IIF(nQtdMark > 0, (nOpcB := 1, oDlgApt:End()), Help(" ",1,"RECNO"))}, {|| nOpcB := 2, oDlgApt:End()}, .F., /*aButtons*/, /*nRecno*/, /*cAlias*/)

		If nOpcB == 1
			nRecno := FNA->(Recno())

			Begin Transaction

			AF110Grv(nRecno,cMarca,cN3Ok,.T.)
			ConfirmSX8()

			End Transaction
		Else
			RollBackSX8()
		EndIf

	Else
		RollBackSX8()
	EndIf

	If Select("TRB") > 0
		dbSelectArea("TRB")
		dbCloseArea()
	EndIf
	
	If _oATFA1101 <> Nil
		_oATFA1101:Delete()
		_oATFA1101 := Nil
	Endif
	
EndIf

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110TudOk   ∫Autor  ≥Renan Guedes     ∫ Data ≥  05/20/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ValidaÁ„o Tudo Ok da tela de apontamentos de produÁ„o       ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110TudOk(lMulti)

Local lRet		:= .T.
Local aArea		:= {}
Local aAreaSN1	:= {}
Local aAreaSN3	:= {}
Local aAreaFNA	:= {}
Local dDataBloq	:= GetNewPar("MV_ATFBLQM", CTOD(""))
Local dUltDepr	:= GetNewPar("MV_ULTDEPR", STOD("19800101"))
Local lTemRev	:= .F.
Local lTemApto	:= .F.
Local cCpobco	:= ""

Default lMulti	:= .F.

aArea := GetArea()

dbSelectArea("SX3")
dbSetOrder(2)		//X3_CAMPO
dbGoTop()

dbSelectArea("SN1")
aAreaSN1 := SN1->(GetArea())
dbSetOrder(1)		//N1_FILIAL+N1_CBASE+N1_ITEM
dbGoTop()

dbSelectArea("SN3")
aAreaSN3 := SN3->(GetArea())
dbSetOrder(1)		//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
dbGoTop()

dbSelectArea("FNA")
aAreaFNA := FNA->(GetArea())
dbSetOrder(2)		//FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)
dbGoTop()

If !Empty(M->FNA_DATA) .And. (M->FNA_DATA <= dDataBloq)
	HELP(" ",1,"AF110BLQM",,STR0029 + DTOC(dDataBloq) ,1,0)		//"A data de movimentaÁ„o do apontamento È menor do que a data de bloqueio de movimentaÁıes:" ## "A data de registro de apontamentos deve ser maior do que a data de bloqueio de movimentaÁıes."
	lRet := .F.
EndIf

//Validacao para o bloquei do proceco
If lRet .And. !CtbValiDt(,M->FNA_DATA  ,,,,{"ATF001"},)
	lRet := .F.
EndIf  

If lRet .and. !Empty(M->FNA_DATA)
	If M->FNA_DATA <= dUltDepr
		Help(" ",1,"A110DEPR1",,STR0030 + DTOC(dUltDepr),1,0)		//"A data de movimentaÁ„o do apontamento È menor do que a data do ˙ltimo c·lculo de depreciaÁ„o:" ##
		lRet := .F.
	ElseIf M->FNA_DATA > LastDay(dUltDepr+1)
		Help(" ",1,"A110DEPR1",,STR0034 + CRLF + STR0035 + DTOC(LastDay(dUltDepr+1)),1,0)		//"O apontamento somente poder· ser realizado no mÍs imediatamente posterior ao ˙ltimo c·lculo de depreciaÁ„o."###"Data limite: "
		lRet := .F.
	Endif
EndIf

If lRet .And. !lMulti
	If SN1->(MsSeek(xFilial("SN1")+M->FNA_CBASE+M->FNA_ITEM))
		If SN1->N1_STATUS != "1"
			lRet := .F.
			Help("",1,"AF110ATFBL")		//"Este bem est· bloqueado."##"Somente bens desbloqueados podem ser movimentados."
		ElseIf !Empty(SN1->N1_DTBLOQ) .AND. (M->FNA_DATA <= SN1->N1_DTBLOQ)
			lRet := .F.
			Help(" ",1,"AF110DTBLQ",, STR0041, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0042})//"Este bem est· bloqueado atravÈs do campo N1_DTBLOQ."####"Somente bens desbloqueados podem ser movimentados."
		EndIf
	EndIf
EndIf

If lRet .And. !lMulti .And. !(M->FNA_OCORR $ "P8|P9|")
	If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) ))
		While FNA->(!EoF()) .And. (FNA->FNA_FILIAL == xFilial("FNA"))	;
			.And. FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA))	;
			== M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA))
			If !(FNA->FNA_OCORR $ "P0|P5|P8|P9|") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				Help("",1,"AF110EXAPT")		//"Existe um apontamento gravado para a mesma data."##"N„o È possÌvel realizar mais de um apontamento para uma mesma data."
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
	EndIf
EndIf

If cPaisLoc=="BOL"
	IF MOVSBOLN3(M->FNA_CBASE,M->FNA_ITEM,SN3->N3_TPDEPR,DTOS(FNA_DATA))[1]
		lRet := .F.
		Help("",1,"AF110EXAPT")
	EndIf
EndIf

If lRet .And. lMulti
	If Empty(M->FNA_DATA)
		lRet := .F.
		If SX3->(MsSeek("FNA_DATA"))
			cCpobco += X3Titulo() + "(FNA_DATA)" + CRLF
		EndIf
	EndIf
	If Empty(AllTrim(M->FNA_OCORR))
		lRet := .F.
		If SX3->(MsSeek("FNA_OCORR"))
			cCpobco += X3Titulo() + "(FNA_OCORR)" + CRLF
		EndIf
	EndIf
	If !lRet
		Help("",1,"AF110CPBCO",,STR0028 + cCpobco,1,0)		//"Campo n„o preenchido."##"Preencha o campo informado."
	EndIf
EndIf

If lRet .And. M->FNA_OCORR == "P0"
	lRet := .F.
	Help("",1,"AF110NOP0")		//"A estimativa de produÁ„o n„o pode ser apontada manualmente."##"Para alterar a estimativa de produÁ„o, selecione a ocorrÍncia de revis„o de estimativa de produÁ„o."
EndIf

If lRet .And. M->FNA_OCORR == "P5"
	lRet := .F.
	Help("",1,"AF110NOP5")		//"A produÁ„o acumulada n„o pode ser apontada manualmente."##"ProduÁıes acumuladas s„o apontadas automaticamente na inclus„o de bens."
EndIf

If lRet .And. M->FNA_OCORR $ "P1|P2|"
	If M->FNA_QUANTD == 0
		lRet := .F.
		Help("",1,"AF110QTD0")		//"A quantidade apontada n„o pode ser igual a 0 (zero)."##"Informe a quantidade do apontamento."
	EndIf
EndIf

If lRet .And. M->FNA_OCORR $ "P2|P3|"
	If Empty(M->FNA_DTPERI) .Or. Empty(M->FNA_DTPERF)
		lRet := .F.
		Help("",1,"AF110NOPER")		//"O perÌodo inicial e/ou o perÌodo final n„o foi informado."##"Informe os perÌodos inicial e final do apontamento."
	Else
		If (M->FNA_DTPERF > LastDay(M->FNA_DTPERI)) .Or. (M->FNA_DTPERF < M->FNA_DTPERI)
			lRet := .F.
			Help("",1,"AF110PERF")		//"Os perÌodos inicial e final do apontamento devem compreender ao mesmo mÍs."##"Informe um perÌodo final maior ou igual ao inicial e menor ou igual ao ˙ltimo dia do mesmo mÍs."
		EndIf
	EndIf
EndIf

If lRet .And. !lMulti .And. M->FNA_OCORR $ "P2|P3|"
	If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) ))
		While FNA->(!EoF()) .And. (FNA->FNA_FILIAL == xFilial("FNA")) .And. (FNA->FNA_CBASE == M->FNA_CBASE) .And.;
				(FNA->FNA_ITEM == M->FNA_ITEM) .And. (FNA->FNA_TIPO == M->FNA_TIPO)
			If ((M->FNA_DTPERI >= FNA->FNA_DTPERI .And. M->FNA_DTPERI <= FNA->FNA_DTPERF)		;
				.Or. (M->FNA_DTPERF >= FNA->FNA_DTPERI .And. M->FNA_DTPERF <= FNA->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERI >= M->FNA_DTPERI .And. FNA->FNA_DTPERI <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERF >= M->FNA_DTPERI .And. FNA->FNA_DTPERF <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERI >= M->FNA_DTPERI .And. FNA->FNA_DTPERI <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERF >= M->FNA_DTPERI .And. FNA->FNA_DTPERF <= M->FNA_DTPERF))	;
				.And. !(FNA->FNA_OCORR $ "P0|P1|P5|P8|P9") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				Help("",1,"AF110PEREX")		//"Existe um apontamento gravado para um perÌodo igual ou correspondente."##"Informe outro perÌodo de apontamento ou estorne o apontamento existente."
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
	EndIf
EndIf

If lRet
	If !(M->FNA_OCORR $ "P4|P9|") .And. !lMulti
		If SN3->(MsSeek(xFilial("SN3")+M->FNA_CBASE+M->FNA_ITEM+M->FNA_TIPO+"0"+M->FNA_SEQ))
			If !Empty(SN3->N3_DEXAUST) .Or. !Empty(SN3->N3_FIMDEPR)
				lRet := .F.
				Help("",1,"AF110EX")		//"Este bem j· foi exaurido."##"Bens exauridos n„o podem ser movimentados."
			ElseIf M->FNA_OCORR == "P2" .And. M->FNA_QUANTD + SN3->N3_PRODACM > SN3->N3_PRODANO
				lRet := .F.
				Help("",1,"AF110P2QT") //"A quantidade apontada, somada ‡ produÁ„o acumulada do bem, È maior que o valor de produÁ„o estimada." ## "Informe uma quantidade menor para este apontamento de produÁ„o."
			EndIf
		EndIf
	ElseIf M->FNA_OCORR == "P4" .And. !lMulti
		If SN3->(MsSeek(xFilial("SN3")+M->FNA_CBASE+M->FNA_ITEM+M->FNA_TIPO+"0"+M->FNA_SEQ))
			If Empty(SN3->N3_DEXAUST) .Or. Empty(SN3->N3_FIMDEPR)
				lRet := .F.
				Help("",1,"AF110NOEX")		//"Este bem n„o foi exaurido."##"Apontamentos complementares somente podem ser realizados para bens j· exauridos."
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. !lMulti .And. M->FNA_OCORR == "P8"
	If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) ))
		While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
			.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD))
			//Verifica se h· apontamentos de produÁ„o para o bem
			If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. !(FNA->FNA_OCORR $ "P0|P1|P5|P8|P9|") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				Help("",1,"AF110NOP8")		//"N„o È possÌvel estornar a revis„o de estimativa de produÁ„o de bens apontados."##"Para alterar a estimativa de produÁ„o È necess·rio realizar um apontamento de revis„o de estimativa de produÁ„o."
				Exit
			EndIf
			//Verifica se h· revis„o de estimativa para o bem
			If (FNA->FNA_DATA == M->FNA_DATA) .And. (FNA->FNA_OCORR == "P1") .And. (FNA->FNA_ESTORN != "1")
				lTemRev := .T.
			EndIf
			FNA->(dbSkip())
		EndDo
		//Check e help do estorno da revis„o de estimativa
		If lRet .And. !lTemRev
			lRet := .F.
			Help("",1,"AF110NOREV")		//"N„o existe revis„o de estimativa de produÁ„o a ser estornada para o bem."##"Somente È possÌvel estornar a revis„o de estimativa de produÁ„o para bens que tiveram a estimativa revisada."
		EndIf
		If lRet
			If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD)+DTOS(M->FNA_DATA) ))
				While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
				.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD))
				If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_ESTORN != "1")
					lRet := .F.
					Help("",1,"AF110APPOS")		//"Existe apontamento posterior a data informada."##"… necess·rio estornar os apontamentos posteriores a data informada."
					Exit
				EndIf
				FNA->(dbSkip())
				EndDo
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. !lMulti .And. M->FNA_OCORR == "P9"
	If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) ))
		While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
			.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) == M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)))
			If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_OCORR $ "P2|P3|P4|") .And. (FNA->FNA_ESTORN != "1")
				lTemApto := .T.
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
		If !lTemApto
			lRet := .F.
			Help("",1,"AF110NOAPT")		//"N„o existe apontamento a ser estornado para o bem."##"Somente È possÌvel estornar apontamentos existentes e no mÍs/ano do c·lculo de depreciaÁ„o."
		Else
			If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) ))
				While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
				.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD))
				If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_OCORR == "P3") .And. (FNA->FNA_ESTORN != "1")
					lRet := .F.
					Help("",1,"AF110EXENC")		//"N„o È possÌvel estornar apontamentos anteriores ao encerramento de produÁ„o para bens cuja produÁ„o foi encerrada."##"… necess·rio, primeiramente, estornar o encerramento de produÁ„o na data em que tenha ocorrido, para ent„o estornar os apontamentos anteriores."
					Exit
				EndIf
				FNA->(dbSkip())
				EndDo
			EndIf
			If lRet
				If FNA->(MsSeek( xFilial("FNA") + M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) ))
					While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
					.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD))
					If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_OCORR $ "P2|P4|") .And. (FNA->FNA_ESTORN != "1")
						lRet := .F.
						Help("",1,"AF110APPOS")		//"Existe apontamento posterior a data informada."##"… necess·rio estornar os apontamentos posteriores a data informada."
						Exit
					EndIf
					FNA->(dbSkip())
					EndDo
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

RestArea(aAreaFNA)
RestArea(aAreaSN3)
RestArea(aAreaSN1)
RestArea(aArea)

Return(lRet)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110IniBrw   ∫Autor  ≥Renan Guedes    ∫ Data ≥  05/24/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Funcao executada antes de iniciar o browse, para atualizar  ∫±±
±±∫          ≥os campos com os dados do registro SN3 posicionado.         ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110IniBrw()

Local aArea		:= {}

aArea := GetArea()
M->FNA_IDMOV	:= GetSXENum("FNA", "FNA_IDMOV")
M->FNA_ITMOV	:= StrZero(1,TamSX3("FNA_ITMOV")[1])
M->FNA_CBASE 	:= SN3->N3_CBASE
M->FNA_ITEM		:= SN3->N3_ITEM
M->FNA_TIPO		:= SN3->N3_TIPO
M->FNA_SEQ		:= SN3->N3_SEQ
M->FNA_SEQREA	:= SN3->N3_SEQREAV
M->FNA_TPSALD	:= SN3->N3_TPSALDO
M->FNA_TPDEPR	:= SN3->N3_TPDEPR
RestArea(aArea)

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110Trb   ∫Autor  ≥Renan Guedes       ∫ Data ≥  05/25/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Montagem do arquivo de trabalho para a funÁ„o de 			  ∫±±
±±∫          ≥apontamentos multiplos                                      ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AF110Trb(cOcor,lEstorno,aRetP1)

Local cTrb
Local cIndex
Local cChave		:= "N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ"
Local aArea			:= {}
Local aStru			:= {}
Local aCampos		:= {}
Local cCampos		:= ""
Local cQuery		:= ""
Local nX			:= 0
Local nTamMoeda		:= TamSX3("FNA_MOEDA")[1]
Local cTypes10		:= IIF(lIsRussia,AtfNValMod({1}, "|"),"") // CAZARINI - 24/03/2017 - If is Russia, add new valuations models - main models
Local aTypes10		:= {}
Local nTypes10		:= 0
Local cTypesNM		:= IIF(lIsRussia,AtfNValMod({3,4}, "|"),"") // CAZARINI - 24/03/2017 - If is Russia, add new valuations models - 17 and 16 models
Local aTypesNM		:= {}
Local nTypesNM		:= 0

Default cOcor		:= ""
Default lEstorno	:= .F.
Default aRetP1		:= {{""},{""}}

aArea := GetArea()

dbSelectArea("SX3")
dbSetOrder(2)		//X3_CAMPO
dbGoTop()
If SX3->(dbSeek("N3_OK"))
	AADD(aStru, {AllTrim(SX3->X3_CAMPO), SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL})
	AADD(aCampos, {AllTrim(SX3->X3_CAMPO),, "", AllTrim(SX3->X3_PICTURE)})
EndIf

dbSetOrder(1)		//X3_ARQUIVO+X3_ORDEM
dbGoTop()
If SX3->(dbSeek("SN3"))
	While SX3->(!EoF()) .And. (SX3->X3_ARQUIVO == "SN3")
		If (AllTrim(SX3->X3_CAMPO) $ "N3_FILIAL|N3_CBASE|N3_ITEM|N3_TIPO|N3_BAIXA|N3_SEQ|N3_SEQREAV|N3_FIMDEPR") .Or. (X3Uso(SX3->X3_USADO) .And. (SX3->X3_NIVEL <= cNivel) .And. (X3_TIPO <> "M"))
			AADD(aStru, {AllTrim(SX3->X3_CAMPO), SX3->X3_TIPO, SX3->X3_TAMANHO, SX3->X3_DECIMAL})
			AADD(aCampos, {AllTrim(SX3->X3_CAMPO),, X3Titulo(), AllTrim(SX3->X3_PICTURE)})
		EndIf
		SX3->(dbSkip())
	EndDo
EndIf

dbSelectArea("FNA")
dbSetOrder(1)		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
dbGoTop()

RestArea(aArea)

aEval(aStru,{|aCpoSQL| nX++, cCampos += aCpoSQL[1]+IIF(nX == Len(aStru)," ",", ")})

cQuery += " SELECT "+cCampos+" FROM "+RetSqlName("SN3")+" SN3 "
cQuery += "		INNER JOIN "+RetSqlName("SN1")+" SN1 "
cQuery += "			ON "
cQuery += "				SN1.N1_FILIAL = SN3.N3_FILIAL AND "
cQuery += "				SN1.N1_CBASE = SN3.N3_CBASE AND "
cQuery += "				SN1.N1_ITEM = SN3.N3_ITEM "

If lEstorno
	cQuery += "		INNER JOIN "+RetSqlName("FNA")+" FNA "
	cQuery += "			ON "
	cQuery += "				FNA.FNA_FILIAL = SN3.N3_FILIAL AND "
	cQuery += "				FNA.FNA_CBASE = SN3.N3_CBASE AND "
	cQuery += "				FNA.FNA_ITEM = SN3.N3_ITEM AND "
	cQuery += "				FNA.FNA_TIPO = SN3.N3_TIPO AND "
	cQuery += "				FNA.FNA_SEQ = SN3.N3_SEQ AND "
	cQuery += "				FNA.FNA_SEQREA = SN3.N3_SEQREAV AND "
	cQuery += "				FNA.FNA_TPSALD = SN3.N3_TPSALDO AND "
	cQuery += "				FNA.FNA_TPDEPR = SN3.N3_TPDEPR "
	cQuery += "		WHERE "
	cQuery += "			SN3.D_E_L_E_T_ = ' ' AND "
	cQuery += "			SN1.D_E_L_E_T_ = ' ' AND "
	cQuery += "			FNA.D_E_L_E_T_ = ' ' AND "
	cQuery += "			FNA.FNA_FILIAL = '"+xFilial("FNA",aRetP1[1])+"' AND "
	cQuery += "			FNA.FNA_IDMOV = '"+aRetP1[2]+"' AND "

	If cOcor != 'P8'
		cQuery += "			FNA.FNA_MOEDA = '1 ' AND "
	Endif

	cQuery += "			FNA.FNA_ESTORN = '2' "
Else
	cQuery += "		WHERE "
	cQuery += "			SN3.D_E_L_E_T_ = ' ' AND "
	cQuery += "			SN1.D_E_L_E_T_ = ' ' AND "
	cQuery += "			SN3.N3_FILIAL = '"+xFilial("SN3",mv_par01)+"' AND "
	If !lIsRussia // CAZARINI - Flag to indicate if is Russia location
		cQuery += "			SN3.N3_TIPO IN ('01','10','16','17') AND "
	Else
		aTypes10 := Separa(cTypes10, '|', .f.)
		aTypesNM := Separa(cTypesNM, '|', .f.)
		
		If len(aTypes10) = 0 .AND. len(aTypesNM) = 0
			cQuery += "			SN3.N3_TIPO IN ('01','10','16','17') AND "
		Else
			cQuery += "			SN3.N3_TIPO IN ('01','10','16','17'"
			
			For nTypes10 := 1 to len(aTypes10)
				cQuery += ",'" + aTypes10[nTypes10] + "')"
			Next nTypes10
			
			For nTypesNM := 1 to len(aTypesNM)
				cQuery += ",'" + aTypesNM[nTypesNM] + "')"
			Next nTypesNM
			
			cQuery += "			) AND "
		Endif
	EndIf
	cQuery += "			SN3.N3_BAIXA = '0' AND "
	cQuery += "			SN3.N3_TPDEPR IN ('4','5','8','9') AND "
	cQuery += " 		(SN3.N3_CBASE >= '"+MV_PAR02+"' AND "
	cQuery += " 		SN3.N3_CBASE <= '"+MV_PAR03+"') AND "
	cQuery += "			(SN3.N3_ITEM >= '"+MV_PAR04+"' AND "
	cQuery += "			SN3.N3_ITEM <= '"+MV_PAR05+"') AND "
	cQuery += " 		(SN3.N3_CCONTAB >= '"+MV_PAR06+"' AND "
	cQuery += " 		SN3.N3_CCONTAB <= '"+MV_PAR07+"') AND "
	If cOcor == "P4"
		cQuery += "		SN3.N3_FIMDEPR != '        ' AND "
	Else
		cQuery += "		SN3.N3_FIMDEPR = '        ' AND "
	EndIf
	cQuery += " 		(SN1.N1_GRUPO >= '"+MV_PAR08+"' AND "
	cQuery += " 		SN1.N1_GRUPO <= '"+MV_PAR09+"') "
EndIf
cQuery += "		ORDER BY N3_FILIAL,N3_CBASE,N3_ITEM,N3_TIPO,N3_BAIXA,N3_SEQ"
cQuery := ChangeQuery(cQuery)

If Select("SQL") != 0
	dbSelectArea("SQL")
	dbCloseArea()
EndIf

dbUseArea(.T., "TOPCONN", TCGENQRY(,,cQuery), "SQL", .F.)
dbGoTop()

If SQL->(EoF())
	dbCloseArea()
	Return
EndIf

For nX := 1 to Len(aStru)
	If !(aStru[nX][2] $ "C/M")
		TCSetField("SQL",aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
	EndIf
   		Next nX

If Select("TRB") != 0
	dbSelectArea("TRB")
	dbCloseArea()
EndIf

If _oATFA1101 <> Nil
	_oATFA1101:Delete()
	_oATFA1101 := Nil
Endif

_oATFA1101 := FWTemporaryTable():New( "TRB" )  
_oATFA1101:SetFields(aStru) 
_oATFA1101:AddIndex("1", {"N3_FILIAL","N3_CBASE","N3_ITEM","N3_TIPO","N3_BAIXA","N3_SEQ"})

//------------------
//CriaÁ„o da tabela temporaria
//------------------
_oATFA1101:Create()  

dbSelectArea("SQL")
dbGoTop()
While SQL->(!EoF())
	RecLock("TRB",.T.)
	For nX := 1 To Len(aStru)
		TRB->&(aStru[nX,1]) := SQL->&(aStru[nX,1])
	Next nX
	MsUnlock()
	SQL->(dbSkip())
EndDo

dbSelectArea("SQL")
dbCloseArea()

dbSelectArea("TRB")
dbGoTop()

Return(aCampos)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110Inv   ∫Autor  ≥Renan Guedes       ∫ Data ≥  05/25/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Marca/desmarca os registros dos bens (SN3) na tabela 		  ∫±±
±±∫          ≥tempor·ria.                                                 ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110Inv(cMarca,lTodos,oMark)

Local nRecno	:= TRB->(Recno())
Local nTamOk	:= TamSX3("N3_OK")[1]
Local aArea		:= {}
Local aAreaSN1	:= {}
Local aAreaSN3	:= {}
Local aAreaFNA	:= {}

Default lTodos	:= .F.

aArea := GetArea()

dbSelectArea("SN1")
aAreaSN1 := SN1->(GetArea())
dbSetOrder(1)		//N1_FILIAL+N1_CBASE+N1_ITEM
dbGoTop()

dbSelectArea("SN3")
aAreaSN3 := SN3->(GetArea())
dbSetOrder(1)		//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
dbGoTop()

dbSelectArea("FNA")
aAreaFNA := FNA->(GetArea())
dbSetOrder(2)		//FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)
dbGoTop()

dbSelectArea("TRB")
If lTodos
	dbGoTop()
EndIf

While !lTodos .Or. TRB->(!EoF())
	//Se estiver marcado, apenas desmarca
	If TRB->N3_OK == cMarca
		RecLock("TRB",.F.)
		TRB->N3_OK := Space(nTamOk)
		TRB->(MsUnlock())
		nQtdMark--
	Else
		If AF110ValMrk(oMark,lTodos)		//Valida a marcaÁ„o do registro
			//Marca o registro
			RecLock("TRB",.F.)
			TRB->N3_OK := cMarca
			TRB->(MsUnlock())
			nQtdMark++
		EndIf
	EndIf

	If lTodos
		TRB->(dbSkip())
	Else
		Exit
	EndIf
EndDo

RestArea(aAreaFNA)
RestArea(aAreaSN3)
RestArea(aAreaSN1)
RestArea(aArea)

TRB->(dbGoTo(nRecno))

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110Grv   ∫Autor  ≥Renan Guedes       ∫ Data ≥  05/26/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥GravaÁ„o dos registros                                      ∫±±
±±∫          ≥Se o apontamento for simples, apenas atualiza alguns dados  ∫±±
±±∫          ≥na tabela FNA.											  ∫±±
±±∫          ≥															  ∫±±
±±∫          ≥Se o apontamento for multiplo, grava todos os registros na  ∫±±
±±∫          ≥tabela FNA.												  ∫±±
±±∫          ≥															  ∫±±
±±∫          ≥Grava os registros na tabela SN4.							  ∫±±
±±∫          ≥															  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AF110Grv(nRecno,cMarca,cN3Ok,lMulti)

Local aTxDepr		:= {}
Local cTabela		:= ""
Local aMemFNA		:= {}
Local nX			:= 0
Local nHdlPrv		:= 0
Local nTotal		:= 0
Local cArquivo		:= ""
Local cLoteAtf		:= LoteCont("ATF")

Private nDecQtd, nDecCoef, nDecMes, nDecAno, nDecAcm

Default nRecno		:= 1
Default cN3Ok		:= "N3_OK"
Default lMulti		:= .F.
Default cMarca		:= ""

nDecQtd := nDecCoef := nDecMes := nDecAno := nDecAcm := 0

//Monta matriz das moedas utilizadas para o c·lculo de exaust„o
aTxDepr := IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )

//Captura os decimais dos campos numÈricos para os apontamentos multiplos
dbSelectArea("SX3")
dbSetOrder(2)		//X3_CAMPO
dbGoTop()
If SX3->(MsSeek("FNA_QUANTD"))
	nDecQtd := SX3->X3_DECIMAL
EndIf
If SX3->(MsSeek("FNA_COEFIC"))
	nDecCoef := SX3->X3_DECIMAL
EndIf
If SX3->(MsSeek("N3_PRODMES"))
	nDecMes := SX3->X3_DECIMAL
EndIf
If SX3->(MsSeek("N3_PRODANO"))
	nDecAno := SX3->X3_DECIMAL
EndIf
If SX3->(MsSeek("N3_PRODACM"))
	nDecAcm := SX3->X3_DECIMAL
EndIf

If lMulti
	//Abre a tabela tempor·ria
	dbSelectArea("TRB")
	dbGoTop()

	cTabela	:= "TRB"
	AADD(aMemFNA, GetSXENum("FNA", "FNA_IDMOV"))
	AADD(aMemFNA, StrZero(1,TamSX3("FNA_ITMOV")[1]))
Else
	dbSelectArea("SN3")
	dbSetOrder(1)		//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
	dbGoTop()

	cTabela	:= "SN3"
	AADD(aMemFNA, M->FNA_IDMOV)
	AADD(aMemFNA, M->FNA_ITMOV)
EndIf

AADD(aMemFNA, M->FNA_DATA)
AADD(aMemFNA, M->FNA_OCORR)
AADD(aMemFNA, M->FNA_DTPERI)
AADD(aMemFNA, M->FNA_DTPERF)
AADD(aMemFNA, M->FNA_QUANTD)

//Abre a contabilizaÁ„o
nHdlPrv := HeadProva(cLoteAtf,"ATFA110",Substr(cUsername,1,6),@cArquivo)

If lMulti
	While TRB->(!EoF())
		If TRB->&(cN3Ok) == cMarca
			//Grava o apontamento em todas as moedas utilizadas
			AF110GrvMoeda(cTabela,aTxDepr,aMemFNA,lMulti,@nHdlPrv,@nTotal,@cArquivo)
		EndIf
		TRB->(dbSkip())
	EndDo
Else
	If SN3->(MsSeek(xFilial("SN3")+M->(FNA_CBASE+FNA_ITEM+FNA_TIPO+"0"+FNA_SEQ)))
		//Grava o apontamento em todas as moedas utilizadas
		AF110GrvMoeda(cTabela,aTxDepr,aMemFNA,lMulti,@nHdlPrv,@nTotal,@cArquivo)
	EndIf
EndIf

Pergunte("AFA110CONT",.F.)
lMostra := IIF(MV_PAR01 == 1, .T., .F.)
lAglutina := IIF(MV_PAR02 == 1, .T., .F.)

If (nHdlPrv > 0) .And. (nTotal > 0)
	RodaProva(nHdlPrv, nTotal)
	cA100Incl(cArquivo,nHdlPrv,3,cLoteAtf,lMostra,lAglutina)
EndIf

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110ValMrk   ∫Autor  ≥Renan Guedes    ∫ Data ≥  06/01/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Valida a marcaÁ„o do item do ativo (SN3) nos apontamentos   ∫±±
±±∫          ≥multiplos                                                   ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function AF110ValMrk(oMark,lTodos)

Local lRet		:= .T.
Local lTemRev	:= .F.
Local dUltDepr	:= GetNewPar("MV_ULTDEPR", STOD("19800101"))
Local lTemApto	:= .T.

Default lTodos	:= .F.

If lRet
	If SN1->(MsSeek(xFilial("SN1")+TRB->(N3_CBASE+N3_ITEM)))
		If SN1->N1_STATUS != "1"
			lRet := .F.
			If !lTodos
				Help("",1,"AF110ATFBL")		//"Este bem est· bloqueado."##"Somente bens desbloqueados podem ser movimentados."
			EndIf
		ElseIf !Empty(SN1->N1_DTBLOQ) .AND. (M->FNA_DATA <= SN1->N1_DTBLOQ)
			lRet := .F.
			If !lTodos
				Help(" ",1,"AF110DTBLQ",, STR0041, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0042})//"Este bem est· bloqueado atravÈs do campo N1_DTBLOQ."####"Somente bens desbloqueados podem ser movimentados"
			EndIf		
		EndIf	
	EndIf
EndIf

If lRet .And. !(M->FNA_OCORR $ "P8|P9|")
	If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA) ))
		While FNA->(!EoF()) .And. (FNA->FNA_FILIAL == TRB->N3_FILIAL)	;
			.And. FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA))	;
			== TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA)
			If !(FNA->FNA_OCORR $ "P0|P5|P8|P9|") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				If !lTodos
					Help("",1,"AF110EXAPT")		//"Existe um apontamento gravado para a mesma data."##"N„o È possÌvel realizar mais de um apontamento para uma mesma data."
				EndIf
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
	EndIf
EndIf

If lRet .And. M->FNA_OCORR $ "P2|P3|"
	If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO) ))
		While FNA->(!EoF()) .And. (FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO) == TRB->(N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO))
			If ((M->FNA_DTPERI >= FNA->FNA_DTPERI .And. M->FNA_DTPERI <= FNA->FNA_DTPERF)		;
				.Or. (M->FNA_DTPERF >= FNA->FNA_DTPERI .And. M->FNA_DTPERF <= FNA->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERI >= M->FNA_DTPERI .And. FNA->FNA_DTPERI <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERF >= M->FNA_DTPERI .And. FNA->FNA_DTPERF <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERI >= M->FNA_DTPERI .And. FNA->FNA_DTPERI <= M->FNA_DTPERF)	;
				.Or. (FNA->FNA_DTPERF >= M->FNA_DTPERI .And. FNA->FNA_DTPERF <= M->FNA_DTPERF))	;
				.And. !(FNA->FNA_OCORR $ "P0|P1|P5|P8|P9") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				If !lTodos
					Help("",1,"AF110PEREX")		//"Existe um apontamento gravado para um perÌodo igual ou correspondente."##"Informe outro perÌodo de apontamento ou estorne o apontamento existente."
				EndIf
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
	EndIf
	//VERIFICA SE A QUANTIDADE DO APONTAMENTO MAIS A PRODU«√O ACUMULADA … MAIOR QUE A PRODU«√O ESTIMADA DO ATIVO 
	If M->FNA_OCORR == "P2" .and. M->FNA_QUANTD + TRB->N3_PRODACM > TRB->N3_PRODANO 
		lRet := .F.
		Help( ,,"AF110P2QT" + CHR(13) + CHR(13) + STR0037 + TRB->N3_CBASE + CHR(13) + STR0038 + TRB->N3_ITEM,, STR0039 , 1, 0,,,,,,{STR0040} ) //"Ativo: " # "Item: " # "A quantidade apontada, somada ‡ produÁ„o acumulada do bem, È maior que o valor de produÁ„o estimada." # "Informe uma quantidade menor para este apontamento de produÁ„o."
	EndIf
EndIf

If lRet
	If !(M->FNA_OCORR $ "P4|P9|")
		If SN3->(MsSeek(xFilial("SN3")+TRB->(N3_CBASE+N3_ITEM+N3_TIPO)+"0"+TRB->N3_SEQ))
			If !Empty(SN3->N3_DEXAUST) .Or. !Empty(SN3->N3_FIMDEPR)
				lRet := .F.
				If !lTodos
					Help("",1,"AF110EX")		//"Este bem j· foi exaurido."##"Bens exauridos n„o podem ser movimentados."
				EndIf
			EndIf
		EndIf
	ElseIf M->FNA_OCORR == "P4"
		If SN3->(MsSeek(xFilial("SN3")+TRB->(N3_CBASE+N3_ITEM+N3_TIPO)+"0"+TRB->N3_SEQ))
			If Empty(SN3->N3_DEXAUST) .Or. Empty(SN3->N3_FIMDEPR)
				lRet := .F.
				If !lTodos
					Help("",1,"AF110NOEX")		//"Este bem n„o foi exaurido."##"Apontamentos complementares somente podem ser realizados para bens j· exauridos."
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. M->FNA_OCORR == "P8"
	If FNA->(MsSeek( xFilial("FNA")+TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO) ))
		While FNA->(!EoF()) .And. FNA->FNA_FILIAL == TRB->N3_FILIAL	;
			.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO))
			//Verifica se h· apontamentos de produÁ„o para o bem
			If (FNA->FNA_DATA == M->FNA_DATA) .And. !(FNA->FNA_OCORR $ "P0|P1|P5|P8|P9|") .And. (FNA->FNA_ESTORN != "1")
				lRet := .F.
				If !lTodos
					Help("",1,"AF110NOP8")		//"N„o È possÌvel estornar a revis„o de estimativa de produÁ„o de bens apontados."##"Para alterar a estimativa de produÁ„o È necess·rio realizar um apontamento de revis„o de estimativa de produÁ„o."
				EndIf
				Exit
			EndIf
			//Verifica se h· revis„o de estimativa para o bem
			If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_OCORR == "P1") .And. (FNA->FNA_ESTORN != "1")
				lTemRev := .T.
			EndIf
			FNA->(dbSkip())
		EndDo
		//Check e help do estorno da revis„o de estimativa
		If lRet .And. !lTemRev
			lRet := .F.
			If !lTodos
				Help("",1,"AF110NOREV")		//"N„o existe revis„o de estimativa de produÁ„o a ser estornada para o bem."##"Somente È possÌvel estornar a revis„o de estimativa de produÁ„o para bens que tiveram a estimativa revisada."
			EndIf
		EndIf
		If lRet
			If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA) ))
				While FNA->(!EoF()) .And. FNA->FNA_FILIAL == xFilial("FNA")	;
				.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO))
				If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_ESTORN != "1")
					lRet := .F.
					Help("",1,"AF110APPOS")		//"Existe apontamento posterior a data informada."##"… necess·rio estornar os apontamentos posteriores a data informada."
					Exit
				EndIf
				FNA->(dbSkip())
				EndDo
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. M->FNA_OCORR == "P9"
	If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA) ))
		While FNA->(!EoF()) .And. FNA->FNA_FILIAL == TRB->N3_FILIAL	;
			.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)) == TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA))
			If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_OCORR $ "P2|P3|P4|") .And. (FNA->FNA_ESTORN != "1")
				lTemApto := .T.
				Exit
			EndIf
			FNA->(dbSkip())
		EndDo
		If !lTemApto
			lRet := .F.
			If !lTodos
				Help("",1,"AF110NOAPT")		//"N„o existe apontamento a ser estornado para o bem."##"Somente È possÌvel estornar apontamentos existentes e no mÍs/ano do c·lculo de depreciaÁ„o."
			EndIf
		Else
			If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA) ))
				While FNA->(!EoF()) .And. FNA->FNA_FILIAL == TRB->N3_FILIAL	;
				.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO))
				If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_OCORR == "P3") .And. (FNA->FNA_ESTORN != "1")
					lRet := .F.
					If !lTodos
						Help("",1,"AF110EXENC")		//"N„o È possÌvel estornar apontamentos anteriores ao encerramento de produÁ„o para bens cuja produÁ„o foi encerrada."##"… necess·rio, primeiramente, estornar o encerramento de produÁ„o na data em que tenha ocorrido, para ent„o estornar os apontamentos anteriores."
					EndIf
					Exit
				EndIf
				FNA->(dbSkip())
				EndDo
			EndIf
			If lRet
				If FNA->(MsSeek( xFilial("FNA") + TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO)+DTOS(M->FNA_DATA) ))
					While FNA->(!EoF()) .And. FNA->FNA_FILIAL == TRB->N3_FILIAL	;
					.And. (FNA->(FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD) == TRB->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQ+N3_SEQREAV+N3_TPSALDO))
					If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_DATA > M->FNA_DATA) .And. (FNA->FNA_OCORR $ "P2|P4|") .And. (FNA->FNA_ESTORN != "1")
						lRet := .F.
						If !lTodos
							Help("",1,"AF110APPOS")		//"Existe apontamento posterior a data informada."##"… necess·rio estornar os apontamentos posteriores a data informada."
						EndIf
						Exit
					EndIf
					FNA->(dbSkip())
					EndDo
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

Return(lRet)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110GrvP0   ∫Autor  ≥Renan Guedes     ∫ Data ≥  06/06/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥GravaÁ„o da estimativa inicial de produÁ„o e/ou produÁ„o	  ∫±±
±±∫          ≥acumulada, chamada atravÈs da integraÁ„o com a rotina de 	  ∫±±
±±∫          ≥inclus„o de Ativo Imobilizado ATFA010                       ∫±±
±±∫          ≥		                                                      ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110GrvAp(nRecno,cOcor)

Local nDecCoef		:= 0 
Local nDecAcm		:= 0
Local aTxDepr		:= {}
Local aFatorDep		:= {}
Local aParam		:= {}
Local dDtMov

dbSelectArea("FNA")

If nRecno > 0
	//Captura os decimais dos campos numÈricos para os apontamentos multiplos
	dbSelectArea("SX3")
	dbSetOrder(2)		//X3_CAMPO
	dbGoTop()
	If SX3->(MsSeek("FNA_COEFIC"))
		nDecCoef := SX3->X3_DECIMAL
	EndIf
	If SX3->(MsSeek("N3_PRODACM"))
		nDecAno := SX3->X3_DECIMAL
	EndIf

	SN3->(dbGoTo(nRecno))

	dDtMov := SN3->N3_DINDEPR

	RecLock("FNA",.T.)
	FNA->FNA_FILIAL	:= SN3->N3_FILIAL
	FNA->FNA_IDMOV	:= GetSXENum("FNA", "FNA_IDMOV")
	FNA->FNA_ITMOV	:= StrZero(1,TamSX3("FNA_ITMOV")[1])
	FNA->FNA_CBASE	:= SN3->N3_CBASE
	FNA->FNA_ITEM	:= SN3->N3_ITEM
	FNA->FNA_TIPO	:= SN3->N3_TIPO
	FNA->FNA_SEQ	:= SN3->N3_SEQ
	FNA->FNA_SEQREA	:= SN3->N3_SEQREAV
	FNA->FNA_TPSALD	:= SN3->N3_TPSALDO
	FNA->FNA_TPDEPR	:= SN3->N3_TPDEPR
	FNA->FNA_DATA	:= SN3->N3_DINDEPR
	FNA->FNA_ESTORN	:= '2'
	Do Case
		Case cOcor == "P0"
			FNA->FNA_QUANTD	:= SN3->N3_PRODANO
			lGeraMov		:= .F.
		Case cOcor == "P5"
			FNA->FNA_QUANTD	:= SN3->N3_PRODACM

			//Calcula o coeficiente de exaust„o
			aTxDepr := IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )
			aFatorDep := {}

			aParam := Array(9)
			aParam[1] := SN3->N3_VORIG1
			aParam[2] := SN3->N3_VRDACM1
			aParam[3] := SN3->N3_TPDEPR
			aParam[4] := SN3->N3_VMXDEPR
			aParam[5] := SN3->N3_PERDEPR
			aParam[6] := SN3->N3_VLSALV1
			aParam[7] := SN3->N3_PRODACM
			aParam[8] := SN3->N3_PRODANO
			aParam[9] := SN3->N3_FIMDEPR

			aFatorDep := AFatorCalc(aTxDepr, SN3->N3_DINDEPR, dDtMov, SN3->N3_TPDEPR, , , aParam)

			FNA->FNA_COEFIC	:= Round( NoRound( aFatorDep[1], nDecAcm+1 ) , nDecCoef )
			FNA->FNA_VALOR := SN3->N3_VRDACM1 
			FNA->FNA_MOEDA := '1'
	EndCase
	FNA->FNA_OCORR	:= cOcor
	FNA->FNA_DTPERI	:= SN3->N3_DINDEPR
	FNA->FNA_DTPERF	:= SN3->N3_FIMDEPR
	MsUnlock()

	ConfirmSX8()

	dbSelectArea("SN3")
	SN3->(dbGoTo(nRecno))
EndIf

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110ValCp   ∫Autor  ≥Renan Guedes     ∫ Data ≥  06/07/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ValidaÁ„o dos campos do apontamento de produÁ„o             ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Parametros≥cCampo = Campo a ser validado (string)                      ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110ValCp(cCampo)

Local lRet		:= .T.

Default cCampo	:= ""

Do Case
	Case Upper(AllTrim(cCampo)) == "FNA_OCORR"
		If M->FNA_OCORR $ "P0|P5|"
			lRet := .F.
			Help("",1,"AF110NOOCR")		//"As ocorrÍncias de estimativa de produÁ„o e produÁ„o complementar n„o podem ser apontadas manualmente."##"Informe outra ocorrÍncia de apontamento."
		EndIf
EndCase

Return(lRet)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AFA110Hist   ∫Autor  ≥Renan Guedes     ∫ Data ≥  06/09/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Browse do histÛrico de apontamentos                         ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AFA110Hist()

Local aCores		:= {}

Private aRotina		:= MenuDef("FNA")
Private cCadastro	:= STR0026

//Cores da legenda das ocorrÍncias de apontamento
AADD(aCores,{"FNA_OCORR == 'P0'"	,"BR_BRANCO"	})
AADD(aCores,{"FNA_OCORR == 'P1'"	,"BR_CINZA"		})
AADD(aCores,{"FNA_OCORR == 'P2'"	,"BR_VERDE"		})
AADD(aCores,{"FNA_OCORR == 'P3'"	,"BR_AMARELO"	})
AADD(aCores,{"FNA_OCORR == 'P4'"	,"BR_AZUL"		})
AADD(aCores,{"FNA_OCORR == 'P5'"	,"BR_MARROM"	})
AADD(aCores,{"FNA_OCORR == 'P8'"	,"BR_PINK"		})
AADD(aCores,{"FNA_OCORR == 'P9'"	,"BR_VERMELHO"	})

dbSelectArea("FNA")
dbSetOrder(1)

mBrowse(6, 1, 22, 75, "FNA", , , , , , aCores)

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110Leg   ∫Autor  ≥Renan Guedes       ∫ Data ≥  06/09/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Legenda das ocorrÍncias do histÛrico de apontamentos        ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110Leg()

Local aLegenda := {}

//Montagem da legenda das ocorrÍncias de apontamento
AADD(aLegenda,{"BR_BRANCO" 	,STR0018 })		//"Estimativa de produÁ„o inicial"
AADD(aLegenda,{"BR_CINZA" 	,STR0019 })		//"Revis„o de estimativa"
AADD(aLegenda,{"BR_VERDE" 	,STR0020 })		//"ProduÁ„o"
AADD(aLegenda,{"BR_AMARELO" ,STR0021 })		//"Encerramento de produÁ„o"
AADD(aLegenda,{"BR_AZUL" 	,STR0022 })		//"ProduÁ„o complementar"
AADD(aLegenda,{"BR_MARROM" 	,STR0023 })		//"ProduÁ„o acumulada"
AADD(aLegenda,{"BR_PINK" 	,STR0024 })		//"Estorno de revis„o de estimativa"
AADD(aLegenda,{"BR_VERMELHO",STR0025 })		//"Estorno de produÁ„o"

BrwLegenda(cCadastro, "Legenda", aLegenda)

Return


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AFA110ParBox   ∫Autor  ≥Renan Guedes   ∫ Data ≥  06/09/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Parambox das perguntas do estorno multiplo                  ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AFA110ParBox(aRetP1, cDataMov)

Local lRet			:= .F.
Local aParamBox		:= {}
Local cParTit		:= STR0027		//"Estornos m˙ltiplos"
Local bParOk		:= {|| AF110ValP1(cDataMov)}
Local aParamRet		:= {}
Local nTamFil		:= IIF(lFWCodFil, FWGETTAMFILIAL, 2)
Local nTamId		:= TamSX3("FNA_IDMOV")[1]
Local nX			:= 0
Local cTitFil		:= ""
Local cTitId		:= ""

aRetP1 := {}

dbSelectArea("SX3")
dbSetOrder(2)		//X3_CAMPO
dbGoTop()
If SX3->(MsSeek("FNA_FILIAL"))
	cTitFil := X3Titulo()
EndIf
If SX3->(MsSeek("FNA_IDMOV"))
	cTitId := X3Titulo()
EndIf

dbSelectArea("FNA")
dbSetOrder(1)		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
dbGoTop()

AADD(aParamBox,{1,cTitFil	,Space(nTamFil)	,"@!","AllwaysTrue()"	,"XM0"	,".T.",025,.T.})
AADD(aParamBox,{1,cTitId	,Space(nTamId)	,"@!","AllwaysTrue()"	,""		,".T.",045,.T.})

aParamRet := Array(Len(aParamBox))
For nX := 1 To Len(aParamBox)
	aParamRet[nX] := aParamBox[nX][3]
Next nX

lRet := ParamBox(aParamBox,@cParTit,@aParamRet,bParOk,,.T.,,,,"AF110P1",.T.,.T.)

If lRet
	aRetP1 := AjRetParam(aParamRet,aParamBox)

	Pergunte("AFA110CONT",.F.)
	lMostra := IIF(MV_PAR01 == 1, .T., .F.)
	lAglutina := IIF(MV_PAR02 == 1, .T., .F.)
EndIf

Return(lRet)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AjRetParam∫Autor  ≥Alvaro Camillo Neto ∫ Data ≥  17/12/08   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Ajusta as repostas do aParambox                             ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ 		                                                      ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AjRetParam(aRet,aParamBox)

Local nX	:= 1

IF ValType(aRet) == "A" .AND. Len(aRet) == Len(aParamBox)
	For nX := 1 to Len(aParamBox)
		If aParamBox[nX][1] == 1
			aRet[nX] := aRet[nX]
		ElseIf aParamBox[nX][1] == 2 .AND. ValType(aRet[nX]) == "C"
			aRet[nX] := aScan(aParamBox[nX][4],{|x| Alltrim(x) == aRet[nX]})
		ElseIf aParamBox[nX][1] == 2 .AND. ValType(aRet[nX]) == "N"
			aRet[nX] := aRet[nX]
		Endif
	Next nX
EndIf

Return aRet


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110ValP1   ∫Autor  ≥Renan Guedes     ∫ Data ≥  06/10/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Valida as respostas da parambox da funÁ„o de estornos	      ∫±±
±±∫          ≥multiplos                                                   ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function AF110ValP1(cDataMov)

Local lRet	:= .T.
Local aArea := GetArea()

FNA->(dbSetOrder(3))
If !MsSeek(xFilial("FNA",MV_PAR01)+MV_PAR02+cDataMov)
	HELP(" ",1,"A110DTPRC",,STR0031 + MV_PAR02 + STR0032 + DTOC(STOD(cDataMov)) + CRLF + STR0033,1,0) //"N„o existe o processo "###" para o dia "###"Para que o apontamento de estorno seja feito corretamente, È necess·rio que o mesmo seja feito na mesma data do processo de apontamento original"
	lRet := .F.
EndIf

RestArea(aArea)

Return(lRet)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110Coefic  ∫Autor  ≥Renan Guedes     ∫ Data ≥  06/19/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Calcula o coeficiente de exaust„o do item do ativo, ou seja,∫±±
±±∫          ≥a taxa de depreciaÁ„o baseada no apontamento realizado      ∫±±
±±∫          ≥															  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AF110Coefic(aTxDepr,cTabela,nQuant,dDtMov,lEncerra)

Local aParam		:= Array(9)
Local aCoefic		:= {}

Default aTxDepr 	:= IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )
Default cTabela		:= "SN3"
Default nQuant		:= 0
Default dDtMov		:= dDataBase	//Temp
Default lEncerra	:= .F.

aParam[1] := (cTabela)->N3_VORIG1
aParam[2] := (cTabela)->N3_VRDACM1
aParam[3] := (cTabela)->N3_TPDEPR
aParam[4] := (cTabela)->N3_VMXDEPR
aParam[5] := (cTabela)->N3_PERDEPR
aParam[6] := (cTabela)->N3_VLSALV1
aParam[7] := IIF(lEncerra, ((cTabela)->N3_PRODANO - (cTabela)->N3_PRODACM) , nQuant )
aParam[8] := (cTabela)->N3_PRODANO
aParam[9] := (cTabela)->N3_FIMDEPR

aCoefic := AFatorCalc(aTxDepr, (cTabela)->N3_DINDEPR, dDtMov, (cTabela)->N3_TPDEPR, , , aParam)

Return(aCoefic)


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥AF110GrvMoeda   ∫Autor  ≥Renan Guedes  ∫ Data ≥  06/20/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Faz a gravaÁ„o do apontamento, inclusive para todas as      ∫±±
±±∫          ≥moedas utilizadas                                           ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ AP                                                        ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Static Function AF110GrvMoeda(cTabela,aTxDepr,aMemFNA,lMulti,nHdlPrv,nTotal,cArquivo)

Local nX			:= 0
Local nY			:= 0
Local cIDMov  		:= ""
Local aFatorDep		:= {}
Local nTamMoeda		:= TamSX3("FNA_MOEDA")[1]
Local nRecFNA		:= 0
Local aRecFNA		:= {}
Local dUltDepr		:= GetNewPar("MV_ULTDEPR", STOD("19800101"))
Local nProdMes		:= 0
Local cPadrao		:= ""
Local lPadrao
Local cTipoFiscal	:= ATFXTpBem(1)
Local cTipoGerenc	:= ATFXTpBem(2)
Local aValDepr		:= {}
Local aDadosComp	:= {}
Local nTaxaMedia	:= 0
Local cN5_TIPO		:= ""
Local lGerouMov		:= .F.
Local cLoteAtf		:= LoteCont("ATF")
Local aAptoEst		:= {}
Local lGerouEst		:= .F.
Local nMoedaEst		:= 1
Local aAptos		:= {}
Local nUltApto		:= 0
Local nQtdEst		:= 0
Local nRecEst		:= 0
Local lEstExaust	:= .F.
Local nProdAcm		:= 0
Local aVrdMes		:= {}
Local aVrdAcm		:= {}
Local lOnOff		:= .T.
Local cTipDepr		:= "" 
Local lVlResUnPr	:= SuperGetMV("MV_ATFUNPR",.F.,"0") == "1" 

Default cTabela		:= "SN3"
Default aTxDepr		:= IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )
Default lMulti		:= .F.

Pergunte("AFA110CONT",.F.)
lOnOff := MV_PAR03 == 1


//Calcula a taxa de depreciaÁ„o
If aMemFNA[4] $ "P2|P3|P4|"
	aFatorDep := {}
	aFatorDep := AF110Coefic( @aTxDepr,cTabela,aMemFNA[7],aMemFNA[3],IIF(aMemFNA[4] == "P3", .T., .F.) )
EndIf

//Verifica se existe
dbSelectArea("FNA")
dbSetOrder(1)		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR

//Grava o apontamento para todas as moedas utilizadas
For nX := 1 To Len(aTxDepr)
	RecLock("FNA", .T.)
	FNA->FNA_FILIAL	:= (cTabela)->N3_FILIAL
	FNA->FNA_IDMOV	:= aMemFNA[1]
	FNA->FNA_ITMOV	:= aMemFNA[2]
	FNA->FNA_CBASE	:= (cTabela)->N3_CBASE
	FNA->FNA_ITEM	:= (cTabela)->N3_ITEM
	FNA->FNA_TIPO	:= (cTabela)->N3_TIPO
	FNA->FNA_SEQ	:= (cTabela)->N3_SEQ
	FNA->FNA_SEQREA	:= (cTabela)->N3_SEQREAV
	FNA->FNA_TPSALD	:= (cTabela)->N3_TPSALDO
	FNA->FNA_TPDEPR	:= (cTabela)->N3_TPDEPR
	FNA->FNA_DATA	:= aMemFNA[3]
	FNA->FNA_OCORR	:= aMemFNA[4]
	FNA->FNA_DTPERI	:= aMemFNA[5]
	FNA->FNA_DTPERF	:= aMemFNA[6]
	FNA->FNA_QUANTD	:= aMemFNA[7]
	FNA->FNA_ESTORN	:= '2'

	If aMemFNA[4] $ "P2|P3|P4|"
		FNA->FNA_COEFIC	:= Round( NoRound( aFatorDep[nX] , nDecCoef+1 ) , nDecCoef )
	EndIf
	If !(aMemFNA[4] $ "P1|P8|")
		FNA->FNA_MOEDA	:= cValToChar(nX) + Space(nTamMoeda - nX) //Temp
	EndIf
	MsUnlock()

	If nX == 1
		//Salva o registro atual
		nRecFNA := FNA->(Recno())

		//Salva os dados do bem
		aRecFNA := Array(9)
		aRecFNA[1] := FNA->FNA_FILIAL
		aRecFNA[2] := FNA->FNA_CBASE
		aRecFNA[3] := FNA->FNA_ITEM
		aRecFNA[4] := FNA->FNA_TIPO
		aRecFNA[5] := FNA->FNA_SEQ
		aRecFNA[6] := FNA->FNA_SEQREA
		aRecFNA[7] := FNA->FNA_TPSALD
		aRecFNA[8] := FNA->FNA_MOEDA
		aRecFNA[9] := FNA->FNA_DATA

		//ProduÁ„o
		nProdMes := 0
		aVrdMes := IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )
		nProdAcm := 0
		aVrdAcm := IIF(lMultMoed, AtfMultMoe(,,{|xMoeda| 0 }) , {0,0,0,0,0} )

		//Apontamento estornado
		aAptoEst := {}

		//Quantidade estornada
		nQtdEst := 0

		dbSelectArea("SN3")
		dbSetOrder(1)		//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
		dbGoTop()

		dbSelectArea("SN4")
		dbSetOrder(1)		//N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+DTOS(N4_DATA)+N4_OCORR+N4_SEQ
		dbGoTop()

		//Atualiza os registros da tabela SN3
		dbSelectArea("FNA")
		dbSetOrder(1)		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
		dbGoTop()

		Do Case
			Case aMemFNA[4] == "P1"		//Apont. de revis„o de estimativa de produÁ„o
				//Restaura o Ìndice e registro atual
				FNA->(dbSetOrder(1))		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
				FNA->(dbGoTo(nRecFNA))

				If SN3->(MsSeek(FNA->FNA_FILIAL+FNA->FNA_CBASE+FNA->FNA_ITEM+FNA->FNA_TIPO+"0"+FNA->FNA_SEQ))
					RecLock("SN3",.F.)
					SN3->N3_PRODANO	:= NoRound( FNA->FNA_QUANTD , nDecAno+1 )
					MsUnlock()
					//LanÁamento padr„o
					cPadrao := "871"		//Apontamento de revis„o da estimativa de produÁ„o

					//Verifica se o lanÁamento padr„o est· configurado
					lPadrao := VerPadrao(cPadrao)

					IF lPadrao .And. lOnOff
						nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
					EndIf

					//Altera a varÌavel de controle do laÁo, para n„o gravar apontamentos para todas as moedas
					nX := Len(aTxDepr)
				EndIf
			Case aMemFNA[4] $ "P2|P3|P4|"		//Apont. de produÁ„o e estorno
				//Verifica todos os apontamentos realizados no mÍs
				FNA->(dbSetOrder(2))		//FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)
				If FNA->(MsSeek(aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]))
					While FNA->(!EoF())	.And. (FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD)) == aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]
						If FNA->FNA_OCORR $ "P2|P3|P4|" .And. FNA->FNA_ESTORN != "1"
							//Atualiza a vari·vel que acumula a depreciaÁ„o total
							aVrdAcm[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR 
							//Verifica a moeda do apontamento
							If FNA->FNA_MOEDA == aRecFNA[8]
								//Atualiza a vari·vel que acumula a produÁ„o total
								nProdAcm += FNA->FNA_QUANTD
							EndIf
							If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1))
								//Atualiza a vari·vel que acumula a depreciaÁ„o total
								aVrdMes[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR
								//Verifica a moeda do apontamento
								If FNA->FNA_MOEDA == aRecFNA[8]
									//Atualiza a vari·vel que acumula a produÁ„o mensal
									nProdMes += FNA->FNA_QUANTD
								EndIf
							EndIf
						EndIf
						If FNA->FNA_OCORR == 'P5' .and. !Empty(FNA->FNA_MOEDA)
							aVrdAcm[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR
							nProdAcm += FNA->FNA_QUANTD
						Endif
						FNA->(dbSkip())
					EndDo
				EndIf

				//Restaura o Ìndice e registro atual
				FNA->(dbSetOrder(1))		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
				FNA->(dbGoTo(nRecFNA))

				//Atualiza os campos do bem
				If SN3->(MsSeek(FNA->FNA_FILIAL+FNA->FNA_CBASE+FNA->FNA_ITEM+FNA->FNA_TIPO+"0"+FNA->FNA_SEQ))
					//Gera as movimentaÁıes na tabela SN4 e SN5
					If aMemFNA[4] $ "P2|P3" .And. cPaisLoc <> "BOL"
						Do Case
							Case SN3->N3_TPDEPR $ "4|5|8|"		//Exaust„o linear
								If SN3->N3_TPDEPR == '4' .and. lVlResUnPr
									aValDepr := AtfMultMoe(,,{|xMoeda| IIF( SN3->&("N3_VORIG"+cValToChar(xMoeda)) > 0, (SN3->&("N3_VORIG"+cValToChar(xMoeda)) + SN3->&("N3_AMPLIA"+cValToChar(xMoeda)) - SN3->&("N3_VRDACM"+cValToChar(xMoeda)) ) * FNA->FNA_COEFIC, 0 ) })
								Else
									aValDepr := AtfMultMoe(,,{|xMoeda| IIF( SN3->&("N3_VORIG"+cValToChar(xMoeda)) > 0, (SN3->&("N3_VORIG"+cValToChar(xMoeda)) + SN3->&("N3_AMPLIA"+cValToChar(xMoeda))) * FNA->FNA_COEFIC, 0 ) })
								EndIf
							Case SN3->N3_TPDEPR == "9"		//Exaust„o por saldo residual
								aValDepr := AtfMultMoe(,,{|xMoeda| IIF( SN3->&("N3_VORIG"+cValToChar(xMoeda)) > 0, (((SN3->&("N3_VORIG"+cValToChar(xMoeda)) + SN3->&("N3_AMPLIA"+cValToChar(xMoeda))) - SN3->&("N3_VRDACM"+cValToChar(xMoeda))) * FNA->FNA_COEFIC), 0 ) })
						EndCase

						//Atualiza os arrays de valor da depreciaÁ„o mensal e acumulada
						For nY := 1 To Len(aValDepr)
							aVrdAcm[nY] += aValDepr[nY]
							aVrdMes[nY] += aValDepr[nY]
						Next nY

						//Atualiza depreciacao acumulada
						RecLock("SN3",.F.)
						If cPaisLoc <> "BOL"
							For nY := 1 To Len(aValDepr)
	 							If SN3->(FieldPos("N3_VRDMES"+cValToChar(nY)) > 0)
									SN3->&("N3_VRDMES"+cValToChar(nY)) := aVrdMes[nY]
								EndIf
	
								If SN3->(FieldPos("N3_VRDACM"+cValToChar(nY)) > 0)
										SN3->&("N3_VRDACM"+cValToChar(nY)) := aVrdAcm[nY] 
								EndIf
							Next nY
						Endif

						//Atualiza Producao acumulada
						SN3->N3_PRODMES	:= Round( NoRound( nProdMes , nDecQtd+1 ) , nDecMes )
						SN3->N3_PRODACM	:= Round( NoRound( (SN3->N3_PRODACM + aMemFNA[7]) , nDecQtd+1 ) , nDecAcm )
						
						If aMemFNA[4] == "P3"
							SN3->N3_DEXAUST	:= aRecFNA[9]
							SN3->N3_FIMDEPR	:= aRecFNA[9]
						EndIf
						MsUnlock()

						//Dados complementares a movimentaÁ„o
						aDadosComp := ATFXCompl(nTaxaMedia, FNA->FNA_COEFIC,/*cMotivo*/,/*cCodBaix*/,/*cFilOrig*/,/*cSerie*/,/*cNota*/,/*nVenda*/,/*cLocal*/, SN3->N3_PRODMES )

						//-----------------------------------------------------------------
						// Avalia o tipo de depreciaÁ„o que ocorrer· (Fiscal ou Gerencial)
						//-----------------------------------------------------------------
						If SN3->N3_TIPO $ cTipoFiscal
							cTipDepr := "06" //DepreciaÁ„o fiscal
						ElseIf SN3->N3_TIPO $ cTipoGerenc
							cTipDepr := "20" //DepreciaÁ„o gerencial
						EndIf 

						If cPaisLoc <> "BOL"
							//GravaÁ„o na tabela SN4 da conta DepreciaÁ„o Acumulada
							ATFXMOV(SN3->N3_FILIAL,@cIDMOV,FNA->FNA_DATA,cTipDepr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"4",0,SN3->N3_TPSALDO,/*aEntidades*/,aValDepr,aDadosComp,,,,,lOnOff,cPadrao)
	
							//LanÁamento padr„o
							Do Case
								Case aMemFNA[4] == "P2"
									cPadrao := "872"		//Apontamento de produÁ„o
						   		Case aMemFNA[4] == "P3"
									cPadrao := "873"
							EndCase
						EndIf

						//Verifica se o lanÁamento padr„o est· configurado
						lPadrao := VerPadrao(cPadrao)

						If lPadrao .And. lOnOff
							nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
						EndIf
						
						If cPaisLoc <> "BOL"
							//GravaÁ„o na tabela SN4 da conta Despesa de DepreciaÁ„o
							ATFXMOV(SN3->N3_FILIAL,@cIDMOV,FNA->FNA_DATA,cTipDepr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"3",0,SN3->N3_TPSALDO,/*aEntidades*/,aValDepr,aDadosComp,,,,,lOnOff,cPadrao)
	
							//LanÁamento padr„o
							Do Case
								Case aMemFNA[4] == "P2"
									If (SN3->N3_RATEIO == "1")
										//Contabiliza o rateio da despesa de depreciaÁ„o
										cPadrao := "87A"
										ATFRTMOV(	SN3->N3_FILIAL,;
													SN3->N3_CBASE,;
													SN3->N3_ITEM,;
													SN3->N3_TIPO,;
													SN3->N3_SEQ,;
													SN4->N4_DATA,;
													SN4->N4_IDMOV,;
													aValDepr,;
													.T.,;
													"1",;
													nHdlPrv,;
													cLoteATF,;
													@nTotal,;
													"0",;
													"ATFA110",;
													cPadrao,lOnOff				)
									Else
										cPadrao := "872"
									EndIf
	
									If cPadrao == "872"
										//Verifica se o lanÁamento padr„o est· configurado
										lPadrao := VerPadrao(cPadrao)
	
										If lPadrao .And. lOnOff
											nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
										EndIf
									EndIf
	
						   		Case aMemFNA[4] == "P3"
									cPadrao := "873"
	
									//Verifica se o lanÁamento padr„o est· configurado
									lPadrao := VerPadrao(cPadrao)
	
									If lPadrao .And. lOnOff
										nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
									EndIf
							EndCase
	
							//Verifica o tipo de saldo a ser atualizado
							Do case
				                Case SN3->N3_TIPO $ cTipoFiscal
									cN5_TIPO := "4"
								Case SN3->N3_TIPO $ cTipoGerenc
									cN5_TIPO := "Y"
								Case SN3->N3_TIPO == "08"
									cN5_TIPO := "K"
								Case SN3->N3_TIPO == "09"
									cN5_TIPO := "L"
								OtherWise
									cN5_TIPO := "4"
				     		EndCase
	
				     		//GravaÁ„o na tabela SN5 da conta Despesa de DepreciaÁ„o
							AtfSaldo(SN3->N3_CDEPREC,FNA->FNA_DATA,cN5_TIPO,aValDepr[1],aValDepr[2],aValDepr[3],aValDepr[4],aValDepr[5],"+",FNA->FNA_COEFIC,SN3->N3_SUBCDEP,,SN3->N3_CLVLDEP,SN3->N3_CCDESP,"3",aValDepr)
	
							//GravaÁ„o na tabela SN5 da conta DepreciaÁ„o Acumulada
							AtfSaldo(SN3->N3_CCDEPR,FNA->FNA_DATA,cN5_TIPO,aValDepr[1],aValDepr[2],aValDepr[3],aValDepr[4],aValDepr[5],"+",FNA->FNA_COEFIC,SN3->N3_SUBCCDE,,SN3->N3_CLVLCDE,SN3->N3_CCCDEP,"4",aValDepr)
						EndIf

						//Controla a gravaÁ„o dos valores dos movimentos para atualizar o apontamento
						lGerouMov := .T.
					Else
						//LanÁamento padrao
						cPadrao := "874"		//Apontamento de complemento de produÁ„o

						//Atualizo a producao mensal e acumulada
						RecLock("SN3",.F.)
						SN3->N3_PRODMES	:= Round( NoRound( nProdMes , nDecQtd+1 ) , nDecMes )
						SN3->N3_PRODACM	:= Round( NoRound( (SN3->N3_PRODACM + aMemFNA[7]) , nDecQtd+1 ) , nDecAcm )
						MsUnlock()


						//Verifica se o lanÁamento padr„o est· configurado
						lPadrao := VerPadrao(cPadrao)

						If lPadrao .And. lOnOff
							nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
						EndIf
					EndIf
				EndIf
			Case aMemFNA[4] == "P8"		//Estorno de revis„o de estim. de produÁ„o
				//Array utilizado para salvar todos os apontamentos de estimativa e revis„o de estimativa de produÁ„o
				aAptos := {}

				FNA->(dbSetOrder(2))		//FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)
				If FNA->(MsSeek( aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7] ))
					While FNA->(!EoF())	.And. (FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD)) == aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]
						If (FNA->FNA_DATA <= LastDay(dUltDepr+1)) .And. (FNA->FNA_OCORR $ "P0|P1|") .And. (FNA->FNA_ESTORN != "1")
							AADD(aAptos, { FNA->FNA_FILIAL, FNA->FNA_IDMOV, FNA->FNA_ITMOV, FNA->FNA_CBASE, FNA->FNA_ITEM, FNA->FNA_TIPO, FNA->FNA_SEQ, FNA->FNA_SEQREA, FNA->FNA_TPSALD, DTOS(FNA->FNA_DATA) })
						EndIf
						FNA->(dbSkip())
					EndDo

					//Verifica o ˙ltimo apontamento de revis„o de estimativa de produÁ„o
					nUltApto := Len(aAptos)

					//Altera o campo de controle de estorno
					If FNA->(MsSeek(aAptos[nUltApto,1]+aAptos[nUltApto,4]+aAptos[nUltApto,5]+aAptos[nUltApto,6]+aAptos[nUltApto,7]+aAptos[nUltApto,8]+aAptos[nUltApto,9]+aAptos[nUltApto,10]))
						RecLock("FNA",.F.)
						FNA->FNA_ESTORN := "1"
						MsUnlock()

						cPadrao := "876"		//Estorno de apontamento de revis„o de estimativa de produÁ„o

						//Verifica se o lanÁamento padr„o est· configurado
						lPadrao := VerPadrao(cPadrao)

						If lPadrao .And. lOnOff
							nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
						EndIf

						//Quantidade estornada
						nQtdEst := FNA->FNA_QUANTD
					EndIf

					//Verifica o apontamento de revis„o de estimativa de produÁ„o que ser· posto em vigor
					nUltApto := Len(aAptos)-1

					//Altera o campo de controle de estorno
					If nUltApto > 0 .and. FNA->(MsSeek(aAptos[nUltApto,1]+aAptos[nUltApto,4]+aAptos[nUltApto,5]+aAptos[nUltApto,6]+aAptos[nUltApto,7]+aAptos[nUltApto,8]+aAptos[nUltApto,9]+aAptos[nUltApto,10]))
					    //Atualiza os campos do bem
						If SN3->(MsSeek(FNA->FNA_FILIAL+FNA->FNA_CBASE+FNA->FNA_ITEM+FNA->FNA_TIPO+"0"+FNA->FNA_SEQ))
					    	RecLock("SN3",.F.)
					    	SN3->N3_PRODANO	:= Round( NoRound( FNA->FNA_QUANTD , nDecQtd+1 ) , nDecAcm )
					    	MsUnlock()
					    EndIf
					EndIf

					//Restaura o Ìndice e registro atual
					FNA->(dbSetOrder(1))		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
					FNA->(dbGoTo(nRecFNA))

					RecLock("FNA",.F.)
					FNA->FNA_QUANTD := nQtdEst
					MsUnlock()
				EndIf

				//Altera a varÌavel de controle do laÁo, para n„o gravar apontamentos para todas as moedas
				nX := Len(aTxDepr)

			Case aMemFNA[4] == "P9" //Estorno de apontamentos de produÁ„o
				//Array utilizado para salvar os dados do apontamento estornado
				aAptoEst := Array(Len(aTxDepr),6)

				//Array utilizado para salvar os valores do apontamento estornado
				aValDepr := Array(Len(aTxDepr))

				FNA->(dbSetOrder(2))		//FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD+DTOS(FNA_DATA)
				If FNA->(MsSeek( aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]+DTOS(aRecFNA[9]) ))
					While FNA->(!EoF())	.And. (FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD)+DTOS(FNA->FNA_DATA)) == aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]+DTOS(aRecFNA[9])
						If FNA->FNA_OCORR $ "P2|P3|P4|"
							RecLock("FNA",.F.)
							FNA->FNA_ESTORN := "1"
							MsUnlock()

							//Moeda do apontamento estornado
							nMoedaEst := Val(FNA->FNA_MOEDA)

							If nMoedaEst == 1
								nRecEst := FNA->(Recno())
							EndIf

							//Salva os dados do apontamento estornado
							aAptoEst[nMoedaEst,1] := FNA->FNA_DTPERI
							aAptoEst[nMoedaEst,2] := FNA->FNA_DTPERF
							aAptoEst[nMoedaEst,3] := FNA->FNA_QUANTD
							aAptoEst[nMoedaEst,4] := FNA->FNA_COEFIC
							aAptoEst[nMoedaEst,5] := FNA->FNA_VALOR
							aAptoEst[nMoedaEst,6] := FNA->FNA_MOEDA

							lGerouEst := .T.

							//Salva os valores a subtrair do saldo das contas
							aValDepr[nMoedaEst] := FNA->FNA_VALOR
						EndIf
						FNA->(dbSkip())
					EndDo

					//Contabiliza o apontamento estornado
					FNA->(dbGoTo(nRecEst))

					If FNA->FNA_OCORR $ "P2|P3|" .and. cPaisLoc <> "BOL"
						If SN3->(MsSeek(FNA->FNA_FILIAL+FNA->FNA_CBASE+FNA->FNA_ITEM+FNA->FNA_TIPO+"0"+FNA->FNA_SEQ))
							If  SN3->N3_TIPO $ cTipoFiscal
								nOcorr := "06"
							Elseif SN3->N3_TIPO $ cTipoGerenc
								nOcorr := "20"
							Endif						
							If SN4->(MsSeek( FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+DTOS(FNA_DATA))+nOcorr+FNA->FNA_SEQ ))
								While SN4->(!EoF())	;
									.And. SN4->(N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+DTOS(N4_DATA)+N4_OCORR+N4_SEQ)	;
									== FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+DTOS(FNA_DATA))+nOcorr+FNA->FNA_SEQ
									Do Case
										Case FNA->FNA_OCORR == "P2"
											If SN4->N4_TIPOCNT == "3"
												If (SN3->N3_RATEIO == "1")
													//Contabiliza o rateio da despesa de depreciaÁ„o
													cPadrao := "87B"
													ATFRTMOV(	SN3->N3_FILIAL,;
																SN3->N3_CBASE,;
																SN3->N3_ITEM,;
																SN3->N3_TIPO,;
																SN3->N3_SEQ,;
																SN4->N4_DATA,;
																SN4->N4_IDMOV,;
																,;
																.T.,;
																"2",;
																nHdlPrv,;
																cLoteATF,;
																@nTotal,;
																"0",;
																"ATFA110",;
																cPadrao,lOnOff				)
												Else
													cPadrao := "877"		//Estorno de apontamento de produÁ„o

													//Verifica se o lanÁamento padr„o est· configurado
													lPadrao := VerPadrao(cPadrao)

													If lPadrao .And. lOnOff
														nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
													EndIf
												EndIf

											Else
												cPadrao := "877"		//Estorno de apontamento de produÁ„o

												//Verifica se o lanÁamento padr„o est· configurado
												lPadrao := VerPadrao(cPadrao)

												If lPadrao .And. lOnOff
													nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
												EndIf
											EndIf
										Case FNA->FNA_OCORR == "P3"
											//Controla se ir· apagar as datas de exaust„o
											lEstExaust := .T.

											cPadrao := "878"		//Estorno de apontamento de encerramento de produÁ„o

											//Verifica se o lanÁamento padr„o est· configurado
											lPadrao := VerPadrao(cPadrao)

											If lPadrao .And. lOnOff
												nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
											EndIf
									EndCase

									//Apaga os registros das movimentaÁıes
									RecLock("SN4",.F.)
									dbDelete()
									MsUnlock()
									SN4->(dbSkip())

								EndDo
							EndIf

							//Verifica o tipo de saldo a ser atualizado
							Do case
				                Case SN3->N3_TIPO $ cTipoFiscal
									cN5_TIPO := "4"
								Case SN3->N3_TIPO $ cTipoGerenc
									cN5_TIPO := "Y"
								Case SN3->N3_TIPO == "08"
									cN5_TIPO := "K"
								Case SN3->N3_TIPO == "09"
									cN5_TIPO := "L"
								OtherWise
									cN5_TIPO := "4"
				     		EndCase

							//GravaÁ„o na tabela SN5 da conta Despesa de DepreciaÁ„o
							AtfSaldo(SN3->N3_CDEPREC,FNA->FNA_DATA,cN5_TIPO,aValDepr[1],aValDepr[2],aValDepr[3],aValDepr[4],aValDepr[5],"-",FNA->FNA_COEFIC,SN3->N3_SUBCDEP,,SN3->N3_CLVLDEP,SN3->N3_CCDESP,"3",aValDepr)

							//GravaÁ„o na tabela SN5 da conta DepreciaÁ„o Acumulada
							AtfSaldo(SN3->N3_CCDEPR,FNA->FNA_DATA,cN5_TIPO,aValDepr[1],aValDepr[2],aValDepr[3],aValDepr[4],aValDepr[5],"-",FNA->FNA_COEFIC,SN3->N3_SUBCCDE,,SN3->N3_CLVLCDE,SN3->N3_CCCDEP,"4",aValDepr)
						EndIf
					ElseIf cPaisLoc <> "BOL"
						cPadrao := "879"		//Estorno de apontamento de complemento de produÁ„o

						//Verifica se o lanÁamento padr„o est· configurado
						lPadrao := VerPadrao(cPadrao)

						If lPadrao .and. lOnOff
							nTotal += DetProva(nHdlPrv,cPadrao,"ATFA110",cLoteAtf)
						EndIf
					EndIf
				EndIf

				If FNA->(MsSeek( aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7] ))
					While FNA->(!EoF())	.And. (FNA->(FNA_FILIAL+FNA_CBASE+FNA_ITEM+FNA_TIPO+FNA_SEQ+FNA_SEQREA+FNA_TPSALD)) == aRecFNA[1]+aRecFNA[2]+aRecFNA[3]+aRecFNA[4]+aRecFNA[5]+aRecFNA[6]+aRecFNA[7]
						If FNA->FNA_OCORR $ "P2|P3|P4|" .And. FNA->FNA_ESTORN != "1"
							//Atualiza a vari·vel que acumula a depreciaÁ„o total
							aVrdAcm[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR
							//Verifica a moeda do apontamento
							If FNA->FNA_MOEDA == aRecFNA[8]
								//Atualiza a vari·vel que acumula a produÁ„o total
								nProdAcm += FNA->FNA_QUANTD
							EndIf
							If (FNA->FNA_DATA > dUltDepr .And. FNA->FNA_DATA <= LastDay(dUltDepr+1))
								//Atualiza a vari·vel que acumula a depreciaÁ„o total
								aVrdMes[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR
								//Verifica a moeda do apontamento
								If FNA->FNA_MOEDA == aRecFNA[8]
									//Atualiza a vari·vel que acumula a produÁ„o mensal
									nProdMes += FNA->FNA_QUANTD
								EndIf
							EndIf
						EndIf
						If FNA->FNA_OCORR == 'P5' .and. !Empty(FNA->FNA_MOEDA)
							aVrdAcm[Val(FNA->FNA_MOEDA)] += FNA->FNA_VALOR
							nProdAcm += FNA->FNA_QUANTD
						Endif
						FNA->(dbSkip())
					EndDo

					//Restaura o Ìndice e registro atual
					FNA->(dbSetOrder(1))		//FNA_FILIAL+FNA_IDMOV+FNA_ITMOV+FNA_MOEDA+FNA_OCORR
					FNA->(dbGoTo(nRecFNA))

					If SN3->(MsSeek(FNA->FNA_FILIAL+FNA->FNA_CBASE+FNA->FNA_ITEM+FNA->FNA_TIPO+"0"+FNA->FNA_SEQ))
						RecLock("SN3",.F.)
						If cPaisLoc <> "BOL"
							For nY := 1 To Len(aVrdMes)
								If SN3->(FieldPos("N3_VRDMES"+cValToChar(nY)) > 0)
									SN3->&("N3_VRDMES"+cValToChar(nY)) := aVrdMes[nY]
								EndIf
							Next nY
	
							For nY := 1 To Len(aVrdAcm)
								If SN3->(FieldPos("N3_VRDACM"+cValToChar(nY)) > 0)
									SN3->&("N3_VRDACM"+cValToChar(nY)) := aVrdAcm[nY] 
								EndIf
							Next nY
					    	
					    	If lEstExaust
								SN3->N3_DEXAUST := CtoD("//")
								SN3->N3_FIMDEPR := CtoD("//")
							EndIf
						EndIf
						SN3->N3_PRODMES	:= Round( NoRound( nProdMes , nDecQtd+1 ) , nDecMes )
					   	SN3->N3_PRODACM	:= Round( NoRound( nProdAcm , nDecQtd+1 ) , nDecAcm )
				    	MsUnlock()
					EndIf
				EndIf
		EndCase
	EndIf

	If lGerouMov
		RecLock("FNA",.F.)
		FNA->FNA_VALOR := SN4->&("N4_VLROC"+cValToChar(nX))
		Msunlock()
	EndIf

	If lGerouEst
		RecLock("FNA",.F.)
		FNA->FNA_DTPERI := aAptoEst[nX,1]
		FNA->FNA_DTPERF := aAptoEst[nX,2]
		FNA->FNA_QUANTD := aAptoEst[nX,3]
		FNA->FNA_COEFIC := aAptoEst[nX,4]
		FNA->FNA_VALOR  := aAptoEst[nX,5]
		Msunlock()
	EndIf

Next nX

//Incrementa o IT. Mov. do apontamento
aMemFNA[2] := Soma1(aMemFNA[2])

Return
