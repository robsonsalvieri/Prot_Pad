#include "FINR810.CH"
#Include "FIVEWIN.Ch"

Static lFWCodFil := FindFunction("FWCodFil")

/*


Ŀ
Funo	  Finr810	 Autor  Alessandro B. Freire   Data  13/07/97 
Ĵ
Descrio  Impressao relatorio mov. de caixa.								  
Ĵ
Sintaxe	  FinR810()																  
Ĵ
 Uso		  FINR810																	  
ٱ


*/
Function FinR810

LOCAL cString	:="SE5"
LOCAL cDesc1	:= OemToAnsi(STR0001)  //"Este programa tem como objetivo imprimir o        "
LOCAL cDesc2	:= OemToAnsi(STR0002)  //"movimento de caixa.                               "
LOCAL cDesc3	:= ""

//Ŀ
// Define Variaveis 
//
PRIVATE Titulo 	:= OemToAnsi(STR0003)  //"Relatorio de Movimento de Caixa No. "
PRIVATE Cabec1 	:= OemToAnsi(STR0004)  // "Conta           C.C.      Depto Descricao                                                       Entrada                 Saida"
PRIVATE Cabec2 	:= ""
PRIVATE aReturn	:= { OemToAnsi(STR0005), 1,OemToAnsi(STR0006), 2, 2, 1, "",1 }  //"Zebrado"###"Administracao"
PRIVATE cPerg		:= "FIR810"
PRIVATE nLastKey	:= 0
PRIVATE nomeprog	:= "FINR810"
PRIVATE cIndiceSE5:= ""
PRIVATE Tamanho	:= "G"

//Ŀ
// Variaveis utilizadas para Impresso do Cabealho e Rodap	  
//
cbtxt 	:= SPACE(10)
cbcont	:= 0
li 		:= 80
m_pag 	:= 0

//Ŀ
// Verifica as perguntas selecionadas 
//
pergunte(cPerg,.F.)
//Ŀ
// Variaveis utilizadas para parametros								  
// mv_par01				// Data				?							  
// mv_par02				// Banco Inicial	?							  
// mv_par03				// Agencia Inicial?							  
// mv_par04				// Conta Inicial	?							  
// mv_par05				// Banco Final 	?							  
// mv_par06				// Agencia Final	?							  
// mv_par07				// Conta Final 	?							  
//

//Ŀ
// Envia controle para a funcao SETPRINT 
//
wnrel := "FINR810"            //Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho)

If nLastKey == 27
	 Return( .F. )
End

SetDefault(aReturn,cString)

If nLastKey == 27
	Return( .F. )
Endif

RptStatus({|lEnd| Fa810Imp(@lEnd,wnRel,cString)},Titulo)
dbSelectArea("SA6")
dbSetOrder(1)
Return( .T. )

/*/


Ŀ
Funo	  Fa810Imp  Autor  Alessandro B. Freire   Data  04/04/97 
Ĵ
Descrio  Impressao relatorio mov. historica do caixa. (J. Santos)   
Ĵ
Sintaxe e  FA810Imp(lEnd,wnRel,cString)										  
Ĵ
Parametros Parametro 1 - lEnd	 - Ao do CodeBlock 					  
			  Parametro 2 - wnRel	 - Ttulo do relatrio					  
			  Parametro 3 - cString - Mensagem 								  
Ĵ
 Uso		  FINR810																	  
ٱ


*/
FUNCTION FA810Imp(lEnd,wnRel,cString)

LOCAL nMovCx	:= 0		// No. do Relatorio
LOCAL nTotPgE	:= 0		// Total da Pagina	 - Entrada
LOCAL nTotPgS	:= 0		// Total da Pagina	 - Saida
LOCAL nTotalE	:= 0		// Total do Relatorio - Entrada
LOCAL nTotalS	:= 0		// Total da Relatorio - Saida
LOCAL nTotal	:= 0		// Total do Relatorio
LOCAL cNatur	:= ""    // Natureza da movimentacao com a picture correta
LOCAL cIndex	:= ""    // Indice temporario
LOCAL nIndex	:= 0		// Ordem do Indice temporario
LOCAL cFiltro	:= ""    // Filtro do Indice temporario
LOCAL cCusto
Local nI		:= 0

dbSelectArea("SE5")

nIndex	:= RetIndex("SE5")
cIndex	:= CriaTrab(NIL,.F.)
cChave	:= "DTOS(E5_DATA)+E5_NATUREZ"

cFiltro	:= 'E5_FILIAL == "'     + xFilial()          + '" .And. '
cFiltro	+= 'DTOS(E5_DATA) == "' + DtoS( mv_par01 )   + '" .And. '
cFiltro	+= 'E5_BANCO+E5_AGENCIA+E5_CONTA >= "'
cFiltro	+= mv_par02+mv_par03+mv_par04 					+ '" .And. '
cFiltro	+= 'E5_BANCO+E5_AGENCIA+E5_CONTA <= "'
cFiltro	+= mv_par05+mv_par06+mv_par07 					+ '"'

IndRegua("SE5",cIndex,cChave,,cFiltro,OemToAnsi(STR0007) )  //"Selecionando Registros"

#IFNDEF TOP
	dbSetIndex( cIndex +OrdBagEXt())
#ENDIF
dbSetOrder( nIndex + 1 )

dbGoTop()

/*
Ŀ
 No h nenhum registro				
 */
If Eof() .And. Bof()
	RetIndex("SE5")
	dbSetOrder(1)
	fErase( cIndex + OrdBagExt() )
	Return( .T. )
EndIf
/*
Ŀ
 Verifica se existem os parametros. Caso negativo, os cria no SX6.			
*/
Titulo += GetMv( "MV_PRXREL" ) +  " "
Titulo += "/"+Str(Year(dDataBase),4)
While ! Eof()

	IF lEnd
		Exit
	End

	If SE5->E5_TIPODOC $ "BA/JR/CM/MT/TL/D2/J2/M2/V2"
		dbSkip()
		Loop
	Endif

	If li > 58
		cabec(titulo,cabec1,cabec2,nomeprog,tamanho,18)
	EndIf

	@li,0 PSAY r810NatPic()
	cCusto := SE5->E5_CCUSTO
	If SE5->E5_TIPODOC == "VL" .And. SE5->E5_RECPAG == "R"
		dbSelectArea("SE1")
		dbSeek(xFilial()+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO)
		cCusto := SE1->E1_CCUSTO
	Elseif SE5->E5_TIPODOC == "VL" .And. SE5->E5_RECPAG == "P"
		dbSelectArea("SE2")
		dbSeek(xFilial()+SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO+SE5->E5_CLIFOR)
		cCusto := SE2->E2_CCUSTO
	Endif
	dbSelectArea("SE5")
	@li,16 PSAY SubStr(cCusto,1,9)
	dbSelectArea("SI3")
	dbSeek(xFilial()+Substr(cCusto,1,9))
	@li,27 PSAY SE2->E2_CCUSTO
	dbSelectArea("SE5")
	aDesc := r810Desc()
	For nI := 1 to Len( aDesc )
		If nI == 2
			li++
		EndIf
		@li,33 PSAY Substr(aDesc[nI],45)
	Next

	If SE5->E5_RECPAG == "R"
		@li,079 PSAY SE5->E5_VALOR Picture "@E 99,999,999,999,999.99"
		nTotPgE	+= SE5->E5_VALOR
	Else
		@li,102 PSAY SE5->E5_VALOR Picture "@E 99,999,999,999,999.99"
		nTotPgS	+= SE5->E5_VALOR
	EndIf
	dbSkip()
	li++
	If li > 56 .or. Eof()
		li++
		@li,029 PSAY OemToAnsi(STR0008)  //"TOTAIS DA PAGINA :"
		@li,079 PSAY nTotPgE Picture "@E 99,999,999,999,999.99"
		@li,102 PSAY nTotPgS Picture "@E 99,999,999,999,999.99"
		nTotalE	+= nTotPgE
		nTotalS	+= nTotPgS
		nTotPgE	:= 0
		nTotPgS	:= 0
	EndIf
EndDo
li++
@li,029 PSAY OemToAnsi(STR0009)  //"TOTAIS DO RELATORIO"
@li,079 PSAY nTotalE Picture "@E 99,999,999,999,999.99"
@li,102 PSAY nTotalS Picture "@E 99,999,999,999,999.99"
li++
Roda(cbcont,cbtxt,"G")
GravaSX6()
dbSelectArea("SE5")
If aReturn[5] == 1
	 Set Printer to
	 dbCommitAll()
	 OurSpool(wnrel)
Endif
MS_FLUSH()
RetIndex("SE5")
fErase( cIndex + OrdBagExt() )
dbSetOrder(1)
Return( .T. )

/*/


Ŀ
Funo	 r810NatPic Autor  Alessandro B. Freire   Data  13/07/97 
Ĵ
Descrio  Retorna a natureza coma picture correta						  
Ĵ
Sintaxe e  a810NatPic()															  
Ĵ
 Uso		  FINR810																	  
ٱ


*/
Static Function r810NatPic()

Local cMasc := Trim( GetMv("MV_MASCNAT") )
Local nI 	:= 1
Local nPos	:= 0
Local cRet	:= ""
Local nTam	:= 0

For nI := 1 to Len( cMasc )
	nPos	:= Val(SubStr( cMasc,nI,1 ))
	cRet	+= SubStr( SE5->E5_NATUREZ, 1+nTam, nPos )+"."
	nTam	+= nPos
Next

cRet := SubStr( cRet,1,Len(cRet)-1)

Return( cRet )

/*/


Ŀ
Funo	 r810Desc	 Autor  Alessandro B. Freire   Data  13/07/97 
Ĵ
Descrio  Retorna o Fornecedor + Historico 								  
Ĵ
Sintaxe e  r810Desc()																  
Ĵ
 Uso		  FINR810																	  
ٱ


*/
Static Function r810Desc()
Local cRet	:= ""
Local aRet	:= {}

If SE5->E5_RECPAG == "R"
	dbSelectArea("SA1")
	dbSetOrder(1)
Else
	dbSelectArea("SA2")
	dbSetOrder(1)
EndIf

If dbSeek( xFilial() + SE5->E5_CLIFOR + SE5->E5_LOJA )
	If SE5->E5_RECPAG == "R"
		cRet := Trim( A1_NOME ) + " - "
	Else
		cRet := Trim( A2_NOME ) + " - "
	EndIf
EndIf

cRet += Trim( SE5->E5_HISTOR )

AAdd( aRet, SubStr( cRet, 01,49 ) )
If Len( cRet ) > 49
	AAdd( aRet, SubStr( cRet, 50,30 ) )
EndIf

dbSelectArea("SE5")

Return( aRet )

/*/


Ŀ
Funo	 GravaSx6	 Autor  Alessandro B. Freire   Data  15/07/97 
Ĵ
Descrio  Grava os os proximos numeros no SX6.							  
Ĵ
Sintaxe e  GravaSX6 																  
Ĵ
 Uso		  FINR810																	  
ٱ


*/
Static Function GravaSX6()
LOCAL cNextRel := GetMv("MV_PRXREL")

PUTMV("MV_PRXREL",StrZero(Val(cNextRel)+1,6))

Return(.T.)



