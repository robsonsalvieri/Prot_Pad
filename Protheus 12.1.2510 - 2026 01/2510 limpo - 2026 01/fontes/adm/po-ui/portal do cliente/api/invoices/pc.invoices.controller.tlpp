#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include 'pc.invoices.controller.ch'

Namespace portal.cliente.invoices
Using Namespace gfin.util

//--------------------------------------------------------------------
/*/{Protheus.doc}Invoices
EndPoint para obter as notas fiscais do cliente.

@body deve ser encaminhado em json com os atributos:
	{
		"customerid": character "código do cliente",
		"storeid": character "loja do cliente",
		"branchid": array "filiais do sistema a ser consideradas na busca",
		"filter": array para filtro com os atributos
				[ atributo a ser filtrado, operador, valor]
				Os atributos possíveis de filtro são:
				"issueDate": character "data de emissão no formato YYYYMMDD",
				"invoicenumber": número do documento
	}
@return JSON,
	hasNext: logical
	items:[
		{
			"invoiceBranch": character,
			"invoiceNumber": character,
			"invoiceSerie": character,
			"invoiceValue": numeric,
			"invoiceCurrency": character,
			"issueDate": character
		}
	]
@author Renato Ito
@since 19/08/2020
/*/
//--------------------------------------------------------------------
@Post('/api/pc/v1/Invoices/')
Function allInvoices()
	Local invoicesService	As Object
	Local response			As Json
	Local body				As Json
	Local bodyContent		As Character

	body := JsonObject():New()
	bodyContent := body:fromJson(oRest:GetBodyRequest())

	//Valida se foi recebido um JSON
	If !(Empty(bodyContent))
		response := answerErrorFormat(403, STR0001, bodyContent)
	Else
		invoicesService := InvoicesService():getInstance()

		If (oRest:getQueryRequest()['limit'] != Nil)
			If Val(oRest:getQueryRequest()['limit']) >= 50
				invoicesService:setLimit(50)
			Else
				invoicesService:setLimit(Val(oRest:getQueryRequest()['limit']))
			EndIf
		Else
			invoicesService:setLimit(25)
		EndIf

		If (oRest:getQueryRequest()['page'] != Nil)
			invoicesService:setPage(Val(oRest:getQueryRequest()['page']))
		Else
			invoicesService:setPage(1)
		EndIf

		response := invoicesService:getInvoices(body, oRest:getHeaderRequest()["Authorization"])
	EndIf

	AnswerRest(response)
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} getInvoiceProducts
EndPoint para obter os detalhes da nota fiscal do cliente.

@body
	{
		"branchId": Character - Filial da nota,
		"invoiceNumber": Character - Número da nota,
		"serie": Character - Série da nota,
		"customerId": Character - Código do cliente,
		"storeId": Character - Loja do cliente
	}

@return Nil

@author richard.lopes
@since 21/08/2020
/*/
//-------------------------------------------------------------------
@Post('/api/pc/v1/InvoiceProducts')
Function getInvoiceProducts()
	Local invoicesService As Object
	Local body			  As Json
	Local response		  As Json
	Local bodyContent	  As Character

	body 		:= JsonObject():New()
	bodyContent := body:FromJson(oRest:GetBodyRequest())

	//Valida se foi recebido um JSON
	If !(Empty(bodyContent))
		response := AnswerErrorFormat(400, STR0001, bodyContent)
	Else
		invoicesService := InvoicesService():getInstance()
		response := InvoicesService:getCustomerInvoiceProducts(body, oRest:getHeaderRequest()["Authorization"])
	EndIf

	AnswerRest(response)
Return

/*/{Protheus.doc} getInvoiceNfe
	Retorna a DANFE + XML da nota 
	@type  Function
	@author Vitor Duca
	@since 21/12/2023
	@version 1.0
/*/
@get('/api/pc/v1/invoices/nfe')
Function getInvoiceNfe()
	Local jQueryParams 	:= oRest:getQueryRequest()			as Json
	Local oService		:= InvoicesService():getInstance() 	as Object
	Local jResponse		:= JsonObject():new()				as Json
	Local cSerie		:= ""								as Character
	Local cNumber		:= ""								as Character
	Local cModel		:= ""								as Character

	if !totvs.framework.api.treports.wellKnow():isAuthOff()
		If !jQueryParams:hasProperty("serie")
			jQueryParams["serie"] := ""
		Endif

		If !jQueryParams:hasProperty("numero") 
			jQueryParams["numero"] := ""
		Endif

		If !jQueryParams:hasProperty("modelo") 
			jQueryParams["modelo"] := ""
		Endif
	
		cSerie := jQueryParams["serie"]
		cNumber := jQueryParams["numero"]
		cModel := jQueryParams["modelo"]

		jResponse := oService:getXmlDanfe(cSerie, cNumber, cModel)

		AnswerRest(jResponse)
	Else
		oRest:updateKeyHeaderResponse("Content-Type", "application/json")
		jResponse := answerFormat(.F., 400, STR0002, STR0003 + ' <br> ' + STR0004 + ' <a href="https://tdn.totvs.com/pages/viewpage.action?pageId=718992131" target="_blank">Funcionalidades - Portal do cliente</a> (' + STR0005 + ' 3 - Notas fiscais)') //"Não foi possivel baixar a Danfe e XML"#"Para utilizar este serviço a segurança do socket do REST deve estar ligada (SECURITY=1)"#"para mais detalhes acesse"#"Tópico"
		AnswerRest(jResponse)
	Endif	

Return

/*/
{Protheus.doc} getVersion
Retorna a versão dos fontes.

*Favor, atualizar versão quando
algo no invoices for alterado.

@return cVersion, Numeric.

@author luiz.nai
@since 29/03/2022
/*/
Function getVersion() As Numeric
	Local cVersion := "20220329"	as Character
	
	If FindClass("totvs.framework.api.treports.wellKnow")
		cVersion 		:= "20231226"
	Endif
Return Val(cVersion)

/*/
{Protheus.doc} config
retorna as configurações das invoices

@author renato.ito
@since 06/10/2023
/*/
@POST('/api/pc/v1/invoices/config')
Function config()
	Local jConfig  := JsonObject():new()     as Json
	Local oService                           as Object
	Local lInvRel  := ExistBlock("PCINVREL") as Logical
	Local lInvObs  := ExistBlock("PCINVOBS") as Logical

	If lInvRel .Or. lInvObs
		oService := backoffice.fin.pc.config.PCConfig():new()
		oService:setBody(oRest:GetBodyRequest())
	EndIf

	If lInvRel
		jConfig["rowReports"] := oService:getRowReports("PCINVREL")
	EndIf

	If lInvObs .And. MethIsMemberOf(oService, "getRowObs")
		jConfig["rowObs"] := oService:getRowObs("PCINVOBS")
	EndIf

	If lInvRel .Or. lInvObs
		FreeObj(oService)
	EndIf

	oRest:setKeyHeaderResponse("Content-Type", "application/json")
	oRest:setResponse(jConfig)
Return
