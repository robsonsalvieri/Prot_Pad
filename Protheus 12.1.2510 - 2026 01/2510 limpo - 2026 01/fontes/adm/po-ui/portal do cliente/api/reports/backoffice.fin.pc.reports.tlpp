#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.fin.pc.reports.ch'

namespace backoffice.fin.pc.reports

/*/{Protheus.doc} apiGetReports
    Endpoint para retornar as opções de relatórios que serão utilizadas no portal do cliente

    @type  Function
    @author Victor Azevedo
    @since 23/08/2023
    @version Protheus 12
/*/
@POST('/api/pc/v1/reports/')
Function apiGetReports()
	Local oService                        as Object
	Local jResponse := JsonObject():New() as Json

	jResponse["items"] := {}

	If ExistBlock("PCREPORTS")
		oService           := backoffice.fin.pc.config.PCConfig():new()
		oService:setBody(oRest:GetBodyRequest())
		jResponse["items"] := oService:getRowReports("PCREPORTS")
		FreeObj(oService)
	EndIf

	oRest:setKeyHeaderResponse("Content-Type", "application/json")
	oRest:setResponse(jResponse)
Return

/*/{Protheus.doc} apiPostReports
    Endpoint para efetivar a extração do relatório selecionado pelo cliente

    @type  Function
    @author Victor Azevedo
    @since 23/08/2023
    @version Protheus 12
/*/
@POST('/api/pc/v1/reports/download/')
Function apiPostReports()
	Local oService  := backoffice.fin.pc.config.PCConfig():new() as Object
	Local jResponse as Json

	oService:setBody(oRest:GetBodyRequest())
	jResponse := oService:getReport()

	If jResponse:hasProperty("file") .And. jResponse:hasProperty("fileName")
		oRest:SetKeyHeaderResponse("Content-Disposition", "attachment; filename=" + jResponse["fileName"] + "")
		oRest:SetKeyHeaderResponse("Content-Type", "application/pdf" )
		oRest:SetResponse(jResponse)
	Else
		oRest:setFault('{"' + STR0001 + '":"' + STR0004 + '"}') // "response" # "Não foi possível realizar o download do arquivo"
		oRest:setStatusCode(403)
	EndIf

Return
