#Include 'tlpp-core.th'
#Include 'backoffice.ngf.preload.service.CH'

Namespace backoffice.ngf.preload
USING NAMESPACE gfin.api.tables
USING NAMESPACE totvs.protheus.backoffice.ngf.report
USING NAMESPACE totvs.protheus.backoffice.fin.functions

/*/{Protheus.doc} NGFPreload
	apresenta os parâmetros e o versionamento desejado
	@type  Static Function
	@daniele.ferreira
	@since 05/04/2022
	@param jPreLoad, Json, pré carregamento da página
	@param jVersion, Json, número da versão
	@param jParams, Json, parâmetros desejados
	@return jPreLoad, Json, apresenta o json montado com os parâmetros e com o versionamento
/*/
Function NGFPreload() as Json
	Local jPreLoad as Json

	Set(_SET_DATEFORMAT, 'dd/mm/yyyy')

	jPreLoad := JsonObject():new()
	jPreLoad["result"]					:= .T.
	jPreLoad["response"]				:= JsonObject():New()
	jPreLoad["response"]['versions']	:= versionsNGF()
	jPreLoad["response"]['params']		:= paramsNGF()
	jPreLoad["response"]['jobs']		:= jobsNGF()
Return jPreLoad

/*/{Protheus.doc} jobsNGF
	retorna os jobs configurados no ambiente
	@type  Static Function
	@author renato.ito
	@since 06/05/2022
	@return jJobs, json, lista de jobs e status ativo ou inativo
/*/
Static Function jobsNGF() as Json
	Local jJobs as Json
	Local aJobsRoutines := {} As Array
	Local nRoutines := 0 As Numeric
	Local cCodeSchedule := "" As Character

	jJobs := JsonObject():new()
	Aadd(aJobsRoutines, "FINA713")
	Aadd(aJobsRoutines, "FINA715")
	Aadd(aJobsRoutines, "FINA717")
	Aadd(aJobsRoutines, "FINA718")

	For nRoutines := 1 to len(aJobsRoutines)
		jJobs[Lower(aJobsRoutines[nRoutines])] := JsonObject():new()
		cCodeSchedule := FwSchdByFunction(aJobsRoutines[nRoutines]+"('" + cEmpAnt + "')")

		If Empty(cCodeSchedule)
			cCodeSchedule := FwSchdByFunction(aJobsRoutines[nRoutines]+'("' + cEmpAnt + '")')
		Endif

		If Empty(cCodeSchedule)
			jJobs[Lower(aJobsRoutines[nRoutines])]["config"] := 'no'
		Else
			jJobs[Lower(aJobsRoutines[nRoutines])]["config"] := 'yes'
		Endif

		If FindFunction("totvs.protheus.backoffice.fin.functions.LastExecSched()") .and. !Empty(cCodeSchedule)
			jJobs[Lower(aJobsRoutines[nRoutines])]["exec"] := LastExecSched(cCodeSchedule)
		Endif
	Next nRoutines

Return jJobs

/*/{Protheus.doc} paramsNGF
	retorna os parâmetros utilizados
	@type  Static Function
	@author renato.ito
	@since 06/05/2022
	@return jJobs, json, lista de parametros e conteúdo
/*/
Static Function paramsNGF() as Json
	Local jParams  as Json

	jParams := JsonObject():new()

	// Aplicação Empréstimos
	jParams['MV_APLCAL1'] := SuperGetMV('MV_APLCAL1', , "")
	jParams['MV_APLCAL2'] := SuperGetMV('MV_APLCAL2', , "")
	jParams['MV_APLCAL3'] := SuperGetMV('MV_APLCAL3', , "")
	jParams['MV_APLCAL4'] := SuperGetMV('MV_APLCAL4', , "")
	jParams['MV_APLCAL5'] := SuperGetMV('MV_APLCAL5', , "")
	jParams['MV_EMPCAL1'] := SuperGetMV('MV_EMPCAL1', , "")
	jParams['MV_EMPCAL2'] := SuperGetMV('MV_EMPCAL2', , "")
	jParams['MV_EMPCAL3'] := SuperGetMV('MV_EMPCAL3', , "")
	jParams['MV_EMPCAL4'] := SuperGetMV('MV_EMPCAL4', , "")
Return jParams

/*/{Protheus.doc} versionsNGF
	retorna as versões dos componentes
	@type  Static Function
	@author renato.ito
	@since 06/05/2022
	@return jJobs, json, lista de componentes e versão
/*/
Static Function versionsNGF() as Json
	Local jVersion  as Json

	jVersion := JsonObject():new()
	// template
	jVersion["ngf.applicationsLoans.template"]	:= applicationsLoansTemplate()
	jVersion["ngf.cashFlow.simulation"] := cashFlowSimulation()

	// SEI->EI_CALCUSR
	jVersion["ngf.applicationsLoans.sei.checkCalcUsr"]	:= checkCalcUsr()

	// balance
	jVersion["ngf.balances.period"] := balancePeriod()
	// cashflow
	jVersion["ngf.cashFlow.consolidated"] := cashFlowConsolidated()
	// cashflow
	jVersion["ngf.cashFlow.sales.purchases"] := cashFlowSalesPurchases()
	// orders
	jVersion["ngf.orders.sales.purchases"] := ordersSalesPurchases()
	// orders machine
	jVersion["gfin.api.orders.machine"] := ordersSalesPurchasesMachine()
	// Tables
	jVersion["ngf.tables"] := ngfTables()
	// Report
	jVersion["ngf.report"] := ngfReport()
	// API pagar
	jVersion["ngf.api.payables"] := ngfAPIPayables()
	// Boleto Hibrido
	jVersion["ngf.bol.hybrid"] := ngfHybridBills()
	// Monitor de boletos
	jVersion["ngf.monitor.bills"] := ngfMonitorBills()
	// Contabilização detprova
	jVersion["detprova.poui"] := detProvaPoUi()

Return jVersion


/*/{Protheus.doc} applicationsLoansTemplate
	verifica a versão para uso dos templates
	@author renato.ito
	@since 05/04/2022
	@return nVersion, numeric, versão
/*/
Static Function applicationsLoansTemplate() as Numeric
	Local nVersion := 0 as Numeric

	If AliasInDic('F7B') .And. FindFunction('gfin.api.tables.getVersion') .And. SEH->(FieldPos("EH_TEMPLT")) > 0
		nVersion := gfin.api.tables.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} checkCalcUsr
	Verifica se a SEI tem o campo EI_CALCUSR
	@author luiz.fribeiro
	@since 04/02/2025
	@return lCalcUsr, Logical, true ou false
/*/
Static Function checkCalcUsr() as Logical
	Local lCalcUsr := .F. as Logical

	If SEI->(FieldPos("EI_CALCUSR")) > 0 .and. findfunction("F716Ver") .and. F716Ver() <> "1"
		lCalcUsr := .T.
	EndIf
Return lCalcUsr

/*/{Protheus.doc} cashFlowSimulation
	verifica a versão para uso do simulação do fluxo de caixa
	@author daniel.muniz
	@since 06/07/2022
	@return nVersion, numeric, versão
/*/
Static Function cashFlowSimulation() as Numeric
	Local nVersion := 0 as Numeric

	If AliasInDic('F7D') .And. AliasInDic('F7E') .And. FindFunction('gfin.api.tables.getVersion')
		nVersion := gfin.api.tables.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} balancePeriod
	verifica a versão para uso do endpoint de balances
	@author vinicius.prado
	@since 28/07/2022
	@return nVersion, numeric, versão
/*/
Static Function balancePeriod() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('gfin.api.balances.getVersion') .And. F76->(FieldPos("F76_BLQLIB")) > 0
		nVersion := gfin.api.balances.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} cashFlowSalesPurchases
	verifica a versão para uso uso do endpoint de cashflow
	@author vinicius.prado
	@since 28/07/2022
	@return nVersion, numeric, versão
/*/
Static Function cashFlowSalesPurchases() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('gfin.api.cashFlow.getVersion') .And. F76->(FieldPos("F76_BLQLIB")) > 0
		nVersion := gfin.api.cashFlow.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} ordersSalesPurchases
	verifica a versão para uso do endpoint de orders
	@author vinicius.prado
	@since 28/07/2022
	@return nVersion, numeric, versão
/*/
Static Function ordersSalesPurchases() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('gfin.api.orders.getVersion') .And. F76->(FieldPos("F76_BLQLIB")) > 0
		nVersion := gfin.api.orders.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} ordersSalesPurchasesMachine
	verifica a versão para uso do endpoint de ordersMachine
	@author vinicius.prado
	@since 06/12/2022
	@return nVersion, numeric, versão
/*/
Static Function ordersSalesPurchasesMachine() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('gfin.api.orders.machine.getVersion') .And. SuperGetMV("MV_VEICULO",,"N") == "S"
		nVersion := gfin.api.orders.machine.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} ngfTables
	verifica a versão para uso do endpoint de tables
	@author vinicius.prado
	@since 08/08/2022
	@return nVersion, numeric, versão
/*/
Static Function ngfTables() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('gfin.api.tables.getVersion')
		nVersion := gfin.api.tables.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} cashFlowConsolidated
	verifica a versão para uso do consolidado
	@author renato.ito
	@since 30/08/2022
	@return nVersion, numeric, versão
/*/
Static Function cashFlowConsolidated() as Numeric
	Local nVersion := 0 as Numeric
	If FindFunction('gfin.api.cashFlow.getVersion') .And. FindFunction('totvs.protheus.backoffice.ngf.cashFlow.consolidated')
		nVersion := gfin.api.cashFlow.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} ngfReport
	verifica a versão para uso do endpoint de Report
	@author Vitor Duca
	@since 19/10/2022
	@return nVersion, numeric, versão
/*/
Static Function ngfReport() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('totvs.protheus.backoffice.ngf.report.getVersion')
		nVersion := totvs.protheus.backoffice.ngf.report.getVersion()
	EndIf
Return nVersion

/*/{Protheus.doc} ngfAPIPayables
	verifica a versão para uso dos recursos da API de pagamentos
	@author Renato Ito
	@since 21/12/2023
	@return nVersion, numeric, versão
/*/
Static Function ngfAPIPayables() as Numeric
	Local nVersion := 0 as Numeric

	If FindFunction('F717VldUso') .And. F717VldUso()
		nVersion := 1
	EndIf
Return nVersion


/*/{Protheus.doc} ngfHybridBills
	verifica a versão para uso dos recursos da API de pagamentos
	@author pequim
	@since 21/12/2023
	@return nVersion, numeric, versão
/*/
Static Function ngfHybridBills() as Numeric
	Local cUseHybridBills := "" as Character
	Local nVersion := 0 as Numeric

	cUseHybridBills := GetPvProfString(GetEnvServer(), "HybridBillsHomolog", "0", GetSrvIniName())
	If cUseHybridBills == '1'
		nVersion := 1
	EndIf
Return nVersion

/*/{Protheus.doc} ngfMonitorBills
	Verifica versão do monitor de pendencias para boletos
	@type  Static Function
	@author Vitor Duca
	@since 04/05/2023
	@version 1.0
	@return nVersion, Numeric, Versão compativel para utilização do recurso
/*/
Static Function ngfMonitorBills() As Numeric
	Local nVersion := 0 as Numeric

	If FindFunction("F713VldAlCa") .and. F713VldAlCa()
		nVersion := 1
	Endif

Return nVersion

/*/{Protheus.doc} detProvaPoUi
	Verifica versão dos artefatos para uso do detprova PO-UI.

	@type  Static Function
	@author Vinicius do Prado
	@since 24/07/2023
	@version 1.0
	@return nVersion, Numeric, Versão compativel para utilização do recurso.
/*/
Function detProvaPoUi() As Numeric
	Local nVersion  :=  0  as Numeric
	Local lDicOk    := .F. as Logical
	Local lFuncOk   := .F. as Logical

	// Verifica dicionario
	If CT2->(FieldPos("CT2_INCONS")) > 0 .And.;
	   CT2->(FieldPos("CT2_PROCES")) > 0
	   lDicOk := .T.
	EndIf

	// Verifica funcoes
	If FindFunction("totvs.protheus.backoffice.fin.tabledata.CV8CT2INCONS") .And.;
	   FindFunction("totvs.protheus.backoffice.fin.tabledata.responseCV8CT2INCONS") .And.;
	   FindFunction("CTB102POUI")
		lFuncOk := .T.
	EndIf

	If lDicOk .And. lFuncOk
		nVersion := 1
	Endif

Return nVersion

/*/{Protheus.doc} menuFINA710
	Cria o menu da aplicacao validando os acessos do usuário

	@type Function
	@author renato.ito
	@since 23/04/2024
	@return aMenu, Array<PoMenuItem>
/*/
Function menuFINA710() as Array
	Local aMenu          := {}                   as Array
	Local jMenuItem                              as Json
	Local aSubMenu       := {}                   as Array
	Local aSubMBanks     := {}                   as Array
	Local aSubMBanksTF   := {}                   as Array
	Local aMenuCus       := {}                   as Array
	Local nMenuCus       := 0                    as Numeric
	Local jMenuCus       := JsonObject():New()   as Json
	Local cUserID        := RetCodUsr(cUserName) as Character

	// indicadores
	if (MPUserHasAccess('FINA710', 1, cUserID, .F.))
		jMenuItem                 := JsonObject():New()
		jMenuItem[ 'feature' ]    := 'indicators'
		jMenuItem[ 'label' ]      := STR0001//'Indicadores'
		jMenuItem[ 'shortLabel' ] := STR0001
		jMenuItem[ 'icon' ]       := 'an an-presentation-chart'
		jMenuItem[ 'tooltip' ]    := STR0016 + ' ' + STR0001 // Acessar Indicadores'
		aadd(aMenu, jMenuItem) //indicadores
	endIf
	// fluxo de caixa
	aSubMenu := {}
	if (MPUserHasAccess('FINA710', 2, cUserID, .F.))
		jMenuItem                         := JsonObject():New()
		jMenuItem[ 'feature' ]            := 'cashFlow_movements'
		jMenuItem[ 'label' ]              := STR0015 //movimentos
		jMenuItem[ 'shortLabel' ]         := STR0015
		AADD(aSubMenu, jMenuItem)
		jMenuItem                         := JsonObject():New()
		jMenuItem[ 'feature' ]            := 'cashFlow_consolidated'
		jMenuItem[ 'label' ]              := STR0014 //consolidado
		jMenuItem[ 'shortLabel' ]         := STR0014
		AADD(aSubMenu, jMenuItem)
		jMenuItem                         := JsonObject():New()
		jMenuItem[ 'feature' ]            := 'cashFlow'
		jMenuItem['label']                := STR0002//'Fluxo de caixa'
		jMenuItem['shortLabel']           := STR0002
		jMenuItem['icon']                 := 'an an-chart-bar'
		jMenuItem[ 'tooltip' ]            := STR0016 + ' ' + STR0002 // Acessar Fluxo de caixa'
		jMenuItem[ 'subItems' ]           := aSubMenu
		AADD(aMenu, jMenuItem) //Fluxo de caixa
	endIf

	//Central de títulos
	if (MPUserHasAccess('FINA040' , 2, cUserID, .F.) .or. MPUserHasAccess('FINA050' , 2, cUserID, .F.))
		jMenuItem                          := JsonObject():New()
		jMenuItem[ 'feature' ]             := 'bills'
		jMenuItem[ 'label' ]               := STR0005//'Central de títulos'
		jMenuItem[ 'shortLabel' ]          := STR0005
		jMenuItem[ 'tooltip' ]             := STR0016 + ' ' + STR0005 // Acessar Central de títulos'
		jMenuItem[ 'icon' ]                := 'an an-monitor'
		jMenuItem[ 'access' ]              := JsonObject():New()
		jMenuItem[ 'access' ][ 'fina040' ] := MPUserHasAccess('FINA040' , 2, cUserID, .F.)
		jMenuItem[ 'access' ][ 'fina050' ] := MPUserHasAccess('FINA050' , 2, cUserID, .F.)
		AADD(aMenu, jMenuItem)
	endIf

	If cPaisLoc == 'BRA'
		// Comunicação bancário - configuracao de contas
		if (MPUserHasAccess('FINA710', 5, cUserID, .F.))
			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_config'
			jMenuItem[ 'label' ]              := STR0006//'Configuração de contas'
			jMenuItem[ 'shortLabel' ]         := STR0006
			AADD(aSubMBanks, jMenuItem)
		endIf
		// Comunicação bancário - contas a receber
		if (MPUserHasAccess('FINA710', 3, cUserID, .F.))
			aSubMenu := {}
			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cr_monitor'
			jMenuItem[ 'label' ]              := STR0007//'Monitor de pendências'
			jMenuItem[ 'shortLabel' ]         := STR0007
			AADD(aSubMenu, jMenuItem)

			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cr_boletos'
			jMenuItem[ 'label' ]              := STR0008//'Boletos emitidos'
			jMenuItem[ 'shortLabel' ]         := STR0008
			AADD(aSubMenu, jMenuItem)

			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cr_log'
			jMenuItem[ 'label' ]              := STR0009//'Log de retorno'
			jMenuItem[ 'shortLabel' ]         := STR0009
			AADD(aSubMenu, jMenuItem)

			if (!Empty(aSubMenu))
				jMenuItem                         := JsonObject():New()
				jMenuItem[ 'label' ]              := STR0010//'Contas a receber'
				jMenuItem[ 'shortLabel' ]         := STR0010
				jMenuItem[ 'subItems' ]           := aSubMenu
				AADD(aSubMBanks, jMenuItem)
			endIf
		endIf

		// Comunicação bancário - contas a pagar
		if (MPUserHasAccess('FINA710', 4, cUserID, .F.))
			aSubMenu := {}
			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cp_monitor'
			jMenuItem[ 'label' ]              := STR0007//'Monitor de pendências'
			jMenuItem[ 'shortLabel' ]         := STR0007
			AADD(aSubMenu, jMenuItem)

			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cp_pagamentos'
			jMenuItem[ 'label' ]              := STR0012//'Pagamentos'
			jMenuItem[ 'shortLabel' ]         := STR0012
			AADD(aSubMenu, jMenuItem)

			jMenuItem                         := JsonObject():New()
			jMenuItem[ 'feature' ]            := 'banks_cp_log'
			jMenuItem[ 'label' ]              := STR0009//'Log de retorno'
			jMenuItem[ 'shortLabel' ]         := STR0009
			AADD(aSubMenu, jMenuItem)

			if (!Empty(aSubMenu))
				jMenuItem                         := JsonObject():New()
				jMenuItem[ 'label' ]              := STR0011//'Contas a pagar'
				jMenuItem[ 'shortLabel' ]         := STR0011
				jMenuItem[ 'subItems' ]           := aSubMenu
				AADD(aSubMBanks, jMenuItem)
			endIf
		endIf
		// Comunicação bancária - configuração de contas TechfIn
		If (MPUserHasAccess('FINA710', 5, cUserID, .F.)) .Or. (MPUserHasAccess('FINA710', 6, cUserID, .F.)) .Or. (MPUserHasAccess('FINA710', 7, cUserID, .F.))
			aSubMenu := {}
			If (MPUserHasAccess('FINA710', 5, cUserID, .F.))
			
				jMenuItem                 := JsonObject():New()
				jMenuItem[ 'feature' ]    := 'bankstechfin_config'
				jMenuItem[ 'label' ]      := STR0021 // 'Configuração de contas Techfin'
				jMenuItem[ 'shortLabel' ] := STR0021 // 'Configuração de contas Techfin'
				AADD(aSubMBanksTF, jMenuItem)
			EndIf
			
			If (MPUserHasAccess('FINA710', 6, cUserID, .F.))
				jMenuItem                 := JsonObject():New()
				jMenuItem[ 'feature' ]    := 'bankstechfin_monitortechfin'
				jMenuItem[ 'label' ]      := STR0022 // 'Monitor'
				jMenuItem[ 'shortLabel' ] := STR0022 // 'Monitor'
				AADD(aSubMenu, jMenuItem)
			EndIf

			If (MPUserHasAccess('FINA710', 7, cUserID, .F.))
				jMenuItem                 := JsonObject():New()
				jMenuItem[ 'feature' ]    := 'bankstechfin_ocurrenciestechfin'
				jMenuItem[ 'label' ]      := STR0023 // 'Ocorrências'
				jMenuItem[ 'shortLabel' ] := STR0023 // 'Ocorrências'
				AADD(aSubMenu, jMenuItem)
			EndIf

			If (!Empty(aSubMenu))
				jMenuItem                 := JsonObject():New()
				jMenuItem[ 'label' ]      := STR0024 // 'Boleto Híbrido'
				jMenuItem[ 'shortLabel' ] := STR0024 // 'Boleto Híbrido'
				jMenuItem[ 'subItems' ]   := aSubMenu
				AADD(aSubMBanksTF, jMenuItem)
			EndIf
		EndIf

		if (!Empty(aSubMBanks))
			jMenuItem                 := JsonObject():New()
			jMenuItem[ 'feature' ]    := 'banks'
			jMenuItem[ 'label' ]      := STR0013//'Comunicação bancário'
			jMenuItem[ 'shortLabel' ] := STR0013
			jMenuItem[ 'icon' ]       := 'an an-barcode'
			jMenuItem[ 'tooltip' ]    := STR0016 + ' ' + STR0013 // Acessar Comunicação bancário
			jMenuItem[ 'subItems' ]   := aSubMBanks
			AADD(aMenu, jMenuItem)
		EndIf

		// Comunicação bancária Techfin
		If (!Empty(aSubMBanksTF))
			jMenuItem                 := JsonObject():New()
			jMenuItem[ 'feature' ]    := 'bankstechfin'
			jMenuItem[ 'label' ]      := STR0025 // 'Comunicação bancária Techfin'
			jMenuItem[ 'shortLabel' ] := STR0025 // 'Comunicação bancária Techfin'
			jMenuItem[ 'icon' ]       := 'an an-bank'
			jMenuItem[ 'tooltip' ]    := STR0016 + ' ' + STR0025 // 'Acessar Comunicação bancária Techfin'
			jMenuItem[ 'subItems' ]   := aSubMBanksTF
			AADD(aMenu, jMenuItem)
		EndIf

	EndIf

	// DashBoard FATA900
	jMenuItem                 := JsonObject():New()
	jMenuItem[ 'feature' ]    := 'advpl'
	jMenuItem[ 'function' ]   := 'FATA900()'
	jMenuItem[ 'label' ]      := STR0017//'Dashboard'
	jMenuItem[ 'shortLabel' ] := STR0017
	jMenuItem[ 'tooltip' ]    := STR0016 + ' ' + STR0017 // Acessar Dashboard
	jMenuItem[ 'icon' ]       := 'an an-laptop'
	AADD(aMenu, jMenuItem)

	if ExistBlock('NGFADDMN')
		aMenuCus := Execblock('NGFADDMN', .F., .F.)
		// [1] function [2] label [3]  icon
		for nMenuCus := 1 to len(aMenuCus)
			jMenuCus := JsonObject():new()
			jMenuCus['feature']    := 'custom'
			jMenuCus['function']   := aMenuCus[nMenuCus][1]
			jMenuCus['label']      := aMenuCus[nMenuCus][2]
			jMenuCus['shortLabel'] := aMenuCus[nMenuCus][2]
			if len(aMenuCus[nMenuCus]) > 2
				jMenuCus['icon'] := aMenuCus[nMenuCus][3]
			endIf
			AADD(aMenu, jMenuCus)
		next
	endIf

return aMenu

/*/{Protheus.doc} menuFINA716
	Cria o menu da aplicacao validando os acessos do usuário

	@type Function
	@author renato.ito
	@since 23/04/2024
	@return aMenu, Array<PoMenuItem>
/*/
function menuFINA716() as array
	local jMenuItem := JsonObject():New() as json
	local aMenu := {} as array

	jMenuItem                 := JsonObject():New()
	jMenuItem[ 'feature' ]    := 'application'
	jMenuItem[ 'label' ]      := STR0018 //'Aplicação'
	jMenuItem[ 'shortLabel' ] := STR0018
	jMenuItem[ 'icon' ]       := 'an an-currency-circle-dollar'
	aadd(aMenu, jMenuItem)

	jMenuItem                 := JsonObject():New()
	jMenuItem[ 'feature' ]    := 'loan'
	jMenuItem[ 'label' ]      := STR0019 //'Empréstimo'
	jMenuItem[ 'shortLabel' ] := STR0019
	jMenuItem[ 'icon' ]       := 'an an-money'
	aadd(aMenu, jMenuItem)

	jMenuItem                 := JsonObject():New()
	jMenuItem[ 'feature' ]    := 'appropriation'
	jMenuItem[ 'label' ]      := STR0020 //'Apropriação'
	jMenuItem[ 'shortLabel' ] := STR0020
	jMenuItem[ 'icon' ]       := 'an an-stack-simple'
	aadd(aMenu, jMenuItem)

return aMenu
