#include 'tlpp-core.th'
#include 'tlpp-rest.th'

NAMESPACE totvs.protheus.backoffice.ngf.rescue
USING NAMESPACE gfin.util

/*/{Protheus.doc}
  EndPoint para estornar os resgates das aplicações

  @author Vitor Duca
  @since 14/12/2021
/*/
@DELETE('/api/gfin/v1/applicationsLoans/rescue/:internalID')
Function reverseRescue()
  Local jResponse as Json
  Local cPathParam := "" As Character
  Local oBody as Json
  Local oHeader := oRest:getHeaderRequest() As Json 

  If oHeader:hasProperty('dataBase') .and. !Empty(oHeader['dataBase'])
    dDataBase := stringIsoToDate(oHeader['dataBase'])
  Endif

  oBody := JsonObject():new()
  oBody:FromJson(oRest:GetBodyRequest())

  cPathParam := FWURLDecode(oRest:getPathParamsRequest()['internalID'])
  jResponse := ReverseRescueApplication(cPathParam, oBody)

  oRest:setResponse(jResponse)
  FreeObj(jResponse)
Return

/*/{Protheus.doc}
  EndPoint para efetuar os resgates das aplicações

  @author Vitor Duca
  @since 04/01/2022
/*/
@POST('/api/gfin/v1/applicationsLoans/rescue')
Function includeRescue()
 	Local oService as Object
  Local oBody as Json
  Local jResponse as Json
  Local oHeader := oRest:getHeaderRequest() As Json 

  If oHeader:hasProperty('dataBase') .and. !Empty(oHeader['dataBase'])
    dDataBase := stringIsoToDate(oHeader['dataBase'])
  Endif

  oBody := JsonObject():new()
  oBody:FromJson(oRest:GetBodyRequest())

  jResponse := RescueApplication(oBody)

	oRest:setResponse(jResponse)

	FreeObj(oService)
Return

/*/{Protheus.doc}
	EndPoint para calcular os campos do resgate, atraves da função
	Fa171Calc

	@author Vitor Duca
	@since 05/01/2022
/*/
@POST('/api/gfin/v1/applicationsLoans/rescue/calculate')
Function calculateRescue()
	Local oBody as Json
  Local oHeader := oRest:getHeaderRequest() As Json 

  If oHeader:hasProperty('dataBase') .and. !Empty(oHeader['dataBase'])
    dDataBase := stringIsoToDate(oHeader['dataBase'])
  Endif

	oBody := JsonObject():new()
	oBody:FromJson(DecodeUTF8(oRest:GetBodyRequest()))

	gfin.util.AnswerRest(totvs.protheus.backoffice.ngf.rescue.CalculationRescue(oBody))
	FREEOBJ(oBody)
Return

