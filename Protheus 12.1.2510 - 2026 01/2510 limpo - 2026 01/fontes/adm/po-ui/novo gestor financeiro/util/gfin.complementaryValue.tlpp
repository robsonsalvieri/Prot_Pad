#include 'tlpp-core.th'

NameSpace gfin.util

Static __oHashCache	as object
static __instance   as object

//-------------------------------------------------------------------
/*/{Protheus.doc } class ComplementaryValue
metodos auxiliares para valores dos títulos

@author Renato Ito
@since 08/10/2020
/*/
//-------------------------------------------------------------------
Class ComplementaryValue

	Public Method new() CONSTRUCTOR
	Public Method Destroy()
	Public Method getInstance()
	Public Method complementaryValue() As Numeric  //Valores acessórios

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc } method new()
construtor da classe
/*/
//-------------------------------------------------------------------
Method new() Class ComplementaryValue
	__oHashCache  := FwHashMap():New()
Return Self
//-------------------------------------------------------------------
/*/{Protheus.doc } method destroy()
destroy da classe
/*/
//-------------------------------------------------------------------
Method destroy() Class ComplementaryValue
	FreeObj(__oHashCache)
	FreeObj(__instance)
Return
//-------------------------------------------------------------------
/*/{Protheus.doc } method getInstance()()
cria a instancia da classe
/*/
//-------------------------------------------------------------------
Method getInstance() Class ComplementaryValue
	If ( __instance == nil )
		__instance := ComplementaryValue():new()
	EndIf
Return __instance

//-------------------------------------------------------------------
/*/{Protheus.doc } method complementaryValue
retorna o valor acessório calculado do título

@param
	recnoTitle, numeric, recno do título da tabela SE1 ou SE2
	idDoc, character, iddoc do título
	filorig, character, filorig do título
	aliasTit, character, alias do título SE1 ou SE2
	movDate,date, data do movimento para o calculo
	balance, numeric, saldo do título a ser considerado
@return
	value, numeric , valor acessório calculado do título

@author Renato Ito
@since 08/10/2020
/*/
//-------------------------------------------------------------------
Method complementaryValue(recnoTitle as numeric, idDoc as character, filorig as character, aliasTit as character, movDate as date, balance as numeric) as numeric class ComplementaryValue
	local tableVa            as character
	local value              as numeric
	local aliasComplementary as character

	value := 0

	If (!__oHashCache:containsKey('complementaryValue'))
    query := createPreparedComplementaryValue()
  Else
    query := __oHashCache:get('complementaryValue')
  EndIf

  queryBind := {;
    FWxFilial("FKC", filorig),;
    FWxFilial(aliasTit, filorig),;
    idDoc,;
    If(aliasTit = 'SE1', "2", "1"); //1=Pagar;2=Receber;3=Ambas
  }
  aliasComplementary := GetNextAlias()
  DbUseArea(.T., "TOPCONN", TCGenQry2(Nil, Nil, query, queryBind), aliasComplementary, .F., .T.)

	tableVa := If(aliasTit = 'SE1', 'R', 'P')
	While (aliasComplementary)->(!EOF())

    dbSelectArea(aliasTit)
		(aliasTit)->(dbGoTo(recnoTitle))
		dbSelectArea('FKC')
		FKC->(dbGoTo((aliasComplementary)->FKC_RECNO))
		dbSelectArea('FKD')
		FKD->(dbGoTo((aliasComplementary)->FKD_RECNO))

		value += FCalcVA(tableVa, movDate, balance)
		(aliasComplementary)->(dbSkip())
	EndDo

	(aliasComplementary)->(dbCloseArea())

return value

//-------------------------------------------------------------------
/*/{Protheus.doc } createPreparedComplementaryValue
prepara o cache da query ComplementaryValue

@return
	statement, object , FwPreparedStatement

@author Renato Ito
@since 08/10/2020
/*/
//-------------------------------------------------------------------
Static Function createPreparedComplementaryValue() as Character

	local query				as character
	local queryFields		as character

	queryFields := "FKC.R_E_C_N_O_ AS FKC_RECNO, "
	queryFields += "FKD.R_E_C_N_O_ AS FKD_RECNO "

	query := "SELECT " + queryFields + " FROM " + RetSqlName("FKD")  + " FKD "
	query += "JOIN " + RetSqlName("FKC")  + " FKC ON "
	query += "FKD.FKD_CODIGO = FKC.FKC_CODIGO "
	query += "WHERE "
	query += "FKC.FKC_FILIAL = ? "
	query += "AND FKD.FKD_FILIAL = ? "
	query += "AND FKD_IDDOC = ? "
	query += "AND FKC.FKC_RECPAG IN (?, '3') "
	query += "AND FKC.FKC_ATIVO = '1'"
	query += "AND FKC.D_E_L_E_T_ = ' ' "
	query += "AND FKD.D_E_L_E_T_ = ' ' "

	query := ChangeQuery(query)

	__oHashCache:put("complementaryValue", query)

Return query
