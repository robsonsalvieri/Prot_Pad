#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.cashierdailymovements.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="FK5,SA6", name="Movimento de caixa diário", country="ALL",customTables="FK5,SA6")

//-------------------------------------------------------------------
/*/{Protheus.doc} CashierDailyMovementsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Movimento de caixa diário

@author Fábio Henrique Andrade
@since 15/05/2023
@version 1.07
*/
//-------------------------------------------------------------------  
class CashierDailyMovementsSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected method initializeBusinessObject()
    protected method mySetSchema()
    
    protected data nCasasDecimais as numeric
    
    protected data lMostraOutrasMoedas as logical
    protected data nMoeda as numeric
    protected data nOperacao as numeric
    protected data cInitialNumber as character
    protected data cFinalNumber as character
    protected data dInitialDate as date
    protected data dFinalDate as date
    protected data cInitialBank as character
    protected data cFinalBank as character
    protected data cInitialClass as character
    protected data cFinalClass as character

    protected method loadStatement() as character
    protected method setLookups()
    protected method loadParameters()
    protected method handleData()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Henrique Andrade
@since 15/05/2023
@version 1.0
*/
//-----------|--------------------------------------------------------   
method new() class CashierDailyMovementsSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setIsCBoxLookup(.T., .T.)
    self:setIsLookup(.T.) 
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable("FK5")
    self:setBranchFilter(.T., .T.)

    self:cAlias := ""
    self:aStruct := {}
    self:nCasasDecimais := TamSx3("M2_MOEDA2")[2] + 1

return self


//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

@author Guilherme de  Paula Santos
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class CashierDailyMovementsSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Movimento Caixa Diário"
    self:setDescription(STR0002) //"Movimento Caixa Diário"
    self:setPergunte("FINR120")
return


//-------------------------------------------------------------------
/*{Protheus.doc} handleData 
Processa os dados do alias aberto.
@author Fábio Henrique Andrade
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method handleData() class CashierDailyMovementsSmartViewBusinessObject
    local nMoedaBco := 1 as numeric
    local nTxMoedBc := 0 as numeric
    local nTxMoeda  := 0 as numeric
    local nVlrSaida := 0 as numeric
    local nVlrEntr  := 0 as numeric

    nTxMoedBc := (self:cAlias)->FK5_TXMOED
    nMoedaBco := (self:cAlias)->A6_MOEDA

    If self:nMoeda > 1 .AND. !Empty((self:cAlias)->FK5_VLMOE2)
        nTxMoeda := RecMoeda((self:cAlias)->FK5_DTDISP,self:nMoeda)
        If nTxMoeda == 0
            nTxMoeda := 1
        EndIf

        If (self:cAlias)->ENTRADA > 0 
            nVlrEntr  := Round(xMoeda((self:cAlias)->ENTRADA, nMoedaBco, self:nMoeda, , self:nCasasDecimais+1, nTxMoedBc, nTxMoeda), self:nCasasDecimais)
            nVlrSaida := (self:cAlias)->SAIDA
        ElseIf (self:cAlias)->SAIDA > 0 
            nVlrSaida := Round(xMoeda((self:cAlias)->SAIDA, nMoedaBco, self:nMoeda, , self:nCasasDecimais+1, nTxMoedBc, nTxMoeda), self:nCasasDecimais)
            nVlrEntr  := (self:cAlias)->ENTRADA
        EndIf
    Else
        If (self:cAlias)->ENTRADA > 0 
            nVlrEntr  := Round(xMoeda((self:cAlias)->ENTRADA, nMoedaBco, self:nMoeda, (self:cAlias)->FK5_DTDISP, self:nCasasDecimais+1, nTxMoedBc, nTxMoeda), self:nCasasDecimais)
            nVlrSaida := (self:cAlias)->SAIDA
        ElseIf (self:cAlias)->SAIDA > 0 
            nVlrSaida := Round(xMoeda((self:cAlias)->SAIDA, nMoedaBco, self:nMoeda, (self:cAlias)->FK5_DTDISP, self:nCasasDecimais+1, nTxMoedBc, nTxMoeda), self:nCasasDecimais)
            nVlrEntr  := (self:cAlias)->ENTRADA
        EndIf
    Endif

    self:jData := {;
        "FK5_DTDISP": totvs.framework.treports.date.stringToTimeStamp( (self:cAlias)->FK5_DTDISP ),;
        "NUMERARIO": (self:cAlias)->FK5_MOEDA,;
        "FK5_NATURE": MascNat( AllTrim((self:cAlias)->FK5_NATURE) ),;
        "SAIDA": nVlrSaida,;
        "ENTRADA": nVlrEntr,;
        "VALOR_MOV": (nVlrEntr - nVlrSaida) }

return

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Fábio Henrique Andrade
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema() class CashierDailyMovementsSmartViewBusinessObject

    local aFieldsFK5 := {} as array

    aFieldsFK5 := {"FK5_FILIAL","FK5_DATA","FK5_MOEDA","FK5_TXMOED","FK5_VLMOE2","FK5_DTDISP", "FK5_BANCO", "FK5_AGENCI", "FK5_CONTA", "FK5_NATURE", "FK5_DOC", "FK5_NUMCH", "FK5_HISTOR"}

    self:oSchema:aliasToSchema("FK5", aFieldsFK5)
    self:oSchema:aliasToSchema("SA6", {"A6_MOEDA"})
    self:finAddProperty("NUMERARIO", STR0004, "string")  //"Numerario"
    self:finAddProperty("ENTRADA"  , STR0005, "number")    //"Recebimentos (B)"
    self:finAddProperty("SAIDA"    , STR0006, "number")      //"Pagamentos (A)"
    self:finAddProperty("VALOR_MOV", STR0007, "number")  //"(B) - (A)"

    FwFreeArray(aFieldsFK5)

return 



//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@return character: cQuery

@author Fábio Henrique Andrade
@since 17/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatement() class CashierDailyMovementsSmartViewBusinessObject
    local cQuery    := ""                                                         as character
    local cOperacao := ""                                                         as character
    Local nConvert  := self:nMoeda                                                as numeric
    local nParam    := 1                                                          as numeric
    local aOperacao :={{"Pagamentos", "P"}, {"Recebimentos", "R"}, {"Ambos", ""}} as array

    cOperacao := aOperacao[self:nOperacao, 2]
    
    //CTE que carrega os saldos bancários para evitar uso de subquery na consulta principal
    cQuery := " SELECT ? "
	cQuery +=   ", COALESCE((CASE WHEN FK5.FK5_RECPAG = 'R' THEN FK5.FK5_VALOR "
    cQuery +=   " ELSE 0 END), 0) ENTRADA, "
    cQuery +=   " COALESCE((CASE WHEN FK5.FK5_RECPAG = 'P' THEN FK5.FK5_VALOR "
    cQuery +=   " ELSE 0 END), 0) SAIDA, "
    cQuery +=   " FK5.FK5_VLMOE2 "
    cQuery += " FROM " + RetSQLName("FK5") + " FK5 "
	cQuery +=   " INNER JOIN " + RetSQLName("SA6") + " SA6 ON " + FwJoinFilial("SA6","FK5") + " AND "
	cQuery +=   "  A6_COD =	FK5_BANCO AND "
	cQuery +=   "  A6_AGENCIA =	FK5_AGENCI AND "
	cQuery +=   "  A6_NUMCON = FK5_CONTA "

    //Considerar a moeda do banco e não a FK5 já que os movimentos bancários sempre serão na moeda do banco
    //independente do conteúdo do FK5_MOEDA
    if !self:lMostraOutrasMoedas
        cQuery += " AND A6_MOEDA = ? "
    endIf

    cQuery += " AND SA6.D_E_L_E_T_ = ? "

    cQuery += " WHERE  "
	cQuery += self:getSQLBranchFilter()
	cQuery +=   " AND FK5_DTDISP  BETWEEN ? AND ? "
	cQuery +=   " AND FK5_BANCO   BETWEEN ? AND ? " 
	cQuery +=   " AND FK5_MOEDA   BETWEEN ? AND ? "
	cQuery +=   " AND FK5_NATURE  BETWEEN ? AND ? "

    If !Empty(cOperacao)
        cQuery += " AND FK5_RECPAG = ? "
    EndIf

	cQuery +=   " AND FK5.D_E_L_E_T_ = ? "  

    cQuery += self:getFilterToSQL()

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParam++, self:getAllFieldsToSQL())
    if !self:lMostraOutrasMoedas
        self:oStatement:SetString(nParam++, Alltrim( StrZero(nConvert,2) ))
    endIf
    self:oStatement:SetString(nParam++, ' ')
    self:setStatementBranchFilter(@nParam)
    self:oStatement:SetDate(nParam++, self:dInitialDate) 
    self:oStatement:SetDate(nParam++, self:dFinalDate) 
    self:oStatement:SetString(nParam++, self:cInitialBank) 
    self:oStatement:SetString(nParam++, self:cFinalBank) 
    self:oStatement:SetString(nParam++, self:cInitialNumber) 
    self:oStatement:SetString(nParam++, self:cFinalNumber)
    self:oStatement:SetString(nParam++, self:cInitialClass)
    self:oStatement:SetString(nParam++, self:cFinalClass)

    if !Empty(cOperacao)
        self:oStatement:SetString(nParam++, cOperacao)
    endIf

    self:oStatement:SetString(nParam++, ' ')

return


//-------------------------------------------------------------------
/*{Protheus.doc} setLookUps
    Define os lookups do objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 20/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method setLookUps() class CashierDailyMovementsSmartViewBusinessObject
    local cQuery := "" as character
    local aFields := {} as array

    cQuery := "SELECT ED_CODIGO, ED_DESCRIC FROM "+ RetSQLName("SED") +" WHERE ED_FILIAL = '" + FWxFilial('SED') + "' AND D_E_L_E_T_ = ' '"
    aFields := {"ED_CODIGO", "ED_DESCRIC"}
    
    self:oSchema:setLookUpQuery("FK5_NATURE", aFields, cQuery, .F.)    
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    Carrega os parâmetros do filtro para as propriedades da classe.
    @author guilherme.sordi@totvs.com.br
    @since 20/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class CashierDailyMovementsSmartViewBusinessObject      

    self:nOperacao := 3

    self:cInitialNumber := MV_PAR01
    self:cFinalNumber   := MV_PAR02
    self:dInitialDate   := MV_PAR03
    self:dFinalDate     := MV_PAR04
    self:cInitialBank   := MV_PAR05
    self:cFinalBank     := MV_PAR06
    self:cInitialClass  := MV_PAR07
    self:cFinalClass    := MV_PAR08
    self:nMoeda         := self:getCurrencyValue(MV_PAR09)
    if MV_PAR10 >= 1 .and. MV_PAR10 <= 3
        self:nOperacao  := MV_PAR10
    endIf
    self:lMostraOutrasMoedas := MV_PAR11 == 1
    
return
