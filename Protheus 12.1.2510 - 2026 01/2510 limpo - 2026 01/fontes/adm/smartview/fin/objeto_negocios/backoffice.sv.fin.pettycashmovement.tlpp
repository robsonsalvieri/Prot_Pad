#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.pettycashmovement.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEU", name="Movimentos do caixinha", country="ALL",customTables="SEU")

//-------------------------------------------------------------------
/*/{Protheus.doc} PettyCashMovementSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de clientes

@author Fábio Henrique Andrade
@since 19/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class PettyCashMovementSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider                                                 
    public method new() as object
    public method loadStatement()
    public method mySetSchema() as object
    
    protected data aTiposMovimento as array
    protected data lCalculaSaldo as logical

    protected data cCodeFrom as character
    protected data cCodeTo as character
    protected data dTypingFrom as date
    protected data dTypingTo as date
    protected data nValue as numeric
    protected data cIndex as character
    protected data nParamOrder as numeric
    
    protected method getTiposMovimento() as array
    protected method getDescricaoTipoMovimento() as character
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method handleData()
    protected method handleStatement()
    protected method getWhere() as character
    protected method initializeBusinessObject()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Henrique Andrade
@since 19/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class PettyCashMovementSmartViewBusinessObject
    _Super:new()
    self:initializeBusinessObject()
    self:setAllTrim(.T.)
    self:setCompleteData(.T.)    

    self:aTiposMovimento := self:getTiposMovimento()
    self:lCalculaSaldo := .T.
    self:nParamOrder := 1
    self:setBranchFilter(.T., .T.)
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author matheus.monteiro@totvs.com.br
@since 04/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class PettyCashMovementSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Movimento do Caixinha"
    self:setDescription(STR0003) //"Relatório de Movimentos do Caixinha"
    self:setPergunte("FINR560")
return

//-------------------------------------------------------------------
/*/{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class PettyCashMovementSmartViewBusinessObject
    Local nBalance := 0 as numeric
    Local cDescription := "" as character

    nBalance := (self:cAlias)->FIJ_SALDO

    cDescription := self:getDescricaoTipoMovimento((self:cAlias)->EU_NROADIA,(self:cAlias)->EU_TIPO,(self:cAlias)->EU_HISTOR)  

    self:jData["DESCRICAO"] := cDescription
    self:jData["SALDO"] := nBalance
    self:jData["EU_VALOR"] := (self:cAlias)->EU_VALOR

    If (self:cAlias)->EU_TIPO $ "00|01|03|11|13"
        self:jData["EU_VALOR"] *= -1
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} mySetSchema
    Define o schema do objeto de negócio.
    Será implementado no objeto de negócio.
    @author matheus.monteiro@totvs.com.br
    @since 05/01/2024
    @version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema() class PettyCashMovementSmartViewBusinessObject
    local aFieldsSEU   := {} as array

    aFieldsSEU   := { "EU_FILIAL","EU_CAIXA", "EU_HISTOR", "EU_BENEF", "EU_NRCOMP","EU_NUM", "EU_NROADIA",;
                     "EU_DTDIGIT", "EU_VALOR", "EU_SLDADIA", "EU_EMISSAO", "EU_TIPO" }

    self:oSchema:aliasToSchema("SEU", aFieldsSEU)

    self:finAddProperty("SALDO",     STR0004, "number", "FIJ_SALDO")  //"Saldo Anterior"
    self:finAddProperty("DESCRICAO", STR0005, "string", "EU_HISTOR") //"Desc. Movimento"
return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Cria o objeto self:oStatement da classe FWExecStatement com a query principal 
do objeto de negócio.

@author matheus.monteiro@totvs.com.br
@since 05/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatement() class PettyCashMovementSmartViewBusinessObject
    local cQuery := "" as character

    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "EU_DTDIGIT" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "EU_EMISSAO" ) )

    self:loadFilterBranches({"SEU"})

    cQuery := " SELECT ? , SEU.R_E_C_N_O_ RECNO "

    if self:lCalculaSaldo
        cQuery += ", COALESCE(FIJ_SALDO,0) FIJ_SALDO "
    else
        cQuery += ", 0 FIJ_SALDO "
    endIf

    cQuery +=   " FROM " + RetSQLName("SEU") + " SEU "
    
    if self:lCalculaSaldo 
        cQuery += " LEFT JOIN " + RetSQLName("FIJ") + " FIJ "
        cQuery +=     " ON " + FWJoinFilial("FIJ", "SEU")        
        cQuery +=     " AND FIJ_DATA = ( "
        cQuery +=         " SELECT "
        cQuery +=             " MAX(FIJ_DATA) "
        cQuery +=         " FROM  " + RetSQLName("FIJ")
        cQuery +=         " WHERE "
        cQuery +=             " " + FWJoinFilial("FIJ", "SEU") + " AND "
        cQuery +=             " FIJ_DATA   < ? AND "
        cQuery +=             " FIJ_CODCX  = SEU.EU_CAIXA AND "
        cQuery +=             " D_E_L_E_T_ = ? "
        cQuery +=     " ) "
        cQuery +=     " AND FIJ_CODCX = EU_CAIXA "
        cQuery +=     " AND FIJ.D_E_L_E_T_ = ? "
    endIf

    cQuery += " WHERE  "
    if self:lEmptyMultiBranches
        cQuery += " SEU.EU_FILIAL = ? "
    else
        cQuery += " SEU.EU_FILIAL IN (?) "
    endIf
    cQuery += self:getWhere()
    cQuery += " AND SEU.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SEU->(IndexKey( 4 )))

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(self:nParamOrder++, self:getAllFieldsToSQL())

    if self:lCalculaSaldo 
        self:oStatement:SetDate(self:nParamOrder++, self:dTypingFrom)
        self:oStatement:SetString(self:nParamOrder++, " ")
        self:oStatement:SetString(self:nParamOrder++, " ")
    endIf
    if self:lEmptyMultiBranches
        self:oStatement:SetString(self:nParamOrder++, self:oFilterBranches["SEU"][1])
    else
        self:oStatement:SetIn(self:nParamOrder++, self:oFilterBranches["SEU"][1])
    endIf
    self:handleStatement()
    self:oStatement:SetString(self:nParamOrder++, " ")
return

//-------------------------------------------------------------------
/*/{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 27/12/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleStatement() class PettyCashMovementSmartViewBusinessObject
    self:oStatement:SetString(self:nParamOrder++, self:cCodeFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cCodeTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dTypingFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dTypingTo)    
return

//-------------------------------------------------------------------
/*{Protheus.doc} getTiposMovimento
Monta o array de tipos para apresentar na tabela

@return array: aTipos

@author Fábio Henrique Andrade
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getTiposMovimento() as array class PettyCashMovementSmartViewBusinessObject
    Local aTipos := {} as array

    aAdd(aTipos,{"00",STR0006}) //"Despesa"
    aAdd(aTipos,{"01",STR0007,STR0008}) //"Adiantamento"#"Adt. "
    aAdd(aTipos,{"02",STR0009,STR0010}) //"Estorno De Despesa"#"Dev. Adiantam"
    aAdd(aTipos,{"10",STR0011}) //"Reposição"
    aAdd(aTipos,{"11",STR0012}) //"Transf. p/fechamen"
    aAdd(aTipos,{"12",STR0013}) //"Dev.p/fecho Cx Menor"
    aAdd(aTipos,{"13",STR0014}) //"Repos. Caixa Menor"
    aAdd(aTipos,{"90",STR0015}) //"Reposição cancelada"
    aAdd(aTipos,{"91",STR0016}) //"Reposição - à espera de autorização"
    aAdd(aTipos,{"92",STR0017}) //"Reposição - à espera de liquidação de título"
return aTipos

//-------------------------------------------------------------------
/*{Protheus.doc} getDescricaoTipoMovimento
Executa a regra de retorno da descricao do Movimento

@return array: cDescricaoTipo

@author Fábio Henrique Andrade
@since 24/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getDescricaoTipoMovimento(cCodAdiant as character, cTipoMov as character, cHistorico as character) as character class PettyCashMovementSmartViewBusinessObject

    local nPosTipo  := 0   as numeric
    local cDescricaoTipo     := ""  as character

    nPosTipo := aScan(self:aTiposMovimento, {|x| AllTrim(Upper(x[1])) == cTipoMov})

    If nPosTipo > 0

        If self:aTiposMovimento[nPosTipo][1] == "02"
            If Empty(cCodAdiant)
                cDescricaoTipo := self:aTiposMovimento[nPosTipo][2]
            Else
                cDescricaoTipo := self:aTiposMovimento[nPosTipo][3]
            EndIf
        ElseIf self:aTiposMovimento[nPosTipo][1] $ "00/11/12/13"
            If Empty(cHistorico)
                cDescricaoTipo := self:aTiposMovimento[nPosTipo][2]
            Else
                cDescricaoTipo := cHistorico
            EndIf
        Else
            cDescricaoTipo := self:aTiposMovimento[nPosTipo][2]
        EndIf

    Else
        cDescricaoTipo := cHistorico
    EndIf
return cDescricaoTipo

//-------------------------------------------------------------------
    /*{Protheus.doc} loadDefaultParameters
    @author matheus.monteiro@totvs.com.br
    @since 05/01/2024
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method loadDefaultParameters() class PettyCashMovementSmartViewBusinessObject
    self:cCodeFrom := ''
    self:cCodeTo := 'ZZZ'
    self:dTypingFrom := cToD(' / / ')
    self:dTypingTo := CtoD(' / / ')
return

//-------------------------------------------------------------------
    /*{Protheus.doc} loadParameters
    @author matheus.monteiro@totvs.com.br
    @since 05/01/2024
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method loadParameters() class PettyCashMovementSmartViewBusinessObject
    If self:oCurrentFilter != Nil 
        self:cCodeFrom := MV_PAR01
        self:cCodeTo := MV_PAR02
        self:dTypingFrom := MV_PAR03
        self:dTypingTo := MV_PAR04
    endIf
return

//-------------------------------------------------------------------
    /*{Protheus.doc} loadParameters
    @author matheus.monteiro@totvs.com.br
    @since 05/01/2024
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method getWhere() as character class PettyCashMovementSmartViewBusinessObject
    Local cWhere := "" as character 
    
    cWhere += " AND EU_CAIXA BETWEEN ? AND ? "
    cWhere += " AND EU_DTDIGIT BETWEEN ? AND ? "
return cWhere
