#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.pettycashmovement.ch"
#include "fwlibversion.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SEU", name="Recibo de despesa ou adiantamento do caixinha", country="ALL",customTables="SEU")

//-------------------------------------------------------------------
    /*/{Protheus.doc} PettyCashAdvanceReceiptSmartViewBusinessObject
    Classe do Objeto de Negócio
    @author guilherme.sordi@totvs.com.br
    @since 30/08/2023
    @version 12.1.2210
    */
//-------------------------------------------------------------------
class PettyCashAdvanceReceiptSmartViewBusinessObject from     totvs.protheus.backoffice.fin.smartView.integratedProvider.PettyCashMovementSmartViewBusinessObject
    public method new() as object

    public method printReceipt() as logical
    public method getLastFileGenerated() as character
    
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method getWhere() as character 
    protected method handleSchema()
    protected method handleData()
    protected method initializeBusinessObject()
    protected method handleStatement()
    protected method updatePrintStatus()
    protected method getPettyCashCurrency() as numeric
    protected method getValueInWords() as character
    protected method getCurrencySymbol() as character   

    protected data cCodeFrom as character
    protected data cCodeTo as character
    protected data dIssueDateFrom as date
    protected data dIssueDateTo as date
    protected data cDocumentFrom as character
    protected data cDocumentTo as character
    protected data nStatusPrinted as numeric

    protected data nPettyCashCurrency as numeric
    protected data cLasfFileGenerated as character
endclass

//-------------------------------------------------------------------
    /*{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 30/08/2023
    @version 1.0
    */
//-------------------------------------------------------------------
method new() class PettyCashAdvanceReceiptSmartViewBusinessObject 
    _Super:new()

    self:setMainTable("SEU")
    self:lCalculaSaldo := .F.
    self:nPettyCashCurrency := 1
    self:cLasfFileGenerated := ""
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author matheus.monteiro@totvs.com.br
@since 08/01/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class PettyCashAdvanceReceiptSmartViewBusinessObject
    self:setDisplayName(STR0018) //"Recibo de adiantamento do caixinha"
    self:setDescription(STR0018) //"Recibo de adiantamento do caixinha"    
    self:setPergunte("FINT011")
return

//-------------------------------------------------------------------
    /*{Protheus.doc} loadDefaultParameters
    @author matheus.monteiro@totvs.com.br
    @since 05/01/2024
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method loadDefaultParameters() class PettyCashAdvanceReceiptSmartViewBusinessObject
    self:cCodeFrom := ''
    self:cCodeTo := ''
    self:dIssueDateFrom := dDatabase
    self:dIssueDateTo := dDatabase
    self:cDocumentFrom := ''
    self:cDocumentTo := ''
    self:nStatusPrinted := 1
return

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class PettyCashAdvanceReceiptSmartViewBusinessObject
    If self:oCurrentFilter != Nil
        self:cCodeFrom := MV_PAR01
        self:cCodeTo := MV_PAR02
        self:dIssueDateFrom := MV_PAR03
        self:dIssueDateTo := MV_PAR04
        self:cDocumentFrom := MV_PAR05
        self:cDocumentTo := MV_PAR06
        self:nStatusPrinted := MV_PAR07 // 1 = not printed, 2 = already printed, 3 = all
    EndIf
return 

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWhere() as character class PettyCashAdvanceReceiptSmartViewBusinessObject
    Local cWhere := "" as character 

    cWhere += " AND EU_CAIXA BETWEEN ? AND ? "
	cWhere += " AND EU_EMISSAO BETWEEN ? AND ? "
	cWhere += " AND EU_NUM BETWEEN ? AND ? "	
    cWhere += " AND EU_TIPO IN (?) "
    
    if self:nStatusPrinted == 1 .or. self:nStatusPrinted == 2
	    cWhere += " AND EU_IMPRESS = ? "
    endIf
return cWhere

//-------------------------------------------------------------------
/*/{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 27/12/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleStatement() class PettyCashAdvanceReceiptSmartViewBusinessObject
    self:oStatement:SetString(self:nParamOrder++, self:cCodeFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cCodeTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetString(self:nParamOrder++, self:cDocumentFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cDocumentTo)
    self:oStatement:SetIn(self:nParamOrder++, {'00','01'})

    if self:nStatusPrinted == 1
		self:oStatement:SetString(self:nParamOrder++, " ")
	elseIf self:nStatusPrinted == 2
		self:oStatement:SetString(self:nParamOrder++, "S")
	endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class PettyCashAdvanceReceiptSmartViewBusinessObject
    
    if (self:cAlias)->EU_TIPO == "00"
        self:jData["RECEIPT_TYPE"] := STR0020 //"Recibo de despesa"
    else
        self:jData["RECEIPT_TYPE"] := STR0021 //"Recibo de adiantamento"
    endIf

    self:nPettyCashCurrency := self:getPettyCashCurrency()
    self:jData["VALUE_WORDS"] := self:getValueInWords()
    self:jData["CURRENCY_SYMBOL"] := self:getCurrencySymbol()
    self:jData["RECEIPT_TEXT_ONE"] := STR0025 //"Recebi em "
    self:jData["RECEIPT_TEXT_TWO"] := STR0026 //" a quantia de "
    self:jData["RECEIPT_TEXT_THREE"] := STR0027 //". Este valor refere-se a "

    self:updatePrintStatus()

return

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class PettyCashAdvanceReceiptSmartViewBusinessObject
    self:finAddProperty("RECEIPT_TYPE", STR0019, "string") //"Tipo de recibo"
    self:finAddProperty("VALUE_WORDS", STR0022, "string") //"Valor por extenso"
    self:finAddProperty("CURRENCY_SYMBOL", STR0023, "string") //"Símbolo da moeda"    
    self:finAddProperty("RECEIPT_TEXT_ONE", STR0025, "string") //"Recebi em "
    self:finAddProperty("RECEIPT_TEXT_TWO", STR0026, "string") //" a quantia de "
    self:finAddProperty("RECEIPT_TEXT_THREE", STR0027, "string") //". Este valor refere-se a "
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    Determina a moeda do caixinha.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getPettyCashCurrency() as numeric class PettyCashAdvanceReceiptSmartViewBusinessObject 
    local nPettyCashCurrency := 1
    
    if SET->(DBSeek(FWxFilial("SET") + (self:cAlias)->EU_CAIXA)) ;
	    .and. SA6->(DbSeek(FWxFilial("SA6") + SET->ET_BANCO + SET->ET_AGEBCO + SET->ET_CTABCO ))
        nPettyCashCurrency := SA6->A6_MOEDA
    endIf
return nPettyCashCurrency

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    Retorna o valor por extenso.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getValueInWords() as character class PettyCashAdvanceReceiptSmartViewBusinessObject
    local cValueInWords := ""

    cValueInWords := Extenso( (self:cAlias)->EU_VALOR, .F., self:nPettyCashCurrency )
    cValueInWords := AllTrim( cValueInWords )
return cValueInWords

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getCurrencySymbol() as character class PettyCashAdvanceReceiptSmartViewBusinessObject
    local cCurrencySymbol := ""

    cCurrencySymbol := SuperGetMv("MV_SIMB" + CValToChar(self:nPettyCashCurrency), .F., "")
return cCurrencySymbol

//-------------------------------------------------------------------
/*/{Protheus.doc} FinIntegratedProvider
    Atualiza o campo EU_IMPRESS do movimento. Implementado para manter o 
    comportamento do legado.
    @author guilherme.sordi@totvs.com.br
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method updatePrintStatus() class PettyCashAdvanceReceiptSmartViewBusinessObject
    SEU->(DbGoTo((self:cAlias)->RECNO))
    if SEU->(recno()) == (self:cAlias)->RECNO
        RecLock("SEU", .F.)
        SEU->EU_IMPRESS := "S"
        SEU->(MsUnlock())
    endIf
return

//-------------------------------------------------------------------
    /*/{Protheus.doc} printReceipt
    Gera um recibo em PDF para o EU_NUM recebido pelos parâmetros.
    @author guilherme.sordi@totvs.com.br
    @since 05/09/2023
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method printReceipt(cDocumentFrom as character, cDocumentTo as character) as logical class PettyCashAdvanceReceiptSmartViewBusinessObject
    local jParams as json
    local nPrintAllStatus := 3 as numeric
    local lSuccess := .F. as logical
    local jPrintInfo as json
    local cSmartViewId := "backoffice.sv.fin.pettycashadvancereceipt.default.rep.bra"
    local aReportType := {"report", "data-grid", "pivot-table"} as array
    local nPrintToFile := 1 as numeric
    local cDefaultPath := GetTempPath() as character
    Local oSmartView As Object
    Local cMsg := "" As Character
    Local lLibVersion := FwLibVersion() < "20240226" As Logical 

    default cDocumentFrom := "0000000000"
    default cDocumentTo := cDocumentFrom

    self:cLasfFileGenerated := ""

    if cDocumentFrom == "0000000000"
        return .F.
    endIf

    jPrintInfo := JsonObject():New() 
    jPrintInfo['name'] := "pettycashreceipt_" + self:varToTimeStamp(Date())
    jPrintInfo['extension'] := "pdf"
    jPrintInfo['path'] := cDefaultPath
    
    If lLibVersion
        jParams := JsonObject():New()
        jParams['SV_MULTBRANCH'] := { }
        jParams['MV_PAR01'] := "   "
        jParams['MV_PAR02'] := "ZZZ"
        jParams['MV_PAR03'] := self:varToTimeStamp(cToD("01/01/2000"))
        jParams['MV_PAR04'] := self:varToTimeStamp(cToD("01/01/2099"))
        jParams['MV_PAR05'] := cDocumentFrom
        jParams['MV_PAR06'] := cDocumentTo
        jParams['MV_PAR07'] := nPrintAllStatus

        lSuccess := totvs.framework.treports.callTReports(cSmartViewId, aReportType[1], nPrintToFile, jPrintInfo, jParams)
    Else 
        oSmartView := totvs.framework.smartview.callSmartView():new(cSmartViewId,aReportType[1])
        oSmartView:setNoInterface(.T.)
        oSmartView:setParam("MV_PAR01", "")
        oSmartView:setParam("MV_PAR02", "ZZZ")
        oSmartView:setParam("MV_PAR03", self:varToTimeStamp(cToD("01/01/2000")))
        oSmartView:setParam("MV_PAR04", self:varToTimeStamp(cToD("01/01/2099")))
        oSmartView:setParam("MV_PAR05", cDocumentFrom)
        oSmartView:setParam("MV_PAR06", cDocumentTo)
        oSmartView:setParam("MV_PAR07", nPrintAllStatus)
        oSmartView:setForceParams(.T.)
        oSmartView:setPrintInfo(jPrintInfo)
        lSuccess := oSmartView:executeSmartView(.T.)

        If !lSuccess
            cMsg := oSmartView:getError()
        EndIf

        oSmartView:Destroy()
    EndIf

    if lSuccess
        self:cLasfFileGenerated := jPrintInfo['path'] + jPrintInfo['name'] + '.' + jPrintInfo['extension']
    endIf

    If oSmartView != Nil   
        FreeObj(oSmartView)
    EndIf

return lSuccess

//-------------------------------------------------------------------
    /*/{Protheus.doc} getLastFileGenerated
    Retorna o nome do arquivo criado pelo método printReceipt().
    @author guilherme.sordi@totvs.com.br
    @since 05/09/2023
    @version 12.1.2210
    */
//-------------------------------------------------------------------
method getLastFileGenerated() as character class PettyCashAdvanceReceiptSmartViewBusinessObject
return self:cLasfFileGenerated
