#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billspayable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2,SED", name="Posição de fornecedores",;
customTables="SE2,SA2,SED", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} SupplierSituationBusinessObject
    Classe para criação do Objeto de Negócio
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class SupplierSituationSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsPayableSmartViewBusinessObject
    public method new() as object
    public method handleSchema()
    public method handleData()
    protected method loadParameters()
    protected method initializeBusinessObject()
    protected data lSX1RetroactiveView as logical
endclass


//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
    @return object: self
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class SupplierSituationSmartViewBusinessObject
    _Super:new()
    self:lMostraSaldoZero := .T.
    self:lSX1RetroactiveView := FWSX1Util():ExistItem("FINR350","Visão retroativa ?          ")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author fabioh.andrade@totvs.com.br
    @since 29/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class SupplierSituationSmartViewBusinessObject    
    self:setDisplayName(STR0002) //"Posição de fornecedores"
    self:setDescription(STR0002) //"Posição de fornecedores"
    self:setPergunte("FINR350") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleSchema
   
    @return object: self:oSchema
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() class SupplierSituationSmartViewBusinessObject

    self:finAddProperty("VALOR_BAIXADO", STR0006, "number") //"Valor baixado"
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return json: Registro após manipulação.
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class SupplierSituationSmartViewBusinessObject

    self:jData["VALOR_BAIXADO"] := abs(self:jData["E2_VALOR"] - (self:jData["E2_SALDO"] - self:jData["E2_ACRESC"] + self:jData["E2_DECRESC"]))

return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class SupplierSituationSmartViewBusinessObject   

    if self:oCurrentFilter != NIL
        self:cSupplierFrom := MV_PAR01 
        self:cSupplierTo := MV_PAR02
        self:cPrefixFrom := MV_PAR03 
        self:cPrefixTo := MV_PAR04
        self:cBillNumberFrom := MV_PAR05
        self:cBillNumberTo := MV_PAR06
        self:dIssueDateFrom := MV_PAR07
        self:dIssueDateTo := MV_PAR08
        self:dDueDateFrom := MV_PAR09
        self:dDueDateTo := MV_PAR10
        self:cClassFrom := MV_PAR11
        self:cClassTo := MV_PAR12

        self:nMoeda := self:getCurrencyValue(MV_PAR13)

        self:lListarOutrasMoedas := MV_PAR14 <> 1

        if self:lSX1RetroactiveView
            if self:lVisaoRetroativa := ( MV_PAR15 == 1 )
                self:dVisaoRetroativa := MV_PAR16
            endIf
        endIf
    endIf
return
