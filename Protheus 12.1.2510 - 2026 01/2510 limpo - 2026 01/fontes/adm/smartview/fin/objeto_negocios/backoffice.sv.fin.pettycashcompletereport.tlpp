#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.pettycashcompletereport.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SET,SED,CTT", name="Listagem e Status do Caixinha", country="ALL", customTables="SET,SED,CTT")


//-------------------------------------------------------------------
/*/{Protheus.doc} PettyCashCompleteReportSmartViewBusinessObject

@author Fábio Henrique Andrade
@since 21/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------  
class PettyCashCompleteReportSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema() as object

    protected data cPettyCashFrom   as character
    protected data cPettyCashTo     as character

    protected method initializeBusinessObject()
    protected method loadDefaultParameters()
    protected method loadParameters()
    protected method loadStatement()
    protected method handleData()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Fábio Henrique Andrade
@since 21/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------   
method new() class PettyCashCompleteReportSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setAllTrim(.T.)
    self:setMainTable("SET", NIL, "SETT")
    self:setBranchFilter(.T., .T.)
    
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class PettyCashCompleteReportSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Listagem e Status do Caixinha"
    self:setDescription(STR0003) //"Listagem e Status do Caixinha"
    self:setPergunte("FINT009")  
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Fábio Henrique Andrade
@since 21/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method mySetSchema() as object class PettyCashCompleteReportSmartViewBusinessObject

    local aFieldsSET   := {} as array
    
    aFieldsSET := {  "ET_FILIAL", "ET_CODIGO", "ET_NOME", "ET_VALOR",;
                     "ET_TPREP", "ET_LIMREP", "ET_BANCO", "ET_AGEBCO", "ET_CTABCO",; 
                     "ET_SITUAC", "ET_NATUREZ", "ET_CC", "ET_DTCRIA", "ET_ULTREP",; 
                     "ET_SALDO" }


    self:oSchema:aliasToSchema( "SET", aFieldsSET )

    self:FinAddProperty("DADOS_CAIXINHA",  STR0004, "string") //"Dados do Caixinha"
	self:FinAddProperty("ED_DESCRIC",      STR0005, "string", "ED_DESCRIC") //"Descrição da Natureza"
    self:FinAddProperty("CTT_DESC01",      STR0006, "string", "CTT_DESC01") //"Descrição do Centro de Custo"

    FwFreeArray(aFieldsSET)    

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@author Fábio Henrique Andrade Silva
@since 21/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class PettyCashCompleteReportSmartViewBusinessObject
    local cQuery        := ""   as character
    local nParamOrder   := 1    as numeric
    local cFieldsToSQL  := replace(self:getAllFieldsToSQL(),"SET", "SETT") as character

    cQuery := " SELECT ? "
    cQuery += "  , COALESCE(CTT_DESC01,' ') CTT_DESC01 "
    cQuery += " FROM " + RetSqlName("SET") + " SETT "
    cQuery += "  JOIN " + RetSqlName("SED") + " SED ON " + FwJoinFilial("SET", "SED", "SETT", "SED")
    cQuery += "   AND SED.ED_CODIGO = SETT.ET_NATUREZ "
    cQuery += "   AND SED.D_E_L_E_T_ = ? "
    cQuery += "  LEFT JOIN " + RetSqlName("CTT") + " CTT ON " + FwJoinFilial("SET", "CTT", "SETT", "CTT")
	cQuery += "   AND CTT.CTT_CUSTO = SETT.ET_CC "
	cQuery += "   AND CTT.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "  AND SETT.ET_CODIGO BETWEEN ? AND ? "
    cQuery += "  AND SETT.D_E_L_E_T_ = ? "

    cQuery += " ? "

    cQuery += " ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(nParamOrder++, cFieldsToSQL)
    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetString(nParamOrder++, ' ')
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cPettyCashFrom)
    self:oStatement:SetString(nParamOrder++, self:cPettyCashTo)
    self:oStatement:SetString(nParamOrder++, ' ')
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParamOrder++, SqlOrder(SET->(IndexKey(1))))
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports
@author Fábio Henrique Andrade
@since 21/08/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() class PettyCashCompleteReportSmartViewBusinessObject

    local cDadosCaixinha := "" as character


    cDadosCaixinha := (self:cAlias)->ET_BANCO + "-" + (self:cAlias)->ET_AGEBCO + "-" + (self:cAlias)->ET_CTABCO

    self:jData := {;
		"ET_TPREP":         X3Combo("ET_TPREP",(self:cAlias)->ET_TPREP),;
        "DADOS_CAIXINHA":   cDadosCaixinha,;
		"ET_SITUAC":        X3Combo("ET_SITUAC",(self:cAlias)->ET_SITUAC),;
		"ET_NATUREZ":       MascNat((self:cAlias)->ET_NATUREZ),;
        "ED_DESCRIC":       AllTrim((self:cAlias)->ED_DESCRIC),;
        "CTT_DESC01":       AllTrim((self:cAlias)->CTT_DESC01);
    }
 
return self:oData

/*/{Protheus.doc} loadDefaultParameters
    @author Guilherme de Paula Santos
    @since 04/01/2024
    @version 12.1.2310
*/
method loadDefaultParameters() class PettyCashCompleteReportSmartViewBusinessObject
    self:cPettyCashFrom      := " "
    self:cPettyCashTo        := "ZZZ"
return

/*/{Protheus.doc} loadParameters
    @author Guilherme de Paula Santos
    @since 04/01/2024
    @version 12.1.2310
*/
method loadParameters() class PettyCashCompleteReportSmartViewBusinessObject
    if self:oCurrentFilter  != NIL
        self:cPettyCashFrom        := MV_PAR01
        self:cPettyCashTo          := MV_PAR02
    endIf
return
