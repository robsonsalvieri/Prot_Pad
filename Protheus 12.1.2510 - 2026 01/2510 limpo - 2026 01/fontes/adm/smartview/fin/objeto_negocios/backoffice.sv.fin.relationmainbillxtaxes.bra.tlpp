#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.fin.relationmainbillxtaxes.bra.ch'

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2,FK7", name="Títulos a pagar x impostos retidos", country="BRA",customTables="SE2,SA2,FK7")
//-------------------------------------------------------------------
/*/{Protheus.doc} RelationMainBillXTaxesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para geração do Títulos a pagar X Impostos

@author Pâmela Bernardo
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class RelationMainBillXTaxesSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema() as object
    
    protected data oStatementTaxes as object

    protected data cSupplierFrom as character 
    protected data cSupplierTo as character 
    protected data cSupplierUnitFrom as character
    protected data cSupplierUnitTo as character 
    protected data dIssueDateFrom as date
    protected data dIssueDateTo as date
    protected data dDueDateFrom as date
    protected data dDueDateTo as date

    protected data lPCCBaixa as logical
    protected data lCalcIssBx as logical
    protected data aReductionTypes as array

    protected data jSupplierData as json

    protected data lUseBillRelated as logical

    protected method initializeBusinessObject()
    protected method setStatementTaxes()
    protected method loadStatement()
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method myProcessData()
    protected method loadSupplierData()
    protected method validUsoBillRelated()
endclass
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Pâmela Bernardo
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class RelationMainBillXTaxesSmartViewBusinessObject
    _Super:new()
    self:initializeBusinessObject()
    self:setPageSize(50)
    self:setAppendInMyAliastoData(.F.)
    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)
    
    self:lPCCBaixa := SuperGetMv("MV_BX10925", .T., "2") == "1"
    self:lCalcIssBx := SuperGetMv("MV_MRETISS", .T., "1") == "2"
    self:aReductionTypes := StrTokArr2(MVABATIM,"|")
    self:jSupplierData := JSonObject():new()
    self:lUseBillRelated := self:validUsoBillRelated()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Pâmela Bernardo
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------

method mySetSchema() as object class RelationMainBillXTaxesSmartViewBusinessObject
    Local aFieldsSE2:= {} as array
    Local aFieldsSA2:= {} as array
    Local aFieldsFK7:= {} as array
    Local cFieldPai := "" as character

    cFieldPai := IIF(self:lUseBillRelated, "FK7_IDDOC", "E2_TITPAI")

    aFieldsSE2 := { "E2_FILIAL", "E2_PREFIXO", "E2_NUM", "E2_PARCELA", "E2_TIPO", "E2_FORNECE",;
                    "E2_LOJA","E2_EMISSAO", "E2_VENCREA", "E2_NATUREZ", "E2_VALOR", "E2_MOEDA",;
                    "E2_TXMOEDA", "E2_IRRF", "E2_ISS", "E2_INSS", "E2_PIS", "E2_COFINS","E2_CSLL",;
                    "E2_SEST"}

    If cFieldPai == "E2_TITPAI"
        aAdd(aFieldsSE2, cFieldPai)
    EndIf
                    
    aFieldsSA2 := { "A2_COD", "A2_LOJA", "A2_NOME", "A2_CGC", "A2_CALCIRF"}

    self:oSchema:aliasToSchema("SE2", aFieldsSE2)
    self:oSchema:aliasToSchema("SA2", aFieldsSA2)

    If cFieldPai == "FK7_IDDOC"
        aFieldsFK7 := {cFieldPai}
        self:oSchema:aliasToSchema("FK7", aFieldsFK7)
    EndIf

    self:finAddProperty("VALORLIQ" , STR0003, "number") //"Valor Liquido"
    self:finAddProperty("VALORBRT" , STR0004, "number") //"Valor Bruto"
    self:finAddProperty("PREFPAI"  , STR0007, "string", "E2_PREFIXO") //"Prefixo Tit Princ"
    self:finAddProperty("NUMPAI"   , STR0008, "string", "E2_NUM") //"Numero Tit Princ"
    self:finAddProperty("PARCPAI"  , STR0009, "string", "E2_PARCELA") //"Parcela Tit Princ"
    self:finAddProperty("TIPOPAI"  , STR0010, "string", cFieldPai) //"Tipo Tit Princ"
    self:finAddProperty("FORNPAI"  , STR0011, "string", "E2_FORNECE") //"Fornecedor Tit Princ"
    self:finAddProperty("LOJAPAI"  , STR0012, "string", "E2_LOJA") //"Loja Tit Princ"
    self:finAddProperty("NATPAI"   , STR0013, "string", "E2_NATUREZ") //"Natureza Tit Princ"
    self:finAddProperty("DTEMISSAO", STR0014, "date", "E2_EMISSAO") //"Emissao Tit Princ"
    self:finAddProperty("DTVENCREA", STR0015, "date", "E2_VENCREA") //"Vencimento Tit Princ"

    FwFreeArray(aFieldsSE2)
    FwFreeArray(aFieldsSA2)
    FwFreeArray(aFieldsFK7)
return self:oSchema


//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class RelationMainBillXTaxesSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Titulos a pagar X Impostos
    self:setDescription(STR0002) //"Titulos a pagar X Impostos
    self:setPergunte("FINR855")
return

//-------------------------------------------------------------------
/*{Protheus.doc} myProcessData()
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Pâmela Bernardo
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method myProcessData() class RelationMainBillXTaxesSmartViewBusinessObject
    Local cFilTitPai           := ""                     as character
    Local cPrefPai             := ""                     as character
    Local cNumPai              := ""                     as character
    Local cParcPai             := ""                     as character
    Local cTipoPai             := ""                     as character
    Local cNatPai              := ""                     as character
    local cNome                := ""                     as character
    Local dEmissao             := dDatabase              as date
    Local dVencimento          := dDatabase              as date
    Local cAliasImpostos       := ""                     as character
    Local nValorLiquido        := 0                      as numeric
    Local nValorBruto          := 0                      as numeric
    Local nTxMoeda             := 0                      as numeric
    Local nValor               := 0                      as numeric
    Local nCasasDecimaisTitulo := TamSx3("E2_VALOR")[2]  as numeric
    Local nCasasDecimaisMoeda  := TamSx3("M2_MOEDA2")[2] as numeric
    Local cTituloPai           := ""                     as character                  

    self:loadSupplierData()

    cFilTitPai  := (self:cAlias)->E2_FILIAL
    cPrefPai    := (self:cAlias)->E2_PREFIXO
    cNumPai     := (self:cAlias)->E2_NUM
    cParcPai    := (self:cAlias)->E2_PARCELA
    cTipoPai    := (self:cAlias)->E2_TIPO
    cNatPai     := (self:cAlias)->E2_NATUREZ
    dEmissao    := (self:cAlias)->E2_EMISSAO
    dVencimento := (self:cAlias)->E2_VENCREA
    cNome       := alltrim((self:cAlias)->A2_NOME)

    If (self:cAlias)->E2_TXMOEDA == 0
        nTxMoeda := RecMoeda((self:cAlias)->E2_EMISSAO, (self:cAlias)->E2_MOEDA)
    Else 
        nTxMoeda := (self:cAlias)->E2_TXMOEDA
    EndIf
    nValor := Round(xMoeda((self:cAlias)->E2_VALOR, (self:cAlias)->E2_MOEDA, 1, (self:cAlias)->E2_EMISSAO, nCasasDecimaisMoeda,nTxMoeda), nCasasDecimaisTitulo)
    nValorBruto := nValor + (self:cAlias)->E2_INSS + (self:cAlias)->E2_SEST

    If !self:lPCCBaixa
        nValorBruto += (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL
    EndIf

    If !self:lCalcIssBx
        nValorBruto += (self:cAlias)->E2_ISS
    EndIf

    If !self:jSupplierData["lIRRFBaixa"]
        nValorBruto += (self:cAlias)->E2_IRRF
    EndIf

    nValorLiquido := nValorBruto

    If self:lUseBillRelated
        cTituloPai := (self:cAlias)->FK7_IDDOC
    Else
        cTituloPai := cPrefPai + cNumPai + cParcPai + cTipoPai + self:jSupplierData["cCode"] + self:jSupplierData["cUnit"]
    EndIf 

    self:setStatementTaxes(cTituloPai, cFilTitPai)
    cAliasImpostos := self:oStatementTaxes:openAlias()
    
    (cAliasImpostos)->(DBGoTop())

    While !(cAliasImpostos)->(Eof())
        nValorLiquido -= (cAliasImpostos)->E2_VALOR

        self:jData := JsonObject():New() 
        self:jData["E2_FILIAL"] := (cAliasImpostos)->E2_FILIAL
        self:jData["E2_FORNECE"] := (cAliasImpostos)->E2_FORNECE
        self:jData["E2_LOJA"] := (cAliasImpostos)->E2_LOJA
        self:jData["E2_PREFIXO"] := (cAliasImpostos)->E2_PREFIXO
        self:jData["E2_NUM"] := (cAliasImpostos)->E2_NUM
        self:jData["E2_PARCELA"] := (cAliasImpostos)->E2_PARCELA
        If self:lUseBillRelated
            self:jData["FK7_IDDOC"] := (cAliasImpostos)->FK7_IDPAI
        Else    
            self:jData["E2_TITPAI"] := (cAliasImpostos)->E2_TITPAI
        EndIf 
        self:jData["E2_EMISSAO"] := (self:varToTimeStamp((cAliasImpostos)->E2_EMISSAO))
        self:jData["E2_VENCREA"] := (self:varToTimeStamp((cAliasImpostos)->E2_VENCREA))
        self:jData["E2_TIPO"] := (cAliasImpostos)->E2_TIPO
        self:jData["E2_VALOR"] := (cAliasImpostos)->E2_VALOR
        self:jData["E2_NATUREZ"] := (cAliasImpostos)->E2_NATUREZ
        self:jData["FORNPAI"] := self:jSupplierData["cCode"]
        self:jData["LOJAPAI"] := self:jSupplierData["cUnit"]
        self:jData["A2_NOME"] := cNome
        self:jData["A2_CGC"] := self:jSupplierData["cCGC"]
        self:jData["PREFPAI"] := cPrefPai
        self:jData["NUMPAI"] := cNumPai
        self:jData["PARCPAI"] := cParcPai
        self:jData["TIPOPAI"] := cTipoPai
        self:jData["NATPAI"] := cNatPai
        self:jData["DTEMISSAO"] := self:varToTimeStamp(dEmissao)
        self:jData["DTVENCREA"] := self:varToTimeStamp(dVencimento)
        self:jData["VALORBRT"] := nValorBruto
        self:jData["VALORLIQ"] := nValorLiquido

        self:processAndAppend()

        (cAliasImpostos)->(DbSkip())
    EndDo    
    (cAliasImpostos)->(DBCloseArea())    
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@return character: cQuery

@author Pâmela Bernardo
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatement() class RelationMainBillXTaxesSmartViewBusinessObject
    local cQuery      := "" as character
    local nParamOrder := 1 as numeric
    Local nFor        := 1 as numeric

    cQuery := "SELECT ? "
    cQuery += " FROM " + RetSQLName("SE2") + " SE2 "
    cQuery += "     JOIN " + RetSQLName("SA2") + " SA2 "
    cQuery += "         ON " + FWJoinFilial("SE2", "SA2")
    cQuery += "         AND SE2.E2_FORNECE = SA2.A2_COD "
    cQuery += "         AND SE2.E2_LOJA = SA2.A2_LOJA "
    cQuery += "         AND SA2.D_E_L_E_T_ = ? "
    If self:lUseBillRelated
        cQuery += " JOIN  " + RetSQLName("FK7") + " FK7 "
        cQuery += "     ON FK7.FK7_ALIAS = ? "
        cQuery += "     AND SE2.E2_FILIAL = FK7.FK7_FILTIT "
        cQuery += "     AND SE2.E2_PREFIXO = FK7.FK7_PREFIX "
        cQuery += "     AND SE2.E2_NUM = FK7.FK7_NUM "
        cQuery += "     AND SE2.E2_PARCELA = FK7.FK7_PARCEL "
        cQuery += "     AND SE2.E2_TIPO = FK7.FK7_TIPO "
        cQuery += "     AND SE2.E2_FORNECE = FK7.FK7_CLIFOR "
        cQuery += "     AND SE2.E2_LOJA = FK7.FK7_LOJA "
        cQuery += "     AND FK7.D_E_L_E_T_ = ? "
    EndIf
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SE2.E2_FORNECE  BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_LOJA  BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_EMISSAO BETWEEN ? AND ? "
    cQuery += " AND SE2.E2_VENCREA BETWEEN ? AND ? "
    cQuery += " AND (SE2.E2_INSS > ? OR SE2.E2_ISS > ? OR SE2.E2_PIS > ? OR SE2.E2_COFINS > ? OR SE2.E2_CSLL > ? OR "
    cQuery += "         SE2.E2_SEST > ? OR (SE2.E2_IRRF > ? OR (SE2.E2_VRETIRF > ? AND SE2.E2_PRETIRF <> ?))) "
    cQuery += " AND SE2.D_E_L_E_T_ = ? "

    cQuery += " ? "

    cQuery += " ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    self:oStatement:= FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nParamOrder++, Space(1))
    If self:lUseBillRelated
        self:oStatement:SetString(nParamOrder++, "SE2")
        self:oStatement:SetString(nParamOrder++, Space(1))
    EndIf 
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cSupplierFrom)
    self:oStatement:SetString(nParamOrder++, self:cSupplierTo)
    self:oStatement:SetString(nParamOrder++, self:cSupplierUnitFrom)
    self:oStatement:SetString(nParamOrder++, self:cSupplierUnitTo)
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(nParamOrder++, self:dIssueDateTo)
    self:oStatement:SetDate(nParamOrder++, self:dDueDateFrom)
    self:oStatement:SetDate(nParamOrder++, self:dDueDateTo)    
    For nFor := 1 to 8
        self:oStatement:SetNumeric(nParamOrder++, 0)
    Next nFor
    self:oStatement:SetString(nParamOrder++, "1")
    self:oStatement:SetString(nParamOrder++, Space(1))
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParamOrder++, SqlOrder(SE2->(IndexKey( 6 ))))
Return

//----------------------------------------------------------------
/*/{Protheus.doc} setStatementTaxes
Função que prepara os dados do título de impostos

@author Pâmela Bernardo
@since  23/05/2023
@version 12
@param cTitPai - Chave de identificação dos título pai dos TXs

/*/
//-----------------------------------------------------------------
method setStatementTaxes(cTitPai as character, cFilTitPai as character) class RelationMainBillXTaxesSmartViewBusinessObject
    Local cQuery := "" as character
    Local nParamOrder := 1 as numeric

    If self:oStatementTaxes == NIL
        If !self:lUseBillRelated
            cQuery:= "SELECT E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_TITPAI, "
            cQuery += "E2_FORNECE, E2_LOJA, E2_NATUREZ, E2_EMISSAO, E2_VENCREA, E2_VALOR "
            cQuery += "FROM " + RetSQLName("SE2") + " SE2 "
            cQuery += "WHERE SE2.E2_FILIAL = ? "
            cQuery += "AND SE2.E2_TITPAI = ? "
            cQuery += "AND SE2.E2_TIPO NOT IN (?) "
            cQuery += "AND SE2.D_E_L_E_T_ = ? "
        Else
            cQuery:= "SELECT E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, FK7_IDPAI, "
            cQuery += "E2_FORNECE, E2_LOJA, E2_NATUREZ, E2_EMISSAO, E2_VENCREA, E2_VALOR "
            cQuery += "FROM " + RetSQLName("FK7") + " FK7 "
            cQuery += "   JOIN SE2T10 SE2 "
            cQuery += "     ON SE2.E2_FILIAL = FK7.FK7_FILTIT "
            cQuery += "     AND SE2.E2_PREFIXO = FK7.FK7_PREFIX "
            cQuery += "     AND SE2.E2_NUM = FK7.FK7_NUM "
            cQuery += "     AND SE2.E2_PARCELA = FK7.FK7_PARCEL "
            cQuery += "     AND SE2.E2_TIPO = FK7.FK7_TIPO "
            cQuery += "     AND SE2.E2_FORNECE = FK7.FK7_CLIFOR "
            cQuery += "     AND SE2.E2_LOJA = FK7.FK7_LOJA "
            cQuery += "     AND SE2.D_E_L_E_T_ = ? " 
            cQuery += "WHERE FK7.FK7_FILTIT = ? "
            cQuery += "AND FK7.FK7_ALIAS = ? "
            cQuery += "AND FK7.FK7_IDPAI = ? "
            cQuery += "AND FK7.FK7_TIPO NOT IN (?) "
            cQuery += "AND FK7.D_E_L_E_T_ = ? "
        EndIf

        cQuery := ChangeQuery(cQuery)
        self:oStatementTaxes := FWExecStatement():New(cQuery)
    EndIf

    If self:lUseBillRelated
        self:oStatementTaxes:SetString(nParamOrder++, Space(1))
    EndIf 
    self:oStatementTaxes:SetString(nParamOrder++, cFilTitPai)
    If self:lUseBillRelated
        self:oStatementTaxes:SetString(nParamOrder++, "SE2")
    EndIf
    self:oStatementTaxes:SetString(nParamOrder++, cTitPai)
    self:oStatementTaxes:setIn(nParamOrder++, self:aReductionTypes)
    self:oStatementTaxes:SetString(nParamOrder++, Space(1))
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    Inicializa as propriedades de parâmetros do objeto de negócio
    para não correr riscos de error.log.
    Será implementado no objeto de negócio.
    @author matheus.monteiro@totvs.com.br
    @since 12/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class RelationMainBillXTaxesSmartViewBusinessObject
    self:cSupplierFrom := " "
    self:cSupplierTo := " " 
    self:cSupplierUnitFrom := " "
    self:cSupplierUnitTo := " " 
    self:dIssueDateFrom := dDatabase
    self:dIssueDateTo := dDatabase
    self:dDueDateFrom := dDatabase 
    self:dDueDateTo := dDatabase
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    Carrega os parâmetros da consulta antes de chamar o getQuery().
    Será implementado no objeto de negócio, de acordo com a necessidade e
    os parâmetros utilizados.
    @author matheus.monteiro@totvs.com.br
    @since 12/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class RelationMainBillXTaxesSmartViewBusinessObject
    If self:oCurrentFilter != Nil
        self:cSupplierFrom := MV_PAR01
        self:cSupplierTo := MV_PAR02 
        self:cSupplierUnitFrom := MV_PAR03
        self:cSupplierUnitTo := MV_PAR04 
        self:dIssueDateFrom := MV_PAR05
        self:dIssueDateTo := MV_PAR06 
        self:dDueDateFrom := MV_PAR07 
        self:dDueDateTo := MV_PAR08
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadSupplierData
    @author guilherme.sordi@totvs.com.br
    @since 23/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadSupplierData() class RelationMainBillXTaxesSmartViewBusinessObject
    if !self:jSupplierData:hasProperty("cCode")
        self:jSupplierData := {"cCode": "", "cUnit": ""}
    endIf
    if (self:jSupplierData["cCode"] <> (self:cAlias)->E2_FORNECE) .or. (self:jSupplierData["cUnit"] <> (self:cAlias)->E2_LOJA)
        self:jSupplierData["cCode"] := (self:cAlias)->E2_FORNECE
        self:jSupplierData["cUnit"] := (self:cAlias)->E2_LOJA
        self:jSupplierData["cCGC"] := self:transformCGC((self:cAlias)->A2_CGC, "A2_CGC")
        self:jSupplierData["lIRRFBaixa"] := (self:cAlias)->A2_CALCIRF == "2"
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} validUsoBillRelated
  Define se pode ser utilizado o relacionamento da FK7_IDPAI através dos campos E2_TITPAI
  @return lRet as Logical
  @author Wesley França
  @since 17/12/2025
/*/
//-------------------------------------------------------------------
method validUsoBillRelated() class RelationMainBillXTaxesSmartViewBusinessObject
	local lRet as Logical 
	local oBillRelated as Object

	If FindFunction("UsaBillRel")
        oBillRelated := UsaBillRel()
        lRet := (oBillRelated <> Nil)
		oBillRelated := Nil
    EndIf
return lRet
