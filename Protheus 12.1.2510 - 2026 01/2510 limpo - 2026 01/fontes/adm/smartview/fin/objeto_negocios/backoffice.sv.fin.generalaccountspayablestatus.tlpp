#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.generalaccountspayablestatus.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,FK2,FK6,SC7", name="Posição Geral do Contas a Pagar", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} GeneralAccountsPayableStatusReportSmartViewBusinessObject
Classe para criação do Objeto de Negócio para geração do Posição Geral do Contas a Pagar

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class GeneralAccountsPayableStatusReportSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method getData() as object
    public method mySetSchema()

    protected data lConvertCurrencyRate as logical

    protected data oQueryTitulos as object
    protected data oQueryBaixas as object
    protected data oQueryCompras as object

    protected data dDueDateFrom as date
    protected data dDueDateTo as date
    protected data dPostingDateFrom as date
    protected data dPostingDateTo as date  
    protected data cPrefixFrom as character
    protected data cPrefixTo as character
    protected data nCurrency as numeric
    protected data nListPosting as numeric
    protected data jRelationSE2SC7 as numeric

    protected method getListOfBills() 
    protected method getListOfPosting() 
    protected method getListOfPurchaseOrders()
    
    protected method setDataListOfBills() as object
    protected method setDataListOfPosting() as object
    protected method setDataListOfPurchaseOrders() as object

    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method initializeBusinessObject() 
    protected method changeCanFilter()
    protected method filterWithRelationOfSC7() as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    _Super:new()

    self:setCompleteData(.F.)
    self:initializeBusinessObject()
    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 02/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Posição Geral do Contas a Pagar"
    self:setDescription(STR0003) //"Posição Geral do Contas a Pagar - Listagem de títulos por categoria e aging."
    self:setPergunte("FINR330")  
return


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    if !self:validEnvironment()
        return self:oData
    endIf

    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadFilterBranches( { "SE2", "SC7" } )
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()
    self:loadLGPDFields()

    //Carrega o bloco de títulos a pagar
    self:getListOfBills()
    self:cAlias := self:oQueryTitulos:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListOfBills()    
    (self:cAlias)->( DBCloseArea() )

    //Carrega o bloco de baixas
    self:getListOfPosting()
    self:cAlias := self:oQueryBaixas:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListOfPosting()
    (self:cAlias)->( DBCloseArea() )

    //Carrega o bloco de pedidos de compras
    self:getListOfPurchaseOrders()
    self:cAlias := self:oQueryCompras:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListOfPurchaseOrders()
    (self:cAlias)->( DBCloseArea() )
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local aFieldsSE2 := {} as array
    Local aFieldsSED := {} as array
    Local aFieldsFK2 := {} as array

    aFieldsSE2 := { "E2_FILIAL", "E2_PREFIXO", "E2_PARCELA", "E2_TIPO", "E2_NATUREZ",;
        "E2_FORNECE", "E2_LOJA", "E2_NOMFOR", "E2_EMISSAO", "E2_BAIXA" }
    aFieldsSED := { "ED_DESCRIC" }
    aFieldsFK2 := { "FK2_VALOR" }

    self:jRelationSE2SC7 := {"E2_FILIAL":"C7_FILIAL","E2_FORNECE":"C7_FORNECE","E2_LOJA":"C7_LOJA","E2_EMISSAO":"C7_EMISSAO"}

    self:oSchema:aliasToSchema("SE2", aFieldsSE2)
    self:oSchema:aliasToSchema("SED", aFieldsSED)
    self:oSchema:aliasToSchema("FK2", aFieldsFK2)

    self:finAddProperty("TITULO_PEDIDO"  , STR0004, "string")         //"Nro. Título/Ped. Compras"
    self:finAddProperty("VENCIMENTO"     , STR0005, "date")     //"Vencto Tit./Prev. Entrega"
    self:finAddProperty("VALOR_TITPED"   , STR0006, "number")       //"Vlr. Titulo/Total Compras"
    self:finAddProperty("SALDO"          , STR0007, "number")       //"Vlr. em Aberto"
    self:finAddProperty("TIPOCOMPRA"     , STR0008, "string")     //"Tipo de Compra"
    self:finAddProperty("TIPOTITULO"     , STR0009, "string")     //"Tipo de Título"
    self:finAddProperty("CATEG_TITULO"   , STR0010, "string")   //"Classificação"
    self:finAddProperty("AGING_TITULO"   , STR0011, "string")   //"Aging do Título"
    self:finAddProperty("VALOR_CORRECAO" , STR0043, "number")   //"Valor de Correção"
    self:finAddProperty("VALOR_ACESSORIO", STR0044, "number")   //"Valor Acessório"

    FwFreeArray(aFieldsSE2)
    FwFreeArray(aFieldsSED)
    FwFreeArray(aFieldsFK2)

    self:changeCanFilter()

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getListOfBills
Retorna a query a ser executada na função getData

@return character: cQuery

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getListOfBills()  class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cQuery  := "" as character
    Local nParamOrder := 1 as numeric

    //Define a query do Objeto de Negócio
    cQuery := "SELECT "
    cQuery += "E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, "
    cQuery += "E2_NATUREZ, ED_DESCRIC, E2_FORNECE, E2_LOJA, E2_NOMFOR, "
    cQuery += "E2_EMISSAO, E2_VENCREA, E2_BAIXA, E2_VALOR, E2_SALDO, "
    cQuery += "E2_TXMOEDA, E2_MOEDA "
    
    cQuery += "FROM " + RetSQLName("SE2") + " SE2 "
    cQuery += "INNER JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SED","SE2") + " AND "
    cQuery += " SED.ED_CODIGO = SE2.E2_NATUREZ AND "
    cQuery += " SED.D_E_L_E_T_ = ? "
    cQuery += "WHERE "
    if self:lEmptyMultiBranches
        cQuery += " SE2.E2_FILIAL = ? AND "
    else
        cQuery += " SE2.E2_FILIAL IN (?) AND "
    endIf
    cQuery += " SE2.E2_VENCREA BETWEEN ? AND ? AND "
    cQuery += " SE2.E2_PREFIXO BETWEEN ? AND ? AND "
    cQuery += " SE2.E2_TIPO NOT IN ? AND " 
    cQuery += " SE2.E2_SALDO > ? AND "

    If !self:lConvertCurrencyRate
        cQuery += " SE2.E2_MOEDA = ? AND "
    EndIf

    cQuery += " SE2.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SE2->(IndexKey( 1 )))

    cQuery := ChangeQuery(cQuery)

    self:oQueryTitulos:= FWExecStatement():New(cQuery)

    self:oQueryTitulos:SetString(nParamOrder++, " ")
    if self:lEmptyMultiBranches
        self:oQueryTitulos:SetString(nParamOrder++, self:oFilterBranches["SE2"][1])
    else
        self:oQueryTitulos:SetIn(nParamOrder++, self:oFilterBranches["SE2"][1])
    endIf
    self:oQueryTitulos:SetDate(nParamOrder++, self:dDueDateFrom)
    self:oQueryTitulos:SetDate(nParamOrder++, self:dDueDateTo)
    self:oQueryTitulos:SetString(nParamOrder++, self:cPrefixFrom)
    self:oQueryTitulos:SetString(nParamOrder++, self:cPrefixTo)
    self:oQueryTitulos:SetUnsafe(nParamOrder++, FormatIn(MVPROVIS + "|" + MVABATIM,"|" ))
    self:oQueryTitulos:SetNumeric(nParamOrder++, 0)

    If !self:lConvertCurrencyRate
        self:oQueryTitulos:SetNumeric(nParamOrder++,self:nCurrency)
    EndIf

    self:oQueryTitulos:SetString(nParamOrder++," ")

Return 

//-------------------------------------------------------------------
/*{Protheus.doc} getListOfPosting
Retorna a query a ser executada na função getData

@return character: cQuery

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getListOfPosting() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cQuery  := "" as character
    Local nParamOrder := 1 as numeric

    cQuery := "SELECT "
	cQuery += " E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_NATUREZ, "
	cQuery += " E2_FORNECE, E2_LOJA, E2_NOMFOR, E2_EMISSAO, E2_VENCREA, E2_BAIXA, "  
	cQuery += " E2_VALOR, E2_SALDO, ED_DESCRIC, FK2_TPDOC, FK2_TXMOED, FK2_MOTBX, "
	cQuery += " FK2_VALOR, FK2_VLMOE2, FK2_DATA, FK2_MOEDA, E2_MOEDA, E2_TXMOEDA " 
    cQuery += " ,(SELECT COALESCE(SUM(CASE WHEN FK6.FK6_TPDOC NOT IN ('DC', 'D2') THEN FK6.FK6_VALMOV ELSE -FK6.FK6_VALMOV END), 0) "
	cQuery += " 	FROM " + RetSQLName("FK6") + " FK6 "
	cQuery += " 	WHERE " + FWJoinFilial("FK6","FK2")
	cQuery += " 	AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
	cQuery += " 	AND FK6.FK6_TABORI = ? "
	cQuery += " 	AND FK6.FK6_TPDOC IN (?) "
	cQuery += " 	AND FK6.D_E_L_E_T_ = ? ) AS VALOR_CORRECAO, "
    cQuery += " (SELECT COALESCE(SUM(CASE WHEN FK6.FK6_TPDOC NOT IN ('DC', 'D2') THEN FK6.FK6_VALMOV ELSE -FK6.FK6_VALMOV END), 0) "
	cQuery += " 	FROM " + RetSQLName("FK6") + " FK6 "
	cQuery += " 	WHERE " + FWJoinFilial("FK6","FK2")
	cQuery += " 	AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
	cQuery += " 	AND FK6.FK6_TABORI = ? "
	cQuery += " 	AND FK6.FK6_TPDOC NOT IN (?) "
	cQuery += " 	AND FK6.D_E_L_E_T_ = ? ) AS VALOR_ACESSORIO "
    cQuery += " FROM " + RetSQLName("SE2") + " SE2 "
	cQuery += " INNER JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SED","SE2") + " AND "
	cQuery += "  SED.ED_CODIGO = E2_NATUREZ AND "
	cQuery += "  SED.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 ON " 
    cQuery += "  FK7.FK7_ALIAS = ? AND "
	cQuery += "  FK7.FK7_FILTIT = SE2.E2_FILIAL AND "
	cQuery += "  FK7.FK7_PREFIX = SE2.E2_PREFIXO AND "
    cQuery += "  FK7.FK7_NUM = SE2.E2_NUM AND "
	cQuery += "  FK7.FK7_PARCEL = SE2.E2_PARCELA AND "
	cQuery += "  FK7.FK7_TIPO = SE2.E2_TIPO AND "
	cQuery += "  FK7.FK7_CLIFOR = SE2.E2_FORNECE AND "
	cQuery += "  FK7.FK7_LOJA = SE2.E2_LOJA AND "
	cQuery += "  FK7.D_E_L_E_T_ = ? AND "
    cQuery += "  " + FWJoinFilial("FK7","SE2")
	cQuery += " INNER JOIN " + RetSQLName("FK2") + " FK2 ON " + FWJoinFilial("FK2","FK7") + " AND "
	cQuery += " FK2.FK2_IDDOC = FK7.FK7_IDDOC AND "
    cQuery += " FK2.FK2_MOTBX NOT IN(?) AND "

    If self:nListPosting == 1
	    cQuery += " FK2.FK2_DATA BETWEEN ? AND ? AND "
    ElseIf self:nListPosting == 2
	    cQuery += " FK2.FK2_DTDIGI BETWEEN ? AND ? AND "
    ElseIf self:nListPosting == 3 
	    cQuery += " FK2.FK2_DTDISP BETWEEN ? AND ? AND "
    EndIf
	
    cQuery += " FK2.FK2_RECPAG = ? AND "
	cQuery += " FK2.D_E_L_E_T_ = ? AND "
	cQuery += " NOT EXISTS( "
	cQuery += "  SELECT FK2EST.FK2_IDDOC FROM " + RetSQLName("FK2") +  "  FK2EST "
	cQuery += "  WHERE FK2EST.FK2_IDDOC = FK2.FK2_IDDOC "
    cQuery += "   AND FK2EST.FK2_SEQ = FK2.FK2_SEQ "
	cQuery += "   AND FK2EST.FK2_TPDOC = ? " 
	cQuery += "   AND FK2EST.D_E_L_E_T_ = ? ) "

	cQuery += "WHERE "
	if self:lEmptyMultiBranches
        cQuery += " SE2.E2_FILIAL = ? AND "
    else
        cQuery += " SE2.E2_FILIAL IN (?) AND "
    endIf
    cQuery += " SE2.E2_PREFIXO BETWEEN ? AND ? AND "

    If !self:lConvertCurrencyRate
        cQuery += " SE2.E2_MOEDA = ? AND "
    EndIf

    cQuery += " SE2.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SE2->(IndexKey( 1 )))

    cQuery := ChangeQuery(cQuery)

    self:oQueryBaixas:= FWExecStatement():New(cQuery)

    //SubQuery VALOR_CORRECAO
    self:oQueryBaixas:SetString(nParamOrder++, 'FK2')
    self:oQueryBaixas:SetIn(nParamOrder++, {'CM', 'VM','C2'})
    self:oQueryBaixas:SetString(nParamOrder++, " ")
    //SubQuery VALOR_ACESSORIO
    self:oQueryBaixas:SetString(nParamOrder++, 'FK2')
    self:oQueryBaixas:SetIn(nParamOrder++, {'CM', 'VM','C2'})
    self:oQueryBaixas:SetString(nParamOrder++, " ")

    self:oQueryBaixas:SetString(nParamOrder++, " ")
    self:oQueryBaixas:SetString(nParamOrder++, 'SE2')
    self:oQueryBaixas:SetString(nParamOrder++, " ")
    self:oQueryBaixas:SetIn(nParamOrder++, {'DSD', 'FAT', 'LIQ', 'STP'})
    self:oQueryBaixas:SetDate(nParamOrder++, self:dPostingDateFrom)
    self:oQueryBaixas:SetDate(nParamOrder++, self:dPostingDateTo)
    self:oQueryBaixas:SetString(nParamOrder++, 'P')
    self:oQueryBaixas:SetString(nParamOrder++, " ")
    self:oQueryBaixas:SetString(nParamOrder++, 'ES')
    self:oQueryBaixas:SetString(nParamOrder++, " ")

    if self:lEmptyMultiBranches
        self:oQueryBaixas:SetString(nParamOrder++, self:oFilterBranches["SE2"][1])
    else
        self:oQueryBaixas:SetIn(nParamOrder++, self:oFilterBranches["SE2"][1])
    endIf
    
    self:oQueryBaixas:SetString(nParamOrder++,self:cPrefixFrom)
    self:oQueryBaixas:SetString(nParamOrder++,self:cPrefixTo)

    if !self:lConvertCurrencyRate
        self:oQueryBaixas:SetNumeric(nParamOrder++,self:nCurrency)
    endIf

    self:oQueryBaixas:SetString(nParamOrder++," ")

Return

//-------------------------------------------------------------------
/*{Protheus.doc} getListOfPurchaseOrders
Retorna a query a ser executada na função getData

@return character: cQuery

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getListOfPurchaseOrders() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cQuery  := "" as character
    Local nParamOrder := 1 as numeric

    //Define a query do Objeto de Negócio
    cQuery := "SELECT "
	cQuery += " C7_FILIAL E2_FILIAL, C7_ITEM, C7_NUM, C7_TES, C7_QUJE, C7_QUANT, C7_PRECO, C7_TOTAL, "
	cQuery += " C7_FORNECE, C7_LOJA, A2_NOME, C7_EMISSAO, C7_DATPRF, C7_MOEDA, C7_TXMOEDA "

    cQuery += " FROM " + RetSQLName("SC7") + " SC7 "
	cQuery += " INNER JOIN " + RetSQLName("SA2") + " SA2 ON " + FWJoinFilial("SA2","SC7") + " AND "
	cQuery += " SA2.A2_COD = SC7.C7_FORNECE AND "
	cQuery += " SA2.A2_LOJA = SA2.A2_LOJA AND "
	cQuery += " SA2.D_E_L_E_T_ = ? "
	cQuery += "INNER JOIN " + RetSQLName("SF4") + " SF4 ON " + FWJoinFilial("SF4","SC7") + " AND "
	cQuery += " SF4.F4_CODIGO = SC7.C7_TES AND "
    cQuery += " SF4.F4_DUPLIC = ? AND "
	cQuery += " SF4.D_E_L_E_T_ = ? "
    cQuery += "WHERE "
    if self:lEmptyMultiBranches
        cQuery += " SC7.C7_FILIAL = ? AND "
    else
        cQuery += " SC7.C7_FILIAL IN (?) AND "
    endIf
	cQuery += " C7_QUJE < C7_QUANT AND "

    If !self:lConvertCurrencyRate
        cQuery += " SC7.C7_MOEDA = ? AND "
    EndIf
    
    cQuery += " SC7.D_E_L_E_T_ = ? "

    cQuery += self:filterWithRelationOfSC7()

    cQuery := ChangeQuery(cQuery)

    self:oQueryCompras:= FWExecStatement():New(cQuery)

    self:oQueryCompras:SetString(nParamOrder++," ")
    self:oQueryCompras:SetString(nParamOrder++, 'S')
    self:oQueryCompras:SetString(nParamOrder++, " ")
    
    if self:lEmptyMultiBranches
        self:oQueryCompras:SetString(nParamOrder++, self:oFilterBranches["SC7"][1])
    else
        self:oQueryCompras:SetIn(nParamOrder++, self:oFilterBranches["SC7"][1])
    endIf    
    
    if !self:lConvertCurrencyRate
        self:oQueryCompras:SetNumeric(nParamOrder++,self:nCurrency)
    endIf

    self:oQueryCompras:SetString(nParamOrder++," ")

Return 
//-------------------------------------------------------------------
/*{Protheus.doc} setDataListOfBills
Adiciona dados dos títulos em aberto no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setDataListOfBills() as object class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cTipoCompra := "" as character
    Local cTipoTitulo := "" as character
    Local cCategoria  := "" as character
    Local cAgingPagar := "" as character
    Local nDias       := 0  as numeric
    Local nSaldo      := 0  as numeric
    Local nValor      := 0  as numeric
    Local nDecs 	  := MsDecimais(MV_PAR04) as numeric

     //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        //Tipo de Compra
        If (StoD((self:cAlias)->E2_VENCREA) - StoD((self:cAlias)->E2_EMISSAO)) <= 1
            cTipoCompra := STR0028 //"À vista"
        Else
            cTipoCompra := STR0029 //"À prazo"
        EndIf

        //Categoria do titulo
        If (self:cAlias)->E2_SALDO > 0
            If dDatabase <= StoD((self:cAlias)->E2_VENCREA) .OR. (self:cAlias)->E2_TIPO $ MVPAGANT
                cCategoria := STR0012 //"TITULOS A VENCER"
                nDias := (StoD((self:cAlias)->E2_VENCREA) - dDatabase)
            Else
                cCategoria := STR0013 //"TITULOS VENCIDOS"
                nDias := (dDatabase - StoD((self:cAlias)->E2_VENCREA))
            EndIf

            //Tipo do Titulo
            If StoD((self:cAlias)->E2_EMISSAO) <= dDatabase
                Do Case
                    Case (self:cAlias)->E2_TIPO  $ MVDUPLIC 
                        cTipoTitulo := STR0014 //Duplicatas
                    Case (self:cAlias)->E2_TIPO  $ MVNOTAFIS
                        cTipoTitulo := STR0015 //Notas fiscais
                    Case (self:cAlias)->E2_TIPO  $ MVTAXA
                        cTipoTitulo := STR0018 //"Impostos"
                    Case (self:cAlias)->E2_TIPO  $ MVPAGANT
                        cTipoTitulo := STR0019 //Pagamento antecipado
                    Case (self:cAlias)->E2_TIPO $ MV_CPNEG
                        cTipoTitulo := STR0020 //Nota de Debito
                    OtherWise
                        cTipoTitulo := STR0021 //Outros
                EndCase
            Else
                cTipoTitulo := "-"
            Endif

            //Classificação de Aging
            If nDias <= 15
                cAgingPagar := STR0022 //"Até 15 Dias"
            ElseIf nDias > 15 .And. nDias <= 30
                cAgingPagar := STR0023 //"De 16 a 30 Dias"
            ElseIf nDias > 30 .And. nDias <= 60
                cAgingPagar := STR0024 //"De 31 a 60 Dias"
            ElseIf nDias > 60 .And. nDias <= 90
                cAgingPagar := STR0025 //"De 61 a 90 Dias"
            Else
                cAgingPagar := STR0026 //"Acima de 90 Dias"
            Endif

        EndIf

        nValor := xMoeda((self:cAlias)->E2_VALOR,(self:cAlias)->E2_MOEDA,MV_PAR04,(self:cAlias)->E2_EMISSAO,nDecs+1,(self:cAlias)->E2_TXMOEDA)
        nSaldo := xMoeda((self:cAlias)->E2_SALDO,(self:cAlias)->E2_MOEDA,MV_PAR04,(self:cAlias)->E2_EMISSAO,nDecs+1,(self:cAlias)->E2_TXMOEDA)

        If (self:cAlias)->E2_TIPO $ (MVPAGANT + "|" + MV_CPNEG)
            nValor := nValor*-1
            nSaldo := nSaldo*-1
        EndIf

        self:jData := {"E2_FILIAL":     AllTrim((self:cAlias)->E2_FILIAL),;
            "E2_FORNECE":   AllTrim((self:cAlias)->E2_FORNECE),;
            "E2_LOJA":      AllTrim((self:cAlias)->E2_LOJA),;
            "E2_NOMFOR":    AllTrim((self:cAlias)->E2_NOMFOR),;
            "E2_PREFIXO":   AllTrim((self:cAlias)->E2_PREFIXO),;
            "TITULO_PEDIDO":AllTrim((self:cAlias)->E2_NUM),;
            "E2_PARCELA":   AllTrim((self:cAlias)->E2_PARCELA),;
            "E2_TIPO":      AllTrim((self:cAlias)->E2_TIPO),;
            "E2_EMISSAO":   (self:varToTimeStamp((self:cAlias)->E2_EMISSAO )),;
            "VENCIMENTO":   (self:varToTimeStamp((self:cAlias)->E2_VENCREA )),;
            "E2_BAIXA":     Nil,;
            "VALOR_TITPED":     nValor,;
            "SALDO":     nSaldo,;
            "FK2_VALOR":    0,;
            "E2_NATUREZ":   MascNat((self:cAlias)->E2_NATUREZ),;
            "ED_DESCRIC":   AllTrim((self:cAlias)->ED_DESCRIC),;
            "TIPOCOMPRA":   cTipoCompra,;
            "TIPOTITULO":   cTipoTitulo,;
            "CATEG_TITULO": cCategoria,;
            "AGING_TITULO": cAgingPagar,;
            "VALOR_CORRECAO":0,;
            "VALOR_ACESSORIO":0;
            }

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )

    EndDo
Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} setDataListOfPosting
Adiciona dados das baixas no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setDataListOfPosting() as object class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cTipoCompra := "" as character
    Local cTipoTitulo := "" as character
    Local cCategoria  := "" as character
    Local cAgingPagar := "" as character
    Local nSaldo      := 0  as numeric
    Local nValor      := 0  as numeric
    Local nBaixa      := 0  as numeric
    Local nDecs 	  := MsDecimais(MV_PAR04) as numeric
    Local nMoeda      := 0  as numeric
    Local nTxMoeda    := 0  as numeric

   
     //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    cCategoria := STR0027 //"VALORES BAIXADOS"

    While !(self:cAlias)->(Eof())

        //Tipo de Compra
        If (StoD((self:cAlias)->E2_VENCREA) - StoD((self:cAlias)->E2_EMISSAO)) <= 1
            cTipoCompra := STR0028 //"À vista"
        Else
            cTipoCompra := STR0029 //"À prazo"
        EndIf

        cAgingPagar := "-"

        nMoeda := Val(AllTrim((self:cAlias)->FK2_MOEDA))
        If (self:cAlias)->E2_TXMOEDA == 0
            nTxMoeda := RecMoeda((self:cAlias)->E2_EMISSAO, (self:cAlias)->E2_MOEDA)
        Else 
            nTxMoeda := (self:cAlias)->E2_TXMOEDA
        EndIf

        If nMoeda == (self:cAlias)->E2_MOEDA
           nTxMoeda := (self:cAlias)->FK2_TXMOED
        EndIf

        nValor := xMoeda((self:cAlias)->E2_VALOR,(self:cAlias)->E2_MOEDA,MV_PAR04,(self:cAlias)->E2_EMISSAO,nDecs+1,nTxMoeda)
        nSaldo := xMoeda((self:cAlias)->E2_SALDO,(self:cAlias)->E2_MOEDA,MV_PAR04,(self:cAlias)->E2_EMISSAO,nDecs+1,nTxMoeda)
        nBaixa := xMoeda((self:cAlias)->FK2_VALOR,nMoeda,MV_PAR04,(self:cAlias)->FK2_DATA ,nDecs+1,(self:cAlias)->FK2_TXMOED)

        If (self:cAlias)->E2_TIPO $ (MVPAGANT + "|" + MV_CPNEG)
            nValor := nValor*-1
            nSaldo := nSaldo*-1
            nBaixa := nBaixa*-1
        EndIf

        self:jData := {"E2_FILIAL":     AllTrim((self:cAlias)->E2_FILIAL),;
            "E2_FORNECE":     AllTrim((self:cAlias)->E2_FORNECE),;
            "E2_LOJA":        AllTrim((self:cAlias)->E2_LOJA),;
            "E2_NOMFOR":      AllTrim((self:cAlias)->E2_NOMFOR),;
            "E2_PREFIXO":     AllTrim((self:cAlias)->E2_PREFIXO),;
            "TITULO_PEDIDO":         AllTrim((self:cAlias)->E2_NUM),;
            "E2_PARCELA":     AllTrim((self:cAlias)->E2_PARCELA),;
            "E2_TIPO":        AllTrim((self:cAlias)->E2_TIPO),;
            "E2_EMISSAO":     (self:varToTimeStamp((self:cAlias)->E2_EMISSAO )),;
            "VENCIMENTO":     (self:varToTimeStamp((self:cAlias)->E2_VENCREA )),;
            "E2_BAIXA":       (self:varToTimeStamp((self:cAlias)->FK2_DATA )),;
            "VALOR_TITPED":       nValor,;
            "SALDO":       nSaldo,;
            "FK2_VALOR":      nBaixa,;
            "E2_NATUREZ":     MascNat((self:cAlias)->E2_NATUREZ),;
            "ED_DESCRIC":     AllTrim((self:cAlias)->ED_DESCRIC),;
            "TIPOCOMPRA":     cTipoCompra,;
            "TIPOTITULO":     cTipoTitulo,;
            "CATEG_TITULO":   cCategoria,;
            "AGING_TITULO":   cAgingPagar,;
            "VALOR_CORRECAO": (self:cAlias)->VALOR_CORRECAO,;
            "VALOR_ACESSORIO":(self:cAlias)->VALOR_ACESSORIO;
            }

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )
    EndDo
Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} setDataListOfPurchaseOrders
Adiciona dados dos pedidos de compras no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 30/05/2023
@version 1.0

*/
//-------------------------------------------------------------------
method setDataListOfPurchaseOrders() as object class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    Local cTipoCompra := "" as character
    Local cTipoTitulo := "" as character
    Local cCategoria  := "" as character
    Local cAgingPagar := "" as character
    Local nDias       := 0  as numeric
    Local nVlrSaldo   := 0  as numeric
    Local nSaldo      := 0  as numeric
    Local nValor      := 0  as numeric
    Local nDecs 	  := MsDecimais(MV_PAR04) as numeric

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    //Tipo de Compra
    cTipoCompra := STR0038 //"Pedido de Compra"

    While !(self:cAlias)->(Eof())       

        //Categoria do titulo
        If StoD((self:cAlias)->C7_DATPRF) < dDataBase
            cCategoria := STR0039 //"PEDIDOS DE COMPRA ATRASADOS"
            nDias := dDataBase - StoD((self:cAlias)->C7_DATPRF)
        Else
            cCategoria := STR0040 //"PEDIDOS DE COMPRA ADIANTADOS"
            nDias := StoD((self:cAlias)->C7_DATPRF) - dDataBase
        EndIf

        //Classificação de Aging
        If nDias <= 15
            cAgingPagar := STR0022 //"Até 15 Dias"
        ElseIf nDias > 15 .And. nDias <= 30
            cAgingPagar := STR0023 //"De 16 a 30 Dias"
        ElseIf nDias > 30 .And. nDias <= 60
            cAgingPagar := STR0024 //"De 31 a 60 Dias"
        ElseIf nDias > 60 .And. nDias <= 90
            cAgingPagar := STR0025 //"De 61 a 90 Dias"
        Else
            cAgingPagar := STR0026 //"Acima de 90 Dias"
        Endif

        //Tipo da Compra
        If (self:cAlias)->C7_QUJE > 0
            cTipoTitulo := STR0041 //"Parcialmente atendido"
        Else
            cTipoTitulo := STR0042 //"Não atendido"
        EndIf

        nVlrSaldo := ((self:cAlias)->C7_QUANT - (self:cAlias)->C7_QUJE) * (self:cAlias)->C7_PRECO

        
        nValor := xMoeda((self:cAlias)->C7_TOTAL,(self:cAlias)->C7_MOEDA,MV_PAR04,(self:cAlias)->C7_EMISSAO,nDecs+1,(self:cAlias)->C7_TXMOEDA)
        nSaldo := xMoeda(nVlrSaldo         ,(self:cAlias)->C7_MOEDA,MV_PAR04,(self:cAlias)->C7_EMISSAO,nDecs+1,(self:cAlias)->C7_TXMOEDA)
        

        self:jData := {"E2_FILIAL":     AllTrim((self:cAlias)->E2_FILIAL),;
            "E2_FORNECE":   AllTrim((self:cAlias)->C7_FORNECE),;
            "E2_LOJA":      AllTrim((self:cAlias)->C7_LOJA),;
            "E2_NOMFOR":    AllTrim((self:cAlias)->A2_NOME),;
            "E2_PREFIXO":   "",;
            "TITULO_PEDIDO":       AllTrim((self:cAlias)->C7_NUM) + "-" + AllTrim((self:cAlias)->C7_ITEM),;
            "E2_PARCELA":   "",;
            "E2_TIPO":      "",;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAlias)->C7_EMISSAO)) ,;
            "VENCIMENTO":   (self:varToTimeStamp((self:cAlias)->C7_DATPRF)),;
            "E2_BAIXA":     Nil,;
            "VALOR_TITPED":     nValor,;
            "SALDO":     nSaldo,;
            "FK2_VALOR":    0,;
            "E2_NATUREZ":   "",;
            "ED_DESCRIC":   "",;
            "TIPOCOMPRA":   cTipoCompra,;
            "TIPOTITULO":   cTipoTitulo,;
            "CATEG_TITULO": cCategoria,;
            "AGING_TITULO": cAgingPagar,;
            "VALOR_CORRECAO":0,;
            "VALOR_ACESSORIO":0;
            }

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )
    EndDo

Return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    Inicializa as propriedades de parâmetros do objeto de negócio
    para não correr riscos de error.log.
    Será implementado no objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    self:nListPosting := 1
    self:dPostingDateFrom := cToD("  /  /    ")
    self:dPostingDateTo := cToD("  /  /    ")
    self:nCurrency := 1
    self:lConvertCurrencyRate := .F.
    self:cPrefixFrom := ""
    self:cPrefixTo := ""
    self:dDueDateFrom := cToD("  /  /    ")
    self:dDueDateTo := cToD("  /  /    ")
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
@author Fábio Henrique Andrade
@since 16/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class GeneralAccountsPayableStatusReportSmartViewBusinessObject   
    if self:oCurrentFilter <> Nil
        self:nListPosting := MV_PAR01
        self:dPostingDateFrom := MV_PAR02
        self:dPostingDateTo := MV_PAR03
        self:nCurrency := MV_PAR04
        self:lConvertCurrencyRate := MV_PAR05 == 1
        self:cPrefixFrom := MV_PAR06
        self:cPrefixTo := MV_PAR07
        self:dDueDateFrom := MV_PAR08
        self:dDueDateTo := MV_PAR09
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} changeCanFilter
@author guilhermed.santos
@since 10/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method changeCanFilter() class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    local aSchemaProperties := {} as array
    local cProperty         := "" as character
    local nX                := 1  as numeric

    aSchemaProperties := self:oSchema:getProperties()
    
    for nX := 1 to Len(aSchemaProperties)
        cProperty := aSchemaProperties[nX]:getName()
        if !self:jRelationSE2SC7:hasProperty(cProperty)
            self:setCanFilter(cProperty,.F.)
        endIf
    next nX
return

//-------------------------------------------------------------------
/*{Protheus.doc} filterWithRelationOfSC7
@author guilhermed.santos
@since  10/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method filterWithRelationOfSC7() as character class GeneralAccountsPayableStatusReportSmartViewBusinessObject
    local aRelationProperties   := {} as array
    local cFilter               := "" as character
    local nX                    := 1  as numeric

    cFilter := self:getFilterToSQL()

    if empty(cFilter)
        return cFilter
    endIf

    aRelationProperties := self:jRelationSE2SC7:GetNames()
    
    for nX := 1 to Len(aRelationProperties)
        cFilter := replace(cFilter,aRelationProperties[nX],self:jRelationSE2SC7:GetJSonObject(aRelationProperties[nX]))
    next nX

return cFilter
