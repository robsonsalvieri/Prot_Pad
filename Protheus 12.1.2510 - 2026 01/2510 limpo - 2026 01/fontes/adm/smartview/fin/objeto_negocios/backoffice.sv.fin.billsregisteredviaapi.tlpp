#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.issueborderaux.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SEA,SA6,FK7", customTables="SE1,SEA", name="Títulos Registrados via API", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} BillsRegisteredViaAPISmartViewBusinessObject
Classe para criação do Objeto de Negócio para a Listagem dos títulos
registrados via API. (Borderôs de pagamento ou recebimento)

@author Fábio Andrade
@since 15/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
class BillsRegisteredViaAPISmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.IssueBorderauxSmartViewBusinessObject
    public method new() as object

    protected method initializeBusinessObject()

    protected method handleSchema() as object
    protected method handleData() as json
    protected method loadParameters()

    protected method setWherePagar()
    protected method setWhereReceber()
    protected method setQryParamsPagar()
    protected method setQryParamsReceber()
    protected method getOperation() as character
    protected method getStatusTransf() as character
   
    protected method getWherePay() as character
    protected method getWhereRec() as character

    protected method getSelectComplement() as character
    protected method getSelectComplementPay() as character
    protected method getSelectComplementRec() as character
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Fábio Andrade
@since 15/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class BillsRegisteredViaAPISmartViewBusinessObject
    _Super:new()
return self


//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 09/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsRegisteredViaAPISmartViewBusinessObject
    self:setDisplayName(STR0006) //"Títulos Registrados via API"
    self:setDescription(STR0007) //"Listagem dos Títulos Registrados via API"
    self:setPergunte("FINT004")
return

/*{Protheus.doc} getSchema
    Complementa a construção do objeto Schema.
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return object: self:oSchema
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleSchema() as object class BillsRegisteredViaAPISmartViewBusinessObject
    _Super:handleSchema()

    self:finAddProperty("SITUACAO", GetSx3Cache("EA_TRANSF", "X3_TITULO"), "string")
    self:finAddProperty("OPERACAO", STR0012, "string") //"Operação"
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Complementa os dados que serão retornados no getData().
    A classe IntegratedProvider não permite polimorfismo nos métodos getData() e getSchema().
    @return json: Registro após manipulação.
    @author guilherme.sordi@totvs.com.br
    @since 07/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData() as json class BillsRegisteredViaAPISmartViewBusinessObject
    _Super:handleData()

    self:jData["SITUACAO"] := self:getStatusTransf()
    self:jData["OPERACAO"] := self:getOperation()
return


//-------------------------------------------------------------------
/*{Protheus.doc} setWherePagar
    Monta o where para os filtros do pergunte da classe filha.
    Preenche a propriedade ::cWherePagar que será concatenada na query de
    borderôs a pagar

    @author Fábio Henrique Andrade
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWherePay() as character class BillsRegisteredViaAPISmartViewBusinessObject
    Local cWhere := "" as character
    
    cWhere += " AND	SE2.E2_EMISSAO BETWEEN ? AND ? "
    cWhere += " AND SE2.E2_VENCREA BETWEEN ? AND ? "
    cWhere += " AND SEA.EA_PORTADO BETWEEN ? AND ? "
    cWhere += " AND SE2.E2_FORNECE BETWEEN ? AND ? "

    If ::lListarOutrasMoedas    
        cWhere += " AND SE2.E2_MOEDA = ? "
    EndIf

return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} setQryParamsPagar
Método que será sobrescrito pelas classes filhas para completarem os parametros 
da query de borderos a pagar

@author Fábio Henrique Andrade Silva
@since 20/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method setQryParamsPagar() class BillsRegisteredViaAPISmartViewBusinessObject

    self:oStatement:SetDate(::nOrdemQryParamsPagar++, MV_PAR02)
    self:oStatement:SetDate(::nOrdemQryParamsPagar++, MV_PAR03)
    self:oStatement:SetDate(::nOrdemQryParamsPagar++, MV_PAR04)
    self:oStatement:SetDate(::nOrdemQryParamsPagar++, MV_PAR05)
    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR06)
    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR07)
    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR08)
    self:oStatement:SetString(::nOrdemQryParamsPagar++, MV_PAR09)

    If ::lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nOrdemQryParamsPagar++, self:nMoeda)
    EndIf      

return 

//-------------------------------------------------------------------
/*{Protheus.doc} setWhereReceber
    Monta o where para os filtros do pergunte da classe filha.
    Preenche a propriedade ::cWhereReceber que será concatenada na query de
    borderôs a receber

    @author Fábio Henrique Andrade
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getWhereRec() class BillsRegisteredViaAPISmartViewBusinessObject
    Local cWhere := "" as character
    
    cWhere += " AND	SE1.E1_EMISSAO BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_VENCREA BETWEEN ? AND ? "
    cWhere += " AND SEA.EA_PORTADO BETWEEN ? AND ? "
    cWhere += " AND SE1.E1_CLIENTE BETWEEN ? AND ? "

    If ::lListarOutrasMoedas    
        cWhere += " AND SE1.E1_MOEDA = ? "
    EndIf

return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} setQryParamsReceber
Método que será sobrescrito pelas classes filhas para completarem os parametros 
da query de borderos a receber

@author Fábio Henrique Andrade Silva
@since 20/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method setQryParamsReceber() class BillsRegisteredViaAPISmartViewBusinessObject

    self:oStatement:SetDate(::nOrdemQryParamsReceber++, MV_PAR02)
    self:oStatement:SetDate(::nOrdemQryParamsReceber++, MV_PAR03)
    self:oStatement:SetDate(::nOrdemQryParamsReceber++, MV_PAR04)
    self:oStatement:SetDate(::nOrdemQryParamsReceber++, MV_PAR05)
    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR06)
    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR07)
    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR08)
    self:oStatement:SetString(::nOrdemQryParamsReceber++, MV_PAR09)

    If ::lListarOutrasMoedas
        self:oStatement:SetString(::nOrdemQryParamsReceber++, STR(::nMoeda))
    EndIf    

return 


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    Complementa a montagem dos parâmetros da classe.
    @author Fábio Henrique Andrade Silva
    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class BillsRegisteredViaAPISmartViewBusinessObject
    local aCarteiras := {"P","R"}

    self:nMoeda := 1
    self:lListarOutrasMoedas := .T.
    self:cCarteiraBordero := aCarteiras[MV_PAR01]
    if type("MV_PAR10") == "N"
        self:lShowInstructions := MV_PAR10 == 1
    endIf
    
    If self:cCarteiraBordero == "P"
        self:cAliasSE := "SE2"
    else
        self:cAliasSE := "SE1"
    endIf
    
    self:lShowSettledBill := .T.
    self:lOnlyCnab := .F.
    self:lOnlyOnline := .T.
    
    FwFreeArray(aCarteiras)
return

//-------------------------------------------------------------------
/*{Protheus.doc} getOperation
    Retorna a operação daquele título no borderô.
    @author Fábio Henrique Andrade Silva
    @return character: Determina o tipo da operação com base no título

    @since 20/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getOperation() as character class BillsRegisteredViaAPISmartViewBusinessObject
    Local cOperacao := "" as character

    if self:lShowInstructions .and. (self:cAlias)->FI2_OPEAPI != "NULL"
        if allTrim((self:cAlias)->FI2_OPEAPI) == "A"
            cOperacao := STR0029 //"Alteração"
        endIf
    else
        If (::cCarteiraBordero) == "P"
            If (::cAlias)->E1_SALDO > 0
                cOperacao := STR0013 //"Registro a pagar"
            Else
                cOperacao := STR0014 //"Baixa a pagar"
            EndIf
        Else
            If (::cAlias)->E1_SALDO > 0
                cOperacao := STR0015 //"Registro a receber"
            Else
                cOperacao := STR0016 //"Baixa a receber"
            EndIf
        EndIf
    endIf
return cOperacao

//-------------------------------------------------------------------
/*{Protheus.doc} getStatusTransf
    Retorna a descrição do status de transferência. O campo não possui CBOX.
    Seria interessante no futuro centralizar essa descrição em uma função do NGF para ficar
    sempre igual e não repetir código.
    @author guilherme.sordi@totvs.com.br
    @since 29/20/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getStatusTransf() as character class BillsRegisteredViaAPISmartViewBusinessObject
    local cTransf := "" as character
    local cDescription := "" as character

    if self:lShowInstructions .and. (self:cAlias)->FI2_TRANSF != 'NULL'
        cTransf := (self:cAlias)->FI2_TRANSF
    else
        cTransf := (self:cAlias)->EA_TRANSF
    endIf
    
    if cTransf == "S"
        cDescription := STR0030 //"Sucesso"
    elseIf cTransf == "F"
        cDescription := STR0031 //"Falha"
    else
        cDescription := STR0032 //"Pendente"
    endIf
return cDescription

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplement
    @author fabioh.andrade@totvs.com.br
    @since 18/09/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getSelectComplement(lQueryUnion as logical) as character class BillsRegisteredViaAPISmartViewBusinessObject
    local cSelectComplement := "" as character

    if self:lShowInstructions
        if lQueryUnion
            cSelectComplement := ", COALESCE(FI2_TRANSF, 'NULL') FI2_TRANSF, COALESCE(FI2_OPEAPI, 'NULL') FI2_OPEAPI "
        else
            cSelectComplement := ", 'NULL' FI2_TRANSF, 'NULL' FI2_OPEAPI "
        endIf
    endIf

return cSelectComplement

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementPay
    @author fabioh.andrade@totvs.com.br
    @since 18/09/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getSelectComplementPay(lQueryUnion as logical) as character class BillsRegisteredViaAPISmartViewBusinessObject
return self:getSelectComplement(lQueryUnion)

//-------------------------------------------------------------------
/*/{Protheus.doc} getSelectComplementRec
    @author fabioh.andrade@totvs.com.br
    @since 18/09/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getSelectComplementRec(lQueryUnion as logical) as character class BillsRegisteredViaAPISmartViewBusinessObject
return self:getSelectComplement(lQueryUnion)
