#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.listofclasses.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SED", name="Lista de Naturezas Financeiras", country="ALL",;
    customTables="SED")


//-------------------------------------------------------------------
/*/{Protheus.doc} listOfClassesTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem de clientes

@author Fábio Henrique Andrade
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class ListOfClassesTReportsBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oFinClass as object
    protected data cCodeFrom as character
    protected data cCodeTo as character

    protected method mySetSchema()
    protected method loadStatement()
    protected method setLookups()
    protected method handleData()
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method initializeBusinessObject()
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Fábio Henrique Andrade
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class ListOfClassesTReportsBusinessObject
    _Super:new()

    self:initializeBusinessObject()
    self:setIsCBoxLookup(.T., .T.)
    self:setIsLookup(.T.) 
    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable("SED")
    self:setBranchFilter(.T., .T.)

    self:oFinClass := FinClassUtil():New()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject

@author guilhermed.santos@totvs.com.br
@since 14/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class ListOfClassesTReportsBusinessObject
    self:setDisplayName(STR0002) // "Naturezas Financeiras"
    self:setDescription(STR0003) // "Listagem das informacoes do Cadastro de Naturezas Financeiras"    
    self:setPergunte("FINT006")
return

//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author guilherme.sordi
    @since 26/10/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class ListOfClassesTReportsBusinessObject
    self:oSchema:aliasToSchema("SED", { "ED_FILIAL", "ED_CODIGO", "ED_DESCRIC","ED_USO", "ED_TIPO", "ED_COND" } )
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData

@author Fábio Henrique Andrade Silva
@since 26/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class ListOfClassesTReportsBusinessObject
    Local cQuery        := "" as character
    local nParamOrder   := 1  as numeric

    cQuery := " SELECT ? "
    cQuery += " FROM " + RetSQLName("SED") + " SED "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += "  AND SED.ED_CODIGO BETWEEN ? AND ? "
    cQuery += "  AND SED.D_E_L_E_T_ = ? "
    cQuery += " ? "
    cQuery += " ORDER BY ? "

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSQL())
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cCodeFrom)
    self:oStatement:SetString(nParamOrder++, self:cCodeTo)
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetUnsafe(nParamOrder++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(nParamOrder++, SqlOrder(SED->(IndexKey( 1 ))))
       
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
@author guilherme.sordi
@since 26/10/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class ListOfClassesTReportsBusinessObject
    self:jData["ED_CODIGO"] := self:oFinClass:MascNat((self:cAlias)->ED_CODIGO)
return

//-------------------------------------------------------------------
/*{Protheus.doc} setLookups
@author guilherme.sordi
@since 26/10/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method setLookups() class ListOfClassesTReportsBusinessObject
    local cQuery := "" as character
    local aFields := {} as array
    local lIsRequired := .F. as logical
    
    cQuery := "SELECT ED_CODIGO, ED_DESCRIC FROM "+ RetSQLName("SED") +" WHERE ED_FILIAL = '" + FWxFilial('SED') + "' AND D_E_L_E_T_ = ' '"
    aFields := {"ED_CODIGO", "ED_DESCRIC"}

    self:oSchema:setLookUpQuery("ED_CODIGO", aFields, cQuery, lIsRequired)
return


//-------------------------------------------------------------------
    /*{Protheus.doc} loadDefaultParameters
    @author guilhermed.santos@totvs.com.br
    @since 14/11/2024
    @version 12.1.2410
    */
//-------------------------------------------------------------------
method loadDefaultParameters() class ListOfClassesTReportsBusinessObject
    self:cCodeFrom := ''
    self:cCodeTo := 'ZZZ'
return

//-------------------------------------------------------------------
    /*{Protheus.doc} loadParameters
    @author guilhermed.santos@totvs.com.br
    @since 14/11/2024
    @version 12.1.2410
    */
//-------------------------------------------------------------------
method loadParameters() class ListOfClassesTReportsBusinessObject
    If self:oCurrentFilter != Nil 
        self:cCodeFrom := MV_PAR01
        self:cCodeTo := MV_PAR02
    endIf
return
