#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billspayable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2,SED", name="Posição de títulos a pagar",; 
customTables="SE2,SA2,SED", country="ALL") 
//-------------------------------------------------------------------
/*/{Protheus.doc} BillsPayableSmartViewBusinessObject
Classe para criação do Objeto de Negócio
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
class BillsPayableSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object
    public method mySetSchema()    
    public method getWhere() as character

    protected data nMoeda as numeric
    protected data lListarOutrasMoedas as logical
    protected data lVisaoRetroativa as logical
    protected data dVisaoRetroativa as date
    protected data lUsaNaturezaSintetica as logical
    protected data lMostraSaldoZero as logical

    protected data nMoedaTitulo as numeric
    protected data nOrdem as numeric
    protected data nTaxaMoedaTitulo as numeric
    protected data cPrefixFrom as character
    protected data cPrefixTo as character
    protected data cBillNumberFrom as character 
    protected data cBillNumberTo as character
    protected data cSupplierFrom as character 
    protected data cSupplierTo as character 
    protected data dIssueDateFrom as date 
    protected data dIssueDateTo as date 
    protected data dDueDateFrom as date 
    protected data dDueDateTo as date 
    protected data cClassFrom as character 
    protected data cClassTo as character

    protected data aAreas as array
    protected data aAreaSE2 as array
    protected data dBackupDatabase as date

    protected data oStatementUltimaBaixa as object
    protected data oFinClassUtil as object

    protected method initializeBusinessObject()
    protected method loadStatement()
    protected method myProcessData()
    protected method loadParameters()
    protected method converteMoeda() as numeric
    protected method mascNat() as character
    protected method calculaJuros() as numeric 
    protected method buscaUltimaBaixa() as date 
    protected method loadDefaultParameters()
    protected method handleStatement()
    protected method completeStatement()
    protected method preWhileMyAliasToData()
    protected method postWhileMyAliasToData()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class BillsPayableSmartViewBusinessObject    
    _Super:new()
    self:initializeBusinessObject()
    self:setAppendInMyAliastoData(.F.)
    self:setAllTrim(.T.)
    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)

    self:nMoedaTitulo := 1
    self:nTaxaMoedaTitulo := 1
    self:nOrdem := 1
    self:lMostraSaldoZero := .F.

    self:oFinClassUtil := FinClassUtil():New()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author matheus.monteiro@totvs.com.br
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsPayableSmartViewBusinessObject    
    self:setDisplayName(STR0012) //"Posição de títulos a pagar"
    self:setDescription(STR0012) //"Posição de títulos a pagar"
    self:setPergunte("FINT003") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos e conjunto de parametros
@return object: self:oSchema
@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class BillsPayableSmartViewBusinessObject
    local aFieldsSE2 := {} as array

    aFieldsSE2 := { "E2_FILIAL", "E2_PREFIXO", "E2_NUM", "E2_PARCELA", "E2_TIPO",;
                    "E2_FORNECE", "E2_LOJA","E2_EMISSAO","E2_VENCTO", "E2_VENCREA", "E2_NATUREZ",;
                    "E2_VALOR", "E2_SALDO", "E2_BAIXA", "E2_ACRESC", "E2_DECRESC", "E2_HIST",;
                    "E2_PORTADO", "E2_NUMBCO"}    

    self:oSchema:aliasToSchema("SE2", aFieldsSE2)
    self:oSchema:aliasToSchema("SED", "ED_DESCRIC")

    self:finAddProperty("ABATIMENTO"    , STR0004, "number") //"Abatimentos"
    self:finAddProperty("VA"            , STR0005, "number") //"Valores acessórios"
    self:finAddProperty("JUROS"         , FWX3Titulo("E2_JUROS"), "number")
    self:finAddProperty("A2_NOME"       , STR0007, "string") //"Razão Social"
    self:finAddProperty("A2_CGC"        , STR0008, "string") //"CNPJ/CPF"
    self:finAddProperty("DADOSFORN"     , STR0009, "string", "A2_NOME") //"Dados do Fornecedor"
    self:finAddProperty("DADOSTIT"      , STR0010, "string", "E2_NUM") //"Dados do Título"
    self:finAddProperty("DADOSNAT"      , STR0011, "string", "ED_DESCRIC") //"Dados do Natureza"
    self:finAddProperty("ATRASO"        , STR0013, "number") //"Atraso"
    self:finAddProperty("SALDO_LIQUIDO" , STR0014, "number") //"Valor líquido"

    FwFreeArray(aFieldsSE2)
    
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData
@author Pâmela Bernardo
@since 23/05/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class BillsPayableSmartViewBusinessObject
    Local cQuery  := "" as character

    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E2_EMISSAO" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E2_VENCTO" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E2_VENCREA" ) )
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "E2_BAIXA" ) )

    cQuery += "SELECT DISTINCT ?, SE2.R_E_C_N_O_ AS RECNO, "
    cQuery += "SA2.A2_COD AS FORN, SA2.A2_LOJA AS LOJA, SA2.A2_NOME AS NOME, SA2.A2_NREDUZ AS NREDZ, "
    cQuery += "SE2.E2_TXMOEDA AS TXMOEDA, SE2.E2_MOEDA AS MOEDA, SE2.E2_SDACRES AS ACRES, SE2.E2_SDDECRE AS DECRESC, "
    cQuery += "SE2.E2_FILORIG AS FILORIG, FK7_IDDOC AS IDDOC"

    cQuery += "FROM " + RetSQLName("SE2") + " SE2 "

    cQuery += "JOIN " + RetSQLName("SED") + " SED "
    cQuery += "ON " + FWJoinFilial("SE2", "SED") + " 
    cQuery += "AND SE2.E2_NATUREZ = SED.ED_CODIGO "
    cQuery += "AND SED.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("SA2") + " SA2 "
    cQuery += "ON " + FWJoinFilial("SE2", "SA2") + " 
    cQuery += "AND SE2.E2_FORNECE = SA2.A2_COD "
    cQuery += "AND SE2.E2_LOJA = SA2.A2_LOJA "
    cQuery += "AND SA2.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("FK7") + " FK7 "
    cQuery += "ON FK7.FK7_ALIAS = ? "
    cQuery += "AND SE2.E2_FILIAL = FK7.FK7_FILTIT "
    cQuery += "AND SE2.E2_PREFIXO = FK7.FK7_PREFIX "
    cQuery += "AND SE2.E2_NUM = FK7.FK7_NUM "
    cQuery += "AND SE2.E2_PARCELA = FK7.FK7_PARCEL "
    cQuery += "AND SE2.E2_TIPO = FK7.FK7_TIPO "
    cQuery += "AND SE2.E2_FORNECE = FK7.FK7_CLIFOR "
    cQuery += "AND SE2.E2_LOJA = FK7.FK7_LOJA "
    cQuery += "AND FK7.D_E_L_E_T_ = ? "

    cQuery += "WHERE "
    cQuery += self:getSQLBranchFilter()
    
    cQuery += self:getWhere()

    if !self:lListarOutrasMoedas
        cQuery += " AND SE2.E2_MOEDA = ? " 
    endIf

    if self:lVisaoRetroativa
        cQuery += " AND SE2.E2_EMISSAO <= ? "
    elseIf !self:lMostraSaldoZero
		cQuery += " AND SE2.E2_SALDO > ? "
    endIf

    cQuery += " AND SE2.E2_TIPO NOT IN ? "
    cQuery += " AND SE2.E2_STATUS <> ? "
    cQuery += " AND SE2.D_E_L_E_T_ = ?  "
    

    cQuery += " ? "

    cQuery += " ORDER BY ? " 

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:handleStatement()

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement() class BillsPayableSmartViewBusinessObject

    self:oStatement:SetUnsafe(self:nOrdem++, self:getAllFieldsToSQL())
    self:oStatement:SetString(self:nOrdem++, " ")
    self:oStatement:SetString(self:nOrdem++, " ")
    self:oStatement:SetString(self:nOrdem++, "SE2")
    self:oStatement:SetString(self:nOrdem++, " ")
    self:setStatementBranchFilter(@self:nOrdem)
    self:completeStatement()
    if !self:lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nOrdem++, self:nMoeda)
    endIf

    if self:lVisaoRetroativa
        self:oStatement:SetDate(self:nOrdem++, self:dVisaoRetroativa)
    elseIf !self:lMostraSaldoZero
        self:oStatement:setNumeric(self:nOrdem++, 0)
    endIf
    self:oStatement:SetUnsafe(self:nOrdem++, FormatIn(MVABATIM,"|"))
    self:oStatement:SetString(self:nOrdem++, "D")
    self:oStatement:SetString(self:nOrdem++, " ")
    
    self:oStatement:SetUnsafe(self:nOrdem++, self:getFilterToSQL())
    self:oStatement:SetUnsafe(self:nOrdem++, SqlOrder(SE2->(IndexKey( 1 ))))
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} completeStatement
    @author fabioh.andrade@totvs.com.br
    @since 24/01/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method completeStatement() class BillsPayableSmartViewBusinessObject

    self:oStatement:SetString(self:nOrdem++, self:cPrefixFrom)
    self:oStatement:SetString(self:nOrdem++, self:cPrefixTo)
    self:oStatement:SetString(self:nOrdem++, self:cBillNumberFrom)
    self:oStatement:SetString(self:nOrdem++, self:cBillNumberTo)
    self:oStatement:SetString(self:nOrdem++, self:cSupplierFrom)
    self:oStatement:SetString(self:nOrdem++, self:cSupplierTo)
    self:oStatement:SetDate(self:nOrdem++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nOrdem++, self:dIssueDateTo)
    self:oStatement:SetDate(self:nOrdem++, self:dDueDateFrom)
    self:oStatement:SetDate(self:nOrdem++, self:dDueDateTo)
    self:oStatement:SetString(self:nOrdem++, self:cClassFrom)
    self:oStatement:SetString(self:nOrdem++, self:cClassTo)

return 

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData

@author fabioh.andrade@totvs.com.br
@since 24/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class BillsPayableSmartViewBusinessObject
    
    self:dBackupDatabase := dDatabase
    self:aAreas := getArea()
    self:aAreaSE2 := SE2->(getArea())

    if self:lVisaoRetroativa
        dDatabase := self:dVisaoRetroativa
    endIf

return 

//-------------------------------------------------------------------
/*{Protheus.doc} myProcessData

@author Pâmela Bernardo
@since 23/05/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method myProcessData() class BillsPayableSmartViewBusinessObject
    Local cCGC                            := ""        as character
    Local cDadosFornecedor                := ""        as character
    Local cDadosTitulo                    := ""        as character
    Local cDadosNatureza                  := ""        as character
    Local nValor                          := 0         as numeric
    Local nAbatimento                     := 0         as numeric
    Local nSaldo                          := 0         as numeric
    Local nValoresAcessorios              := 0         as numeric
    Local nJurosCalculado                 := 0         as numeric   
    Local nSaldoLiquido                   := 0         as numeric
    Local nDiasAtraso                     := 0         as numeric
    Local oStatementLegadoBxCanc                       as object
    Local lCompensacaoMultifilialSaldoTit := .T.       as logical
    Local lUsaSaldoTitFK                  := .T.       as logical
    Local lNegativo                       := .F.       as logical
    Local cCarteira                       := "P"       as character

    SE2->(DbGoTo((self:cAlias)->RECNO))

    lNegativo := ((self:cAlias)->E2_TIPO $ MVPAGANT + "|" + MV_CPNEG)
    
    cCGC := SmartViewFinUtils():transformCGC((self:cAlias)->A2_CGC)

    cDadosFornecedor := allTrim((self:cAlias)->FORN) + "/" + allTrim((self:cAlias)->LOJA) + " - " + allTrim((self:cAlias)->NOME) + "("+ allTrim((self:cAlias)->NREDZ) +")"
    
    cDadosNatureza := self:oFinClassUtil:mascNat(allTrim((self:cAlias)->E2_NATUREZ))  +  " - "  +  allTrim((self:cAlias)->ED_DESCRIC)
    
    cDadosTitulo := (self:cAlias)->E2_PREFIXO + "-" + (self:cAlias)->E2_NUM + "-" + (self:cAlias)->E2_PARCELA + "-" + (self:cAlias)->E2_TIPO

    self:nTaxaMoedaTitulo := (self:cAlias)->TXMOEDA
    self:nMoedaTitulo := (self:cAlias)->MOEDA

    if self:lVisaoRetroativa
        nSaldo := SaldoTit((self:cAlias)->E2_PREFIXO, (self:cAlias)->E2_NUM, (self:cAlias)->E2_PARCELA, (self:cAlias)->E2_TIPO, (self:cAlias)->E2_NATUREZ, cCarteira, (self:cAlias)->E2_FORNECE,;
                (self:cAlias)->MOEDA, dDatabase, dDatabase, (self:cAlias)->E2_LOJA, FwXFilial("SE5", (self:cAlias)->FILORIG), /*nTxMoeda*/, /*nTipoData*/, /*lFinR*/, oStatementLegadoBxCanc, /*lIsTxContr*/, lCompensacaoMultifilialSaldoTit, lUsaSaldoTitFK)            
    else
        nSaldo := (self:cAlias)->E2_SALDO + (self:cAlias)->ACRES - (self:cAlias)->DECRESC
    endIf
    
    if !lNegativo .and. ( nSaldo > 0 )
        nAbatimento := SomaAbat((self:cAlias)->E2_PREFIXO, (self:cAlias)->E2_NUM, (self:cAlias)->E2_PARCELA, cCarteira, self:nMoedaTitulo, NIL, (self:cAlias)->E2_FORNECE, (self:cAlias)->E2_LOJA)
    endIf
    
    //Retorno do VA sem conversão
    nValoresAcessorios := FValAcess((self:cAlias)->E2_PREFIXO,(self:cAlias)->E2_NUM,(self:cAlias)->E2_PARCELA,;
                    (self:cAlias)->E2_TIPO,(self:cAlias)->E2_FORNECE,(self:cAlias)->E2_LOJA,;
                    (self:cAlias)->E2_NATUREZ, .F., "", cCarteira, /*dDatabase*/,;
                    /*aValAces*/, self:nMoedaTitulo, self:nMoedaTitulo, self:nTaxaMoedaTitulo, /*cIdFKD*/, self:lVisaoRetroativa )
    
    nSaldoLiquido := nSaldo - nAbatimento + nValoresAcessorios

    if self:lVisaoRetroativa .and. !self:lMostraSaldoZero .and. nSaldoLiquido == 0
        return
    endIf

    nDiasAtraso := 0
    if !lNegativo .and. dDatabase > (self:cAlias)->E2_VENCREA .and. nSaldo > 0
        nDiasAtraso := dateDiffDay((self:cAlias)->E2_VENCTO, dDatabase)
    endIf

    nJurosCalculado := self:calculaJuros(nSaldo)

    nSaldoLiquido += nJurosCalculado

    nValor := (self:cAlias)->E2_VALOR
    If lNegativo
        nValor := nValor*-1
        nSaldo := nSaldo*-1
        nSaldoLiquido *= -1
    EndIf

    self:jData := {;
        "A2_CGC":      allTrim(cCGC),;
        "A2_NOME":     allTrim((self:cAlias)->A2_NOME),;
        "E2_VALOR":    self:converteMoeda(nValor),;
        "E2_SALDO":    self:converteMoeda(nSaldo),;
        "E2_ACRESC":   self:converteMoeda((self:cAlias)->E2_ACRESC),;
        "E2_DECRESC":  self:converteMoeda((self:cAlias)->E2_DECRESC),;
        "JUROS":       self:converteMoeda(nJurosCalculado),;
        "ABATIMENTO":  self:converteMoeda(nAbatimento),;
        "VA":          self:converteMoeda(nValoresAcessorios),;
        "ATRASO":      nDiasAtraso,;
        "SALDO_LIQUIDO": self:converteMoeda(nSaldoLiquido),;
        "DADOSFORN":   cDadosFornecedor,;
        "DADOSNAT":    cDadosNatureza,;
        "DADOSTIT":    cDadosTitulo }

    self:processAndAppend()

return

//-------------------------------------------------------------------
/*{Protheus.doc} postWhileMyAliasToData

@author fabioh.andrade@totvs.com.br
@since 24/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method postWhileMyAliasToData() class BillsPayableSmartViewBusinessObject
    
    if self:lVisaoRetroativa
        dDatabase := self:dBackupDatabase
    endIf

    restArea(self:aAreaSE2)
    restArea(self:aAreas)
    fwFreeArray(self:aAreaSE2)
    fWFreeArray(self:aAreas)
    
return 


//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    @author matheus.monteiro@totvs.com.br
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class  BillsPayableSmartViewBusinessObject
    self:nMoeda := 1
    self:lListarOutrasMoedas := .F.
    self:lVisaoRetroativa := .F.
    self:dVisaoRetroativa := dDatabase
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Se não receber o objeto de filtro limpa as propriedas de filtro da classe.
Se receber o objeto de filtro, carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

@author guilherme.sordi@totvs.com.br
@since 02/06/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BillsPayableSmartViewBusinessObject   

    if self:oCurrentFilter != NIL
        self:cSupplierFrom := MV_PAR01 
        self:cSupplierTo := MV_PAR02
        self:cPrefixFrom := MV_PAR03 
        self:cPrefixTo := MV_PAR04
        self:cBillNumberFrom := MV_PAR05
        self:cBillNumberTo := MV_PAR06
        self:dIssueDateFrom := MV_PAR07
        self:dIssueDateTo := MV_PAR08
        self:dDueDateFrom := MV_PAR09
        self:dDueDateTo := MV_PAR10
        self:cClassFrom := MV_PAR11
        self:cClassTo := MV_PAR12

        self:nMoeda := self:getCurrencyValue(MV_PAR13)

        self:lListarOutrasMoedas := MV_PAR14 <> 1    
        if self:lVisaoRetroativa := ( MV_PAR15 == 1 )
            self:dVisaoRetroativa := MV_PAR16
        endIf
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class BillsPayableSmartViewBusinessObject
    Local cWhere := " " as character 

    cWhere += "AND SE2.E2_PREFIXO BETWEEN ? AND ?  " 
    cWhere += "AND SE2.E2_NUM   BETWEEN ? AND ?   " 
    cWhere += "AND SE2.E2_FORNECE BETWEEN ? AND ?  " 
    cWhere += "AND SE2.E2_EMISSAO BETWEEN ? AND ?  " 
    cWhere += "AND SE2.E2_VENCREA BETWEEN ? AND ?  " 
    cWhere += "AND SE2.E2_NATUREZ BETWEEN ? AND ?  "

return cWhere


//-------------------------------------------------------------------
/*{Protheus.doc} converteMoeda
Concentra toda a regra de negócio para a conversão de moedas.
Todos os cálculos do objeto de negócio são feitos na moeda do título 
e a conversão para a moeda parametrizada pelo usuário é a última etapa
da extração de dados.

@param nValor, numeric, Valor a ser convertido para a moeda de apresentação
@author guilherme.sordi@totvs.com.br
@since 23/05/2023

@version 12.1.2310  
*/
//-------------------------------------------------------------------
method converteMoeda(nValor as numeric) as numeric class BillsPayableSmartViewBusinessObject
    local nValorConvertido := 0                      as numeric

    nValorConvertido := xMoeda(nValor, self:nMoedaTitulo, self:nMoeda, dDatabase, self:nDecimals, self:nTaxaMoedaTitulo)

return nValorConvertido


//-------------------------------------------------------------------
/*{Protheus.doc} calculaJuros
    Responsável por calcular o juros na data base do relatório.
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 15/06/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calculaJuros(nSaldo) as numeric class BillsPayableSmartViewBusinessObject
    local nJuros := 0 as numeric
    local dUltimaBaixa := cToD("        ") as date

    if (self:cAlias)->E2_VENCREA >= dDatabase
        return 0
    endIf

    if self:lVisaoRetroativa
        dUltimaBaixa := self:buscaUltimaBaixa()
    else
        dUltimaBaixa := (self:cAlias)->E2_BAIXA
    endIf

    nJuros := fa080Juros(self:nMoedatitulo, nSaldo, "SE2", dUltimaBaixa, self:nTaxaMoedaTitulo, self:nDecimals)

return nJuros


//-------------------------------------------------------------------
/*{Protheus.doc} buscaUltimaBaixa
    Responsável por buscar a última data de baixa válida para o título posicionado,
    levando em conta a data base do sistema.
    É o equivalente ao E1_BAIXA/E2_BAIXA para a visão retroativa.
    @return character
    @author guilherme.sordi@totvs.com.br
    @since 15/06/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method buscaUltimaBaixa() as date class BillsPayableSmartViewBusinessObject
    local dUltimaBaixa := dDatabase as date
    local cQuery := "" as character
    local nParamOrder := 1 as numeric

    if self:oStatementUltimaBaixa == NIL
        cQuery := "SELECT MAX(FK2_DATA) ULTIMA_BAIXA "
        cQuery += "FROM " + RetSQLName("FK2") + " FK2 "
        cQuery += "WHERE FK2_IDDOC = ? "
        cQuery += "AND NOT EXISTS ( "
        cQuery += "    SELECT EST.FK2_IDDOC "
        cQuery += "    FROM " + RetSQLName("FK2") + " EST "
        cQuery += "    WHERE EST.FK2_IDDOC = FK2.FK2_IDDOC "
        cQuery += "    AND EST.FK2_SEQ = FK2.FK2_SEQ "
        cQuery += "    AND EST.FK2_TPDOC = ? "
        cQuery += "    AND EST.D_E_L_E_T_ = ? "
        cQuery += "    AND EST.FK2_DATA <= ? "
        cQuery += ") "
        cQuery += "AND FK2.FK2_TPDOC <> ? "
        cQuery += "AND FK2.FK2_DATA <= ? "
        cQuery += "AND FK2.D_E_L_E_T_ = ? "

        cQuery := changeQuery(cQuery)
        
        self:oStatementUltimaBaixa := FwExecStatement():New(cQuery)
    endIf
    self:oStatementUltimaBaixa:setString(nParamOrder++, (self:cAlias)->IDDOC)
    self:oStatementUltimaBaixa:setString(nParamOrder++, "ES")
    self:oStatementUltimaBaixa:setString(nParamOrder++, " ")
    self:oStatementUltimaBaixa:setDate(nParamOrder++, dDatabase)
    self:oStatementUltimaBaixa:setString(nParamOrder++, "ES")
    self:oStatementUltimaBaixa:setDate(nParamOrder++, dDatabase)
    self:oStatementUltimaBaixa:setString(nParamOrder++, " ")

    dUltimaBaixa := sTod(self:oStatementUltimaBaixa:ExecScalar("ULTIMA_BAIXA"))

return dUltimaBaixa
