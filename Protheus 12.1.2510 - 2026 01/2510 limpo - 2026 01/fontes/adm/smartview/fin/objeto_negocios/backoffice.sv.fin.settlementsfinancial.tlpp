#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.settlementsfinancial.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SE2,FK2", name="Liquidações Financeiras",;
customTables="SE1,SE2", country="ALL")

class SettlementsFinancialSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object 
    public method mySetSchema()
    public method getData() as object

    protected data nOrderParamsReceivable as numeric
    protected data nOrderParamsPayable as numeric
    protected data oQryPayable as object
    protected data oQryReceivable as object 
    protected data oQryReceivableChild as object
    protected data oQryPayableChild as object
    protected data cAlias as character    
    protected data cAliasChild as character    
    protected data cSettlementNumberFrom as character
    protected data cSettlementNumberTo as character
    protected data dIssueDateFrom as date
    protected data dIssueDateTo as date 
    protected data cCustomerFrom as character
    protected data cCustomerTo as character 
    protected data cSupplierFrom as character 
    protected data cSupplierTo as character
    protected data lReceivable as logical
    protected data lPayable as logical
    protected data cBillFinished as character 
    protected data cBillGenerated as character 
    protected data cPortfoliosListPayable as character 
    protected data cPortfoliosListReceivable as character 
    protected data cDocumentNumber    as character
    protected data cSupplierCode    as character
    protected data cInvoiceNumber    as character
    protected data cCustomerCode   as character
    protected data cSettlementNumber    as character
    protected data cProcessNumber    as character
    
    protected data nCurrency as numeric
    protected data lConvertCurrencyRate as logical
    protected data nCurrencyFrom as numeric
    protected data dConversion as date
    protected data nContratctedCurrencyRate as numeric
    
    protected method loadStatementReceivableBills()
    protected method loadStatementReceivableChild()
    protected method loadStatementPayableBills()
    protected method loadStatementPayableChild()

    protected method setDataReceivable()
    protected method appendReceivableChild()
    protected method setDataPayable()
    protected method appendPayableChild()
    
    protected method initializeBusinessObject()
    protected method loadParameters()
    protected method loadDefaultParameters() 
    protected method convertCurrency() as numeric
    protected method convertBillCurrencyPayable() as numeric
    protected method convertBillCurrencyReceivable() as numeric
    protected method preAppendData()
    protected method addBranchNameToChildData()

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Matheus Monteiro da Silva
@since 30/11/2023
@version 12.1.2210

*/
//-------------------------------------------------------------------
method new() as object class SettlementsFinancialSmartViewBusinessObject
    _Super:new()

    self:setAlltrim(.T.)
    self:initializeBusinessObject()
    self:loadDefaultParameters()
    self:setAllowSmartViewFilter(.F.)
    self:setMainTable('SE2')
    self:setBranchFilter(.T., .T.)

    self:nOrderParamsReceivable := 1
    self:nOrderParamsPayable := 1

    self:cBillFinished := STR0011//Baixado
    self:cBillGenerated := STR0012//Gerado
    self:cPortfoliosListPayable := STR0009 //Pagar
    self:cPortfoliosListReceivable  := STR0010 //Receber
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    Verifica se a carteira escolhida na consulta e a pagar,receber ou ambas e executa a query correspondente.
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SettlementsFinancialSmartViewBusinessObject

    if !self:validEnvironment()
        return self:oData
    endIf
    
    self:setPage(nPage)
    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadLGPDFields()
    self:loadCustomFieldsPayAndRec()
    self:loadFilterBranches( { "SE1", "FK2" } )
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()

    If self:lPayable
        self:loadStatementPayableBills()
        self:oQryPayable:setFields(self:aStruct)
        self:cAlias := self:oQryPayable:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout) 
        self:setDataPayable(oFilter)
    EndIf 

    If self:lReceivable
        self:loadStatementReceivableBills()
        self:oQryReceivable:setFields(self:aStruct)
        self:cAlias := self:oQryReceivable:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
        self:setDataReceivable(oFilter)
    EndIf 

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 30/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class SettlementsFinancialSmartViewBusinessObject
    self:setDisplayName(STR0001) //"Liquidações financeiras"
    self:setDescription(STR0002) //"Irá extrair a relação Liquidacoes Financeiras. "
    self:setPergunte("FINT029")  
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadParameters
    Define o schema do objeto de negócio.
    Será implementado no objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class SettlementsFinancialSmartViewBusinessObject
    Local aFieldsSE2 := {} as array

    aFieldsSE2 := {"E2_FILIAL","E2_PREFIXO","E2_PARCELA", "E2_TIPO", "E2_EMISSAO",;
                     "E2_VALOR","E2_NUM"}

    self:oSchema:aliasToSchema("SE2", aFieldsSE2)

    self:finAddProperty("NRO_LIQUIDACAO",STR0003 , "string") //Número da liquidação
    self:finAddProperty("STATUS",STR0004, "string") //Status
    self:finAddProperty("CARTEIRA",STR0005, "string") //Carteira
    self:finAddProperty("DADOS_CLIFOR",STR0006, "string") //Dados Cli/For
    self:finAddProperty("DADOS_BANCO",STR0007, "string", "E1_BCOCHQ") //Dados do banco
    self:finAddProperty("VA",STR0008, "number") //Valores acessórios
    self:finAddProperty("EMITENTE",STR0013, "string") //Emitente
    self:finAddProperty("PROCESSO",STR0014, "string") //Processo
    self:finAddProperty("JUROS",STR0015, "number") //Juros
    self:finAddProperty("DESCONTOS",STR0016, "number") //Descontos
    self:finAddProperty("VALOR_LIQUIDADO",STR0017, "number") //Valor Liquidado

    FwFreeArray(aFieldsSE2)
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementPayableBills
Retorna a query pai do Contas a Pagar, executada na função getData.
Irá trazer os registros baixados após liquidação.
@return character: cQuery

@author Matheus Monteiro
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatementPayableBills() class SettlementsFinancialSmartViewBusinessObject
    Local  cQuery := "" as character 
    
    if self:oQryPayable == Nil

        cQuery += " SELECT DISTINCT SE2.E2_FILIAL, SE2.E2_PREFIXO, SE2.E2_PARCELA, SE2.E2_TIPO, SE2.E2_EMISSAO, SE2.E2_VALOR  "
        cQuery += ", SE2.E2_FORNECE, SE2.E2_LOJA, SE2.E2_NOMFOR, '' AS EMITENTE,  SE2.E2_AGECHQ, SE2.E2_CTACHQ, SE2.E2_BCOCHQ, SE2.E2_NUM, SE2.E2_NUMLIQ, SE2.E2_ACRESC, SE2.E2_DECRESC "
        cQuery += ", SE2.E2_MOEDA AS MOEDASE2, SE2.E2_TXMOEDA AS TXMOEDASE2, SE2.E2_JUROS,SE2.E2_DESCONT,FK2.FK2_DOC AS DOCUMENTO "
        cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
        cQuery += " FROM " + RetSQLName("FK6") +" FK6 "
        cQuery += " WHERE " + FWJoinFilial("FK6","FK2")
        cQuery += " AND FK6_IDORIG = FK2.FK2_IDFK2 "
        cQuery += " AND FK6_TABORI = ? "
        cQuery += " AND FK6_TPDOC = ? "
        cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS VA " 
        cQuery += " ? "
        cQuery += " FROM " + RetSQLName("FK2") + " FK2 "
        cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
        cQuery += "     ON FK2.FK2_FILIAL = FK7.FK7_FILTIT "
        cQuery += "     AND FK2.FK2_IDDOC = FK7.FK7_IDDOC"
        cQuery += "     AND FK7.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("SE2") + " SE2 "
        cQuery += "     ON FK7.FK7_FILTIT = SE2.E2_FILIAL "
        cQuery += "     AND FK7.FK7_PREFIX = SE2.E2_PREFIXO " 
        cQuery += "     AND FK7.FK7_NUM = SE2.E2_NUM "
        cQuery += "     AND FK7.FK7_PARCEL = SE2.E2_PARCELA " 
        cQuery += "     AND FK7.FK7_TIPO = SE2.E2_TIPO "
        cQuery += "     AND FK7.FK7_CLIFOR = SE2.E2_FORNECE " 
        cQuery += "     AND FK7.FK7_LOJA = SE2.E2_LOJA "
        cQuery += "     AND SE2.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FI8") + " FI8 "  
        cQuery += "     ON  " + FWJoinFilial("FI8", "SE2")
        cQuery += "     AND SE2.E2_PREFIXO = FI8.FI8_PRFORI " 
        cQuery += "     AND SE2.E2_NUM = FI8.FI8_NUMORI " 
        cQuery += "     AND SE2.E2_PARCELA = FI8.FI8_PARORI "
        cQuery += "     AND SE2.E2_TIPO = FI8.FI8_TIPORI "
        cQuery += "     AND SE2.E2_FORNECE = FI8.FI8_FORORI " 
        cQuery += "     AND SE2.E2_LOJA = FI8.FI8_LOJORI " 
        cQuery += "     AND FI8.D_E_L_E_T_ = ? "
        cQuery += " WHERE "
        
        if self:lEmptyMultiBranches
            cQuery += " FK2.FK2_FILIAL = ? AND "
        else
            cQuery += " FK2.FK2_FILIAL IN (?) AND "
        endIf

        cQuery += "     FK2.FK2_DOC BETWEEN ? AND ? "
        cQuery += "     AND SE2.E2_EMISSAO BETWEEN ? AND ? "
        cQuery += "     AND SE2.E2_FORNECE BETWEEN ? AND ? "
        cQuery += "     AND SE2.D_E_L_E_T_ = ?  "
        cQuery += "     AND FK2.FK2_MOTBX = ? "
        cQuery += "     AND FK2.FK2_TPDOC = ? "
        cQuery += "     AND FK2.FK2_RECPAG = ? "

        If !self:lConvertCurrencyRate
            cQuery += " AND SE2.E2_MOEDA = ? "
        EndIf

        cQuery += " ORDER BY ? " 

        cQuery := ChangeQuery(cQuery)
        
        self:oQryPayable := FWExecStatement():New(cQuery)

    endIf
    

    self:oQryPayable:SetString(self:nOrderParamsPayable++, "FK2")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, "VA")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, " ")
    self:oQryPayable:SetUnsafe(self:nOrderParamsPayable++, self:getCustomFieldsPayToSelect(.T. /*lApllyCommaAtBeginning*/))
    self:oQryPayable:SetString(self:nOrderParamsPayable++, " ")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, " ")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, " ")

    If self:lEmptyMultiBranches
        self:oQryPayable:SetString(self:nOrderParamsPayable++, self:oFilterBranches["FK2"][1])
    Else
        self:oQryPayable:SetIn(self:nOrderParamsPayable++, self:oFilterBranches["FK2"][1])
    EndIf

    self:oQryPayable:SetString(self:nOrderParamsPayable++, self:cSettlementNumberFrom)
    self:oQryPayable:SetString(self:nOrderParamsPayable++, self:cSettlementNumberTo)
    self:oQryPayable:SetDate(self:nOrderParamsPayable++,   self:dIssueDateFrom)
    self:oQryPayable:SetDate(self:nOrderParamsPayable++,  self:dIssueDateTo)
    self:oQryPayable:SetString(self:nOrderParamsPayable++, self:cSupplierFrom)
    self:oQryPayable:SetString(self:nOrderParamsPayable++, self:cSupplierTo)
    self:oQryPayable:SetString(self:nOrderParamsPayable++, " ")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, "LIQ")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, "BA")
    self:oQryPayable:SetString(self:nOrderParamsPayable++, "P")

    If !self:lConvertCurrencyRate
        self:oQryPayable:SetNumeric(self:nOrderParamsPayable++, self:nCurrency)
    EndIf

    self:oQryPayable:SetUnsafe(self:nOrderParamsPayable++, SqlOrder(SE2->(IndexKey( 1 ))))

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementPayableChild
Retorna a query filha do Contas a Pagar, executada na função getData.
Irá trazer os registros gerados após liquidação.
@return character: cQuery

@author Matheus Monteiro
@since 07/12/2023
@version 1.0
'' AS PROCESSO
*/
//-------------------------------------------------------------------
method loadStatementPayableChild() class SettlementsFinancialSmartViewBusinessObject
    local cQuery := "" as character 
    local nParamOrder := 1 as numeric
    
    If self:oQryPayableChild = NIL

        cQuery += " SELECT DISTINCT SE2.E2_FILIAL, SE2.E2_PREFIXO, SE2.E2_PARCELA, SE2.E2_TIPO, SE2.E2_EMISSAO, SE2.E2_VALOR  "
        cQuery += ", SE2.E2_FORNECE, SE2.E2_LOJA, SE2.E2_NOMFOR,'' AS EMITENTE,  SE2.E2_AGECHQ, SE2.E2_CTACHQ, SE2.E2_BCOCHQ, SE2.E2_NUM, SE2.E2_NUMLIQ, SE2.E2_ACRESC, SE2.E2_DECRESC "
        cQuery += ", SE2.E2_MOEDA AS MOEDASE2, SE2.E2_TXMOEDA AS TXMOEDASE2,SE2.E2_JUROS,SE2.E2_DESCONT, FK2.FK2_DOC AS DOCUMENTO "
        cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
        cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
        cQuery += " WHERE " + FWJoinFilial("FK6","FK2")
        cQuery += " AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
        cQuery += " AND FK6.FK6_TABORI = ? "
        cQuery += " AND FK6.FK6_TPDOC = ? "
        cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS VA " 
        cQuery += " ? "
        cQuery += " FROM " + RetSQLName("FK2") + " FK2 "
        cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
        cQuery += "     ON FK2.FK2_FILIAL = FK7.FK7_FILTIT "
        cQuery += "     AND FK2.FK2_IDDOC = FK7.FK7_IDDOC"
        cQuery += "     AND FK7.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FI8") + " FI8 "  
        cQuery += "     ON " + FWJoinFilial("FI8","FK7")
        cQuery += "     AND FK7.FK7_PREFIX = FI8.FI8_PRFORI "
        cQuery += "     AND FK7.FK7_NUM = FI8.FI8_NUMORI "
        cQuery += "     AND FK7.FK7_PARCEL = FI8.FI8_PARORI "
        cQuery += "     AND FK7.FK7_TIPO = FI8.FI8_TIPORI "
        cQuery += "     AND FK7.FK7_CLIFOR = FI8.FI8_FORORI "
        cQuery += "     AND FK7.FK7_LOJA = FI8.FI8_LOJORI "
        cQuery += "     AND FI8.D_E_L_E_T_ = ? "
        cQuery  += " JOIN " + RetSQLName("SE2") + " SE2 "
        cQuery  += "     ON FI8.FI8_FILDES = SE2.E2_FILIAL " 
        cQuery  += "     AND FI8.FI8_PRFDES = SE2.E2_PREFIXO "
        cQuery  += "     AND FI8.FI8_NUMDES = SE2.E2_NUM "
        cQuery  += "     AND FI8.FI8_PARDES = SE2.E2_PARCELA "
        cQuery  += "     AND FI8.FI8_TIPDES = SE2.E2_TIPO "
        cQuery  += "     AND FI8.FI8_FORDES = SE2.E2_FORNECE "
        cQuery  += "     AND FI8.FI8_LOJDES = SE2.E2_LOJA "
        cQuery  += "     AND SE2.D_E_L_E_T_ = ?  "
        cQuery += " WHERE "

        if self:lEmptyMultiBranches
            cQuery += " FK2.FK2_FILIAL = ? AND "
        else
            cQuery += " FK2.FK2_FILIAL IN (?) AND "
        endIf

        cQuery += "     SE2.E2_FORNECE = ? "
        cQuery += "     AND SE2.E2_NUMLIQ = ?  "
        cQuery += "     AND FK2.FK2_RECPAG = ? "

        cQuery += " ORDER BY ? "
        cQuery := ChangeQuery(cQuery)
        
        self:oQryPayableChild := FWExecStatement():New(cQuery)
    EndIf

    self:oQryPayableChild:SetString(nParamOrder++, "FK2")
    self:oQryPayableChild:SetString(nParamOrder++, "VA")
    self:oQryPayableChild:SetString(nParamOrder++, " ")
    self:oQryPayableChild:SetUnsafe(nParamOrder++, self:getCustomFieldsPayToSelect(.T. /*lApllyCommaAtBeginning*/))
    self:oQryPayableChild:SetString(nParamOrder++, " ")
    self:oQryPayableChild:SetString(nParamOrder++, " ")
    self:oQryPayableChild:SetString(nParamOrder++, " ")
    
    If self:lEmptyMultiBranches
        self:oQryPayableChild:SetString(nParamOrder++, self:oFilterBranches["FK2"][1])
    Else
        self:oQryPayableChild:SetIn(nParamOrder++, self:oFilterBranches["FK2"][1])
    EndIf

    self:oQryPayableChild:SetString(nParamOrder++, self:cSupplierCode)
    self:oQryPayableChild:SetString(nParamOrder++, self:cDocumentNumber)
    self:oQryPayableChild:SetString(nParamOrder++, "P")
    self:oQryPayableChild:SetUnsafe(nParamOrder++, SqlOrder(SE2->(IndexKey( 1 ))))

return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementReceivableBills
Retorna a query pai do Contas a Receber, executada na função getData.
Irá trazer os registros baixados após liquidação.
@return character: cQuery

@author Matheus Monteiro
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatementReceivableBills() class SettlementsFinancialSmartViewBusinessObject
    Local cQuery := "" as character 

    if self:oQryReceivable == NIL 

        cQuery += " SELECT  SE1.E1_FILIAL E2_FILIAL, SE1.E1_PREFIXO E2_PREFIXO, SE1.E1_PARCELA E2_PARCELA, SE1.E1_TIPO E2_TIPO, SE1.E1_EMISSAO E2_EMISSAO, SE1.E1_VALOR E2_VALOR " 
        cQuery += " ,E1_CLIENTE E2_FORNECE, SE1.E1_LOJA E2_LOJA, SE1.E1_NOMCLI E2_NOMFOR, SE1.E1_EMITCHQ AS EMITENTE, SE1.E1_AGECHQ E2_AGECHQ, SE1.E1_CTACHQ E2_CTACHQ,SE1.E1_BCOCHQ E2_BCOCHQ,SE1.E1_NUM E2_NUM "
        cQuery += " ,SE1.E1_NUMLIQ E2_NUMLIQ,SE1.E1_DESCONT E2_DESCONT, FO1.FO1_VLJUR E2_ACRESC,SE1.E1_JUROS E2_JUROS, FO1.FO1_DESCON E2_DECRESC "
        cQuery += " ,SE1.E1_MOEDA AS MOEDASE1,SE1.E1_TXMOEDA AS TXMOEDASE1 " 
        cQuery += " ,FO1.FO1_VACESS AS ACESSORIOS, FO0.FO0_NUMLIQ AS NUMLIQ,FO0.FO0_PROCES AS PROCESSO "
        cQuery += " ? "
        cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
        cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
        cQuery += "     ON SE1.E1_FILIAL = FK7.FK7_FILTIT "
        cQuery += "     AND SE1.E1_PREFIXO = FK7.FK7_PREFIX "
        cQuery += "     AND SE1.E1_NUM = FK7.FK7_NUM "
        cQuery += "     AND SE1.E1_PARCELA = FK7.FK7_PARCEL "
        cQuery += "     AND SE1.E1_TIPO = FK7.FK7_TIPO " 
        cQuery += "     AND SE1.E1_CLIENTE = FK7.FK7_CLIFOR "
        cQuery += "     AND SE1.E1_LOJA = FK7.FK7_LOJA "
        cQuery += "     AND FK7_ALIAS = ? "
        cQuery += "     AND FK7.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FO1") + " FO1 "
        cQuery += "     ON " + FWJoinFilial("FO1","FK7")
        cQuery += "     AND FK7.FK7_FILTIT =  FO1.FO1_FILIAL "
        cQuery += "     AND FK7.FK7_IDDOC = FO1.FO1_IDDOC "
        cQuery += "     AND FO1.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FO0") + " FO0 "
        cQuery += "     ON  " + FWJoinFilial("FO0","FO1")
        cQuery += "     AND FO1.FO1_PROCES = FO0.FO0_PROCES "
        cQuery += "     AND FO1.FO1_VERSAO = FO0.FO0_VERSAO "
        cQuery += "     AND FO0.D_E_L_E_T_ = ? "
        cQuery += " WHERE "

        if self:lEmptyMultiBranches
            cQuery += " SE1.E1_FILIAL = ? AND "
        else
            cQuery += " SE1.E1_FILIAL IN (?) AND "
        endIf

        cQuery += "     FO0.FO0_NUMLIQ BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_EMISSAO BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_CLIENTE BETWEEN ? AND ? "
        cQuery += "     AND SE1.D_E_L_E_T_ = ?  "
        cQuery += "     AND SE1.E1_STATUS = ? "
        cQuery += "     AND SE1.E1_TIPOLIQ = ?

        If !self:lConvertCurrencyRate
            cQuery += " AND SE1.E1_MOEDA = ? "
        EndIf

        cQuery += " GROUP BY SE1.E1_FILIAL , SE1.E1_PREFIXO , SE1.E1_PARCELA , SE1.E1_TIPO, "
        cQuery += " SE1.E1_EMISSAO , SE1.E1_VALOR , SE1.E1_CLIENTE , SE1.E1_LOJA , SE1.E1_NOMCLI , "
        cQuery += " SE1.E1_EMITCHQ , SE1.E1_AGECHQ , SE1.E1_CTACHQ,  SE1.E1_BCOCHQ , SE1.E1_NUM , "
        cQuery += " SE1.E1_NUMLIQ , SE1.E1_DESCONT , FO1.FO1_VLJUR , SE1.E1_JUROS ,  FO1.FO1_DESCON , "
        cQuery += " SE1.E1_MOEDA,  SE1.E1_TXMOEDA ,  FO1.FO1_VACESS ,  FO0.FO0_NUMLIQ , FO0.FO0_PROCES  "
        cQuery += " ? "

        cQuery += " ORDER BY ? "

        cQuery := ChangeQuery(cQuery)
        
        self:oQryReceivable := FWExecStatement():New(cQuery)
    endIf

    self:oQryReceivable:SetUnsafe(self:nOrderParamsReceivable++, self:getCustomFieldsRecToSelect(.T. /*lApllyCommaAtBeginning*/))
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, "SE1")
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, " ")
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, " ")
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, " ")
    
    If self:lEmptyMultiBranches
        self:oQryReceivable:SetString(self:nOrderParamsReceivable++, self:oFilterBranches["SE1"][1])
    Else
        self:oQryReceivable:SetIn(self:nOrderParamsReceivable++, self:oFilterBranches["SE1"][1])
    EndIf

    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, self:cSettlementNumberFrom)
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, self:cSettlementNumberTo)
    self:oQryReceivable:SetDate(self:nOrderParamsReceivable++, self:dIssueDateFrom)
    self:oQryReceivable:SetDate(self:nOrderParamsReceivable++, self:dIssueDateTo)
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, self:cCustomerFrom)
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, self:cCustomerTo)
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, " ")
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, "B")
    self:oQryReceivable:SetString(self:nOrderParamsReceivable++, "LIQ")

    If !self:lConvertCurrencyRate
        self:oQryReceivable:SetNumeric(self:nOrderParamsReceivable++, self:nCurrency)
    EndIf

    self:oQryReceivable:SetUnsafe(self:nOrderParamsReceivable++, self:getCustomFieldsRecToGroupBy(.T. /*lApllyCommaAtBeginning*/))
    self:oQryReceivable:SetUnsafe(self:nOrderParamsReceivable++, SqlOrder(SE1->(IndexKey( 1 ))))
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatementReceivableChild
Retorna a query pai do Contas a Receber, executada na função getData.
Irá trazer os registros gerados após liquidação.
@return character: cQuery

@author Matheus Monteiro
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method loadStatementReceivableChild() class SettlementsFinancialSmartViewBusinessObject
    local cQuery := "" as character
    local nParamOrder := 1 as numeric

    if self:oQryReceivableChild == NIL 

        cQuery += " SELECT SE1.E1_FILIAL E2_FILIAL, SE1.E1_PREFIXO E2_PREFIXO, SE1.E1_PARCELA E2_PARCELA, SE1.E1_TIPO E2_TIPO, SE1.E1_EMISSAO E2_EMISSAO, SE1.E1_VALOR E2_VALOR  " 
        cQuery += " ,SE1.E1_CLIENTE  E2_FORNECE, SE1.E1_LOJA E2_LOJA, SE1.E1_NOMCLI E2_NOMFOR, SE1.E1_EMITCHQ AS EMITENTE, SE1.E1_AGECHQ E2_AGECHQ, SE1.E1_CTACHQ E2_CTACHQ,SE1.E1_BCOCHQ E2_BCOCHQ,SE1.E1_NUM E2_NUM "
        cQuery += " ,SE1.E1_NUMLIQ E2_NUMLIQ,SE1.E1_DESCONT E2_DESCONT, FO1.FO1_VLJUR E2_ACRESC,SE1.E1_JUROS E2_JUROS, FO1.FO1_DESCON E2_DECRESC "
        cQuery += " ,SE1.E1_MOEDA AS MOEDASE1,SE1.E1_TXMOEDA AS TXMOEDASE1 " 
        cQuery += " ,FO1.FO1_VACESS AS ACESSORIOS, FO0.FO0_NUMLIQ AS NUMLIQ,FO0.FO0_PROCES AS PROCESSO "
        cQuery += " ? "
        cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
        cQuery += " JOIN " + RetSQLName("FK7") + " FK7 "
        cQuery += "     ON SE1.E1_FILIAL = FK7.FK7_FILTIT "
        cQuery += "     AND SE1.E1_PREFIXO = FK7.FK7_PREFIX "
        cQuery += "     AND SE1.E1_NUM = FK7.FK7_NUM "
	    cQuery += "     AND SE1.E1_PARCELA = FK7.FK7_PARCEL "
	    cQuery += "     AND SE1.E1_TIPO = FK7.FK7_TIPO " 
	    cQuery += "     AND SE1.E1_CLIENTE = FK7.FK7_CLIFOR "
	    cQuery += "     AND SE1.E1_LOJA = FK7.FK7_LOJA "
	    cQuery += "     AND FK7_ALIAS = ? "
	    cQuery += "     AND FK7.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FO2") + " FO2 "
        cQuery += "     ON  " + FWJoinFilial("FO2","SE1")
        cQuery += "     AND  FO2.FO2_PREFIX = SE1.E1_PREFIXO  "
        cQuery += "     AND  FO2.FO2_NUM = SE1.E1_NUM "
        cQuery += "     AND  FO2.FO2_PARCEL = SE1.E1_PARCELA "
        cQuery += "     AND  FO2.FO2_TIPO = SE1.E1_TIPO  "
        cQuery += "     AND  FO2.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FO1") + " FO1 "
        cQuery += "     ON  " + FWJoinFilial("FO1","FO2") 
        cQuery += "     AND FO2.FO2_PROCES = FO1.FO1_PROCES "
        cQuery += "     AND FO2.FO2_VERSAO = FO1.FO1_VERSAO "
        cQuery += "     AND FO1.D_E_L_E_T_ = ? "
        cQuery += " JOIN " + RetSQLName("FO0") + " FO0 ON " + FWJoinFilial("FO0","FO1")
        cQuery += "     AND FO1.FO1_PROCES = FO0.FO0_PROCES "
        cQuery += "     AND FO1.FO1_VERSAO = FO0.FO0_VERSAO "
        cQuery += "     AND FO0.D_E_L_E_T_ = ? "
        cQuery += "  WHERE "

        If self:lEmptyMultiBranches
            cQuery += " SE1.E1_FILIAL = ? AND "
        Else
            cQuery += " SE1.E1_FILIAL IN (?) AND "
        EndIf

        cQuery += "     SE1.E1_CLIENTE = ? "
        cQuery += "     AND SE1.E1_NUMLIQ = ?  "
        cQuery += "     AND SE1.E1_ORIGEM = ? " 
        cQuery += "     AND SE1.D_E_L_E_T_ = ?  "
        cQuery += " GROUP BY SE1.E1_FILIAL , SE1.E1_PREFIXO , SE1.E1_PARCELA , SE1.E1_TIPO, "
        cQuery += " SE1.E1_EMISSAO , SE1.E1_VALOR , SE1.E1_CLIENTE , SE1.E1_LOJA , SE1.E1_NOMCLI , "
        cQuery += " SE1.E1_EMITCHQ , SE1.E1_AGECHQ , SE1.E1_CTACHQ,  SE1.E1_BCOCHQ , SE1.E1_NUM , "
        cQuery += " SE1.E1_NUMLIQ , SE1.E1_DESCONT , FO1.FO1_VLJUR , SE1.E1_JUROS ,  FO1.FO1_DESCON , "
        cQuery += " SE1.E1_MOEDA,  SE1.E1_TXMOEDA ,  FO1.FO1_VACESS ,  FO0.FO0_NUMLIQ , FO0.FO0_PROCES  "
        cQuery += " ? "
        cQuery += "ORDER BY ? "

        cQuery := changeQuery(cQuery)
        
        self:oQryReceivableChild := FWExecStatement():New(cQuery)
    endIf

    self:oQryReceivableChild:SetUnsafe(nParamOrder++, self:getCustomFieldsRecToSelect(.T. /*lApllyCommaAtBeginning*/))
    self:oQryReceivableChild:SetString(nParamOrder++, "SE1")
    self:oQryReceivableChild:SetString(nParamOrder++, " ")
    self:oQryReceivableChild:SetString(nParamOrder++, " ")
    self:oQryReceivableChild:SetString(nParamOrder++, " ")
    self:oQryReceivableChild:SetString(nParamOrder++, " ")
    
    If self:lEmptyMultiBranches
        self:oQryReceivableChild:SetString(nParamOrder++, self:oFilterBranches["SE1"][1])
    Else
        self:oQryReceivableChild:SetIn(nParamOrder++, self:oFilterBranches["SE1"][1])
    EndIf

    self:oQryReceivableChild:SetString(nParamOrder++, self:cCustomerCode)
    self:oQryReceivableChild:SetString(nParamOrder++, self:cSettlementNumber)
    self:oQryReceivableChild:SetString(nParamOrder++, "FINA460")
    self:oQryReceivableChild:SetString(nParamOrder++, " ")
    self:oQryReceivableChild:SetUnsafe(nParamOrder++, self:getCustomFieldsRecToGroupBy(.T. /*lApllyCommaAtBeginning*/))
    self:oQryReceivableChild:SetUnsafe(nParamOrder++, SqlOrder(SE1->(IndexKey(1))))

return

/*
{Protheus.doc} appendPayableChild
    Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Filha do Contas a pagar).
    @author Matheus Monteiro da Silva
    @since 01/11/2023
    @version 12.1.2410
*/
method appendPayableChild() class SettlementsFinancialSmartViewBusinessObject
    Local cSupplierCustomer        := ""    as character
    Local cBankBranchAccount       := ""    as character
    Local cCheckIssuer             := ""    as character
    Local nAccessoryValueChild              as numeric
    Local nValueLiquidated                  as numeric
    Local cSettlementNumberChild   := ""    as character
    
    self:loadStatementPayableChild()

    self:cAliasChild:= self:oQryPayableChild:openAlias()

    (self:cAliasChild)->(DBGoTop())
 
    While !(self:cAliasChild)->(Eof())

        cSupplierCustomer       := (self:cAliasChild)->E2_FORNECE + "/" + (self:cAliasChild)->E2_LOJA + " - " + (self:cAliasChild)->E2_NOMFOR
        cBankBranchAccount      := (self:cAliasChild)->E2_BCOCHQ + "/" + (self:cAliasChild)->E2_AGECHQ + " - " + (self:cAliasChild)->E2_CTACHQ
        nAccessoryValueChild    := (self:convertBillCurrencyPayable((self:cAliasChild)->VA))
        cCheckIssuer            := (self:cAliasChild)->EMITENTE
        nValueLiquidated        := (self:convertBillCurrencyPayable((self:cAliasChild)->E2_VALOR + (self:cAliasChild)->E2_ACRESC - (self:cAliasChild)->E2_DECRESC))
        cSettlementNumberChild  := (self:cAliasChild)->E2_NUMLIQ

        self:jData := {; 
            "CARTEIRA": self:cPortfoliosListPayable, ;
            "PROCESSO": (self:cAliasChild)->DOCUMENTO, ;
            "NRO_LIQUIDACAO": (self:cAliasChild)->DOCUMENTO, ;
            "DADOS_CLIFOR": cSupplierCustomer, ;
            "E2_PREFIXO": (self:cAliasChild)->E2_PREFIXO, ;
            "E2_NUM": (self:cAliasChild)->E2_NUM, ;
            "E2_FILIAL": Alltrim((self:cAliasChild)->E2_FILIAL), ;
            "E2_PARCELA": (self:cAliasChild)->E2_PARCELA, ;
            "E2_TIPO": (self:cAliasChild)->E2_TIPO, ;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAliasChild)->E2_EMISSAO)), ;
            "EMITENTE": cCheckIssuer, ;
            "E2_VALOR": (self:convertBillCurrencyPayable((self:cAliasChild)->E2_VALOR)), ;
            "VALOR_LIQUIDADO": nValueLiquidated, ;
            "JUROS": (self:convertBillCurrencyPayable((self:cAliasChild)->E2_ACRESC)), ;
            "DESCONTOS": (self:convertBillCurrencyPayable((self:cAliasChild)->E2_DECRESC)), ;
            "VA": nAccessoryValueChild, ;
            "DADOS_BANCO": cBankBranchAccount, ;
            "STATUS": self:cBillGenerated;
        }

        self:processAndAppend()
        
        (self:cAliasChild)->(DbSkip())
    Enddo

    (self:cAliasChild)->( DBCloseArea() )
    self:cAliasChild := ""
 
return

//-------------------------------------------------------------------
/*{Protheus.doc} setDataPayable
Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Pai Contas a pagar).
@param oFilter, objeto, contém o filtro do TReports
@author Matheus Monteiro da Silva
@since 04/12/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method setDataPayable() class SettlementsFinancialSmartViewBusinessObject
    Local cBankBranchAccount       := ""    as character
    Local cSupplierCustomer        := ""    as character
    Local cCheckIssuer             := ""    as character
    Local nAccessoryValue                   as numeric
    Local nValueLiquidated                  as numeric
    
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        cSupplierCustomer       := (self:cAlias)->E2_FORNECE + "/" + (self:cAlias)->E2_LOJA + " - " + (self:cAlias)->E2_NOMFOR
        cBankBranchAccount      := (self:cAlias)->E2_BCOCHQ + "/" + (self:cAlias)->E2_AGECHQ + " - " + (self:cAlias)->E2_CTACHQ
        nAccessoryValue         := (self:convertBillCurrencyPayable((self:cAlias)->VA))
        cCheckIssuer            := (self:cAlias)->EMITENTE
        nValueLiquidated        := 0
        self:cDocumentNumber    := (self:cAlias)->DOCUMENTO
        self:cSupplierCode      := (self:cAlias)->E2_FORNECE

        self:jData := {; 
            "CARTEIRA": self:cPortfoliosListPayable, ;
            "PROCESSO": self:cDocumentNumber, ;
            "NRO_LIQUIDACAO": self:cDocumentNumber, ;
            "DADOS_CLIFOR": cSupplierCustomer, ;
            "E2_PREFIXO": (self:cAlias)->E2_PREFIXO, ;
            "E2_NUM": (self:cAlias)->E2_NUM, ;
            "E2_FILIAL": Alltrim((self:cAlias)->E2_FILIAL), ;
            "E2_PARCELA": (self:cAlias)->E2_PARCELA, ;
            "E2_TIPO": (self:cAlias)->E2_TIPO, ;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAlias)->E2_EMISSAO)), ;
            "EMITENTE": cCheckIssuer, ;
            "E2_VALOR": (self:convertBillCurrencyPayable((self:cAlias)->E2_VALOR)), ;
            "VALOR_LIQUIDADO": nValueLiquidated, ;
            "JUROS": (self:convertBillCurrencyPayable((self:cAlias)->E2_JUROS)), ;
            "DESCONTOS": (self:convertBillCurrencyPayable((self:cAlias)->E2_DESCONT)), ;
            "VA": nAccessoryValue, ;
            "DADOS_BANCO": cBankBranchAccount, ;
            "STATUS": self:cBillFinished;     
        }   

        self:processAndAppend()

        (self:cAlias)->(DbSkip()) 
    
        If self:cDocumentNumber + self:cSupplierCode   <>  (self:cAlias)->DOCUMENTO + (self:cAlias)->E2_FORNECE 
            self:appendPayableChild()  
        EndIf                     
    Enddo

return

/*
{Protheus.doc} appendReceivableChild
    Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Filha Conts a Receber).
    @author Matheus Monteiro da Silva
    @since 01/11/2023
    @version 12.1.2410
*/
method appendReceivableChild() class SettlementsFinancialSmartViewBusinessObject
    Local cSupplierCustomer        := ""    as character
    Local cBankBranchAccount       := ""    as character
    Local cCheckIssuer             := ""    as character
    Local nAccessoryValueChild              as numeric
    Local nValueLiquidated                  as numeric

    self:loadStatementReceivableChild()

    self:cAliasChild := self:oQryReceivableChild:openAlias()

    (self:cAliasChild)->(DBGoTop())
 
    While !(self:cAliasChild)->(Eof())

        cSupplierCustomer       := (self:cAliasChild)->E2_FORNECE + "/" + (self:cAliasChild)->E2_LOJA + " - " + (self:cAliasChild)->E2_NOMFOR
        cBankBranchAccount      := (self:cAliasChild)->E2_BCOCHQ + "/" + (self:cAliasChild)->E2_AGECHQ + " - " + (self:cAliasChild)->E2_CTACHQ
        cCheckIssuer            := (self:cAliasChild)->EMITENTE
        nAccessoryValueChild    := (self:convertBillCurrencyReceivable((self:cAliasChild)->ACESSORIOS))
        nValueLiquidated        := (self:convertBillCurrencyReceivable((self:cAliasChild)->E2_VALOR + (self:cAliasChild)->E2_ACRESC - (self:cAliasChild)->E2_DECRESC))

        self:jData := {; 
            "CARTEIRA": self:cPortfoliosListReceivable, ;
            "PROCESSO": (self:cAliasChild)->PROCESSO, ;
            "NRO_LIQUIDACAO": (self:cAliasChild)->NUMLIQ, ;
            "DADOS_CLIFOR": cSupplierCustomer, ;
            "E2_PREFIXO": (self:cAliasChild)->E2_PREFIXO, ;
            "E2_NUM": (self:cAliasChild)->E2_NUM, ;
            "E2_FILIAL": Alltrim((self:cAliasChild)->E2_FILIAL), ;
            "E2_PARCELA": (self:cAliasChild)->E2_PARCELA, ;
            "E2_TIPO": (self:cAliasChild)->E2_TIPO, ;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAliasChild)->E2_EMISSAO)), ;
            "EMITENTE": cCheckIssuer, ;
            "E2_VALOR": (self:convertBillCurrencyReceivable((self:cAliasChild)->E2_VALOR)), ;
            "VALOR_LIQUIDADO": nValueLiquidated, ;
            "JUROS": (self:convertBillCurrencyReceivable((self:cAliasChild)->E2_ACRESC)), ;
            "DESCONTOS": (self:convertBillCurrencyReceivable((self:cAliasChild)->E2_DECRESC)), ;
            "VA": nAccessoryValueChild, ;
            "DADOS_BANCO": cBankBranchAccount, ;
            "STATUS": self:cBillGenerated;
        }

        self:processAndAppend()
        
        (self:cAliasChild)->(DbSkip())
    Enddo

    (self:cAliasChild)->( DBCloseArea() )
    self:cAliasChild := ""
 
return

//-------------------------------------------------------------------
/*{Protheus.doc} myAliasToData
Atribui ao objeto oData o conteúdo do Alias e campos calculados (Query Pai Contas a Receber).
@param oFilter, objeto, contém o filtro do TReports
@author Matheus Monteiro da Silva
@since 04/12/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method setDataReceivable() class SettlementsFinancialSmartViewBusinessObject
    Local cBankBranchAccount       := ""    as character
    Local cSupplierCustomer        := ""    as character
    Local nAccessoryValue                   as numeric
    Local cCheckIssuer             := ""    as character
    Local nValueLiquidated                  as numeric
    
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof()) 

        cSupplierCustomer       := (self:cAlias)->E2_FORNECE + "/" + (self:cAlias)->E2_LOJA + " - " + (self:cAlias)->E2_NOMFOR
        cBankBranchAccount      := (self:cAlias)->E2_BCOCHQ + "/" + (self:cAlias)->E2_AGECHQ + " - " + (self:cAlias)->E2_CTACHQ
        cCheckIssuer            := (self:cAlias)->EMITENTE
        nAccessoryValue         := (self:convertBillCurrencyReceivable((self:cAlias)->ACESSORIOS))
        nValueLiquidated        := 0
        self:cProcessNumber           := (self:cAlias)->PROCESSO
        self:cCustomerCode          := (self:cAlias)->E2_FORNECE
        self:cSettlementNumber        := (self:cAlias)->NUMLIQ


        self:jData := {; 
            "CARTEIRA": self:cPortfoliosListReceivable, ;
            "PROCESSO": self:cProcessNumber, ;
            "NRO_LIQUIDACAO": self:cSettlementNumber, ;
            "DADOS_CLIFOR": cSupplierCustomer, ;
            "E2_PREFIXO": (self:cAlias)->E2_PREFIXO, ;
            "E2_NUM": (self:cAlias)->E2_NUM, ;
            "E2_FILIAL": Alltrim(((self:cAlias)->E2_FILIAL)), ;
            "E2_PARCELA": (self:cAlias)->E2_PARCELA, ;
            "E2_TIPO": (self:cAlias)->E2_TIPO, ;
            "E2_EMISSAO": (self:varToTimeStamp((self:cAlias)->E2_EMISSAO)), ;
            "EMITENTE": cCheckIssuer, ;
            "E2_VALOR": (self:convertBillCurrencyReceivable((self:cAlias)->E2_VALOR)), ;
            "VALOR_LIQUIDADO": nValueLiquidated, ;
            "JUROS": (self:convertBillCurrencyReceivable((self:cAlias)->E2_JUROS)), ;
            "DESCONTOS": (self:convertBillCurrencyReceivable((self:cAlias)->E2_DESCONT)), ;
            "VA": nAccessoryValue, ;
            "DADOS_BANCO": cBankBranchAccount, ;
            "STATUS": self:cBillFinished ;
        }

        self:processAndAppend()  
        (self:cAlias)->(DbSkip()) 

        If  self:cCustomerCode  + self:cSettlementNumber  <> (self:cAlias)->E2_FORNECE + (self:cAlias)->NUMLIQ 
            self:appendReceivableChild()  
        EndIf           
    Enddo 
return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    Inicializa as propriedades de parâmetros do objeto de negócio
    para não correr riscos de error.log.
    Será implementado no objeto de negócio.
    @author guilherme.sordi@totvs.com.br
    @since 08/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class SettlementsFinancialSmartViewBusinessObject
    self:cSettlementNumberFrom := ""
    self:cSettlementNumberTo := "ZZZ"
    self:dIssueDateFrom := cToD("  /  /  ")
    self:dIssueDateTo := cToD("  /  /  ")
    self:cCustomerFrom := " "
    self:cCustomerTo := "ZZZZ"
    self:cSupplierFrom := " "
    self:cSupplierTo := "ZZZZ"
    self:nCurrency := 1
    self:lConvertCurrencyRate := .F.
return

/*/{Protheus.doc} loadParameters
    Carrega os parâmetros da consulta antes de chamar o loadStatement().
    Será implementado no objeto de negócio, de acordo com a necessidade e
    os parâmetros utilizados.
    @author guilherme.sordi@totvs.com.br
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class SettlementsFinancialSmartViewBusinessObject
    
    if self:oCurrentFilter != NIL
        self:cSettlementNumberFrom := MV_PAR01
        self:cSettlementNumberTo := MV_PAR02 
        self:dIssueDateFrom := MV_PAR03
        self:dIssueDateTo := MV_PAR04
        self:cCustomerFrom := MV_PAR05
        self:cCustomerTo := MV_PAR06
        self:cSupplierFrom:= MV_PAR07
        self:cSupplierTo := MV_PAR08
        self:lPayable := (MV_PAR09 == 1 .or. MV_PAR09 == 3)
        self:lReceivable:= (MV_PAR09 == 2 .or. MV_PAR09 == 3)
            
        self:nCurrency := self:getCurrencyValue(MV_PAR10)
        self:lConvertCurrencyRate := MV_PAR11 <> 1
    EndIf
return

/*/{Protheus.doc} convertCurrency
    Converte o valor da moeda. Será chamado por convertBillCurrency ou convertPostCurrency após parametrização pelo título ou pela baixa.
    @author matheus.monteiro@totvs.com.br
    @since 04/12/2023
    @version 12.1.2210
*/
method convertCurrency(nValue as numeric) as numeric class SettlementsFinancialSmartViewBusinessObject    
    local nCurrencyRateFrom := 0 as numeric
    local nCurrencyRateTo := 0 as numeric

    if self:nCurrencyFrom == 1
        nCurrencyRateTo := self:nContratctedCurrencyRate
    else
        nCurrencyRateFrom := self:nContratctedCurrencyRate
    endIf

    nValue := xMoeda(nValue, self:nCurrencyFrom, self:nCurrency, self:dConversion, self:nDecimals, nCurrencyRateFrom, nCurrencyRateTo)
return nValue


/*/{Protheus.doc} convertBillCurrencyPayable
    Define os dados do título como parâmetros de conversão e retorna o valor convertido.
    @author matheus.monteiro@totvs.com.br
    @since 04/12/2023
    @version 12.1.2210
*/
method convertBillCurrencyPayable(nValue as numeric) as numeric class SettlementsFinancialSmartViewBusinessObject
    
    self:nCurrencyFrom := (self:cAlias)->MOEDASE2
    self:dConversion := (self:cAlias)->E2_EMISSAO
    self:nContratctedCurrencyRate := (self:cAlias)->TXMOEDASE2

return self:convertCurrency(nValue)

/*/{Protheus.doc} convertBillCurrencyReceivable
    Define os dados do título como parâmetros de conversão e retorna o valor convertido.
    @author matheus.monteiro@totvs.com.br
    @since 04/12/2023
    @version 12.1.2210
*/
method convertBillCurrencyReceivable(nValue as numeric) as numeric class SettlementsFinancialSmartViewBusinessObject
    
    self:nCurrencyFrom := (self:cAlias)->MOEDASE1
    self:dConversion := (self:cAlias)->E2_EMISSAO
    self:nContratctedCurrencyRate := (self:cAlias)->TXMOEDASE1
    
return self:convertCurrency(nValue)


//-------------------------------------------------------------------
/*/{Protheus.doc} preAppendData
    Manipula o jData imediatamente antes do append, depois que todos os outros métodos relacionados
    ao jData já foram executados.
    @author guilhermed.santos@totvs.com.br
    @since 19/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method preAppendData() class SettlementsFinancialSmartViewBusinessObject
    self:addBranchNameToChildData()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addBranchNameToChildData
    Adiciona o nome da Filial ao jData do filho
    @author guilhermed.santos@totvs.com.br
    @since 19/12/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method addBranchNameToChildData() class SettlementsFinancialSmartViewBusinessObject
    local cBranchCode := "" as character
    
    if empty(self:cAliasChild)
        return
    endIf

    cBranchCode := (self:cAliasChild)->&(self:cMainBranchField)

    if Empty(cBranchCode)
        cBranchCode := "DEFAULT"
        self:jData["FILIAL_NAME"] := allTrim(self:jBranchNames[AllTrim(cBranchCode)])   
    else    
        self:jData["FILIAL_NAME"] := cBranchCode + " - " + allTrim(self:jBranchNames[AllTrim(cBranchCode)])   
    endIf
return
