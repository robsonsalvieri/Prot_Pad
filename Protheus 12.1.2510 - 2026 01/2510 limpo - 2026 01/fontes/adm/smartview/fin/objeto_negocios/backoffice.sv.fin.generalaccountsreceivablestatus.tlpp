#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.generalaccountsreceivablestatus.ch"


namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,FK1,FK6,SC5,SC6", name="Posição Geral do Contas a Receber", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    Classe para criação do Objeto de Negócio para geração da Posição geral do contas a receber

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class GeneralAccountsReceivableStatusReportSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oQueryTitulos as object
    protected data oQueryBaixas as object
    protected data oQueryVendas as object

    protected data nListpostingsby      as numeric
    protected data dIssueDateFrom       as date
    protected data dIssueDateTo         as date
    protected data nCurrency            as numeric
    protected data lListarOutrasMoedas  as logical
    protected data cInitialprefix       as character
    protected data cFinalprefix         as character
    protected data dFromDueDate         as date
    protected data dToDueDate           as date
    protected data cSelectTitulos       as character
    protected data cSelectBaixas        as character
    protected data cSelectVendas        as character
    protected data cWhereTitulos        as character
    protected data cWhereBaixas         as character
    protected data cWhereVendas         as character
    protected data jRelationSE1SC5      as array

    protected method initializeBusinessObject()
    protected method getData() as object
    protected method mySetSchema()
    protected method loadParameters()

    protected method getListaDeTitulos()
    protected method getListaDeBaixas()
    protected method getListaDePedidosDeVendas()

    protected method setDataListaDeTitulos()            as object
    protected method setDataListaDeBaixas()             as object
    protected method setDataListaDePedidosDeVendas()    as object

    protected method getAgingText() as character
    protected method getTypeOfBillText() as character
    protected method changeCanFilter()
    protected method filterWithRelationOfSC5() as character


endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Método de instância da classe

    @return object: self

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-----------|--------------------------------------------------------   
method new() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()

    self:setIsCBoxLookup(.T., .T.)
    self:setIsLookup(.T.)

    self:loadParameters()

    self:cSelectTitulos := ""
    self:cSelectBaixas  := ""
    self:cSelectVendas  := ""

    self:cWhereTitulos  := ""
    self:cWhereBaixas   := ""
    self:cWhereVendas   := ""

    self:setCompleteData(.T.)
    self:setMainTable("SE1")
    self:setBranchFilter(.T., .T.)

return self


//-------------------------------------------------------------------
/*/{Protheus.doc} initializeBusinessObject
    Método utilizado para inicializar dados do objeto de negócio, como nome, descrição e pergunta.

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------  
method initializeBusinessObject() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    self:setDisplayName(STR0002) //Posição Geral do Contas a Receber
    self:setDescription(STR0003) //Posição Geral do Contas a Receber - Listagem de títulos por categoria e aging.
    self:setPergunte("FINT028")
return


//-------------------------------------------------------------------
/*{Protheus.doc} getData
    Retorna os dados do objeto de negócio

    @param nPage, numérico, indica a página atual do relatório
    @param oFilter, objeto, contém o filtro do TReports
    @return object: self:oData

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    if !self:validEnvironment()
        return self:oData
    endIf

    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadFilterBranches( { "SE1", "SC6" } )
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()
    self:loadLGPDFields()

    //Carrega o bloco de títulos a receber
    self:getListaDeTitulos()
    self:cAlias  := self:oQueryTitulos:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListaDeTitulos()    
    (self:cAlias)->( DBCloseArea() )

    //Carrega o bloco de baixas
    self:getListaDeBaixas()
    self:cAlias  := self:oQueryBaixas:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListaDeBaixas()
    (self:cAlias)->( DBCloseArea() )

    //carrega o bloco de pedidos de Vendas
    self:getListaDePedidosDeVendas()
    self:cAlias  := self:oQueryVendas:openAlias(/*cAlias*/, self:cLifeTime, self:cTimeout)
    self:setDataListaDePedidosDeVendas()
    (self:cAlias)->( DBCloseArea() )

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} mySetSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Guilherme de Paula Santos
@since 23/11/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method mySetSchema() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject

    local aFieldsSE1 := {} as array
    local aFieldsSED := {} as array
    local aFieldsFK1 := {} as array

    aFieldsSE1 := {"E1_FILIAL", "E1_PREFIXO", "E1_PARCELA", "E1_TIPO", "E1_NATUREZ",;
        "E1_CLIENTE", "E1_LOJA", "E1_NOMCLI", "E1_EMISSAO", "E1_BAIXA"}
    aFieldsSED := {"ED_DESCRIC"}
    aFieldsFK1 := {"FK1_VALOR"}

    self:jRelationSE1SC5 := {"E1_FILIAL":"C5_FILIAL","E1_CLIENTE":"C5_CLIENTE","E1_LOJA":"C5_LOJA","E1_EMISSAO":"C5_EMISSAO"}

    self:oSchema:aliasToSchema("SE1", aFieldsSE1)
    self:oSchema:aliasToSchema("SED", aFieldsSED)
    self:oSchema:aliasToSchema("FK1", aFieldsFK1)

    self:finAddProperty("E1_NUM"         , STR0004, "string")   //"Nro. Título/Ped. Vendas"
    self:finAddProperty("E1_VENCREA"     , STR0005, "date"  )   //"Vencto Tit./Prev. Entrega"
    self:finAddProperty("E1_VALOR"       , STR0006, "number")   //"Vlr. Titulo/Total Vendas"
    self:finAddProperty("E1_SALDO"       , STR0007, "number")   //"Vlr. em Aberto"
    self:finAddProperty("TIPOVENDA"     , STR0008, "string" )   //"Tipo de Venda"
    self:finAddProperty("TIPOTITULO"     , STR0009, "string")   //"Tipo de Título"
    self:finAddProperty("CATEG_TITULO"   , STR0010, "string")   //"Classificação"
    self:finAddProperty("AGING_TITULO"   , STR0011, "string")   //"Aging do Título"
    self:finAddProperty("VALOR_CORRECAO" , STR0042, "number")   //"Valor de Correção"
    self:finAddProperty("VALOR_ACESSORIO", STR0043, "number")   //"Valor Acessório"

    FwFreeArray(aFieldsSE1)
    FwFreeArray(aFieldsSED)
    FwFreeArray(aFieldsFK1)

    self:changeCanFilter()

return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} getListaDeTitulos

    @author Guilherme de Paula Santos
    @since  23/11/2023
    @version 12.1.2310
    
*/
//-------------------------------------------------------------------
method getListaDeTitulos() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local cQuery := ""  as Character
    local nParam := 1   as numeric

    cQuery := " SELECT "
    cQuery += "  E1_FILIAL, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, "
    cQuery += "  E1_NATUREZ, ED_DESCRIC, E1_CLIENTE, E1_LOJA, E1_NOMCLI, "
    cQuery += "  E1_EMISSAO, E1_VENCREA, E1_BAIXA, E1_VALOR, E1_SALDO, "
    cQuery += "  E1_TXMOEDA, E1_MOEDA "

    cQuery += " ? "

    cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
    cQuery += " INNER JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SED", "SE1") + " 
    cQuery += "  AND SED.ED_CODIGO = SE1.E1_NATUREZ"
    cQuery += "  AND SED.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    if self:lEmptyMultiBranches
        cQuery += " SE1.E1_FILIAL = ? AND "
    else
        cQuery += " SE1.E1_FILIAL IN (?) AND "
    endIf
    cQuery += " SE1.E1_VENCREA BETWEEN ? AND ? AND "
    cQuery += " SE1.E1_PREFIXO BETWEEN ? AND ? AND "
    cQuery += " SE1.E1_TIPO NOT IN ? AND " 
    cQuery += " SE1.E1_SALDO > ? AND "
    
    if !self:lListarOutrasMoedas
        cQuery += " SE1.E1_MOEDA = ? AND "
    endIf

    cQuery += self:cWhereTitulos

    cQuery += " SE1.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery := ChangeQuery(cQuery)

    self:oQueryTitulos := FWExecStatement():New(cQuery)

    self:oQueryTitulos:SetUnsafe(nParam++, self:cSelectTitulos)
    self:oQueryTitulos:SetString(nParam++, ' ')
    if self:lEmptyMultiBranches
        self:oQueryTitulos:SetString(nParam++, self:oFilterBranches["SE1"][1])
    else
        self:oQueryTitulos:SetIn(nParam++, self:oFilterBranches["SE1"][1])
    endIf
    self:oQueryTitulos:SetDate(nParam++, self:dFromDueDate)
    self:oQueryTitulos:SetDate(nParam++, self:dToDueDate)
    self:oQueryTitulos:SetString(nParam++,self:cInitialprefix)
    self:oQueryTitulos:SetString(nParam++,self:cFinalprefix)
    self:oQueryTitulos:SetUnsafe(nParam++, FormatIn(MVPROVIS + "|" + MVABATIM,"|" ))
    self:oQueryTitulos:SetNumeric(nParam++, 0)

    if !self:lListarOutrasMoedas
        self:oQueryTitulos:SetNumeric(nParam++,self:nCurrency)
    endIf

    self:oQueryTitulos:SetString(nParam++, ' ')

return


//-------------------------------------------------------------------
/*{Protheus.doc} getListaDeBaixas

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getListaDeBaixas() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local cQuery := ""  as Character
    local nParam := 1   as numeric

    cQuery := " SELECT "
    cQuery += " E1_FILIAL, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, E1_NATUREZ, E1_CLIENTE, "
    cQuery += " E1_LOJA, E1_NOMCLI, E1_EMISSAO, E1_VENCREA, E1_BAIXA, E1_VALOR, E1_SALDO, "
    cQuery += " ED_DESCRIC, FK1_TPDOC, FK1_TXMOED, FK1_MOTBX, FK1_VALOR, FK1_VLMOE2, FK1_DATA, "
    cQuery += " FK1_MOEDA, E1_MOEDA, E1_TXMOEDA, "
    cQuery += "   (SELECT COALESCE(SUM(CASE WHEN FK6.FK6_TPDOC NOT IN ('DC', 'D2') THEN FK6.FK6_VALMOV ELSE - FK6.FK6_VALMOV END),0) "
    cQuery += " 	FROM " + RetSQLName("FK6") + " FK6 "
	cQuery += " 	WHERE " + FWJoinFilial("FK6","FK1") + " AND "
    cQuery += " 	FK6.FK6_IDORIG = FK1.FK1_IDFK1 AND "
    cQuery += " 	FK6.FK6_TABORI = ? AND "
    cQuery += " 	FK6.FK6_TPDOC IN (?) AND "
    cQuery += " 	FK6.D_E_L_E_T_ = ?) AS VALOR_CORRECAO, "
    cQuery += "   (SELECT COALESCE(SUM(CASE WHEN FK6.FK6_TPDOC NOT IN ('DC', 'D2') THEN FK6.FK6_VALMOV ELSE - FK6.FK6_VALMOV END),0) "
    cQuery += " 	FROM " + RetSQLName("FK6") + " FK6 "
	cQuery += " 	WHERE " + FWJoinFilial("FK6","FK1") + " AND "
    cQuery += " 	FK6.FK6_IDORIG = FK1.FK1_IDFK1 AND "
    cQuery += " 	FK6.FK6_TABORI = ? AND "
    cQuery += " 	FK6.FK6_TPDOC NOT IN (?) AND "
    cQuery += " 	FK6.D_E_L_E_T_ = ?) AS VALOR_ACESSORIO "

    cQuery += self:cSelectBaixas

    cQuery += " FROM " + RetSQLName("SE1") + " SE1 "
	cQuery += " INNER JOIN " + RetSQLName("SED") + " SED ON " + FWJoinFilial("SED", "SE1") + " AND "
    cQuery += "     SED.ED_CODIGO = E1_NATUREZ AND "
	cQuery += "     SED.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 ON "
    cQuery += "     FK7.FK7_ALIAS = ? AND "
	cQuery += "     FK7.FK7_FILTIT = SE1.E1_FILIAL AND "
	cQuery += "     FK7.FK7_PREFIX = SE1.E1_PREFIXO AND "
    cQuery += "     FK7.FK7_NUM = SE1.E1_NUM AND "
	cQuery += "     FK7.FK7_PARCEL = SE1.E1_PARCELA AND "
	cQuery += "     FK7.FK7_TIPO = SE1.E1_TIPO AND "
	cQuery += "     FK7.FK7_CLIFOR = SE1.E1_CLIENTE AND "
	cQuery += "     FK7.FK7_LOJA = SE1.E1_LOJA AND "
	cQuery += "     FK7.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName("FK1") + " FK1 ON " + FWJoinFilial("FK1","FK7") + " AND "
	cQuery += "     FK1.FK1_IDDOC = FK7.FK7_IDDOC AND "
    cQuery += "     FK1.FK1_MOTBX NOT IN (?) AND "

    If self:nListpostingsby == 1
	    cQuery += " FK1.FK1_DATA BETWEEN ? AND ? AND "
    ElseIf self:nListpostingsby == 2
	    cQuery += " FK1.FK1_DTDIGI BETWEEN ? AND ? AND "
    ElseIf self:nListpostingsby == 3 
	    cQuery += " FK1.FK1_DTDISP BETWEEN ? AND ? AND "
    EndIf

    cQuery += " FK1.FK1_RECPAG = ? AND "
    cQuery += " FK1.D_E_L_E_T_ = ? AND "
    cQuery += " NOT EXISTS ("
    cQuery += "     SELECT FK1EST.FK1_IDDOC FROM " + RetSQLName("FK1") + " FK1EST "
    cQuery += "     WHERE FK1EST.FK1_IDDOC = FK1.FK1_IDDOC AND "
    cQuery += "     FK1EST.FK1_SEQ = FK1.FK1_SEQ AND "
	cQuery += "     FK1EST.FK1_TPDOC = ? AND "
	cQuery += "     FK1EST.D_E_L_E_T_ = ? ) "
    cQuery += "WHERE "
	if self:lEmptyMultiBranches
        cQuery += " SE1.E1_FILIAL = ? AND "
    else
        cQuery += " SE1.E1_FILIAL IN (?) AND "
    endIf
    cQuery += " SE1.E1_PREFIXO BETWEEN ? AND ? AND "

    if !self:lListarOutrasMoedas
        cQuery += " SE1.E1_MOEDA = ? AND "
    endIf

    cQuery += self:cWhereBaixas

    cQuery += " SE1.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery := ChangeQuery(cQuery)

    self:oQueryBaixas := FWExecStatement():New(cQuery)

    //SubQuery - VALOR_CORRECAO
    self:oQueryBaixas:SetString(nParam++, 'FK1')
    self:oQueryBaixas:SetIn(nParam++, {'CM','VM','C2'})
    self:oQueryBaixas:SetString(nParam++, ' ')

    //SubQuery - VALOR_ACESSORIO
    self:oQueryBaixas:SetString(nParam++, 'FK1')
    self:oQueryBaixas:SetIn(nParam++, {'CM','VM','C2'})
    self:oQueryBaixas:SetString(nParam++,' ')

    //Query
    self:oQueryBaixas:SetString(nParam++, ' ')

    self:oQueryBaixas:SetString(nParam++, 'SE1')
    self:oQueryBaixas:SetString(nParam++, ' ')

    self:oQueryBaixas:SetIn(nParam++, {'DSD', 'FAT', 'LIQ', 'STP'})
    self:oQueryBaixas:SetDate(nParam++, self:dIssueDateFrom)
    self:oQueryBaixas:SetDate(nParam++, self:dIssueDateTo)
    self:oQueryBaixas:SetString(nParam++, 'R')
    self:oQueryBaixas:SetString(nParam++, ' ')

    self:oQueryBaixas:SetString(nParam++, 'ES')
    self:oQueryBaixas:SetString(nParam++, ' ')

    if self:lEmptyMultiBranches
        self:oQueryBaixas:SetString(nParam++, self:oFilterBranches["SE1"][1])
    else
        self:oQueryBaixas:SetIn(nParam++, self:oFilterBranches["SE1"][1])
    endIf

    self:oQueryBaixas:SetString(nParam++,self:cInitialprefix)
    self:oQueryBaixas:SetString(nParam++,self:cFinalprefix)

    if !self:lListarOutrasMoedas
        self:oQueryBaixas:SetNumeric(nParam++,self:nCurrency)
    endIf

    self:oQueryBaixas:SetString(nParam++, ' ')

return


//-------------------------------------------------------------------
/*{Protheus.doc} getListaDePedidosDeVendas

    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getListaDePedidosDeVendas() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local cQuery := "" as Character
    local nParam := 1  as numeric

    cQuery := " SELECT "
    cQuery += "  C6_FILIAL E1_FILIAL, C6_ITEM, C6_NUM, C6_TES, C6_QTDENT, "
    cQuery += "  C6_QTDVEN, C6_PRCVEN, C6_VALOR, C6_CLI, C6_LOJA, "
    cQuery += "  A1_NOME, C5_EMISSAO, C6_ENTREG, C5_MOEDA, C5_TXMOEDA "

    cQuery += self:cSelectVendas

    cQuery += " FROM " + RetSQLName("SC6") + " SC6 "
    cQuery += " INNER JOIN " + RetSQLName("SC5") + " SC5 ON " + FWJoinFilial("SC5", "SC6")  + " AND "
    cQuery += "     SC5.C5_NUM = SC6.C6_NUM AND "
    cQuery += "     SC5.C5_CLIENTE = SC6.C6_CLI AND "
    cQuery += "     SC5.C5_LOJACLI = SC6.C6_LOJA AND "
    cQuery += "     SC5.D_E_L_E_T_ = ? "
    cQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1 ON " + FWJoinFilial("SA1","SC6") + " AND "
    cQuery += "     SA1.A1_COD = SC6.C6_CLI AND "
    cQuery += "     SA1.A1_LOJA = SC6.C6_LOJA AND "
    cQuery += "     SA1.D_E_L_E_T_ = ? "
    cQuery += " INNER JOIN " + RetSQLName("SF4") + " SF4 ON " + FWJoinFilial("SF4","SC6") + " AND "
    cQuery += "     SF4.F4_CODIGO = SC6.C6_TES AND "
    cQuery += "     SF4.F4_DUPLIC = ? AND "
    cQuery += "     SF4.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    if self:lEmptyMultiBranches
        cQuery += " SC6.C6_FILIAL = ? AND "
    else
        cQuery += " SC6.C6_FILIAL IN (?) AND "
    endIf
    cQuery += " C6_QTDENT < C6_QTDVEN AND "

    if !self:lListarOutrasMoedas
        cQuery += " SC5.C5_MOEDA = ? AND "
    endIf

    cQuery += self:cWhereVendas

    cQuery += " SC6.D_E_L_E_T_ = ? "

    cQuery += self:filterWithRelationOfSC5()

    cQuery := ChangeQuery(cQuery)

    self:oQueryVendas := FWExecStatement():New(cQuery)

    self:oQueryVendas:SetString(nParam++, ' ')
    self:oQueryVendas:SetString(nParam++, ' ')
    self:oQueryVendas:SetString(nParam++, 'S')
    self:oQueryVendas:SetString(nParam++, ' ')

    if self:lEmptyMultiBranches
        self:oQueryVendas:SetString(nParam++, self:oFilterBranches["SC6"][1])
    else
        self:oQueryVendas:SetIn(nParam++, self:oFilterBranches["SC6"][1])
    endIf   

    if !self:lListarOutrasMoedas
        self:oQueryVendas:SetNumeric(nParam++,self:nCurrency)
    endIf

    self:oQueryVendas:SetString(nParam++, ' ')

return


//-------------------------------------------------------------------
/*{Protheus.doc} setDataListaDeTitulos
Adiciona dados dos títulos em aberto no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 26/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setDataListaDeTitulos(oFilter as object) as object class GeneralAccountsReceivableStatusReportSmartViewBusinessObject

    Local cTipoVenda    := "" as character
    Local cTipoTitulo   := "" as character
    Local cCategoria    := "" as character
    Local cAgingReceber := "" as character
    Local nDias         := 0  as numeric
    Local nSaldo        := 0  as numeric
    Local nValor        := 0  as numeric

    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        If (StoD((self:cAlias)->E1_VENCREA) - StoD((self:cAlias)->E1_EMISSAO)) <= 1
            cTipoVenda := STR0028 //"À vista"
        Else
            cTipoVenda := STR0029 //"À prazo"
        EndIf

        If (self:cAlias)->E1_SALDO > 0
            If dDatabase <= StoD((self:cAlias)->E1_VENCREA) .OR. (self:cAlias)->E1_TIPO $ MVRECANT
                cCategoria := STR0012 //"TITULOS A VENCER"
                nDias := (StoD((self:cAlias)->E1_VENCREA) - dDatabase)
            Else
                cCategoria := STR0013 //"TITULOS VENCIDOS"
                nDias := (dDatabase - StoD((self:cAlias)->E1_VENCREA))
            EndIf

            If StoD((self:cAlias)->E1_EMISSAO) <= dDatabase            
                cTipoTitulo := self:getTypeOfBillText((self:cAlias)->E1_TIPO)
            Else
                cTipoTitulo := "-"
            Endif

            cAgingReceber := self:getAgingText(nDias)

        EndIf

        nValor := xMoeda((self:cAlias)->E1_VALOR,(self:cAlias)->E1_MOEDA,self:nCurrency,(self:cAlias)->E1_EMISSAO,self:nDecimals,(self:cAlias)->E1_TXMOEDA)
        nSaldo := xMoeda((self:cAlias)->E1_SALDO,(self:cAlias)->E1_MOEDA,self:nCurrency,(self:cAlias)->E1_EMISSAO,self:nDecimals,(self:cAlias)->E1_TXMOEDA)


        If (self:cAlias)->E1_TIPO $ (MVRECANT + "|" + MV_CRNEG)
            nValor := nValor*-1
            nSaldo := nSaldo*-1
        EndIf

        self:jData := {"E1_FILIAL":     AllTrim((self:cAlias)->E1_FILIAL),;
            "E1_CLIENTE":   AllTrim((self:cAlias)->E1_CLIENTE),;
            "E1_LOJA":      AllTrim((self:cAlias)->E1_LOJA),;
            "E1_NOMCLI":    AllTrim((self:cAlias)->E1_NOMCLI),;
            "E1_PREFIXO":   AllTrim((self:cAlias)->E1_PREFIXO),;
            "E1_NUM":       AllTrim((self:cAlias)->E1_NUM),;
            "E1_PARCELA":   AllTrim((self:cAlias)->E1_PARCELA),;
            "E1_TIPO":      AllTrim((self:cAlias)->E1_TIPO),;
            "E1_EMISSAO":   self:varToTimeStamp( (self:cAlias)->E1_EMISSAO ),;
            "E1_VENCREA":   self:varToTimeStamp( (self:cAlias)->E1_VENCREA ),;
            "E1_BAIXA":     Nil,;
            "E1_VALOR":     nValor,;
            "E1_SALDO":     nSaldo,;
            "FK1_VALOR":    0,;
            "E1_NATUREZ":   MascNat((self:cAlias)->E1_NATUREZ),;
            "ED_DESCRIC":   AllTrim((self:cAlias)->ED_DESCRIC),;
            "TIPOVENDA":   cTipoVenda,;
            "TIPOTITULO":   cTipoTitulo,;
            "CATEG_TITULO": cCategoria,;
            "AGING_TITULO": cAgingReceber,;
            "VALOR_CORRECAO":0,;
            "VALOR_ACESSORIO":0}

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )

    EndDo

Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} setDataListaDeBaixas
Adiciona dados das baixas no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setDataListaDeBaixas(oFilter as object) as object class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    Local cTipoVenda := "" as character
    Local cTipoTitulo := "" as character
    Local cCategoria  := "" as character
    Local cAgingReceber := "" as character
    Local nSaldo      := 0  as numeric
    Local nValor      := 0  as numeric
    Local nBaixa      := 0  as numeric
    Local nMoeda      := 0  as numeric
    Local nTxMoeda    := 0  as numeric
    
    (self:cAlias)->(DBGoTop())

    cCategoria := STR0027 //"VALORES BAIXADOS"

    While !(self:cAlias)->(Eof())

        If (StoD((self:cAlias)->E1_VENCREA) - StoD((self:cAlias)->E1_EMISSAO)) <= 1
            cTipoVenda := STR0028 //"À vista"
        Else
            cTipoVenda := STR0029 //"À prazo"
        EndIf

        cTipoTitulo := self:getTypeOfBillText((self:cAlias)->E1_TIPO)

        cAgingReceber := "-"

        nMoeda := Val(AllTrim((self:cAlias)->FK1_MOEDA))
        If (self:cAlias)->E1_TXMOEDA == 0
            nTxMoeda := RecMoeda((self:cAlias)->E1_EMISSAO, (self:cAlias)->E1_MOEDA)
        Else 
            nTxMoeda := (self:cAlias)->E1_TXMOEDA
        EndIf

        If nMoeda == (self:cAlias)->E1_MOEDA
            nTxMoeda := (self:cAlias)->FK1_TXMOED
        EndIf

        nValor := xMoeda((self:cAlias)->E1_VALOR,(self:cAlias)->E1_MOEDA,self:nCurrency,(self:cAlias)->E1_EMISSAO,self:nDecimals,nTxMoeda)
        nSaldo := xMoeda((self:cAlias)->E1_SALDO,(self:cAlias)->E1_MOEDA,self:nCurrency,(self:cAlias)->E1_EMISSAO,self:nDecimals,nTxMoeda)
        nBaixa := xMoeda((self:cAlias)->FK1_VALOR,nMoeda,self:nCurrency,(self:cAlias)->FK1_DATA ,self:nDecimals,(self:cAlias)->FK1_TXMOED)

        If (self:cAlias)->E1_TIPO $ (MVRECANT + "|" + MV_CRNEG)
            nValor := nValor*-1
            nSaldo := nSaldo*-1
            nBaixa := nBaixa*-1
        EndIf

        self:jData := {"E1_FILIAL":     AllTrim((self:cAlias)->E1_FILIAL),;
            "E1_CLIENTE":     AllTrim((self:cAlias)->E1_CLIENTE),;
            "E1_LOJA":        AllTrim((self:cAlias)->E1_LOJA),;
            "E1_NOMCLI":      AllTrim((self:cAlias)->E1_NOMCLI),;
            "E1_PREFIXO":     AllTrim((self:cAlias)->E1_PREFIXO),;
            "E1_NUM":         AllTrim((self:cAlias)->E1_NUM),;
            "E1_PARCELA":     AllTrim((self:cAlias)->E1_PARCELA),;
            "E1_TIPO":        AllTrim((self:cAlias)->E1_TIPO),;
            "E1_EMISSAO":     self:varToTimeStamp( (self:cAlias)->E1_EMISSAO ),;
            "E1_VENCREA":     self:varToTimeStamp( (self:cAlias)->E1_VENCREA ),;
            "E1_BAIXA":       self:varToTimeStamp( (self:cAlias)->FK1_DATA ),;
            "E1_VALOR":       nValor,;
            "E1_SALDO":       nSaldo,;
            "FK1_VALOR":      nBaixa,;
            "E1_NATUREZ":     MascNat((self:cAlias)->E1_NATUREZ),;
            "ED_DESCRIC":     AllTrim((self:cAlias)->ED_DESCRIC),;
            "TIPOVENDA":     cTipoVenda,;
            "TIPOTITULO":     cTipoTitulo,;
            "CATEG_TITULO":   cCategoria,;
            "AGING_TITULO":   cAgingReceber,;
            "VALOR_CORRECAO": (self:cAlias)->VALOR_CORRECAO,;
            "VALOR_ACESSORIO":(self:cAlias)->VALOR_ACESSORIO}

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )
    EndDo


Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} setDataListaDePedidosDeVendas
Adiciona dados dos pedidos de vendas no objeto de negócios

@return character: cQuery

@author Fábio Andrade
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method setDataListaDePedidosDeVendas(oFilter as object) as object class GeneralAccountsReceivableStatusReportSmartViewBusinessObject

    Local cTipoVenda := "" as character
    Local cTipoTitulo := "" as character
    Local cCategoria  := "" as character
    Local cAgingReceber := "" as character
    Local nDias       := 0  as numeric
    Local nVlrSaldo   := 0  as numeric
    Local nSaldo      := 0  as numeric
    Local nValor      := 0  as numeric

    (self:cAlias)->(DBGoTop())

    cTipoVenda := STR0037 //"Pedido de Venda"    

    While !(self:cAlias)->(Eof())       

        If StoD((self:cAlias)->C6_ENTREG) < dDataBase
            cCategoria := STR0038 //"PEDIDOS DE VENDA ATRASADOS"
            nDias := dDataBase - StoD((self:cAlias)->C6_ENTREG)
        Else
            cCategoria := STR0039 //"PEDIDOS DE VENDA ADIANTADOS"
            nDias := StoD((self:cAlias)->C6_ENTREG) - dDataBase
        EndIf

        cAgingReceber := self:getAgingText(nDias)

        If (self:cAlias)->C6_QTDENT > 0
            cTipoTitulo := STR0040 //"Parcialmente atendido"
        Else
            cTipoTitulo := STR0041 //"Não atendido"
        EndIf

        nVlrSaldo := ((self:cAlias)->C6_QTDVEN - (self:cAlias)->C6_QTDENT) * (self:cAlias)->C6_PRCVEN
        
        nValor := xMoeda((self:cAlias)->C6_VALOR,(self:cAlias)->C5_MOEDA,self:nCurrency,(self:cAlias)->C5_EMISSAO,self:nDecimals,(self:cAlias)->C5_TXMOEDA)
        nSaldo := xMoeda(nVlrSaldo,(self:cAlias)->C5_MOEDA,self:nCurrency,(self:cAlias)->C5_EMISSAO,self:nDecimals,(self:cAlias)->C5_TXMOEDA)       

        self:jData := {"E1_FILIAL":     AllTrim((self:cAlias)->E1_FILIAL),;
            "E1_CLIENTE":   AllTrim((self:cAlias)->C6_CLI),;
            "E1_LOJA":      AllTrim((self:cAlias)->C6_LOJA),;
            "E1_NOMCLI":    AllTrim((self:cAlias)->A1_NOME),;
            "E1_PREFIXO":   "",;
            "E1_NUM":       AllTrim((self:cAlias)->C6_NUM) + "-" + AllTrim((self:cAlias)->C6_ITEM),;
            "E1_PARCELA":   "",;
            "E1_TIPO":      "",;
            "E1_EMISSAO":   self:varToTimeStamp( (self:cAlias)->C5_EMISSAO ),;
            "E1_VENCREA":   self:varToTimeStamp( (self:cAlias)->C6_ENTREG ),;
            "E1_BAIXA":     Nil,;
            "E1_VALOR":     nValor,;
            "E1_SALDO":     nSaldo,;
            "FK1_VALOR":    0,;
            "E1_NATUREZ":   "",;
            "ED_DESCRIC":   "",;
            "TIPOVENDA":   cTipoVenda,;
            "TIPOTITULO":   cTipoTitulo,;
            "CATEG_TITULO": cCategoria,;
            "AGING_TITULO": cAgingReceber,;
            "VALOR_CORRECAO":0,;
            "VALOR_ACESSORIO":0}

        self:processAndAppend()

        (self:cAlias)->( dbSkip() )
    EndDo

Return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    Carrega os parâmetros do filtro para as propriedades da classe.
    @author Guilherme de Paula Santos
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject

    self:lListarOutrasMoedas := .F.
    self:nCurrency := 1

    if self:oCurrentFilter != NIL
        self:nListpostingsby     := MV_PAR01
        self:dIssueDateFrom      := MV_PAR02
        self:dIssueDateTo        := MV_PAR03
        self:nCurrency           := self:getCurrencyValue(MV_PAR04)
        self:lListarOutrasMoedas := MV_PAR05 == 1
        self:cInitialprefix      := MV_PAR06
        self:cFinalprefix        := MV_PAR07
        self:dFromDueDate        := MV_PAR08
        self:dToDueDate          := MV_PAR09        
    endIf
return


//-------------------------------------------------------------------
/*{Protheus.doc} getAgingText
    @author guilherme.sordi@totvs.com.br
    @since 30/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getAgingText(nDias as numeric) as character class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local cAgingText := "" as character
    default nDias := 0

    If nDias <= 15
        cAgingText := STR0022 //"Até 15 Dias"
    ElseIf nDias > 15 .And. nDias <= 30
        cAgingText := STR0023 //"De 16 a 30 Dias"
    ElseIf nDias > 30 .And. nDias <= 60
        cAgingText := STR0024 //"De 31 a 60 Dias"
    ElseIf nDias > 60 .And. nDias <= 90
        cAgingText := STR0025 //"De 61 a 90 Dias"
    Else
        cAgingText := STR0026 //"Acima de 90 Dias"
    Endif
return cAgingText


//-------------------------------------------------------------------
/*{Protheus.doc} getTypeOfBillText
    @author guilherme.sordi@totvs.com.br
    @since 30/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getTypeOfBillText(cType as character) as character class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local cTypeOfBillText := "" as character
    default cType := ""

    Do Case
        Case cType $ MVDUPLIC 
            cTypeOfBillText := STR0014 //Duplicatas

        Case cType $ MVNOTAFIS
            cTypeOfBillText := STR0015 //Notas fiscais

        Case cType $ MVRECANT
            cTypeOfBillText := STR0019 //Recebimento antecipado

        Case cType$ MV_CRNEG
            cTypeOfBillText := STR0020 //Nota de Debito

        OtherWise
            cTypeOfBillText := STR0021 //Outros
    EndCase
return cTypeOfBillText


//-------------------------------------------------------------------
/*{Protheus.doc} changeCanFilter
@author guilhermed.santos
@since 10/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method changeCanFilter() class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local aSchemaProperties := {} as array
    local cProperty         := "" as character
    local nX                := 1  as numeric

    aSchemaProperties := self:oSchema:getProperties()

    for nX := 1 to Len(aSchemaProperties)
        cProperty := aSchemaProperties[nX]:getName()
        if !self:jRelationSE1SC5:hasProperty(cProperty)
            self:setCanFilter(cProperty,.F.)
        endIf
    next nX
return


//-------------------------------------------------------------------
/*{Protheus.doc} filterWithRelationOfSC5
@author guilhermed.santos
@since 10/01/2024
@version 12.1.2310
*/
//-------------------------------------------------------------------
method filterWithRelationOfSC5() as character class GeneralAccountsReceivableStatusReportSmartViewBusinessObject
    local aRelationProperties   := {} as array
    local cFilter               := "" as character
    local nX                    := 1  as numeric

    cFilter := self:getFilterToSQL()

    if empty(cFilter)
        return cFilter
    endIf

    aRelationProperties := self:jRelationSE1SC5:GetNames()
    
    for nX := 1 to Len(aRelationProperties)
        cFilter := replace(cFilter,aRelationProperties[nX],self:jRelationSE1SC5:GetJSonObject(aRelationProperties[nX]))
    next nX

return cFilter
