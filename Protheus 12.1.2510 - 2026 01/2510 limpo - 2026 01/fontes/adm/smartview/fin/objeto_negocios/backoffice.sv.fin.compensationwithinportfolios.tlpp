#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.compensationwithinportfolios.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="FK1,FK2,SE1,SE2,SA1,SA2",;
    customTables="FK1,FK2,SE1,SE2,SA1,SA2", name="Compensação entre carteiras", country="ALL")

/*/{Protheus.doc} CompensationWithinPortfoliosSmartViewBusinessObject
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
class CompensationWithinPortfoliosSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data cCompensationNumberFrom as character
    protected data cCompensationNumberTo as character
    protected data dCompensationFrom as date
    protected data dCompensationTo as date
    protected data nCurrency as numeric
    protected data lConvertCurrencyRate as logical
    protected data lOnlyActive as logical 
    protected data lOnlyCancelled as logical

    protected data nCurrencyFrom as numeric
    protected data dConversion as date
    protected data nContratctedCurrencyRate as numeric

    protected method mySetSchema()
    protected method initializeBusinessObject()
    protected method loadStatement()
    protected method handleData()
    protected method loadParameters()
    protected method loadDefaultParameters()
    protected method convertCurrency() as numeric
    protected method convertBillCurrency() as numeric
    protected method convertPostCurrency() as numeric
endclass


/*/{Protheus.doc} new
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method new() class CompensationWithinPortfoliosSmartViewBusinessObject
    _Super:new()

    self:initializeBusinessObject()

    self:setAllowSmartViewFilter(.F.)
    self:setMainTable("SE2")
    self:setBranchFilter(.T., .T.)

    self:nCurrencyFrom := 1
    self:dConversion := cToD("  /  /    ")
    self:nContratctedCurrencyRate := 0
return self

/*/{Protheus.doc}
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method initializeBusinessObject() class CompensationWithinPortfoliosSmartViewBusinessObject    
    self:setDisplayName(STR0001)  //Compensação entre carteiras 
    self:setDescription(STR0001)  //Compensação entre carteiras     
    self:setPergunte("FINT017")
return


/*/{Protheus.doc} mySetSchema
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method mySetSchema() class CompensationWithinPortfoliosSmartViewBusinessObject

    local aFieldsSE2 := {}  as array
    local aFieldsFK2 := {}  as array
    local aFieldsSA2 := {}  as array

    aFieldsSE2 := { "E2_FILIAL", "E2_PREFIXO", "E2_NUM", "E2_PARCELA", "E2_TIPO", "E2_FORNECE", "E2_LOJA", "E2_VALOR", "E2_JUROS", "E2_MULTA", "E2_DESCONT" }
    aFieldsFK2 := { "FK2_VALOR", "FK2_RECPAG", "FK2_IDPROC", "FK2_DATA" }
    aFieldsSA2 := { "A2_NOME", "A2_NREDUZ" }

    self:finAddProperty("CLIFOR_COD" , STR0002, "string", "A2_COD" ) //"Código cliente/fornecedor"
    self:finAddProperty("CLIFOR_LOJA" , STR0003, "string", "A2_LOJA" ) //"Loja"
    self:finAddProperty("VA" , STR0004, "number" ) //Valores acessórios"
    self:finAddProperty("COMP_STATUS" , STR0007, "string" ) //"Situação da compensação"
    self:finAddProperty("CLIFOR_DATA" , STR0010, "string" ) //"Dados cliente/fornecedor"

    self:oSchema:aliasToSchema("SE2", aFieldsSE2 )
    self:oSchema:aliasToSchema("FK2", aFieldsFK2 )
    self:oSchema:aliasToSchema("SA2", aFieldsSA2 )
   
    FwFreeArray(aFieldsSE2)
    FwFreeArray(aFieldsFK2)
    FwFreeArray(aFieldsSA2)

return


/*/{Protheus.doc} loadStatement
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method loadStatement() class CompensationWithinPortfoliosSmartViewBusinessObject
    local cQuery := "" as character
    local nParamOrder := 1 as numeric

    self:loadCustomFieldsPayAndRec()

    cQuery += "SELECT E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA, E2_VALOR, E2_EMISSAO, E2_MOEDA, E2_TXMOEDA, "
    cQuery += "FK2.FK2_VALOR, FK2.FK2_RECPAG, "
    cQuery += "FK2.FK2_IDPROC, FK2.FK2_DATA, FK2.FK2_MOEDA, FK2.FK2_TXMOED, A2_COD, A2_LOJA, A2_NOME, A2_NREDUZ, "

    cQuery += self:cSelectCustomFieldsPay

    cQuery += " CASE WHEN FK2EST.FK2_IDFK2 IS NULL THEN '1' ELSE '2' END ATIVO "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK2.FK2_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC = ? "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS JUROS "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK2.FK2_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC IN (?)  "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS MULTA " 

    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK2.FK2_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC IN  (?) "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS DESCONTO "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK2.FK2_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK2.FK2_IDFK2 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC = ? "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS VA " 

    cQuery += "FROM " + RetSQLName("FK2") + " FK2 "

    cQuery += "LEFT JOIN " + RetSQLName("FK2") + " FK2EST "
    cQuery += "ON " + FWJoinFilial("FK2","FK2","FK2","FK2EST") + " 
    cQuery += "AND FK2.FK2_IDDOC = FK2EST.FK2_IDDOC "
    cQuery += "AND FK2.FK2_SEQ = FK2.FK2_SEQ "
    cQuery += "AND FK2EST.FK2_TPDOC = ? "
    cQuery += "AND FK2EST.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("FK7") + " FK7  "
    cQuery += "ON FK2.FK2_IDDOC = FK7_IDDOC "
    cQuery += "AND FK7.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("SE2") + " SE2 "
    cQuery += "ON FK7_FILTIT = E2_FILIAL " 
    cQuery += "AND FK7_PREFIX = E2_PREFIXO "
    cQuery += "AND FK7_NUM = E2_NUM "
    cQuery += "AND FK7_PARCEL = E2_PARCELA "
    cQuery += "AND FK7_TIPO = E2_TIPO "
    cQuery += "AND FK7_CLIFOR = E2_FORNECE "
    cQuery += "AND FK7_LOJA = E2_LOJA "
    cQuery += "AND FK7_ALIAS = ? "
    cQuery += "AND SE2.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("SA2") + " SA2 "
    cQuery += "ON " + FWJoinFilial("SA2","SE2") + " 
    cQuery += "AND E2_FORNECE = A2_COD "
    cQuery += "AND E2_LOJA = A2_LOJA "
    cQuery += "AND SA2.D_E_L_E_T_ = ? "

    cQuery += "WHERE "

    cQuery += self:getSQLBranchFilter()

    cQuery += "AND FK2.FK2_IDPROC BETWEEN ? AND ? "
    cQuery += "AND FK2.FK2_MOTBX = ? "
    cQuery += "AND FK2.FK2_DATA BETWEEN ? AND ? "

    if !self:lConvertCurrencyRate
        cQuery += "AND SE2.E2_MOEDA = ? "
    endIf

    cQuery += "AND FK2.D_E_L_E_T_ = ? "

    if self:lOnlyActive
        cQuery += "AND FK2EST.FK2_IDFK2 IS NULL "
    endIf

    if self:lOnlyCancelled
        cQuery += "AND FK2EST.FK2_IDFK2 IS NOT NULL "
    endIf

    cQuery += "UNION "

    cQuery += "SELECT E1_FILIAL, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, E1_CLIENTE, E1_LOJA, E1_VALOR, E1_EMISSAO, E1_MOEDA, E1_TXMOEDA, "
    cQuery += "FK1.FK1_VALOR, FK1.FK1_RECPAG, "
    cQuery += "FK1.FK1_IDPROC, FK1.FK1_DATA, FK1.FK1_MOEDA, FK1.FK1_TXMOED, A1_COD, A1_LOJA, A1_NOME, A1_NREDUZ, "

    cQuery += self:cSelectCustomFieldsRec
    
    cQuery += " CASE WHEN FK1EST.FK1_IDFK1 IS NULL THEN '1' ELSE '2' END ATIVO "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK1.FK1_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK1.FK1_IDFK1 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC = ? "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS JUROS "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK1.FK1_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK1.FK1_IDFK1 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC IN (?) "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS MULTA " 

    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK1.FK1_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK1.FK1_IDFK1 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC IN (?) "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS DESCONTO "
    
    cQuery += ", ( SELECT COALESCE(SUM(FK6.FK6_VALMOV),0)  "
    cQuery += " FROM " + RetSQLName("FK6") + " FK6 "
    cQuery += " WHERE FK6.FK6_FILIAL = FK1.FK1_FILIAL "
    cQuery += " AND FK6.FK6_IDORIG = FK1.FK1_IDFK1 "
    cQuery += " AND FK6.FK6_TABORI = ? "
    cQuery += " AND FK6.FK6_TPDOC = ? "
    cQuery += " AND FK6.D_E_L_E_T_ = ? ) AS VA " 

    cQuery += "FROM " + RetSQLName("FK1") + " FK1 "    

    cQuery += "LEFT JOIN " + RetSQLName("FK1") + " FK1EST "
    cQuery += "ON " + FWJoinFilial("FK1","FK1","FK1","FK1EST") + " 
    cQuery += "AND FK1.FK1_IDDOC = FK1EST.FK1_IDDOC "
    cQuery += "AND FK1.FK1_SEQ = FK1.FK1_SEQ "
    cQuery += "AND FK1EST.FK1_TPDOC = ? "
    cQuery += "AND FK1EST.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("FK7") + " FK7  "
    cQuery += "ON FK1.FK1_IDDOC = FK7.FK7_IDDOC "
    cQuery += "AND FK7.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("SE1") + " SE1 "
    cQuery += "ON FK7_FILTIT = E1_FILIAL " 
    cQuery += "AND FK7_PREFIX = E1_PREFIXO "
    cQuery += "AND FK7_NUM = E1_NUM "
    cQuery += "AND FK7_PARCEL = E1_PARCELA "
    cQuery += "AND FK7_TIPO = E1_TIPO "
    cQuery += "AND FK7_CLIFOR = E1_CLIENTE "
    cQuery += "AND FK7_LOJA = E1_LOJA "
    cQuery += "AND FK7_ALIAS = ? "
    cQuery += "AND SE1.D_E_L_E_T_ = ? "

    cQuery += "JOIN " + RetSQLName("SA1") + " SA1 "
    cQuery += "ON " + FWJoinFilial("SA1","SE1") + " 
    cQuery += "AND E1_CLIENTE = A1_COD "
    cQuery += "AND E1_LOJA = A1_LOJA "
    cQuery += "AND SA1.D_E_L_E_T_ = ? "

    cQuery += "WHERE 
    
    If self:lEmptyMultiBranches
        cQuery += "FK1.FK1_FILIAL = ?  "
    Else 
        cQuery += "FK1.FK1_FILIAL IN (?)  "
    EndIf 

    cQuery += "AND FK1.FK1_IDPROC BETWEEN ? AND ? "
    cQuery += "AND FK1.FK1_MOTBX = ? "
    cQuery += "AND FK1.FK1_DATA BETWEEN ? AND ? "

    if !self:lConvertCurrencyRate
        cQuery += "AND SE1.E1_MOEDA = ? "
    endIf

    cQuery += "AND FK1.D_E_L_E_T_ = ? "

    if self:lOnlyActive
        cQuery += "AND FK1EST.FK1_IDFK1 IS NULL "
    endIf

    if self:lOnlyCancelled
        cQuery += "AND FK1EST.FK1_IDFK1 IS NOT NULL "
    endIf

    cQuery := changeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetString(nParamOrder++, "FK2")
    self:oStatement:SetString(nParamOrder++, "JR")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK2")
    self:oStatement:SetIn(nParamOrder++, {'MT', 'M2'})
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK2")
    self:oStatement:SetIn(nParamOrder++, {'DC', 'D2'})
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK2")
    self:oStatement:SetString(nParamOrder++, "VA")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "ES")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "SE2")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, " ")
    self:setStatementBranchFilter(@nParamOrder)

    self:oStatement:setString(nParamOrder++, self:cCompensationNumberFrom)
    self:oStatement:setString(nParamOrder++, self:cCompensationNumberTo)
    self:oStatement:SetString(nParamOrder++, "CEC")
    self:oStatement:setDate(nParamOrder++, self:dCompensationFrom)
    self:oStatement:setDate(nParamOrder++, self:dCompensationTo)
    
    if !self:lConvertCurrencyRate
        self:oStatement:SetNumeric(nParamOrder++, self:nCurrency)
    endIf

    self:oStatement:SetString(nParamOrder++, " ")

    self:oStatement:SetString(nParamOrder++, "FK1")
    self:oStatement:SetString(nParamOrder++, "JR")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK1")
    self:oStatement:SetIn(nParamOrder++, {'MT', 'M2'})
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK1")
    self:oStatement:SetIn(nParamOrder++, {'DC', 'D2'})
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "FK2")
    self:oStatement:SetString(nParamOrder++, "VA")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "ES")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, "SE1")
    self:oStatement:SetString(nParamOrder++, " ")
    self:oStatement:SetString(nParamOrder++, " ")

    If self:lEmptyMultiBranches
        self:oStatement:SetString(nParamOrder++, self:getAliasBranchFilter("FK1")[1])
    Else 
        self:oStatement:SetIn(nParamOrder++, self:getAliasBranchFilter("FK1"))
    EndIf

    self:oStatement:setString(nParamOrder++, self:cCompensationNumberFrom)
    self:oStatement:setString(nParamOrder++, self:cCompensationNumberTo)

    self:oStatement:SetString(nParamOrder++, "CEC")

    self:oStatement:setDate(nParamOrder++, self:dCompensationFrom)
    self:oStatement:setDate(nParamOrder++, self:dCompensationTo)
    
    if !self:lConvertCurrencyRate
        self:oStatement:SetNumeric(nParamOrder++, self:nCurrency)
    endIf

    self:oStatement:SetString(nParamOrder++, " ")
       
return 

/*/{Protheus.doc} handleData
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method handleData() class CompensationWithinPortfoliosSmartViewBusinessObject
    local cCliFor := "" as character
    local cPortfolio := "" as character
    local cStatus := "" as character

    cCliFor := allTrim((self:cAlias)->E2_FORNECE) + '/' + allTrim((self:cAlias)->E2_LOJA) + " - " + allTrim((self:cAlias)->A2_NOME) + " (" + allTrim((self:cAlias)->A2_NREDUZ) + ")"
    
    if (self:cAlias)->FK2_RECPAG == 'R'
        cPortfolio := STR0006 //"Receber"
    else
        cPortfolio := STR0005 //"Pagar"
    endIf

    if (self:cAlias)->ATIVO == '1'
        cStatus := STR0008 //"Ativo"
    else
        cStatus := STR0009 //"Cancelado"
    endIf

    self:jData := {;
        "E2_FILIAL": (self:cAlias)->E2_FILIAL, ; 
        "E2_PREFIXO": (self:cAlias)->E2_PREFIXO, ; 
        "E2_NUM": (self:cAlias)->E2_NUM, ; 
        "E2_PARCELA": (self:cAlias)->E2_PARCELA, ; 
        "E2_TIPO": (self:cAlias)->E2_TIPO, ; 
        "E2_FORNECE": (self:cAlias)->E2_FORNECE, ; 
        "E2_LOJA": (self:cAlias)->E2_LOJA, ; 
        "E2_VALOR": self:convertBillCurrency( (self:cAlias)->E2_VALOR ), ; 
        "FK2_VALOR": self:convertPostCurrency( (self:cAlias)->FK2_VALOR ), ; 
        "FK2_RECPAG": cPortfolio, ; 
        "FK2_IDPROC": (self:cAlias)->FK2_IDPROC, ; 
        "FK2_DATA": self:varToTimeStamp((self:cAlias)->FK2_DATA), ; 
        "A2_NOME": allTrim((self:cAlias)->A2_NOME), ; 
        "A2_NREDUZ": allTrim((self:cAlias)->A2_NREDUZ), ;
        "CLIFOR_COD": (self:cAlias)->A2_COD, ;
        "CLIFOR_LOJA": (self:cAlias)->A2_LOJA, ;
        "E2_JUROS": self:convertBillCurrency( (self:cAlias)->JUROS ), ;
        "E2_MULTA": self:convertBillCurrency( (self:cAlias)->MULTA ), ;
        "E2_DESCONT": self:convertBillCurrency( (self:cAlias)->DESCONTO ), ;
        "VA": (self:cAlias)->VA, ;
        "COMP_STATUS": cStatus, ;
        "CLIFOR_DATA": cCliFor;
    }
 
return

/*/{Protheus.doc} loadParameters
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method loadParameters() class CompensationWithinPortfoliosSmartViewBusinessObject   
    if self:oCurrentFilter != NIL      
        self:cCompensationNumberFrom := MV_PAR01
        self:cCompensationNumberTo := MV_PAR02
        self:dCompensationFrom := MV_PAR03
        self:dCompensationTo := MV_PAR04
        self:lOnlyActive := MV_PAR05 == 2
        self:lOnlyCancelled := MV_PAR05 == 3
        self:nCurrency := self:getCurrencyValue(MV_PAR06)
        self:lConvertCurrencyRate := MV_PAR07 == 2
    endIf
return

/*/{Protheus.doc} loadDefaultParameters
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method loadDefaultParameters() class CompensationWithinPortfoliosSmartViewBusinessObject
    self:cCompensationNumberFrom := ""
    self:cCompensationNumberTo := ""
    self:dCompensationFrom := cToD("  /  /    ")
    self:dCompensationTo := cToD("  /  /    ")
    self:nCurrency := 1
    self:lConvertCurrencyRate := .F.
    self:lOnlyActive := .F.
    self:lOnlyCancelled := .F.
return


/*/{Protheus.doc} convertCurrency
    Converte o valor da moeda. Será chamado por convertBillCurrency ou convertPostCurrency após parametrização pelo título ou pela baixa.
    @author guilherme.sordi@totvs.com.br
    @since 11/10/2023
    @version 12.1.2210
*/
method convertCurrency(nValue as numeric) as numeric class CompensationWithinPortfoliosSmartViewBusinessObject    
    local nCurrencyRateFrom := 0 as numeric
    local nCurrencyRateTo := 0 as numeric

    if self:nCurrencyFrom == 1
        nCurrencyRateTo := self:nContratctedCurrencyRate
    else
        nCurrencyRateFrom := self:nContratctedCurrencyRate
    endIf

    nValue := xMoeda(nValue, self:nCurrencyFrom, self:nCurrency, self:dConversion, self:nDecimals, nCurrencyRateFrom, nCurrencyRateTo)
return nValue


/*/{Protheus.doc} convertBillCurrency
    Define os dados do título como parâmetros de conversão e retorna o valor convertido.
    @author guilherme.sordi@totvs.com.br
    @since 18/10/2023
    @version 12.1.2210
*/
method convertBillCurrency(nValue as numeric) as numeric class CompensationWithinPortfoliosSmartViewBusinessObject
    
    self:nCurrencyFrom := (self:cAlias)->E2_MOEDA
    self:dConversion := (self:cAlias)->E2_EMISSAO
    self:nContratctedCurrencyRate := (self:cAlias)->E2_TXMOEDA

return self:convertCurrency(nValue)


/*/{Protheus.doc} convertPostCurrency
    Define os dados da baixa como parâmetros de conversão e retorna o valor convertido.
    @author guilherme.sordi@totvs.com.br
    @since 18/10/2023
    @version 12.1.2210
*/
method convertPostCurrency(nValue as numeric) as numeric class CompensationWithinPortfoliosSmartViewBusinessObject
  
    self:nCurrencyFrom := val((self:cAlias)->FK2_MOEDA)
    self:dConversion := (self:cAlias)->FK2_DATA
    self:nContratctedCurrencyRate := (self:cAlias)->FK2_TXMOED

return self:convertCurrency(nValue)
