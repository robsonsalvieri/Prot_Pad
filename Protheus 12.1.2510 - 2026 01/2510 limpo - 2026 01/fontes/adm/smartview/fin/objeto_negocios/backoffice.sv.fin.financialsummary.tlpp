#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.financialsummary.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SE2,SE3,FK1,FK2,FK7", name="Resumo Financeiro", country="ALL")


//-------------------------------------------------------------------
/*/{Protheus.doc} FinancialSummarySmartViewBusinessObject

@author Fábio Henrique Andrade
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class FinancialSummarySmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oQryBillsPay as object 
    protected data oQryBillsRec as object 
    protected data oQryComission as object
    protected data oQrySettledRec as object
    protected data oQrySettledPay as object

    protected data lDailyTypePeriod as logical
    protected data lWeeklyTypePeriod as logical
    protected data lBiweeklyTypePeriod as logical
    protected data lMontlyTypePeriod as logical
    protected data nQuantityOfPeriods as numeric
    protected data cTypeInterval as character

    protected data cClientCodeFrom as character
    protected data cClientCodeTo as character
    protected data cSuplierCodeFrom as character
    protected data cSuplierCodeTo as character
    protected data cClassCodeFrom as character
    protected data cClassCodeTo as character
    protected data lListOtherCurrencies as logical

    protected data lFirstPeriodByDataBase as logical
    protected data lFirstPeriodByFirstDay as logical
    protected data dRetroactiveDate as date
    protected data lRetroactiveBalance as logical
    protected data lShowProvisionals as logical
    protected data lShowCommissions as logical

    protected data aInitialDates as array
    protected data aFinalDates as array
    protected data dInitialDate as date
    protected data dFinalDate as date

    protected data nTotalValueOfLoans as numeric
    protected data nTotalValueOfInvestments as numeric
    protected data nTotalValueInBanks as numeric
    protected data nTotalValueInCash as numeric
    protected data cActualPeriod as character

    protected data nIssueCurrency as numeric
    protected data dConversionDate as date 
    protected data nCurrency as numeric 
    protected data nCurrencyRate as numeric
    protected data nBankCurrency as numeric

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatementBillsPayable()
    protected method loadStatementBillsReceivable()
    protected method loadStatementCommissions()
    protected method loadStatementSettledPayables()
    protected method loadStatementSettledReceivables()
    protected method setArrayOfDates() 
    protected method getMonday() as date
    protected method getData() as object
    protected method loadPayableValues()
    protected method loadReceivableValues()
    protected method calcBillsPayable() as numeric
    protected method calcSettledPayable() as numeric
    protected method calcCommissions() as json
    protected method calcBillsReceivable() as numeric
    protected method calcSettledReceivable() as numeric
    protected method setDefaultParameters()
    protected method loadParameters()
    protected method convertMovementCurrency() as numeric
    protected method setLoansAndInvestmentsValues()
    protected method calculateLoansAndInvestmentsValues() as array
    protected method loadBanksAndCashValues()
    protected method getCashBalance() as numeric
    protected method getBanksBalance() as numeric
    protected method getBalance(lCash as logical) as numeric
    protected method getBanksList(lCash as logical) as array   

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Fábio Henrique Andrade
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class FinancialSummarySmartViewBusinessObject
    _Super:new()

    self:lDailyTypePeriod       := .F.
    self:lWeeklyTypePeriod      := .F.
    self:lBiweeklyTypePeriod    := .F.
    self:lMontlyTypePeriod      := .F.
    self:nQuantityOfPeriods     := 0

    self:cClientCodeFrom        := ""
    self:cClientCodeTo          := ""
    self:cSuplierCodeFrom       := ""
    self:cSuplierCodeTo         := ""
    self:cClassCodeFrom         := ""
    self:cClassCodeTo           := ""
    self:lListOtherCurrencies   := .F.

    self:lFirstPeriodByDataBase := .F.
    self:lFirstPeriodByFirstDay := .F.
    self:dRetroactiveDate       := CtoD("//")
    self:lRetroactiveBalance    := .F.
    self:lShowProvisionals      := .F.
    self:lShowCommissions       := .F.

    self:aInitialDates          := {}
    self:aFinalDates            := {}
    self:dInitialDate           := CtoD("//")
    self:dFinalDate             := CtoD("//")
    self:cActualPeriod          := ""

    self:nTotalValueOfLoans         := 0
    self:nTotalValueOfInvestments   := 0
    self:nTotalValueInBanks         := 0
    self:nTotalValueInCash          := 0

    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable('SE2')
    self:setBranchFilter(.T., .T.)

    self:initializeBusinessObject()

    self:loadParameters()
   
return self

//-------------------------------------------------------------------
    /*{Protheus.doc} initializeBusinessObject
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method initializeBusinessObject() class FinancialSummarySmartViewBusinessObject
    self:setDisplayName(STR0002) //"Resumo Financeiro"
    self:setDescription(STR0002) //"Resumo Financeiro"
    self:setPergunte("FINT032")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class FinancialSummarySmartViewBusinessObject    
    self:finAddProperty("SALDO_EM_CAIXA"          , STR0003, "number") //"Saldo em Caixa"
    self:finAddProperty("SALDO_EM_BANCOS"         , STR0004, "number") //"Saldo em Bancos"
    self:finAddProperty("SALDO_EM_APLICACOES"     , STR0005, "number") //"Saldo em Aplicações"
    self:finAddProperty("SALDO_EM_EMPRESTIMOS"    , STR0006, "number") //"Saldo em Empréstimos"
    self:finAddProperty("TOTAL_DISPONIVEL"        , STR0007, "number") //"Total do Disponível"
    self:finAddProperty("PERIODO"                 , STR0008, "string") //"Período"
    self:finAddProperty("CARTEIRA"                , STR0009, "string") //"Carteira"
    self:finAddProperty("EM_ABERTO"               , STR0010, "number") //"Em Aberto"
    self:finAddProperty("REALIZADO"               , STR0011, "number") //"Realizado"
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class FinancialSummarySmartViewBusinessObject

    local nCurrentNumber    := 0 as numeric

    if !self:validEnvironment()
        return self:oData
    endIf
    
    self:setFilter(oFilter)
    self:setMVPAR()
    self:loadParameters()
    self:loadFilterBranches( { "SE1", "SE2", "SE3" } )
    self:loadMainTableSelectedBranches()
    self:loadBranchNames()

    self:setArrayOfDates()

    self:setLoansAndInvestmentsValues()
    self:loadBanksAndCashValues()
   
    For nCurrentNumber := 1 to Len(self:aInitialDates)
        self:dInitialDate := self:aInitialDates[nCurrentNumber]
        self:dFinalDate := self:aFinalDates[nCurrentNumber]

        self:cActualPeriod :=  STRZERO(nCurrentNumber,2) + " " + self:cTypeInterval + STR0012 + DtoC(self:dInitialDate) + STR0013 + DtoC(self:dFinalDate) // " de " # " até "

        self:loadReceivableValues()
        self:loadPayableValues()
    Next
return self:oData


//-------------------------------------------------------------------
/*/{Protheus.doc} loadReceivableValues
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadReceivableValues() class FinancialSummarySmartViewBusinessObject

    local cPortfolio        := STR0014 as character //"Receber
    local nOpenValues       := 0 as numeric
    local nSettledValues    := 0 as numeric
    local nTotalAvailable   := 0 as numeric

    nOpenValues     := self:calcBillsReceivable()
    nSettledValues  := self:calcSettledReceivable()
    nTotalAvailable := self:nTotalValueInCash + self:nTotalValueInBanks + self:nTotalValueOfInvestments - self:nTotalValueOfLoans

    self:jData := JSONObject():new() 

    self:jData["SALDO_EM_CAIXA"] := self:nTotalValueInCash
    self:jData["SALDO_EM_BANCOS"] := self:nTotalValueInBanks
    self:jData["SALDO_EM_APLICACOES"] := self:nTotalValueOfInvestments
    self:jData["SALDO_EM_EMPRESTIMOS"] := self:nTotalValueOfLoans
    self:jData["TOTAL_DISPONIVEL"] := nTotalAvailable
    self:jData["PERIODO"] := self:cActualPeriod
    self:jData["CARTEIRA"] := cPortfolio
    self:jData["EM_ABERTO"] := nOpenValues
    self:jData["REALIZADO"] := nSettledValues

    self:oData:appendData(self:jData)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} loadPayableValues
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadPayableValues() class FinancialSummarySmartViewBusinessObject

    local cPortfolio        := STR0015 as character // "Pagar"
    local nOpenValues       := 0 as numeric
    local nSettledValues    := 0 as numeric
    local nTotalAvailable   := 0 as numeric
    local jTotalCommissions := JSONObject():new() as json

    nOpenValues     := self:calcBillsPayable()
    nSettledValues  := self:calcSettledPayable()
    nTotalAvailable := self:nTotalValueInCash + self:nTotalValueInBanks + self:nTotalValueOfInvestments - self:nTotalValueOfLoans

    if self:lShowCommissions
        jTotalCommissions := self:calcCommissions()
        nOpenValues       += jTotalCommissions["ABERTO"]
        nSettledValues    += jTotalCommissions["REALIZADO"]
    endIf

    self:jData := JSONObject():new() 

    self:jData["SALDO_EM_CAIXA"] := self:nTotalValueInCash
    self:jData["SALDO_EM_BANCOS"] := self:nTotalValueInBanks
    self:jData["SALDO_EM_APLICACOES"] := self:nTotalValueOfInvestments
    self:jData["SALDO_EM_EMPRESTIMOS"] := self:nTotalValueOfLoans
    self:jData["TOTAL_DISPONIVEL"] := nTotalAvailable
    self:jData["PERIODO"] := self:cActualPeriod
    self:jData["CARTEIRA"] := cPortfolio
    self:jData["EM_ABERTO"] := nOpenValues
    self:jData["REALIZADO"] := nSettledValues

    self:oData:appendData(self:jData)

return


//-------------------------------------------------------------------
/*/{Protheus.doc} calcBillsPayable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcBillsPayable() as numeric class FinancialSummarySmartViewBusinessObject
    local aArea := {} as array
    local aAreaSE2 := {} as array
    local nTotalPayable := 0 as numeric
    local nIssueValue := 0 as numeric
     
    aArea := getArea()
    aAreaSE2 := SE2->(getArea())

    self:loadStatementBillsPayable()
    self:cAlias := self:oQryBillsPay:openAlias()

    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof()) 

        SE2->(DBGoTo((self:cAlias)->SE2RECNO)) // SaldoTit, precisa do SE2 posicionado

        nIssueValue := 0
        
        if self:lRetroactiveBalance
            nIssueValue := SaldoTit( SE2->E2_PREFIXO, SE2->E2_NUM, SE2->E2_PARCELA, SE2->E2_TIPO, SE2->E2_NATUREZ, "P",;
                                    SE2->E2_FORNECE,self:nCurrency,self:dRetroactiveDate,self:dRetroactiveDate, SE2->E2_LOJA )
        else
            self:nCurrencyRate  := SE2->E2_TXMOEDA
            self:nIssueCurrency := SE2->E2_MOEDA
            self:nBankCurrency := self:nCurrency

            nIssueValue := self:convertMovementCurrency( SE2->E2_SALDO + SE2->E2_SDACRES - SE2->E2_SDDECRE )
        endIf
        
        if !(SE2->E2_TIPO$MVPAGANT+"/"+MV_CPNEG) .And. !( !self:lRetroactiveBalance .And. nTotalPayable == 0 ) 
            //*** nao deve olhar abatimento pois e zerado o saldo na liquidacao final do titulo
            nIssueValue -=SomaAbat( SE2->E2_PREFIXO, SE2->E2_NUM, SE2->E2_PARCELA, "P",1)
        endIf   

        if SE2->E2_TIPO $ MVPAGANT+"/"+MV_CPNEG
            nTotalPayable -= nIssueValue
        else
            nTotalPayable += nIssueValue
        endIf

        (self:cAlias)->(DbSkip())

    Enddo

    (self:cAlias)->(dbCloseArea())

    restArea(aAreaSE2)
    restArea(aArea)
    fwFreeArray(aAreaSE2)
    fWFreeArray(aArea)

return nTotalPayable

//-------------------------------------------------------------------
/*/{Protheus.doc} calcBillsReceivable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcBillsReceivable() as numeric class FinancialSummarySmartViewBusinessObject
    local aArea := {} as array
    local aAreaSE1 := {} as array
    local nTotalReceivable := 0 as numeric
    local nIssueValue := 0 as numeric

    aArea := getArea()
    aAreaSE1 := SE1->(getArea())

    self:loadStatementBillsReceivable()
    self:cAlias  := self:oQryBillsRec:openAlias()

    (self:cAlias)->(DBGoTop())    

    While !(self:cAlias)->(Eof()) 

        SE1->(DBGoTo((self:cAlias)->SE1RECNO)) // SaldoTit, precisa do SE2 posicionado

        nIssueValue := 0

        if self:lRetroactiveBalance
            nIssueValue := SaldoTit( SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA,;
                                SE1->E1_TIPO, SE1->E1_NATUREZ,"R", SE1->E1_CLIENTE,self:nCurrency,self:dRetroactiveDate,self:dRetroactiveDate, SE1->E1_LOJA)
        else
            self:nCurrencyRate  := SE1->E1_TXMOEDA
            self:nIssueCurrency := SE1->E1_MOEDA
            self:nBankCurrency := self:nCurrency

            nIssueValue := self:convertMovementCurrency( SE1->E1_SALDO + SE1->E1_SDACRES - SE1->E1_SDDECRE )
        endIf
        
        if ! ( SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG ) .And. !( !self:lRetroactiveBalance .And. nTotalReceivable == 0 ) 
            //*** nao deve olhar abatimento pois e zerado o saldo na liquidacao final do titulo
            nIssueValue -=SomaAbat( SE1->E1_PREFIXO, SE1->E1_NUM, SE1->E1_PARCELA,"P",1 )
        endIf   

        if SE1->E1_TIPO $ MVRECANT+"/"+MV_CRNEG
            nTotalReceivable -= nIssueValue
        else
            nTotalReceivable += nIssueValue
        endIf


        (self:cAlias)->(DbSkip())

    Enddo 

    (self:cAlias)->(dbCloseArea())

    restArea(aAreaSE1)
    restArea(aArea)
    fwFreeArray(aAreaSE1)
    fWFreeArray(aArea)

return nTotalReceivable


//-------------------------------------------------------------------
/*/{Protheus.doc} calcCommissions
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcCommissions() as json class FinancialSummarySmartViewBusinessObject
    local nCommissionValue := 0 as numeric
    local jTotalCommissions := JSONObject():new() as json

    jTotalCommissions['ABERTO']     := 0
    jTotalCommissions['REALIZADO']  := 0

    self:loadStatementCommissions()
    self:cAlias  := self:oQryComission:openAlias()

    (self:cAlias)->(DBGoTop())    

    if (self:cAlias)->(Eof()) 
        (self:cAlias)->(DBCloseArea())
        return jTotalCommissions
    endIf

    While !(self:cAlias)->(Eof()) 

        self:nIssueCurrency  := Val((self:cAlias)->E3_MOEDA)
        self:dConversionDate := (self:cAlias)->E3_VENCTO
        self:nCurrencyRate   := 0

        nCommissionValue := self:convertMovementCurrency((self:cAlias)->E3_COMIS)
        
        if !Empty((self:cAlias)->E3_DATA)
            jTotalCommissions['REALIZADO']  += nCommissionValue
        else 
            jTotalCommissions['ABERTO']  += nCommissionValue
        endIf   

        (self:cAlias)->(DbSkip())

    endDo 

    (self:cAlias)->(DBCloseArea())

return jTotalCommissions


//-------------------------------------------------------------------
/*/{Protheus.doc} calcSettledPayable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcSettledPayable() as numeric class FinancialSummarySmartViewBusinessObject
    local nTotalValue := 0 as numeric

    self:loadStatementSettledPayables()
    self:cAlias  := self:oQrySettledPay:openAlias()

    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof()) 

        self:nIssueCurrency  := Val((self:cAlias)->FK2_MOEDA)
        self:dConversionDate := (self:cAlias)->FK2_DATA
        self:nCurrencyRate   := (self:cAlias)->FK2_TXMOED

        nTotalValue += self:convertMovementCurrency((self:cAlias)->FK2_VALOR)

        (self:cAlias)->(DbSkip())

    endDo 

    (self:cAlias)->(DBCloseArea())

return nTotalValue

//-------------------------------------------------------------------
/*/{Protheus.doc} calcSettledReceivable
    @author Fábio Henrique Andrade
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method calcSettledReceivable() as numeric class FinancialSummarySmartViewBusinessObject
    local nTotalValue := 0 as numeric

    self:loadStatementSettledReceivables()
    self:cAlias  := self:oQrySettledRec:openAlias()

    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        self:nIssueCurrency  := Val((self:cAlias)->FK1_MOEDA)
        self:dConversionDate := (self:cAlias)->FK1_DATA
        self:nCurrencyRate   := (self:cAlias)->FK1_TXMOED

        nTotalValue += self:convertMovementCurrency((self:cAlias)->FK1_VALOR)

        (self:cAlias)->(DbSkip())

    endDo 

    (self:cAlias)->(DBCloseArea())

return nTotalValue

/*/{Protheus.doc} loadParameters
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
*/
method loadParameters() class FinancialSummarySmartViewBusinessObject   
    
    local aIntervalTypes := {STR0016,STR0017,STR0018,STR0019} as array //"Dia" # "Semana" # "Quinzena" # "Mês"

    if self:oCurrentFilter  == NIL
        self:setDefaultParameters()
    else
    
        self:lDailyTypePeriod       := MV_PAR01 == 1
        self:lWeeklyTypePeriod      := MV_PAR01 == 2
        self:lBiweeklyTypePeriod    := MV_PAR01 == 3
        self:lMontlyTypePeriod      := MV_PAR01 == 4
        self:cTypeInterval          := aIntervalTypes[MV_PAR01]
        if MV_PAR02 > 0
            self:nQuantityOfPeriods     := MV_PAR02
        endIf
        self:lFirstPeriodByDataBase := MV_PAR03 == 1
        self:lFirstPeriodByFirstDay := MV_PAR03 == 2
        self:lRetroactiveBalance    := MV_PAR04 == 1
        self:dRetroactiveDate       := MV_PAR05

        self:cClientCodeFrom        := MV_PAR06
        self:cClientCodeTo          := MV_PAR07
        self:cSuplierCodeFrom       := MV_PAR08
        self:cSuplierCodeTo         := MV_PAR09
        self:cClassCodeFrom         := MV_PAR10
        self:cClassCodeTo           := MV_PAR11
        self:nCurrency              := self:getCurrencyValue(MV_PAR12)
        self:lListOtherCurrencies   := !(MV_PAR13 == 1)

        self:lShowProvisionals      := MV_PAR14 == 1
        self:lShowCommissions       := MV_PAR15 == 1
    
    endIf
return

/*/{Protheus.doc} setDefaultParameters
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
*/
method setDefaultParameters() class FinancialSummarySmartViewBusinessObject
    self:lDailyTypePeriod       := .F.
    self:lWeeklyTypePeriod      := .F.
    self:lBiweeklyTypePeriod    := .F.
    self:lMontlyTypePeriod      := .F.
    self:nQuantityOfPeriods     := 0
    
    self:cClientCodeFrom        := ""
    self:cClientCodeTo          := ""
    self:cSuplierCodeFrom       := ""
    self:cSuplierCodeTo         := ""
    self:cClassCodeFrom         := ""
    self:cClassCodeTo           := ""
    self:nCurrency              := 1
    self:lListOtherCurrencies   := .F.

    self:lFirstPeriodByDataBase := .F.
    self:lFirstPeriodByFirstDay := .F.
    self:dRetroactiveDate       := CtoD("//")
    self:lRetroactiveBalance    := .F.
    self:lShowProvisionals      := .F.
    self:lShowCommissions       := .F.
return

//----------------------------------------------------------------
/*/{Protheus.doc}convertMovementCurrency
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method convertMovementCurrency(nValue as numeric) as numeric class FinancialSummarySmartViewBusinessObject
	local nConvertedValue := 0 as numeric

	nConvertedValue := xMoeda(nValue, self:nIssueCurrency, self:nCurrency, self:dConversionDate, self:nDecimals, self:nCurrencyRate)

return nConvertedValue


//----------------------------------------------------------------
/*/{Protheus.doc} getMonday
    @author Fábio Henrique Andrade
    @since 11/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method getMonday(dDateToAdjust) as date class FinancialSummarySmartViewBusinessObject

	if dow(dDateToAdjust) >= 2				// Se passou de segunda-feira...
		do while dow(dDateToAdjust) <> 2	// Volta até Segunda-Feira
			dDateToAdjust--
		endDo
	else									// Senão avança até Segunda
		dDateToAdjust++
	endIf

return dDateToAdjust

//----------------------------------------------------------------
/*/{Protheus.doc} setArrayOfDates
    @author Fábio Henrique Andrade
    @since 11/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method setArrayOfDates() class FinancialSummarySmartViewBusinessObject
	local dInitialDate   := dDataBase as date
	local nCurrentNumber := 1 as numeric
	local dFinalDate     := dDataBase as date

    if self:lRetroactiveBalance
        dInitialDate := self:dRetroactiveDate
    endIf

    If self:nQuantityOfPeriods == 0
        self:nQuantityOfPeriods := 1 
    EndIf

	if self:lFirstPeriodByFirstDay

		if self:lWeeklyTypePeriod
			dInitialDate := DataValida(self:getMonday(dInitialDate))
		endif

		if self:lBiweeklyTypePeriod
			if day(dInitialDate) > 15
				dInitialDate := CtoD("15/" + str(month(dInitialDate),2) + "/" + str(year(dInitialDate),4),"ddmmyy")
			else
				dInitialDate := CtoD("01/" + str(month(dInitialDate),2) + "/" + str(year(dInitialDate),4),"ddmmyy")
			endIf
		endIf

		if self:lMontlyTypePeriod
			dInitialDate := CtoD("01/" + str(month(dInitialDate),2) + "/" + str(year(dInitialDate),4),"ddmmyy")
		endIf
	endIf

	for nCurrentNumber := 1 to self:nQuantityOfPeriods

		dFinalDate := dInitialDate
		if self:lWeeklyTypePeriod
			dFinalDate += 6
		elseIf self:lBiweeklyTypePeriod
			dFinalDate += 14
		elseIf self:lMontlyTypePeriod
			if self:lFirstPeriodByFirstDay
				dFinalDate := LastDay(dFinalDate)
			else
				dFinalDate += 29
			endif
		endif

		aAdd(self:aInitialDates, dInitialDate)
		aAdd(self:aFinalDates, dFinalDate)

		dInitialDate := dFinalDate + 1
	next

return

//----------------------------------------------------------------
/*/{Protheus.doc} setLoansAndInvestmentsValues
    @author Fábio Henrique Andrade
    @since 11/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method setLoansAndInvestmentsValues() class FinancialSummarySmartViewBusinessObject
	local aCalculatedValues := {} as array
	local dFinalDate := self:aFinalDates[Len(self:aFinalDates)]
    local nX    := 0 as numeric 
    local aBranches := {} as array
    local aSehArea as array
    local aArea as array

	self:nIssueCurrency  := 1
	self:dConversionDate := dDataBase
	self:nCurrencyRate   := 0

    aArea := getArea()
    aSehArea := getArea("SEH")

    if self:lEmptyMultiBranches
        aBranches := { cFilAnt }
    else
        aBranches := aClone(self:aMultiBranches)
    endIf

    For nX := 1 to Len(self:aMultiBranches)
        
        dbSelectArea("SEH")
        SEH->(dbSetOrder(2))
        SEH->( dbSeek(FWxFilial("SEH", self:aMultiBranches[nX]) +"A") )

        While !Eof() .And. SEH->EH_FILIAL == FWxFilial("SEH", self:aMultiBranches[nX])  .And. SEH->EH_STATUS == "A"

            aCalculatedValues := self:calculateLoansAndInvestmentsValues(dFinalDate)

            If SEH->EH_APLEMP == "EMP"
                self:nTotalValueOfLoans += aCalculatedValues[2,1]
            Else
                self:nTotalValueOfInvestments += aCalculatedValues[1]
            EndIf
            SEH->(dbSkip())
        EndDo
    Next

    restArea(aSehArea)
    restArea(aArea)

	self:nTotalValueOfLoans         := self:convertMovementCurrency(self:nTotalValueOfLoans)
	self:nTotalValueOfInvestments   := self:convertMovementCurrency(self:nTotalValueOfInvestments)

    fwFreeArray(aBranches)

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadBanksAndCashValues
    @author guilherme.sordi@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method loadBanksAndCashValues() class FinancialSummarySmartViewBusinessObject
	self:nTotalValueInCash  := self:getCashBalance()
	self:nTotalValueInBanks := self:getBanksBalance()
return

//----------------------------------------------------------------
/*/{Protheus.doc} getCashBalance
    @author guilherme.sordi@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getCashBalance() as numeric class FinancialSummarySmartViewBusinessObject
    local lCash := .T. as logical
return self:getBalance(lCash)

//----------------------------------------------------------------
/*/{Protheus.doc} getBanksBalance
    @author guilherme.sordi@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getBanksBalance() as numeric class FinancialSummarySmartViewBusinessObject
    local lCash := .F. as logical
return self:getBalance(lCash)

//----------------------------------------------------------------
/*/{Protheus.doc} getBalance
    @author guilherme.sordi@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getBalance(lCash as logical) as numeric class FinancialSummarySmartViewBusinessObject
    local aBalances := {} as array
    local aBanks    := {} as array
    local nX as numeric
    local nY as numeric
    local nBalanceValue as numeric
    local dBalanceDate  := dDataBase as date

    if self:lRetroactiveBalance
        dBalanceDate := self:dRetroactiveDate
    EndIf

    oBanks := Banks():new()
    oBanks:setBranchFilter(self:aMultiBranches)
    oBanks:setCurrency(self:nCurrency)
    aBanks := self:getBanksList(lCash)

    for nX := 1 to len(aBanks)
        aBalances := oBanks:getBankBalances(aBanks[nX], dBalanceDate)
        for nY := 1 to len(aBalances)
            nBalanceValue += aBalances[nY]["e8_salatua"]
        next    
    next
return nBalanceValue

//----------------------------------------------------------------
/*/{Protheus.doc} getBanksList
    @author guilherme.sordi@totvs.com.br
    @since 13/12/2024
    @version 12.1.2410
/*/
//-----------------------------------------------------------------
method getBanksList(lCash as logical) as array class FinancialSummarySmartViewBusinessObject
    local cAlias as character
    local oStBanks as object
    local aBanksList := {} as array
    local nParam := 1 as numeric
    local cQuery as character

    cQuery := " SELECT R_E_C_N_O_ RECNO "
    cQuery += " FROM " + retSqlName("SA6") + " SA6 "
    cQuery += " WHERE A6_FILIAL IN (?) "
    cQuery += " AND A6_FLUXCAI IN (?) "
    if lCash
        cQuery += " AND SUBSTRING(A6_COD, 1, 2) = ? "
    else
        cQuery += " AND SUBSTRING(A6_COD, 1, 2) <> ? "
    endIf
    cQuery += " AND D_E_L_E_T_ = ? "

    oStBanks := FWExecStatement():new(ChangeQuery(cQuery))
    oStBanks:SetIn(nParam++, self:getAliasBranchFilter("SA6"))
    oStBanks:SetIn(nParam++, {"S", " "})
    oStBanks:SetString(nParam++, "CX")

    oStBanks:SetString(nParam++, " ")

    cAlias := oStBanks:openAlias()
    aBanksList := self:aliasToSimpleArray(cAlias, "RECNO")
    (cAlias)->(dbCloseArea())
return aBanksList


//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementBillsPayable
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementBillsPayable() class FinancialSummarySmartViewBusinessObject

	local cQuery  := "" as character
	local nParamOrder := 1 as numeric

	cQuery := " SELECT E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, R_E_C_N_O_ SE2RECNO "
	cQuery += " FROM " + RetSQLName("SE2")
	cQuery += " WHERE "

    if self:lEmptyMultiBranches
        cQuery += " E2_FILIAL = ? AND "
    else
        cQuery += " E2_FILIAL IN (?) AND "
    endIf

	cQuery += " E2_VENCREA >= ? "
	cQuery += " AND E2_VENCREA <= ? "
	cQuery += " AND E2_FORNECE >= ? "
	cQuery += " AND E2_FORNECE <= ? "
	cQuery += " AND E2_NATUREZ >= ? "
	cQuery += " AND E2_NATUREZ <= ? "
	cQuery += " AND E2_EMISSAO <= ? "
	cQuery += " AND E2_TIPO NOT IN (?) "
	cQuery += " AND E2_FLUXO <> ? "

	if !self:lShowProvisionals
		cQuery += " AND E2_TIPO NOT IN (?) " 
	endIf

	if !self:lRetroactiveBalance
		cQuery += " AND E2_SALDO > ? "
	endIf

	cQuery += " AND D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)

	self:oQryBillsPay := FWExecStatement():New(cQuery)


    If self:lEmptyMultiBranches
        self:oQryBillsPay:SetString(nParamOrder++, self:oFilterBranches["SE2"][1])
    Else
        self:oQryBillsPay:SetIn(nParamOrder++, self:oFilterBranches["SE2"][1])
    EndIf

	self:oQryBillsPay:SetDate(nParamOrder++, self:dInitialDate)
	self:oQryBillsPay:SetDate(nParamOrder++, self:dFinalDate)
	self:oQryBillsPay:SetString(nParamOrder++, self:cSuplierCodeFrom)
	self:oQryBillsPay:SetString(nParamOrder++, self:cSuplierCodeTo)
	self:oQryBillsPay:SetString(nParamOrder++, self:cClassCodeFrom)
	self:oQryBillsPay:SetString(nParamOrder++, self:cClassCodeTo)
	self:oQryBillsPay:SetDate(nParamOrder++, dDatabase)
    self:oQryBillsPay:SetIn(nParamOrder++, StrTokArr2(MVABATIM,"|"))
    self:oQryBillsPay:SetString(nParamOrder++, 'N')

    If !self:lShowProvisionals
		self:oQryBillsPay:SetIn(nParamOrder++, StrTokArr2(MVPROVIS,"|"))
	EndIf

    If !self:lRetroactiveBalance
		self:oQryBillsPay:SetNumeric(nParamOrder++, 0)
	EndIf

    self:oQryBillsPay:SetString(nParamOrder++, ' ')

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementBillsReceivable
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementBillsReceivable() class FinancialSummarySmartViewBusinessObject

	local cQuery  := "" as character
	local nParamOrder := 1 as numeric
	Local cListOfSituations := FN022LSTCB(2) //lista situações descontadas

	cQuery := " SELECT E1_FILIAL, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, R_E_C_N_O_ SE1RECNO "
	cQuery += " FROM " + RetSQLName("SE1")
	cQuery += " WHERE "
	
    If self:lEmptyMultiBranches
        cQuery += " E1_FILIAL = ? AND "
    Else
        cQuery += " E1_FILIAL IN (?) AND "
    EndIf

	cQuery += " E1_VENCREA >= ? "
	cQuery += " AND E1_VENCREA <= ? "
	cQuery += " AND E1_CLIENTE >= ? "
	cQuery += " AND E1_CLIENTE <= ? "
	cQuery += " AND E1_NATUREZ >= ? "
	cQuery += " AND E1_NATUREZ <= ? "
	cQuery += " AND E1_EMISSAO <= ? "
	cQuery += " AND E1_TIPO NOT IN (?) "
	cQuery += " AND E1_SITUACA NOT IN (?) "
	cQuery += " AND E1_FLUXO IN (?) "

	If !self:lShowProvisionals
		cQuery += " AND E1_TIPO NOT IN (?) "
	EndIf

	If !self:lRetroactiveBalance
		cQuery += " AND E1_SALDO  > ? "
	EndIf

	cQuery += " AND D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)

	self:oQryBillsRec := FWExecStatement():New(cQuery)


    If self:lEmptyMultiBranches
        self:oQryBillsRec:SetString(nParamOrder++, self:oFilterBranches["SE1"][1])
    Else
        self:oQryBillsRec:SetIn(nParamOrder++, self:oFilterBranches["SE1"][1])
    EndIf

	self:oQryBillsRec:SetDate(nParamOrder++, self:dInitialDate)
	self:oQryBillsRec:SetDate(nParamOrder++, self:dFinalDate)
	self:oQryBillsRec:SetString(nParamOrder++, self:cClientCodeFrom)
	self:oQryBillsRec:SetString(nParamOrder++, self:cClientCodeTo)
	self:oQryBillsRec:SetString(nParamOrder++, self:cClassCodeFrom)
	self:oQryBillsRec:SetString(nParamOrder++, self:cClassCodeTo)
	self:oQryBillsRec:SetDate(nParamOrder++, dDatabase)
    self:oQryBillsRec:SetIn(nParamOrder++, StrTokArr2(MVABATIM, "|"))
    self:oQryBillsRec:SetIn(nParamOrder++, StrTokArr2(cListOfSituations, "|"))
    self:oQryBillsRec:SetIn(nParamOrder++, {"S", "N"})

    If !self:lShowProvisionals
		self:oQryBillsRec:SetIn(nParamOrder++, StrTokArr2(MVPROVIS, "|"))
	EndIf

    If !self:lRetroactiveBalance
		self:oQryBillsRec:SetNumeric(nParamOrder++, 0)
	EndIf

    self:oQryBillsRec:SetString(nParamOrder++, ' ')

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementCommissions
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementCommissions()  class FinancialSummarySmartViewBusinessObject

	local cQuery  := "" as character
	local nParamOrder := 1 as numeric

	cQuery := " SELECT E3_FILIAL, E3_PREFIXO, E3_NUM, E3_PARCELA, E3_SEQ, E3_VEND, 
    cQuery += " E3_COMIS, E3_DATA, E3_VENCTO, E3_MOEDA "
	cQuery += " FROM " + RetSQLName("SE3") 
	cQuery += " WHERE "
	
    If self:lEmptyMultiBranches
        cQuery += " E3_FILIAL = ? AND "
    Else
        cQuery += " E3_FILIAL IN (?) AND "
    EndIf

	cQuery += " E3_VENCTO >= ? "
	cQuery += " AND E3_VENCTO <= ? "
	cQuery += " AND D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)

	self:oQryComission := FWExecStatement():New(cQuery)


    If self:lEmptyMultiBranches
        self:oQryComission:SetString(nParamOrder++, self:oFilterBranches["SE3"][1])
    Else
        self:oQryComission:SetIn(nParamOrder++, self:oFilterBranches["SE3"][1])
    EndIf

	self:oQryComission:SetDate(nParamOrder++, self:dInitialDate)
	self:oQryComission:SetDate(nParamOrder++, self:dFinalDate)
    self:oQryComission:SetString(nParamOrder++, ' ')

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementSettledPayables
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementSettledPayables() class FinancialSummarySmartViewBusinessObject

	local cQuery  := "" as character
	local nParamOrder := 1 as numeric

	cQuery := " SELECT "
    cQuery += "  FK2_TPDOC, FK2_TXMOED, FK2_VALOR, FK2_DATA, FK2_DTDIGI, FK2_MOEDA "
	cQuery += " FROM " + RetSQLName("SE2") + " SE2 "
	cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 ON FK7.FK7_ALIAS = ? AND "
	cQuery += "  FK7.FK7_FILTIT = SE2.E2_FILIAL AND "
	cQuery += "  FK7.FK7_PREFIX = SE2.E2_PREFIXO AND "
	cQuery += "  FK7.FK7_NUM = SE2.E2_NUM AND "
	cQuery += "  FK7.FK7_PARCEL = SE2.E2_PARCELA AND "
	cQuery += "  FK7.FK7_TIPO = SE2.E2_TIPO AND "
	cQuery += "  FK7.FK7_CLIFOR = SE2.E2_FORNECE AND "
	cQuery += "  FK7.FK7_LOJA = SE2.E2_LOJA AND "
	cQuery += "  FK7.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName("FK2") + " FK2 ON " + FWJoinFilial("FK7","FK2") + " AND "
    cQuery += "  FK2.FK2_IDDOC = FK7.FK7_IDDOC AND "
    cQuery += "  FK2.FK2_MOTBX NOT IN (?) AND "
    cQuery += "  FK2.FK2_DATA BETWEEN ? AND ? AND "
    cQuery += "  FK2.D_E_L_E_T_ = ? AND "
	cQuery += " NOT EXISTS( "
	cQuery += "  SELECT FK2EST.FK2_IDDOC FROM " + RetSQLName("FK2") + " FK2EST "
	cQuery += "  WHERE FK2EST.FK2_IDDOC = FK2.FK2_IDDOC "
	cQuery += "   AND FK2EST.FK2_SEQ = FK2.FK2_SEQ "
	cQuery += "   AND FK2EST.FK2_TPDOC = ? "
	cQuery += "   AND FK2EST.D_E_L_E_T_ = ? ) "
	cQuery += "WHERE "

	If self:lEmptyMultiBranches
        cQuery += " SE2.E2_FILIAL = ? AND "
    Else
        cQuery += " SE2.E2_FILIAL IN (?) AND "
    EndIf

	cQuery += " SE2.E2_NATUREZ BETWEEN ? AND ? "
	cQuery += " AND SE2.E2_FORNECE BETWEEN ? AND ? "

	If !self:lListOtherCurrencies
		cQuery += " AND SE2.E2_MOEDA = ? "
	EndIf

	cQuery += " AND SE2.D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)

	self:oQrySettledPay := FWExecStatement():New(cQuery)

    self:oQrySettledPay:SetString(nParamOrder++, 'SE2')
    self:oQrySettledPay:SetString(nParamOrder++, ' ')
    self:oQrySettledPay:SetIn(nParamOrder++, {'DSD', 'STP'})
    self:oQrySettledPay:SetDate(nParamOrder++, self:dInitialDate)
	self:oQrySettledPay:SetDate(nParamOrder++, self:dFinalDate)
    self:oQrySettledPay:SetString(nParamOrder++, ' ')
    self:oQrySettledPay:SetString(nParamOrder++, 'ES')
    self:oQrySettledPay:SetString(nParamOrder++, ' ')
    

    If self:lEmptyMultiBranches
        self:oQrySettledPay:SetString(nParamOrder++, self:oFilterBranches["SE2"][1])
    Else
        self:oQrySettledPay:SetIn(nParamOrder++, self:oFilterBranches["SE2"][1])
    EndIf

	self:oQrySettledPay:SetString(nParamOrder++, self:cClassCodeFrom)
	self:oQrySettledPay:SetString(nParamOrder++, self:cClassCodeTo)
	self:oQrySettledPay:SetString(nParamOrder++, self:cSuplierCodeFrom)
	self:oQrySettledPay:SetString(nParamOrder++, self:cSuplierCodeTo)

	If !self:lListOtherCurrencies
		self:oQrySettledPay:SetNumeric(nParamOrder++, self:nCurrency)
	EndIf

    self:oQrySettledPay:SetString(nParamOrder++, ' ')

return

//----------------------------------------------------------------
/*/{Protheus.doc} loadStatementSettledReceivables
    @author Fábio Henrique Andrade
    @since 12/12/2023
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method loadStatementSettledReceivables() class FinancialSummarySmartViewBusinessObject

	local cQuery  := "" as character
	local nParamOrder := 1 as numeric

	cQuery := "SELECT "
    cQuery += " FK1_TXMOED, FK1_VALOR, FK1_DATA, FK1_DTDIGI, FK1_MOEDA "
	cQuery += "FROM " + RetSQLName("SE1") + " SE1 "
	cQuery += " INNER JOIN " + RetSQLName("FK7") + " FK7 ON FK7.FK7_ALIAS = ? AND "
	cQuery += " FK7.FK7_FILTIT = SE1.E1_FILIAL AND "
	cQuery += " FK7.FK7_PREFIX = SE1.E1_PREFIXO AND "
	cQuery += " FK7.FK7_NUM = SE1.E1_NUM AND "
	cQuery += " FK7.FK7_PARCEL = SE1.E1_PARCELA AND "
	cQuery += " FK7.FK7_TIPO = SE1.E1_TIPO AND "
	cQuery += " FK7.FK7_CLIFOR = SE1.E1_CLIENTE AND "
	cQuery += " FK7.FK7_LOJA = SE1.E1_LOJA AND "
	cQuery += " FK7.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSQLName("FK1") + " FK1 ON " + FWJoinFilial("FK7","FK1") + " AND "
	cQuery += " FK1.FK1_IDDOC = FK7.FK7_IDDOC AND "
	cQuery += " FK1.FK1_MOTBX NOT IN (?) AND "
	cQuery += " FK1.FK1_DATA BETWEEN ? AND ? AND "
	cQuery += " FK1.D_E_L_E_T_ = ? AND "
	cQuery += " NOT EXISTS( "
	cQuery += "  SELECT FK1EST.FK1_IDDOC FROM  " + RetSQLName("FK1") + " FK1EST "
	cQuery += "  WHERE FK1EST.FK1_IDDOC = FK1.FK1_IDDOC "
	cQuery += "   AND FK1EST.FK1_SEQ = FK1.FK1_SEQ "
	cQuery += "   AND FK1EST.FK1_TPDOC = ? "
	cQuery += "   AND FK1EST.D_E_L_E_T_ = ? ) "
	cQuery += "WHERE "

    If self:lEmptyMultiBranches
        cQuery += " SE1.E1_FILIAL = ? AND "
    Else
        cQuery += " SE1.E1_FILIAL IN (?) AND "
    EndIf

	cQuery += " SE1.E1_NATUREZ BETWEEN ? AND ? AND "
	cQuery += " SE1.E1_CLIENTE BETWEEN ? AND ? AND "

	If !self:lListOtherCurrencies
		cQuery += " E1_MOEDA = ? AND "
	EndIf

	cQuery += " SE1.D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)

	self:oQrySettledRec := FWExecStatement():New(cQuery)

    self:oQrySettledRec:SetString(nParamOrder++, 'SE1')
    self:oQrySettledRec:SetString(nParamOrder++, ' ')
    self:oQrySettledRec:SetIn(nParamOrder++, {'DSD', 'STP'})
	self:oQrySettledRec:SetDate(nParamOrder++, self:dInitialDate)
	self:oQrySettledRec:SetDate(nParamOrder++, self:dFinalDate)
    self:oQrySettledRec:SetString(nParamOrder++, ' ')
    self:oQrySettledRec:SetString(nParamOrder++, 'ES')
    self:oQrySettledRec:SetString(nParamOrder++, ' ')
    

    If self:lEmptyMultiBranches
        self:oQrySettledRec:SetString(nParamOrder++, self:oFilterBranches["SE1"][1])
    Else
        self:oQrySettledRec:SetIn(nParamOrder++, self:oFilterBranches["SE1"][1])
    EndIf

	self:oQrySettledRec:SetString(nParamOrder++, self:cClassCodeFrom)
	self:oQrySettledRec:SetString(nParamOrder++, self:cClassCodeTo)
	self:oQrySettledRec:SetString(nParamOrder++, self:cClientCodeFrom)
	self:oQrySettledRec:SetString(nParamOrder++, self:cClientCodeTo)

	If !self:lListOtherCurrencies
		self:oQrySettledRec:SetNumeric(nParamOrder++, self:nCurrency)
	EndIf

    self:oQrySettledRec:SetString(nParamOrder++, ' ')

return

//----------------------------------------------------------------
/*/{Protheus.doc} calculateLoansAndInvestmentsValues
    @author Fábio Henrique Andrade
    @since 01/04/2024
    @version 12.1.2310
/*/
//-----------------------------------------------------------------
method calculateLoansAndInvestmentsValues(dData as date) as array class FinancialSummarySmartViewBusinessObject
	Local aRetCalc := {}  as array
	Local oTemplate := Nil as object

	If SEH->EH_TIPO == "TEM"
		If oTemplate == NIL
			oTemplate := totvs.protheus.backoffice.ngf.template.Template():new()
		Endif

		If oTemplate:openTemplate(SEH->EH_FILIAL, SEH->EH_NUMERO, SEH->EH_REVISAO)
			oTemplate:setValorBase(SEH->EH_SALDO)
			oTemplate:setDateVenc(dData)
			oTemplate:calcTemplate()

			If SEH->EH_APLEMP == "APL"
				aRetCalc := oTemplate:getAplLegado()
			Else
				aRetCalc := oTemplate:getEmpLegado()
			Endif
		Endif
	Else
		aRetCalc := Fa171Calc(dData,,,,,,,,.F.)
	Endif

return aRetCalc
