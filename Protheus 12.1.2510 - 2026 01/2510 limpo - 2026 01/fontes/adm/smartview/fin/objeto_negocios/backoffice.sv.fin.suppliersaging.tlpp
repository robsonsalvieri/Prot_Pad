#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billspayable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider


@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE2,SA2,SED", name="Aging Fornecedores",;
customTables="SE2,SA2,SED", country="ALL")

//-------------------------------------------------------------------
    /*/{Protheus.doc} SuppliersAgingSmartViewBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
    https: https://jiraproducao.totvs.com.br/browse/DMBKPROT-3227
*/
//-------------------------------------------------------------------

class SuppliersAgingSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsPayableSmartViewBusinessObject
    public method new() as object
    protected method completeStatement() 
    protected method loadParameters()
    protected method getWhere() as character
    protected method initializeBusinessObject()
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class SuppliersAgingSmartViewBusinessObject
    _Super:new()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author matheus.monteiro@totvs.com.br
    @since 23/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class SuppliersAgingSmartViewBusinessObject
    self:setDisplayName(STR0017) //"Aging Fornecedores"
    self:setDescription(STR0017) //"Aging Fornecedores"
    self:setPergunte("FINT026")
return

//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class SuppliersAgingSmartViewBusinessObject
    Local cWhere := "" as character

    cWhere += "AND SE2.E2_FORNECE  BETWEEN ? AND ?  "
    cWhere += "AND SE2.E2_EMISSAO  BETWEEN ? AND ?  "

return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} completeStatement
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method completeStatement()  class SuppliersAgingSmartViewBusinessObject
    self:oStatement:SetString(self:nOrdem++, self:cSupplierFrom)
    self:oStatement:SetString(self:nOrdem++, self:cSupplierTo)
    self:oStatement:SetDate(self:nOrdem++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nOrdem++, self:dIssueDateTo)
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author matheus.monteiro@totvs.com.br
    @since 22/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class SuppliersAgingSmartViewBusinessObject      
    
    if self:oCurrentFilter != nil       
        self:cSupplierFrom := MV_PAR01
        self:cSupplierTo := MV_PAR02  
        self:dIssueDateFrom  := MV_PAR03
        self:dIssueDateTo   := MV_PAR04
        self:nMoeda := self:getCurrencyValue(MV_PAR05)
        self:lListarOutrasMoedas := MV_PAR06 <> 1

        if self:lVisaoRetroativa := ( MV_PAR07 == 1 )
            self:dVisaoRetroativa := MV_PAR08
        endIf
    endIf

return
