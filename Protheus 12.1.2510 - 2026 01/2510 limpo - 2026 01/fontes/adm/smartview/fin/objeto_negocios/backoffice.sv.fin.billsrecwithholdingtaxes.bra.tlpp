#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include 'backoffice.sv.fin.billsrecwithholdingtaxes.bra.ch'

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SE1,SA1", name="Títulos a receber com retenção de impostos", country="BRA",customTables="SE1,SA1")
//-------------------------------------------------------------------
/*/{Protheus.doc} billsrecwithholdingtaxesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para geração do Títulos a receber com retenção de impostos

@author Cássio Dias
@since 08/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
class BillsRecWithholdingTaxesSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
      
    public method new()  as object

    protected data cCustomerCodeFrom as character
    protected data cCustomerCodeTo as character
    protected data dIssueDateFrom as date 
    protected data dIssueDateTo as date 
    protected data nCurrency as numeric
    protected data aBackupDbArea as array
    protected data aBackupDbAreaSE1 as array

    protected data lIRRFBaixa  as Logical
    protected data lPCCBaixa  as logical
    protected data lCalcIssBx as logical

    protected method initializeBusinessObject()
    protected method loadParameters()
    protected method loadDefaultParameters()
      
    protected method loadStatement()
    protected method preWhileMyAliasToData()
    protected method postWhileMyAliasToData()
    protected method mySetSchema()   
    protected method handleData()  
    protected method convertCurrencyValue() as numeric
    
    protected method getTaxValues()  as numeric
    protected method getGrossValues()  as numeric
    protected method getNetAmount() as numeric
    protected method getTaxes() as character
    protected method getFormatedText() as json

endclass
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Cássio Dias
@since 08/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class BillsRecWithholdingTaxesSmartViewBusinessObject
    _Super:new()
    self:initializeBusinessObject()
    self:setCompleteData(.T.)
    self:setMainTable("SE1")
    self:setBranchFilter(.T., .T.)

    self:nCurrency := 1
    self:lCalcIssBx := GetNewPar("MV_MRETISS","1") == "2" 
    self:lPCCBaixa := SuperGetMv("MV_BX10925",.T.,"2") == "1" 
    self:lIRRFBaixa := .F.
    self:aBackupDbArea := {}
    self:aBackupDbAreaSE1 := {}
return self


//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 19/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class BillsRecWithholdingTaxesSmartViewBusinessObject
    self:setDisplayName(STR0002) //"Títulos a receber com retenção de impostos"
    self:setDescription(STR0002) //"Títulos a receber com retenção de impostos"
    self:setPergunte("FINT014") 
return

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Cássio Dias
@since 08/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method mySetSchema()  class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local aFieldsSE1 as array
    Local aFieldsSA1 as array
   
    aFieldsSE1 := { "E1_FILIAL","E1_PREFIXO","E1_NUM","E1_PARCELA","E1_TIPO",;
                    "E1_EMISSAO","E1_VALOR","E1_CLIENTE","E1_LOJA", "E1_TIPOLIQ", "E1_TXMOEDA",;
                    "E1_ISS", "E1_INSS", "E1_IRRF", "E1_CSLL" , "E1_COFINS", "E1_PIS", "E1_FABOV",;
                    "E1_FACS", "E1_IMA", "E1_FMPEQ","E1_NATUREZ", "E1_JUROS", "E1_MULTA", "E1_DESCONT",;
                    "E1_ACRESC", "E1_DECRESC", "A1_RECIRRF", "E1_MOEDA"}
    aFieldsSA1 := {"A1_NOME", "A1_RECIRRF"}
    
    self:oSchema:aliasToSchema("SE1", aFieldsSE1)
    self:oSchema:aliasToSchema("SA1", aFieldsSA1)
    
    self:finAddProperty("DADOSTIT"	 , STR0007, "string","E1_NUM") //"Dados do Título"
    self:finAddProperty("DADOSCLI"  , STR0006, "string","A1_NOME")   //"DadosCli"
    self:finAddProperty("VALORIMP"  , STR0009, "number")   //"ValorImp"-
    self:finAddProperty("VALORLIQ"  , STR0005, "number")   //"ValorLiq
    self:finAddProperty("IMPOSTOS"  , STR0008, "string")   //"Impostos Retidos
    self:finAddProperty("LIQUIDACAO", STR0010, "string") //"Liquidação"--

    FwFreeArray(aFieldsSE1)
    FWFreeArray(aFieldsSA1)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData
    @author matheus.monteiro@totvs.com.br
    @since 21/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------

method preWhileMyAliasToData() class BillsRecWithholdingTaxesSmartViewBusinessObject
    self:aBackupDbArea := getArea()
    self:aBackupDbAreaSE1 := SE1->(getArea())
return

//-------------------------------------------------------------------
/*{Protheus.doc} postWhileMyAliasToData
    @author matheus.monteiro@totvs.com.br
    @since 21/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method postWhileMyAliasToData() class BillsRecWithholdingTaxesSmartViewBusinessObject
    restArea(self:aBackupDbAreaSE1)
    restArea(self:aBackupDbArea)
    FwFreeArray(self:aBackupDbAreaSE1)
    FwFreeArray(self:aBackupDbArea)
    self:aBackupDbArea := {}
    self:aBackupDbAreaSE1 := {}
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
    Atribui ao objeto oData o conteúdo do Alias e campos calculados.
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleData() class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local nTaxValues	    := 0    as numeric
    Local nGrossValue	    := 0    as numeric
    Local nNetAmount	    := 0    as numeric
    Local cTaxes            := "" as character 
    Local jFormatedText     as json
    Local lLiquidacao := .F.  as Logical
  
    jFormatedText := self:getFormatedText()

    nTaxValues  := self:getTaxValues()
    nGrossValue := self:getGrossValues()
    nNetAmount  := self:getNetAmount()
    cTaxes      := self:getTaxes()

    lLiquidacao := !Empty((self:cAlias)->E1_TIPOLIQ)

    If lLiquidacao
        cLiquida:= STR0003 //"Sim"
    Else 
        cLiquida:= STR0004 //"Nao"
    EndIf 

    self:jData := {;
        "E1_FILIAL":(self:cAlias)->E1_FILIAL,;
        "E1_PREFIXO":(self:cAlias)->E1_PREFIXO,;
        "E1_NUM":(self:cAlias)->E1_NUM,;
        "E1_PARCELA":(self:cAlias)->E1_PARCELA,;
        "E1_TIPO":(self:cAlias)->E1_TIPO,;
        "DADOSTIT":jFormatedText["cInvoiceData"],; 
        "DADOSCLI":jFormatedText["cCustomerData"],;
        "E1_EMISSAO":(self:varToTimeStamp((self:cAlias)->E1_EMISSAO)),;
        "E1_VALOR":self:convertCurrencyValue(nGrossValue),;
        "VALORLIQ":self:convertCurrencyValue(nNetAmount),;
        "VALORIMP":self:convertCurrencyValue(nTaxValues),;
        "IMPOSTOS":cTaxes,;
        "LIQUIDACAO":cLiquida}
return

//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query a ser executada na função getData
@author Cássio Dias
@since 08/09/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method loadStatement() class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local cQuery := "" as character
    Local nParamOrder := 1 as numeric

    cQuery := " SELECT ? " 

    cQuery += " FROM " + RetSqlName("SE1") + " SE1 "
    cQuery += " JOIN " + RetSqlName("SA1") + " SA1 "
    cQuery += " ON  " + FWJoinFilial("SE1","SA1")
    cQuery += " AND SE1.E1_CLIENTE = SA1.A1_COD "
    cQuery += " AND SE1.E1_LOJA = SA1.A1_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "
    cQuery += " WHERE "
    cQuery += self:getSQLBranchFilter()
    cQuery += " AND SE1.E1_CLIENTE BETWEEN ? AND ? "
    cQuery += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    cQuery += " AND (E1_ISS <> ? OR E1_IRRF <> ? OR E1_INSS <> ? OR E1_CSLL <> ? OR E1_COFINS <> ? OR E1_PIS <> ? "
    cQuery += " OR E1_FABOV <> ? OR E1_FACS <> ? OR E1_IMA <> ? OR E1_FMPEQ <> ?) AND "
    cQuery += " SE1.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    cQuery += " ORDER BY " + SqlOrder(SE1->(IndexKey( 1 )))

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetUnsafe(nParamOrder++, self:getAllFieldsToSql())
    self:oStatement:SetString(nParamOrder++, " ")
    self:setStatementBranchFilter(@nParamOrder)
    self:oStatement:SetString(nParamOrder++, self:cCustomerCodeFrom)
    self:oStatement:SetString(nParamOrder++, self:cCustomerCodeTo)
    self:oStatement:SetDate(nParamOrder++,   self:dIssueDateFrom)
    self:oStatement:SetDate(nParamOrder++,   self:dIssueDateTo)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetNumeric(nParamOrder++,  0)
    self:oStatement:SetString(nParamOrder++, " ")
    
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} loadDefaultParameters
    Inicializa as propriedades de parâmetros do objeto de negócio
    para não correr riscos de error.log.
    Será implementado no objeto de negócio.
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadDefaultParameters() class BillsRecWithholdingTaxesSmartViewBusinessObject
    self:cCustomerCodeFrom := ""
    self:cCustomerCodeTo := "ZZZ"
    self:dIssueDateFrom := cToD("  /  /  ")
    self:dIssueDateTo := cToD("  /  /  ")
return

/*/{Protheus.doc} loadParameters
    Carrega os parâmetros da consulta antes de chamar o loadStatement().
    Será implementado no objeto de negócio, de acordo com a necessidade e
    os parâmetros utilizados.
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class BillsRecWithholdingTaxesSmartViewBusinessObject
    self:cCustomerCodeFrom := MV_PAR01
    self:cCustomerCodeTo := MV_PAR02 
    self:dIssueDateFrom := MV_PAR03
    self:dIssueDateTo := MV_PAR04
return

//-------------------------------------------------------------------
/*{Protheus.doc} getDiscountValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getFormatedText() as json class BillsRecWithholdingTaxesSmartViewBusinessObject
    local jFormatedText := JsonObject():New() as json
    jFormatedText["cInvoiceData"] := (self:cAlias)->E1_PREFIXO+" - "+(self:cAlias)->E1_NUM+" - "+(self:cAlias)->E1_PARCELA+" - "+(self:cAlias)->E1_TIPO
    jFormatedText["cCustomerData"] := (self:cAlias)->E1_CLIENTE + '/' + (self:cAlias)->E1_LOJA + ' - ' + allTrim((self:cAlias)->A1_NOME)     
return jFormatedText


/*/{Protheus.doc} getTaxValues
    Cálculos dos impostos 
    Implementado durante a revisão em 20/12/2023
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getTaxValues() as numeric class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local nTaxValues	:= 0    as numeric

    nTaxValues :=(self:cAlias)->E1_INSS 
    nTaxValues += (self:cAlias)->E1_FABOV + (self:cAlias)->E1_FACS + (self:cAlias)->E1_IMA + (self:cAlias)->E1_FMPEQ    
    nTaxValues += (self:cAlias)->E1_PIS + (self:cAlias)->E1_COFINS + (self:cAlias)->E1_CSLL
    nTaxValues += (self:cAlias)->E1_ISS
    nTaxValues += (self:cAlias)->E1_IRRF
return nTaxValues



/*/{Protheus.doc} getGrossValues
    Cálculos do valor bruto. 
    Implementado durante a revisão em 20/12/2023
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getGrossValues(nValue as numeric) as numeric class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local nGrossValue	:= 0    as numeric
  
    default nValue := (self:cAlias)->E1_VALOR

    nGrossValue := nValue + (self:cAlias)->E1_INSS 

     If !self:lPCCBaixa
        nGrossValue += (self:cAlias)->E1_PIS + (self:cAlias)->E1_COFINS + (self:cAlias)->E1_CSLL
    EndIf

     If !self:lCalcIssBx
        nGrossValue += (self:cAlias)->E1_ISS
    EndIf

    If self:lIRRFBaixa := ((self:cAlias)->A1_RECIRRF == "1")
    EndIf

    If !self:lIRRFBaixa
        nGrossValue += (self:cAlias)->E1_IRRF
    EndIf
  
return nGrossValue


/*/{Protheus.doc} getNetAmount
    Cálculos do valor bruto. 
    Implementado durante a revisão em 20/12/2023
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getNetAmount() as numeric class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local nNetAmount	:= 0    as numeric
    Local nDecres   := 0    as numeric
    Local nAcres    := 0    as numeric
    Local nVa       := 0    as numeric
    Local lLiquidacao := .F.  as Logical

    nValue := (self:cAlias)->E1_VALOR

    nNetAmount := nValue 

    nNetAmount -= (self:cAlias)->E1_FABOV + (self:cAlias)->E1_FACS + (self:cAlias)->E1_IMA + (self:cAlias)->E1_FMPEQ 

     If self:lPCCBaixa
        nNetAmount -= (self:cAlias)->E1_PIS + (self:cAlias)->E1_COFINS + (self:cAlias)->E1_CSLL
    EndIf

     If self:lCalcIssBx
        nNetAmount -= (self:cAlias)->E1_ISS 
    EndIf   

    If self:lIRRFBaixa .And. ((self:cAlias)->A1_RECIRRF == "1")
        nNetAmount -= (self:cAlias)->E1_IRRF
    EndIf

    nVa	:= FValAcess((self:cAlias)->E1_PREFIXO,(self:cAlias)->E1_NUM,(self:cAlias)->E1_PARCELA,;
            (self:cAlias)->E1_TIPO,(self:cAlias)->E1_CLIENTE,(self:cAlias)->E1_LOJA,;
            (self:cAlias)->E1_NATUREZ, lLiquidacao, /*cCodVa*/,"R",/*dBaixa*/,/*aValAces*/,;
            (self:cAlias)->E1_MOEDA,1,(self:cAlias)->E1_TXMOEDA)

    nAcres := (self:cAlias)->E1_ACRESC + (self:cAlias)->E1_JUROS + (self:cAlias)->E1_MULTA
    nDecres := (self:cAlias)->E1_DECRESC + (self:cAlias)->E1_DESCONT

    If nVa > 0
        nAcres += nVa
    Else 
        nDecres -= nVa
    EndIf

    nNetAmount := nNetAmount + nAcres - nDecres
  
return nNetAmount


/*/{Protheus.doc} getGrossValues
    Cálculos do valor bruto. 
    Implementado durante a revisão em 20/12/2023
    @author matheus.monteiro@totvs.com.br
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getTaxes() class BillsRecWithholdingTaxesSmartViewBusinessObject
    Local cTaxes := "" as character
    Local nX := 0 as numeric
    Local aTaxes := {} as array
    
    
    aTaxes := {"E1_ISS", "E1_IRRF", "E1_INSS", "E1_PIS", "E1_COFINS", "E1_CSLL", "E1_FABOV", "E1_FACS", "E1_IMA", "E1_FMPEQ"}

    for nX := 1 to len(aTaxes)
        if !Empty( (self:cAlias)->&(aTaxes[nX]) )
                cTaxes += replace(aTaxes[nX], "E1_", "") + " "
        endIf
    next nX
    cTaxes := allTrim(cTaxes)
  
return cTaxes


//-------------------------------------------------------------------
/*{Protheus.doc} convertCurrencyValue
    @author guilherme.sordi@totvs.com.br
    @since 05/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method convertCurrencyValue(nValue as numeric, jCurrencyVariables as json) as numeric class BillsRecWithholdingTaxesSmartViewBusinessObject

    local dConvertion := dDatabase as numeric
    local nContractedRate := 0 as numeric
    local nOriginRate := 0 as numeric
    local nDestinationRate := 0 as numeric
    local nMovimentCurrency := 1 as numeric

    if jCurrencyVariables == NIL
        jCurrencyVariables := {"dConvertion": (self:cAlias)->E1_EMISSAO, "nContractedRate": (self:cAlias)->E1_TXMOEDA, "nMovimentCurrency": (self:cAlias)->E1_MOEDA}
    endIf

    dConvertion := jCurrencyVariables["dConvertion"]
    nContractedRate := jCurrencyVariables["nContractedRate"]
    nMovimentCurrency := jCurrencyVariables["nMovimentCurrency"]

    if nMovimentCurrency == 1
        nDestinationRate := nContractedRate
    else
        nOriginRate := nContractedRate
    endIf
    
    nValue := xMoeda(nValue, nMovimentCurrency, self:nCurrency, dConvertion, self:nDecimals, nOriginRate, nDestinationRate)
return nValue
