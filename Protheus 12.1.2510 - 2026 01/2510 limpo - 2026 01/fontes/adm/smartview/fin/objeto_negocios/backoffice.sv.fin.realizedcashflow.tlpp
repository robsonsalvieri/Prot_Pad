#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.realizedcashflow.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="FK5", customTables="FK5", name="Fluxo de caixa realizado", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} RealizedCashFlowTReportsBusinessObject
Classe para criação do Objeto de Negócio
@author guilherme.sordi@totvs.com.br
@since 23/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
class RealizedCashFlowTReportsBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.FinIntegratedProvider
    public method new() as object

    protected data oFinClassUtil as object
    protected data nSaldoAnterior as numeric
    protected data jClassificacao as json
    protected data nSaldoDiaAnterior as numeric
    protected data dDiaAtual as date
    protected data nSaldoInicial as numeric
    protected data nSaldoDiaAtual as numeric

    //Parâmetros do objeto de negócio
    protected data dDataDe as date
    protected data dDataAte as date
    protected data cBanco as character
    protected data cAgencia as character
    protected data cConta as character
    protected data nMoeda as numeric
    protected data lConverterPorDataMovimento as logical

    //Conversão de moeda
    protected data nMoedaMovimento as numeric
    protected data dConversaoMoeda as date
    protected data nTaxaMoedaMovimento as numeric    

    protected method initializeBusinessObject()
    protected method mySetSchema()
    protected method loadStatement()
    protected method handleData()
    protected method preWhileMyAliasToData()
    protected method loadParameters()
    protected method setDefaultParameters()
    protected method loadClassificacoes()
    protected method converteMoeda() as numeric
    protected method getOpeningBalance() as numeric
    protected method getMoedaBanco() as numeric
    protected method getSa6Recnos() as array
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author guilherme.sordi@totvs.com.br
@since 23/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class RealizedCashFlowTReportsBusinessObject    
    _Super:new()

    self:setCompleteData(.T.)
    self:setAllTrim(.T.)
    self:setMainTable("FK5")
    self:setBranchFilter(.T., .T.)

    self:initializeBusinessObject()
    self:loadClassificacoes()
    
    self:nSaldoAnterior := 0

    self:nMoedaMovimento := 1
    self:dConversaoMoeda := dDatabase
    self:nTaxaMoedaMovimento := 1

    self:oFinClassUtil := totvs.protheus.backoffice.fin.smartView.integratedProvider.FinClassUtil():New()

return self

//-------------------------------------------------------------------
    /*{Protheus.doc} initializeBusinessObject
    @author Fábio Henrique Andrade
    @since 23/11/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method initializeBusinessObject() class RealizedCashFlowTReportsBusinessObject
    self:setDisplayName(STR0002) //"Fluxo de caixa realizado"
    self:setDescription(STR0002) //"Fluxo de caixa realizado"
    self:setPergunte("FINT007")
return


//-------------------------------------------------------------------
    /*{Protheus.doc} mySetSchema
    Determina a estrutura dos campos
    @author Fábio Henrique Andrade
    @since 07/12/2023
    @version 12.1.2310
    */
//-------------------------------------------------------------------
method mySetSchema() class RealizedCashFlowTReportsBusinessObject
    local aFieldsFK5 := {} as array

    aFieldsFK5 := { "FK5_FILIAL", "FK5_IDMOV", "FK5_DATA", "FK5_VALOR", "FK5_RECPAG", "FK5_NATURE" }

    self:oSchema:aliasToSchema("FK5", aFieldsFK5)

    self:finAddProperty("SALDO_INICIAL" , STR0003, "number") //Saldo Inicial
    self:finAddProperty("VAL_REC" , STR0004, "number") //Entrada
    self:finAddProperty("VAL_PAG" , STR0005, "number") //Saída
    self:finAddProperty("SALDO_INICIAL_DIARIO" , STR0006, "number") //Saldo inicial diário
    self:finAddProperty("CLASSIFICACAO" , STR0007, "string") //Classificação
  
    FwFreeArray(aFieldsFK5)
    
return


//-------------------------------------------------------------------
/*{Protheus.doc} loadStatement
Retorna a query do objeto de negócio
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadStatement() class RealizedCashFlowTReportsBusinessObject
    Local cQuery := "" as character
    Local nOrdem := 1  as numeric

    self:aStruct := {}
    aAdd(self:aStruct, FWSX3Util():GetFieldStruct( "FK5_DATA" ) )
            
    cQuery := "SELECT ?, FK5.FK5_BANCO BANCO, FK5.FK5_AGENCI AGENCIA, FK5.FK5_CONTA CONTA, "
    cQuery += " FK5.FK5_TPDOC TIPODOC, FK5.FK5_TXMOED TAXA_MOVIMENTO, SED.ED_DESCRIC DESCRICAO_NATUREZA "
    cQuery += "FROM " + retSQLName("FK5") + " FK5 "
    cQuery += "INNER JOIN " + retSQLName("SED") + " SED "
    cQuery += "ON " + FWJoinFilial("FK5","SED") + " 
    cQuery += "AND FK5.FK5_NATURE = SED.ED_CODIGO "
    cQuery += "AND SED.D_E_L_E_T_ = ? "
    cQuery += "WHERE "    
    cQuery += self:getSQLBranchFilter()

    if !Empty(self:cBanco)
        cQuery += "AND FK5.FK5_BANCO = ? "
        cQuery += "AND FK5.FK5_AGENCI = ? "
        cQuery += "AND FK5.FK5_CONTA = ? "
    endIf

    cQuery += "AND FK5.FK5_DATA BETWEEN ? AND ? "
    cQuery += "AND FK5.D_E_L_E_T_ = ? "

    cQuery += self:getFilterToSQL()

    if !Empty(self:cBanco)
        cQuery += " ORDER BY FK5_DATA, FK5_NATURE, FK5_RECPAG "
    else
        cQuery += " ORDER BY FK5_BANCO, FK5_AGENCI, FK5_CONTA, FK5_DATA, FK5_NATURE, FK5_RECPAG "
    endIf

    cQuery := ChangeQuery(cQuery)

    self:oStatement:= FWExecStatement():New(cQuery)
    
    self:oStatement:SetUnsafe(nOrdem++, self:getAllFieldsToSQL())
    self:oStatement:SetString(nOrdem++, " ")
    self:setStatementBranchFilter(@nOrdem)    

    if !Empty(self:cBanco)
        self:oStatement:SetString(nOrdem++, self:cBanco)
        self:oStatement:SetString(nOrdem++, self:cAgencia)
        self:oStatement:SetString(nOrdem++, self:cConta)
    endIf

    self:oStatement:SetDate(nOrdem++, self:dDataDe)
    self:oStatement:SetDate(nOrdem++, self:dDataAte)
    self:oStatement:SetString(nOrdem++, " ")
       
return

//-------------------------------------------------------------------
/*{Protheus.doc} preWhileMyAliasToData

@author Fábio Henrique Andrade
@since 12/01/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method preWhileMyAliasToData() class RealizedCashFlowTReportsBusinessObject

    self:nMoedaMovimento := self:getMoedaBanco()
    self:dConversaoMoeda := dDatabase
    self:nTaxaMoedaMovimento := 0

    self:nSaldoDiaAnterior := self:nSaldoInicial := self:getOpeningBalance()

    self:dDiaAtual := (self:cAlias)->FK5_DATA
    self:nSaldoDiaAtual := 0
return

//-------------------------------------------------------------------
/*{Protheus.doc} getOpeningBalance
    @description Retorna o saldo inicial do período somando todos os bancos filtrados
    @author guilherme.sordi@totvs.com.br
    @since 20/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getOpeningBalance() as numeric class RealizedCashFlowTReportsBusinessObject
    local oBanks := Banks():new() as object
    local aRecnos := self:getSa6Recnos()
    local nX as numeric
    local nY as numeric
    local nBalance as numeric

    oBanks:setBranchFilter(self:aMultiBranches)
    oBanks:setCurrency(self:nMoedaMovimento)
    for nX := 1 to len(aRecnos)
        aBalances := oBanks:getBankBalances(aRecnos[nX], self:dDataDe)
        for nY := 1 to len(aBalances)
            nBalance += aBalances[nY]['e8_salatua']
        next
    next
return nBalance

//-------------------------------------------------------------------
/*{Protheus.doc} getSa6Recnos
    @description Retorna uma lista com os RECNOS de SA6 localizados para o 
    filtro de filial e banco
    @author guilherme.sordi@totvs.com.br
    @since 20/11/2024
    @version 12.1.2410
*/
//-------------------------------------------------------------------
method getSa6Recnos() as array class RealizedCashFlowTReportsBusinessObject
    local aRecnos := {} as array
    local oStBank := FWExecStatement():new() as object
    local nParamOrder := 1 as numeric
    local cQuery as character
    local cAlias as character

    cQuery := " SELECT R_E_C_N_O_ RECNO "
    cQuery += " FROM " + retSQLName("SA6") + " SA6 "
    cQuery += " WHERE "
    cQuery += " A6_FILIAL IN (?) "
    
    if !empty(self:cBanco)
        cQuery += " AND A6_COD = ? "
        cQuery += " AND A6_AGENCIA = ? "
        cQuery += " AND A6_NUMCON = ? "
    Endif

    cQuery += " AND D_E_L_E_T_ = ? "

    oStBank:setQuery(changeQuery(cQuery))
    oStBank:setIn(nParamOrder++, self:getAliasBranchFilter("SA6"))    
    if !empty(self:cBanco)
        oStBank:setString(nParamOrder++, self:cBanco)
        oStBank:setString(nParamOrder++, self:cAgencia)
        oStBank:setString(nParamOrder++, self:cConta)
    endif
    oStBank:setString(nParamOrder++, ' ')

    cAlias := oStBank:openAlias()
    aRecnos := self:aliasToSimpleArray(cAlias, "RECNO")
    (cAlias)->(dbCloseArea())

return aRecnos

//-------------------------------------------------------------------
/*{Protheus.doc} handleData
Atribui ao objeto oData o conteúdo do Alias e campos calculados.
@param oFilter, objeto, contém o filtro do TReports
@author Pâmela Bernardo
@since 23/05/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method handleData(oFilter as object) class RealizedCashFlowTReportsBusinessObject    
    local cNatureza := "" as character
    local nValRec := 0 as numeric
    local nValPag := 0 as numeric
    local cClassificacao := "" as character

    cNatureza := self:oFinClassUtil:mascNat( (self:cAlias)->FK5_NATURE ) + " - " + allTrim( (self:cAlias)->DESCRICAO_NATUREZA )
    cClassificacao := (self:cAlias)->TIPODOC + (self:cAlias)->FK5_RECPAG

    if self:jClassificacao:hasProperty(cClassificacao)
        cClassificacao := self:jClassificacao[cClassificacao]
    else
        cClassificacao := "N/A"
    endIf

    nValRec := 0
    nValPag := 0

    if (self:dDiaAtual != (self:cAlias)->FK5_DATA)
        self:nSaldoDiaAnterior += self:nSaldoDiaAtual
        self:nSaldoDiaAtual := 0
        self:dDiaAtual := (self:cAlias)->FK5_DATA
    endIf

    if (self:cAlias)->FK5_RECPAG == "R"
        nValRec := (self:cAlias)->FK5_VALOR
    else
        nValPag := (self:cAlias)->FK5_VALOR
    endIf            
    self:nSaldoDiaAtual += nValRec - nValPag

    if self:lConverterPorDataMovimento
        self:nTaxaMoedaMovimento := (self:cAlias)->TAXA_MOVIMENTO
        self:dConversaoMoeda := (self:cAlias)->FK5_DATA
    endIf

    self:jData := {;
        "FK5_VALOR": self:converteMoeda((self:cAlias)->FK5_VALOR),;
        "FK5_NATURE": cNatureza,;
        "SALDO_INICIAL": self:converteMoeda(self:nSaldoInicial),;
        "VAL_REC": self:converteMoeda(nValRec),;
        "VAL_PAG": self:converteMoeda(nValPag),;
        "SALDO_INICIAL_DIARIO": self:converteMoeda(self:nSaldoDiaAnterior),;
        "CLASSIFICACAO": cClassificacao;
    }

return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters

Se não receber o objeto de filtro limpa as propriedas de filtro da classe.
Se receber o objeto de filtro, carrega os parâmetros informados pelo usuário para as variáveis públicas MV_PAR
e atualiza as propriedades de filtro da classe.

As propriedades de filtro da classe ajudam a compreesão do código (boas práticas de código limpo).

@param oFilter, objeto, contém o filtro do TReports
@author guilherme.sordi@totvs.com.br
@since 23/06/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------
method loadParameters() class RealizedCashFlowTReportsBusinessObject   

    if self:oCurrentFilter != NIL
        self:dDataDe := MV_PAR01
        self:dDataAte := MV_PAR02
        self:cBanco := PadR(MV_PAR03, TamSx3("A6_COD")[1], " ")
        self:cAgencia := PadR(MV_PAR04, TamSx3("A6_AGENCIA")[1], " ")
        self:cConta := PadR(MV_PAR05, TamSx3("A6_NUMCON")[1], " ")
        self:nMoeda := self:getCurrencyValue(MV_PAR07)
        self:lConverterPorDataMovimento := MV_PAR08 == 1
    endIf

return

/*/{Protheus.doc} setDefaultParameters
    @author Fábio Henrique Andrade
    @since 22/12/2023
    @version 12.1.2310
*/
method setDefaultParameters() class RealizedCashFlowTReportsBusinessObject

    self:dDataDe := dDatabase
    self:dDataAte := dDatabase
    self:cBanco := self:cAgencia := self:cConta := ""
    self:lConverterPorDataMovimento := .F.

return

//-------------------------------------------------------------------
/*{Protheus.doc} converteMoeda
Concentra toda a regra de negócio para a conversão de moedas.
Todos os cálculos do objeto de negócio são feitos na moeda do título 
e a conversão para a moeda parametrizada pelo usuário é a última etapa
da extração de dados.

@param nValor, numeric, Valor a ser convertido para a moeda de apresentação
@author guilherme.sordi@totvs.com.br
@since 23/05/2023
@version 12.1.2210  
*/
//-------------------------------------------------------------------
method converteMoeda(nValor as numeric) as numeric class RealizedCashFlowTReportsBusinessObject
    local nValorConvertido := 0 as numeric

    nValorConvertido := xMoeda(nValor, self:nMoedaMovimento, self:nMoeda, self:dConversaoMoeda, self:nDecimals, self:nTaxaMoedaMovimento)

return nValorConvertido


//-------------------------------------------------------------------
/*{Protheus.doc} getMoedaBanco
    Retorna a moeda do banco definido pelos parâmetros.
    @author guilherme.sordi@totvs.com.br
    @since 27/06/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method getMoedaBanco() as numeric class RealizedCashFlowTReportsBusinessObject
    local aArea as array
    local aAreaSA6 as array
    local nMoedaBanco := 1 as numeric

    aArea := getArea()
    aAreaSA6 := SA6->(getArea())

    SA6->(DbSetOrder(1))
    if SA6->(MSSeek(FWXFilial("SA6") + self:cBanco + self:cAgencia + self:cConta))
        nMoedaBanco := SA6->A6_MOEDA
    endIf

    restArea(aAreaSA6)
    restArea(aArea)

    FwFreeArray(aAreaSA6)
    FWFreeArray(aArea)
return nMoedaBanco

//-------------------------------------------------------------------
/*{Protheus.doc} loadClassificacoes
    @author fabioh.andrade@totvs.com.br
    @since 22/12/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadClassificacoes() class RealizedCashFlowTReportsBusinessObject
    self:jClassificacao := JSONObject():new()

    //TIPO + CARTEIRA
    self:jClassificacao["APP"] := STR0008 //"Aplicações Financeiras"
    self:jClassificacao["APR"] := STR0009 //"Estorno de Aplicações"
    self:jClassificacao["RFR"] := STR0010 //"Resgate de Aplicações"
    self:jClassificacao["RFP"] := STR0011 //"Estorno de Resgate de Aplicações"
    self:jClassificacao["EPR"] := STR0012 //"Empréstimos Recebidos"
    self:jClassificacao["EPP"] := STR0013 //"Estorno de Emprestimos Recebidos"
    self:jClassificacao["PEP"] := STR0014 //"Pagamento de Empréstimos"
    self:jClassificacao["PER"] := STR0015 //"Estorno de Pagamento de Empréstimos"
    self:jClassificacao["JRP"] := STR0016 //"Juros Pagos"
    self:jClassificacao["J2P"] := STR0016 //"Juros Pagos"
    self:jClassificacao["TLP"] := STR0016 //"Juros Pagos"
    self:jClassificacao["JRR"] := STR0017 //"Juros Recebidos"
    self:jClassificacao["J2R"] := STR0017 //"Juros Recebidos"
    self:jClassificacao["TLR"] := STR0017 //"Juros Recebidos"
    self:jClassificacao["MTP"] := STR0018 //"Multas Pagas"
    self:jClassificacao["M2P"] := STR0018 //"Multas Pagas"
    self:jClassificacao["MTR"] := STR0019 //"Multas Recebidas"
    self:jClassificacao["M2R"] := STR0019 //"Multas Recebidas"
    self:jClassificacao["DCP"] := STR0020 //"Descontos Obtidos"
    self:jClassificacao["D2P"] := STR0020 //"Descontos Obtidos" 
    self:jClassificacao["DCR"] := STR0021 //"Descontos Concedidos"
    self:jClassificacao["D2R"] := STR0021 //"Descontos Concedidos"
    self:jClassificacao["TTR"] := STR0022 //"Transferências Recebidas"
    self:jClassificacao["TTP"] := STR0023 //"Transferências Realizadas"

return 
