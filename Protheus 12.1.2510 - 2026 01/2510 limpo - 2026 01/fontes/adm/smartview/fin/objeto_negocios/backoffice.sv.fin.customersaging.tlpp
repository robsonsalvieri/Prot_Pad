#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider


@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Aging Clientes",; 
customTables="SA1,SED,SE1", country="ALL")

//-------------------------------------------------------------------
    /*/{Protheus.doc} CustomersAgingSmartViewBusinessObject
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------

class CustomersAgingSmartViewBusinessObject from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject
    public method new() as object
    protected method handleStatement() 
    protected method loadParameters()
    protected method getWhere() as character
    protected method initializeBusinessObject()
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class CustomersAgingSmartViewBusinessObject
    _Super:new()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} initializeBusinessObject()
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method initializeBusinessObject() class CustomersAgingSmartViewBusinessObject
    self:setDisplayName(STR0024) //"Aging Clientes"
    self:setDescription(STR0024) //"Aging Clientes"
    self:setPergunte("FINT023")
return


//-------------------------------------------------------------------
/*{Protheus.doc} getWhere
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getWhere() as character class CustomersAgingSmartViewBusinessObject
    Local cWhere := "" as character

    cWhere += " AND SE1.E1_CLIENTE BETWEEN ? AND  ? "
    cWhere += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "

    if !self:lListarOutrasMoedas
        cWhere += "AND SE1.E1_MOEDA = ? " 
    endIf

    if self:lVisaoRetroativa
        cWhere += "AND SE1.E1_EMISSAO <= ? "
    else
        if !self:lMostraSaldoZero
            cWhere += "AND SE1.E1_SALDO > ? "
        endIf
    endIf
return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} handleStatement
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method handleStatement()  class CustomersAgingSmartViewBusinessObject

    self:oStatement:SetString(self:nParamOrder++, self:cCustomerFrom)
    self:oStatement:SetString(self:nParamOrder++, self:cCustomerTo)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateFrom)
    self:oStatement:SetDate(self:nParamOrder++, self:dIssueDateTo)

    if !self:lListarOutrasMoedas
        self:oStatement:SetNumeric(self:nParamOrder++, self:nMoeda)
    endIf
    
    if self:lVisaoRetroativa
        self:oStatement:SetDate(self:nParamOrder++, self:dVisaoRetroativa)
    else
        if !self:lMostraSaldoZero
            self:oStatement:SetNumeric(self:nParamOrder++, 0)
        endIf
    endIf

return


//-------------------------------------------------------------------
/*{Protheus.doc} loadParameters
    @author matheus.monteiro@totvs.com.br
    @since 17/11/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method loadParameters() class CustomersAgingSmartViewBusinessObject      
    if self:oCurrentFilter  != NIL

        self:cCustomerFrom := MV_PAR01
        self:cCustomerTo := MV_PAR02  
        self:dIssueDateFrom  := MV_PAR03
        self:dIssueDateTo   := MV_PAR04
        self:nMoeda := self:getCurrencyValue(MV_PAR05)
        self:lListarOutrasMoedas := MV_PAR06 <> 1

        if self:lVisaoRetroativa := ( MV_PAR07 == 1 )
            self:dVisaoRetroativa := MV_PAR08
        endIf
    endIf
return


