#INCLUDE 'msobject.ch'
#INCLUDE 'topconn.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'

namespace custom.financeiro.paymentorderaccounting.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} paymentorderaccountingTReportsBusinessObjectMEX
@description Classe para creación del Objeto de Negocio de Ordenes de pago Conciliadas para TReports MEXICO
@author Marcelo Hruschka
@since 30/10/2024
@version 1.0 
*/
//-------------------------------------------------------------------------------

@totvsFrameworkTReportsIntegratedProvider( active=.F., team='SIGAFIN', tables='SA2,SEK', name='Ordenes de pago Conciliadas', country='MEX', initialRelease='12.1.2410', customTables='SA2,SEK,CT2' )
class paymentorderaccountingTReportsBusinessObjectMEX from custom.financeiro.paymentorderaccounting.integratedprovider.paymentorderaccountingTReportsBusinessObject
	public method new() as object
	public method preData() as object
	public method processData() as object
	public method preSchema() as object
endclass

method new() class paymentorderaccountingTReportsBusinessObjectMEX
	_Super:new()
return self

method preData() as object class paymentorderaccountingTReportsBusinessObjectMEX
return self:oData

method processData() as object class paymentorderaccountingTReportsBusinessObjectMEX

	Local cQuery as character
	Local oExecMEX as object
	Local nX as numeric
    Local cAliasMEX as character

    aFieldsMEX := { 'EK_UUID','EK_FECTIMB','EK_VALIMP1' }
	aStructMEX := getStrutObj( aFieldsMEX, {} )

	cQuery := "SELECT SEK.EK_UUID, SEK.EK_FECTIMB, SEK.EK_VALIMP1"
	cQuery += "   FROM " + RetSqlName("SEK") + " SEK"
	cQuery += "   WHERE SEK.R_E_C_N_O_ = ?
	cQuery := ChangeQuery(cQuery)
	oExecMEX := FwExecStatement():New( cQuery )
    oExecMEX:SetString( 01, AllTrim(Str(self:nRecnoSEK)) )
	cAliasMEX := oExecMEX:OpenAlias( )
	For nX := 1 To Len(aStructMEX)
		If ( aStructMEX[nX][3] == 'date' )
			self:jItems[aStructMEX[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasMEX )->&( aStructMEX[nX][5] ) ) )
		Else
			self:jItems[aStructMEX[nX][1]] := ( cAliasMEX )->&( aStructMEX[nX][5] )
		Endif
	Next

	// fecha tabela
	( cAliasMEX )->( DbCloseArea( ) )
	oExecMEX:Destroy( )
	oExecMEX := Nil

return

method preSchema() as object class paymentorderaccountingTReportsBusinessObjectMEX

	Local nX as numeric

	aFieldsMEX := { 'EK_UUID','EK_FECTIMB','EK_VALIMP1' }
	aStructMEX := getStrutObj( aFieldsMEX, {} )
	For nX := 1 To Len(aStructMEX)
		self:addProperty( aStructMEX[nX][1], aStructMEX[nX][2], aStructMEX[nX][3], aStructMEX[nX][4], aStructMEX[nX][5] )
	Next

return self:oSchema

