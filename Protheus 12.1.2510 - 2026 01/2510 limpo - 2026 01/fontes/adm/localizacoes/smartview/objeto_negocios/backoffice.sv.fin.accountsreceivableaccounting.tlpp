#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.accountsreceivable.accounting.ch'

namespace custom.financeiro.accountsreceivableaccounting.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fin.salesinvoice.accounting
@description Classe para creación del Objeto de Negocio de Recibos de cobro conciliadas para TReports
@author Marcelo Hruschka
@since 31/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SA1,SEL', name='Recibos de cobro Conciliadas', country='ALL', initialRelease='12.1.2410', customTables='SA1,SEL,CT2' )
class accountsreceivableaccountingTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecnoSEL as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class accountsreceivableaccountingTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Recibos de cobro Conciliadas'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Recibos de cobro Conciliadas'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financieiro'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV510' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'M0_CGC'	    , STR0007 , 'string', STR0007 	} ) // 'RFC Emisor' 
	aAdd( aCpos, { 'M0_NOMECOM'	, STR0008 , 'string', STR0008	} ) // 'Nombre Emisor'
	aAdd( aCpos, { 'EL_ALQIM1P' , STR0009 , 'number', STR0009	} ) // 'Tasa Imp Peso'
	aAdd( aCpos, { 'EL_VALIM1P' , STR0010 , 'number', STR0010	} ) // 'Val Imp Peso' 
	aAdd( aCpos, { 'EL_XCANCEL' , STR0017 , 'string', STR0017	} ) // 'Forma de Pago'
	aAdd( aCpos, { 'EL_FORPGOP' , STR0016 , 'string', STR0016	} ) // 'Forma de Pago'
	aAdd( aCpos, { 'EL_BANCOP'  , STR0011 , 'string', STR0011	} ) // 'Banco Pago'
	aAdd( aCpos, { 'CT2_DOCLSL' , STR0012 , 'string', STR0012   } ) // 'Doc/Lote/Sublote' 
	aAdd( aCpos, { 'CT2_DTACTB' , STR0013 , 'date'  , STR0013   } ) // 'Fecha de Asiento'
	aAdd( aCpos, { 'CT2_TPSLD'  , STR0014 , 'string', STR0014	} ) // 'Tipo Saldo' 
	aAdd( aCpos, { 'CT2_HISTO'  , STR0015 , 'string', STR0015 	} ) // 'Historial' 
	

	// demais campos
	self:aFields := {'M0_CGC','M0_NOMECOM','A1_CGC','A1_NOME','EL_EMISSAO','EL_VALOR','EL_VLMOED1','EL_VALIM1P','EL_ALQIM1P','EL_MOEDA','EL_TXMOEDA','EL_TIPODOC','EL_NUMERO','EL_SERIE','EL_CLIORIG','EL_LOJORIG','EL_CLIENTE','EL_LOJA','EL_RECIBO','EL_PREFIXO','EL_FILIAL','EL_TIPO','EL_PARCELA','EL_FORPGOP','EL_BANCOP','EL_XCANCEL','CT2_DOCLSL','CT2_DTACTB','CT2_TPSLD','CT2_HISTO'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class accountsreceivableaccountingTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character
	Local cExp 			as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local oExecB        as object
	Local cAliasA       as character
	Local cAliasB       as character
	Local cChaveCTL     as character

	Local cFilialCTL    as character
	Local cFilialCV3    as character

	cFilialCTL := xFilial("CTL")
	cFilialCV3 := xFilial("CV3")

	cChaveCTL := AllTrim(POSICIONE("CTL",1,xFilial("CTL")+"575","CTL_KEY"))
	cChaveCTL := IIF("SQL" $ TcGetDB(),cChaveCTL,StrTran(cChaveCTL,"+","||"))

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			    := ''
	cAliasB			    := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )
	cCpoSEL := getCpoUser( aCustomFields, 'SEL', 'EL', ',' )
	cCpoCT2 := getCpoUser( aCustomFields, 'CT2', 'CT2', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT"
	cQuery += " SEL.EL_RECIBO,SEL.EL_CLIORIG,SEL.EL_LOJORIG,SA1.A1_LOJA,SA1.A1_NOME,SA1.A1_CGC,SEL.EL_TIPO,SEL.EL_PREFIXO,SEL.EL_NUMERO,SEL.EL_PARCELA,? "
	cQuery += " SEL.EL_VALOR,SEL.EL_MOEDA,SEL.EL_EMISSAO,SEL.EL_VLMOED1,SEL.EL_BANCO,SEL.EL_AGENCIA,SEL.EL_CONTA,SEL.EL_VALIMP1,SEL.EL_ALQIMP1,? "
	cQuery += " SM0.M0_CGC,SM0.M0_NOMECOM,SEL.EL_TXMOEDA,SEL.EL_TIPODOC,SEL.EL_SERIE,SEL.EL_CLIENTE, SEL.EL_LOJA,"
	cQuery += " SEL.EL_FILIAL, ROUND(SEL.EL_VALIMP1 * SEL.EL_TXMOEDA,2) EL_VALIM1P,"
	cQuery += " ROUND(SEL.EL_ALQIMP1 * SEL.EL_TXMOEDA,2) EL_ALQIM1P, SEL.EL_CANCEL EL_XCANCEL,"
	cQuery += " SELP.EL_TIPODOC EL_FORPGOP,"
	cQuery += " SELP.EL_BANCO||'/'||SELP.EL_AGENCIA||'/'||SELP.EL_CONTA EL_BANCOP,"
	cQuery += " ?  CHVE, SEL.R_E_C_N_O_  REGSEL"
	cQuery += " FROM " + RetSqlName("SEL") + " SEL "
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = SEL.EL_CLIORIG AND SA1.A1_LOJA = SEL.EL_LOJORIG AND SA1.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN SYS_COMPANY SM0  ON SM0.M0_CODIGO = ? AND SM0.M0_CODFIL = ? AND SM0.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SEL") + " SELP ON SELP.EL_FILIAL = ? AND SELP.EL_RECIBO = SEL.EL_RECIBO AND SELP.EL_TIPODOC IN ( ? ) AND SELP.D_E_L_E_T_ = ? "
	cQuery += " WHERE SEL.EL_FILIAL= ?"
	cQuery += "   AND SEL.EL_EMISSAO BETWEEN ? AND ?"
	cQuery += "   AND SEL.EL_CLIORIG BETWEEN ? AND ?"
	cQuery += "   AND SEL.EL_TIPODOC = ?"
	cQuery += "   AND SEL.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next
	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	
	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSA1)
	oExecA:SetUnSafe( nSeq += 1, cCpoSEL)
	oExecA:SetUnSafe( nSeq += 1, StrTran(cChaveCTL,"EL_","SEL.EL_"))
	oExecA:SetString( nSeq += 1, xFilial("SA1") )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SEL") )
	oExecA:SetIn( nSeq += 1, {'CH','TF','EF','CO','CC'} )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SEL") )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )
	oExecA:SetString( nSeq += 1, 'TB' )
	oExecA:SetString( nSeq += 1, ' ' )
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	// ConOut(oExecA:getFixQuery( ))
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecnoSEL := ( cAliasA )->REGSEL

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf LEFT(self:aStruct[nx][5],4) = "CT2_"

				cQuery := "SELECT CT2_LOTE,CT2_SBLOTE,CT2_DOC, CT2_DATA CT2_DTACTB, ?" 
				cQuery += " CT2_TPSALD  CT2_TPSLD, CT2_HIST CT2_HISTO"
				cQuery += " FROM " + RetSqlName("CT2") + " CT2"
				cQuery += " INNER JOIN " + RetSqlName("CTL") + " CTL ON CTL_FILIAL = ? AND CTL_LP = ? AND CTL.D_E_L_E_T_ = ?"
				cQuery += " INNER JOIN " + RetSqlName("CV3") + " CV3 ON CV3_FILIAL = ? AND CV3_KEY = ? AND CV3.D_E_L_E_T_ = ? "
				cQuery += " WHERE CV3.CV3_RECDES <> ? "
				cQuery += "   AND CT2.R_E_C_N_O_ = CAST(CV3.CV3_RECDES AS ? )"   
				cQuery += "   AND CT2.D_E_L_E_T_ = ?"
				cQuery := ChangeQuery(cQuery)
				oExecB := FwExecStatement():New( cQuery )
				oExecB:SetUnSafe( 01, cCpoCT2)
				oExecB:SetString( 02, cFilialCTL )
				oExecB:SetString( 03, "575" )
				oExecB:SetString( 04, ' ' )
				oExecB:SetString( 05, cFilialCV3 )
				oExecB:SetString( 06, ( cAliasA )->CHVE )
				oExecB:SetString( 07, ' ' )
				oExecB:SetString( 08, ' ' )
				oExecB:SetUnsafe( 09, IIF("ORACLE" $ TcGetDB(),"INT","BIGINT"))
				oExecB:SetString( 10, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cAliasB := oExecB:OpenAlias( )
				If ( self:aStruct[nX][3] == 'date' )
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasB )->&( self:aStruct[nX][5] ) ) )
				ElseiF self:aStruct[nX][5] == "CT2_DOCLSL"
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->CT2_LOTE+"/"+( cAliasB )->CT2_SBLOTE+"/"+( cAliasB )->CT2_DOC					
				Else
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
				Endif

				// fecha tabela
				( cAliasB )->( DbCloseArea( ) )
				oExecB:Destroy( )
				oExecB := Nil

			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][1] == 'EL_XCANCEL' )
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "T"
				   self:jItems[self:aStruct[nX][1]] := "Si"
				Else
					self:jItems[self:aStruct[nX][1]] := "No"
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class accountsreceivableaccountingTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )

