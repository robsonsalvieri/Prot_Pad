#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'backoffice.sv.fin.cheques.ch'

namespace custom.financeiro.cheques.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} ChequesTReportsBusinessObject     
Classe para creación del Objeto de Negocio de compras para TReports
@author Cristian Gustavo
@since 21/07/2023
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIN", name="Cheques", tables="SEF, SE1, FRF, SX5", country="ALL", initialRelease="12.1.2310" )
class ChequesTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Method new() 		   as Object
	Public Method getData() 	   as Object
	Public Method getSchema() 	   as Object

	Protected data aFields 		as Array
	Protected data aStruct 		as Array

	Protected data jItems		as Json

endclass

//-------------------------------------------------------------------
	/*{Protheus.doc} new
	Método de instância da classe
	 
	@Return object: self
	 
	@author Cristian Gustavo
	@since 21/07/2023
	@version 1.0
	*/
//-------------------------------------------------------------------
Method new(oFilter as object) as object class ChequesTReportsBusinessObject

	Local aCpos 	as Array

	aCpos	 := {}

	_Super:new()

	// Define a Área
	self:appendArea(STR0001) // "Financiero"

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0010 ) // "Cheques"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0010 ) // "Cheques"

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte('FINSV231') // Indica o pergunte que será utilizado
		IIf(!self:setErrorStatus( 400, STR0002, STR0003 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0004,,, ),'') //'Sin Preguntas' //'¡Verifique el grupo de preguntas dado!' //'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0005 ,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	//Somente para cheques emitidos
	aAdd( aCpos, { 'EF_IMPCHQ', STR0006, 'string', STR0007 } ) // 'Imprimir cheques usados?' # 'Imp.Cheques usados'

	//Cheques emitidos e cheques recebidos
	aAdd( aCpos, { 'FRF_MOTIVO', STR0008 , 'string', STR0008 } ) // 'Situación'
	aAdd( aCpos, { 'EF_NOMBCO' , STR0009 , 'string', STR0009 } ) // 'Nombre Banco Cheque'

	self:aFields := { "EF_BANCO", "EF_AGENCIA", "EF_CONTA", "EF_CART", "EF_TALAO", "EF_NOMBCO", "EF_NUM", "EF_VALOR", "EF_DATA", "EF_VENCTO", "EF_FORNECE", "EF_LOJA", "EF_BENEF", "EF_STATUS", "FRF_MOTIVO", "FRF_DATDEV", "FRF_DATPAG", 'EF_IMPCHQ'}

	self:aStruct := getStrutObj( self:aFields, aCpos, .T. )

Return(self)

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param oFilter, objeto, contiene el filtro del TReports
@Return object: self:oData
@author Cristian Gustavo
@since 21/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData( nPage as numeric, oFilter as object ) as object class ChequesTReportsBusinessObject

	Local aPDFields 	as Array
	Local aFiltro		as Array
	Local aCpos			as Array

	Local lObfuscated	as Logical

	Local jItems 		as json

	Local cAlias 		:= GetNextAlias() as character
	Local cFiltro		as Character
	Local cExp			as Character
	Local cQuery		as Character

	Local cCampo		as Character
	Local cCodBanco		as Character
	Local cCodPostal	as Character
	Local cCodAgenc		as Character
	Local cCodCuenta	as Character
	Local cOpcQuerry 	as Character
	Local cImpChqU      as Character

	Local nSkip 		as Numeric
	Local nX 			as Numeric
	Local nPosExp       as Numeric
	Local nSeq       as Numeric

	Local oQuery		as object

	aPDFields 		:= {}
	aFiltro			:= {}
	aCpos			:= {}
	lObfuscated		:= Len( aPDFields ) != Len( self:aFields )

	cExp			:= ''
	cQuery			:= ''
	cImpChqU        := 'N'

	cCampo			:= ''

	cCodBanco		:= ''
	cCodAgenc		:= ''
	cCodCuenta		:= ''
	cCodPostal		:= ''
	cOpcQuerry		:= '1'

	nSkip 			:= 0
	nX 				:= 0
	nPosExp        := 0

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	If oFilter:hasFilter()
		cFiltro := oFilter:getSQLExpression( )

		// Converte string de parametros em array
		aFiltro := getParamToArr( cFiltro )

		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )

		//Campo virtual para ¿Imprimir cheques no usados ?
		nPosExp := aScan( aFiltro, { | x | AllTrim( x[1] ) == 'EF_IMPCHQ' } )
		If ( nPosExp > 0 )
			cImpChqU := SubStr( aFiltro[nPosExp, 3], 2, 1 )
		EndIf

		// Realiza tratamento de filtro para limpar os que não serão utilizados na query
		If ( nPosExp > 0 )
			// Exclui o elemento do Array
			aDel( aFiltro, nPosExp )

			// Redimensiona o Array
			aSize( aFiltro, ( Len( aFiltro ) - 1 ) )
		EndIf

		// Campo virtual a ser localizado
		nPosExp := aScan( aFiltro, { | x | AllTrim( x[1] ) == 'EF_CART' } )
		If ( nPosExp > 0 )
			cOpcQuerry := SubStr( aFiltro[nPosExp, 3], 2, 1 )
		EndIf

		// Realiza tratamento de filtro para limpar os que não serão utilizados na query
		If ( nPosExp > 0 )
			// Exclui o elemento do Array
			aDel( aFiltro, nPosExp )

			// Redimensiona o Array
			aSize( aFiltro, ( Len( aFiltro ) - 1 ) )
		EndIf
	EndIf

	If jParams:HasProperty('MV_PAR08')
	   cImpChqU := IIF(jParams['MV_PAR08'][1]==1,"S","N")
	Endif

	If cOpcQuerry == "1" //Cheques recebidos
		cQuery += "SELECT "
		cQuery += "SEF.EF_BANCO, SEF.EF_AGENCIA, SEF.EF_CONTA, SE1.E1_CONTA, SEF.EF_CART, SEF.EF_TALAO, FJO.FJO_NOME, SEF.EF_NUM, SEF.EF_VALOR, "
		cQuery += "SEF.EF_DATA, SEF.EF_VENCTO, SEF.EF_FORNECE, SEF.EF_LOJA, SEF.EF_BENEF, SEF.EF_SUBSCHE, SEF.EF_STATUS, FRF.FRF_MOTIVO, FRF.FRF_DATDEV, FRF.FRF_DATPAG, "
		cQuery += "' ' EF_IMPCHQ "
		cQuery += "FROM " + RetSqlName("SEF") + " SEF "
		cQuery += "INNER JOIN " + RetSqlName("SE1") + " SE1 "
		cQuery += "   ON SE1.E1_FILIAL   = ? "
		cQuery += "      AND SE1.E1_NUM = SEF.EF_NUM "
		cQuery += "      AND SE1.E1_CLIENTE = SEF.EF_CLIENTE "
		cQuery += "      AND SE1.E1_LOJA = SEF.EF_LOJACLI "
		cQuery += "      AND SE1.E1_BCOCHQ = SEF.EF_BANCO "
		cQuery += "      AND SE1.E1_AGECHQ = SEF.EF_AGENCIA "
		cQuery += "      AND SE1.E1_CTACHQ = SEF.EF_CONTA "
		cQuery += "      AND SE1.E1_RECIBO = SEF.EF_TITULO "
		cQuery += "      AND SE1.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("FRF") + " FRF "
		cQuery += "   ON FRF.FRF_FILIAL = ? "
		cQuery += "      AND FRF.FRF_BANCO = SEF.EF_BANCO "
		cQuery += "  	  AND FRF.FRF_AGENCI = SEF.EF_AGENCIA "
		cQuery += "  	  AND FRF.FRF_CONTA = SEF.EF_CONTA "
		cQuery += "  	  AND FRF.FRF_PREFIX = SEF.EF_PREFIXO "
		cQuery += "  	  AND FRF.FRF_NUM = SEF.EF_NUM "
		cQuery += "  	  AND FRF.FRF_CART = SEF.EF_CART "
		cQuery += "  	  AND FRF.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("FJO") + " FJO "
		cQuery += "   ON FJO.FJO_FILIAL = ? "
		cQuery += "      AND FJO.FJO_COD = SEF.EF_BANCO "
		cQuery += "      AND FJO.D_E_L_E_T_ = ? "
		cQuery += "WHERE SEF.EF_FILIAL = ? "
		cQuery += "   AND EF_BANCO = ? "
		cQuery += "   AND EF_AGENCIA = ? "
		cQuery += "   AND SEF.EF_CART = ? "
		cQuery += "   AND EF_DATA BETWEEN ? AND ? "
		cQuery += "   AND EF_VENCTO BETWEEN ? AND ? "
		cQuery += "   AND SEF.D_E_L_E_T_ = ? "

		// Agrega os filtros do SMARTVIEW na QUERY
		For nX := 1 To Len( aFiltro )
			If !AllTrim(aFiltro[nX, 1]) $ 'EF_CART,EF_IMPCHQ'
				cQuery += " AND ? " + ' ' + " ? ? "
			Endif
		Next

		cQuery += "ORDER BY ? "
	Else // Cheques emitidos
		cQuery += "SELECT "
		cQuery += "SEF.EF_BANCO, SEF.EF_AGENCIA , SEF.EF_CONTA, SEF.EF_CART, SEF.EF_TALAO, FJO.FJO_NOME, SEF.EF_NUM, SEF.EF_VALOR, SEF.EF_DATA, SEF.EF_VENCTO, "
		cQuery += "SEF.EF_FORNECE, SEF.EF_LOJA, SEF.EF_BENEF, SEF.EF_STATUS, SEF.EF_SUBSCHE, FRF.FRF_MOTIVO, FRF.FRF_DATDEV, FRF.FRF_DATPAG, "
		cQuery += "' '  EF_IMPCHQ "
		cQuery += "FROM " + RetSqlName("SEF") + " SEF "
		cQuery += "LEFT JOIN " + RetSqlName("FRF") + " FRF "
		cQuery += "   ON FRF.FRF_FILIAL = ? "
		cQuery += "      AND FRF.FRF_BANCO = SEF.EF_BANCO "
		cQuery += "  	  AND FRF.FRF_AGENCI = SEF.EF_AGENCIA "
		cQuery += "  	  AND FRF.FRF_CONTA = SEF.EF_CONTA "
		cQuery += "  	  AND FRF.FRF_PREFIX = SEF.EF_PREFIXO "
		cQuery += "  	  AND FRF.FRF_NUM = SEF.EF_NUM "
		cQuery += "  	  AND FRF.FRF_CART = SEF.EF_CART "
		cQuery += "  	  AND FRF.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("FJO") + " FJO "
		cQuery += "   ON FJO.FJO_FILIAL = ? "
		cQuery += "  	  AND FJO.FJO_COD = SEF.EF_BANCO "
		cQuery += "  	  AND FJO.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SX5") + " SX5 "
		cQuery += "   ON SX5.X5_FILIAL = ? "
		cQuery += "  	  AND SX5.X5_TABELA = ? "
		cQuery += "  	  AND SX5.X5_CHAVE = FRF.FRF_MOTIVO "
		cQuery += "  	  AND SX5.D_E_L_E_T_ = ? "
		cQuery += "WHERE SEF.EF_FILIAL = ? "
		cQuery += "   AND SEF.EF_BANCO = ? "
		cQuery += "   AND SEF.EF_AGENCIA = ? "
		cQuery += "   AND SEF.EF_CONTA = ? "
		cQuery += "   AND SEF.EF_CART = ? "

		//¿De fecha de emision ? - ¿A fecha de emision ?
		cQuery += "   AND ( SEF.EF_DATA BETWEEN ? AND ? OR LTRIM(SEF.EF_DATA) = ? )"

		//¿De fecha de vencimiento ? - ¿A fecha de vencimiento ?
		cQuery += "   AND ( SEF.EF_VENCTO BETWEEN ? AND ? OR LTRIM(SEF.EF_VENCTO) = ? )"

		// Agrega os filtros do SMARTVIEW na QUERY
		For nX := 1 To Len( aFiltro )
			If !AllTrim(aFiltro[nX, 1]) $ 'EF_CART,EF_IMPCHQ'
				cQuery += " AND ? " + ' ' + " ? ? "
			Endif
		Next

		If cImpChqU == 'N' //¿Imprime Cheques no usados ?
			cQuery += "   AND SEF.EF_IMPRESS <> ? "
		Endif
		cQuery += "   AND SEF.D_E_L_E_T_ = ? "
		cQuery += "ORDER BY ? "
	EndIf

	If !Empty( cQuery )
		// Executa a QUERY e cria uma tabela temporaria com os dados retornados
		cQuery := ChangeQuery( cQuery )

		oQuery := FwExecStatement():New()

		//Define a consulta e os parâmetros
		oQuery:SetQuery( cQuery )

		If cOpcQuerry == "1" //Cheques recebidos
			oQuery:SetString( 1, FWxFilial( 'SE1' ) )
			oQuery:SetString( 2, ' ' )
			oQuery:SetString( 3, FWxFilial( 'FRF' ) )
			oQuery:SetString( 4, ' ' )
			oQuery:SetString( 5, FWxFilial( 'FJO' ) )
			oQuery:SetString( 6, ' ' )
			oQuery:SetString( 7, FWxFilial( 'SEF' ) )

			oQuery:SetString( 8, jParams['MV_PAR01'][1] )
			oQuery:SetString( 9, jParams['MV_PAR02'][1] )

			oQuery:SetString( 10, 'R' )

			oQuery:SetString( 11, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )
			oQuery:SetString( 12, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )

			oQuery:SetString( 13, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )
			oQuery:SetString( 14, DtoS( FwDateTimeToLocal( jParams['MV_PAR07'][1] )[1] ) )

			oQuery:SetString( 15, ' ' )
			nSeq := 15
			For nX := 1 To Len( aFiltro )
				If !AllTrim(aFiltro[nX, 1]) $ 'EF_CART,EF_IMPCHQ'
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
				Endif
			Next

			oQuery:SetUnsafe( nSeq += 1, 'SE1.E1_FILIAL, SE1.E1_PORTADO, SE1.E1_AGEDEP, SE1.E1_CONTA, SE1.E1_BCOCHQ, SE1.E1_AGECHQ, SE1.E1_CTACHQ, SE1.E1_PREFIXO, SE1.E1_NUM' )
		Else // Cheques emitidos
			oQuery:SetString( 1, FWxFilial( 'FRF' ) )
			oQuery:SetString( 2, ' ' )
			oQuery:SetString( 3, FWxFilial( 'FJO' ) )
			oQuery:SetString( 4, ' ' )

			oQuery:SetString( 5, FWxFilial( 'SX5' ) )
			oQuery:SetString( 6, 'G0' )
			oQuery:SetString( 7, ' ' )

			oQuery:SetString( 8, FWxFilial( 'SEF' ) )
			oQuery:SetString( 9, jParams['MV_PAR01'][1] )
			oQuery:SetString( 10, jParams['MV_PAR02'][1])
			oQuery:SetString( 11, jParams['MV_PAR03'][1])
			oQuery:SetString( 12, 'P' )

			oQuery:SetString( 13, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )
			oQuery:SetString( 14, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )
			oQuery:SetString( 15, ' ' )

			oQuery:SetString( 16, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )
			oQuery:SetString( 17, DtoS( FwDateTimeToLocal( jParams['MV_PAR07'][1] )[1] ) )
			oQuery:SetString( 18, ' ' )

			nSeq := 18

			For nX := 1 To Len( aFiltro )
				If !AllTrim(aFiltro[nX, 1]) $ 'EF_CART,EF_IMPCHQ'
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
					oQuery:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
				Endif
			Next

			// ¿Imprime Cheques no usados ?
			If cImpChqU == 'N'
				oQuery:SetString( ( nSeq += 1 ), 'S' )
			Endif
			oQuery:SetString( ( nSeq += 1 ), ' ' )
			oQuery:SetUnsafe( ( nSeq += 1 ), 'SEF.EF_FILIAL, SEF.EF_PORTADO, SEF.EF_AGENCIA, SEF.EF_CONTA, SEF.EF_BANCO, SEF.EF_PREFIXO, SEF.EF_NUM' )
		EndIf

		// cria alias
		oQuery:OpenAlias( cAlias )

		While !((cAlias))->(Eof())
			jItems := JsonObject():new()

			// Verifica se precisa fazer o tratamento para LGPD
			aPDFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, self:aFields)
			lObfuscated := Len(aPDFields) != Len(self:aFields)

			For nX := 1 To Len(self:aStruct)
				If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
					jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(((cAlias))->&(self:aStruct[nX][5]))
				Else
					If ( self:aStruct[nx][3] == 'date' )
						jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAlias )->&( self:aStruct[nX][5] ) ) )
					ElseIf ( self:aStruct[nx][1] == 'FRFMOTIVO' )
						jItems[self:aStruct[nX][1]] := AllTrim( UPPER( POSICIONE( "SX5", 1, XFilial("SX5") + 'G0' + (((cAlias)))->&(self:aStruct[nX][5]), "X5DESCRI()")))
					ElseIf ( self:aStruct[nx][1] == 'EFNOMBCO' )
						jItems[self:aStruct[nX][1]] := ( cAlias )->FJO_NOME
					Else
						jItems[self:aStruct[nX][1]] := (((cAlias)))->&(self:aStruct[nX][5])
					EndIf
				EndIf
			Next

			// Inclui os dados no objeto paea retorno ao SmartView
			self:oData:appendData( jItems )

			( cAlias )->( DbSkip( ) )
		End

		// Se não for o último registro indica que terá próxima página
		self:setHasNext( !( ( cAlias ) )->( EOF( ) ) )

		// Checa se o arquivo já esta aberto
		If ( SELECT( cAlias ) > 0 )
			( cAlias )->( DbCloseArea( ) )
		EndIf
	EndIf

	// fecha objeto
	If oQuery <> Nil
		oQuery:Destroy()
		oQuery := Nil
		FwFreeObj(oQuery)
	EndIf

Return( self:oData )


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna la estructura de los campos
	
@Return object: self:oSchema
	
@author Cristian Gustavo
@since 21/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class ChequesTReportsBusinessObject
	Local nX as numeric

	//Estructura con los campos para el objeto de negocios.
	For nX:=1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1],self:aStruct[nX][2],self:aStruct[nX][3],self:aStruct[nX][4],self:aStruct[nX][5])
	Next nX

Return self:oSchema
