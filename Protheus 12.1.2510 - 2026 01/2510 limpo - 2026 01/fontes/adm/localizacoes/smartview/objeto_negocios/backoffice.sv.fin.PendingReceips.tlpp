#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.pendingreceips.ch'

namespace custom.financeiro.pendingreceips.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fin.pendingreceips
@description Classe para creación del Objeto de Negocio de Comprobante de entrega de recibo para SMARTVIEW
@author Leonardo Pereira
@since 23/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SEY,SAQ', name='Comprobante de entrega de recibo', country='ALL', initialRelease='12.1.2410', customTables='SEY,SAQ' )
class pendingreceipsTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class pendingreceipsTReportsBusinessObject

	Local aCpos := { } as array

	_Super:new( )

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Comprobante de entrega de recibo'

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Comprobante de entrega de recibo'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financeiro'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV514' )
		IIf( !self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'' ) // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'EY_TOTAL' , STR0007, 'number', STR0007 } ) // 'Total'

	// demais campos
	self:aFields := { 'AQ_COD', 'AQ_NOME', 'AQ_END', 'AQ_CGC', 'M0_CGC', 'M0_NOMECOM', 'M0_ENDENT', 'M0_BAIRENT', 'M0_CODMUN', 'M0_TEL', 'EY_DATA', 'EY_SERIE', 'EY_TALAO', 'EY_RECINI', 'EY_RECFIN', 'EY_TIPOREC', 'EY_TOTAL' , 'EY_STATUS', 'EY_RECPEND'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class pendingreceipsTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		  as character
	Local cFiltro 		  as character
	Local cExp 			  as character
	Local cCpoSEY		  as character
	Local cCpoSAQ 		  as character
	Local cAliasA       as character

	Local nX			     as numeric
	Local n1			     as numeric
	Local nSeq			  as numeric

	Local jParams 		  as json

	Local aPDFields	  as array
	Local aFiltro		  as array
	Local aCustomFields as array

	Local lObfuscated	  as logical

	Local oExecA		  as object

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			:= ''

	nX 				:= 0
	n1				   := 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSEY := getCpoUser( aCustomFields, 'SEY', 'EY', ',' )
	cCpoSAQ := getCpoUser( aCustomFields, 'SAQ', 'AQ', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { | x, y | x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "

	cQuery += " SAQ.AQ_COD, SAQ.AQ_NOME, SAQ.AQ_END, SAQ.AQ_CGC, " + cCpoSAQ
	cQuery += " SM0.M0_CGC, SM0.M0_NOMECOM, SM0.M0_ENDENT, SM0.M0_BAIRENT, SM0.M0_CODMUN, SM0.M0_TEL, SEY.EY_DATA, SEY.EY_SERIE, SEY.EY_TALAO, SEY.EY_RECINI, SEY.EY_RECFIN, " + cCpoSEY
	cQuery += " SEY.EY_TIPOREC, 0 EY_TOTAL, EY_STATUS, EY_RECPEND"

	cQuery += "FROM " + RetSqlName( 'SEY' ) + " SEY "

	cQuery += "INNER JOIN " + RetSqlName( "SAQ" ) + " SAQ "
	cQuery += "   ON SAQ.AQ_FILIAL = ? "
	cQuery += "      AND SAQ.AQ_COD = SEY.EY_COBRAD "
	cQuery += "      AND SAQ.D_E_L_E_T_ = ? "

	cQuery += "INNER JOIN SYS_COMPANY SM0 "
	cQuery += "   ON SM0.M0_CODIGO = ? "
	cQuery += "      AND SM0.M0_CODFIL = ? "
	cQuery += "      AND SM0.D_E_L_E_T_ = ? "

	cQuery += "WHERE SEY.EY_FILIAL = ? "
	cQuery += "   AND SEY.EY_COBRAD BETWEEN ? AND ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += "   AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery += "   AND SEY.D_E_L_E_T_ = ? "

	cQuery := ChangeQuery( cQuery )

	oExecA := FwExecStatement( ):New( cQuery )

	nSeq := 0

	oExecA:SetString( nSeq += 1, FWxFilial( "SAQ" ) )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, FWxFilial( "SEY" ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )
	oExecA:SetString( nSeq += 1, ' ' )

	cQuery := oExecA:getFixQuery( )

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] == 'EY_TOTAL' )
				self:jItems[self:aStruct[nX][1]] := ( ( Val( ( cAliasA )->EY_RECFIN ) - Val( ( cAliasA )->EY_RECINI ) ) + 1 )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )
	End

	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Leonardo Pereira
@since 23/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class pendingreceipsTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
