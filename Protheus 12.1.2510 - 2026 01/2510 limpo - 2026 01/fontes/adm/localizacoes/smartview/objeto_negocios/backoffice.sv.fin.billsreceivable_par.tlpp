#include "totvs.ch"
#INCLUDE 'topconn.ch'
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.fin.billsreceivable.ch"

namespace totvs.protheus.backoffice.fin.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGAFIN", tables="SA1,SED,SE1,FRV,FK7", name="Situación de títulos por cobrar",; 
customTables="SA1,SED,SE1", country="PAR")

//-------------------------------------------------------------------
/*/{Protheus.doc} BillsReceivableSmartViewBusinessObjectPAR
Clase para la creación del Objeto de Negocio - Localización Paraguay
@author Rafael Parma
@since 08/09/2025
@version 12.1.2510
*/
//-------------------------------------------------------------------
class BillsReceivableSmartViewBusinessObjectPAR from totvs.protheus.backoffice.fin.smartView.integratedProvider.BillsReceivableSmartViewBusinessObject

	public method new() as object
	public method preData() as object
	public method processData() as object
	public method preSchema() as object

endclass

method new() class BillsReceivableSmartViewBusinessObjectPAR
	_Super:new()
return self

method preData() as object class BillsReceivableSmartViewBusinessObjectPAR
return self:oData

method processData() as object class BillsReceivableSmartViewBusinessObjectPAR

    Local nParam as numeric
	Local cQuery as character
    Local cNFOri as character
	Local cAliasPAR as character    
	Local oExecPAR as object

    nParam := 1
    cNFOri := ""

    If SE1->E1_TIPO == "NCC"        
        cQuery := " SELECT DISTINCT(SD1.D1_NFORI) D1_NFORI "
        cQuery += " FROM " + RetSqlName( "SD1" ) + " SD1 "
        cQuery += " WHERE SD1.D1_FILIAL = ? "
        cQuery += " AND SD1.D1_DOC = ? "
        cQuery += " AND SD1.D1_SERIE = ? "        
        cQuery += " AND SD1.D1_FORNECE = ? "
        cQuery += " AND SD1.D1_LOJA = ? "
        cQuery += " AND SD1.D_E_L_E_T_ = ? "

        cQuery := ChangeQuery( cQuery )
        oExecPAR := FwExecStatement():New( cQuery )

        oExecPAR:SetString(nParam++, FWXFilial("SD1"))    
        oExecPAR:SetString(nParam++, SE1->E1_NUM)
        oExecPAR:SetString(nParam++, SE1->E1_PREFIXO)
        oExecPAR:SetString(nParam++, SE1->E1_CLIENTE)
        oExecPAR:SetString(nParam++, SE1->E1_LOJA)
        oExecPAR:SetString(nParam++, " ")

        cAliasPAR := oExecPAR:OpenAlias( )
        If !((cAliasPAR)->(Eof()))
            While !((cAliasPAR)->(Eof()))
                If cNFOri != "" 
                    cNFOri += ", "
                EndIf
                cNFOri += (cAliasPAR)->D1_NFORI
                (cAliasPAR)->(dbSkip())
            Enddo
        EndIf
        ( cAliasPAR )->( DbCloseArea( ) )
        oExecPAR:Destroy( )        
    EndIf
    
    self:jData['NFORI'] := cNFOri
    oExecPAR := Nil

Return

method preSchema() as object class BillsReceivableSmartViewBusinessObjectPAR

    self:finAddProperty("NFORI", FWX3Titulo("D1_NFORI"), "string") 
    
Return self:oSchema
