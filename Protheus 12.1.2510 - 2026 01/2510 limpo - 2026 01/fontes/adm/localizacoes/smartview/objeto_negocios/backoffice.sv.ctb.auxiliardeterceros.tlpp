#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.ctb.auxiliardeterceros.ch'

namespace custom.contabil.auxiliardeterceros.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.auxiliardeterceros
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 17/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACTB', tables='CT1', name='Auxiliar de terceros', country='COL', initialRelease='12.1.2410', customTables='' )
class auxiliardetercerosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class auxiliardetercerosTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Auxiliar de terceros'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Auxiliar de terceros'

	// Define a Área
	self:appendArea( STR0002 ) // 'Contabilid de Gestión'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CTBSV821' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos adicionais
	aAdd( aCpos, { 'XPARTIDA' 	, STR0007 , 'string', STR0007	} ) // 'XPARTIDA'
	aAdd( aCpos, { 'NIT' 		, STR0008 , 'string', STR0008	} ) // 'NIT'
	aAdd( aCpos, { 'NITDESC' 	, STR0009 , 'string', STR0009	} ) // 'NIT'
	aAdd( aCpos, { 'SLDINI' 	, STR0012 , 'number', STR0012	} ) // 'SLD.INI'
	aAdd( aCpos, { 'DEBITO' 	, STR0010 , 'number', STR0010	} ) // 'DEBITO'
	aAdd( aCpos, { 'CREDITO' 	, STR0011 , 'number', STR0011	} ) // 'CREDITO'
	aAdd( aCpos, { 'MOVIMENTO' 	, STR0013 , 'number', STR0013	} ) // 'SLD.ACTUAL'

	// demais campos
	self:aFields := {'CT2_FILIAL','CT2_DATA','CT2_LOTE','CT2_SBLOTE','CT2_DOC','CT2_LINHA','CT1_CONTA','CT1_DESC01','CTT_CUSTO','CTD_ITEM','CTH_CLVL','XPARTIDA','CT2_HIST','NIT','NITDESC','SLDINI','DEBITO','CREDITO','MOVIMENTO'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class auxiliardetercerosTReportsBusinessObject

	// Declaracao de variaveis
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character
	Local nSeq			as numeric


	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	jParams := oFilter:getParameters( )

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT CQ0_FILIAL AS CT2_FILIAL, ? AS CT2_DATA, '' CT2_LOTE, '' CT2_SBLOTE, '' CT2_DOC, ' ' CT2_LINHA, CT1_CONTA, CT1_DESC01, '' XPARTIDA, 'SALDO INICIAL' AS CT2_HIST, '' AS NIT, '' AS NITDESC, 0 AS DEBITO, 0 AS CREDITO,"
	cQuery += " CASE WHEN CT1_NORMAL = ? THEN ISNULL(SUM(CQ0DT.CQ0_DEBITO),0)-ISNULL(SUM(CQ0DT.CQ0_CREDIT),0) ELSE ISNULL(SUM(CQ0DT.CQ0_CREDIT),0)-ISNULL(SUM(CQ0DT.CQ0_DEBITO),0) END  AS MOVIMENTO, '' AS CTT_CUSTO, '' AS CTD_ITEM, '' AS CTH_CLVL, 0 AS SLDINI"
	cQuery += " FROM " + RetSqlName("CT1") + " CT1"
	cQuery += " LEFT JOIN " + RetSqlName("CQ0") + " CQ0DT ON CQ0DT.CQ0_FILIAL = ? AND CT1.CT1_CONTA = CQ0DT.CQ0_CONTA AND CQ0DT.D_E_L_E_T_ = ?"
	cQuery += " WHERE CT1.CT1_FILIAL = ?"
	cQuery += " AND CT1.CT1_CONTA BETWEEN ? AND ?"
	cQuery += " AND CQ0DT.CQ0_MOEDA = ?"
	cQuery += " AND CQ0DT.CQ0_TPSALD = ?"
	cQuery += " AND CQ0DT.CQ0_DATA < ?
	cQuery += " AND CT1.D_E_L_E_T_ = ?"
	cQuery += " GROUP BY CT1_NORMAL, CT1_CONTA, CQ0_FILIAL, CT1_DESC01"
	cQuery += " UNION
	cQuery += " SELECT CT2_FILIAL, CT2_DATA, CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_LINHA, CT2_DEBITO AS CT1_CONTA, CT1_DESC01, CT2_CREDIT AS XPARTIDA, CT2_HIST, CT2_EC05DB AS NIT,
	cQuery += " ( SELECT TOP 1 CV0_DESC FROM " + RetSqlName("CV0") + " WHERE CV0_FILIAL = ? AND CV0_CODIGO  = CT1.CT1_CONTA AND D_E_L_E_T_ = ? ) AS NITDESC,
	cQuery += " CT2_VALOR AS DEBITO, 0 AS CREDITO, CASE WHEN CT1_NORMAL = ? THEN CT2_VALOR ELSE CT2_VALOR*(-1) END AS MOVIMENTO, CT2_CCD AS CTT_CUSTO, CT2_ITEMD AS CTD_ITEM, CT2_CLVLDB AS CTH_CLVL, 0 AS SLDINI"
	cQuery += " FROM " + RetSqlName("CT2") + " CT2
	cQuery += " LEFT JOIN " + RetSqlName("CT1") + " CT1 ON CT1.CT1_FILIAL = ? AND CT1_CONTA = CT2_DEBITO AND CT1.D_E_L_E_T_ = ?"
	cQuery += " WHERE CT2.CT2_FILIAL = ?"
	cQuery += " AND CT2.CT2_DATA BETWEEN ? AND ?"
	cQuery += " AND CT2.CT2_DEBITO BETWEEN ? AND ?"
	cQuery += " AND CT2.CT2_MOEDLC = ?"
	cQuery += " AND CT2.CT2_TPSALD = ?"
	cQuery += " AND CT2.D_E_L_E_T_ = ?"
	cQuery += " UNION
	cQuery += " SELECT CT2_FILIAL, CT2_DATA, CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_LINHA, CT2_CREDIT AS CT1_CONTA, CT1_DESC01, CT2_DEBITO AS XPARTIDA, CT2_HIST, CT2_EC05CR AS NIT,
	cQuery += " ( SELECT TOP 1 CV0_DESC FROM " + RetSqlName("CV0") + " WHERE CV0_FILIAL = ? AND CV0_CODIGO  = CT1.CT1_CONTA AND D_E_L_E_T_ = ? ) AS NITDESC,
	cQuery += " 0 AS DEBITO, CT2_VALOR AS CREDITO, CASE WHEN CT1_NORMAL = ? THEN CT2_VALOR*(-1) ELSE CT2_VALOR END AS MOVIMENTO, CT2_CCC AS CTT_CUSTO, CT2_ITEMC AS CTD_ITEM, CT2_CLVLCR AS CTH_CLVL, 0 AS SLDINI"
	cQuery += " FROM " + RetSqlName("CT2") + " CT2
	cQuery += " LEFT JOIN " + RetSqlName("CT1") + " CT1 ON CT1.CT1_FILIAL = ? AND CT1_CONTA = CT2_CREDIT AND CT1.D_E_L_E_T_ = ?"
	cQuery += " WHERE CT2.CT2_FILIAL = ?"
	cQuery += " AND CT2.CT2_DATA BETWEEN ? AND ?"
	cQuery += " AND CT2.CT2_DEBITO BETWEEN ? AND ?"
	cQuery += " AND CT2.CT2_MOEDLC = ?"
	cQuery += " AND CT2.CT2_TPSALD = ?"
	cQuery += " AND CT2.D_E_L_E_T_ = ?"
	cQuery += " UNION"
	cQuery += " SELECT CQ0_FILIAL AS CT2_FILIAL, ? AS CT2_DATA, '' CT2_LOTE, '' CT2_SBLOTE, '' CT2_DOC, ' ' CT2_LINHA, CT1_CONTA, CT1_DESC01, '' XPARTIDA, 'SALDO FINAL' AS CT2_HIST, '' AS NIT, '' AS NITDESC, 0 AS DEBITO, 0 AS CREDITO,"
	cQuery += " CASE WHEN CT1_NORMAL = ? THEN ISNULL(SUM(CQ0DT.CQ0_DEBITO),0)-ISNULL(SUM(CQ0DT.CQ0_CREDIT),0) ELSE ISNULL(SUM(CQ0DT.CQ0_CREDIT),0)-ISNULL(SUM(CQ0DT.CQ0_DEBITO),0) END  AS MOVIMENTO, '' AS CTT_CUSTO, '' AS CTD_ITEM, '' AS CTH_CLVL, '' AS SLDINI"
	cQuery += " FROM " + RetSqlName("CT1") + " CT1"
	cQuery += " LEFT JOIN " + RetSqlName("CQ0") + " CQ0DT ON CQ0DT.CQ0_FILIAL = ? AND CT1.CT1_CONTA = CQ0DT.CQ0_CONTA AND CQ0DT.D_E_L_E_T_ = ?"
	cQuery += " WHERE CT1.CT1_FILIAL = ?"
	cQuery += " and CT1.CT1_CONTA BETWEEN ? AND ?"
	cQuery += " AND CQ0DT.CQ0_MOEDA = ?"
	cQuery += " AND CQ0DT.CQ0_TPSALD = ?"
	cQuery += " AND CQ0DT.CQ0_DATA <= ?"
	cQuery += " AND CT1.D_E_L_E_T_ = ?"
	cQuery += " GROUP BY CT1_NORMAL, CT1_CONTA, CQ0_FILIAL, CT1_DESC01"
	cQuery += " ORDER BY 1, 2, 3"

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ) )  // CT2_DATA INICIAL
	oExecA:SetString( nSeq += 1, '1' 			 )	// CT1_NORMAL
	oExecA:SetString( nSeq += 1, xFilial("CQ0") )	// CQ0_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CQ0.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CT1") )	// CT1_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// CT1_CONTA DE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// CT1_CONTA ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )	// CQ0_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// CQ0_TPSALD
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ) ) // CQ0_DATA <
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CV0") )	// CV0_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CV0.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, '1' 			 )	// CT1_NORMAL
	oExecA:SetString( nSeq += 1, xFilial("CT1") )	// CT1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CT2") )	// CT2_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ))	// CT2_DATA DE
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )	// CT2_DATA ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// CT2_DEBITO DE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// CT2_DEBITO ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )	// CT2_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// CT2_TPSALD
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT2.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CV0") )	// CV0_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CV0.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, '1' 			 )	// CT1_NORMAL
	oExecA:SetString( nSeq += 1, xFilial("CT1") )	// CT1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CT2") )	// CT2_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ))	// CT2_DATA DE
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )	// CT2_DATA ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// CT2_CREDIT DE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// CT2_CREDIT ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )	// CT2_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// CT2_TPSALD
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT2.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) ) // CT2_DATA FINAL
	oExecA:SetString( nSeq += 1, '1' 			 )	// CT1_NORMAL
	oExecA:SetString( nSeq += 1, xFilial("CQ0") )	// CQ0_FILIAL
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CQ0.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CT1") )	// CT1_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// CT1_CONTA DE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// CT1_CONTA ATE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )	// CQ0_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// CQ0_TPSALD
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) ) // CQ0_DATA <
	oExecA:SetString( nSeq += 1, ' ' 			 )	// CT1.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )


	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class auxiliardetercerosTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
