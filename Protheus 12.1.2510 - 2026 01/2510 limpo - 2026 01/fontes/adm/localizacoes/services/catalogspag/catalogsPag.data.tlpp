#include 'tlpp-core.th'

namespace tr.Catconpag
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/ Catconpag
Class Catconpag Data

@author José González
@since 03/06/2022
/*/
//-------------------------------------------------------------------
Class CatconpagAdapter From FWAdapterBaseV2
	Data cTableNick 

	Public Method new()
	Public Method Catconpag1Adapter()
EndClass

Method new(cResMethod As Character) Class CatconpagAdapter
	_Super:new(cResMethod)
Return


Static Function AddMapFields(oSelf , cfield,aCampos)
Local ni := 1
	For nI:= 1 to len(aCampos)
		oSelf:AddMapFields(alltrim(aCampos[nI][1])	,alltrim(aCampos[nI][2])	,.T.,.T.,{ alltrim(aCampos[nI][2])	, 'C',GetSx3Cache("FJN_COD"	,"X3_TAMANHO"),0})
	Next
Return 

Static Function getQuery(cTableNick , lTblFJN) As Character
	Local cQuery 
	Default lTblFJN := .F.
	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + RetSqlName(cTableNick) + IIF(lTblFJN, " FJN ", "")
	If lTblFJN
		cQuery += " INNER JOIN " + RetSqlName('FJO') + " FJO "
		cQuery += " ON FJN_COD = FJO_COD "
    	cQuery += " AND FJO_FILIAL = '"+ FWxFilial( 'FJO' ) +"'"
		cQuery += " AND FJO.D_E_L_E_T_ = ' ' "
	EndIf
	cQuery += " WHERE #QueryWhere#"
Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/ Catconpag

	Prepara y retorna el detalle de los catalogos Paginados

@author José González
@since 03/06/2022
/*/
//-------------------------------------------------------------------
Method Catconpag1Adapter( cConsul, cFiltro,cFiltro2,jParams) Class CatconpagAdapter
	Local cQueryFields := ""
	Local cQueryWhere :=""
	Local aActivedArea 
	Local oFWSXB 
	local nI := 1
	local cTable 	
	local cTitle 	
	local aCampos := {}
	local cFilter 
	local cTipo 
	local afilters := {}
	Local cDesc 	:= ""
	Local cLanguaje	:= RetAcsName()
	Local lTblFJN := .F.

	Default cFiltro2 := ""
	Default jParams := Nil 

	afilters:=	separa(cFiltro2,"|")

	oFWSXB := FWSXB():New(cConsul)
	oFWSXB:Activate()
	oFWSXB:BuildLookUpStruct()
	oFWSXB:GetStruct()
	cTable 	:= oFWSXB:CTABLE
	cTitle 	:= oFWSXB:CTITLE
	cFilter := Alltrim(oFWSXB:CFILTER)
	cTipo 	:= oFWSXB:ANODES[1]:CCOLUNA

	If cFiltro2 <> "" .And. Len(afilters) <= 1 .And. cFiltro == "" .And. oFWSXB:ANODES[2]:CKEY $ "SA6|FJNCON"
		cFiltro := cFiltro2
	EndIf

	IF cTipo == "DB" .AND. ctable == "SA6"
		Aadd(aCampos,{"codigo","A6_COD"})
		Aadd(aCampos,{"agencia","A6_AGENCIA"})
		Aadd(aCampos,{"cuenta","A6_NUMCON"})
		Aadd(aCampos,{"nombre","A6_NOME"})
		cFilter += "A6_FILIAL = '"+ xFilial("SA6") +"' AND A6_BLOCKED <> '1' "
		If cFiltro <> ""
			cFilter += "AND (A6_COD LIKE '%"+cFiltro+"%'"
			cFilter += "OR A6_AGENCIA LIKE '%"+cFiltro+"%'"
			cFilter += "OR A6_NUMCON LIKE '%"+cFiltro+"%'"
			cFilter += "OR A6_NOME LIKE '%"+cFiltro+"%')"
		EndIf
		If cFiltro2 <> "" .and. Len(afilters) > 1
			cFilter += "AND (A6_COD = '"+afilters[1]+"'"
			cFilter += " And A6_AGENCIA = '"+afilters[2]+"'"
			cFilter += " And A6_NUMCON = '"+afilters[3]+"')"
		EndIf
		FilBancos(@cFilter, cFiltro <> "".Or. cFiltro2 <> "", .F.,jParams)
	EndIF
	IF cTipo == "DB" .AND. ctable == "SX5"
		IF cLanguaje == ".ACE" 		//Español
			cDesc := "X5_DESCSPA"
		ELSEIF cLanguaje == '.ACI'	//Ingles
			cDesc := "X5_DESCENG"
		ELSE
			cDesc := "X5_DESCRI"	//Portugues
		ENDIF
		Aadd(aCampos,{"value","X5_CHAVE"})
		Aadd(aCampos,{"label",cDesc})
		If cFiltro <> ""
			iF cFilter  <>""
			cFilter += "AND "
			ENDIF
			cFilter += "(X5_CHAVE LIKE '%"+cFiltro+"%'"
			cFilter += "OR X5_DESCRI LIKE '%"+cFiltro+"%')"
		EndIf
		If cFiltro2 <> "" 
			iF cFilter  <>""
				cFilter += "AND "
			ENDIF
			cFilter += " X5_CHAVE = '"+cFiltro2+"'"
		EndIf
	EndIF
	If cTipo == "RE" .AND. oFWSXB:ANODES[2]:CKEY == "FJNCON" 
		lTblFJN := .T.
        cTable := ALLTRIM(oFWSXB:ANODES[1]:CCONTEM)
		cSpecif := "FJNCON()"
		Aadd(aCampos,{"codigo","FJN_COD"})
		Aadd(aCampos,{"agencia","FJN_AGENCI"})
		Aadd(aCampos,{"postal","FJN_POSTAL"})
		Aadd(aCampos,{"nomage","FJO_NOME"})
		cFilter += "FJN_FILIAL = '"+ xFilial("FJN") +"' "
		If cFiltro <> ""
			cFilter += "AND (FJN_COD LIKE '%"+cFiltro+"%'"
			cFilter += "OR FJN_AGENCI LIKE '%"+cFiltro+"%'"
			cFilter += "OR FJN_POSTAL LIKE '%"+cFiltro+"%'"
			cFilter += "OR FJO_NOME LIKE '%"+cFiltro+"%')"
		EndIf
		If cFiltro2 <> "" .and. Len(afilters) > 1
			cFilter += "AND (FJN_COD = '"+afilters[1]+"'"
			cFilter += " And FJN_AGENCI = '"+afilters[2]+"'"
			cFilter += " And FJN_POSTAL = '"+afilters[3]+"')"
		EndIf

		FilBancos(@cFilter, cFiltro <> "".Or. cFiltro2 <> "", .T.,jParams)
	EndIf
	
	//Asignamos el nombre de la tabla a utilizar
	::cTableNick := cTable
	aActivedArea   := FwGetArea()

	//Agregamos los campos Json/ResultSet
	AddMapFields(self,cTable,aCampos)

	//Informamos el query a utilizar en la API
	::SetQuery(getQuery(cTable, lTblFJN))
	For nI := 1 to len(aCampos)
		cQueryFields += aCampos[nI][2]
		If nI > 1 .and. nI < (len(aCampos)) //4
		cQueryFields += " , "
		EndIF
	Next
	If !lTblFJN
		cQueryWhere := " D_E_L_E_T_ = ' '  "
	Else
		cQueryWhere := " FJN.D_E_L_E_T_ = ' '  "
	EndIf

	if cFilter <> ""
		cQueryWhere += " and " + cFilter
	EndIf

	//Configuramos los campos para el query
	::SetWhere(cQueryWhere)
	::SetFields(cQueryFields)

	//Informamos el ordenamiento a ser utilizado en la Query
	::SetOrder(aCampos[2][2])

	//Ejecuta la consulta, retorna .T. si todo ocurre conforme a lo esperado
	If ::Execute()
		//Genera un archivo Json
		::FillGetResponse()
	EndIf

	FwrestArea(aActivedArea)
Return Nil

/*/{Protheus.doc} FilBancos
Metodo que aplica el filtro avanzando para los campos busca banco y busca banco cheque
@type function
@version  1
@author carlos.espinoza
@since 29/01/2024
/*/
Function FilBancos(cFilter,lFiltros,lBcoCh,jParams)
Local cCodigo := ""
Local cAgencia := ""
Local cPosOCta := ""
Local cNombre := ""

Default cFilter := ""
Default lFiltros := .F.
Default lBcoCh := .F.
Default jParams := Nil

If jParams <> Nil .And. !lFiltros
	//Indica el codigo del banco/banco cheque
	If !(Empty(jParams['codigo']))
		cCodigo  := jParams['codigo']
	EndIf
	//Indica la agencia del banco/banco cheque
	If !(Empty(jParams['agencia']))
		cAgencia  := jParams['agencia']
	EndIf
	//Indica la postal del banco cheque
	If !(Empty(jParams['postal']))
		cPosOCta := jParams['postal']
	EndIf
	//Indica la cuenta del banco 
	If !(Empty(jParams['cuenta']))
		cPosOCta := jParams['cuenta']
	EndIf
	//Indica la descripcion del banco/banco cheque
	If !(Empty(jParams['descripcion']))
		cNombre  := jParams['descripcion']
	EndIf

	If (!Empty(cCodigo) .Or. !Empty(cAgencia) .Or. !Empty(cPosOCta) .Or. !Empty(cNombre))
		cFilter += " AND ( "
		If !Empty(cCodigo)
			cFilter += IIF(lBcoCh, " FJN_COD ", " A6_COD ") + " LIKE '%"+cCodigo+"%' "
		EndIf

		If !Empty(cCodigo) .And. !Empty(cAgencia)
			cFilter += " AND "
		EndIf

		If !Empty(cAgencia)
			cFilter += IIF(lBcoCh, " FJN_AGENCI ", " A6_AGENCIA ") + " LIKE '%"+cAgencia+"%' "
		EndIf

		If (!Empty(cCodigo) .Or. !Empty(cAgencia)) .And. !Empty(cPosOCta)
			cFilter += " AND "
		EndIf

		If !Empty(cPosOCta)
			cFilter += IIF(lBcoCh, " FJN_POSTAL ", " A6_NUMCON ") + " LIKE '%"+cPosOCta+"%' "
		EndIf

		If (!Empty(cCodigo) .Or. !Empty(cAgencia) .Or. !Empty(cPosOCta)) .And. !Empty(cNombre)
			cFilter += " AND "
		EndIf

		If !Empty(cNombre)
			cFilter += IIF(lBcoCh, " FJO_NOME ", " A6_NOME ") + " LIKE '%"+cNombre+"%' "
		EndIf
		
		cFilter += " ) "
	EndIf
EndIf

Return Nil
