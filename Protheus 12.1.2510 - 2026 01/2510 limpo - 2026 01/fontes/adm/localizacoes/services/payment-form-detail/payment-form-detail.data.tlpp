#include 'tlpp-core.th'

namespace tr.paymentFormDetail

/*/{Protheus.doc} paymentFormDetailData
Declaraciones de metodos de la clase
@type class
@version  1
@author José González
@since 07/05/2021
/*/
Class paymentFormDetailData
	Data tableNick As Character

	Public Method new()
	Public Method getpaymentFormDetailAdapter()
	
EndClass

/*/{Protheus.doc} paymentFormDetailData::new
Inicializacion de la clase
@type method
@version  1
@author José González
@since 07/05/2021
/*/
Method new() class paymentFormDetailData
return 

/*/{Protheus.doc} paymentFormDetailData::getpaymentFormDetailAdapter
Prepara y retorna la forma de pago  del titulo seleccionado
@type method
@version  1
@author José González
@since 07/05/2021
/*/
method getpaymentFormDetailAdapter(cBranch,cClient,cLoja,cReceipt,cVersion,cSerie,oCoins) class paymentFormDetailData
	Local oPaymentFormDetail := JsonObject():New()
	Local cQueryFields As Character
	Local cQueryWhere As Character
	Local cQuery As  Character
	Local cAlias As  Character
	Local aCoins := {} As Array
	Local aPaymentF := {}
	Local aDoctos := {}
	Local atotal := {}
	Local nPos 	:= 1 As Numeric
	Local nCont := 1 As Numeric
	Local nDecs := 0
	Local nMoeda := 0 As Numeric
	Local cTipos := MVRECANT+"/"+MV_CRNEG
	Local lF998VISFP As logical
	Local aF998VISFP:= {} As Array
	Local aAuxPE	:= {}
	Local aArea		:= {}
	Local nAux		:= 0
	Local nX		:= 0
	Local nY		:= 0
	Local cAux		
	Local nParcela	:= SEL->(ColumnPos('EL_PARCELA')) 

	lF998VISFP := ExistBlock("F998VISFP") 

	//Se genera un array donde se almacenaran las monedas, su estructura es aCoins[moneda,nombre moneda,nTB,nComp,nRec,tasa]
	While nCont <= LEN(oCoins)
		Aadd(aCoins,{oCoins[nCont]['moneda'],oCoins[nCont]['property'],0,0,0,0})
		nCont ++
	ENDDO

	cAlias := GetNextAlias()
	cQueryFields := " EL_TIPO,EL_TIPODOC,EL_PREFIXO,EL_NUMERO,EL_VALOR, EL_VLMOED1,EL_EMISSAO, EL_DTVCTO,EL_MOEDA,EL_TPCRED,EL_BANCO,EL_AGENCIA,EL_CONTA,EL_CLIENTE,EL_SERIE,EL_PARCELA,EL_DESCONT,EL_JUROS,EL_MULTA,EL_FILIAL "
	If cPaisloc == "ARG" 
		cQueryFields += " ,EL_TIPODOC,EL_BCOCHQ ,EL_AGECHQ ,EL_CTACHQ ,EL_POSTAL ,EL_PARCELA,EL_TERCEIR,EL_ENDOSSA, EL_CGC ,EL_SELDOC "
	EndIf 

	IF lF998VISFP
		aF998VISFP := ExecBlock("F998VISFP")
		FOR nCont := 1 TO LEN(aF998VISFP)
			cQueryFields += " , "+aF998VISFP[nCont][1]+" "
		NEXT
	ENDIF

	FOR nCont:= 1 TO LEN(aCoins)
		If aCoins[nCont][1] != 1
			cQueryFields += ", EL_TXMOE0"+ALLTRIM(STR(aCoins[nCont][1]))
		ENDIF
	NEXT

	cQueryWhere := " EL_FILIAL ='"		+ xFilial("SEL",cBranch)  	+"' "
	cQueryWhere += " AND EL_SERIE ='"	+ 	 IIf(Empty(cSerie), SPACE(1), cSerie)	+"' "
	cQueryWhere += " AND EL_RECIBO ='"	+ cReceipt 	+"' "
	If cPaisloc == "ARG"
		cQueryWhere += " AND EL_VERSAO ='"	+ cVersion 	+"' "
	ENDIF
	cQueryWhere += " AND D_E_L_E_T_ = ' ' "
	cQuery := "SELECT "+ cQueryFields +" FROM "+ RetSqlName("SEL") +" WHERE "+ cQueryWhere

	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cAlias)

	WHILE (cAlias)->(!EOF())
		nDecs:= MsDecimais(VAL((cAlias)->EL_MOEDA))
		IF Alltrim((cAlias)->EL_TIPODOC)  $ "EF|CH|TF|RI|RG|RS|RB|RR|CC|DC|RM|CO|CD"
			Aadd(aPaymentF,JsonObject():new())
			nPos := Len(aPaymentF)
			aPaymentF[nPos]['type'			] 	:= Alltrim((cAlias)->EL_TIPO )
			aPaymentF[nPos]['tipo'			] 	:= Alltrim((cAlias)->EL_TIPO )
			aPaymentF[nPos]['tipodoc'		] 	:= Alltrim((cAlias)->EL_TIPODOC )
			aPaymentF[nPos]['prefix'		] 	:= Alltrim(REPLACE((cAlias)->EL_PREFIXO,chr(9)," "))
			aPaymentF[nPos]['prefixo'		] 	:= Alltrim(REPLACE((cAlias)->EL_PREFIXO,chr(9)," "))
			aPaymentF[nPos]['number'		] 	:= Alltrim((cAlias)->EL_NUMERO )
			aPaymentF[nPos]['numero'		] 	:= Alltrim((cAlias)->EL_NUMERO )
			aPaymentF[nPos]['value'			] 	:= IIF(cPaisloc=="PAR" .AND. VAL((cAlias)->EL_MOEDA) == 1 ,ROUND((cAlias)->EL_VALOR,nDecs) ,(cAlias)->EL_VALOR )  
			aPaymentF[nPos]['valor'			] 	:= IIF(cPaisloc=="PAR" .AND. VAL((cAlias)->EL_MOEDA) == 1 ,ROUND((cAlias)->EL_VALOR,nDecs) ,(cAlias)->EL_VALOR )  
			aPaymentF[nPos]['issueDate'		] 	:= Alltrim((cAlias)->EL_EMISSAO )
			aPaymentF[nPos]['emissao'		] 	:= Alltrim((cAlias)->EL_EMISSAO )
			aPaymentF[nPos]['dueDate'		] 	:= Alltrim((cAlias)->EL_DTVCTO )
			aPaymentF[nPos]['dtvcto'		] 	:= Alltrim((cAlias)->EL_DTVCTO )
			aPaymentF[nPos]['moeda'			] 	:= (cAlias)->EL_MOEDA 
			aPaymentF[nPos]['creditType'	]	:= Alltrim((cAlias)->EL_TPCRED )
			aPaymentF[nPos]['bank'			] 	:= Alltrim((cAlias)->EL_BANCO )
			aPaymentF[nPos]['branch'		] 	:= Alltrim((cAlias)->EL_AGENCIA )
			aPaymentF[nPos]['bankAccount'	]	:= Alltrim((cAlias)->EL_CONTA )
			IF nParcela > 0
				aPaymentF[nPos]['parcela'		] 	:= Alltrim((cAlias)->EL_PARCELA )
			ENDIF

			IF  cPaisloc == "ARG" 
				aPaymentF[nPos]['bcochq'		]	:= Alltrim((cAlias)->EL_BCOCHQ )
				aPaymentF[nPos]['agechq'		] 	:= Alltrim((cAlias)->EL_AGECHQ )
				aPaymentF[nPos]['ctachq'		] 	:= Alltrim((cAlias)->EL_CTACHQ )
				aPaymentF[nPos]['postal'		]	:= Alltrim((cAlias)->EL_POSTAL )

				aPaymentF[nPos]['terceir'		]	:= IIF(VAZIO(Alltrim((cAlias)->EL_TERCEIR )),"1",Alltrim((cAlias)->EL_TERCEIR ))
				aPaymentF[nPos]['endossa'		] 	:= IIF(VAZIO(Alltrim((cAlias)->EL_ENDOSSA )),"2",Alltrim((cAlias)->EL_ENDOSSA ))
				aPaymentF[nPos]['cgc'			] 	:= Alltrim((cAlias)->EL_CGC )
				aPaymentF[nPos]['seldoc'		]	:= IIF(VAZIO(Alltrim((cAlias)->EL_SELDOC )),"2",Alltrim((cAlias)->EL_SELDOC ))
			Endif

			IF lF998VISFP
				For nAux := 1	To LEN(aF998VISFP)
					cAux := "Alltrim((cAlias)->"+aF998VISFP[nAux][1]+")
					aPaymentF[nPos][aF998VISFP[nAux][4]] := &cAux
				Next
			ENDIF

			//tratamiento  para edicion de fechas con la configuración de Modos de Pago Diferidos
			aArea := FJS->(GetArea())
			DbSelectArea("FJS")
			FJS->(DbSetOrder(1)) //FJS_FILIAL+FJS_TIPO
			If FJS->(MSSEEK(xFilial("FJS")+ (cAlias)->EL_TIPO ))  
				If ALLTRIM(FJS->FJS_TPVAL) == "2" 
					aPaymentF[nPos]['editData'] := .T.
				Else
					aPaymentF[nPos]['editData'] := .F.
				EndIF
			EndIf
			FJS->(RestArea(aArea))
		Else 
			Aadd(aDoctos,JsonObject():new())
			nPos := Len(aDoctos) 
			aDoctos[nPos]['customer'		] := Rtrim((cAlias)->EL_CLIENTE ) 
			aDoctos[nPos]['series'			] := Alltrim((cAlias)->EL_PREFIXO )
			aDoctos[nPos]['billnumber'		] := Alltrim((cAlias)->EL_NUMERO  ) 
			aDoctos[nPos]['installment'		] := Alltrim((cAlias)->EL_PARCELA ) 
			aDoctos[nPos]['type'			] := Alltrim((cAlias)->EL_TIPO    ) 
			aDoctos[nPos]['issueDate'		] := Alltrim((cAlias)->EL_EMISSAO ) 
			aDoctos[nPos]['dueDate'			] := Alltrim((cAlias)->EL_DTVCTO  ) 
			If Alltrim((cAlias)->EL_TIPO) $ cTipos
				aDoctos[nPos]['billvalue'		] := -(cAlias)->EL_VALOR
			Else
				aDoctos[nPos]['billvalue'		] := (cAlias)->EL_VALOR
			EndIf
			aDoctos[nPos]['currency'		] := (cAlias)->EL_MOEDA    
			aDoctos[nPos]['discount'		] := (cAlias)->EL_DESCONT 
			aDoctos[nPos]['interest'		] := (cAlias)->EL_JUROS   
			aDoctos[nPos]['fine'			] := (cAlias)->EL_MULTA   
			aDoctos[nPos]['branch'			] := Alltrim((cAlias)->EL_FILIAL  ) 
		Endif

		//Obtendra la pocisión de la moneda
		nMoeda := AScanx(aCoins,{|x,y| y = VAL((cAlias)->EL_MOEDA)})

		If Subs((cAlias)->EL_TIPODOC,1,2)=="TB"
			If (cAlias)->EL_TIPO $ MVRECANT+"/"+MV_CRNEG
				//Se asigna nComp a la moneda correspondiente
				aCoins[nMoeda][4]+=(cAlias)->EL_VALOR
			Else
				//Se asigna nTb a la moneda 
				aCoins[nMoeda][3]+=(cAlias)->EL_VALOR
			Endif
		ElseIf   Subs((cAlias)->EL_TIPODOC,1,2)$"RI|RG|RB|RS|RM|RR|CO"
			//Se asigna nRec a la moneda correspondiente
			aCoins[nMoeda][5]+=(cAlias)->EL_VALOR
		ElseIf Alltrim((cAlias)->EL_TIPODOC)  $ "EF|CH|TF|CC|DC|CD"
			//Se asigna nComp y nRec a la moneda correspondiente
			aCoins[nMoeda][4]+=(cAlias)->EL_VALOR //nComp
			aCoins[nMoeda][5]+=(cAlias)->EL_VALOR //nRec
		Endif

		//Se asigna la tasa
		If VAL((cAlias)->EL_MOEDA) > 1
			aCoins[nMoeda][6] := &("(cAlias)->EL_TXMOE" + StrZero(VAL((cAlias)->EL_MOEDA),2))
		Else
			aCoins[nMoeda][6] :=  1
		EndIf
		
		(cAlias)->(DbSkip())
	EndDo
		IF lF998VISFP
			For nX := 1 to LEN(aF998VISFP)
				Aadd(aAuxPE,JsonObject():new())
				nY := Len(aAuxPE)
				aAuxPE[nY]['label'] 	:= aF998VISFP[nX][2]
				aAuxPE[nY]['property'] 	:= aF998VISFP[nX][4]
				aAuxPE[nY]['visible'] 	:= aF998VISFP[nX][3]
			Next
		ENDIF

		oPaymentFormDetail ['payments'	]:= aPaymentF
		oPaymentFormDetail ['titles'	]:= aDoctos
		oPaymentFormDetail ['paymentsColumns'	]:= aAuxPE

		FOR nCont:= 1 TO LEN(aCoins)
			nDecs:= MsDecimais(aCoins[nCont][1])
			Aadd(atotal,JsonObject():new()) 
			atotal[LEN(atotal)]['coin'		] := UPPER(aCoins[nCont][2])
			atotal[LEN(atotal)]['valuation'	] := aCoins[nCont][6]
			atotal[LEN(atotal)]['received'	] := Round(aCoins[nCont][5],nDecs) 
			atotal[LEN(atotal)]['balance'	] := Round(aCoins[nCont][4]-aCoins[nCont][3],nDecs) 
			atotal[LEN(atotal)]['currency'	] := aCoins[nCont][1] 
			oPaymentFormDetail ['totals'	]:= atotal
		NEXT
	(cAlias)->(DbCloseArea())
 
Return oPaymentFormDetail
