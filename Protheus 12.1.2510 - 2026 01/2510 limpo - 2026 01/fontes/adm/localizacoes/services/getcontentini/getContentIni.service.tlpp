#include 'tlpp-core.th'

namespace tr.getContentIni

/*/{Protheus.doc} getContentIniService
Se definen los metodos de la clase
@type class
@version  1
@author luis.aboytes
@since 15/9/2021
/*/
Class getContentIniService
	Public Method new()
	Public Method getPictFormat()
EndClass

/*/{Protheus.doc} getContentIniService::new
Inicializador de la clase
@type method
@version  1
@author luis.aboytes
@since 15/9/2021
/*/
Method new() Class getContentIniService
Return Nil

/*/{Protheus.doc} getPictFormat
Metodo creado para obtener el valor del parametro PictFormat
@type function
@version  1
@author luis.aboytes
@since 14/9/2021
/*/
Method getPictFormat() Class getContentIniService
	Local cServerIni 	:= "" As Character
	Local cClientIni 	:= "" As Character
	Local cPadrao 		:= "undefined" As Character
	Local cSesionLanguage := "" As Character
	Local cEnviroment	:= "" As Character
	Local cIniLanguage	:= "" As Character
	Local oResponse 	:= JsonObject():New()
	Local oContent	 	:= JsonObject():new()
	Local lfactor := .F. As Logical
	Local lMex := .F. 	As Logical
	Local lArg := .F.	As Logical
	Local lPar := .F.	As Logical
	Local lChi := .F.	As Logical
	Local lPer := .F.	As Logical
	Local lBol := .F. 	As Logical
	Local lUru := .F.   As Logical
	Local lCheckreader	:= .F. As Logical
	Local lAuto        := isblind()
    Local oRetRules    := JsonObject():new()

	oRetRules['search'] :=  MPUserHasAccess('FINA998', 1, , .F.)
	oRetRules['new'] 	:=  MPUserHasAccess('FINA998', 3, , .F.)
	oRetRules['edit'] 	:=  MPUserHasAccess('FINA998', 9, , .F.)
	oRetRules['cfdi']	:=  MPUserHasAccess('FINA998', 9, , .F.)
	oRetRules['view'] 	:=  MPUserHasAccess('FINA998', 2, , .F.)
	oRetRules['print'] 	:=  MPUserHasAccess('FINA998', 5, , .F.)
	oRetRules['send'] 	:=  MPUserHasAccess('FINA998', 6, , .F.)
	oRetRules['cancel'] :=  MPUserHasAccess('FINA998', 7, , .F.)
	oRetRules['delete'] :=  MPUserHasAccess('FINA998', 8, , .F.)



	cServerIni := GetAdv97()
	cClientIni := iif(lAuto,"smartclient.ini",GetRemoteIniName())
	cEnviroment := GetEnvServer()
	cSesionLanguage := "LANGUAGE"+UPPER(GetEnvServer())
	cIniLanguage := StrTran( cClientIni, "smartclient.ini", "language.ini")

	cPadrao := "undefined"

	oContent['pictformat'] := GetPvProfString(cEnviroment, "PictFormat", "undefined", cServerIni)
	oContent['dateFormat'] := UPPER(GetPvProfString(cEnviroment, "DateFormat", "undefined", cServerIni))
	oContent['lenguagesmartclient'] := GetPvProfString(cSesionLanguage, "currentlanguage", "undefined", cIniLanguage)
	oContent['dateSystem'] := dtos(dDataBase)
	oContent['isCallMata465n'] := IsInCallStack("MATA465N")
	if oContent['isCallMata465n']
		SA1->(DbSetOrder(1)) //A1_FILIAL+A1_COD+A1_LOJA
		If SA1->(MsSeek(xFilial("SA1")+SF1->(F1_FORNECE+F1_LOJA)))
			oContent['client'] :=  SF1->F1_FORNECE
			oContent['loja'] := SF1->F1_LOJA
		Endif
	Endif

	lMex := (cPaisLoc == "MEX")
	lArg := (cPaisLoc == "ARG")
	lCheckreader := iIF(lArg,SuperGetMv("MV_CMC7FIN") == "S",lCheckreader)
	lPar := (cPaisLoc == "PAR")
	lChi := cPaisLoc == "CHI"
	lPer := (cPaisLoc == "PER")
	lBol := cPaisLoc == "BOL"
	lUru := cPaisLoc == "URU"

	oContent['mexico'] := lMex
	oContent['argentina'] := lArg
	oContent['paraguay'] := lPar
	oContent['chile'] := lChi
	oContent['peru'] := lPer
	oContent['bolivia'] := lBol
	oContent['uruguay'] := lUru

	If (cPaisLoc == "MEX" .And. SuperGetMv("MV_CFDI33",.F.,.F.) .And. Iif(lAuto,.T.,SEL->(FieldPos("EL_FACTOR")) > 0))
		lfactor := .T.
	EndIf
	oContent['factoring' ] := 	lfactor
	oContent['checkReader'] := lCheckreader
	oContent['pgoAutr'] := SuperGetMv("MV_PGOAUTR",.F.,.F.)
	oContent['serieEnabled'] := SuperGetMv("MV_SERREC",.F.,.F.)
	oContent['isEnableCFDI'] := SuperGetMv("MV_CFDI33",.F.,.F.) .Or. SuperGetMV("MV_CFDI40", .F., .F.) 
	oContent['datesystem'] := dtos(dDataBase)
	oContent['lF998defpan'] := ExistBlock("F998DEFPAN") 
	oContent['fina998bus']	:= FWSX1Util():ExistPergunte("FINA998BUS")
	oContent['fin998a']	:= FWSX1Util():ExistPergunte("FIN998A")
	oContent['percentDiscount']	:= ExistBlock('F998VALBX')
	oContent['fpadvpl'] := 	SuperGetMv("MV_FPADVPL",.F.,.F.) 
	oContent['recmvc'] := 	SuperGetMv("MV_RECMVC",.F.,.F.)
	oContent['rules'] := oRetRules
	oResponse['result'] := .T.
	oResponse['response'] := oContent
	
Return oResponse
