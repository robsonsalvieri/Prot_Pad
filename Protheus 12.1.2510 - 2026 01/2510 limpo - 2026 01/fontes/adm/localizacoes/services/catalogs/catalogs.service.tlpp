#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace tr.Catalogs

/*/{Protheus.doc} CatalogsService
Declaracion de metodos
@type class
@version  1
@author José González
@since 23/03/2021
/*/
Class CatalogsService
	Public Method new()
	Public Method getCatalogsService()
EndClass

/*/{Protheus.doc} CatalogsService::new
Constructor de la clase
@type method
@version  1
@author José González
@since 23/03/2021
/*/
Method new() Class CatalogsService
Return

/*/{Protheus.doc} CatalogsService::getCatalogsService
retorna el detalle del titulo seleccionado
@type method
@version  1
@author José González
@since 23/03/2021
/*/
Method getCatalogsService (oJParams,oJfilter) class  CatalogsService

	Local oCatalogsAdapter As Object
	Local oFWSXB As Object
	Local response := JsonObject():New() As Object
	Local oJson := JsonObject():New() As Object
	Local cCatalog := "" As Character
	Local cTable As Character
	local cTitle As Character
	local aCampos := {} As Array
	local aSearch :={} As Array
	local aCpos :={} As Array
	local nI := 1
	local cSpecif := ""
	Local cfiltro := ""
	oCatalogsAdapter :=CatalogsData():new()
	SetFunName("FINA998")

	//Se asignan los valores del parametros params a variables
	cCatalog	:=	oJParams['catalog']
	cfilterA	:=	oJParams['filtro'] 
	cfiltro		:=	oJfilter['filter']
	oFWSXB := FWSXB():New(cCatalog)
	oFWSXB:Activate()
	oFWSXB:BuildLookUpStruct()
	oFWSXB:GetStruct()

	cTable := oFWSXB:CTABLE
	cTitle := oFWSXB:CTITLE
	aCampos := oFWSXB:ARETURNSFIELDS
	cFilter := Alltrim(oFWSXB:CFILTER)
	cTipo := oFWSXB:ANODES[1]:CCOLUNA

	If cTipo == "RE" .AND. subStr(oFWSXB:ANODES[2]:CCONTEM,1,7) == "F812SXB"
		cTable := subStr(oFWSXB:ANODES[2]:CCONTEM,10,4)
		cSpecif := "F812SXB"
	EndIf
	If cTipo == "RE" .AND. oFWSXB:ANODES[2]:CKEY == "FJNCON"
        cTable := ALLTRIM(oFWSXB:ANODES[1]:CCONTEM)
		cSpecif := "FJNCON()"
	EndIf
	If cTipo == "RE" .AND. subStr(oFWSXB:ANODES[2]:CCONTEM,1,8) == "F087ACEC" //Consulta especifica para obtener las compensaciones
		cTable := ALLTRIM(oFWSXB:ANODES[1]:CCONTEM)
		cSpecif := "F087ACEC"
	EndIf
	If LEN(oFWSXB:AINDEXSEARCH) >= 1
		aSearch := oFWSXB:AINDEXSEARCH[1][5]
	EndIf

	For nI:= 1 to len(aSearch)
		aadd(aCpos,Alltrim(aSearch[nI]:CCONTEM))
	Next

	oJson := oCatalogsAdapter:getCatalogsAdapter(cTable,aCampos,cfilter,aCpos,cTipo,cSpecif,cfiltro,cCatalog,cfilterA,cTitle)

	response["result"] := .T.
	response["response"] := oJson

return response

