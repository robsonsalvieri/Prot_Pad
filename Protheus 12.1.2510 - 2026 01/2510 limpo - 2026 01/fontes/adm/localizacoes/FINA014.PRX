#INCLUDE "FINA014.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINA014  ³ Autor ³ Paulo Augusto         ³ Data ³ 06/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Informar e Atualizar data de pagamento da comissao dos      ³±±
±±³          ³Cobradores.                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FINA014(lAutomato)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oDlg
Local cCadastro := OemToAnsi(STR0003) //"Atual. Pag. de Comiss„o     "
LOCAL nOpca := 0
LOCAL aCA:={ OemToAnsi(STR0001),OemToAnsi(STR0002)}  //"Confirma"###"Abandona"
Local aSays:={}, aButtons:={}

Private aRotina 	:= MenuDef()
Private cCodDiario 	:= ""
Default lAutomato 	:= .F.  // Tratamiento para scripts de automatización
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                     ³
//³ mv_par01            // Considera da data                 ³
//³ mv_par02            // ate a data                        ³
//³ mv_par03            // Do Vendedor                       ³
//³ mv_par04            // Ate o vendedor                    ³
//³ mv_par05            // Data de Pagamento                 ³
//³ mv_par06            // Gera ctas a Pagar (Sim/Nao)       ³
//³ mv_par07            // Contabiliza on-line               ³
//³ mv_par08            // Mostra lcto Contabil              ³
//³ mv_par09            // Vencimento de                     ³
//³ mv_par10            // Vencimento Ate                    ³
//³ mv_par11            // Considera data (Vencto/Pagamento) ³
//³ mv_par12            // Gera Titulo Por(Cobrad./Rec./Doc) ³
//³ mv_par13            // Serie do Tit. Gerado (COB-default)³
//³ mv_par14            // Moeda do Titulo a ser gerada      ³
//³ mv_par15            // Status do coletor    			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pergunte("FIN014",.F.)
AADD(aSays,OemToAnsi( STR0004 ) ) //"      Este programa tem como objetivo solicitar e atualizar"
AADD(aSays,OemToAnsi( STR0005 ) ) //"a data para pagamento das comiss”es dos Cobradores.        "
AADD(aButtons, { 5,.T.,{|| Pergunte("FIN014",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1, If( CA014Ok(), o:oWnd:End(), nOpca:=0 ) }} )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )

If !lAutomato // tratamiento para scripts de automatización
  FormBatch( cCadastro, aSays, aButtons ,,220,380)
	If nOpca == 1
		Processa({|lEnd| fa012Processa()})
	Endif
Else
  If ca014Ok()
    Processa({|lEnd| fa012Processa()})
  Endif
Endif
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³fa012Proce³ Autor ³ Paulo Augusto         ³ Data ³ 06/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function fa012Processa()

Local nRegistro 	:= 0
Local cCobrAnt  	:= ""
Local nVlrComis 	:= 0
Local cPrefixo		:= mv_par13
Local cParcela 	:= ""
Local cNatureza 	:= ""
Local cTipo     	:= ""
Local nHdlPrv   	:= 0
Local cArquivo  	:= ""
Local nTotal    	:= 0
Local nIndex		:= 0
Local cPadrao   	:= "510"
Local lPadrao   	:= VerPadrao(cPadrao)
Local lDigita   	:= If(mv_par08==1,.T.,.F.)
Local dVencto
Local lGerafin 	:= .f.
Local cRecAnt		:= ""
Local nTamNumero 	:= tamsx3("E2_NUM")[1]
Local cNumero   	:= " "
Local lGera 		:= .f.
Local cQuery      := ""  
Local cFiltro     := ""
Local	cIndice		:= ""
Local aStru			:={}
Local lQuery		:=	.F.
Local aFlagCTB		:= {}
Local lUsaFlag		:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/) 
Local oObj          := Nil //Objeto de Grupo de Preguntas
Local aX1Pre        := {}	
Local lStatCob      := .F.

Private cLote  	:= ""

oObj := FWSX1Util():New()
oObj:AddGroup("FIN014")
oObj:SearchGroup()
aX1Pre := oObj:GetGroup("FIN014")

If Len(aX1Pre[2]) >= 15 //Si existe pregunta de ESTATUS COBRADOR
	lStatCob := .T.
EndIF

If Select ("TRB") <> 0
	dbSelectArea("TRB")
	dbCloseArea()
EndIf	

#IFDEF TOP
   If TcSrvType() != "AS/400"
      lQuery := .T.
   Endif
#ENDIF

LoteCont( "FIN" )

If lQuery 
  
 	dbSelectArea("SEX")
 	aStru := SEX->(dbStruct())
	cQuery := "SELECT R_E_C_N_O_ RECNO FROM " + RetSQLname("SEX")
	cQuery += " WHERE D_E_L_E_T_ <> '*'"
	cQuery += " AND EX_FILIAL  = '"  + xFilial("SEX") + "'"
	cQuery += " AND EX_EMISSAO  BETWEEN '" + Dtos(mv_par01) + "' AND '" + Dtos(mv_par02)+ "'"	
	cQuery += " AND EX_COBRAD   BETWEEN '" + mv_par03 + "' AND '" + mv_par04 + "'"
	cQuery += " AND EX_VENCTO   BETWEEN '" + Dtos(mv_par09) + "' AND '" + Dtos(mv_par10) + "'"   
	cQuery += " AND EX_DATA='"+Dtos(Ctod(""))+"'"
	cQuery += " ORDER BY EX_COBRAD, EX_NUM, EX_EMISSAO"
	cQuery := ChangeQuery(cQuery)
			
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), "TRB", .F., .T.)
   cAlias:="TRB"
	dbSelectArea(cAlias)     
	bCond	:=	{|| .T. }
ELSE

	dbSelectArea("SEX")
	cFiltro	:= 'Dtos(EX_EMISSAO) >= "'+Dtos(mv_par01)+'" .AND. Dtos(EX_EMISSAO) <= "'+Dtos(mv_par02)+'" .AND. '
	cFiltro	+= 'Dtos(EX_VENCTO) >= "'+Dtos(mv_par09)+'" .AND. Dtos(EX_VENCTO) <= "'+Dtos(mv_par10)+'" .AND. '
  	cFiltro	+= 'Dtos(EX_DATA) == Space(8)'
	cIndice	:= 'EX_COBRAD+EX_NUM+Dtos(EX_EMISSAO)'
	DbSetOrder(4)
	dbSeek(xFilial('SEX')+mv_par03,.t.)                    
	bCond	:=	{ || EX_COBRAD <= mv_par04}  
	cAlias:="SEX"
ENDIF
#IFDEF TOP 
		DbSelectArea('SEX')
		MsGoTo((cAlias)->RECNO)
	#ELSE
		While !&(cFiltro) .AND. !Eof()
			DbSkip()
			Loop
		Enddo	
#ENDIF	
While  !Eof()  .AND. Eval(bCond)
//	dbSelectArea(cAlias)
  	dVencto := If( mv_par11 == 1,SEX->EX_VENCTO, mv_par05)
	cCobrAnt := SEX->EX_COBRAD
	cRecAnt	:= SEX->EX_NUM
	aAreaAtu := GetArea()
	nVlrComis += xMoeda(SEX->EX_COMIS,SEX->EX_MOEDA,mv_par14,dDatabase)
	
	If lStatCob
		IF SAQ->(ColumnPos("AQ_STATUS")) == 0
			If !MsgYesNo(OemToAnsi(STR0008),OemToAnsi(STR0007))  //"El campo de status (AQ_STATUS) no existe y se realizará la actualización de la fecha de pago para todos los cobradores.  ¿Desea continuar?"
				Return
			EndIf
		EndIf
		IF SAQ->(ColumnPos("AQ_STATUS")) > 0
			dbSelectArea("SAQ")
			dbSetOrder(1)
			If dbSeek(xFilial("SAQ")+cCobrAnt,.F.)
				Do Case
					Case mv_par15 == 1
						If SAQ->AQ_STATUS == "1" .Or. SAQ->AQ_STATUS == " " 
							RecLock('SEX',.F.)
							Replace SEX->EX_DATA with dVencto
							MsUnLock()
						EndIf	
					Case mv_par15 == 2 
						If SAQ->AQ_STATUS == "2"
							RecLock('SEX',.F.)
							Replace SEX->EX_DATA with dVencto
							MsUnLock()
						EndIf	
					Otherwise	
						RecLock('SEX',.F.)
						Replace SEX->EX_DATA with dVencto
						MsUnLock()
				EndCase	
			EndIF
		Else 
			If RecLock('SEX',.F.)
				Replace SEX->EX_DATA with dVencto
				MsUnLock()
			EndIf	
		EndIf 
	Else
		If RecLock('SEX',.F.)
			Replace SEX->EX_DATA with dVencto
			MsUnLock()
		EndIf	
	EndIf	
	


	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aqui e' gerado o Tit. a Pagar (SE2) para o Cobrador.         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)
	dbSkip()
	#IFDEF TOP 
		DbSelectArea('SEX')
		MsGoTo((cAlias)->RECNO)
	#ELSE
	While !&(cFiltro) .and. !eof()
		DbSkip()
		Loop
	Enddo	
	#ENDIF	
	
	If cCobrAnt != SEX->EX_COBRAD .and. MV_PAR12 ==1  
		lGera:=.t.
	ElseIf cRecAnt != SEX->EX_NUM .and. MV_PAR12 ==2 	 
		lGera:=.t.
	ElseIf MV_PAR12 ==3 
		lGera:=.t.
	EndIf			 
	If SEX->(EOF())
		lGera:=.t.
	EndIf	


	If  MV_PAR06 == 1  .and. nVlrComis != 0 .and. lGera
		lGera:=.f.   
		dbSelectArea("SAQ")
		dbSetOrder(1)
		If dbSeek(xFilial("SAQ")+cCobrAnt,.F.).and.  SAQ->AQ_GERASE2 == "1" 
			dbSelectArea("SA2")
			dbSetOrder(1)
			dbSeek(xFilial("SA2")+SAQ->AQ_FORNECE+SAQ->AQ_LOJA,.F.)
			If !Found()
				dbSelectArea("SA2")
				dbSetOrder(1)
				dbSeek(xFilial()+GetMv("MV_FORNCOB"),.F.)
				If  !Found()
					dbSelectArea("SA2")
					RecLock("SA2",.T.)
					SA2->A2_FILIAL := xFilial()
					SA2->A2_COD    := SubStr(GetMV("MV_FORNCOB"),1,6)
					SA2->A2_LOJA	:= SubStr(GetMV("MV_FORNCOB"),7,2)
					SA2->A2_NOME	:= "COBRAD"
					SA2->A2_NREDUZ := "COBRAD"
					SA2->A2_BAIRRO := "."
					SA2->A2_MUN 	:= "."
					SA2->A2_EST 	:= "."
					SA2->A2_END 	:= "."
					MsUnlock()
				EndIf
				dbSeek(xFilial()+GetMv("MV_FORNCOB"),.F.)
			EndIf
			dbSelectArea("SED")
			dbSetOrder(1)
			dbSeek(xFilial("SED")+SA2->A2_NATUREZ,.F.)
			If ( Found() )
				cNatureza := SA2->A2_NATUREZ
			Else
				cNatureza := ""
			EndIf
			If SAQ->(Found()) .And. SA2->(Found()) .and. SAQ->AQ_GERASE2 == "1"
				aAreaAtu := GetArea()
			   dbSelectArea("SX5")
				If dbSeek(xFilial("SX5")+"99"+cPrefixo)
				  cNumero	:= Subs(x5Descri(),1,nTamNumero)
				Else
				 	dbSeek(xFilial("SX5")+"99"+"COB")	   
					cPrefixo:="COB"
					cNumero	:= Subs(x5Descri(),1,nTamNumero)
					Help(" ",1,"A014NOPR")
				EndIf
				
				cNumero    := Subs(x5Descri(),1,nTamNumero)
				RestArea(aAreaAtu)	
				While !a014ChkNum(cPrefixo,cNumero) // Caso o numero ja esteja travado solicita
		         cNumero:=Soma1(cNumero,nTamNumero)
				EndDo
				
				cTipo      := If (nVlrComis > 0 , "DP " , left(MV_CPNEG,3) )
				cParcela   := GetMv("MV_1DUP")
				
				cUltParc   := TamParcela("E2_PARCELA","Z","ZZ","ZZZ")
				dbSelectArea("SE2")
				dbSetOrder(1)
				dbSeek(xFilial("SE2")+cPrefixo+cNumero+cParcela,.F.)
				While ( SE2->(Found()) )
					If ( cParcela == cUltParc )
						cParcela := GetMv("MV_1DUP")
						cNumero  := Soma1(cNumero,Len(SE2->E2_NUM))
					Else
						cParcela	:= Soma1(cParcela,Len(SE2->E2_PARCELA))
					EndIf
					dbSeek(xFilial("SE2")+cPrefixo+cNumero+cParcela,.F.)
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Aqui sÆo calculados os impostos sobre a comissÆo do Vendedor.³
				//³ Para tal, ‚ necess rio que, no fornecedor utilizado para o   ³
				//³ titulo de comissÆo esteja cadastrada natureza que calcule    ³
				//³ impostos.                                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
				nIrrf := 0
				nIss	:= 0
				nInss := 0
				If !Empty(cNatureza) .and. cTipo == "DP "
					FI0140Nat(@nVlrComis,@nIrrf,@nIss,@nInss)
				Endif
				RecLock("SE2",.T.)
				SE2->E2_FILIAL    := xFilial()
				SE2->E2_PREFIXO   := cPrefixo
				SE2->E2_NUM       := cNumero
				SE2->E2_PARCELA   := cParcela
				SE2->E2_TIPO      := cTipo
				SE2->E2_FORNECE   := SA2->A2_COD
				SE2->E2_LOJA      := SA2->A2_LOJA
				SE2->E2_NOMFOR    := SA2->A2_NREDUZ
				SE2->E2_VALOR     := Abs(nVlrComis)
				SE2->E2_EMIS1     := dDataBase
				SE2->E2_EMISSAO   := dDataBase
				SE2->E2_VENCTO    := dVencto
				SE2->E2_VENCREA   := DataValida(SE2->E2_VENCTO,.T.)
				SE2->E2_VENCORI   := SE2->E2_VENCTO
				SE2->E2_SALDO     := SE2->E2_VALOR
				SE2->E2_NATUREZ   := cNatureza
				SE2->E2_MOEDA     := mv_par14
				SE2->E2_VLCRUZ    := xMoeda(SE2->E2_VALOR,SE2->E2_MOEDA,1,dDatabase)
				SE2->E2_IRRF		:= nIrrf
				SE2->E2_ISS			:= nIss
				SE2->E2_INSS		:= nInss
				SE2->E2_ORIGEM    := "FINA050"
				SE2->E2_RATEIO    := "N"
				SE2->E2_FLUXO     := "S"
				nRegistro := Recno()
				MsUnlock()
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ponto de Entrada apos a gravacao do SE2 e antes de gerar a contabilidade³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("FSE2014")
					ExecBlock("FSE2014",.F.,.F.)
				Endif          
				
				
				dbSelectArea("SE2")
				dbSetOrder(6)
				dbGoto(nRegistro)
				If ( mv_par07 == 1 .And. lPadrao ) // Contabiliza On-Line
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicializa Lancamento Contabil                                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						nHdlPrv := HeadProva( cLote,;
						                      "FINA014" /*cPrograma*/,;
						                      Substr( cUsuario, 7, 6 ),;
						                      @cArquivo )

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Prepara Lancamento Contabil                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil 
							aAdd( aFlagCTB, {"E2_LA", "S", "SE2", SE2->( Recno() ), 0, 0, 0} )
						Endif
						nTotal += DetProva( nHdlPrv,;
						                    cPadrao,;
						                    "FINA014" /*cPrograma*/,;
						                    cLote,;
						                    /*nLinha*/,;
						                    /*lExecuta*/,;
						                    /*cCriterio*/,;
						                    /*lRateio*/,;
						                    /*cChaveBusca*/,;
						                    /*aCT5*/,;
						                    /*lPosiciona*/,;
						                    @aFlagCTB,;
						                    /*aTabRecOri*/,;
						                    /*aDadosProva*/ )

					If UsaSeqCor()
						aDiario := {}
						aDiario := {{"SE2",nRegistro,cCodDiario,"E2_NODIA","E2_DIACTB"}}
					Else
						aDiario := {} 
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Efetiva Lan‡amento Contabil                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						RodaProva( nHdlPrv,;
						           nTotal )

						cA100Incl( cArquivo,;
						           nHdlPrv,;
						           3 /*nOpcx*/,;
						           cLote,;
						           lDigita,;
						           .F. /*lAglut*/,;
						           /*cOnLine*/,;
						           /*dData*/,;
						           /*dReproc*/,;
						           @aFlagCTB,;
						           /*aDadosProva*/,;
						           aDiario )
						aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento
					
					dbSelectArea("SE2")
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza flag de Lan‡amento Cont bil		  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !lUsaFlag
						Reclock("SE2")
						Replace E2_LA With "S"
						MsUnlock()
					Endif
					
				Endif
				aAreaAtu := GetArea()
				dbSelectArea("SX5")
				dbSeek(xFilial("SX5")+"99"+cprefixo)
								
				RecLock("SX5",.F.)
				Replace X5_DESCRI  with Soma1(cNumero,nTamNumero)
				Replace X5_DESCSPA with Soma1(cNumero,nTamNumero)
				Replace X5_DESCENG with Soma1(cNumero,nTamNumero)
				MsUnLock()
				RestArea(aAreaAtu)
			EndIf
		EndIf
		nVlrComis := 0		
	EndIf
	dbSelectArea(cAlias)
EndDo
If Select ("TRB") <> 0
	dbSelectArea("TRB")
	dbCloseArea()
EndIf	

dbSelectArea("SEX")
dbSetOrder(1)
Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ca014Ok   ºAutor  ³Anderson Goncalves  º Data ³  28/05/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação do botao OK                                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ FINA014                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function ca014Ok()

Local lRet := .T.
Local lAutomato := IsBlind() //tratamiento para scripts de automatización

If UsaSeqCor()
	cCodDiario := CTBAVerDia()
EndIf

If !lAutomato
	If lRet .and. !MsgYesNo(OemToAnsi(STR0006),OemToAnsi(STR0007))  //"Confirma a Atual. Pag. de Comiss„o?"###"Aten‡„o"
		lRet := .F.
	EndIf
Endif
Return(lRet) 


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³FI014NAT	³ Autor ³ Mauricio Pequim Jr	  ³ Data ³ 28/11/00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula os impostos se a natureza assim o mandar			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ FI014Nat()																  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ FINA014																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FI0140Nat(nVlrComis,nIrrf,nIss,nInss)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Natureza pede calculo do IRRF            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SED->ED_CALCIRF == "S" .and. !(SE2->E2_TIPO $ MV_CPNEG)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Pessoa Fisica ou Juridica, para fins de  ³
	//³ calculo do irrf                                    	³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IF SA2->A2_TIPO == "F"
		nIrrf:=fa050tabir(nVlrComis)
	Else
		nIrrf := Round((nVlrComis * IIF(SED->ED_PERCIRF>0,SED->ED_PERCIRF,GetMV("MV_ALIQIRF"))/100),2)
	EndIF
EndIf
If ( nIrrf <= GetMv("MV_VLRETIR") ) // Se Vlr. for Baixo nao considera
	nIrrf := 0
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Natureza pede calculo do ISS (FORNECEDOR NO RECOLHE) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SED->ED_CALCISS == "S" .and. SA2->A2_RECISS != "S"
	nIss := Round(((nVlrComis * GetMV("MV_ALIQISS"))/100),2)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Natureza pede calculo do INSS (RECOLHE INSS P/ FORNEC)³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SED->ED_CALCINS == "S" .and. SA2->A2_RECINSS == "S"
	nInss := Round((nVlrComis * (SED->ED_PERCINS/100)),2)
	If ( nInss < GetMv("MV_VLRETIN") ) // Tratamento de Dispensa de Ret. de Inss.
		nInss := 0
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desconta valores de impostos do valor total da comissÆo           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nVlrComis -= (nIrrf + nIss + nInss)

Return .t.


Static Function a014ChkNum(cSerie,cNumero)

Local lRet	:=	.T.
Local cMayUse 	:= ""
Local aAreaAtu


aAreaAtu:=GetArea()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a NF esta sendo digitada em outra estacao.              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMayUse  := "SE2"+xFilial("SE2")+cNumero+cSerie

If !MayIUseCode(cMayUse)
	MsgAlert(OemToAnsi("TESTE"))
	lRet	:=	.F.
EndIf
RestArea(aAreaAtu)

Return(lRet)
                
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³17/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
Local aRotina :={	{"","AxPesqui"	,0 , 1},;
	{"","AxVisual"	,0 , 2},;
	{"","AxInclui"	,0 , 3},;
	{"","AxAltera"	,0 , 4},;
	{"","AxDeleta"	,0 , 5} }  // Somente para contabilizacao

Return(aRotina)
