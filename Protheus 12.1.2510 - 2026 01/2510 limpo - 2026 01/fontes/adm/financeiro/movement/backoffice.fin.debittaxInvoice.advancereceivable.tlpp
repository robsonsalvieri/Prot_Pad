#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.fin.DebitTaxInvoice

//-------------------------------------------------------------------
/*/{Protheus.doc} AdvanceReceivable
    @description Classe responsável por retornar os adiantamentos 
    disponíveis na tabela SE1 (Contas a Receber) e que possam ser vinculados
    as notas de débito.
    Obs: Utiliza métodos herdados da classe totvs.protheus.backoffice.fin.abstract.DefaultGetModel

	@author fabio.casagrande
	@since 08/11/2025
	@type class
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
class AdvanceReceivable from totvs.protheus.backoffice.fin.abstract.DefaultGetModel
    public method new() as object 
    public method setParameters(jParameters as json)
    public method clear() as json

    // override methods ==========================
    protected method loadMapFields()
    protected method getQuery() as character
    protected method getOrder() as character
    protected method setCalcData()
    // ===========================================

    protected data oMainExec as object    
    protected data jParameters as json
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method new() as object class AdvanceReceivable
    _Super:new()
    self:clear()
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method clear() class AdvanceReceivable
    _Super:clear()
    self:setPageSize(100)
    self:setIsCaseSensitive(.T.)
    self:setUseSpaces(.T.)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setParameters
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method setParameters(jParameters as json) class AdvanceReceivable
    default jParameters := JsonObject():new()
    if jParameters != nil .and. ValType(jParameters) == "J"
        self:jParameters := jParameters
    endif
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getOrder
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
*/
//-------------------------------------------------------------------
method getOrder() as character class AdvanceReceivable
    local cKey as character
    local aArea := FwGetArea() as array
    local aAreaFK7 := {} as array

    DbSelectArea("FK7")
    aAreaFK7 := FK7->(FWGetArea())
    
    cKey := SqlOrder(FK7->(IndexKey( 4 ))) //FK7_ALIAS, FK7_FILTIT, FK7_PREFIX, FK7_NUM, FK7_PARCEL, FK7_TIPO, FK7_CLIFOR, FK7_LOJA

    FwRestArea(aAreaFK7)
    FwRestArea(aArea)
return cKey

//-------------------------------------------------------------------
/*/{Protheus.doc} loadMapFields
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method loadMapFields() class AdvanceReceivable
    self:addMapFields('FK7_ALIAS', 'FK7_ALIAS', .F.)    
    self:addMapFields('identification', 'FK7_IDDOC', .T.)
    self:addMapFields('billBranch', 'FK7_FILTIT', .T.)
    self:addMapFields('billPrefix', 'FK7_PREFIX', .T.)
    self:addMapFields('billNumber', 'FK7_NUM', .F.)
    self:addMapFields('billInstallment', 'FK7_PARCEL', .T.)
    self:addMapFields('billType', 'FK7_TIPO', .T.)
    self:addMapFields('participantCode', 'FK7_CLIFOR', .T.)
    self:addMapFields('participantUnit', 'FK7_LOJA', .T.)
    self:addMapFields('participantName', 'E1_NOMCLI', .T.)
    self:addMapFields('billCurrency', "E1_MOEDA", .T.)
    self:addMapFields('billValue', "E1_VALOR", .T.)
    self:addMapFields('billIssueDate', "E1_EMISSAO", .T., .F., { 'E1_EMISSAO', 'D', TamSX3('FK2_DATA')[1], 0 })
    self:addMapFields('billDueDate', "E1_VENCTO", .T., .F., { 'E1_VENCTO', 'D', TamSX3('FK2_DATA')[1], 0 })
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method getQuery() as character class AdvanceReceivable
    local cQuery := "" as character
    local nParam := 1 as numeric
    local cSep := If("|"$MVRECANT,"|",",") as character
            
    cQuery := " SELECT ? FROM "
    cQuery += "( SELECT ? "
    cQuery += "FROM " + RetSqlName("SE1") + " SE1 "
    cQuery += "INNER JOIN " + RetSqlName("FK7") + " FK7 ON "
    cQuery +=   "FK7.FK7_ALIAS = ? "
    cQuery +=   "AND FK7.FK7_FILTIT = SE1.E1_FILIAL "
    cQuery +=   "AND FK7.FK7_PREFIX = SE1.E1_PREFIXO "
    cQuery +=   "AND FK7.FK7_NUM = SE1.E1_NUM "
    cQuery +=   "AND FK7.FK7_PARCEL = SE1.E1_PARCELA "
    cQuery +=   "AND FK7.FK7_TIPO = SE1.E1_TIPO "
    cQuery +=   "AND FK7.FK7_CLIFOR = SE1.E1_CLIENTE "
    cQuery +=   "AND FK7.FK7_LOJA = SE1.E1_LOJA "
    cQuery +=   "AND FK7.D_E_L_E_T_ = ? "        
    cQuery += "WHERE "
    cQuery +=   "SE1.E1_FILIAL = ? "

    if self:jParameters:hasProperty("billPrefix")
        cQuery +=   "AND SE1.E1_PREFIXO = ? "
    endif

    if self:jParameters:hasProperty("billNumber")
        cQuery +=   "AND SE1.E1_NUM = ? "
    endif
            
    cQuery +=   "AND SE1.E1_CLIENTE = ? "
    cQuery +=   "AND SE1.E1_LOJA = ? "
    cQuery +=   "AND SE1.E1_TIPO IN  ? "
    cQuery +=   "AND SE1.E1_VALOR = SE1.E1_SALDO "

    if self:jParameters:hasProperty("FromDate") .and. self:jParameters:hasProperty("ToDate")
        cQuery += " AND SE1.E1_EMISSAO BETWEEN ? AND ? "
    endif

    cQuery +=   "AND SE1.D_E_L_E_T_ = ? "        
    cQuery +=   "AND NOT EXISTS "
    cQuery +=       "(SELECT 1 FROM " + RetSqlName("F7Q") + " F7Q "
    cQuery +=       "WHERE F7Q.F7Q_IDDOC = FK7.FK7_IDDOC "
    cQuery +=       "AND F7Q.D_E_L_E_T_ = ?) "
    cQuery += " ? ) TEMPTABLE "
    cQuery := changeQuery(cQuery)
    self:oMainExec := FWExecStatement():new(cQuery)

    self:oMainExec:setUnsafe(nParam++, "#QueryFields#")
    self:oMainExec:setUnsafe(nParam++, self:getMapFieldsToString())    
    self:oMainExec:SetString(nParam++, 'SE1')
    self:oMainExec:SetString(nParam++, ' ')
    self:oMainExec:SetString(nParam++, FWxFilial("SE1", self:jParameters["billBranch"]))

    if self:jParameters:hasProperty("billPrefix")
        self:oMainExec:SetString(nParam++, self:jParameters["billPrefix"] )
    endif

    if self:jParameters:hasProperty("billNumber")
        self:oMainExec:SetString(nParam++, self:jParameters["billNumber"] )
    endif

    self:oMainExec:SetString(nParam++, self:jParameters["participantCode"])
    self:oMainExec:SetString(nParam++, self:jParameters["participantUnit"])
    self:oMainExec:SetUnSafe(nParam++, FormatIn(MVRECANT,cSep))        

    if self:jParameters:hasProperty("FromDate") .and. self:jParameters:hasProperty("ToDate")
        self:oMainExec:SetDate(nParam++, self:jParameters["FromDate"] )
        self:oMainExec:SetDate(nParam++, self:jParameters["ToDate"] )        
    endif

    self:oMainExec:SetString(nParam++, ' ')
    self:oMainExec:SetString(nParam++, ' ')
    self:oMainExec:setUnsafe(nParam++, "#QueryWhere#")

    cQuery := self:oMainExec:getFixQuery()
return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} setCalcData
    Método herdado da classe totvs.protheus.backoffice.fin.abstract.DefaultGetModel
    Define o conteúdo dos campos calculados para cada registro retornado na query principal do
    FwAdapterBaseV2.
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method setCalcData() class AdvanceReceivable

    self:jCalcData['identification']	:= (self:cAlias)->FK7_IDDOC
    self:jCalcData['billBranch'] 		:= (self:cAlias)->FK7_FILTIT
    self:jCalcData['billPrefix']		:= (self:cAlias)->FK7_PREFIX
    self:jCalcData['billNumber']		:= (self:cAlias)->FK7_NUM
    self:jCalcData['billInstallment']   := (self:cAlias)->FK7_PARCEL
    self:jCalcData['billType']		    := (self:cAlias)->FK7_TIPO            
    self:jCalcData['participantCode']	:= (self:cAlias)->FK7_CLIFOR
    self:jCalcData['participantUnit']	:= (self:cAlias)->FK7_LOJA
    self:jCalcData['participantName']	:= (self:cAlias)->E1_NOMCLI
    self:jCalcData['billCurrency']	    := (self:cAlias)->E1_MOEDA
    self:jCalcData['billValue']	        := (self:cAlias)->E1_VALOR
    self:jCalcData['billIssueDate']	    := dToC((self:cAlias)->E1_EMISSAO)
    self:jCalcData['billDueDate']	    := dToC((self:cAlias)->E1_VENCTO)

return
