#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.fin.debittaxinvoice.ch'

namespace totvs.protheus.backoffice.fin.debittaxinvoice

//-------------------------------------------------------------------
/*/{Protheus.doc} DebitTaxInvoice
    Classe centralizadora de componentes de consulta e manipulação de dados
    do módulo SIGAFIN relacionados a Nota Fiscal de Débito.

    @type class
    @author fabio.casagrande
    @since 30/10/2025
/*/
//-------------------------------------------------------------------
class DebitTaxInvoice
    public method new() as object
    public method clear()
    public method setParameters(jParameters as json)
    public method setPageSize(nPageSize as numeric)
    public method getAdvancePayments(nPage as numeric) as json
    public method getAdvanceReceivable(nPage as numeric) as json
    public method prepareRecordF7Q()
    public method recordInsertF7Q() as logical
    public method validateDeletionF7Q() as logical
    public method validateAdvanceTitleDeletion(cPortfolioTable as character) as logical
    public method prepareDeletionF7Q() as logical
    public method recordDeletionF7Q() as logical
    public method setBillLock(cIdDoc as character) as logical
    public method setBillUnLock(cIdDoc as character) as logical

    public method getValidatedEnvironment() as logical
    public method setValidatedEnvironment(lValue as logical)
    public method getResult() as json
    public method setResult(jValue as json)
    public method getCanDelete() as logical
    public method setCanDelete(lValue as logical)
    public method getPrepareDeletionF7Q() as json
    public method setPrepareDeletionF7Q(jValue as json)

    protected method validateEnvironment()  as logical
    protected method validateParameters(cMethod as character) as logical
    protected method newException(cDescription as character) as object
    protected method logError(oError as object)

    protected data oAdvancePayments as object
    protected data oAdvanceReceivable as object
    protected data jParameters as json
    protected data aMandatoryParameters as array
    protected data lValidatedEnvironment as logical
    protected data jResult as json
    protected data lCanDelete as logical
    protected data jPrepareDeletionF7Q as json

    protected method hasFinancialMovements(cIdDoc as character) as logical
    protected method positionInF7Q(nIndexOrder as numeric, aContents as array) as logical
    protected method billLockControl(cIdDoc as character, lRequestedLock as logical) as logical
    protected method getBillDataFromFK7(cIdDoc as character) as json
    protected method buildBillKey(jBillData as json) as character
    protected method performBillLockOperation(cBillAlias as character, cBillKey as character, lRequestedLock as logical) as logical
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method new() as object class DebitTaxInvoice
    self:clear()
    self:validateEnvironment()
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
    Limpa e inicializa as propriedades da classe
    
    @author fabio.casagrande
    @since 30/10/2025
    @version 12.1.2510
/*/
//-------------------------------------------------------------------
method clear() class DebitTaxInvoice
    self:jParameters := JsonObject():new()
    self:jResult := JsonObject():new()    
    self:jPrepareDeletionF7Q := JsonObject():new()  
    self:aMandatoryParameters := {"billBranch", "participantCode", "participantUnit"}
    self:oAdvancePayments := AdvancePayment():new()
    self:oAdvanceReceivable := AdvanceReceivable():new()    
    self:lValidatedEnvironment := .F.
    self:lCanDelete := .F.
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getValidatedEnvironment
    @return logical, Flag de ambiente válido
/*/
//-------------------------------------------------------------------
method getValidatedEnvironment() as logical class DebitTaxInvoice
return self:lValidatedEnvironment

/*/{Protheus.doc} setValidatedEnvironment
    @param lValue logical, Ajusta flag de ambiente (uso controlado/testes)
/*/
method setValidatedEnvironment(lValue as logical) class DebitTaxInvoice
    default lValue := .F.

    if ValType(lValue) == "L"
        self:lValidatedEnvironment := lValue
    endif
return

/*/{Protheus.doc} getResult
    @return json, Retorna JSON preparado do registro
/*/
method getResult() as json class DebitTaxInvoice
return self:jResult

/*/{Protheus.doc} setResult
    @param jValue json, Define JSON preparado
/*/
method setResult(jValue as json) class DebitTaxInvoice
    default jValue := JsonObject():new()
    
    if jValue != NIL .and. ValType(jValue) == "J"
        self:jResult := jValue
    endif
return

/*/{Protheus.doc} getCanDelete
    @return logical, Retorna se possível excluir registro
/*/
method getCanDelete() as logical class DebitTaxInvoice
return self:lCanDelete

/*/{Protheus.doc} setCanDelete
    @param lValue logical, Ajusta permissão de deleção
/*/
method setCanDelete(lValue as logical) class DebitTaxInvoice
    default lValue := .F.

    if ValType(lValue) == "L"
        self:lCanDelete := lValue
    endif
return

/*/{Protheus.doc} getPrepareDeletionF7Q
    @return json, JSON preparado para deleção
/*/
method getPrepareDeletionF7Q() as json class DebitTaxInvoice
return self:jPrepareDeletionF7Q

/*/{Protheus.doc} setPrepareDeletionF7Q
    @param jValue json, Define JSON de deleção
/*/
method setPrepareDeletionF7Q(jValue as json) class DebitTaxInvoice
    default jValue := JsonObject():new()

    if jValue != NIL .and. ValType(jValue) == "J"
        self:jPrepareDeletionF7Q := jValue
    endif
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAdvancePayments
    @author fabio.casagrande
    @since 08/11/2025
    @type method
    @version 12.1.2510
    @param nPage numeric, Número da página a ser retornada
    @return json, Retorna os dados dos adiantamentos de pagamentos (PA)
/*/
//-------------------------------------------------------------------
method getAdvancePayments(nPage as numeric) as json class DebitTaxInvoice   
    local jResult := JsonObject():new() as json
    
    default nPage := 1      

    try
        self:validateParameters() 
        self:oAdvancePayments:processData(nPage)
        jResult:fromJson(self:oAdvancePayments:getJSONResponse())
    catch oError
        self:logError(oError)
        jResult['code'] := "500"
        jResult['message'] := STR0004 //"Erro ao processar a operação solicitada."
        jResult['detailedMessage'] := oError:description
    endTry

return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAdvanceReceivable
    @author fabio.casagrande
    @since 08/11/2025
    @type method
    @version 12.1.2510
    @param nPage numeric, Número da página a ser retornada
    @return json, Retorna os dados dos adiantamentos de recebíveis (RA)
/*/
//-------------------------------------------------------------------
method getAdvanceReceivable(nPage as numeric) as json class DebitTaxInvoice 
    local jResult := JsonObject():new() as json
    
    default nPage := 1      

    try
        self:validateParameters() 
        self:oAdvanceReceivable:processData(nPage)
        jResult:fromJson(self:oAdvanceReceivable:getJSONResponse())
    catch oError
        self:logError(oError)
        jResult['code'] := "500"
        jResult['message'] := STR0004 //"Erro ao processar a operação solicitada."
        jResult['detailedMessage'] := oError:description
    endTry

return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} validateParameters
    @author fabio.casagrande
    @since 08/11/2025
    @type method
    @version 12.1.2510
    @param cMethod character, Nome do método que está solicitando a validação (opcional)
    @return logical, .T. se os parâmetros estão válidos 
/*/
//-------------------------------------------------------------------
method validateParameters(cMethod as character) as logical class DebitTaxInvoice
    local nX := 1 as numeric
    local cParameter := "" as character
    local lReturn := .T. as logical
    local aMandatory := aClone(self:aMandatoryParameters) as Array
    local aMandatoryCustom := {} as Array
    
    default cMethod := ""

    if cMethod == "prepareRecordF7Q"

        aMandatoryCustom := { "documentNumber", "documentSeries", "mainSourceTable", "documentBranch", "identification" }

    elseif cMethod == "validateDeletionF7Q"

        aMandatoryCustom := { "documentNumber", "documentSeries", "mainSourceTable", "documentBranch"}

    endif

    if Len(aMandatoryCustom) > 0
        for nX := 1 To Len(aMandatoryCustom)
            AADD(aMandatory, aMandatoryCustom[nX])
        next nX
    endif

    for nX := 1 to len(aMandatory)
        cParameter := aMandatory[nX]
        if !self:jParameters:hasProperty(cParameter)
            lReturn := .F.
            throw self:newException(STR0005 + ' ' + cParameter) //Parâmetro obrigatório não informado
        endif
    next nX

return lReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} setParameters
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method setParameters(jParameters as json) class DebitTaxInvoice
    default jParameters := JsonObject():new()
    
    if jParameters != nil .and. ValType(jParameters) == "J"
        self:jParameters := jParameters
        self:oAdvancePayments:setParameters(self:jParameters)
        self:oAdvanceReceivable:setParameters(self:jParameters)
    endif
return

//-------------------------------------------------------------------
/*/{Protheus.doc} newException
	@author fabio.casagrande
	@since 17/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method newException(cDescription as character) as object class DebitTaxInvoice    
	local oError := ErrorClass():New() 
	oError:description := cDescription
return oError

//-------------------------------------------------------------------
/*/{Protheus.doc} logError
	@author fabio.casagrande
	@since 08/11/2025
	@type method
	@version 12.1.2510
/*/
//-------------------------------------------------------------------
method logError(oError as object) class DebitTaxInvoice
    if oError != NIL    
        FWLogMsg("WARN", , "SIGAFIN","DebitTaxInvoice", procName(1), , , , , {{"description", oError:description}, /*{"errorEnv", oError:errorEnv},*/ {"errorStack", oError:errorStack}})
    endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} validateEnvironment
    Verifica se o RPO contem as versões adequadas dos fontes
    que possuem funções que o componente vai consumir.
    
    @author fabio.casagrande
	@since 30/10/2025
	@version 12.1.2510
    @return logical, .T. se o ambiente está ok
/*/
//-------------------------------------------------------------------
method validateEnvironment() as logical class DebitTaxInvoice
    local cCompFK7 := "" as character
    local cCompF7Q := "" as character
    local aArea := FwGetArea() as array  

    if FWAliasInDic("F7Q",.F.)
        self:lValidatedEnvironment := .T.
        DbSelectArea("F7Q") //Para criar a tabela no banco de dados, caso ainda não exista.

        // Modo de compartilhamento das tabelas
        cCompFK7  := FWModeAccess("FK7", 1) + FWModeAccess("FK7", 2) + FWModeAccess("FK7", 3)
        cCompF7Q  := FWModeAccess("F7Q", 1) + FWModeAccess("F7Q", 2) + FWModeAccess("F7Q", 3)

        if (cCompFK7 <> cCompF7Q)
            self:lValidatedEnvironment := .F.
            Help(,,"F7QCOMP",,STR0001,1,0,,,,,,{STR0002}) // Modo de compartilhamento de tabelas incorreto.
        endif
    else
        self:lValidatedEnvironment := .F.
        Help(,,"NotExistF7Q",,STR0001,1,0,,,,,,{STR0003}) // A tabela F7Q não existe no dicionário de dados.
    endIf

    FwRestArea(aArea)

return self:lValidatedEnvironment

//-------------------------------------------------------------------
/*/{Protheus.doc} prepareRecordF7Q
Componente para preparar dados do registro F7Q em formato JSON

@type Method
@param billBranch, Character, Filial
@param documentSeries, Character, Série do documento
@param documentNumber, Character, Número do documento
@param participantCode, Character, Código do participante
@param participantUnit, Character, Loja do participante
@param mainSourceTable, Character, Tabela origem principal
@Return Character, JSON com os dados preparados
@author sidney.silva
@since 30/10/2025
/*/
//-------------------------------------------------------------------
Method prepareRecordF7Q() Class DebitTaxInvoice
    Local jResponse     as Json
    Local nX            as Numeric
    Local lContinue := .T. as logical

    If self:lValidatedEnvironment
        try
            self:validateParameters("prepareRecordF7Q") 
            lContinue := .T.
        catch oError
            self:logError(oError)
            self:jResult['code'] := "500"
            self:jResult['message'] := STR0004 //"Erro ao processar a operação solicitada."
            self:jResult['detailedMessage'] := oError:description
            lContinue := .F.            
        endTry
    Endif

    if lContinue
        jResponse := JsonObject():New()
        jResponse["F7Q_FILIAL"]     := xFilial("FK7", self:jParameters["billBranch"])
        jResponse["F7Q_CLIFOR"]     := self:jParameters["participantCode"]
        jResponse["F7Q_LOJA"]       := self:jParameters["participantUnit"]
        jResponse["F7Q_DOC"]        := self:jParameters["documentNumber"]
        jResponse["F7Q_SERIE"]      := self:jParameters["documentSeries"]
        jResponse["F7Q_TABORI"]     := self:jParameters["mainSourceTable"]
        jResponse["F7Q_FILORI"]     := self:jParameters["documentBranch"]
        jResponse["F7Q_IDDOC"]      := {}
        For nX := 1 To Len(self:jParameters["identification"])
            AADD(jResponse["F7Q_IDDOC"], self:jParameters["identification"][nX])
        Next nX
        self:jResult := jResponse
    endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} recordInsertF7Q
Componente para inserir registro na tabela F7Q

@type Method
@param jRecordData, Json, JSON com os dados para inserção
@Return logical, .T. se a inserção foi bem sucedida
@author sidney.silva
@since 30/10/2025
/*/
//-------------------------------------------------------------------
Method recordInsertF7Q() as logical Class DebitTaxInvoice
	Local aArea          as Array
	Local nX             as Numeric
	Local nY             as Numeric
	Local lSuccess       as logical
	Local aKeys          as Array

	aArea       := FwGetArea()
	lSuccess    := .F.
	aKeys       := { "F7Q_FILIAL", "F7Q_IDF7Q", "F7Q_IDDOC", "F7Q_TABORI", "F7Q_SERIE", "F7Q_DOC", "F7Q_CLIFOR", "F7Q_LOJA", "F7Q_FILORI" }

	If self:lValidatedEnvironment

		If self:jResult != NIL .And. self:jResult:hasProperty('F7Q_IDDOC') .And. Len(self:jResult['F7Q_IDDOC']) > 0 .And. self:jResult:hasProperty('F7Q_TABORI')
			DbSelectArea("F7Q")
			F7Q->(DbSetOrder(2))
			For nY := 1 To Len(self:jResult['F7Q_IDDOC'])
				If !F7Q->(DbSeek(self:jResult['F7Q_IDDOC'][nY] + self:jResult['F7Q_TABORI']))
					RecLock("F7Q", .T.)
					For nX := 1 To Len(aKeys)
						If aKeys[nX] == "F7Q_IDDOC"
							F7Q->(&(aKeys[nX])) := self:jResult['F7Q_IDDOC'][nY]
						ElseIf aKeys[nX] == "F7Q_IDF7Q"
							F7Q->(&(aKeys[nX])) := FWUUIDV4()
						Else
							F7Q->(&(aKeys[nX])) := self:jResult[aKeys[nX]]
						EndIf
					Next nX
					F7Q->(MsUnlock())
					lSuccess := .T.
				EndIf
			Next nY
		EndIf

		FwRestArea(aArea)

	EndIf

Return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} validateAdvanceTitleDeletion
    @description Valida se PA selecionado para exclusão possui vinculo com ND.
    @type method
    @param cPortfolioTable, Character, Identificação da carteira através da tabela (SE1 ou SE2)
    @return logical, .T. se a validação permitir a exclusão
    @author guilhermed.santos
    @since 11/11/2025
/*/
//-------------------------------------------------------------------
method validateAdvanceTitleDeletion(cPortfolioTable as character) as logical class DebitTaxInvoice
    local aArea             := {} as array
    local aAreaF7Q          := {} as array
    local cKey              := "" as character
    local lCanDelete        := .T. as logical
    local cIdDoc            := "" as character

    default cPortfolioTable := "SE2"

    if self:lValidatedEnvironment
        aArea := FwGetArea()
        aAreaF7Q := F7Q->(FwGetArea())
        
        if cPortfolioTable == "SE2"
            cKey := SE2->E2_FILIAL + "|" +  SE2->E2_PREFIXO + "|" + SE2->E2_NUM + "|" + SE2->E2_PARCELA + "|" + SE2->E2_TIPO + "|" +SE2->E2_FORNECE + "|" + SE2->E2_LOJA
        else
            cKey := SE1->E1_FILIAL + "|" +  SE1->E1_PREFIXO + "|" + SE1->E1_NUM + "|" + SE1->E1_PARCELA + "|" + SE1->E1_TIPO + "|" +SE1->E1_CLIENTE + "|" + SE1->E1_LOJA
        endIf

        cIdDoc := FinBuscaFK7(cKey,cPortfolioTable)

        if !empty(cIdDoc) .and. self:positionInF7Q(2,{cIdDoc})
            Help(,,"ExisteF7Q",,STR0008,1,0,,,,,,{STR0009})//"Existe uma nota na tabela F7Q vinculado a este adiantamento"//"Exclua a nota antes de excluir o adiantamento"
            lCanDelete := .F.
        endIf

        FwRestArea(aAreaF7Q)
        FwRestArea(aArea)
    endIf

    self:lCanDelete := lCanDelete
return lCanDelete

//-------------------------------------------------------------------
/*/{Protheus.doc} validateDeletionF7Q
    @description Componente para validar exclusão de adiantamentos
    @type method
    @return logical, .T. se a validação permitir a exclusão
    @author guilhermed.santos
    @since 06/11/2025
/*/
//-------------------------------------------------------------------
method validateDeletionF7Q() as logical class DebitTaxInvoice
    local aParameters   := {} as array
    local aArea         := {} as array
    local aAreaF7Q      := {} as array

    aArea := FwGetArea()

    if !(self:lValidatedEnvironment)
        return self:lCanDelete := .F.
    endIf

    try
        self:validateParameters("validateDeletionF7Q") 
        self:lCanDelete := .T.
    catch oError
        self:lCanDelete := .F.
        self:logError(oError)
        self:jResult['code'] := "500"
        self:jResult['message'] := STR0004 //"Erro ao processar a operação solicitada."
        self:jResult['detailedMessage'] := oError:description   
    endTry
    
    if self:lCanDelete
        aAreaF7Q := F7Q->(FwGetArea())

        aadd(aParameters, self:jParameters["mainSourceTable"])
        aadd(aParameters, self:jParameters["documentBranch"]) 
        aadd(aParameters, self:jParameters["documentSeries"]) 
        aadd(aParameters, self:jParameters["documentNumber"]) 
        aadd(aParameters, self:jParameters["participantCode"])
        aadd(aParameters, self:jParameters["participantUnit"])

        if !self:positionInF7Q(3,aParameters)
            Help(,,"F7Q_Not_Found",,STR0010,1,0,,,,,,{STR0011})// "Dados da nota de débito não localizados na tabela de vínculo com o titulo de adiantamento."
            return self:lCanDelete := .F.
        endIf  

        while F7Q->(!eof()) .and. F7Q->F7Q_TABORI == aParameters[1] .and. F7Q->F7Q_FILORI == aParameters[2]  ;
                            .and. F7Q->F7Q_SERIE == aParameters[3] .and. F7Q->F7Q_DOC == aParameters[4];
                            .and. F7Q->F7Q_CLIFOR == aParameters[5] .and. F7Q->F7Q_LOJA == aParameters[6]
            if self:hasFinancialMovements(F7Q->F7Q_IDDOC)
                self:lCanDelete := .F.
                Help(,,"PA_Movimentado",,STR0006,1,0,,,,,,{STR0007})// "Existe movimentação financeira no título de adiantamento vinculado a esta nota"//"Estorne a movimentação do título de adiantamento antes de efetuar a exclusão da nota"
                exit
            endIf
            F7Q->(dbSkip())
        endDo

        FwRestArea(aAreaF7Q)
    endIf

    FwRestArea(aArea)    

return self:lCanDelete

//-------------------------------------------------------------------
/*/{Protheus.doc} prepareDeletionF7Q
    @description Componente para preparar dados do registro F7Q em formato JSON
    @type Method
    @Return Nil
    @author guilhermed.santos
    @since 07/11/2025
/*/
//-------------------------------------------------------------------
method prepareDeletionF7Q() class DebitTaxInvoice
    local aArea         := {} as array
    local aAreaF7Q      := {} as array
    local aParameters   := {} as array
    local aResponse     := {} as array

    if self:lCanDelete
        self:lCanDelete := .F.

        aadd(aParameters, self:jParameters["mainSourceTable"])
        aadd(aParameters, self:jParameters["documentBranch"]) 
        aadd(aParameters, self:jParameters["documentSeries"]) 
        aadd(aParameters, self:jParameters["documentNumber"]) 
        aadd(aParameters, self:jParameters["participantCode"])
        aadd(aParameters, self:jParameters["participantUnit"])

        aArea := FwGetArea()
        aAreaF7Q := F7Q->(FwGetArea())

        if self:positionInF7Q(3,aParameters)
            self:lCanDelete := .T.
            while F7Q->(!eof()) .and. F7Q->F7Q_TABORI == aParameters[1] .and. F7Q->F7Q_FILORI == aParameters[2] .and. F7Q->F7Q_SERIE == aParameters[3] .and. F7Q->F7Q_DOC == aParameters[4];
                                .and. F7Q->F7Q_CLIFOR == aParameters[5] .and. F7Q->F7Q_LOJA == aParameters[6]
                aadd(aResponse,{"F7Q_FILIAL":   F7Q->F7Q_FILIAL,;
                                "F7Q_IDF7Q":    F7Q->F7Q_IDF7Q,;
                                "F7Q_IDDOC":    F7Q->F7Q_IDDOC,;
                                "F7Q_TABORI":   F7Q->F7Q_TABORI,;
                                "F7Q_SERIE":    F7Q->F7Q_SERIE,;
                                "F7Q_DOC":      F7Q->F7Q_DOC,;
                                "F7Q_CLIFOR":   F7Q->F7Q_CLIFOR,;
                                "F7Q_LOJA":     F7Q->F7Q_LOJA,;
                                "F7Q_FILORI":   F7Q->F7Q_FILORI})
                
                self:jPrepareDeletionF7Q["response"] := aResponse
                
                F7Q->(dbSkip())
            endDo
        endIf

        FwRestArea(aAreaF7Q)
        FwRestArea(aArea)
    endIf

return

//-------------------------------------------------------------------
/*/{Protheus.doc} recordDeletionF7Q
    @description Componente para efetuar a exclusão do registro na tabela F7Q.
    @type Method
    @Return Logical, .T. se a deleção foi bem sucedida
    @author guilhermed.santos
    @since 08/11/2025
/*/
//-------------------------------------------------------------------
method recordDeletionF7Q() As Logical Class DebitTaxInvoice
    local aArea     := {} as array
    local aAreaF7Q  := {} as array
    local aResponse := {} as array
    local aKey      := {} as array
    local lSuccess  := .F. as Logical
    local nCounter  := 1 as numeric

    if self:lCanDelete .and. self:jPrepareDeletionF7Q:hasProperty("response")
        aResponse := self:jPrepareDeletionF7Q["response"]

        aArea := FwGetArea()
        aAreaF7Q := F7Q->(FwGetArea())

        for nCounter := 1 to len(aResponse)
            aKey := {aResponse[nCounter]['F7Q_IDDOC'], aResponse[nCounter]['F7Q_TABORI']}

            if self:positionInF7Q(2,aKey)
                RecLock("F7Q", .F.)
                    F7Q->(dbDelete())
                F7Q->(msUnlock())
                lSuccess := .T.
            endIf

        next nCounter

        FwRestArea(aAreaF7Q)
        FwRestArea(aArea)
    endIf

return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} hasFinancialMovements
    @description Verifica se existe movimento financeiro no PA/RA vinculado a nota de débito/crédito
    @type method
    @param cIdDoc, character, Código de identificação do título na FK7 e F7Q.
    @return logical, .T. se existe movimento financeiro no PA/RA.
    @author guilhermed.santos
    @since 06/11/2025
/*/
//-------------------------------------------------------------------
method hasFinancialMovements(cIdDoc as character) as logical class DebitTaxInvoice
    local jParameters   := JsonObject():New() as json
    local jReturn       := JsonObject():New() as json
    local lHasMovement  := .F. as logical
    local oMovements    := Nil as object

    default cIdDoc := ""
    
    if !empty(cIdDoc)
        jParameters['branch'] := xFilial("FK7")
        jParameters['lcompensated']		:= .F.

        oMovements  := totvs.protheus.backoffice.fin.movements.Movements():New(jParameters)
        oMovements:setIdDoc(cIdDoc)
        jReturn := oMovements:getWriteOffsByBill()
        
        lHasMovement := len(jReturn['document']) > 0
    endIf

return lHasMovement

//-------------------------------------------------------------------
/*/{Protheus.doc} positionInF7Q
    @description Posiciona o ponteiro na F7Q com o ID utilizado na transação.
    @type method
    @param nIndexOrder, numeric, Número do índice da F7Q que será utilizado para o Seek
    @param aContents, array, Valores que serão concatenados e utilizados do dbSeek()
    @return logical, .T. se concluiu o dbSeek
    @author guilhermed.santos
    @since 09/11/2025
/*/
//-------------------------------------------------------------------
method positionInF7Q(nIndexOrder as numeric, aContents as array) as logical class DebitTaxInvoice
    local lReturn   := .F. as logical
    local cKey      := "" as character
    local nCounter  := 1 as numeric

    default nIndexOrder := 1
    default aContents := {}


    if len(aContents) > 0
        for nCounter := 1 to len(aContents)
            cKey += aContents[nCounter]
        next nCounter

        if !empty(cKey)
            dbSelectArea("F7Q")
            F7Q->(dbSetOrder(nIndexOrder))
            F7Q->(dbGoTop())
            lReturn := F7Q->(dbSeek(cKey))
        endIf
    endIf

return lReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} setBillLock
    @description Tenta bloquear o registro do título de adiantamento 
        na sua tabela de origem (SE1 ou SE2). Será utilizado quando 
        o usuário selecionar um título de adiantamento para vincular
        a uma nota de débito, evitando que outro usuário altere o 
        mesmo título em outra transação.
    @author fabio.casagrande
	@since 10/11/2025
	@type method
	@version 12.1.2510
    @param cIdDoc character, Identificação do título a ser 
    bloqueado na tabela de origem (SE1 ou SE2)
    */
//-------------------------------------------------------------------
method setBillLock(cIdDoc as character) as logical class DebitTaxInvoice
    default cIdDoc := ""
return self:billLockControl(cIdDoc, .T.)

//-------------------------------------------------------------------
/*/{Protheus.doc} setBillUnLock
    @description Desbloqueia o registro do título de adiantamento 
        na sua tabela de origem (SE1 ou SE2). Será utilizado quando 
        o usuário finalizar a gravação da nota de débito ou cancelar 
        a operação, liberando o título para ser utilizado.
	@author fabio.casagrande
	@since 10/11/2025
	@type method
	@version 12.1.2510
    @param cIdDoc character, Identificação do título a ser 
    desbloqueado na tabela de origem (SE1 ou SE2)
    @return logical, Retorna .T. se o título foi desbloqueado com sucesso
/*/
//-------------------------------------------------------------------
method setBillUnLock(cIdDoc as character) as logical class DebitTaxInvoice
    default cIdDoc := ""   
return self:billLockControl(cIdDoc, .F.)

//-------------------------------------------------------------------
/*/{Protheus.doc} BillLockControl
    @description Método de controle de bloqueio/desbloqueio do título 
        de adiantamento na sua tabela de origem (SE1 ou SE2), baseado 
        no título informado.
	@author fabio.casagrande
	@since 10/11/2025
	@version 12.1.2510
	@type method
    @param cIdDoc character, Identificação do documento na tabela FK7
    @param lRequestedLock, logical, Indica se o título deve ser bloqueado (.T.) ou desbloqueado (.F.)
    @return logical, Indica se a operação de bloqueio/desbloqueio foi realizada com sucesso
/*/
//-------------------------------------------------------------------
method billLockControl(cIdDoc as character, lRequestedLock as logical) as logical class DebitTaxInvoice
    local lSuccess := .F. as logical
    local aArea := {} as array
    local jBillData := JsonObject():new() as json
    local cBillKey := "" as character
    
    default cIdDoc := ""
    default lRequestedLock := .T.

    if self:lValidatedEnvironment .and. !Empty(cIdDoc)
        aArea := FwGetArea()
        
        // Busca dados do título na FK7
        jBillData := self:getBillDataFromFK7(cIdDoc)
        
        if jBillData["found"]
            // Constrói a chave do título baseada no alias
            cBillKey := self:buildBillKey(jBillData)
            
            // Executa a operação de bloqueio/desbloqueio
            lSuccess := self:performBillLockOperation(jBillData["alias"], cBillKey, lRequestedLock)
        endif
        
        FwRestArea(aArea)
    endif

return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} getBillDataFromFK7
    @description Busca dados do título na tabela FK7
    @type method
    @param cIdDoc character, Identificação do documento na tabela FK7
    @return json, Objeto com dados do título (alias e encontrado)
    @author fabio.casagrande
    @since 12/11/2025
/*/
//-------------------------------------------------------------------
method getBillDataFromFK7(cIdDoc as character) as json class DebitTaxInvoice
    local jBillData := JsonObject():new() as json
    local aAreaFK7 := {} as array
    
    default cIdDoc := ""

    jBillData["found"] := .F.
    jBillData["alias"] := ""
    jBillData["branch"] := ""
    jBillData["prefix"] := ""
    jBillData["number"] := ""
    jBillData["installment"] := ""
    jBillData["type"] := ""
    jBillData["participantCode"] := ""
    jBillData["participantUnit"] := ""

    if !Empty(cIdDoc)
        aAreaFK7 := FK7->(FwGetArea())
        
        FK7->(dbSetOrder(5))
        if FK7->(dbSeek(cIdDoc))
            jBillData["found"] := .T.
            jBillData["alias"] := FK7->FK7_ALIAS
            jBillData["branch"] := FK7->FK7_FILTIT
            jBillData["prefix"] := FK7->FK7_PREFIX
            jBillData["number"] := FK7->FK7_NUM
            jBillData["installment"] := FK7->FK7_PARCEL
            jBillData["type"] := FK7->FK7_TIPO
            jBillData["participantCode"] := FK7->FK7_CLIFOR
            jBillData["participantUnit"] := FK7->FK7_LOJA
        endif
        
        FwRestArea(aAreaFK7)
    endif
    
return jBillData

//-------------------------------------------------------------------
/*/{Protheus.doc} buildBillKey
    @description Constrói a chave do título baseada no alias da tabela
    @type method
    @param jBillData json, Objeto JSON com os dados do título
    @return character, Chave formatada para busca do título
    @author fabio.casagrande
    @since 12/11/2025
/*/
//-------------------------------------------------------------------
method buildBillKey(jBillData as json) as character class DebitTaxInvoice
    local cBillKey := "" as character

    default jBillData := JsonObject():new()
    
    if Len(jBillData:GetNames()) > 8
        cBillKey := jBillData["branch"] + jBillData["prefix"] + jBillData["number"] + jBillData["installment"] + jBillData["type"]
        if jBillData["alias"]  == "SE2"
            cBillKey += jBillData["participantCode"] + jBillData["participantUnit"]
        endif
    endif    

return cBillKey

//-------------------------------------------------------------------
/*/{Protheus.doc} performBillLockOperation
    @description Executa a operação de bloqueio/desbloqueio do título
    @type method
    @param cBillAlias character, Alias da tabela do título (SE1 ou SE2)
    @param cBillKey character, Chave do título formatada para busca
    @param lRequestedLock logical, Indica se deve bloquear (.T.) ou desbloquear (.F.)
    @return logical, .T. se a operação foi executada com sucesso
    @author fabio.casagrande
    @since 12/11/2025
/*/
//-------------------------------------------------------------------
method performBillLockOperation(cBillAlias as character, cBillKey as character, lRequestedLock as logical) as logical class DebitTaxInvoice
    local lSuccess := .F. as logical
    local aAreaAlias := {} as array
    local lBlocked := .F. as logical
    local lRequestedUnLock := .F. as logical

    default cBillAlias := ""
    default cBillKey := ""
    default lRequestedLock := .F.
    
    lRequestedUnLock := !lRequestedLock

    if !Empty(cBillAlias) .and. !Empty(cBillKey)
        aAreaAlias := (cBillAlias)->(FwGetArea())
        
        (cBillAlias)->(DbSetOrder(1))
        if (cBillAlias)->(DbSeek(cBillKey))
            lBlocked := (cBillAlias)->(SimpleLock())
            lSuccess := .T.
        
            if lRequestedLock .and. !lBlocked
                lSuccess := .F.                    
                Help(,,"LockedAdvanceBill",,STR0012,1,0,,,,,,{STR0013}) //## "Esse título já está sendo utilizado em outra operação no sistema."
            elseIf lRequestedUnLock .and. lBlocked 
                (cBillAlias)->(MsUnLock()) //Libera o registro bloqueado anteriormente.
            EndIf
        endif
        
        FwRestArea(aAreaAlias)
    endif
    
return lSuccess
