#INCLUDE "FINA380.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWCOMMAND.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "TBICONN.CH" 
#INCLUDE "FWLIBVERSION.CH"

Static __lMetric    := .F.

//--------------------------------------------------------------         
/*/{Protheus.doc} FINA380
Conciliação bancária

@author Pilar S. Albaladejo
@since 24/10/1994
@version 12
/*/
//--------------------------------------------------------------
Function FinA380( nPosArotina, lAutomato )

	Local lExibeTela	:= !(IsBlind())
	Local dDataVrf		:= IIF(lAutomato, dDataBase, Date())
	Local lFinished		:= .F.

	__lMetric   := FwLibVersion() >= "20210517"

	Default lAutomato	:= .F.
	Default nPosArotina	:= 0

	nAutoMsg := 0	//Variável vem do TestCase

	//Rotina descontinada a partir de 01/01/2025
	If GetRpoRelease() > "12.1.033"
		If DTOS(dDataVrf) >= '20250101'
			lFinished := .T.
			If lAutomato
				If Dtos(dDataVrf) == '20250101'
					nAutoMsg := 2
				ElseIf Dtos(dDataVrf) == '20250320'
					nAutoMsg := 3
				Endif
			Endif
			F380ExpRot("FINA380","https://tdn.totvs.com/x/PKL5Kg", lAutomato, lFinished) 
			Return .T.
		Else
			If lExibeTela
				F380ExpRot("FINA380","https://tdn.totvs.com/x/PKL5Kg", lAutomato) 
			EndIf			
			If lAutomato .and. Dtos(dDataVrf) == '20241231'
				nAutoMsg := 1
				Return .T.
			Endif
		Endif
	EndIf

Return

/*/{Protheus.doc} F380ExpRot
	Apresenta uma tela informando que a rotina sera descontinuada
	@type  Function
	@author reynaldo
	@since 30/06/2021
	@version 1.0
	@param cExpirFunc, caracter, nome da rotina que deve ser descontinuada
	@param cDescrFunc, caracter, descricaod a rotina e nome da rotina que substitui a rotina descontinuada
	@param cExpiraData, caracter, data de experira??o a ser informada deve estar no formato AAAAMMDD
	@param cEndWeb, caracter, endere?o http referente a rotina que esta sendo descontinuada
/*/
Function F380ExpRot(cExpirFunc as character, cEndWeb as character, lAutomato As Logical, lFinished as Logical, nPauseDays as numeric )
	Local dDate      as date
	Local oProfile   as object
	Local aLoad      as array
	Local cShow      as character
	Local lCheck     as logical

	DEFAULT nPauseDays := 7
	DEFAULT lAutomato := .F.
	DEFAULT lFinished := .F.

	dDate := Date()
	oProfile := FwProFile():New()
	oProfile:SetTask("ESTExpired") //Nome da sess?o
	oProfile:SetType(cExpirFunc) //Valor
	aLoad := oProfile:Load()
	If Empty(aLoad)
		cShow := "00000000"
	Else
		cShow := aLoad[1]
	Endif

	// reseta o controle de nPauseDays dias e volta apresentar a tela de advertencia
	If cShow <> "00000000" .and. STOD(cShow) + nPauseDays <= dDate
		cShow := "00000000"
		oProfile:SetProfile({cShow})
		oProfile:Save()
	EndIf

	If cShow == "00000000"
		lCheck := DlgExpRot(nPauseDays, cEndWeb, lAutomato, lFinished )

		If lCheck
			cShow := dtos(date())
			oProfile:SetProfile({cShow})
			oProfile:Save()
		EndIf

	EndIf

	oProfile:Destroy()
	oProfile := nil
	aLoad := aSize(aLoad,0)
	aLoad := nil

Return

/*/{Protheus.doc} DlgExpRot
	Apresenta uma tela informando que a rotina sera descontinuada
	@type  Function
	@author reynaldo
	@since 30/06/2021
	@version 1.0
	@param cExpiraData, caracter, data de experiração a ser informada deve estar no formato AAAAMMDD
	@param nPauseDays, numeric, numero de dias que a mensagem pode ser ocultada
	@param cDescrFunc, caracter, descricaod a rotina e nome da rotina que substitui a rotina descontinuada
	@param cEndWeb, caracter, endere?o http referente a rotina que esta sendo descontinuada
	@return lCheck, logico, Verdadeiro se foi escolhido para desabilitar a mensagem por 3O dias
/*/
Static Function DlgExpRot(nPauseDays as numeric, cEndWeb as character, lAutomato as Logical, lFinished as Logical )
Local oSay1         as object
Local oSay2         as object
Local oSay3         as object
Local oCheck1       as object
Local oModal        as object
Local cMsg1  := ""  as character
Local cMsg2  := ""  as character
Local cMsg3  := ""  as character
Local cMsg4  := ""  as character
Local lCheck := .F. as logical

If !lAutomato
	oModal := FWDialogModal():New()
	oModal:SetCloseButton( .F. )
	oModal:SetEscClose( .F. )
	oModal:setTitle(STR0108) //"Comunicado Ciclo de Vida de Sofware - TOTVS Linha Protheus"

	//define a altura e largura da janela em pixel
	oModal:setSize(170, 250)

	oModal:createDialog()

	oModal:AddButton( STR0109, {||oModal:DeActivate()}, STR0109, , .T., .F., .T., ) // "Confirmar"

	oContainer := TPanel():New( ,,, oModal:getPanelMain() )
	oContainer:Align := CONTROL_ALIGN_ALLCLIENT

	If lFinished
		cMsg1 := STR0118		//"Rotina descontinuada!"
	Else
		cMsg1 := i18n(STR0110)	//"Esta rotina será descontinuada!"
		cMsg3 := STR0111 + CRLF + STR0117  //"Encerramento do ciclo de vida da rotina: após 31/10/2024"###"Bloqueio no acesso das rotinas, após 31/12/2024"
		cMsg4 := STR0116		//"Agradecemos pela compreensão."
	Endif
	oSay1 := TSay():New( 10,10,{||cMsg1 },oContainer,,,,,,.T.,,,220,20,,,,,,.T.)

	cMsg2 := Alltrim(STR0112)+space(01) 	//"Clique" "para saber mais informações a respeito da nova rotina de conciliação bancária e também as dúvidas frequentes!"
	If ! Empty(cEndWeb)
		cMsg2 += "<b><a target='_blank' href='"+cEndWeb+"'> "
		cMsg2 += STR0113 // "aqui"
		cMsg2 += " </a></b>"
		cMsg2 += "<span style='font-family: Verdana; font-size: 12px; color: #565759;' >" + ' ' +"</span>"
		cMsg2 += STR0119 // "para saber mais informações a respeito da nova rotina de conciliação bancária e também as dúvidas frequentes!"
		oSay2 := TSay():New(30,10,{||cMsg2},oContainer,,,,,,.T.,,,220,20,,,,,,.T.)
		oSay2 := TSay():New(30,10,{||cMsg2},oContainer,,,,,,.T.,,,220,20,,,,,,.T.)
		oSay2:bLClicked := {|| MsgRun( STR0114, "URL",{|| ShellExecute("open",cEndWeb,"","",1) } ) } // "Abrindo o link... Aguarde..."
	EndIf
	oSay3 := TSay():New( 50,10,{||cMsg3 },oContainer,,,,,,.T.,,,220,20,,,,,,.T.)
	oSay3 := TSay():New( 70,10,{||cMsg4 },oContainer,,,,,,.T.,,,220,20,,,,,,.T.)

	If !lFinished
		lCheck := .F.
		oCheck1 := TCheckBox():New(+100,10,i18n(STR0115,{strzero(nPauseDays,2)}) ,{|x|If(Pcount()==0,lCheck,lCheck:=x)},oContainer,220,21,,,,,,,,.T.,,,) // "Não apresentar esta mensagem nos proximos #1[30]# dias."
	Endif

	oModal:Activate()

Endif

Return lCheck
