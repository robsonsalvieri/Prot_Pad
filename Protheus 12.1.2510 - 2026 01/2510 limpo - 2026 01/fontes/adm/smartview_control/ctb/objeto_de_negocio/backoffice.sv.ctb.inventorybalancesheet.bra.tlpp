#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.inventorybalancesheet.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CT7", name="Balancete de Inventário", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} InventoryBalanceSheetSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete de Inventário
para o TReports
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class InventoryBalanceSheetSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object 

    public method ExecDBSkipTemp()          as logical
    public method processCustomData()       as json    
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    protected data cDescMoeda as character  

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class InventoryBalanceSheetSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Balancete de Inventário"
    
    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("CTR046")

    ::cDescMoeda := ""

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class InventoryBalanceSheetSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Inventário"

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() as json class InventoryBalanceSheetSmartViewBusinessObject    
    //Execução antes do append
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preSchema() as object class InventoryBalanceSheetSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class InventoryBalanceSheetSmartViewBusinessObject 
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class InventoryBalanceSheetSmartViewBusinessObject
    self:oSchema:addProperty("DESCMOEDA"  , STR0004, "string", STR0004, "DESCMOEDA"  ,,,,.F. ) // "Descrição Moeda"
    self:oSchema:addProperty("NomeEmpresa", STR0005, "string", STR0005, "NomeEmpresa",,,,.F. ) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0006, "string", STR0006, "NomeFilial" ,,,,.F. ) // "Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0007, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class InventoryBalanceSheetSmartViewBusinessObject   
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)
    self:handleCtgGerPlanParams( oFilter )
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class InventoryBalanceSheetSmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class InventoryBalanceSheetSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class InventoryBalanceSheetSmartViewBusinessObject

    local cContaIni     as character
    local cContaFim     as character
    local cMoeda        as character
    local cTpsald       as character
    local cFilSeg       as character
    local cConteudIni   as character
    local cConteudFim   as character
    local cConteudCont  as character
    local nDivide       as numeric
    local nGrupo        as numeric
    local lVlrZerado    as logical
    local lPrintZero    as logical
    local lImpAntLP     as logical
    local lImpSint      as logical    
    local lImpMov       as logical    
    local lRecDesp0		as logical
    local aSetOfBook    as array
    local aSelFil       as array
    local aCtbMoeda     as array
    local dDataIni      as date
    local dDataFim      as date
    local dDataLP       as date

    //Carrega os valores default
    cMoeda      := "01"
    cTpsald     := "1"
    aSelFil     := {}
    lImpMov     := .T.
    nDivide     := 1

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Atribui parâmetros nas variáveis
    dDataIni     := MV_PAR01 // Data Inicial
    dDataFim     := MV_PAR02 // Data Final
    cContaIni    := MV_PAR03 // Conta Inicial
    cContaFim    := MV_PAR04 // Conta Final
    lImpSint     := IIf(MV_PAR05 == 1 .Or. MV_PAR05 == 3, .T., .F.) // Imprime sinteticas
    aSetOfBook   := IIf(!Empty(MV_PAR06), CTBSetOf(MV_PAR06), CTBSetOf("")) // Codigo de Configuração dos Livros
    lVlrZerado   := IIf(MV_PAR07 == 1, .T., .F.) // Saldos Zerados?
    cMoeda       := IIf(!Empty(MV_PAR08), MV_PAR08, cMoeda)  // Moeda ?
    cTpSald      := IIf(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) // Saldos? 1=Reais;2=OrcadoS;3=Gerenciais
    nGrupo       := MV_PAR11 // Quebra ? 1=Por Grupo; 2=Por Codigo; 3=Nao Quebra
    cFilSeg      := MV_PAR12 // Filtra Segmento ?
    cConteudIni  := MV_PAR13 // Conteudo Ini Segmen ?
    cConteudFim  := MV_PAR14 // Conteudo Fim Segmen ?
    cConteudCont := MV_PAR15 // Conteudo Contido em ?    
    lPrintZero   := IIf(MV_PAR17 == 1, .T., .F.) // Imprime valor 0.00 ?
    nDivide      := IIf(!Empty(MV_PAR19), MV_PAR19, nDivide) // Divide por ?    
    lImpAntLP    := IIf(MV_PAR21 == 1, .T., .F.) // Posição Ant. L/P?
    dDataLP      := MV_PAR22 // Data Lucros/Perdas?
    lRecDesp0	 := IIf(MV_PAR23 == 1, .T., .F.) // Ignora Sl Ant Rec./Desp. ?
    cRecDesp     := MV_PAR24 // Grupo Receitas/Despesas ?
    dDtZeraRD	 := MV_PAR25 // Data Sl Ant. Receitas/Desp. ?
    cMoedaDsc    := IIf(!Empty(MV_PAR27), MV_PAR27, cMoeda) // Descricao na Moeda ?

    aCtbMoeda    := CtbMoeda(::cMoeda, ::nDivide)
    ::cDescMoeda := Alltrim(aCtbMoeda[2])

    //Atribui variáveis nos atributos
    ::dDataIni   := dDataIni
    ::dDataFim   := dDataFim
    ::cAlias     := "CT7" // Alias do Arquivo
    ::cIdent     := ""    // Identificador do arquivo a ser processado
    ::cContaIni  := cContaIni
    ::cContaFim  := cContaFim
    ::cMoeda     := cMoeda
    ::cSaldos    := cTpsald
    ::nGrupo     := nGrupo
    ::aSetOfBook := aSetOfBook
    ::cSegmento  := cFilSeg
    ::cSegIni    := cConteudIni
    ::cSegFim    := cConteudFim
    ::cFiltSegm  := cConteudCont
    ::lNImpMov   := .F.
    ::lImpConta  := .F.
    ::lImpAntLP  := lImpAntLP
    ::dDataLP    := dDataLP
    ::nDivide    := nDivide
    ::lVlrZerado := lVlrZerado
    ::lImpSint   := lImpSint
    ::lRecDesp0  := lRecDesp0
    ::cRecDesp   := cRecDesp
    ::dDtZeraRD  := dDtZeraRD
    ::lPrintZero := lPrintZero
    ::cMoedaDsc  := cMoedaDsc
    ::aSelFil    := aSelFil
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Usado para appendar dados ao schema. Chamado no GetData da classe pai

@return object: self:oData

@author Renan Gremes
@since 04/12/2024
@version 1.0*/
//-------------------------------------------------------------------

method processCustomData() as json class InventoryBalanceSheetSmartViewBusinessObject
    ::jDados["DESCMOEDA"  ] := ::cDescMoeda
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar o append
 
@return logico: lRet
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp() as logical class InventoryBalanceSheetSmartViewBusinessObject
    
    local lRet       := .T. as logical
    local cSepara    := ""  as character
    local cMascara   := ""  as character
    local nDigitAte  := 0   as numeric

    cSegAte := MV_PAR20 // Imprimir Ate o segmento?

    //Tratativa mascara
    cMascara := Iif(Empty(::aSetOfBook[2]), SuperGetMv("MV_MASCARA"), RetMasCtb(::aSetOfBook[2],@cSepara))  

    if !Empty(::aSetOfBook[5])
        //Verifica Se existe filtragem Ate o Segmento
        if !Empty(cSegAte)
            nDigitAte := CtbRelDig(cSegAte, cMascara)
            if Len(Alltrim(cArqTmp->CONTA)) > nDigitAte
                lRet := .F.
            endif
        endif
    endif    

    if MV_PAR05 == 1		// Apenas sinteticas
        lRet := (cArqTmp->TIPOCONTA == "1")
    elseif mv_par05 == 2	// Apenas analiticas
        lRet := (cArqTmp->TIPOCONTA == "2")
    endif

return lRet
