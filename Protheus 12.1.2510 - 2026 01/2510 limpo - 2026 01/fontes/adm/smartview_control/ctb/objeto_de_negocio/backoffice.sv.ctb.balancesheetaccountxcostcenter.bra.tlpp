#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetaccountxcostcenter.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CT3, CTT, CTD, CTH, CTZ, CTO", name="Balancete Conta x Centro de Custo", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetAccountxCostCenterSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Conta x Centro de Custo
para o TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceSheetAccountxCostCenterSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object   

    public method preschema()               as object
    public method preData()                 as object
    public method processData()             as json  

    public method processCustomData()       as json    
    public method ExecDBSkipTemp()          as logical    

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    private method f145Soma() as numeric

    protected data nDecimais as numeric
    protected data cPicture  as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Balancete Contas x Centro de Custos"
    
    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("CTR145") 

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceSheetAccountxCostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete Analitico Sintetico Modelo 1"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject

    self:oSchema:addProperty("SOMADEB"	  , STR0007, "number", STR0007, "SOMADEB"	 ,,,,.F.) //"Soma Debito"
    self:oSchema:addProperty("SOMADEB_M"  , STR0008, "string", STR0008, "SOMADEB_M"  ,,,,.F.) //"Soma Debito Mascara"
    self:oSchema:addProperty("SOMACRD"	  , STR0009, "number", STR0009, "SOMACRD"	 ,,,,.F.) //"Soma Credito"
    self:oSchema:addProperty("SOMACRD_M"  , STR0010, "string", STR0010, "SOMACRD_M"  ,,,,.F.) //"Soma Credito Mascara"
    self:oSchema:addProperty("SOMAMOV"    , STR0011, "number", STR0011, "SOMAMOV"    ,,,,.F.) //"Soma Movimento"
    self:oSchema:addProperty("SOMAMOV_M"  , STR0012, "string", STR0012, "SOMAMOV_M"  ,,,,.F.) //"Soma Movimento Mascara"
    self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial" ,,,,.F.) //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0006 , "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR145")
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    //Se imprime sinteticas, appenda as contas sintéticas na tabela temporária
    if ::lImpSint
        ::CtContaSup()
    endif
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceSheetAccountxCostCenterSmartViewBusinessObject

    local cContaIni     as character
    local cContaFim     as character
    local cCCIni        as character
    local cCCFim        as character
    local cMoeda        as character
    local cTpsald       as character      
    local nDivide  := 1 as numeric    
    local lImpAntLP     as logical
    local lImpSint      as logical
    local lImpMov       as logical
    local lCompEnt      as logical
    local lVlrZerado    as logical
    local lPrintZero    as logical  
    local aSetOfBook    as array
    local aSelFil       as array
    local dDataIni      as date
    local dDataFim      as date
    local dDataLP       as date
    
    //Carrega os valores default
    cMoeda      := "01"    
    cTpsald     := "1"
    aSelFil     := {}
    lImpMov     := .T.

    //Atribui parâmetros nas variáveis
    dDataIni     := MV_PAR01 // Data Inicial
    dDataFim     := MV_PAR02 // Data Final
    cContaIni    := MV_PAR03 // Conta Inicial
    cContaFim    := MV_PAR04 // Conta Final
    cCCIni       := MV_PAR05 // C. Custo Inicial
    cCCFim       := MV_PAR06 // C. Custo Final
    lImpSint     := IIf(MV_PAR07 == 1 .Or. MV_PAR07 == 3, .T., .F.) // Imprime sinteticas    
    aSetOfBook   := IIf(!Empty(MV_PAR08), CTBSetOf(MV_PAR08),   CTBSetOf("")) // Codigo de Configuração dos Livros
    lVlrZerado	 := IIf(MV_PAR09 == 1, .T., .F.)
    cMoeda       := IIf(!Empty(MV_PAR10), MV_PAR10, cMoeda)  // Moeda ?
    cTpsald      := IIf(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), cTpsald) // Imprime Saldos ?    
    cFilSeg      := MV_PAR14 // Filtra Segmento No. ?
    cConteudIni  := MV_PAR15 // Conteudo Ini Segmen ?
    cConteudFim  := MV_PAR16 // Conteudo Fim Segmen ?
    cConteudCont := MV_PAR17 // Conteudo Contido em ?
    lPrintZero   := IIf(MV_PAR22 == 1, .T., .F.) // Imprime valor zerado?
    nDivide      := IIf(Empty(MV_PAR24), nDivide, MV_PAR24)    
    lImpAntLP    := IIf(MV_PAR26 == 1, .T., .F.) // Posição Ant. L/P?
    dDataLP      := MV_PAR27 // Data Lucros/Perdas?
    lCompEnt     := IIf(MV_PAR28 == 1, .T., .F.) // Imp. Conta S/ Custo ?    

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT3"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT3"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT3"]
    EndIf

    //Atribui variáveis nos atributos
    ::dDataIni   := dDataIni
    ::dDataFim   := dDataFim
    ::cAlias     := "CT3" // Alias do Arquivo
    ::cIdent     := ""    // Identificador do arquivo a ser processado
    ::cContaIni  := cContaIni
    ::cContaFim  := cContaFim
    ::cCCIni     := cCCIni
    ::cCCFim     := cCCFim    
    ::lImpSint   := lImpSint
    ::aSetOfBook := aSetOfBook
    ::cHeader    := "CT1" // Header do arquivo
    ::lVlrZerado := lVlrZerado
    ::lPrintZero := lPrintZero
    ::cMoeda     := cMoeda
    ::cSaldos    := cTpsald
    ::cSegmento  := cFilSeg
    ::cSegIni    := cConteudIni
    ::cSegFim    := cConteudFim
    ::cFiltSegm  := cConteudCont
    ::nDivide    := nDivide    
    ::lImpAntLP  := lImpAntLP
    ::dDataLP    := dDataLP
    ::aSelFil    := aSelFil
    ::lCompEnt   := lCompEnt
    ::lImpConta  := .T.    
    ::lNImpMov   := !::lImpMov

    ::nDecimais := DecimalCTB(::aSetOfBook, MV_PAR10)
    ::cPicture  := ::aSetOfBook[4]

return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que ser? consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que ser? consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceSheetAccountxCostCenterSmartViewBusinessObject 
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que ser? consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method processData() as json class BalanceSheetAccountxCostCenterSmartViewBusinessObject
return ::jDados


//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Usado para appendar dados ao schema. Chamado no GetData da classe pai

@return object: self:oData

@author gustavo.campos
@since 12/12/2024
@version 1.0*/
//-------------------------------------------------------------------

method processCustomData() as json class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    local TAM_VALOR := 18 as numeric
    local nTotDeb   as numeric
    local nTotCrd   as numeric
    local nTotMov   as numeric  

    nTotDeb := self:f145Soma("D")
    nTotCrd := self:f145Soma("C")

    nTotMov := (nTotCrd - nTotDeb)

    ::jDados["SOMADEB"	  ] := nTotDeb
    ::jDados["SOMADEB_M"  ] := RZValorCtb(nTotDeb,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SOMACRD"	  ] := nTotCrd
    ::jDados["SOMACRD_M"  ] := RZValorCtb(nTotCrd,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SOMAMOV"    ] := nTotMov
    ::jDados["SOMAMOV_M"  ] := RZValorCtb(nTotMov,,,TAM_VALOR-2,::nDecimais,.T.,::cPicture,If(CTT->CTT_NORMAL == "0" .or. Empty(CTT->CTT_NORMAL),CT1->CT1_NORMAL,CTT->CTT_NORMAL),,,,,,::lPrintZero,.F.)

    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName
return ::jDados


//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar o append
 
@return logico: lRet
 
@author Renan Gremes
@since 01/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp() as logical class BalanceSheetAccountxCostCenterSmartViewBusinessObject
    
    local lRet       := .T. as logical

    if MV_PAR07 == 1		// Apenas sinteticas
        lRet := (cArqTmp->TIPOCONTA == "1")
    elseif mv_par07 == 2	// Apenas analiticas
        lRet := (cArqTmp->TIPOCONTA == "2")
    endif

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} f145Soma
Soma o valor do lançamento
 
@return numérico
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method f145Soma(cTipo as character) as numeric class BalanceSheetAccountxCostCenterSmartViewBusinessObject

    local nRetValor	:= 0  as numeric
    local tpCCusto  := "" as character
                     
    if Empty(cArqTmp->TIPOCC)
    	tpCCusto := cArqTmp->TIPOCONTA
    else
    	tpCCusto := cArqTmp->TIPOCC
    endif

	if MV_PAR07 != 1					// Imprime Analiticas ou Ambas
	 	if tpCCusto == "2"		
			if cArqTmp->TIPOCONTA== "2" 
				if cTipo == "D"
					nRetValor := cArqTmp->SALDODEB
				elseif cTipo == "C"
					nRetValor := cArqTmp->SALDOCRD
				endif
			endif	
			if cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			elseif cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			endif
		endif
	else
		if tpCCusto == "1" .And. cArqTmp->NIVEL1
			if cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			elseif cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			endif
		endif
		if tpCCusto == "1" .And. Empty(cArqTmp->CCSUP)
			if cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			elseif cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			endif
		endif
	endif	
	
return nRetValor
