#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.smartviewctbr420raz.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACTB", tables="CT1, CT2", name="Classe Pai - SmartViewCtbr420Raz", country="ALL")

class SmartViewCtbr420Raz from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getData()        as object
    public method getSchema()      as object
    public method getDescription() as character
    public method getAreas()       as array

    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

    public method processCustomData()       as json

    public method getCTBR420RazSchema()  as object
    public method getCTBR420RazData()    as object
    public method getCTBR420RazAppend()  as object
    public method handleExeCTBR420Raz()

    protected method exeCTBR420Raz()     as object
    protected method setCTBR420RazParams()

    //parametros que serão alimentados por MV_PAR
    private   data oQry             as object
    protected data aStruct          as array
    protected data jItens           as json    
    protected data cAlias           as character

    protected data oMeter           as object
    protected data oText            as object
    protected data oDlg             as object
    protected data lEnd             as logical
    protected data cArqTmp          as character
    protected data cContaIni        as character
    protected data cContaFim        as character
    protected data cMoeda           as character
    protected data dDataIni         as date
    protected data dDataFim         as date
    protected data aSetOfBook       as array
    protected data lNoMov           as logical
    protected data cSaldo           as character
    protected data cTpRel           as character
    protected data lAnalitico       as logical
    protected data cLoteIni         as character
    protected data cLoteFim         as character
    protected data cSbLoteIni       as character
    protected data cSbLoteFim       as character
    protected data cDocIni          as character
    protected data cDocFim          as character
    protected data cFiltCT1         as character
    protected data aSelFil          as array
    protected data lImpDoc0         as logical
    protected data jParams          as json

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character

endclass

method new() as object class SmartViewCtbr420Raz
    _Super:new()
    
    //seta atributos com valores default
    ::cAlias        :=  ""
    ::aStruct       :=  {}
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
    Define um descrição padrão
    @author gustavo.campos
    @since 15/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getDescription() as character class SmartViewCtbr420Raz
return STR0002 + STR0003 //"Objeto contendo informações sobre o Conta por Documento Fiscal"

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
    Define o nome da Area, sem a necessidade de declarar no fonte que estiver herdando
    @author gustavo.campos
    @since 15/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
Method getAreas() as array class SmartViewCtbr420Raz
Return {STR0001} //"Contabilidade" 

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Atualiza o objeto oData ao ser executado
    @author gustavo.campos
    @since 15/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SmartViewCtbr420Raz

    //Verifica se o oFilter nao esta vindo de outro lugar e pega os parametros preenchidos
    if oFilter <> NIL
        //Carrega lista de parametros para a memória
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte )
        self:jParams := oFilter:getParameters()
    EndIf

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()

    self:setCTBR420RazParams()        //Carrega propriedades e valores default

    self:getCTBR420RazData( nPage, oFilter )
    self:exeCTBR420Raz( oFilter )     //Executa ctgercomp com as propriedades passadas
    self:getCTBR420RazAppend(oFilter) //Adiciona ao objeto do smartview

    //Elimina da memória a instância do objeto
    FreeObj(oFilter:getParameters())
    FreeObj(self:jParams)
    FreeObj(self:oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Executa a query conforme parametrização
    @author gustavo.campos
    @since 13/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class SmartViewCtbr420Raz

    self:oSchema:addProperty("CONTA"        ,STR0004,'string',STR0004,"CONTA")      //Codigo da Conta
    self:oSchema:addProperty("CONTA_M"      ,STR0005,'string',STR0005,"CONTA_M")    //Codigo da Conta Mascara
    self:oSchema:addProperty("CONTAD"       ,STR0006,'string',STR0006,"CONTAD")     //Codigo Conta Debito
    self:oSchema:addProperty("CONTAC"       ,STR0007,'string',STR0007,"CONTAC")     //Codigo Conta Credito
    self:oSchema:addProperty("TIPO"       	,STR0008,'string',STR0008,"TIPO")       //"Tipo do lançamento"
    self:oSchema:addProperty("LANCDEB"		,STR0009,'number',STR0009,"LANCDEB")    //Lançamento Debito
    self:oSchema:addProperty("LANCCRD"		,STR0010,'number',STR0010,"LANCCRD")    //Lançamento Crédito
    self:oSchema:addProperty("SALDOSCR"	    ,STR0011,'number',STR0011,"SALDOSCR")   //Saldo
    self:oSchema:addProperty("TPSLDANT"	    ,STR0012,'string',STR0012,"TPSLDANT")   //Sinal do Saldo Anterior
    self:oSchema:addProperty("TPSLDATU"	    ,STR0013,'string',STR0013,"TPSLDATU")   //Sinal do Saldo Atual 
    self:oSchema:addProperty("DATAL"		,STR0014,'date'  ,STR0014,"DATAL")      //data do Lançamento
    self:oSchema:addProperty("LOTE" 		,STR0015,'string',STR0015,"LOTE")       //LOTE
    self:oSchema:addProperty("SUBLOTE" 	    ,STR0016,'string',STR0016,"SUBLOTE")    //SUBLOTE
    self:oSchema:addProperty("DOC" 		    ,STR0017,'string',STR0017,"DOC")        //DOcumento
    self:oSchema:addProperty("LINHA"	    ,STR0018,'string',STR0018,"LINHA")      //Linha 
    self:oSchema:addProperty("SEQLAN"		,STR0019,'string',STR0019,"SEQLAN")     //Sequencia do Lançamento
    self:oSchema:addProperty("SEQHIST"		,STR0020,'string',STR0020,"SEQHIST")    //Sequencia do Historico
    self:oSchema:addProperty("EMPORI"		,STR0021,'string',STR0021,"EMPORI")     //Empresa Original
    self:oSchema:addProperty("FILORI"		,STR0022,'string',STR0022,"FILORI")     //Filial Original
    self:oSchema:addProperty("CT2KEY"   	,STR0023,'string',STR0023,"CT2KEY")     //CHAVE DE AGLUTINACAO DOC.FISCAL
    self:oSchema:addProperty("HP"        	,STR0024,'string',STR0024,"HP")         //HISTORICO PADRÃO
    self:oSchema:addProperty("HISTORICO"	,STR0025,'string',STR0025,"HISTORICO")  //HISTORICO
    self:oSchema:addProperty("NOMOV"		,STR0026,'boolean',STR0026,"NOMOV")      //Conta Sem Movimento
    self:oSchema:addProperty("GRUPO"		,STR0027,'string',STR0027,"GRUPO")      //GRUPO
    self:oSchema:addProperty("ALIAS"		,STR0028,'string',STR0028,"ALIAS")      //ALIAS
    self:oSchema:addProperty("CCUSTO"		,STR0029,'string',STR0029,"CCUSTO")     //Codigo do Centro de Custo
    self:oSchema:addProperty("CCUSTO_M"		,STR0030,'string',STR0030,"CCUSTO_M")   //Codigo Centro de Custo Mascara
    self:oSchema:addProperty("ITEM"		    ,STR0031,'string',STR0031,"ITEM")       //Codigo do Item Contábil
    self:oSchema:addProperty("ITEM_M"		,STR0032,'string',STR0032,"ITEM_M")     //Codigo Item Contábil Mascara
    self:oSchema:addProperty("CLVL"		    ,STR0033,'string',STR0033,"CLVL")       //Código da Classe de Valor
    self:oSchema:addProperty("CLVL_M"		,STR0034,'string',STR0034,"CLVL_M")     //Código Classe de Valor Mascara
    self:oSchema:addProperty("XPARTIDA"   	,STR0035,'string',STR0035,"XPARTIDA")   //Contra Partida
    self:oSchema:addProperty("OPERACAO"	    ,STR0036,'string',STR0036,"OPERACAO")   //Descricao da operacao
    self:oSchema:addProperty("FILIAL"		,STR0037,'string',STR0037,"FILIAL")     //Filial do Sistema

    self:getCTBR420RazSchema()

return self:oSchema


//-------------------------------------------------------------------
/*/{Protheus.doc} exeCTBR420Raz
    Executa a query conforme parametrização
    @author gustavo.campos
    @since 13/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method exeCTBR420Raz(oFilter as object) as object class SmartViewCtbr420Raz

Ctbr420Raz( ::oMeter,;
            ::oText,;
            ::oDlg,;
            ::lEnd,;
            ::cArqTmp,;
            ::cContaIni,;
            ::cContaFim,;
            ::cMoeda,;
            ::dDataIni,;
            ::dDataFim,;
            ::aSetOfBook,;
            ::lNoMov,;
            ::cSaldo,;
            ::cTpRel,;
            ::lAnalitico,;
            ::cLoteIni,;
            ::cLoteFim,;
            ::cSbLoteIni,;
            ::cSbLoteFim,;
            ::cDocIni,;
            ::cDocFim,;
            ::cFiltCT1,;
            ::aSelFil)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getCTBR420RazAppend
    Define o dados que serão appendados na temporaria gerada na CtGerComp,
    podendo serem sobrepostos no handleCTBR420RazAppend 
    @author gustavo.campos
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getCTBR420RazAppend(oFilter as object) as object class SmartViewCtbr420Raz
   
    Local nDecimais     := 0    as Numeric
    Local cPicture      := ""   as Character
    Local cSepara       := ""   as Character

    Local cMascCta      := ""   as character
    Local cMascCus      := ""   as character
    Local cMascIte      := ""   as character
    Local cMascClv      := ""   as character
    Local cConta        := ""   as character
    Local cCusto        := ""   as character
    Local cItem         := ""   as character
    Local cClvl         := ""   as character
    Local aArea         := {}   as array

    aArea := getArea()

    //coletando cada mascara
    cMascCta   := IIF(Empty(::aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(::aSetOfBook[2],@cSepara)) //Mascara da Conta
    cMascCus   := IIF(Empty(::aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(::aSetOfBook[6],@cSepara)) //Mascara do Centro de Custo
    cMascIte   := IIF(Empty(::aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(::aSetOfBook[7],@cSepara)) //Mascara do Item Contabil    
    cMascClv   := IIF(Empty(::aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(::aSetOfBook[8],@cSepara)) //Mascara da Classe Valor

    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) 
    cPicture  := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

    ::cAlias:= "cArqTmp"

    dbSelectArea(::cAlias)
    (::cAlias)->(DBGoTop())

    While !(::cAlias)->(Eof())

        cConta := EntidadeCTB(cArqTMP->CONTA ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
        cCusto := EntidadeCTB(cArqTMP->CCUSTO ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
        cItem  := EntidadeCTB(cArqTMP->ITEM  ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
        cClvl  := EntidadeCTB(cArqTMP->CLVL  ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)

        If !::lImpDoc0 .And. Empty((::cAlias)->CT2KEY) 
            (::cAlias)->(dbSkip())
            Loop
        Endif

        ::jItens:= {;
                "CONTA"     : (::cAlias)->CONTA,;
                "CONTA_M"   : cConta,;
                "CONTAD"    : (::cAlias)->CONTAD,;
                "CONTAC"    : (::cAlias)->CONTAC,;  
                "TIPO"      : (::cAlias)->TIPO,;
                "LANCDEB"   : (::cAlias)->LANCDEB,;   
                "LANCCRD"   : (::cAlias)->LANCCRD,; 
                "SALDOSCR"  : (::cAlias)->SALDOSCR,;     
                "TPSLDANT"  : (::cAlias)->TPSLDANT,;   
                "TPSLDATU"  : (::cAlias)->TPSLDATU,;     
                "DATAL"     : totvs.framework.treports.date.dateToTimeStamp((::cAlias)->DATAL),;   
                "LOTE"      : (::cAlias)->LOTE,;     
                "SUBLOTE"   : (::cAlias)->SUBLOTE,;   
                "DOC"       : (::cAlias)->DOC,;     
                "LINHA"     : (::cAlias)->LINHA,;   
                "SEQLAN"    : (::cAlias)->SEQLAN,;    
                "SEQHIST"   : (::cAlias)->SEQHIST,;  
                "EMPORI"    : (::cAlias)->EMPORI,;   
                "FILORI"    : (::cAlias)->FILORI,;   
                "CT2KEY"    : (::cAlias)->CT2KEY,;   
                "HISTORICO" : (::cAlias)->HISTORICO,;   
                "NOMOV"     : (::cAlias)->NOMOV,;   
                "GRUPO"     : (::cAlias)->GRUPO,;   
                "ALIAS"     : (::cAlias)->ALIAS,;   
                "CCUSTO"    : (::cAlias)->CCUSTO,;   
                "CCUSTO_M"  : cCusto,;   
                "ITEM"      : (::cAlias)->ITEM,;   
                "ITEM_M"    : cItem,;   
                "CLVL"      : (::cAlias)->CLVL,;   
                "CLVL_M"    : cClvl,;   
                "XPARTIDA"  : (::cAlias)->XPARTIDA,;   
                "OPERACAO"  : (::cAlias)->OPERACAO,; 
                "FILIAL"    : (::cAlias)->FILIAL}
        
            self:processData()
            self:processCustomData()
            self:oData:appendData(::jItens)

            (::cAlias)->(dbSkip())
                
    EndDo
        
    (::cAlias)->(DBCloseArea())

    restarea(aArea)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} setCTBR420RazParams
    Define um valor default para os atributos abaixo
    @author gustavo.campos
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method setCTBR420RazParams(oFilter as object) class SmartViewCtbr420Raz

    ::lAnalitico	:= .T.	
    ::lNoMov		:= .F.
    ::cTpRel        := "1"
    ::cLoteIni	    := ""
    ::cLoteFim	    := ""
    ::cSbLoteIni	:= ""
    ::cSbLoteFim	:= ""
    ::cDocIni		:= ""
    ::cDocFim		:= ""
    ::aSelFil       := {}

    self:handleCTBR420RazParams()

return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Executa a query conforme parametrização
    @author gustavo.campos
    @since 13/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class SmartViewCtbr420Raz

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author gustavo.campos
    @since 13/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class SmartViewCtbr420Raz
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author gustavo.campos
    @since 13/09/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class SmartViewCtbr420Raz

return ::JItens
