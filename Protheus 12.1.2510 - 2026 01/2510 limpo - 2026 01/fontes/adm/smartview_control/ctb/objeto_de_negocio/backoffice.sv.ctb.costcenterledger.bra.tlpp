#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.costcenterledger.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CT3, CTT, CTD, CTH", name="Razão Centro de Custos", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ReferentialChartofAccountsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Razão Classe Valor
para o TReports
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class CostCenterLedgerSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbgerraz    

    public method new()                   as object
    public method getAreas()              as array
    public method getDescription()        as character

    public method preData()               as object
    public method preSchema()             as object
    public method processData()           as json
    public method ExecDBSkipTemp()        as logical

    public method getCtgGerRazData()      as object    
    public method getCtGerRazSchema()     as object
    public method handleCtgGerRazAppend() as json
    public method handleCtgGerRazParams()

    protected data jDados as json
    protected data aSaldo as array
    protected data nTotCtaDeb  as numeric
    protected data nTotCtaCred as numeric
    protected data nTotCtaSld  as numeric 
    protected data nSaldoCCAtu as numeric
    protected data nSaldoAtu as numeric
    protected data cCustoAnt as character
    protected data cContaAnt as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class CostCenterLedgerSmartViewBusinessObject

    _Super:new()    
    self:setDisplayName(STR0001) // "Razao Centro de Custos"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("CTR440")

    ::aSaldo      := {}
    ::nTotCtaDeb  := 0
    ::nTotCtaCred := 0
    ::nTotCtaSld  := 0
    ::nSaldoAtu     := 0
    ::nSaldoCCAtu  := 0
    ::cCustoAnt     := ""
    ::cCOntaAnt     := ""

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class CostCenterLedgerSmartViewBusinessObject
return STR0002 // "Objeto contendo informações sobre os Razao Centro de Custos" 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class CostCenterLedgerSmartViewBusinessObject
return {STR0003} // "Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Renan Gremes
@since 07/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class CostCenterLedgerSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class CostCenterLedgerSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class CostCenterLedgerSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerRazSchema
Método chamada antes do getSchema do objeto principal
 
@return object: self:oSchema
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerRazSchema() as object class CostCenterLedgerSmartViewBusinessObject
    
    local lCanFilter := .F. as logical
    
    //Adição de novos campos ao schema   
    self:oSchema:addProperty("SLDANT",      STR0008, "number",  STR0008, "SLDANT",,,,lCanFilter)      // Saldo Anterior
    self:oSchema:addProperty("SLDANT_M",    STR0009, "string",  STR0009, "SLDANT_M",,,,lCanFilter)    // Saldo Anterior Mascara
    self:oSchema:addProperty("SLDATU",      STR0010, "number",  STR0010, "SLDATU",,,,lCanFilter)      // Saldo Atual
    self:oSchema:addProperty("SLDATU_M",    STR0011, "string",  STR0011, "SLDATU_M",,,,lCanFilter)    // Saldo Atual Mascara
    self:oSchema:addProperty("DESCCC",      STR0012, "string",  STR0012, "DESCCC",,,,lCanFilter)      // Descricao C. Custo
    self:oSchema:addProperty("TROCACTA",    STR0014, "boolean", STR0014, "TROCACTA",,,,lCanFilter)    // Logico para identificar se trocou a conta
    self:oSchema:addProperty("TOTCTADEB",   STR0015, "number",  STR0015, "TOTCTADEB",,,,lCanFilter)   // Total Conta Debito
    self:oSchema:addProperty("TOTCTACRED",  STR0016, "number",  STR0016, "TOTCTACRED",,,,lCanFilter)  // Total Conta Credito
    self:oSchema:addProperty("TOTCTASLD",   STR0017, "number",  STR0017, "TOTCTASLD",,,,lCanFilter)   // Total Conta Saldo
    self:oSchema:addProperty("TOTCTTSLD",   STR0021, "number",  STR0021, "TOTCTTSLD",,,,lCanFilter)   // "Total Centro de Custo"
    self:oSchema:addProperty("TOTCTTSLD_M",STR0022, "string",  STR0022, "TOTCTTSLD_M",,,,lCanFilter)   //  "Total Centro de Custo Masc"
    self:oSchema:addProperty("NomeEmpresa" ,STR0018, "string",  STR0018 , "NomeEmpresa",,,,lCanFilter  ) //  "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"  ,STR0019, "string",  STR0019 , "NomeFilial" ,,,,lCanFilter  ) //  "Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0020, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerRazData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerRazData(nPage as numeric, oFilter as object) as object class CostCenterLedgerSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR440")
    self:handleCtgGerRazParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())
        
return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazParams
Atribui valores aos atributos da classe principal

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerRazParams(oFilter as object)  class CostCenterLedgerSmartViewBusinessObject
    
    local cTipo         as character    
    local aSelFil       as array
    local lJunta        as logical

    //Carrega os valores default
    cTipo       := "2" // Razão por Classe de Valor    
    lJunta      := .T.    

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Atribui
    ::cCustoIni    := AllTrim(MV_PAR01)      // Do Centro de Custo ?
    If Empty(::cCustoIni)
		CTT->(DbSeek(xFilial("CTT")))
		::cCustoIni := CTT->CTT_CUSTO
    EndIf
    ::cCustoFim    := MV_PAR02      // Ate o Centro de Custo ?
    ::dDataIni     := MV_PAR03      // Da data ?
    ::dDataFim     := MV_PAR04      // Ate a data ?
    ::cMoeda       := iif(Empty(MV_PAR05), "01", MV_PAR05) // Moeda ?
    ::cSaldo       := Iif(Empty(MV_PAR06),"1",PadR(AllTrim(MV_PAR06), TamSX3("CT2_TPSALD")[1])) // Tipo de Saldo ?
    ::aSetOfBook   := iif(!Empty(MV_PAR07), CTBSetOf(MV_PAR07),   CTBSetOf("")) // Cod. Config. Livros ?
    ::lAnalit      := .T. // Tipo Relatorio ? - SOMENTE Analitico
    ::lNoMov       := iif(MV_PAR09 == 1, .T., .F.)  // Impr. CC  S/ Movim ?
    ::nCodConta    := MV_PAR10                      // Impr Cod Conta ?
    ::cContaIni    := MV_PAR12      // Da Conta ?
    ::cContaFim    := MV_PAR13      // Ate Conta ?
    ::cItemIni     := MV_PAR15      // Do Item Contabil ?
    ::cItemFim     := MV_PAR16      // Ate o Item Contabil ?
    ::cCLVLIni     := MV_PAR18      // Da Classe de Valor ?
    ::cCLVLFim     := MV_PAR19      // Ate a Classe de Valor ?
    ::lCCRes       := iif(MV_PAR24 == 2, .T., .F.)  // Impr Cod C.Custo ?
    ::lItemRes     := iif(MV_PAR25 == 2, .T., .F.)  // Impr Cod Item ?
    ::lClVlRes     := iif(MV_PAR26 == 2, .T., .F.)  // Impr Cod Cl.Valor ?
    ::lPrintZero   := iif(MV_PAR27 == 1, .T., .F.) // Imprime Valor 0,00 ?
    ::aSelFil      := aSelFil       // Seleciona Filiais ?
    ::lJunta       := lJunta        // Indica se junta CC ou nao    
    ::cTipo        := cTipo         // Tipo do relatório
    ::lRazCC	   := .T.
    ::lPageControl := .T.
return
 
//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazAppend
Deverá ser chamado no momento do processamento da query
 
@return object: self:oData
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerRazAppend() as json class CostCenterLedgerSmartViewBusinessObject

    local cDescCC     as character
    local cPicture    as character
    local cSldAtuM    as character
    local cSldAntM    as character
    local aSaldoAnt   as array
    local nDecimais   as numeric
    local TAM_VALOR := 17 as numeric
    local lTodasFil   as logical
    Local lTrocaCt  := .F. as Logical

    //Descricao CC
    cDescCC   := CtbDescMoeda("CTT->CTT_DESC"+::cMoeda)
    ::aSaldo  := SaldTotCT3(cArqTmp->CCUSTO,cArqTmp->CCUSTO,::cContaIni,::cContaFim,cArqTmp->DATAL,::cMoeda,::cSaldo,FWxFilial("CT3",cArqTmp->FILORI))

    //Pegando os saldos
    if cArqTmp->FILIAL+cArqTmp->CONTA <> ::cContaAnt
        ::nTotCtaDeb := ::nTotCtaCred := ::nTotCtaSld := 0
        aSaldoAnt := SaldTotCT3(cArqTmp->CCUSTO,cArqTmp->CCUSTO,::cContaIni,::cContaFim,::dDataIni,::cMoeda,::cSaldo,xFilial("CT3",cArqTmp->FILORI),,,,,,,,@lTodasFil)
        ::nSaldoAtu := aSaldoAnt[6]
        lTrocaCt:= .T.
    EndIf
    if cArqTmp->FILIAL+cArqTmp->CCUSTO <> ::cCustoAnt
        aSaldoAnt := SaldTotCT3(cArqTmp->CCUSTO,cArqTmp->CCUSTO,::cContaIni,::cContaFim,::dDataIni,::cMoeda,::cSaldo,xFilial("CT3",cArqTmp->FILORI),,,,,,,,@lTodasFil)
        ::nSaldoAtu := aSaldoAnt[6]
        ::nSaldoCCAtu := aSaldoAnt[6]
        IIF( !lTrocaCt,::nSaldoAtu := aSaldoAnt[6],0)
    Endif
    ::nSaldoAtu := ::nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD
    ::nSaldoCCAtu := ::nSaldoCCAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD
    //Tratamento mascara saldos
    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda)
    cPicture  := iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")
    cSldAntM  := RZValorCTB(::aSaldo[6],,,TAM_VALOR-2,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
    cSldAtuM  := RZValorCTB(::nSaldoAtu,,,TAM_VALOR-2,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
    cSldCCAtuM  := RZValorCTB(::nSaldoCCAtu,,,TAM_VALOR-2,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)

    //Totaliza por conta
    ::nTotCtaDeb  += cArqTMP->LANCDEB
    ::nTotCtaCred += cArqTMP->LANCCRD
    ::nTotCtaSld  += ::nSaldoAtu

    //Manipulação de conteúdo do json    
    ::jDados["DESCCC"]     := cDescCC
    ::jDados["SLDANT"]     := ::aSaldo[6]
    ::jDados["SLDANT_M"]   := cSldAntM
    ::jDados["SLDATU"]     := ::nSaldoAtu
    ::jDados["SLDATU_M"]   := cSldAtuM
    ::jDados["TOTCTTSLD"]   := ::nSaldoCCAtu
    ::jDados["TOTCTTSLD_M"] := cSldCCAtuM
    ::jDados["TOTCTADEB"]  := ::nTotCtaDeb
    ::jDados["TOTCTACRED"] := ::nTotCtaCred
    ::jDados["TOTCTASLD"]  := ::nTotCtaSld

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := ::cCompanyName
    ::jDados["NomeFilial" ] := ::cBranchName

    ::cCustoAnt := FwxFilial("CT3",cArqTmp->FILORI)+cArqTmp->CCUSTO
    ::cContaAnt := FwxFilial("CT3",cArqTmp->FILORI)+cArqTmp->CONTA

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Chamado no momento do processamento do append
 
@return object: self:oData
 
@author Renan Gremes
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp(cArqTmp as character) as logical class CostCenterLedgerSmartViewBusinessObject
   
    local lRet	:= .F.

	if Self:lNoMov .and. ::aSaldo[6] == 0 .and. cArqTmp->LANCDEB == 0 .and. cArqTmp->LANCCRD == 0
		if CtbExDtFim("CTT")
			dbSelectArea("CTT")
			dbSetOrder(1)
			if MsSeek(xFilial()+cArqTmp->CCUSTO)
				if !CtbVlDtFim("CTT", ::dDataIni)
					lRet	:= .T.
	            endif
		    endif
		endif
	endif

return lRet
