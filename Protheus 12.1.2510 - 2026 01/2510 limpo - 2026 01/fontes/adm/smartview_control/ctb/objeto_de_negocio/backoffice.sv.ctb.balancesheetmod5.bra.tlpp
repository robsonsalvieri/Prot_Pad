#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetmod5.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT7", name="Balancete Modelo 5", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetMod5SmartViewBusinessObject
Classe para a criação do Objeto de Negócio do relatório Balancete Modelo 5 
para o TReports
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetMod5SmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method preData()             as object
    public method preSchema()           as object
    public method processData()         as json

    public method processCustomData()  as Json
    public method ExecDBSkipTemp()

    protected data nDecimais as numeric
    protected data cPicture  as character
    protected data aTamVal   as array
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetMod5SmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // "Balancete Modelo 5"
    self:setPergunte("CTR044") //Indica o pergunte que será utilizado no relatório

    self:aTamVal := TAMSX3("CT2_VALOR")
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetMod5SmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balancete Modelo 5"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceSheetMod5SmartViewBusinessObject
    
    self:oSchema:addProperty("SOMAANTDB"  , STR0003, "number", STR0003, "SOMAANTDB"  ,,,,.F.) //"Soma Mov. Ant. Deb"
    self:oSchema:addProperty("SOMAANTCR"  , STR0004, "number", STR0004, "SOMAANTCR"  ,,,,.F.) //"Soma Mov. Ant. Cred"
    self:oSchema:addProperty("SOMADEB"    , STR0005, "number", STR0005, "SOMADEB"    ,,,,.F.) //"Soma Mov. Deb"
    self:oSchema:addProperty("SOMACRD"    , STR0006, "number", STR0006, "SOMACRD"    ,,,,.F.) //"Soma Mov. Cred"
    self:oSchema:addProperty("SOMAMOVDB"  , STR0007, "number", STR0007, "SOMAMOVDB"  ,,,,.F.) //"Soma Mov. Atu. Deb"
    self:oSchema:addProperty("SOMAMOVCR"  , STR0008, "number", STR0008, "SOMAMOVCR"  ,,,,.F.) //"Soma Mov. Atu. Cred"
    self:oSchema:addProperty("NomeEmpresa", STR0009, "string", STR0009, "NomeEmpresa",,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0010, "string", STR0010, "NomeFilial" ,,,,.F.) //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0011, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetMod5SmartViewBusinessObject
    
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR044")//Carrega lista de parametros para a memória
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERCOMP

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetMod5SmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetMod5SmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetMod5SmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetMod5SmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetMod5SmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Deverá ser chamado no momento do processamento da query
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetMod5SmartViewBusinessObject
    local nSaldoAtualDeb  := 0 as numeric
    local nSaldoAtualCred := 0 as numeric
    local nSomaAntDB      := 0 as numeric
    local nSomaAntCR      := 0 as numeric
    local nSomaDB         := 0 as numeric
    local nSomaCR         := 0 as numeric
    local nSomaMovDB      := 0 as numeric
    local nSomaMovCR      := 0 as numeric

    nSaldoAtualDeb  := Iif((cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR) > 0, cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR, 0)
    nSaldoAtualCred := Iif((cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR) < 0, cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR, 0)

    //Cálculo dos totais
    If (::lImpSint .AND. cArqTmp->NIVEL1) .OR. (!::lImpSint .AND. cArqTmp->TIPOCONTA == "2")
        nSomaAntDB	:= cArqTmp->SALDOANTDB
		nSomaAntCR	:= cArqTmp->SALDOANTCR
		nSomaDB 	:= cArqTmp->SALDODEB
		nSomaCR		:= cArqTmp->SALDOCRD
        
        If (cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR) > 0 
            nSomaMovDB := (cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR)
        Endif
    
        If (cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR) < 0 
            nSomaMovCR := (cArqTmp->SALDOATUDB - cArqTmp->SALDOATUCR)
        Endif
	Endif
    
    //Reatribui valores especificos deste relatorio
    ::jDados["SALDOANTDB_M"] := ValorCTB(cArqTmp->SALDOANTDB,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDOANTCR_M"] := ValorCTB(cArqTmp->SALDOANTCR,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDODEB_M"  ] := ValorCTB(cArqTmp->SALDODEB  ,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDOCRD_M"  ] := ValorCTB(cArqTmp->SALDOCRD  ,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)    
    ::jDados["SALDOADB"    ] := nSaldoAtualDeb
    ::jDados["SALDOADB_M"  ] := ValorCTB(nSaldoAtualDeb,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDOACR"    ] := nSaldoAtualCred
    ::jDados["SALDOACR_M"  ] := ValorCTB(nSaldoAtualCred,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.) 

    //Atribui valores novos especificos deste relatorio
    ::jDados["SOMAANTDB"  ] := nSomaAntDB
	::jDados["SOMAANTCR"  ] := nSomaAntCR
    ::jDados["SOMADEB"    ] := nSomaDB
	::jDados["SOMACRD"    ] := nSomaCR
    ::jDados["SOMAMOVDB"  ] := nSomaMovDB
	::jDados["SOMAMOVCR"  ] := nSomaMovCR

    //Atribui a empres/filial logada
    ::jDados["NomeEmpresa" ] := self:cCompanyName
    ::jDados["NomeFilial"  ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar se pula linha no append
 
@return logico: lRet
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) class BalanceSheetMod5SmartViewBusinessObject
   
    Local lRet := .T.   
    
    If mv_par05 == 1		// Apenas sinteticas
	lRet := (cArqTmp->TIPOCONTA == "1")
    ElseIf mv_par05 == 2	// Apenas analiticas
        lRet := (cArqTmp->TIPOCONTA == "2")
    EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Bruno Oliveira (BO)
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetMod5SmartViewBusinessObject
   
    local cContaIni         as character
    local cContaFim         as character
    local cMoeda  :=  "01"  as character
    local cTpsald :=  "1"   as character
    Local nDivide := 1      as numeric
    local lImpAntLP         as logical
    Local lImpSint          as logical 
    Local lPrintZero        as logical
    Local lSldZerado        as logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as date
    local dDataFim          as date
    local dDataLP           as date
    local cFilSeg           as character
    local cConteudIni       as character
    local cConteudFim       as character
    local cConteudCont      as character
    local lRecDesp0         as logical
    local cRecDesp          as character
    local dDtZeraRD         as date
    local cDescMoeda :="01" as character

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    dDataIni    := MV_PAR01                                                 // Data Inicial ?                
    dDataFim    := MV_PAR02                                                 // Data Final ?                  
    cContaIni	:= MV_PAR03                                                 // Conta Inicial ?               
    cContaFim	:= MV_PAR04                                                 // Conta Final ?                 
    lImpSint    := Iif(MV_PAR05==1 .Or. MV_PAR05 ==3,.T.,.F.)               // Imprime Contas ?              
    aSetOfBook  := iIf(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf(""))    // Cod. Config. Livros ?         
    lSldZerado  := Iif(MV_PAR07==1,.T.,.F.)                                 // Saldos Zerados ?              
    cMoeda      := iIf(!empty(MV_PAR08),MV_PAR08,cMoeda)                    // Moeda ?          
    cTpsald     := iif(!Empty(MV_PAR09), PadR(AllTrim(MV_PAR09), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo ?    
    lPrintZero  := Iif(MV_PAR13==1,.T.,.F.)                                 // Imprime Valor 0.00 ?              
    lImpAntLP   := Iif(MV_PAR15 == 1,.T.,.F.)                               // Posicao Ant. L/P ?            
    dDataLP     := MV_PAR16                                                 // Data Lucros/Perdas ?      
    lRecDesp0   := Iif(MV_PAR17==1,.T.,.F.)                                 // Ignora Sl Ant.Rec/Des ?       
    cRecDesp    := MV_PAR18                                                 // Grupos Receitas/Despesas ?    
    dDtZeraRD   := MV_PAR19                                                 // Data Sld Ant. Receitas/Desp. ?              
    
    //Setando os atributos
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT7"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := .F.
    self:lImpConta      := .F.
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:lRecDesp0      := lRecDesp0
    self:cRecDesp       := cRecDesp 
    self:dDtZeraRD      := dDtZeraRD
    self:cMoedaDsc      := cDescMoeda
    self:aSelFil        := aSelFil
    self:lImpSint       := lImpSint

    self:nDecimais := DecimalCTB(::aSetOfBook, cMoeda)
    self:cPicture  := ::aSetOfBook[4]
 
return
