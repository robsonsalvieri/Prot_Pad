#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetitemxcostcenter.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTV, CTD", name="Balancete de Itens x Centro de Custo", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetItemxCostCenterSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete de Itens x Centro de Custo
para o TReports
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetItemxCostCenterSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object 

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp()
    public method vldExeCtgGerPlan()    

    protected data nTotal1 as numeric
    protected data nTotal2 as numeric
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete de Itens x Centro de Custo"
    self:setPergunte("CTR330") //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetItemxCostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Item x Centro Custo"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetItemxCostCenterSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
    self:oSchema:addProperty("NomeEmpresa"  , STR0005, "string", STR0005, "NomeEmpresa" ,,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"   , STR0006, "string", STR0006, "NomeFilial"  ,,,,.F.) //"Nome Filial"
    self:oSchema:addProperty("SALDODEBACUM" , STR0003, "number", STR0003, "SALDODEBACUM",,,,.F.) //"Debito Acumulado"
    self:oSchema:addProperty("SALDOCRDACUM" , STR0004, "number", STR0004, "SALDOCRDACUM",,,,.F.) //"Credito Acumulado"
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method processCustomData() as json class BalanceSheetItemxCostCenterSmartViewBusinessObject

    ::jDados["NomeEmpresa"]  := self:cCompanyName
    ::jDados["NomeFilial" ]  := self:cBranchName
    ::jDados["SALDODEBACUM"] := self:nTotal1
    ::jDados["SALDOCRDACUM"] := self:nTotal2

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetItemxCostCenterSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR330")//Carrega lista de parametros para a memória   
    self:handleCtgGerPlanParams( oFilter )    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Metodo que valida se vai imprimir o relatório
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method vldExeCtgGerPlan() class BalanceSheetItemxCostCenterSmartViewBusinessObject
    Local lRet := .T. as Logical
    Local aCtbMoeda := {} as Array

    If !ct040Valid(mv_par08)
        lRet := .F.    
	EndIf
    //Valida se a moeda é valida!
    aCtbMoeda  	:= CtbMoeda(mv_par10,self:nDivide)
    If Empty(aCtbMoeda[1])
        lRet := .F.
    Endif
Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) class BalanceSheetItemxCostCenterSmartViewBusinessObject

    Local lRet := .T. as Logical

    If mv_par07 == 1					// So imprime Sinteticos
        lRet := cArqTmp->TIPOCC <>  '2'  
    ElseIf mv_par07 == 2				// So imprime Analiticos
        lRet := cArqTmp->TIPOCC <>  '1'  
    EndIf

    //Soma campos totais gerais
    If mv_par07 == 1					// So imprime Sinteticas - Soma Sinteticas
        If cArqTmp->TIPOCC == "1"
            If cArqTmp->NIVEL1 
                self:nTotal1	:= cArqTmp->SALDODEB
                self:nTotal2	:= cArqTmp->SALDOCRD
            EndIf
        EndIf
    Else // Soma Analiticas
        If cArqTmp->TIPOCC == "2"
            self:nTotal1	:= cArqTmp->SALDODEB
            self:nTotal2	:= cArqTmp->SALDOCRD
        EndIf
    EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Bruno Oliveira (BO)
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetItemxCostCenterSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    Local cItemIni          as Character
    Local cItemFim          as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    Local nDivide := 1      as numeric
    local lMov              as logical
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date

    dDataIni    :=  MV_PAR01 //      	Data Inicial ?                
    dDataFim    :=  MV_PAR02 //      	Data Final ?  
    cContaIni   :=  Space(Len(CriaVar("CT1_CONTA")))//          Conta Inicial ?               
    cContaFim   :=  Repl('Z',Len(CriaVar("CT1_CONTA"))) //        Conta Final ?  
    cItemIni    :=  MV_PAR03 //         Item Contabil Inicial ?
    cItemFim    :=  MV_PAR04 //         Item Contabil Final?   
    cCCIni      :=  MV_PAR05 //         CC Inicial ?
    cCCFim      :=  MV_PAR06 //         CC Final? 
                
    lImpSint    := Iif(MV_PAR07==1 .Or. MV_PAR07==3,.T.,.F.) //      	Imprime Contas ?              
    aSetOfBook  := Iif(!Empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf("")) //      	Cod. Config. Livros ?         
    lSldZerado  := Iif(MV_PAR09==1,.T.,.F.) //Saldos Zerados                            
    cMoeda      := Iif(!empty(MV_PAR10),MV_PAR10,cMoeda) //Moeda                                            
    cTpsald     := Iif(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), cTpsald) //Tipo de Saldo
    cFilSeg     := MV_PAR14 //Filtra Segmento No. ?  
    cConteudIni := MV_PAR15 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR16 //Conteudo Fim Segmen ?        
    cConteudCont:= MV_PAR17 //Conteudo Contido em ?
    lMov        := Iif(MV_PAR18 == 1,.T.,.F.) //Imprime Coluna Movimento ?
    lImpCod     := Iif(MV_PAR20==1,.T.,.F.) //Imprime codigo ?
    lPrintZero  := Iif(MV_PAR23==1,.T.,.F.) //Imprime Valor 0.00  ?
    nDivide     := Iif(empty(MV_PAR24),nDivide,MV_PAR24)
    lImpAntLP   := Iif(MV_PAR25 == 1,.T.,.F.) //Posição Ant. L/P?
    dDataLP     := MV_PAR26 //Data Lucros/Perdas?      
    aSelFil     := {} // MV_PAR24 Seleciona filiais?    

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cItemIni       := cItemIni  
    self:cItemFim       := cItemFim 

    self:cCCIni         := cCCIni  
    self:cCCFim         := cCCFim  
    self:cAlias         := "CTV"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := lMov
    self:lImpConta      := .T.
    self:cHeader        := "CTD"
    self:aGeren         := nil
    
return
