#INCLUDE "totvs.ch"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "backoffice.sv.ctb.BookDiary.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT2" , name="Livro Diario",country="ALL",,customTables="CT2")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BookDiarySmartViewBusinessObject
Classe para criação do Objeto de Negócio de contendo informações Livro Diário
 
@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class BookDiarySmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()            as object
	public method getDescription() as character
	public method getAreas()       as array
	public method getData()        as object
	public method getSchema()      as object

	//usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

	protected method ImprimeEntConta()  as character


    protected data jDados  		as json
	protected data oQuery1 		as object
	protected data oIntegratedProvider  as object
	protected data aSelFil      as array
	
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object Class BookDiarySmartViewBusinessObject
	_Super:new()
	self:setDisplayName(STR0001)
	self:setPergunte("CTR110") //Indica o pergunte que será utilizado no relatório
	self:aSelFil    := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class BookDiarySmartViewBusinessObject
return STR0002

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BookDiarySmartViewBusinessObject
return {STR0003}

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BookDiarySmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
Metodo que será consumido pelo M.I.
AJusta o Json conforme alteração no processData e preSchema

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BookDiarySmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
Metodo que será consumido pelo M.I.
Será usado para alterar a Query antes do getData.

@author Controladoria
@since 07/07/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class BookDiarySmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------

method getData(nPage as numeric, oFilter as object) as object class BookDiarySmartViewBusinessObject

	local cDelete  		:= space(1) as character
	local cMascara 		:= SuperGetMv("MV_MASCARA") as character
	local cSeparador	:= "" as character
	local cQuery   as character	
	local nVlrCred 	  := 0 	as numeric
	local nVlrDeb  	  := 0 	as numeric
	local nParamOrder := 1  as numeric
	local cCompanyName        as character
	local cBranchName         as character
	Local nCountFil   := 0    as numeric
	Local cFilName	  := ""   as character

	Private cAlias   as character

	CTBsetValueMVPAR(oFilter:getParameters(), "CTR110")

	//Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())

	::oIntegratedProvider := ControlIntegratedProvider():New()
	cCompanyName    := ::oIntegratedProvider:getCompanyName()
	cBranchName     := ::oIntegratedProvider:getBranchName()


	::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .T.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"SN3"})

	If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
		::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
	Else
		::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
	EndIf

	if empty(::aSelFil)
		aAdd(::aSelFil, FWxFilial("CT2"))
	endif

	For nCountFil := 1 To Len(::aSelFil)        
		cFilAnt 	:= ::aSelFil[nCountFil]
		nParamOrder := 1
		cFilName	:= FWFilialName()

		cQuery  := "SELECT CT2_FILIAL, CT2_DATA, CT2_CREDIT, CT2_DEBITO, CT2_HIST, CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_LINHA, CT2_VALOR, CT2_DC "
		cQuery  += "FROM "+RetSQLName("CT2")
		cQuery  += " WHERE "
		cQuery  += "CT2_FILIAL = ? "
		cQuery  += "AND CT2_DATA  >= ? "
		cQuery  += "AND CT2_DATA  <= ? "
		cQuery  += "AND CT2_MOEDLC = ? "
		cQuery  += "AND CT2_TPSALD = ? "
		If MV_PAR12 <> 2
			cQuery  += "AND CT2_VALOR <> ? "
		Endif
		cQuery  += "AND D_E_L_E_T_ = ? "

		//Os filtros serão setados na interface do novo TReports
		if(oFilter:hasFilter(),cQuery += " AND " + oFilter:getSQLExpression(),)

		self:oQuery1:= FWPreparedStatement():New(ChangeQuery(cQuery))

		self:oQuery1:SetString(nParamOrder++, FWxFilial("CT2"))
		self:oQuery1:SetDate(nParamOrder++,	  MV_PAR01)
		self:oQuery1:SetDate(nParamOrder++,	  MV_PAR02)
		self:oQuery1:SetString(nParamOrder++, MV_PAR03)
		self:oQuery1:SetString(nParamOrder++,  PadR(AllTrim(MV_PAR05), TamSX3("CT2_TPSALD")[1]))
		If MV_PAR12 <> 2
			self:oQuery1:SetNumeric(nParamOrder++, 0)	
		Endif
		self:oQuery1:SetString(nParamOrder++, cDelete)

		cQuery := self:oQuery1:GetFixQuery()

		cAlias := MPSysOpenQuery(ChangeQuery(cQuery))

		dData := (cAlias)->CT2_DATA

		While !(cAlias)->(Eof())
			nVlrDeb := 0
			nVlrCred := 0

			if (cAlias)->CT2_DC=="1" .Or. (cAlias)->CT2_DC=="3"
				nVlrDeb := (cAlias)->CT2_VALOR
			endif

			if (cAlias)->CT2_DC=="2" .Or. (cAlias)->CT2_DC=="3"
				nVlrCred := (cAlias)->CT2_VALOR
			endif

			::jDados := JsonObject():new()
			::jDados["CT2_FILIAL"]  	:= (cAlias)->CT2_FILIAL
			::jDados["FILIALDESC"]  	:= AllTrim(cFilName)
			::jDados["CT2_DATA"]  		:= totvs.framework.treports.date.stringToTimeStamp((cAlias)->CT2_DATA)	
			::jDados["CT2_DEBITO"]    	:= self:ImprimeEntConta(cMascara,cSeparador,.T.) 
			::jDados["CT2_CREDIT"]    	:= self:ImprimeEntConta(cMascara,cSeparador,.F.)
			::jDados["CT2_HIST"]  		:= (cAlias)->CT2_HIST
			::jDados["NUMLANC"]     	:= (cAlias)->CT2_LOTE + (cAlias)->CT2_SBLOTE + (cAlias)->CT2_DOC + (cAlias)->CT2_LINHA
			::jDados["VLRDEB"]  		:= nVlrDeb
			::jDados["VLRCRED"]  		:= nVlrCred
			::jDados["CT2_DC"]  		:= (cAlias)->CT2_DC
			
			::jDados["NomeEmpresa" ] 	:= cCompanyName
			::jDados["NomeFilial"  ] 	:= cBranchName

			self:processData()
			self:oData:appendData(::jDados)

			(cAlias)->( dbSkip() )

		EndDo
		(cAlias)->( DBCloseArea() )

	Next nCountFil
	

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos

@param

@return oSchema: objeto com schema dos campos

@author Ednilson Queiroz de Castro
@since 06/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class BookDiarySmartViewBusinessObject

    local aFieldsCT2 := {}  as array    
	local lCanFilter := .F. as logical	

	aFieldsCT2 := { "CT2_FILIAL","CT2_DATA", "CT2_DEBITO", "CT2_CREDIT", "CT2_HIST", "CT2_DC"}

	self:oSchema:aliasToSchema("CT2",aFieldsCT2)

	self:oSchema:addProperty("NUMLANC", 	STR0004, "string", STR0004, "NUMLANC"	  ,,,,lCanFilter)
	self:oSchema:addProperty("VLRDEB" , 	STR0005, "number", STR0005, "VLRDEB"   	  ,,,,lCanFilter)
	self:oSchema:addProperty("VLRCRED", 	STR0006, "number", STR0006, "VLRCRED"  	  ,,,,lCanFilter)
	self:oSchema:addProperty("FILIALDESC",	STR0010, "string", STR0010, "FILIALDESC"  ,,,,lCanFilter)//"Descrição Filial"
	self:oSchema:addProperty("NomeEmpresa",	STR0007, "string", STR0007, "NomeEmpresa" ,,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty("NomeFilial" ,	STR0008, "string", STR0008, "NomeFilial"  ,,,,lCanFilter) // Nome Filial

    self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} ImprimeEntConta
Verifica Entdida da Conta

@return character

@author Ewerton Franklin
@since 10/12/2024
@version 1.0*/
//-------------------------------------------------------------------

method ImprimeEntConta(cMascara as character, cSeparador as character , lDeb as logical) as character class BookDiarySmartViewBusinessObject

	Local cRet := ""
	Local nTamConta	:= 40

	DEFAULT cMascara   := " "
	DEFAULT cSeparador := " " 
	DEFAULT lDeb	   := .T.

	If lDeb 
		If !Empty((cAlias)->CT2_DEBITO)					/// Se a Conta a Debito estiver preenchida
			dbSelectArea("CT1")
			dbSetOrder(1)
			If MsSeek(xFilial("CT1")+(cAlias)->CT2_DEBITO,.F.)	/// e existir no plano de contas
				If mv_par13 == 2							/// Impressao do Codigo Reduzido
					cRet := EntidadeCTB(CT1->CT1_RES,0 ,00,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				ElseIf mv_par13 == 3 			 	/// Impressao do Codigo de Impressao (se o campo existir)
					cRet := EntidadeCTB(CT1->CT1_CODIMP,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				Else										/// Impressao do Codigo Normal
					cRet := EntidadeCTB((cAlias)->CT2_DEBITO,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				Endif
			Else
				cRet := EntidadeCTB((cAlias)->CT2_DEBITO,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
			Endif
		Endif                              
	Else                 
		If !Empty((cAlias)->CT2_CREDIT)
			dbSelectArea("CT1")
			dbSetOrder(1)
			If MsSeek(xFilial("CT1")+(cAlias)->CT2_CREDIT,.F.)
				If mv_par13 == 2							/// Impressao do Codigo Reduzido
					cRet := EntidadeCTB(CT1->CT1_RES,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				ElseIf mv_par13 == 3 						/// Impressao do Codigo de Impressao (se o campo existir)
					cRet := EntidadeCTB(CT1->CT1_CODIMP,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				Else										/// Impressao do Codigo Normal
					cRet := EntidadeCTB((cAlias)->CT2_CREDIT,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
				Endif
			Else
				cRet := EntidadeCTB((cAlias)->CT2_CREDIT,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
			Endif
		Endif
	EndIf	
			
	dbSelectArea("CT2")

Return cRet
