#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.ctb.smartviewctbfunctions.ch"

static cArqtmp := " " as character

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACTB", tables="CQ1,CT1,CT2,CT3,CT4,CT7,CTD,CTH,CTN,CTO,CTS,CTT,CTU,CTZ,SLD", name="Classe Pai - SmartviewCtbFunctions", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} SmartviewCtbFunctions
Classe genérica para a montagem do objeto de negócio com base na CTGerPlan
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class SmartviewCtbFunctions from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getData()             as object
    public method getSchema()           as object    
    public method getDescription()      as character
    public method getAreas()            as array
    
    public method getCtGerPlanSchema()  as object
    public method getCtgGerPlanData()   as object
    public method getCtgGerPlanAppend() as object

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()   //popula pra tabela temporaria conforme parametros
    public method handleAdditionalContent()  //popula conteudo adicional na tabela temporaria

    public method handleExeCtgGerPlan()   //popula pra tabela temporaria conforme parametros
    public method CompVld()
    public method ExecImpSint()
    public method ExecDBSkipTemp()
    
    public method processCustomData() as Json

    public method preData()             as object
    public method preSchema()           as object
    public method processData()         as json

    public method getNextPage()

    protected method exeCtgGerPlan()    as object
    protected method setCtgGerPlanParams()  //carrega os atributos default da ctgerplan
    protected method CtContaSup()

    public method vldExeCtgGerPlan() // Valida se executa a função ctgerplan

    static method setValueMVPAR()
    
    //atributos - na ordem que está sendo chamado na função
    protected data oMeter         as object
    protected data oText          as object
    protected data oDlg           as object
    protected data lEnd           as logical
    protected data cArqtmp        as character
    protected data dDataIni       as date
    protected data dDataFim       as date
    protected data cAlias         as character
    protected data cIdent         as character
    protected data cContaIni      as character
    protected data cContaFim      as character
    protected data cCCIni         as character
    protected data cCCFim         as character
    protected data cItemIni       as character
    protected data cItemFim       as character
    protected data cClvlIni       as character
    protected data cClVlFim       as character
    protected data cMoeda         as character
    protected data cSaldos        as character
    protected data aSetOfBook     as array
    protected data cSegmento      as character
    protected data cSegIni        as character
    protected data cSegFim        as character
    protected data cFiltSegm      as character
    protected data lNImpMov       as logical
    protected data lImpConta      as logical
    protected data nGrupo         as numeric
    protected data cHeader        as character
    protected data lImpAntLP      as logical
    protected data dDataLP        as character
    protected data nDivide        as numeric
    protected data lVlrZerado     as logical
    protected data cFiltroEnt     as character
    protected data cCodFilEnt     as character
    protected data cSegmentoG     as character
    protected data cSegIniG       as character
    protected data cSegFimG       as character
    protected data cFiltSegmG     as character
    protected data lUsGaap        as logical
    protected data cMoedConv      as character
    protected data cConsCrit      as character
    protected data dDataConv      as date
    protected data nTaxaConv      as numeric
    protected data aGeren         as array
    protected data lImpMov        as logical
    protected data lImpSint       as logical
    protected data cFilUSU        as character
    protected data lRecDesp0      as logical
    protected data cRecDesp       as character
    protected data dDtZeraRD      as date
    protected data lImp3Ent       as logical
    protected data lImp4Ent       as logical
    protected data lImpEntGer     as logical
    protected data lFiltraCC      as logical
    protected data lFiltraIt      as logical
    protected data lFiltraCV      as logical
    protected data cMoedaDsc      as character
    protected data lMovPeriodo    as logical
    protected data aSelFil        as array
    protected data dDtCorte       as date
    protected data lPlGerSint     as logical
    protected data lConsSaldo     as logical
    protected data lCompEnt       as logical
    protected data cArqAux        as character
    protected data cArqTmpAux     as character
    protected data lUsaNmVis      as logical
    protected data cNomeVis       as character
    protected data lCttSint       as logical
    protected data lTodasFil      as logical
    protected data cQuadroCTB     as character
    protected data aEntidades     as array
    protected data cCodEntidade   as character
    protected data lDemDRE        as logical
    protected data lPeriodo0      as logical
    protected data lSemestre      as logical
    protected data dFinalA        as date
    protected data aRecTemp       as array

    //atributos para tratamento de impressão  
    protected data lPrintZero     as logical //se imprime valor 0,00

    protected data jDados          as json
    protected data jParams         as json

    protected data dPeriodo0 as date
    protected data dSemestre as date

    //Atributo para controlar paginação nas queries
    protected data lPageControl as logical

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character

    protected data lCTBR185SV          as logical
    protected data lCTBR100SV          as logical
    protected data lCTBR140SV          as logical
    protected data lAdditional         as logical

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
    Método de instância da classe
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() as object class SmartviewCtbFunctions
    _Super:new()
    ::cArqTmp      := " "
    ::lPeriodo0    := .F.
    ::lSemestre    := .F.
    ::lPageControl := .F.
    ::lAdditional  := .F.
    ::aRecTemp     := {} //Salva os registros adicionais que já foram appendados na tabela temporária para não duplicar, quando paginado
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
    Define o nome da Area, sem a necessidade de declarar no fonte que estiver herdando
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getAreas() as array class SmartviewCtbFunctions
return {STR0001} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
    Define um descrição padrão
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getDescription() as character class SmartviewCtbFunctions
return STR0002+STR0003 //"Objeto contendo informações sobre" + "RELATORIO" 

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Atualiza o objeto oData ao ser executado
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SmartviewCtbFunctions

    if oFilter <> NIL
        //Carrega lista de parametros para a memória
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte )
        self:jParams := oFilter:getParameters()
    endif

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:oIntegratedProvider:setStep(10000) //Seta o step da paginação
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()

    self:setCtgGerPlanParams()
    self:CompVld(::nDivide)

    self:getCtgGerPlanData( nPage, oFilter )

    If self:vldExeCtgGerPlan()
        self:oIntegratedProvider:setPageRangeByStep(.T.)
        self:exeCtgGerPlan( oFilter )
        self:getCtgGerPlanAppend(oFilter,nPage)
    endif

    //Elimina da memória a instância do objeto
    FreeObj(oFilter:getParameters())
    FreeObj(self:jParams)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Define um schema padrão quando for chamado o metodo getCtGerPlanSchema() na classe filha
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getSchema() as object class SmartviewCtbFunctions

    self:oSchema:addProperty("CONTA"    	,STR0004  ,"string" ,STR0004  ,"CONTA")       // Codigo da Conta
    self:oSchema:addProperty("CONTA_M"    	,STR0005  ,"string" ,STR0005  ,"CONTA")       // Cod Conta Mascara
    self:oSchema:addProperty("SUPERIOR"		,STR0006  ,"string" ,STR0006  ,"SUPERIOR")    // Conta Superior    
    self:oSchema:addProperty("NORMAL"		,STR0007  ,"string" ,STR0007  ,"NORMAL")      // Situacao    
    self:oSchema:addProperty("CTARES"		,STR0008  ,"string" ,STR0008  ,"CTARES")      // Codigo Reduzido da Conta    
    self:oSchema:addProperty("DESCCTA"		,STR0009  ,"string" ,STR0009  ,"DESCCTA")     // Descricao da Conta    
    self:oSchema:addProperty("CUSTO"		,STR0010  ,"string" ,STR0010  ,"CUSTO")       // Codigo do Centro de Custo
    self:oSchema:addProperty("CUSTO_M"		,STR0011  ,"string" ,STR0011  ,"CUSTO")       // Cod. do CCusto Mascara
    self:oSchema:addProperty("CCRES"		,STR0012  ,"string" ,STR0012  ,"CCRES")       // Codigo Reduzido do Centro de Custo    
    self:oSchema:addProperty("DESCCC"		,STR0013  ,"string" ,STR0013  ,"DESCCC")      // Descricao do Centro de Custo    
    self:oSchema:addProperty("ITEM"			,STR0014  ,"string" ,STR0014  ,"ITEM")        // Codigo do Item Contabil   
    self:oSchema:addProperty("ITEM_M"		,STR0062  ,"string" ,STR0062  ,"ITEM")        // Codigo do Item Mascara   
    self:oSchema:addProperty("ITEMRES"		,STR0015  ,"string" ,STR0015  ,"ITEMRES")     // Codigo Reduzido do Item    
    self:oSchema:addProperty("DESCITEM"		,STR0016  ,"string" ,STR0016  ,"DESCITEM")    // Descricao do Item    
    self:oSchema:addProperty("CLVL"			,STR0017  ,"string" ,STR0017  ,"CLVL")        // Codigo da Classe de Valor    
    self:oSchema:addProperty("CLVL_M"		,STR0063  ,"string" ,STR0063  ,"CLVL")        // Codigo da ClVl Mascara   
    self:oSchema:addProperty("CLVLRES"		,STR0018  ,"string" ,STR0018  ,"CLVLRES")     // Cod. Red. Classe de Valor    
    self:oSchema:addProperty("DESCCLVL"		,STR0019  ,"string" ,STR0019  ,"DESCCLVL")    // Descricao da Classe de Valor    
    self:oSchema:addProperty("SALDOANT"		,STR0020  ,"number" ,STR0020  ,"SALDOANT")    // Saldo Anterior    
    self:oSchema:addProperty("SALDOANT_M"	,STR0021  ,"string" ,STR0021  ,"SALDOANT")    // Sld Anterior Mascara    
    self:oSchema:addProperty("SALDOANTDB"	,STR0022  ,"number" ,STR0022  ,"SALDOANTDB")  // Saldo Anterior Debito    
    self:oSchema:addProperty("SALDOANTDB_M"	,STR0023  ,"string" ,STR0023  ,"SALDOANTDB")  // Sld Anterior Debito Mascara    
    self:oSchema:addProperty("SALDOANTCR"	,STR0024  ,"number" ,STR0024  ,"SALDOANTCR")  // Saldo Anterior Credito    
    self:oSchema:addProperty("SALDOANTCR_M"	,STR0025  ,"string" ,STR0025  ,"SALDOANTCR")  // Sld Anterior Credito Mascara   
    self:oSchema:addProperty("SALDODEB"		,STR0026  ,"number" ,STR0026  ,"SALDODEB")    // Debito    
    self:oSchema:addProperty("SALDODEB_M"	,STR0027  ,"string" ,STR0027  ,"SALDODEB")    // Debito Mascara    
    self:oSchema:addProperty("SALDOCRD"		,STR0028  ,"number" ,STR0028  ,"SALDOCRD")    // Credito    
    self:oSchema:addProperty("SALDOCRD_M"	,STR0029  ,"string" ,STR0029  ,"SALDOCRD")    // Credito Mascara   
    self:oSchema:addProperty("SALDOATU"		,STR0030  ,"number" ,STR0030  ,"SALDOATU")    // Saldo Atual    
    self:oSchema:addProperty("SALDOATU_M"	,STR0031  ,"string" ,STR0031  ,"SALDOATU")    // Sld Atual Mascara   
    self:oSchema:addProperty("SALDOADB"		,STR0032  ,"number" ,STR0032  ,"SALDOADB")    // Saldo Atual Debito    
    self:oSchema:addProperty("SALDOADB_M"	,STR0033  ,"string" ,STR0033  ,"SALDOADB")    // Sld Atual Debito Mascara    
    self:oSchema:addProperty("SALDOACR"		,STR0034  ,"number" ,STR0034  ,"SALDOACR")    // Saldo Atual Credito    
    self:oSchema:addProperty("SALDOACR_M"	,STR0035  ,"string" ,STR0035  ,"SALDOACR")    // Sld Atual Credito Mascara   
    self:oSchema:addProperty("TIPOCONTA"	,STR0036  ,"string" ,STR0036  ,"TIPOCONTA")   // Tipo de Conta
    self:oSchema:addProperty("TIPOCC"		,STR0037  ,"string" ,STR0037  ,"TIPOCC")      // Tipo de Centro de Custo
    self:oSchema:addProperty("TIPOITEM"		,STR0038  ,"string" ,STR0038  ,"TIPOITEM")    // Tipo de Item Contabil
    self:oSchema:addProperty("TIPOCLVL"		,STR0039  ,"string" ,STR0039  ,"TIPOCLVL")    // Tipo de Classe de Valor
    self:oSchema:addProperty("CCSUP"		,STR0040  ,"string" ,STR0040  ,"CCSUP")       // Codigo do Centro de Custo Superior     
    self:oSchema:addProperty("CCNORMAL"		,STR0041  ,"string" ,STR0041  ,"CCNORMAL")    // Situacao CC   
    self:oSchema:addProperty("ITSUP"		,STR0042  ,"string" ,STR0042  ,"ITSUP")       // Codigo do Item Superior    
    self:oSchema:addProperty("ITNORMAL"		,STR0043  ,"string" ,STR0043  ,"ITNORMAL")    // Situacao Item    
    self:oSchema:addProperty("CLSUP"		,STR0044  ,"string" ,STR0044  ,"CLSUP")       // Codigo da Clvl Superior    
    self:oSchema:addProperty("CLNORMAL"		,STR0045  ,"string" ,STR0045  ,"CLNORMAL")    // Situacao CLVL   
    self:oSchema:addProperty("ORDEM"		,STR0046  ,"string" ,STR0046  ,"ORDEM")       // Ordem    
    self:oSchema:addProperty("GRUPO"		,STR0047  ,"string" ,STR0047  ,"GRUPO")       // Grupo Contabil    
    self:oSchema:addProperty("IDENTIFI"		,STR0048  ,"string" ,STR0048  ,"IDENTIFI")    // Identificador
    self:oSchema:addProperty("TOTVIS"		,STR0049  ,"string" ,STR0049  ,"TOTVIS")      // Resultado da Vis?o    
    self:oSchema:addProperty("SLDENT"		,STR0050  ,"string" ,STR0050  ,"SLDENT")      // Saldo entidade    
    self:oSchema:addProperty("FATSLD"		,STR0051  ,"string" ,STR0051  ,"FATSLD")      // Fator Entidade   
    self:oSchema:addProperty("VISENT"		,STR0052  ,"string" ,STR0052  ,"VISENT")      // Visualiza Entidade    
    self:oSchema:addProperty("ESTOUR"		,STR0053  ,"string" ,STR0053  ,"ESTOUR")      // Conta Estourada    
    self:oSchema:addProperty("NIVEL1"		,STR0054  ,"boolean",STR0054  ,"NIVEL1")      // Nivel    
    self:oSchema:addProperty("NATCTA"		,STR0055  ,"string" ,STR0055  ,"NATCTA")      // Natureza da Conta    
    self:oSchema:addProperty("DESCCONT"		,STR0056  ,"string" ,STR0056  ,"DESCCONT")    // Cont. Descri??o Conta     
    self:oSchema:addProperty("FILIAL"		,STR0057  ,"string" ,STR0057  ,"FILIAL")      // Filial    
    self:oSchema:addProperty("COLUNA"		,STR0058  ,"number" ,STR0058  ,"COLUNA")      // Coluna    
    self:oSchema:addProperty("IMPSAL"		,STR0059  ,"string" ,STR0059  ,"IMPSAL")      // Imprime Saldo    
    self:oSchema:addProperty("MOVIMENTO"	,STR0060  ,"number" ,STR0060  ,"MOVIMENTO")   // Movimento do Periodo    
    self:oSchema:addProperty("MOVIMENTO_M"	,STR0061  ,"string" ,STR0061  ,"MOVIMENTO")   // Mov do Periodo Mascara

    self:oSchema:addProperty("SALDOSEM"	    ,STR0072  ,"number" ,STR0072  ,"SALDOSEM")    // Saldo por semestre
    self:oSchema:addProperty("SALDOSEM_M"	,STR0076  ,"string" ,STR0076  ,"SALDOSEM_M")  // Saldo por semestre Mascara
    self:oSchema:addProperty("SALDOPER"	    ,STR0073  ,"number" ,STR0073  ,"SALDOPER")    // Saldo 2 Periodos Anteriores
    self:oSchema:addProperty("SALDOPER_M"	,STR0077  ,"string" ,STR0077  ,"SALDOPER_M")  // Saldo 2 Periodos Anteriores Mascara
    self:oSchema:addProperty("MOVIMPER"	    ,STR0074  ,"number" ,STR0074  ,"MOVIMPER")    // Saldo Periodo Determinado
    self:oSchema:addProperty("MOVIMPER_M"	,STR0079  ,"string" ,STR0079  ,"MOVIMPER_M")  // Saldo Periodo Determinado Mascara

    self:oSchema:addProperty("NIVEL"	    ,STR0075  ,"number" ,STR0075  ,"NIVEL")       // Nivel"
    self:oSchema:addProperty("GRUPOORDEM"   ,STR0078 ,"number"  ,STR0078  ,"GRUPOORDEM")  // Ordenação do Grupo

    self:getCtGerPlanSchema()

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class SmartviewCtbFunctions
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class SmartviewCtbFunctions
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class SmartviewCtbFunctions
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar se irá pular registro
 
@return lógico

@author wilton.santos
@since 26/07/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method ExecDBSkipTemp() class SmartviewCtbFunctions
    Local lRet := .T.
Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Método para validar se deve executar a chamada da funcao CtGerPlan()
 
@return lógico
 
@author wilton.santos
@since 26/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
method vldExeCtgGerPlan() class SmartviewCtbFunctions
    Local lRet as Logical
    Local aCtbMoeda as array 
    Local cMsgError as character

    lRet        := .T.
    aCtbMoeda   := {}
    cMsgError   := ""
    
    If lRet
		aCtbMoeda := CtbMoeda(::cMoeda, ::aSetOfBook[9])
    	If Empty(aCtbMoeda[1])                       
			lRet := .F.
			FwLogMsg("WARN",, "NOMOEDA",,, , STR0080 , , ,) //Smart View; Mensagem retornada pelo metodo
			self:setErrorStatus(400, "NOMOEDA", STR0080) 
    	endif
	endif

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getCtgGerPlanAppend
    Define o dados que serão appendados na temporaria gerada na ctgerplan,
    podendo serem sobrepostos no handleCtgGerPlanAppend 
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCtgGerPlanAppend(oFilter as object, nPage As Numeric) as object class SmartviewCtbFunctions

    local cMascCta           as character
    local cMascCus           as character
    local cMascIte           as character
    local cMascClv           as character
    local cConta             as character
    local cCusto             as character
    local cItem              as character
    local cClvl              as character
    local cPicture           as character
    local cSepara            as character
    local nDecimais          as numeric
    local TAM_VALOR := 17    as numeric
    local aArea := {}        as array

    Local lSaldoPer := .F.   as logical
    Local lMovPer   := .F.   as logical
    Local cTpValor	:= SuperGetMv("MV_TPVALOR")  as character
    Local nGrupoOrdem        := 0   as numeric
    Local cGrupoBkp          as character
    aArea := getArea()
    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) //DecimalCTB(aSetOfBook,mv_par08)
    cPicture  := Iif(!Empty(::aSetOfBook[4]),::aSetOfBook[4],"@E 999,999,999,999,999.99")
    
    
    //coletando cada maskara
    cMascCta   := IIF(Empty(::aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(::aSetOfBook[2],@cSepara))//Mascara da Conta
    cMascCus   := IIF(Empty(::aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(::aSetOfBook[6],@cSepara))//Mascara do Centro de Custo
    cMascIte   := IIF(Empty(::aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(::aSetOfBook[7],@cSepara))//Mascara do Item Contabil    
    cMascClv   := IIF(Empty(::aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(::aSetOfBook[8],@cSepara))//Mascara da Classe Valor

    //Verifica se adiciona valores aos campos de periodo determinado
    lSaldoPer := cArqTmp->(FieldPos("SALDOPER")) > 0
    lMovPer   := cArqTmp->(FieldPos("SALDOPER")) > 0
    cArqTmp->(DBGoTop())

    While !cArqTmp->(Eof())

        //Salva o grupo
        cGrupoBkp := cArqTMP->GRUPO        
        
        //nao imprime sinteticas
        If !::lImpSint
		    If self:ExecImpSint(cArqTmp)
                cArqTmp->(DbSkip())
                //Valida se é ultimo registro no while e se ainda tem registros na paginação
                self:getNextPage()
                Loop
            endif
        endif

        //Metodo para pular determinado registro
        If !self:ExecDBSkipTemp(cArqTmp)
            cArqTmp->(DbSkip())
            //Valida se é ultimo registro no while e se ainda tem registros na paginação
            self:getNextPage()
            Loop
        endif

        //Tratamento para mascara
        cConta := EntidadeCTB(cArqTMP->CONTA ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
        cCusto := EntidadeCTB(cArqTMP->CUSTO ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
        cItem  := EntidadeCTB(cArqTMP->ITEM  ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
        cClvl  := EntidadeCTB(cArqTMP->CLVL  ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)

        //NovO objeto
	    ::jDados := JsonObject():new()

        ::jDados["CONTA"]           := cArqTMP->CONTA
        ::jDados["CONTA_M"]         := cConta
        ::jDados["SUPERIOR"]        := cArqTMP->SUPERIOR
        ::jDados["NORMAL"]          := cArqTMP->NORMAL
        ::jDados["CTARES"]          := cArqTMP->CTARES
        ::jDados["DESCCTA"]         := cArqTMP->DESCCTA
        ::jDados["CUSTO"]           := cArqTMP->CUSTO
        ::jDados["CUSTO_M"]         := cCusto
        ::jDados["CCRES"]           := cArqTMP->CCRES
        ::jDados["DESCCC"]          := cArqTMP->DESCCC
        ::jDados["ITEM"]            := cArqTMP->ITEM
        ::jDados["ITEM_M"]          := cItem
        ::jDados["ITEMRES"]         := cArqTMP->ITEMRES
        ::jDados["DESCITEM"]        := cArqTMP->DESCITEM
        ::jDados["CLVL"]            := cArqTMP->CLVL
        ::jDados["CLVL_M"]          := cClvl
        ::jDados["CLVLRES"]         := cArqTMP->CLVLRES
        ::jDados["DESCCLVL"]        := cArqTMP->DESCCLVL
        ::jDados["SALDOANT"]        := cArqTMP->SALDOANT
        ::jDados["SALDOANT_M"]      := ValorCTB(cArqTmp->SALDOANT ,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDOANTDB"]      := cArqTMP->SALDOANTDB
        ::jDados["SALDOANTDB_M"]    := ValorCTB(cArqTMP->SALDOANTDB,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDOANTCR"]      := cArqTMP->SALDOANTCR
        ::jDados["SALDOANTCR_M"]    := ValorCTB(cArqTMP->SALDOANTCR,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDODEB"]        := cArqTMP->SALDODEB
        ::jDados["SALDODEB_M"]      := ValorCTB(cArqTMP->SALDODEB,,,TAM_VALOR,nDecimais,.F.,cPicture,"1",,,,,,::lPrintZero,.F.)
        ::jDados["SALDOCRD"]        := cArqTMP->SALDOCRD
        ::jDados["SALDOCRD_M"]      := ValorCTB(cArqTMP->SALDOCRD,,,TAM_VALOR-2,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
        ::jDados["SALDOATU"]        := cArqTMP->SALDOATU
        ::jDados["SALDOATU_M"]      := ValorCTB(cArqTMP->SALDOATU,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDOADB"]        := cArqTMP->SALDOATUDB
        ::jDados["SALDOADB_M"]      := ValorCTB(cArqTMP->SALDOATUDB,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDOACR"]        := cArqTMP->SALDOATUCR
        ::jDados["SALDOACR_M"]      := ValorCTB(cArqTMP->SALDOATUCR,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["TIPOCONTA"]       := cArqTMP->TIPOCONTA
        ::jDados["TIPOCC"]          := cArqTMP->TIPOCC
        ::jDados["TIPOITEM"]        := cArqTMP->TIPOITEM
        ::jDados["TIPOCLVL"]        := cArqTMP->TIPOCLVL
        ::jDados["CCSUP"]           := cArqTMP->CCSUP
        ::jDados["CCNORMAL"]        := cArqTMP->CCNORMAL
        ::jDados["ITSUP"]           := cArqTMP->ITSUP
        ::jDados["ITNORMAL"]        := cArqTMP->ITNORMAL
        ::jDados["CLSUP"]           := cArqTMP->CLSUP
        ::jDados["CLNORMAL"]        := cArqTMP->CLNORMAL
        ::jDados["ORDEM"]           := cArqTMP->ORDEM
        ::jDados["GRUPO"]           := cArqTMP->GRUPO
        ::jDados["IDENTIFI"]        := cArqTMP->IDENTIFI
        ::jDados["TOTVIS"]          := cArqTMP->TOTVIS
        ::jDados["SLDENT"]          := cArqTMP->SLDENT
        ::jDados["FATSLD"]          := cArqTMP->FATSLD
        ::jDados["VISENT"]          := cArqTMP->VISENT
        ::jDados["ESTOUR"]          := cArqTMP->ESTOUR
        ::jDados["NIVEL1"]          := cArqTMP->NIVEL1
        ::jDados["NATCTA"]          := cArqTMP->NATCTA
        if cArqTmp->(FieldPos("DESCCONT")) > 0
            ::jDados["DESCCONT"]        := cArqTMP->DESCCONT
        endif
        ::jDados["FILIAL"]          := cArqTMP->FILIAL
        if cArqTmp->(FieldPos("COLUNA")) > 0
            ::jDados["COLUNA"]          := cArqTMP->COLUNA
        endif
        if cArqTmp->(FieldPos("IMPSAL")) > 0
            ::jDados["IMPSAL"]          := cArqTMP->IMPSAL
        endif
        ::jDados["MOVIMENTO"]       := cArqTMP->MOVIMENTO
        ::jDados["MOVIMENTO_M"]     := ValorCTB(cArqTMP->MOVIMENTO,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
        ::jDados["SALDOSEM"]        := cArqTMP->SALDOSEM
        ::jDados["SALDOSEM_M"]      := ValorCTB(cArqTMP->SALDOSEM,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,::lPrintZero,.F.)
        ::jDados["SALDOPER"]        := IIf(lSaldoPer,cArqTMP->SALDOPER,0 )
        ::jDados["SALDOPER_M"]      := IIf(lSaldoPer,ValorCTB(cArqTMP->SALDOPER,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,::lPrintZero,.F.),0)
        ::jDados["MOVIMPER"]        := IIf(lMovPer,cArqTMP->MOVIMPER,0 )
        ::jDados["MOVIMPER_M"]      := IIf(lMovPer,ValorCTB(cArqTmp->MOVIMPER,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,::lPrintZero,.F.),0)
        ::jDados["GRUPOORDEM"]      := nGrupoOrdem
         
        self:processData()
        self:processCustomData()

        self:oData:appendData(::jDados)
        self:oIntegratedProvider:lAppend := .T. //Valida se appendou registro, caso pule nas validações acima
        cArqTmp->(DbSkip())

        //Valida se é ultimo registro no while e se ainda tem registros na paginação
        self:getNextPage()

        If cGrupoBkp <> cArqTMP->GRUPO
            nGrupoOrdem++
        endif       

    EndDo  

    self:handleCtgGerPlanAppend(oFilter)

    CTBGerClean()

    dbselectArea("CT2")

    FreeObj(self:oIntegratedProvider)

    restarea(aArea)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Método para appendar dados no json
 
@return object: string
 
@author wilton.santos
@since 28/07/2023
@version 12.1.2310
*/
//-------------------------------------------------------------------
method processCustomData() class SmartviewCtbFunctions    
Return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} setCtgGerPlanParams
    Define um valor default para os atributos abaixo
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method setCtgGerPlanParams(oFilter as object) class SmartviewCtbFunctions

    ::lConsSaldo    := .F.
    ::lPlGerSint    := .F.
    ::cSegmentoG 	:= ""
    ::lUsGaap		:=.F.
    ::cMoedConv	    := ""
    ::cConsCrit	    := ""
    ::dDataConv	    := CTOD("  /  /  ")
    ::nTaxaConv	    := 0
    ::lImpSint	    := .T.
    ::lImpMov		:= .T.
    ::cSegmento	    := ""
    ::cFilUsu		:= ".T."
    ::lRecDesp0	    := .F.
    ::cRecDesp 	    := ""
    ::dDtZeraRD	    := CTOD("  /  /  ")
    ::lImp3Ent	    := .F.
    ::lImp4Ent	    := .F.
    ::lImpEntGer	:= .F.
    ::lImpConta	    := .T.
    ::lFiltraCC	    := .F.
    ::lFiltraIt	    := .F.
    ::lFiltraCV	    := .F.
    ::cMoedaDsc	    := '01'
    ::lMovPeriodo   := .F.
    ::aSelFil 	    := {}
    ::dDtCorte 	    := CTOD("  /  /  ")
    ::lCompEnt	    := .F.
    ::cArqAux		:= "cArqTmp"
    ::cArqTmpAux    := ::cArqAux+"SV"
    ::cArqTmp 	    := ""
    ::lUsaNmVis	    := .F.
    ::lCttSint	    := .F.
    ::cQuadroCTB    := ""
    ::lTodasFil     := .F.
    ::aEntidades    := {}
    ::cCodEntidade  := ""
    ::lDemDRE       :=.F.
    ::dFinalA 	    := CTOD("  /  /  ")
    ::nDivide       := 1
    ::lPrintZero    := .F.
    ::lImpAntLP     := .F.
    ::lCTBR185SV    := .F.
    ::lCTBR100SV    := .F.
    ::lCTBR140SV    := .F.

    self:handleCtgGerPlanParams()

return 
//-------------------------------------------------------------------
/*/{Protheus.doc} exeCtgGerPlan
    Executa a CtgerPlan conforme atributos preenchidos na classe filha
    @author wilton.santos
    @since 28/07/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method exeCtgGerPlan(oFilter as object) as object class SmartviewCtbFunctions
    Private nSaldoSEM := 0 as numeric
    Private dPeriodo0 as date
    Private dSemestre as date

    If self:lPeriodo0
        If ValType(::dPeriodo0) == "D" 
            dPeriodo0 := ::dPeriodo0
        endif
    endif

    If self:lSemestre
        If ValType(::dSemestre) == "D"
            dSemestre := ::dSemestre
        endif
    endif

    CTGerPlan( 	"",;
                "",;
                "",;
                "",;
                @cArqtmp,;
                ::dDataIni,;
                ::dDataFim,;
                ::cAlias,;
                ::cIdent,;
                ::cContaIni,;
                ::cContaFim,;
                ::cCCIni,;
                ::cCCFim,;
                ::cItemIni,;
                ::cItemFim,;
                ::cClvlIni,;
                ::cClVlFim,;
                ::cMoeda,;
                ::cSaldos,;
                ::aSetOfBook,;
                ::cSegmento,;
                ::cSegIni,;
                ::cSegFim,;
                ::cFiltSegm,;
                ::lNImpMov,;
                ::lImpConta,;
                ::nGrupo,;
                ::cHeader,;
                ::lImpAntLP,;
                ::dDataLP,;
                ::nDivide,;
                ::lVlrZerado,;
                ::cFiltroEnt,;
                ::cCodFilEnt,;
                ::cSegmentoG,;
                ::cSegIniG,;
                ::cSegFimG,;
                ::cFiltSegmG,;
                ::lUsGaap,;
                ::cMoedConv,;
                ::cConsCrit,;
                ::dDataConv,;
                ::nTaxaConv,;
                ::aGeren,;
                ::lImpMov,;
                ::lImpSint,;
                ::cFilUSU,;
                ::lRecDesp0,;
                ::cRecDesp,;
                ::dDtZeraRD,;
                ::lImp3Ent,;
                ::lImp4Ent,;
                ::lImpEntGer,;
                ::lFiltraCC,;
                ::lFiltraIt,;
                ::lFiltraCV,;
                ::cMoedaDsc,;
                ::lMovPeriodo,;
                ::aSelFil,;
                ::dDtCorte,;
                ::lPlGerSint,;
                ::lConsSaldo,;
                ::lCompEnt,;
                ::cArqAux,;
                ::lUsaNmVis,;
                ::cNomeVis,;
                ::lCttSint,;
                ::lTodasFil,;
                ::cQuadroCTB,;
                ::aEntidades,;
                ::cCodEntidade,;
                ::lDemDRE,;
                ::dFinalA,;
                Nil,;
                Nil,;
                ::lCTBR185SV,;
                ::lCTBR100SV,;
                ::lCTBR140SV,;
                Nil,;
                self:lPageControl,;
                self:oIntegratedProvider:nRecnoI,;
                self:oIntegratedProvider:nRecnoF,;
                @::oIntegratedProvider:nTotalRows,;
                @::oIntegratedProvider:nLastRowPage,;
                @::aRecTemp)

    self:handleExeCtgGerPlan()

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} ExecImpSint
    Verifica quais entidades irão imprimir sintetico
    @author gustavo.campos
    @since 30/08/2023
    @version 12.1.2310
/*/ 
//-------------------------------------------------------------------
method ExecImpSint(cArqTmp As character) class SmartviewCtbFunctions

    Local lRetorno := .T.

    lRetorno := cArqTmp->TIPOCONTA == '1'

Return lRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} CompVld
    Ajusta o par?metro nDivide vindo do objeto de neg?cio
    @author gustavo.campos
    @since 30/08/2023
    @version 12.1.2310
/*/ 
//-------------------------------------------------------------------
method CompVld(nDivide as Numeric) class SmartviewCtbFunctions

    If nDivide == 1			// Divide por 1 - Não se aplica
	   ::nDivide := 1
    ElseIf nDivide == 2			// Divide por cem
	   ::nDivide := 100
    ElseIf nDivide == 3		// Divide por mil
	    ::nDivide := 1000
    ElseIf nDivide == 4		// Divide por milhao
	    ::nDivide := 1000000
    endif

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getNextPage
    Valida se é fim de arquivo e tem próxima página para consultar

    @author Renan Gremes
    @since 10/09/2025
    @version 12.1.2310
/*/ 
//-------------------------------------------------------------------
method getNextPage() class SmartviewCtbFunctions
    if cArqTmp->(Eof())
        if self:oIntegratedProvider:hasNextPage()
            CTBGerClean()
            self:oIntegratedProvider:lAppend := .T.
            self:oIntegratedProvider:setPageRangeByStep(.F.)
            self:exeCtgGerPlan()
            cArqTmp->(dbGoTop())
        elseif self:lAdditional //Se não tiver próxima página e o relatório existir conteúdo adicional
            self:handleAdditionalContent()
        endif
    endif
return

//-------------------------------------------------------------------
/*{Protheus.doc} CtContaSup
Adiciona contas superiores/sintéticas na tabela temporária
 
@author Renan Gremes
@since 07/11/2025
@version 1.0
*/
//-------------------------------------------------------------------
method CtContaSup(cAlias as character) class SmartviewCtbFunctions
    local cContaSup	 as character
    local cDesc		 as character
    local cNatCta	 as character
    local nSaldoAnt	 as numeric
    local nSaldoAtu	 as numeric
    local nSaldoDeb	 as numeric
    local nSaldoCrd	 as numeric
    local nMovimento as numeric
    local nSaldoAntD as numeric
    local nSaldoAntC as numeric
    local nSaldoAtuD as numeric
    local nsaldoAtuC as numeric
    local nRegTmp	 as numeric
    local nFirstReg  as numeric

    default cAlias := "cArqTmp"

	aSaveArea	 := GetArea() 
	cContaSup	 := ""
	cDesc		 := ""
	cNatCta	 	 := ""
	nSaldoAnt	 := 0
	nSaldoAtu	 := 0
	nSaldoDeb	 := 0
	nSaldoCrd	 := 0
	nMovimento 	 := 0
	nSaldoAntD 	 := 0
	nSaldoAntC 	 := 0
	nSaldoAtuD 	 := 0
	nsaldoAtuC 	 := 0
	nRegTmp	 	 := 0
    nFirstReg    := 0

    nSaldoAnt	:= cArqTmp->SALDOANT
    nSaldoAtu	:= cArqTmp->SALDOATU
    nSaldoDeb	:= cArqTmp->SALDODEB
    nSaldoCrd	:= cArqTmp->SALDOCRD
    nMovimento	:= cArqTmp->MOVIMENTO

    nSaldoAtuD	:= cArqTmp->SALDOATUDB
    nSaldoAtuC	:= cArqTmp->SALDOATUCR

    // Grava sinteticas
    dbSelectArea(cAlias)
    dbGoTop()

    //Reseta flag de conteúdo adicional para não chamar esta função mais de uma vez.
    self:lAdditional := .F.

    while !Eof()
        if &(cAlias)->(TIPOCONTA) == "1"
            dbSkip()
            Loop
        endif

        nRegTmp	:= Recno()
        nSaldoAnt	:= &(cAlias)->(SALDOANT)
        nSaldoAtu	:= &(cAlias)->(SALDOATU)
        nSaldoDeb	:= &(cAlias)->(SALDODEB)
        nSaldoCrd	:= &(cAlias)->(SALDOCRD)
        nMovimento	:= &(cAlias)->(MOVIMENTO)
        nSaldoAtuD	:= &(cAlias)->(SALDOATUDB)
        nSaldoAtuC	:= &(cAlias)->(SALDOATUCR)

        dbSelectArea("CT1")
        CT1->(dbSetOrder(1))

        cContaSup := &(cAlias)->(SUPERIOR)
		if Empty(cContaSup)
			dbSelectArea(cAlias)
			Replace NIVEL1 With .T.
			dbSelectArea("CT1")
		endif    
        CT1->(DbSeek(FWxFilial()+cContaSup))

        while CT1->(!Eof()) .and. CT1->CT1_FILIAL == FWxFilial()

			if Empty(self:cMoedaDsc)
				cDesc := &("CT1->CT1_DESC"+self:cMoeda)
			else
				cDesc := &("CT1->CT1_DESC"+self:cMoedaDsc)
			endif

			if Empty(cDesc)		// Caso nao preencher descricao da moeda selecionada
				cDesc := CT1->CT1_DESC01
			endif

            dbSelectArea(cAlias)
            dbSetOrder(1)

            if &(cAlias)->(FieldPos("NATCTA")) > 0
                cNatCta := &(cAlias)->(NATCTA)
            endif

            if !MsSeek(cContaSup)
                dbAppend()
                if nFirstReg == 0
                    nFirstReg := Recno()
                endif
                Replace CONTA		With cContaSup
                Replace SUPERIOR	With CT1->CT1_CTASUP
                Replace DESCCTA	    With cDesc
                Replace TIPOCONTA	With CT1->CT1_CLASSE
                Replace CTARES    	With CT1->CT1_RES
                Replace NORMAL   	With CT1->CT1_NORMAL
                Replace GRUPO		With CT1->CT1_GRUPO
                if &(cAlias)->(FieldPos("NATCTA")) > 0
                    Replace NATCTA	With cNatCta
                endif
            endif

            Replace	SALDOANT 	 With &(cAlias)->(SALDOANT  ) + nSaldoAnt
            Replace SALDOANTDB   With &(cAlias)->(SALDOANTDB) + nSaldoAntD
            Replace SALDOANTCR	 With &(cAlias)->(SALDOANTCR) + nSaldoAntC
            Replace SALDOATU 	 With &(cAlias)->(SALDOATU 	) + nSaldoAtu
            Replace SALDOATUDB	 With &(cAlias)->(SALDOATUDB) + nSaldoAtuD
            Replace SALDOATUCR	 With &(cAlias)->(SALDOATUCR) + nsaldoAtuC
            Replace SALDODEB 	 With &(cAlias)->(SALDODEB 	) + nSaldoDeb
            Replace SALDOCRD 	 With &(cAlias)->(SALDOCRD 	) + nSaldoCrd

            if !self:lNImpMov
                Replace MOVIMENTO With &(cAlias)->(MOVIMENTO) + nMovimento
            endif

			if self:lSemestre		// Saldo por semestre
				Replace SALDOSEM With &(cAlias)->(SALDOSEM) + nSaldoSEM
			endif

			if self:lPeriodo0		// Saldo dois periodos anteriores
				Replace SALDOPER With &(cAlias)->(SALDOPER) + nSaldoSEM
			endif           

            dbSelectArea("CT1")
            cContaSup := CT1->CT1_CTASUP
            if empty(CT1->CT1_CTASUP)
                dbSelectArea(cAlias)
                Replace NIVEL1 With .T.
                dbSelectArea("CT1")
                exit
            endif
            MsSeek(xFilial()+cContaSup)
        endDo
        dbSelectArea(cAlias)
        dbGoto(nRegTmp)
        dbSkip()	
    endDo

Return
