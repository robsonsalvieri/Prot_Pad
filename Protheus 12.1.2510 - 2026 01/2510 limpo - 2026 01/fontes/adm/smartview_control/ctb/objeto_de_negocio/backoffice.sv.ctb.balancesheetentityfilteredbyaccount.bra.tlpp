#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetentityfilteredbyaccount.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT3, CT4, CTI", name="Balancete 1 Entidade Filtrada por Conta", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete 1 Entidade Filtrada por Conta
para o TReports
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    //usados na internacionalizacao
    public method preschema()      			as object 
    public method preData()        			as object
    public method processData()    			as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp() as logical

    private method f245Soma() as numeric

    protected data nDecimais as numeric
    protected data cPicture  as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete 1 Entidade Filtrada por Conta"
    self:setPergunte("CTR245") //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete 1 Entidade Filtrada por Conta"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("NomeEmpresa",STR0003, "string", STR0003, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0004, "string", STR0004, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
    local TAM_VALOR := 16 as numeric
    local nTotDeb   as numeric
    local nTotCrd   as numeric
    local nTotMov   as numeric  

    nTotDeb := self:f245Soma("D")
    nTotCrd := self:f245Soma("C")

    nTotMov := (nTotCrd - nTotDeb)

    ::jDados["SALDODEB"	  ] := nTotDeb
    ::jDados["SALDODEB_M" ] := ValorCTB(nTotDeb,,,TAM_VALOR,::nDecimais,.F.,::cPicture,,,,,,,::lPrintZero,.F., .F.)
    ::jDados["SALDOCRD"	  ] := nTotCrd
    ::jDados["SALDOCRD_M" ] := ValorCTB(nTotCrd,,,TAM_VALOR,::nDecimais,.F.,::cPicture,,,,,,,::lPrintZero,.F., .F.)
    ::jDados["MOVIMENTO"  ] := nTotMov
    ::jDados["MOVIMENTO_M"] := iif(nTotMov < 0, ValorCTB(nTotMov,,,TAM_VALOR,::nDecimais,.T.,::cPicture,"1",,,,,,::lPrintZero,.F.),;
	                           iif(nTotMov > 0, ValorCTB(nTotMov,,,TAM_VALOR,::nDecimais,.T.,::cPicture,"2",,,,,,::lPrintZero,.F.), nil))
	::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial"]  := self:cBranchName
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR245")//Carrega lista de parametros para a memória
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) as logical class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
   
    Local lRet       := .T.  As Logical
    Local cClasse	 := ""   As Character
    Local nCont      := 0 as Numeric
    Local nDigitAte  := 0 as Numeric
    Local cMascara   := "" as Character
    Local cSegAte 	 := mv_par14  as Character// Imprimir ate o Segmento?
    Local cSeparador := "" as Character

    If  mv_par03 == 1
		cMascara	:= IIF (Empty(::aSetOfBook[6]),"",RetMasCtb(::aSetOfBook[6],@cSeparador)) // Mascara do Centro de Custo
	ElseIf mv_par03 == 2
		cMascara	:= IIF (Empty(::aSetOfBook[7]),"",RetMasCtb(::aSetOfBook[7],@cSeparador)) // Mascara do Item
	ElseIf mv_par03 == 3
		cMascara	:= IIF (Empty(::aSetOfBook[8]),"",RetMasCtb(::aSetOfBook[8],@cSeparador)) // Mascara da Classe Valor
    else
        cMascara	:= IIF (Empty(::aSetOfBook[6]),"",RetMasCtb(::aSetOfBook[6],@cSeparador)) // Mascara do Centro de Custo    
	EndIf
	
	If mv_par03 == 1
		cClasse	:= cArqTmp->TIPOCC
	ElseIf mv_par03 == 2
		cClasse	:= cArqTmp->TIPOITEM
	ElseIf mv_par03 == 3
		cClasse	:= cArqTmp->TIPOCLVL
	EndIf  
    
    If mv_par08 == 1		// Apenas sinteticas
	    lRet := (cClasse == "1")
    ElseIf mv_par08 == 2	// Apenas analiticas
        lRet := (cClasse == "2")
    EndIf

    // Verifica Se existe filtragem Ate o Segmento
	//Filtragem ate o Segmento ( antigo nivel do SIGACON)		
	If !Empty(cSegAte)
		For nCont := 1 to Val(cSegAte)
			nDigitAte += Val(Subs(cMascara,nCont,1))	
		Next
		If Len(Alltrim(cClasse)) > nDigitAte
			lRet := .F.
		Endif
	EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Bruno Oliveira (BO)
@since 29/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character    
    Local nDivide := 1      as numeric
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date
    Local cCCIni   := ""    as Character
    Local cCCFim   := ""    as Character
    Local cItemIni := ""    as Character
    Local cItemFim := ""    as Character
    Local cClVlIni := ""    as Character
    Local cClVlFim := ""    as Character
    Local cAlias   := ""    as Character

    Local lRecDesp0		:= Iif(mv_par30==1,.T.,.F.) as Logical
    Local cRecDesp		:= mv_par31 as Character
    Local dDtZeraRD		:= mv_par32 as Date

    nDivide := iif(empty(MV_PAR23),nDivide,MV_PAR23)
  
	If mv_par03 == 1
		cAlias	:= "CT3"                    
		cCCIni	:= mv_par04
		cCCFim	:= mv_par05
	ElseIf mv_par03 == 2
		cAlias	:= "CT4"   
		cItemIni:= mv_par04
		cItemFim:= mv_par05	
		cCCIni	:= mv_par26
		cCCFim	:= mv_par27
	ElseIf mv_par03 == 3
		cAlias	:= "CTI"   
		cClVlIni:= mv_par04
		cClVlFim:= mv_par05
		cCCIni	:= mv_par26
		cCCFim	:= mv_par27
		cItemIni := mv_par28
		cItemFim := mv_par29
    else
        //No Smart View , Permite colocar outros valores, para não gerar erro virá a Entidade C. Custo
        cAlias	:= "CT3"                    
        cCCIni	:= mv_par04
        cCCFim	:= mv_par05 
	EndIf

    dDataIni    :=  MV_PAR01 //Data Inicial ?                
    dDataFim    :=  MV_PAR02 //Data Final ?  
    cContaIni   :=  MV_PAR06 //Conta Inicial ?               
    cContaFim   :=  MV_PAR07 //Conta Final ?
                
    lImpSint    := iIf(MV_PAR08==1 .Or. MV_PAR08==3,.T.,.F.) //Imprime Entidades ?              
    aSetOfBook  := iIf(!Empty(MV_PAR09),CTBSetOf(MV_PAR09),CTBSetOf("")) //Cod. Config. Livros ?         
    lSldZerado  := iIf(MV_PAR10==1,.T.,.F.) //Saldos Zerados                            
    cMoeda      := iIf(!empty(MV_PAR11),MV_PAR11,cMoeda) //Moeda                                            
    cTpsald     := iif(!Empty(MV_PAR13), PadR(AllTrim(MV_PAR13), TamSX3("CT2_TPSALD")[1]), cTpsald) //Tipo de Saldo
    cFilSeg     := MV_PAR15 //Filtra Segmento No. ?  
    cConteudIni := MV_PAR16 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR17 //Conteudo Fim Segmen ?        
    cConteudCont:= MV_PAR18 //Conteudo Contido em ?

    lPrintZero  := Iif(MV_PAR21==1,.T.,.F.) //Imprime Valor 0.00  ?
    lImpCod     := Iif(MV_PAR22==1,.T.,.F.) //Imprime codigo ?
    lImpAntLP   := Iif(MV_PAR24==1,.T.,.F.) //Posição Ant. L/P?
    dDataLP     := MV_PAR25 //Data Lucros/Perdas?      
    aSelFil     := {xFilial('CTU')}  

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim

    self:cCCIni         := cCCIni
    self:cCCFim         := cCCFim
    self:cItemIni       := cItemIni
    self:cItemFim       := cItemFim
    self:cClvlIni       := cClvlIni  
    self:cClVlFim       := cClVlFim  
    self:cAlias         := cAlias//são obrigatórios
    self:cIdent         := ""    //são obrigatórios   
    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := lImpCod
    self:lImpConta      := .F.
    self:aGeren         := nil

    self:lRecDesp0      := lRecDesp0
    self:cRecDesp       := cRecDesp
    self:dDtZeraRD      := dDtZeraRD
    self:lPrintZero     := lPrintZero

    ::nDecimais := DecimalCTB(::aSetOfBook, MV_PAR11)
    ::cPicture  := ::aSetOfBook[4]  
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} f245Soma
Soma o valor do lançamento
 
@return numérico
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method f245Soma(cTipo as character) as numeric class BalanceSheetEntityFilteredbyAccountSmartViewBusinessObject

    Local nRetValor	:= 0  as numeric
    Local cClasse	:= "" as character
	
	If MV_PAR03 == 1
		cClasse	:= cArqTmp->TIPOCC
	ElseIf MV_PAR03 == 2
		cClasse	:= cArqTmp->TIPOITEM
	ElseIf MV_PAR03 == 3
		cClasse	:= cArqTmp->TIPOCLVL
	EndIf

	If MV_PAR08 == 1 // So imprime Sinteticas - Soma Sinteticas
		If cClasse == "1" .And. cArqTmp->NIVEL1
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	Else // Soma Analiticas		
		If cClasse == "2"
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf	    
    EndIf
	
return nRetValor
