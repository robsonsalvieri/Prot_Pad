#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.costcenter.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTT", name="Centro de Custos", country="ALL",,customTables="CTT")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CostCenterSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cad Centro de Custo para o SmartView
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class CostCenterSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getData() as object
    public method getSchema() as object

    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json

    protected method getQuery() 

    protected data jItens       as json
    protected data oStatement   as object
    protected data cAlias       as character
    protected data oIntegratedProvider  as object
    protected data aAllFields   as array


EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class CostCenterSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Cadastro de Centro de Custo"
    self:setPergunte("CTR020") //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class CostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Cadastro de Centro de Custo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class CostCenterSmartViewBusinessObject
Return {STR0003}//"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class CostCenterSmartViewBusinessObject
    local nX := 0           as numeric
    local cId,cRealName     as character
    local cMascara	 := ""  as character
    local cSeparador := ""  as character
    local cCompanyName        as character
	local cBranchName         as character

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR020")

	::oIntegratedProvider := ControlIntegratedProvider():New()
	cCompanyName    := ::oIntegratedProvider:getCompanyName()
	cBranchName     := ::oIntegratedProvider:getBranchName()
      
    self:getQuery(oFilter)
    self:cAlias := self:oStatement:OpenAlias()

    If Empty(mv_par06)		
        cMascara := SuperGetMV("MV_MASCCUS", .F.)      
    Else
        cMascara := RetMasCtb(mv_par06,@cSeparador)
    EndIf

    While !(self:cAlias)->(Eof()) 

        ::jItens := JsonObject():new()

        For nX := 1 to len (::aAllFields)

            cId := ::aAllFields[nX]:getName()
            cRealName := ::aAllFields[nX]:getRealName()

            If cId == "CTT_CUSTO" .OR. cId == "CTT_CCSUP"               
                ::jItens[cId] := EntidadeCTB((self:cAlias)->&(cRealName) ,000,000,030,.F.,cMascara,cSeparador,,,.F.,,.F.)
            ElseIf ::aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(cRealName))
            Else
                ::jItens[cId] := (self:cAlias)->&(cRealName)                
            EndIf

        Next nX

        ::jItens["NomeEmpresa" ] 	:= cCompanyName
		::jItens["NomeFilial"  ] 	:= cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())

    EndDo

    dbSelectArea(self:cAlias)
    (self:cAlias)->(DBCloseArea())

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters()) 
    self:oStatement:Destroy()
    self:oStatement := nil  

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class CostCenterSmartViewBusinessObject
    Local aFieldsCTT := {}  as Array
    Local lCanFilter := .F. as Logical 

    aFieldsCTT := { "CTT_FILIAL", "CTT_CUSTO" , "CTT_CLASSE", "CTT_NORMAL", "CTT_DESC01", "CTT_DESC02", "CTT_DESC03",;
                    "CTT_DESC04", "CTT_DESC05", "CTT_BLOQ"  , "CTT_DTEXIS", "CTT_DTEXSF", "CTT_CCLP"  , "CTT_CCPON" ,;
                    "CTT_BOOK"  , "CTT_CCSUP" , "CTT_TIPO01", "CTT_RES"  }

    self:oSchema:aliasToSchema("CTT", aFieldsCTT)

    self:aAllFields := self:getStructFields()
	
	self:oSchema:addProperty( "NomeEmpresa",STR0004   ,"string"   ,STR0004  , "NomeEmpresa" ,,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty( "NomeFilial" ,STR0005   ,"string"   ,STR0005  , "NomeFilial"  ,,,,lCanFilter) // Nome Filial

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Prepara a query a ser executada
 
@param oFilter objeto: objeto contendo filtro do relatório
 
@return nil
 
@author Douglas Silva
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getQuery(oFilter as Object) class CostCenterSmartViewBusinessObject
    local cQuery        := ""   as character
    local nParamOrder   := 1    as numeric
    local lBloqueado            as Logical

    lBloqueado := iif(MV_PAR05==2,.T.,.F.)

    cQuery := " SELECT  "+self:getSQLFields(.F., , .F.)
    cQuery += " FROM "+RetSqlName("CTT")+" CTT "
    cQuery += " WHERE "

    cQuery += " CTT.CTT_FILIAL =  ? "
    cQuery += " AND CTT.CTT_CUSTO >= ? "
    cQuery += " AND CTT.CTT_CUSTO <= ? "
    if lBloqueado
        cQuery  += " AND CTT.CTT_BLOQ <> ? "
    EndIf
	cQuery += " AND CTT.D_E_L_E_T_ = ? "

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY CTT_CUSTO "

    cQuery := ChangeQuery(cQuery)
    
    self:oStatement := FWExecStatement():New(cQuery) 

    self:oStatement:SetString(nParamOrder++, FWxFilial("CTT"))
    self:oStatement:SetString(nParamOrder++, MV_PAR01)
    self:oStatement:SetString(nParamOrder++, MV_PAR02)
    if lBloqueado
        self:oStatement:SetString(nParamOrder++, '1')
    EndIf
	self:oStatement:SetString(nParamOrder++, Space(1))
    
    self:oStatement:GetFixQuery()

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    Adiciona novos campos ao schema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class CostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que ser? consumido pelo M.I.
    AJusta o Json conforme altera??o no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class CostCenterSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que ser? consumido pelo M.I.
    Ser? usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class CostCenterSmartViewBusinessObject
return ::jItens
