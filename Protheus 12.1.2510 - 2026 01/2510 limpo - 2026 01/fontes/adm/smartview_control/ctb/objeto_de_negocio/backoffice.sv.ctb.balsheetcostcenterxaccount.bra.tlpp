#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balsheetcostcenterxaccount.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CTT", name="Balancete Centro de Custos x Contas", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetCostCenterxAccountSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Centro de Custos x Contas 
para o TReports
 
@author Controladoria
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceSheetCostCenterxAccountSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    //usados na internacionalização
    public method preData()                as object
    public method preSchema()              as object
    public method processData()            as json
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp()
    public method vldExeCtgGerPlan()
    public method processCustomData()       as json

    protected method somaTotal()            as numeric
    protected method RetValorCtb()          as character

    protected data jDados       as json
    protected data nTotCrd      as numeric
    protected data nTotDeb      as numeric
    protected data nTotMov      as numeric
    protected data nTotSldAnt   as numeric
    protected data nTotSldAtu   as numeric
    protected data nGrupoOrdAnt as numeric
    protected data cCustoAnt    as character
    protected data cValorCtb    as character
    protected data cMvMasc      as character
    protected data cMvCus       as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Controladoria
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //str0001 - "Balancete Centro de Custos x Contas"
    self:setPergunte("CTR180") //Indica o pergunte que será utilizado no relatório
    self:setIsLookUp(.T.)

    self:cCustoAnt  := ""
    self:nTotSldAnt := 0
    self:nTotSldAtu := 0
    self:nTotCrd    := 0
    self:nTotDeb    := 0
    self:nTotMov    := 0
    self:cMvMasc    := SuperGetMv("MV_MASCARA")
    self:cMvCus     := SuperGetMV("MV_MASCCUS")

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since 07/07/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceSheetCostCenterxAccountSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre os Balancete Centro de Custos x Contas" 

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@author Renan Gremes
@since 09/11/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class BalanceSheetCostCenterxAccountSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} getCtGerPlanSchema
    Metodo getCtGerPlanSchema
    
    @author Controladoria
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getCtGerPlanSchema() as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject

    self:oSchema:addProperty("TOT_MOV"       , STR0003  , "string", STR0003 , "TOT_MOV"      ) //"Total Mov. Acumulado"
    self:oSchema:addProperty("TOT_DEBITO"    , STR0004  , "string", STR0004 , "TOT_DEBITO"   ) //"Total Débito Acumulado"
    self:oSchema:addProperty("TOT_CREDITO"   , STR0005  , "string", STR0005 , "TOT_CREDITO"  ) //"Total Crédito Acumulado"
    self:oSchema:addProperty("TOT_SALDOANT"  , STR0006  , "string", STR0006 , "TOT_SALDOANT" ) //"Total Saldo Anterior"    
    self:oSchema:addProperty("TOT_SALDOATU"  , STR0007  , "string", STR0007 , "TOT_SALDOATU" ) //"Total Saldo Atual" 
    self:oSchema:addProperty("NomeEmpresa"   , STR0008  , "string", STR0008 , "NomeEmpresa"  ) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    , STR0009  , "string", STR0009 , "NomeFilial"   ) //"Nome Filial"
    
    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0010 , "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getCtgGerPlanData
    Metodo getCtgGerPlanData
    
    @author Controladoria
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR180")
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} handleExeCtgGerPlan
    Metodo handleExeCtgGerPlan
    
    @author Controladoria
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} handleCtgGerPlanAppend
    Metodo handleCtgGerPlanAppend
    
    @author Controladoria
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetCostCenterxAccountSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Utilizado antes do append no data
 
@return object: self:oData
 
@author Ewerton Franklin
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processCustomData() as json class BalanceSheetCostCenterxAccountSmartViewBusinessObject
    
    If cArqTmp->TIPOCONTA == "2"   
		If ::cCustoAnt == cArqTmp->CUSTO         		    
			If cArqTmp->TIPOCC == "2"
				::nTotSldAnt	+= cArqTmp->SALDOANT  
				::nTotDeb += cArqTmp->SALDODEB	     
				::nTotCrd += cArqTmp->SALDOCRD	     
		    	::nTotSldAtu += cArqTmp->SALDOATU  
		    Endif
		Else    	    
    	    If cArqTmp->TIPOCC == "2"
	    	    ::nTotSldAnt	+= cArqTmp->SALDOANT  
				::nTotDeb += cArqTmp->SALDODEB	     
				::nTotCrd += cArqTmp->SALDOCRD	     
		    	::nTotSldAtu += cArqTmp->SALDOATU  
            Endif 
		Endif
	
	Endif

     self:nTotMov := (self:nTotCrd - self:nTotDeb)
    //Adiciona totais no data
    ::jDados["TOT_DEBITO" ]  := self:RetValorCtb(self:nTotDeb) 
    ::jDados["TOT_CREDITO"]  := self:RetValorCtb(self:nTotCrd) 
    ::jDados["TOT_SALDOATU"] := self:RetValorCtb(self:nTotSldAtu)
    ::jDados["TOT_SALDOANT"] := self:RetValorCtb(self:nTotSldAnt)
    ::jDados["TOT_MOV" ]     := self:RetValorCtb(self:nTotMov)  
   
    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := ::cCompanyName
    ::jDados["NomeFilial" ] := ::cBranchName

    ::cCustoAnt := cArqTmp->CUSTO

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Método para validar se deve executar a chamada da funcao CtGerPlan()
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Bruno Oliveira
@since 16/04/2024
@version 1.0
*/
//-------------------------------------------------------------------

method vldExeCtgGerPlan() class BalanceSheetCostCenterxAccountSmartViewBusinessObject
    Local lRet := .T. as Logical
    Local aCtbMoeda := {} as Array

    if !Empty( mv_par08 )
    	lRet := VdSetOfBook( mv_par08 , .F. ) // codigo do livro , visao gerencial
	endif

    //Valida se a moeda é valida!
    aCtbMoeda  	:= CtbMoeda(mv_par10,self:nDivide)
    If Empty(aCtbMoeda[1])
        lRet := .F.
    Endif
Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Bruno Oliveira
@since 16/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp(cArqTmp As character) class BalanceSheetCostCenterxAccountSmartViewBusinessObject
   
    Local lRet          := .T. as Logical
    Local lVlrZerado	:= Iif(mv_par09==1,.T.,.F.) as Logical 

    Local cSepara1		:= "" as Character
    Local cSepara2		:= "" as Character
    Local aSetOfBook    := CTBSetOf(mv_par08) as Array

    Local cMascara1		:= IIF (Empty(aSetOfBook[2]),::cMvMasc,RetMasCtb(aSetOfBook[2],@cSepara1)) as Character//Mascara da Conta
    Local cMascara2		:= IIF (Empty(aSetOfBook[6]),::cMvCus ,RetMasCtb(aSetOfBook[6],@cSepara2)) as Character//Mascara do Centro de Custo
    Local nDigitAte     := 0 as Numeric
    Local nDigCCAte     := 0 as Numeric

    If mv_par33 == 1					// So imprime Sinteticas
        lRet := cArqTmp->TIPOCC <> "2"
    ElseIf mv_par33 == 2				// So imprime Analiticas
        lRet := cArqTmp->TIPOCC <> "1"
    EndIf

    IF ( MV_PAR07 == 3 .Or. ( ( MV_PAR07 == 1 .And. cArqTmp->TIPOCONTA == '1' ) .Or. ( MV_PAR07 == 2 .And. cArqTmp->TIPOCONTA == '2' ) ) )
        If !lVlrZerado .AND. cArqTmp->SALDOANT == 0 .AND. cArqTmp->SALDODEB == 0 .AND. cArqTmp->SALDOCRD == 0 .AND. cArqTmp->SALDOATU == 0 ; lRet := .F. ; Endif
    Endif

    //Filtragem ate o Segmento da Conta( antigo nivel do SIGACON)
    If !Empty(mv_par14)
        nDigitAte := CtbRelDig(mv_par14,cMascara1) 	
        If Len(Alltrim(cArqTmp->CONTA)) > nDigitAte
            lRet := .T.
        else
             lRet := .F.      
        Endif
    EndIf

    //Filtragem ate o Segmento do CC( antigo nivel do SIGACON)
    If !Empty(mv_par28)
        nDigCCAte := CtbRelDig(mv_par28,cMascara2) 	
        lRet := iif(Len(Alltrim(cArqTmp->CUSTO)) > nDigCCAte, .T., .F.)        
    EndIf
    
return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} handleCtgGerPlanParams
    Metodo handleCtgGerPlanParams
    
    @author Controladoria
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter as object) class BalanceSheetCostCenterxAccountSmartViewBusinessObject
local lMov           as logical
local lImpAntLP      as logical
local aSetOfBook     as array
local cContaIni      as character
local cContaFim      as character
local dDataIni       as date
local dDataFim       as date
local dDataLP        as date
local cCusIni        as character
local cCusFim        as character
local cMoeda  :="01" as character
local cTpsald :="1"  as character
local cFilSeg        as character
local cConteudIni    as character
local cConteudFim    as character
local cConteudCont   as character
local lVlrZerado     as logical
local aSelFil        as array
local lPrintZero     as logical
local lCttSint       as logical
local lRecDesp0		 as logical 
local cRecDesp		 as character 
local dDtZeraRD		 as date
local nDivide		 as numeric
local nGrupo		 as numeric
    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    ::oIntegratedProvider:setStep(10000) //Seta o step da paginação

    dDataIni    := MV_PAR01                                                         //  	Data Inicial ?                	      	               	               	               
    dDataFim    := MV_PAR02                                                         //  	Data Final ?                  	      	               	               	               
    cContaIni   := MV_PAR03                                                         //  	Conta Inicial ?               	CT1   	               	               	               
    cContaFim   := MV_PAR04                                                         //  	Conta Final ?                 	CT1   	               	               	               
    cCusIni     := MV_PAR05                                                         //  	Do Centro de Custo ?          	CTT   	               	               	               
    cCusFim     := MV_PAR06                                                         //  	Ate Centro de Custo ?         	CTT   	               	               	               
    aSetOfBook  := iIf(!Empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf())              //  	Cod. Config. Livros ?         	CTN   	               	               	               
    lVlrZerado  := iIf(MV_PAR09==1,.T.,.F.)                                         //  	Saldos Zerados ?              	      	Sim            	Nao            	               
    cMoeda      := Iif(Empty(MV_PAR10),cMoeda,MV_PAR10)                             //  	Moeda ?                       	CTO   	               	               	               
    cTpsald     := Iif(Empty(MV_PAR12),cTpsald,PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]))                            //  	Tipo de Saldo ?               	SLD   	               	               	               
    nGrupo      := MV_PAR13
    cFilSeg     := MV_PAR15                                                         //    	Filtra Segmento No. ?         
    cConteudIni := MV_PAR16                                                         //    	Conteudo Ini Segmen ?         
    cConteudFim := MV_PAR17                                                         //    	Conteudo Fim Segmen ?         
    cConteudCont:= MV_PAR18                                                         //    	Conteudo Contido em ?         
    lMov        := iIf(MV_PAR19==1,.T.,.F.)                                         //  	Imprime Coluna Mov. ?         	      	Sim            	Nao            	               
    lPrintZero  := iIf(MV_PAR22==1,.T.,.F.)                                         //  	Imprime Valor 0.00 ?          	      	Sim            	Nao            	               
    nDivide     := MV_PAR24 	                                                    //      Divide por ?
	lImpAntLP   := iIf(MV_PAR26==1,.T.,.F.)                                         //     	Posicao Ant. L/P ?            	      	Sim            	Nao            	               
    dDataLP     := MV_PAR27                                                         //     	Data Lucros/Perdas ?          	CTZ   	               	               	               
    lCttSint    := Iif(MV_PAR33 == 1 .or. MV_PAR33 == 3,.T.,.F.)
    lRecDesp0	:= Iif(MV_PAR34==1,.T.,.F.)                                         //     	Ignora Sl Ant.Rec/Des ?       	      	Sim            	Nao
    cRecDesp	:= MV_PAR35                                                         //     	Grupo Receitas/Desp. ?        	      	               	   
    dDtZeraRD	:= MV_PAR36                                                         //     	Data Sld Ant.Rec/Desp ?       	      	               	                                                           

    //Setando os atributos
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT3"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cCCIni         := cCusIni
    self:cCCFim         := cCusFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := !lMov
    self:lImpConta      := .T.
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lVlrZerado
    self:nGrupo         := nGrupo
    self:cHeader        := "CTT"
    self:lRecDesp0      := lRecDesp0
    self:cRecDesp       := cRecDesp 
    self:dDtZeraRD      := dDtZeraRD
    self:aSelFil        := aSelFil
    self:lCttSint       := lCttSint
    self:lPrintZero     := lPrintZero
    self:lPageControl   := .T.
   
return 

//-------------------------------------------------------------------
/*{Protheus.doc} RetValorCtb
Retorna Mascara

@return character

@author Ewerton Franklin
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method RetValorCtb(nValCtb as numeric) as character class BalanceSheetCostCenterxAccountSmartViewBusinessObject

Local cPicture as character
Local TAM_VALOR := 17 as numeric
Local nDecimais as numeric
nDecimais := DecimalCTB(::aSetOfBook,mv_par10)
cPicture := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

DEFAULT nValCtb := 0

::cValorCtb := "0"

If nValCtb <=  0
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
Else
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
EndIf

Return ::cValorCtb
