#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.ledger.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CTT, CTD, CTH", name="Razão", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ReferentialChartofAccountsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Razão para o TReports
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------------------

class LedgerSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbgerraz
    
    public method new()                as object
    public method getAreas()           as array
    public method getDescription()     as character

    public method preData()            as object
    public method preSchema()          as object
    public method processData()        as json

    public method processCustomData()  as json
    public method getCtGerRazSchema()  as object
    public method getCtgGerRazData()   as object
    public method handleCtgGerRazParams()

    protected method getSaldos()

    protected data nSaldoAtu as numeric
    protected data lSldAntCC as logical
    protected data lSldAntIt as logical
    protected data lSldAntCv as logical
    protected data aSaldo    as array
    protected data aSaldoAnt as array
    protected data jDados    as json
    protected data _oQryImpH as character


endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class LedgerSmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // "Razao"
    self:setPergunte("CTR400")   // Indica o pergunte que será utilizado no relatório

    ::nSaldoAtu     := 0
    ::lSldAntCC     := .F.
    ::lSldAntIt     := .F.
    ::lSldAntCv     := .F.
    ::aSaldo        := {}
    ::aSaldoAnt     := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class LedgerSmartViewBusinessObject
return STR0002 // "Objeto contendo informacoes sobre o Razao Contabil"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class LedgerSmartViewBusinessObject
return {STR0003} // "Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Renan Gremes
@since 31/12/2024
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class LedgerSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class LedgerSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class LedgerSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 10/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method processCustomData() as json class LedgerSmartViewBusinessObject
    local cNormal   := ""  as character
    local cPicture 	:= ::aSetOfBook[4] as character    
    local nDecimais := iif(DecimalCTB(::aSetOfBook,::cMoeda) == 0, SuperGetMv("MV_CENT"), DecimalCTB(::aSetOfBook,::cMoeda)) as numeric
    local nSaldoAnt := 0   as numeric
    local TAM_VALOR := 20  as numeric

    //Se trocou conta, pega saldo
    if cArqTmp->CONTA <> ::cContaAnt
        self:getSaldos()
        ::nSaldoAtu := 0
        ::nSaldoAtu := ::aSaldoAnt[6]
        nSaldoAnt   := ::aSaldo[6]
    endif
    ::nSaldoAtu := ::nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD
   
    //Manipulação de conteúdo do json
    ::jDados["SALDOANT"] := nSaldoAnt
    ::jDados["TPSLDANT"] := RZValorCTB(nSaldoAnt,,,TAM_VALOR,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)
    ::jDados["SALDOSCR"] := ::nSaldoAtu
    ::jDados["TPSLDATU"] := RZValorCTB(::nSaldoAtu,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cNormal,,,,,,::lPrintZero,.F.,,,.F.)

return

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerRazSchema
Método chamada antes do getSchema do objeto principal
 
@return object: self:oSchema
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerRazSchema() as object class LedgerSmartViewBusinessObject
    self:oSchema:addProperty("SALDOANT", STR0004, "number", STR0004, "SALDOANT") // Saldo Anterior

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0005, "string", .T.) //str000
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)    
    
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerRazData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerRazData(nPage as numeric, oFilter as object) as object class LedgerSmartViewBusinessObject

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR400")

    self:handleCtgGerRazParams(oFilter)
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazParams
Atribui valores aos atributos da classe principal

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 31/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerRazParams(oFilter as object) class LedgerSmartViewBusinessObject

    local cContaIni      as character
    local cContaFim      as character
    local cCustoIni      as character
    local cCustoFim      as character
    local cItemIni       as character
    local cItemFim       as character
    local cCLVLIni       as character
    local cCLVLFim       as character
    local cMoeda := "01" as character
    local cSaldo := "1"  as character
    local cMoedaDesc     as character
    local dDataIni       as date
    local dDataFim       as date
    local aSetOfBook     as array
    local aSelFil        as array
    local lNoMov         as logical
    local lJunta         as logical
    local lAnalit        as logical
    local lSldAnt        as logical
    local nCodConta      as numeric
    local lClVlRes       as logical
    local lCCRes         as logical
    local lItemRes       as logical

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    if Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    endif

    cContaIni     := MV_PAR01                    // Da Conta ?
    cContaFim     := MV_PAR02                    // Ate Conta ?
    dDataIni      := MV_PAR03                    // Da Data ?
    dDataFim      := MV_PAR04                    // Ate a Data ?
    cMoeda        := iif(empty(MV_PAR05),cMoeda,MV_PAR05) // Moeda ?
    cSaldo        := iif(empty(MV_PAR06),cSaldo,MV_PAR06) // Tipo de Saldo ?
    aSetOfBook    := iif(empty(MV_PAR07),CTBSetOf(""),CTBSetOf(MV_PAR07)) // Cod. Config. Livros ?
    lAnalit       := iif(MV_PAR08==1,.T.,.F.)    // Tipo Relatorio ?
    lNoMov        := iif(MV_PAR09==1,.T.,.F.)    // Impr. Cta S/ Movim ?
    lSldAnt       := iif(MV_PAR09==3,.T.,.F.)    // Impr. Cta S/ Movim ?
    lJunta		  := iif(MV_PAR10==1,.T.,.F.)    // Junta C.C. Contabil ?
    nCodConta     := MV_PAR11                    // Impr Cod Conta ? 1-Normal; 2-Reduzido; 3-Impressao 
    cCustoIni     := MV_PAR13                    // Do Centro de Custo ?
    cCustoFim     := MV_PAR14                    // Ate Centro de Custo ?
    cItemIni      := MV_PAR16                    // Do Item Contabil ?
    cItemFim      := MV_PAR17                    // Ate Item Contabil ?
    cCLVLIni      := MV_PAR19                    // Da Classe de Valor ?
    cCLVLFim      := MV_PAR20                    // Ate Classe de Valor ?
    lCCRes        := iif(MV_PAR25==2,.T.,.F.)    // Impr Cod C.Custo ? 1-Normal; 2-Reduzido
    lItemRes      := iif(MV_PAR26==2,.T.,.F.)    // Impr Cod Item ? 1-Normal; 2-Reduzido
    lClVlRes      := iif(MV_PAR27==2,.T.,.F.)    // Impr Cod Cl.Valor ? 1-Normal; 2-Reduzido
    lPrintZero    := iif(MV_PAR30==1,.T.,.F.)    // Imprime Valor 0,00 ?
    ::lSldAntCC	  := iif(MV_PAR33==2,.T.,.F.)    // Saldo Ant. nivel?Cta/C.C/Item/Cl.Vlr
    ::lSldAntIt   := iif(MV_PAR33==3,.T.,.F.)    // Saldo Ant. nivel?Cta/C.C/Item/Cl.Vlr
    ::lSldAntCv   := iif(MV_PAR33==4,.T.,.F.)    // Saldo Ant. nivel?Cta/C.C/Item/Cl.Vlr
    cMoedaDesc    := iif(Empty(MV_PAR34),cMoeda,MV_PAR34) // Moeda Descricao ?

    MV_PAR39      := " "                         // Range de Conta ?
    MV_PAR40      := " "                         // Range de Centro de Custo ?
    MV_PAR41      := " "                         // Range de Item Contabil ?
    MV_PAR42      := " "                         // Range de Classe de Valor ?

    ::cContaIni    := cContaIni
    ::cContaFim    := cContaFim
    ::cCustoIni    := cCustoIni
    ::cCustoFim    := cCustoFim
    ::cItemIni     := cItemIni
    ::cItemFim     := cItemFim
    ::cCLVLIni     := cCLVLIni
    ::cCLVLFim     := cCLVLFim
    ::cMoeda       := cMoeda
    ::dDataIni     := dDataIni
    ::dDataFim     := dDataFim
    ::aSetOfBook   := aSetOfBook
    ::lNoMov       := lNoMov
    ::cSaldo       := cSaldo
    ::lJunta       := lJunta
    ::lAnalit      := lAnalit
    ::lSldAnt      := lSldAnt
    ::aSelFil      := aSelFil
    ::cTipo        := "1" // Razao por Conta
    ::lPrintZero   := lPrintZero
    ::nCodConta    := nCodConta
    ::lClVlRes     := lClVlRes
    ::lCCRes       := lCCRes
    ::lItemRes     := lItemRes
    ::cMoedaDesc   := cMoedaDesc

return

//-------------------------------------------------------------------
/*{Protheus.doc} getSaldos
Método para carregar saldos
 
@return object: string
 
@author Renan Gremes
@since 16/09/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method getSaldos() class LedgerSmartViewBusinessObject    
    Local lTodasFil := .F. as logical

    If ::lSldAntCC
		::aSaldo    := SaldTotCT3(::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
		::aSaldoAnt := SaldTotCT3(::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,::cMoeda,::cSaldo,::aSelFil)
	ElseIf ::lSldAntIt
		::aSaldo    := SaldTotCT4(::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
		::aSaldoAnt := SaldTotCT4(::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,::cMoeda,::cSaldo,::aSelFil)
	ElseIf ::lSldAntCv
		::aSaldo    := SaldTotCTI(::cClVlIni,::cClVlFim,::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
		::aSaldoAnt := SaldTotCTI(::cClVlIni,::cClVlFim,::cItemIni,::cItemFim,::cCustoIni,::cCustoFim,cArqTmp->CONTA,cArqTmp->CONTA,::dDataIni,::cMoeda,::cSaldo,::aSelFil)
	Else        
		::aSaldo 	:= SaldoCT7Fil(cArqTmp->CONTA,cArqTmp->DATAL,::cMoeda,::cSaldo,,,,::aSelFil,,lTodasFil)
		::aSaldoAnt	:= SaldoCT7Fil(cArqTmp->CONTA,::dDataIni,::cMoeda,::cSaldo,"CTBR400",,,::aSelFil,,lTodasFil)
	EndIf

return
