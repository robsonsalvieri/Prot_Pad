#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.movecostcxaccount12months.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT3, CT3", name="Mov.cc X Cta 12mes", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} MoveCostCxAccount12MonthsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Centro Custo X Conta 12 meses
para o SmartView
 
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class MoveCostCxAccount12MonthsSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtGerComp
    public method new()                   as object 
    public method getAreas()              as array
    public method getDescription()        as Character

    public method preData()               as object
    public method preSchema()             as object
    public method processData()           as json

    public method getCtGerCompData()      as object  //Carregado na classe pai
    public method getCtGerCompSchema()    as object // Carregado na classe pai

    public method handleCtGerCompParams() //carrega os parametros
    public method handleCtGerCompAppend()  as object    //Executado na classe pai
    public method handleExeCtGerComp()     as object    //Executado na classe pai

Endclass

//-------------------------------------------------------------------------------
/*{Protheus.doc} New
Método de instância da classe
 
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

method new() as object class MoveCostCxAccount12MonthsSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0001) //- STR0001 "Mov.cc X Cta 12mes"
    self:setPergunte("CTR285") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de getDescription da classe
 @return object: string
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

method getDescription() as Character class MoveCostCxAccount12MonthsSmartViewBusinessObject
return STR0002  //"Objeto contendo informações sobre o relatorio de Comparativo de Centro de Custo X Contas em 12 meses" 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Ewerton Franklin
@since 01/03/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class MoveCostCxAccount12MonthsSmartViewBusinessObject
return {STR0003} // "Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Ewerton Franklin
@since 01/03/2024
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Ewerton Franklin
@since 01/03/2024
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Ewerton Franklin
@since 01/03/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processData() as json class MoveCostCxAccount12MonthsSmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompData
Método chamada antes do getSchema do objeto principal
 
@return object: self:oSchema
 
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerCompData(nPage as numeric, oFilter as object) as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
    self:handleCtGerCompParams( oFilter )
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompSchema
Método chamada do getSchema do objeto principal
 
@return object: self:oSchema
 
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerCompSchema() as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompParams
Método chamada dos parametros do objeto principal
  
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompParams( oFilter as object ) class MoveCostCxAccount12MonthsSmartViewBusinessObject
    
    Local cAlias      := "CT3"                                      AS Character
    Local cIdent      := ""                                         AS Character

    Local dDataIni    := MV_PAR01                                   AS Date
    Local dDataFim    := MV_PAR02                                   AS Date
    Local cCCIni      := MV_PAR03                                   AS Character
    Local cCCFim      := MV_PAR04                                   AS Character
    Local cContaIni   := MV_PAR05                                   AS Character
    Local cContaFim   := MV_PAR06                                   AS Character
    Local lImpSint    := IIf(MV_PAR07 != 2,.T.,.F.)                 AS Logical
    Local lCT1Sint	  := Iif(MV_PAR07 != 2,.T.,.F.)                 AS Logical
    Local aSetOfBook  := IIf(empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf("")) AS Array
    Local lVlrZerado  := iif(MV_PAR09==1,.T.,.F.)                   AS Logical
    Local cMoeda      := iif(Empty(MV_PAR10),"01",MV_PAR10)         AS Character 
    Local cSaldos     := IIf(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), "1")          AS Character
    Local cSegmento   := MV_PAR13                                   AS Character
    Local cSegIni     := MV_PAR15                                   AS Character
    Local cSegFim     := MV_PAR16                                   AS Character
    Local cFiltSegm   := MV_PAR17                                   AS Character
    local lPrintZero  := Iif(MV_PAR22==1, .T.,.F.)                  AS Logical
    Local nDivide     := IIF(Empty(MV_PAR23),1,MV_PAR23)            AS Numeric
    Local lImpAntLP   := Iif(MV_PAR24==1,.T.,.F.)                   AS Logical
    Local dDataLP     := MV_PAR25                                   AS Date
       
    Local lCTTSint    := Iif(MV_PAR28 != 2,.T.,.F.)                 AS Logical
    Local lImpTotS    := Iif(MV_PAR29 == 1,.T.,.F.)                 AS Logical 
    Local cTpVlr      := IIF(MV_PAR30 == 1,"M","S" )                AS Character

    //Variaveis fixas no relatorio 
    Local lMeses      := .T.                                        AS logical
    Local cHeader     := "CTT"                                      AS Character
    Local lNImpMov    := .F.                                        AS Logical
    Local lImpTotSA   := (lImpTotS .And. (lCT1Sint .Or. lCTTSint))  AS Logical
    Local cString     := "CTT"                                      AS Character

    Local lExercicio  :=  .T.                                       AS Logical
    Local lZeradas    :=  .F.                                       AS Logical

    Local aPeriodos := {} as array
    Local aMeses    := {} as array
    local nX        := 0  as numeric
    local nMeses    := 1  as numeric    

    aPeriodos := ctbPeriodos(MV_PAR10, MV_PAR01, MV_PAR02, .T., .F.)

    For nX := 1 to len(aPeriodos)
        //Se a Data do periodo eh maior ou igual a data inicial solicitada no relatorio.
        If aPeriodos[nX][1] >= MV_PAR01 .And. aPeriodos[nX][2] <= MV_PAR02 
            If nMeses <= 12
                AADD(aMeses,{StrZero(nMeses,2),aPeriodos[nX][1],aPeriodos[nX][2]})	
            EndIf
            nMeses += 1
        EndIf
    Next nX
        
    //setar os parametros da classe CtGerComp conforme os MV_PARxx
    self:cAlias      := cAlias
    self:cIdent      := cIdent
    self:dDataIni    := dDataIni
    self:dDataFim    := dDataFim
    self:cCCIni      := cCCIni
    self:cCCFim      := cCCFim
    self:cContaIni   := cContaIni
    self:cContaFim   := cContaFim
    self:lImpSint    := lImpSint
    self:aSetOfBook  := aSetOfBook
    self:cMoeda      := cMoeda
    self:lVlrZerado  := lVlrZerado
    self:cSaldos     := cSaldos
    self:cSegmento   := cSegmento
    self:cSegIni     := cSegIni
    self:cSegFim     := cSegFim
    self:cFiltSegm   := cFiltSegm
    self:lPrintZero  := lPrintZero
    self:nDivide     := nDivide
    self:lImpAntLP   := lImpAntLP
    self:dDataLP     := dDataLP
    self:cTpVlr      := cTpVlr

    self:lMeses      := lMeses
    self:aMeses      := aMeses
    self:cHeader     := cHeader
    self:lNImpMov    := lNImpMov
    self:lImpTotS    := lImpTotSA
    self:cString     := cString

    //Utilizadas no metodo CTBPERIODOS para preencher o aMeses, verificar conforme relatorio seu valor
    self:lExercicio :=  lExercicio
    self:lZeradas   :=  lZeradas
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompAppend
Método chamada append do objeto principal
  
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtGerCompAppend(oFilter as object) as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
//execucao do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtGerComp
Método chamada gercomp
  
@author Controladoria
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtGerComp(oFilter as object) as object class MoveCostCxAccount12MonthsSmartViewBusinessObject
//execucao da CtGerComp
return self:oData

