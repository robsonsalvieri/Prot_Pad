#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancecostcenter.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTU, CTT", name="Balancete Centro de Custo", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceCostCenterSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Centro de Custo
para o TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceCostCenterSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                    as object 
    public method getAreas()               as array
    public method getDescription()         as Character
    public method getCtgGerPlanData()      as object
    public method getCtGerPlanSchema()     as object

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend() as object
    public method handleExeCtgGerPlan()    as object

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object    

    public method ExecDBSkipTemp()
    public method processCustomData()       as json

    protected method somaTotal()            as numeric

    protected data jDados  as json
    protected data nTotCrd as numeric
    protected data nTotDeb as numeric
    protected data nTotMov as numeric
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceCostCenterSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete de Centro Custo"
    self:setPergunte("CTR200") //Indica o pergunte que será utilizado no relatório

    self:nTotCrd := 0
    self:nTotDeb := 0
    self:nTotMov := 0

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceCostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Centro Custo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BalanceCostCenterSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class BalanceCostCenterSmartViewBusinessObject

    self:oSchema:addProperty("TOT_MOV"       ,STR0007 , "string", STR0007 , "TOT_MOV"      ) //"Total Mov. Acumulado"
    self:oSchema:addProperty("TOT_DEBITO"    ,STR0008 , "string", STR0008 , "TOT_DEBITO"   ) //"Total Débito Acumulado"
    self:oSchema:addProperty("TOT_CREDITO"   ,STR0009 , "string", STR0009 , "TOT_CREDITO"  ) //"Total Crédito Acumulado"    
    self:oSchema:addProperty("NomeEmpresa"   ,STR0004 , "string", STR0004 , "NomeEmpresa"  ) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    ,STR0005 , "string", STR0005 , "NomeFilial"   ) //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0006 , "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceCostCenterSmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceCostCenterSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceCostCenterSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(),self:cPergunte)
    self:handleCtgGerPlanParams( oFilter )
    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceCostCenterSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    local cCCIni            as character
    local cCCFim            as character
    Local nDivide := 1      as numeric
    local lMov              as logical
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date

    dDataIni    := MV_PAR01                                 //Data Inicial ?
    dDataFim    := MV_PAR02                                 //Data Final ?
    cContaIni   := Space(Len(CriaVar("CT1_CONTA")))         //Conta Inicial ?
    cContaFim   := Repl('Z',Len(CriaVar("CT1_CONTA")))      //Conta Final ?
    cCCIni      := MV_PAR03                                 //Do Centro de Custo ?
    cCCFim      := MV_PAR04                                 //Ate o Centro de Custo ?
    lImpSint    := iif(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.) //Imprime Contas ?
    aSetOfBook  := iif(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf("")) //Cod. Config. Livros ?
    lSldZerado  := iif(MV_PAR07==1,.T.,.F.)                 //Saldos Zerados
    cMoeda      := iif(!empty(MV_PAR08),MV_PAR08,cMoeda)    //Moeda
    cTpsald     := MV_PAR10                                 //Tipo de Saldo
    cFilSeg     := MV_PAR12                                 //Filtra Segmento No. ?
    cConteudIni := MV_PAR13                                 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR14                                 //Conteudo Fim Segmen ?
    cConteudCont:= MV_PAR15                                 //Conteudo Contido em ?
    lMov        := !(MV_PAR16==1)                           //Imprime Coluna Movimento ?
    lPrintZero  := iif(MV_PAR18==1,.T.,.F.)                 //Imprime Valor 0.00  ?
    nDivide     := iif(empty(MV_PAR20),nDivide,MV_PAR20)
    lImpAntLP   := iif(MV_PAR21 == 1,.T.,.F.)               //Posição Ant. L/P?
    dDataLP     := MV_PAR22                                 //Data Lucros/Perdas?

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cCCIni         := cCCIni  
    self:cCCFim         := cCCFim  
    self:cAlias         := "CTU" //são obrigatórios
    self:cIdent         := "CTT" //são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := lMov
    self:lPrintZero     := lPrintZero
    self:lImpConta      := .F.
    self:aGeren         := nil
    
return 

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Se pula o registro da  tabela  cArqTemp
 
@return Lógico
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method ExecDBSkipTemp() class BalanceCostCenterSmartViewBusinessObject
    
    Local lRet := .T.

	If MV_PAR05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCC == "2"
			lRet := .F.
		EndIf
	EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Usado para appendar dados ao schema. Chamado no GetData da classe pai

@return object: self:oData

@author Renan Gremes
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method processCustomData() as json class BalanceCostCenterSmartViewBusinessObject

    Local cPicture as character
    Local TAM_VALOR := 17 as numeric
    Local nDecimais as numeric

    nDecimais := DecimalCTB(::aSetOfBook,mv_par08)
    cPicture := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

    self:nTotCrd += self:somaTotal("D") //Total Débito Acumulado
	self:nTotDeb += self:somaTotal("C") //Total Crédito Acumulado
    self:nTotMov := (self:nTotCrd - self:nTotDeb) //Total Mov. Acumulado

    //Adiciona totais no data
    ::jDados["TOT_DEBITO" ] := ValorCTB(self:nTotDeb,,,TAM_VALOR,nDecimais,.F.,cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["TOT_CREDITO"] := ValorCTB(self:nTotCrd,,,TAM_VALOR,nDecimais,.F.,cPicture,"2",,,,,,::lPrintZero,.F.,.F.)

    If Round(NoRound(self:nTotMov,3),2) <= 0
        ::jDados["TOT_MOV" ] := ValorCTB(self:nTotMov,,,TAM_VALOR,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
    ElseIf Round(NoRound(self:nTotMov,3),2) > 0
        ::jDados["TOT_MOV" ] := ValorCTB(self:nTotMov,,,TAM_VALOR,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
    EndIf

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} somaTotal
Soma lançamentos crédito e débito

@return numerico

@author Renan Gremes
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method somaTotal(cTipo as character) as numeric class BalanceCostCenterSmartViewBusinessObject

    Local nRetValor	:= 0 as numeric

	If MV_PAR05 == 1 // So imprime Sinteticas - Soma Sinteticas
		If cArqTmp->TIPOCC == "1" .And. cArqTmp->NIVEL1
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	Else // Soma Analiticas	
		If cArqTmp->TIPOCC == "2"
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	EndIf

return nRetValor  

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceCostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceCostCenterSmartViewBusinessObject 
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method processData() as json class BalanceCostCenterSmartViewBusinessObject
return ::jDados
