#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.analyticalsyntheticbalancesheetmod1.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CQ1, CT7, CTO, CTZ, CTN", name="Balancete Analitico Sintetico Modelo 1", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Analitico Sintetico Modelo 1
para o TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method ExecDBSkipTemp()          as logical
    
    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object
    
    public method processCustomData()       as json
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    protected method somaTotal()            as numeric

    protected data jDados  as json
    protected data nTotCrd as numeric
    protected data nTotDeb as numeric
    protected data nTotMov as numeric
    protected data nGrupoOrdAnt as numeric

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Balancete Analitico Sintetico Modelo 1"
    // Indica o pergunte que será utilizado no relatório
    self:setPergunte("CTR040")	
    self:nGrupoOrdAnt := 0
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete Analitico Sintetico Modelo 1"

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Utilizado antes do append no data
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processCustomData() as json class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
    
    Local cPicture as character
    Local TAM_VALOR := 17 as numeric
    Local nDecimais as numeric
    nDecimais := DecimalCTB(::aSetOfBook,mv_par08)
    cPicture := Iif(!Empty(::aSetOfBook[4]),::aSetOfBook[4],"@E 999,999,999,999,999.99")

    If ::nGrupo == 1
        If ::jDados["GRUPOORDEM"]  <> self:nGrupoOrdAnt
            self:nTotCrd := 0
            self:nTotDeb := 0
            self:nTotMov := 0
        EndIf
    EndIf

    self:nTotCrd += self:somaTotal("D") //Total Débito Acumulado
	self:nTotDeb += self:somaTotal("C") //Total Crédito Acumulado
    self:nTotMov := (self:nTotCrd - self:nTotDeb) //Total Mov. Acumulado

    //Adiciona totais no data
    ::jDados["TOT_DEBITO" ] := ValorCTB(self:nTotDeb,,,TAM_VALOR,nDecimais,.F.,cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["TOT_CREDITO"] := ValorCTB(self:nTotCrd,,,TAM_VALOR,nDecimais,.F.,cPicture,"2",,,,,,::lPrintZero,.F.,.F.)

    If Round(NoRound(self:nTotMov,3),2) <= 0
        ::jDados["TOT_MOV" ] := ValorCTB(self:nTotMov,,,TAM_VALOR,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
    ElseIf Round(NoRound(self:nTotMov,3),2) > 0
        ::jDados["TOT_MOV" ] := ValorCTB(self:nTotMov,,,TAM_VALOR,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
    EndIf

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := ::cCompanyName
    ::jDados["NomeFilial" ] := ::cBranchName

    If ::nGrupo == 1
         self:nGrupoOrdAnt := self:jDados["GRUPOORDEM"]
    EndIf
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Valida registros a serem pulados
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp(cArqTmp As character) as logical class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
   
    local lRet := .T. as logical 
    
    if MV_PAR05 == 1        // Apenas sinteticas
        lRet := (cArqTmp->TIPOCONTA == "1")
    elseif MV_PAR05 == 2    // Apenas analiticas
        lRet := (cArqTmp->TIPOCONTA == "2")
    endif

    //Se desconsiderar saldo anterior de contas de receita e despesas e não imprimir zeros
    If cArqTmp->TIPOCONTA == "2"
        If !::lVlrZerado  
            If cArqTmp->SALDOANT == 0 .And. cArqTmp->SALDODEB == 0  .And.  cArqTmp->SALDOCRD == 0 .And. cArqTmp->SALDOATU  == 0
                lRet := .F.
            EndIf
        EndIf
    Else
        If ::lRecDesp0 .And. !::lVlrZerado
        //Se não houver saldo na conta não imprimir a mesma, caso o saldo anterior seja desconsiderado lRecDesp0
            If cArqTmp->SALDOANT == 0 .And. cArqTmp->SALDODEB == 0  .And.  cArqTmp->SALDOCRD == 0 .And. cArqTmp->SALDOATU  == 0
                lRet := .F.
            EndIf
        EndIf
    EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject 

    self:oSchema:addProperty("TOT_MOV"       , STR0006, "string", STR0006, "TOT_MOV"      ) //"Total Mov. Acumulado"
    self:oSchema:addProperty("TOT_DEBITO"    , STR0007, "string", STR0007, "TOT_DEBITO"   ) //"Total Débito Acumulado"
    self:oSchema:addProperty("TOT_CREDITO"   , STR0008, "string", STR0008   , "TOT_CREDITO"  ) //"Total Crédito Acumulado"    
    self:oSchema:addProperty("NomeEmpresa"    ,STR0003 , "string", STR0003 , "NomeEmpresa"  ) // STR0003 - "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"     ,STR0004 , "string", STR0004 , "NomeFilial"   ) //STR0004 - "Nome Filial"
    
    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0005 , "string", .T.)  //STR0005 - "Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)    
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR040")

    self:handleCtgGerPlanParams( oFilter )

    FreeObj(oFilter:getParameters())
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados
@type
@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject

    local cContaIni     as character
    local cContaFim     as character
    local cMoeda        as character
    local cTpsald       as character
    local cRecDesp      as character
    local cMoedaDsc     as character    
    local nDivide       as numeric
    local nGrupo        as numeric    
    local lImpAntLP     as logical
    local lImpSint      as logical
    local lPrintZero    as logical
    local lSldZerado    as logical
    local lPulaSint     as logical
    local lRecDesp0     as logical
    local aSetOfBook    as array
    local aSelFil       as array
    local dDataIni      as date
    local dDataFim      as date
    local dDataLP       as date
    local dDtZeraRD     as date

    //Carrega os valores
    cMoeda      := "01"
    cMoedaDsc   := cMoeda
    cTpsald     := "1"

    //Atribui parâmetros nas variáveis
    dDataIni     := MV_PAR01 // Data Inicial
    dDataFim     := MV_PAR02 // Data Final
    cContaIni    := MV_PAR03 // Conta Inicial
    cContaFim    := MV_PAR04 // Conta Final    
    lImpSint     := IIf(MV_PAR05 == 1 .Or. MV_PAR05 == 3, .T., .F.) // Imprime sinteticas
    aSetOfBook   := IIf(!Empty(MV_PAR06), CTBSetOf(MV_PAR06),   CTBSetOf("")) // Codigo de Configuração dos Livros
    lSldZerado   := IIf(MV_PAR07 == 1, .T., .F.) // Saldos Zerados ?
    cMoeda       := IIf(!Empty(MV_PAR08), MV_PAR08, cMoeda)  // Moeda ?
    cTpsald      := IIf(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo ? 
    nGrupo       := MV_PAR11   
    cFilSeg      := MV_PAR12 // Filtra Segmento No. ?
    cConteudIni  := MV_PAR13 // Conteudo Ini Segmen ?
    cConteudFim  := MV_PAR14 // Conteudo Fim Segmen ?
    cConteudCont := MV_PAR15 // Conteudo Contido em ?    
    lPulaSint    := IIf(MV_PAR17 == 1, .T., .F.) // Salta Linha Sintet. ?
    lPrintZero   := IIf(MV_PAR18 == 1, .T., .F.) // Imprime Valor 0.00  ?
    lImpCod      := IIf(MV_PAR19 == 1, .T., .F.) // Imprime codigo ?

    // Tratamento do parâmetro Divide por ?
    if MV_PAR20 == 1 		// Não se aplica
	    nDivide  := 1
    elseif MV_PAR20 == 2	// Divide por cem
		nDivide  := 100
	elseif MV_PAR20 == 3	// Divide por mil
		nDivide  := 1000
	elseif MV_PAR20 == 4	// Divide por milhao
		nDivide  := 1000000
	endif

    cImprimeSeg  := MV_PAR21 // Imprimir ate o Segmento?
    lImpAntLP    := IIf(MV_PAR22 == 1, .T., .F.) // Posição Ant. L/P?
    dDataLP      := MV_PAR23 // Data Lucros/Perdas?
    lRecDesp0    := IIf(MV_PAR25 == 1, .T., .F.) // Ignora Sl Ant.Rec/Des ?
    cRecDesp     := MV_PAR26 // Grupos Receitas/Despesas ?
    dDtZeraRD    := MV_PAR27 // Data Sld Ant. Receitas/Desp. ?
    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    //Atribui variáveis nos atributos
    ::dDataIni   := dDataIni
    ::dDataFim   := dDataFim
    ::cAlias     := "CT7" // Alias do Arquivo
    ::cIdent     := ""    // Identificador do arquivo a ser processado
    ::cContaIni  := cContaIni
    ::cContaFim  := cContaFim
    ::lImpSint   := lImpSint
    ::aSetOfBook := aSetOfBook
    ::lVlrZerado := lSldZerado
    ::cMoeda     := cMoeda
    ::cSaldos    := cTpsald
    ::nGrupo     := nGrupo
    ::cSegmento  := cFilSeg
    ::cSegIni    := cConteudIni
    ::cSegFim    := cConteudFim
    ::cFiltSegm  := cConteudCont
    ::nDivide    := nDivide    
    ::lImpAntLP  := lImpAntLP
    ::dDataLP    := dDataLP
    ::lRecDesp0  := lRecDesp0
    ::cRecDesp   := cRecDesp
    ::dDtZeraRD  := dDtZeraRD
    ::cMoedaDsc  := cMoedaDsc
    ::aSelFil    := aSelFil
    ::lNImpMov   := .F.
    ::lImpConta  := .F.
    
return 

/*/{Protheus.doc} AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject::preSchema
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preSchema() as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject 
return self:oSchema

/*/{Protheus.doc} AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject::preData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preData() as object class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject 
return self:oData

/*/{Protheus.doc} AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject::processData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method processData() as json class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject
return ::jDados



//-------------------------------------------------------------------
/*{Protheus.doc} somaTotal
Soma lançamentos crédito e débito

@return numerico

@author Renan Gremes
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method somaTotal(cTipo as character) as numeric class AnalyticalSyntheticBalanceSheetMod1SmartViewBusinessObject

    Local nRetValor	:= 0 as numeric

	If MV_PAR05 == 1 // So imprime Sinteticas - Soma Sinteticas
		If cArqTmp->TIPOCONTA  == "1" .And. cArqTmp->NIVEL1
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	Else // Soma Analiticas	
		If cArqTmp->TIPOCONTA  == "2"
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	EndIf

return nRetValor  
