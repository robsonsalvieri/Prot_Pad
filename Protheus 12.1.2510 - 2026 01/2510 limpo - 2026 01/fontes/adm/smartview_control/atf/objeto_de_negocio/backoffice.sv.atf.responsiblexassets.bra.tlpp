#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.responsiblexassets.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1, SND, RD0", name="Responsáveis x Bens", country="ALL", customTables="SN1, SND, RD0")

//-------------------------------------------------------------------------------
/*{Protheus.doc} responsiblexassetsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class responsiblexassetsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json 

    protected method getQuery()     as character
    
    protected data cAlias     as character
    protected data aAllFields as array
    protected data aSelFil    as array
    protected data jItens     as json
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object Class responsiblexassetsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Responsáveis x Bens"
    self:setPergunte("AFR320")   //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class responsiblexassetsSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Responsáveis x Bens"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class responsiblexassetsSmartViewBusinessObject
return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class responsiblexassetsSmartViewBusinessObject

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class responsiblexassetsSmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class responsiblexassetsSmartViewBusinessObject

return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class responsiblexassetsSmartViewBusinessObject

    local cCompanyName  as character
    local cBranchName   as character
    local cDDD          as character
    local cTelefone     as character
    local aPDFields     := {}           as array
    local aCposLGPD     := {"RD0_DDD","RD0_FONE"} as array
    local lObfuscated   := .F.          as logical
    local nX := 0  as numeric

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR320")
    
    //Tratamento LGPD 
    aPDFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aCposLGPD)
    lObfuscated := len(aPDFields) > 0

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SND"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SND"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SND"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SND"]
    EndIf

    cQuery := self:getQuery(oFilter)
    self:cAlias := MPSysOpenQuery(cQuery)

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    while (self:cAlias)->(!Eof())
        ::jItens := JsonObject():new()

        //Ofusca o campo de tratamento LGPD
        if lObfuscated
            cDDD      := AllTrim((self:cAlias)->RD0_DDD)
            cTelefone := AllTrim((self:cAlias)->RD0_FONE)

            cDDD      := FwProtectedDataUtil():ValueAsteriskToAnonymize(cDDD)
            cTelefone := FwProtectedDataUtil():ValueAsteriskToAnonymize(cTelefone)
        endif
        
        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getRealName() == "RD0_DDD" //Trata DDD LGPD
                ::jItens[self:aAllFields[nX]:getName()] := cDDD
            elseif self:aAllFields[nX]:getRealName() == "RD0_FONE" //Trata telefone LGPD
                ::jItens[self:aAllFields[nX]:getName()] := cTelefone
            else
                if self:aAllFields[nX]:getType() == "date"
                    ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
                elseif self:aAllFields[nX]:getType() == "string"
                    ::jItens[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
                else
                    ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
                endif
            endif
		Next nX

        ::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial"]  := cBranchName

        self:processData()     
        self:oData:appendData(::jItens)           
       
        (self:cAlias)->(dbSkip())

        //Reseta variável
        cDDD := ""
        cTelefone := ""
    EndDO

    (self:cAlias)->(dbCloseArea())
      
    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)
    FreeObj(::jItens)
     
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema

@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class responsiblexassetsSmartViewBusinessObject

    local aFieldsSND := {}  as array
    local aFieldsSN1 := {}  as array
    local aFieldsRD0 := {}  as array
    
    aFieldsSND := {"ND_FILIAL","ND_CBASE","ND_ITEM"}

    aFieldsSN1 := {"N1_DESCRIC","N1_CHAPA","N1_LOCAL","N1_QUANTD","N1_DTCLASS","N1_AQUISIC"}

    aFieldsRD0 := {"RD0_CODIGO","RD0_DDD","RD0_FONE","RD0_NOME"}

    self:oSchema:aliasToSchema("SND", aFieldsSND)
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("RD0", aFieldsRD0)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    //Adiciona campos customizados
    self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa") //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial")  //"Nome Filial"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0006, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)   

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    @author Bruno Oliveira
    @since 14/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------

method getQuery(oFilter as Object) as Character class responsiblexassetsSmartViewBusinessObject

    local cQuery      := ""       as character
    local cSpace      := Space(1) as character    
    local nParamOrder := 1        as numeric
    local oStatement as object

    cQuery := "SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
	cQuery += " FROM "+RetSqlName("SND")+" SND "
	cQuery += " LEFT JOIN "+RetSqlName("SN1")+" SN1 ON SN1.N1_FILIAL IN ( ? ) "
	cQuery += " AND SN1.N1_CBASE = SND.ND_CBASE "
	cQuery += " AND SN1.N1_ITEM  = SND.ND_ITEM AND SN1.D_E_L_E_T_= ? "
	cQuery += " LEFT JOIN "+RetSqlName("RD0")+" RD0 ON RD0.RD0_FILIAL = ? "
	cQuery += " AND RD0.RD0_CODIGO = SND.ND_CODRESP AND RD0.D_E_L_E_T_= ? "

    cQuery += " WHERE SND.ND_FILIAL IN ( ? ) "
    cQuery += " AND SND.ND_CODRESP  >= ? "
	cQuery += " AND SND.ND_CODRESP  <= ? "
	cQuery += " AND SND.ND_CBASE    >= ? "
	cQuery += " AND SND.ND_ITEM     >= ? "
	cQuery += " AND SND.ND_CBASE    <= ? "
	cQuery += " AND SND.ND_ITEM     <= ? "
	cQuery += " AND SND.ND_STATUS = ? "
	cQuery += " AND SND.D_E_L_E_T_= ? "

    //Filtro do Usuario
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

	cQuery += " ORDER BY  " +SqlOrder(SND->(IndexKey(1)))
    
    cQuery := ChangeQuery(cQuery)    
    oStatement := FWExecStatement():New(cQuery)  
    
    oStatement:SetIn(    nParamOrder++, ::aSelFil)
    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetString(nParamOrder++, FWxFilial("RD0"))
    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetIn(    nParamOrder++, ::aSelFil)
    oStatement:SetString(nParamOrder++, MV_PAR01 )
    oStatement:SetString(nParamOrder++, MV_PAR02)
    oStatement:SetString(nParamOrder++, MV_PAR03)
    oStatement:SetString(nParamOrder++, MV_PAR04)
    oStatement:SetString(nParamOrder++, MV_PAR05)
    oStatement:SetString(nParamOrder++, MV_PAR06)
    oStatement:SetString(nParamOrder++, "1")
    oStatement:SetString(nParamOrder++, cSpace)

    cQuery := oStatement:GetFixQuery()

return cQuery
