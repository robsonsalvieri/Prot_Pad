#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.entriesbyaccountingitem.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3", name="Lançamento Por Item Contábil", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio do Lançamento por Centro de Custo para o TReports
Fonte/Perg      ATFR190 / ATR190
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
@Revitalização  Renato July - DEZ/2023
*/
//-------------------------------------------------------------------------------
Class entriesbyaccountingitemSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()                 as object
	public method getDescription()      as character
	public method getAreas()            as array
	public method getData()             as object
	public method getSchema()           as object

	//
	public method preschema()           as object
	public method preData()             as object
	public method processData()         as json

	protected data aCamposQry           as array
	protected data jDados               as json
	protected data oQueryN3 as object
	protected data oIntegratedProvider 	as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object class entriesbyaccountingitemSmartViewBusinessObject
	_Super:new()
	self:setDisplayName( STR0001 )  //"Lançamento por Centro de Custo"
	self:setPergunte( "ATR270" )    //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class entriesbyaccountingitemSmartViewBusinessObject
return STR0002 //"Objeto contendo informações das Lançamento por Centro de Custo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@return object: string
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getAreas() as array class entriesbyaccountingitemSmartViewBusinessObject
return { STR0003 } //"Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class entriesbyaccountingitemSmartViewBusinessObject

	Local cQuery        := ""       as character
	Local cWhere        := ""       as character
	Local cChave        := ""       as character
	Local cMoedaTmp     := ""       as character
	Local cMoedasTMp    := ""       as character
	Local cFilAtu 	    := ""       as character
	Local cCustode                  as character
	Local cCustoAte                 as character
	Local cContaDe                  as character
	Local cContaAte                 as character
	Local cCompanyName	:= ""		as Character
    Local cBranchName   := ""		as Character
	Local lSelClas                  as logical
	Local lAtfCusPrv    := .F.      as logical
	Local lSelFil	    := .F.      as logical
	Local lAtClDepr     := .F.      as logical
	Local cMoeda        := AllTrim( SuperGetMV("MV_ATFMOED",.F.,"1") )   as character
	Local dUltDepr 		:= SuperGetMV("MV_ULTDEPR",.F.,"19800101") as date

	Local cEntidade := " " , cConta := " ", cCentroCus:=" "

	Local nParamOrder   := 1   		as Numeric
	Local nTamEmp   	:= 0   		as Numeric

	// Acumuladores de Conta
	Local nCtaVlOri1 := nCtaDpAcm1 := nCtaDpMes1 := nCtaCrDep1 := nCtaCrMon1 := 0
	Local nCtaVlOri2 := nCtaDpAcm2 := nCtaDpMes2 := nCtaCrDep2 := nCtaCrMon2 := 0
	Local nCtaVlAmp1 := nCtaVlAmp2 := 0

	// Acumuladores de Centro de Custo
	Local nValOri1 := nDepAcm1 := nDepMes1 := nCorDep1 := nCorMon1 := 0
	Local nValOri2 := nDepAcm2 := nDepMes2 := nCorDep2 := nCorMon2 := 0
	Local nValAmp1 := nValAmp2 := 0

	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(),"ATR270")

	//Definição de parametros.
	cItemde     := MV_PAR01                             // MV_PAR01	Do Item Contabil ?              C
	cCItemAte   := MV_PAR02                             // MV_PAR02	Ate Item Contabil ?           	C
	cCustode    := MV_PAR03                             // MV_PAR03	Do Centro de Custo ?          	C
	cCustoAte   := MV_PAR04                             // MV_PAR04	Ate Centro de Custo ?         	C
	cContaDe    := MV_PAR05                             // MV_PAR03 Da Conta ?	                    C
	cContaAte   := MV_PAR06                             // MV_PAR04 Ate Conta ?	                    C
	nMoeda      := MV_PAR07                             // MV_PAR07	Qual Moeda ?                  	N
	lSelClas    := .F.            						// MV_PAR08 Selec Classif Patrimonial ?	    N	Sempre Falso no smartView  
	lAtfCusPrv  := AFXAtCsPrv() 						// MV_PAR09 Exibir Ativos Real. Provis ?	N	Sim	Não
	lSelFil     := .F. 									// MV_PAR10	Seleciona Filiais ?	            N	Sempre Falso no smartView  

	//Salva nome empresa e filial logada
    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .F.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"SN3"})

	If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
		aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
	Else
		aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
	EndIf

	cChave 	:= ""+SqlOrder(SN3->(IndexKey(6)))+""
	lCTDExcl := .F.
	lCTDTExFil := .F.
	lCTDExUnN := .F.
	if !empty(aSelFil)
		cFilSN1 := "SN1.N1_FILIAL IN ( ? ) "
		cFilSN3 := "SN3.N3_FILIAL IN ( ? ) "
	else
		cFilSN1 := "SN1.N1_FILIAL =  ?  "
		cFilSN3 := "SN3.N3_FILIAL = ?  "
	endif
	cWhere := "(SN3.N3_BAIXA < ? OR (SN3.N3_BAIXA >= ? AND SN3.N3_DTBAIXA LIKE ? ))"

	If lAtfCusPrv .and. MV_PAR09 == 2
		cWhere += " And SN3.N3_ATFCPR <> ? "
	Endif

	cWhere := ""+ cWhere + ""

	//Select para trabalho.
	cQuery :="SELECT * "
	cQuery +="FROM "
	cQuery += RetSQLName("SN3") + " SN3 "+","
	cQuery += RetSQLName("SN1") + " SN1 "
	cQuery +=" WHERE "
	cQuery += cFilSN3
	cQuery +="	AND SN3.N3_SUBCTA >= ?"		 
	cQuery +="	AND SN3.N3_SUBCTA <= ?"		 
	cQuery +="	AND SN3.N3_CCUSTO >= ?"		  
	cQuery +="	AND SN3.N3_CCUSTO <= ?"		 
	cQuery +="	AND SN3.N3_CCONTAB >= ?"	  
	cQuery +="	AND SN3.N3_CCONTAB <= ?"	 
	cQuery +="	AND (SN3.N3_SUBCTA <> ? AND SN3.N3_CDEPREC <> ? AND SN3.N3_CCDEPR <> ? ) "
	cQuery +="	AND "+cWhere
	cQuery +="	AND SN3.D_E_L_E_T_= ? "
	cQuery +="	AND "+cFilSN1
	cQuery +="	AND SN1.N1_CBASE = SN3.N3_CBASE "
	cQuery +="	AND SN1.N1_ITEM = SN3.N3_ITEM "
	cQuery +="	AND SN1.D_E_L_E_T_= ? "
	cQuery +="	ORDER BY "+cChave

	cQuery := ChangeQuery(cQuery)
	self:oQueryN3:= FWPreparedStatement():New(cQuery)	

	if !empty(aSelFil)
		self:oQueryN3:setIn(nParamOrder++,aSelFil)
	else
		self:oQueryN3:SetString(nParamOrder++,FWxFilial("SN1"))
	endif

	self:oQueryN3:SetString(nParamOrder++,cItemde)
    self:oQueryN3:SetString(nParamOrder++,cCItemAte)
    self:oQueryN3:SetString(nParamOrder++,cCustode)
    self:oQueryN3:SetString(nParamOrder++,cCustoAte)
    self:oQueryN3:SetString(nParamOrder++,cContaDe)
    self:oQueryN3:SetString(nParamOrder++,cContaAte)

    self:oQueryN3:SetString(nParamOrder++,Space(1))
    self:oQueryN3:SetString(nParamOrder++,Space(1))
    self:oQueryN3:SetString(nParamOrder++,Space(1))

    self:oQueryN3:SetString(nParamOrder++,"1")
    self:oQueryN3:SetString(nParamOrder++,"1")
    self:oQueryN3:SetString(nParamOrder++,Left(Dtos(dUltDepr), 6))

	If lAtfCusPrv .and. MV_PAR09==2
    	self:oQueryN3:SetString(nParamOrder++,"1")
	Endif

    self:oQueryN3:SetString(nParamOrder++,Space(1))
	if !empty(aSelFil)
		self:oQueryN3:setIn(nParamOrder++,aSelFil)
	else
		self:oQueryN3:SetString(nParamOrder++,FWxFilial("SN3"))
	endif
    self:oQueryN3:SetString(nParamOrder++,Space(1))

	cQuery := self:oQueryN3:GetFixQuery()

	//Executa a query.
	cAlias := MPSysOpenQuery(ChangeQuery(cQuery))
	(cAlias)->(dbGoTop())

	cMoedaTmp	:= SuperGetMv("MV_SIMB"+AllTrim(cMoeda),.F.,"R$")
	cMoedasTMp	:= SuperGetMv("MV_SIMB"+AllTrim(Str(MV_PAR07)),.F.,"R$")

	Do While ! (cAlias)->(Eof())

		cEntidade := (cAlias)->N3_SUBCTA
		Do While (cAlias)->(!Eof()) .And. (cAlias)->N3_SUBCTA == cEntidade

			//Verifica o saldo para impressao ou nao do bem
			::jDados := JsonObject():new()

			cCentroCus  := (cAlias)->N3_CUSTBEM
			cConta      := (cAlias)->N3_CCONTAB
			Do While (cAlias)->(!Eof()) .And.(cAlias)->(N3_SUBCTA)  == cEntidade;
					 .And. (cAlias)->N3_CUSTBEM == cCentroCus;
					 .And. (cAlias)->N3_CCONTAB == cConta ;
					 .and. If(lCTDExcl,(Substr((cAlias)->N3_FILIAL,1,nTamEmp) == cFilAtu),.T.)

				cD1_N3FILIAL		    := AllTrim((cAlias)->N3_FILIAL)
				cD1_N3SUBCTA			:= AllTrim((cAlias)->N3_SUBCTA)
				cD1_N3CCUSTO		    := AllTrim((cAlias)->N3_CCUSTO)
				cD1_N3CCONTAB		    := AllTrim((cAlias)->N3_CCONTAB)
				cD1_N3CBASE		        := AllTrim((cAlias)->N3_CBASE)
				cD1_N3ITEM		        := AllTrim((cAlias)->N3_ITEM)
				cD1_N3TIPO		        := AllTrim((cAlias)->N3_TIPO)
				cD1_N1DESCRIC		    := AllTrim((cAlias)->N1_DESCRIC)
				cD1_N1PATRIM		    := AllTrim((cAlias)->N1_PATRIM)

				lAtClDepr := Iif(FindFunction("AtClssVer"),AtClssVer((cAlias)->N1_PATRIM),(cAlias)->N1_PATRIM $ "NID")

				//Acumula Conta em Moeda 1
				If lAtClDepr .Or. (cAlias)->N1_PATRIM $ " P"
					nCtaVlOri1 += (cAlias)->N3_VORIG1
					nCtaDpAcm1 += (cAlias)->N3_VRDACM1
					nCtaVlAmp1 += (cAlias)->N3_AMPLIA1
					nCtaDpMes1 += (cAlias)->N3_VRDMES1
					nCtaCrDep1 += (cAlias)->N3_VRCDA1
					nCtaCrMon1 += (cAlias)->N3_VRCACM1
				Else
					nCtaVlOri1 -= (cAlias)->N3_VORIG1
					nCtaVlAmp1 -= (cAlias)->N3_AMPLIA1
					nCtaDpAcm1 -= (cAlias)->N3_VRDACM1
					nCtaDpMes1 -= (cAlias)->N3_VRDMES1
					nCtaCrDep1 -= (cAlias)->N3_VRCDA1
					nCtaCrMon1 -= (cAlias)->N3_VRCACM1
				Endif

				//Acumula Conta em Moeda do Ativo
				If lAtClDepr .Or. (cAlias)->N1_PATRIM $ " P"
					nCtaVlOri2 += (cAlias)->&("N3_VORIG"+AllTrim( Str( MV_PAR07 )))
					nCtaVlAmp2 += (cAlias)->&("N3_AMPLIA"+AllTrim( Str( MV_PAR07 )))
					nCtaDpAcm2 += (cAlias)->&("N3_VRDACM"+AllTrim( Str( MV_PAR07 )))
					nCtaDpMes2 += (cAlias)->&("N3_VRDMES"+AllTrim( Str( MV_PAR07 )))
					nCtaCrDep2 += IIf(cMoeda=="1",(cAlias)->N3_VRCDA1,0)
					nCtaCrMon2 += IIf(cMoeda=="1",(cAlias)->N3_VRCACM1,0)
				Else
					nCtaVlOri2 -= (cAlias)->&("N3_VORIG"+AllTrim( Str( MV_PAR07 )))
					nCtaVlAmp2 -= (cAlias)->&("N3_AMPLIA"+AllTrim( Str( MV_PAR07 )))
					nCtaDpAcm2 -= (cAlias)->&("N3_VRDACM"+AllTrim( Str( MV_PAR07 )))
					nCtaDpMes2 -= (cAlias)->&("N3_VRDMES"+AllTrim( Str( MV_PAR07 )))
					nCtaCrDep2 -= IIf(cMoeda=="1",(cAlias)->N3_VRCDA1,0)
					nCtaCrMon2 -= IIf(cMoeda=="1",(cAlias)->N3_VRCACM1,0)
				Endif

				//Acumula Centro de Custo em Moeda 1
				If lAtClDepr .Or. (cAlias)->N1_PATRIM $ " P"
					nValOri1 += (cAlias)->N3_VORIG1
					nValAmp1 += (cAlias)->N3_AMPLIA1
					nDepAcm1 += (cAlias)->N3_VRDACM1
					nDepMes1 += (cAlias)->N3_VRDMES1
					nCorDep1 += (cAlias)->N3_VRCDA1
					nCorMon1 += (cAlias)->N3_VRCACM1
				Else
					nValOri1 -= (cAlias)->N3_VORIG1
					nValAmp1 -= (cAlias)->N3_AMPLIA1
					nDepAcm1 -= (cAlias)->N3_VRDACM1
					nDepMes1 -= (cAlias)->N3_VRDMES1
					nCorDep1 -= (cAlias)->N3_VRCDA1
					nCorMon1 -= (cAlias)->N3_VRCACM1
				Endif

				//Acumula Centro de Custo em Moeda do Ativo
				If lAtClDepr .Or. (cAlias)->N1_PATRIM $ " P"
					nValOri2 += (cAlias)->&("N3_VORIG"+AllTrim( Str( MV_PAR07 ) ))
					nValAmp2 += (cAlias)->&("N3_AMPLIA"+AllTrim( Str( MV_PAR07 ) ))
					nDepAcm2 += (cAlias)->&("N3_VRDACM"+AllTrim( Str( MV_PAR07 ) ))
					nDepMes2 += (cAlias)->&("N3_VRDMES"+AllTrim( Str( MV_PAR07 ) ))
					nCorDep2 += IIf(cMoeda=="1",(cAlias)->N3_VRCDA1,0)
					nCorMon2 += IIf(cMoeda=="1",(cAlias)->N3_VRCACM1,0)
				Else
					nValOri2 -= (cAlias)->&("N3_VORIG"+AllTrim( Str( MV_PAR07 ) ))
					nValAmp2 -= (cAlias)->&("N3_AMPLIA"+AllTrim( Str( MV_PAR07 ) ))
					nDepAcm2 -= (cAlias)->&("N3_VRDACM"+AllTrim( Str( MV_PAR07 ) ))
					nDepMes2 -= (cAlias)->&("N3_VRDMES"+AllTrim( Str( MV_PAR07 ) ))
					nCorDep2 -= IIf(cMoeda=="1",(cAlias)->N3_VRCDA1,0)
					nCorMon2 -= IIf(cMoeda=="1",(cAlias)->N3_VRCACM1,0)
				Endif

				//Popular o jDados
				dbSelectArea(cAlias)
				cConta      := (cAlias)->N3_CCONTAB
				(cAlias)->(dbSkip())
				If (cAlias)->N3_CCONTAB <> cConta .Or. (cAlias)->N3_CUSTBEM <> cCentroCus

					//Campos da query
					::jDados["N3_FILIAL"]		    := cD1_N3FILIAL
					::jDados["N3_SUBCTA"]			:= cD1_N3SUBCTA
					::jDados["N3_CCUSTO"]		    := cD1_N3CCUSTO
					::jDados["N3_CCONTAB"]		    := cD1_N3CCONTAB
					::jDados["N3_CBASE"]		    := cD1_N3CBASE
					::jDados["N3_ITEM"]		        := cD1_N3ITEM
					::jDados["N3_TIPO"]		        := cD1_N3TIPO
					::jDados["N1_DESCRIC"]		    := cD1_N1DESCRIC
					::jDados["N1_PATRIM"]		    := cD1_N1PATRIM
					::jDados["N3_TXDEPR1"]		    := (cAlias)->N3_TXDEPR1
					::jDados["N3_VORIG1"]		    := nCtaVlOri1+nCtaCrMon1+nCtaVlAmp1
					::jDados["N3_AMPLIA1"]		    := nCtaVlAmp1
					::jDados["N3_VRDACM1"]		    := nCtaDpAcm1
					::jDados["N3_VRDMES1"]		    := nCtaDpMes1
					::jDados["N3_VRCDA1"]		    := nCtaCrDep1
					::jDados["N3_VRCACM1"]		    := nCtaCrMon1

					Do Case
					Case nMoeda == 2
						::jDados["N3_TXDEPR2"]		    := (cAlias)->N3_TXDEPR2
						::jDados["N3_VORIG2"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
						::jDados["N3_AMPLIA2"]		    := nCtaVlAmp2
						::jDados["N3_VRDACM2"]		    := nCtaDpAcm2
						::jDados["N3_VRDMES2"]		    := nCtaDpMes2
						::jDados["N3_VRCDA1"]		    := nCtaCrDep2
						::jDados["N3_VRCACM1"]		    := nCtaCrMon2
					Case nMoeda == 3
						::jDados["N3_TXDEPR3"]		    := (cAlias)->N3_TXDEPR3
						::jDados["N3_VORIG3"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
						::jDados["N3_AMPLIA3"]		    := nCtaVlAmp2
						::jDados["N3_VRDACM3"]		    := nCtaDpAcm2
						::jDados["N3_VRDMES3"]		    := nCtaDpMes2
						::jDados["N3_VRCDA1"]		    := nCtaCrDep2
						::jDados["N3_VRCACM1"]		    := nCtaCrMon2
					Case nMoeda == 4
						::jDados["N3_TXDEPR4"]		    := (cAlias)->N3_TXDEPR4
						::jDados["N3_VORIG4"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
						::jDados["N3_AMPLIA4"]		    := nCtaVlAmp2
						::jDados["N3_VRDACM4"]		    := nCtaDpAcm2
						::jDados["N3_VRDMES4"]		    := nCtaDpMes2
						::jDados["N3_VRCDA1"]		    := nCtaCrDep2
						::jDados["N3_VRCACM1"]		    := nCtaCrMon2
					Case nMoeda == 5
						::jDados["N3_TXDEPR5"]		    := (cAlias)->N3_TXDEPR5
						::jDados["N3_VORIG5"]		    := nCtaVlOri2+nCtaCrMon2+nCtaVlAmp2
						::jDados["N3_AMPLIA5"]		    := nCtaVlAmp2
						::jDados["N3_VRDACM5"]		    := nCtaDpAcm2
						::jDados["N3_VRDMES5"]		    := nCtaDpMes2
						::jDados["N3_VRCDA1"]		    := nCtaCrDep2
						::jDados["N3_VRCACM1"]		    := nCtaCrMon2
					EndCase

					//Campos para a composição do Desing
					::jDados["NMOEDAC"]             := Alltrim(Str(MV_PAR07))
					::jDados["NMOEDATF"]			:= cMoedaTmp
					::jDados["NMOEDAS"] 			:= cMoedasTmp
					::jDados[ "NomeEmpresa" ]       := cCompanyName
                	::jDados[ "NomeFilial" ]        := cBranchName

				EndIf

			EndDo

			self:processData()
			self:oData:appendData(::jDados)

			dbSelectArea(cAlias)
			nCtaVlOri1 := nCtaDpAcm1 := nCtaDpMes1 := nCtaCrDep1 := nCtaCrMon1 := 0
			nCtaVlOri2 := nCtaDpAcm2 := nCtaDpMes2 := nCtaCrDep2 := nCtaCrMon2 := 0
			nCtaVlAmp1 := nCtaVlAmp2 := 0

		EndDo
	Enddo

	//Se não for o último registro indica que terá próxima página
	self:setHasNext( !(cAlias)->(EoF()) )

	IIf(Select(cAlias)>0,(cAlias)->(DbCloseArea()),MSErase(cAlias))

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class entriesbyaccountingitemSmartViewBusinessObject

	Local aFieldsSN1            as array
	Local cQuerySN1             as character
	Local aFieldsSN3            as array
	Local cQuerySN3             as character
	Local lCanFilter 	 := .F. as logical


	//CC Despesa Conta Val Orig M1 Depr.Acum M1 Depr Mes M1 Cor Dep Acum Corr Acum M1
	
	aCamposQry := {;
		"N3_FILIAL","N3_SUBCTA","N3_CCUSTO","N3_CCONTAB","N3_CBASE","N3_ITEM","N3_TIPO",;
		"N3_TXDEPR1","N3_VORIG1","N3_VRCACM1","N3_VRDACM1","N3_VRCDA1","N3_AMPLIA1","N3_VRDMES1","N3_INDICE1","N3_VLACEL1",;
		"N3_TXDEPR2","N3_VORIG2","N3_VRCACM2","N3_VRDACM2","N3_VRCDA2","N3_AMPLIA2","N3_VRDMES2","N3_INDICE2","N3_VLACEL2",;
		"N3_TXDEPR3","N3_VORIG3","N3_VRCACM3","N3_VRDACM3","N3_VRCDA3","N3_AMPLIA3","N3_VRDMES3","N3_INDICE3","N3_VLACEL3",;
		"N3_TXDEPR4","N3_VORIG4","N3_VRCACM4","N3_VRDACM4","N3_VRCDA4","N3_AMPLIA4","N3_VRDMES4","N3_INDICE4","N3_VLACEL4",;
		"N3_TXDEPR5","N3_VORIG5","N3_VRCACM5","N3_VRDACM5","N3_VRCDA5","N3_AMPLIA5","N3_VRDMES5","N3_INDICE5","N3_VLACEL5"}
	
	aCampoQryn1 :={"N1_DESCRIC","N1_PATRIM"}

	self:oSchema:aliasToSchema("SN1",aCampoQryn1)
	self:oSchema:aliasToSchema("SN3",aCamposQry)

	cQuerySN1   := " SELECT N1_FILIAL,N1_CBASE,N1_ITEM FROM "+ RetSQLName("SN1") +" WHERE D_E_L_E_T_ = ' '"
	aFieldsSN1  := { "N1_FILIAL" , "N1_CBASE" , "N1_ITEM" }

	cQuerySN3   := " SELECT N3_FILIAL,N3_CBASE,N3_ITEM FROM "+ RetSQLName("SN3") +" WHERE D_E_L_E_T_ = ' '"
	aFieldsSN3  := { "N3_FILIAL" , "N3_CBASE" , "N3_ITEM" }

	//Seta os parametros manuais no SmartView
	self:addProperty( "NMOEDAC"  , STR0004  , "string" , STR0004 , "NMOEDAC",,,,lCanFilter  )
	self:addProperty( "NMOEDATF" , STR0005  , "string" , STR0005 , "NMOEDATF",,,,lCanFilter )
	self:addProperty( "NMOEDAS"  , STR0006  , "string" , STR0006 , "NMOEDAS",,,,lCanFilter  )
	self:addProperty( "NomeEmpresa", STR0007, "string", STR0007, "NomeEmpresa",,,,lCanFilter ) // Nome Empresa
    self:addProperty( "NomeFilial",  STR0008, "string", STR0008, "NomeFilial",,,,lCanFilter  ) // Nome Filial
	
	//Seta o lookup a partir da query
	self:oSchema:setLookUpQuery( "N1_CBASE" , aFieldsSN1 , cQuerySN1 , .F. )
	self:oSchema:setLookUpQuery( "N3_CBASE" , aFieldsSN3 , cQuerySN3 , .F. )

	//Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
@author         Controladoria
@since          14/06/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class entriesbyaccountingitemSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class entriesbyaccountingitemSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class entriesbyaccountingitemSmartViewBusinessObject
return ::jDados
