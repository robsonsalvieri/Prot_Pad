#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.listrecoverablevalueofasset.ch"

namespace totvs.protheus.backoffice.sv.atf.listrecoverablevalueofasset.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SA2,SN1,SN3", name="Relação do Valor Recuperável de Ativo", country="ALL", customTables="SN1,SN3,SA2")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ListRecoverableValueofAssetSmartViewBusinessObject
classe para criação do Objeto de Negócio de Relação do Valor Recuperável de Ativo
para o TReports

@author         Renan Leite Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
class ListRecoverableValueofAssetSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 
    
    protected method getQuery()         as character
    protected method setDataList()      as object
    protected method AFR410SNJ()        as numeric
    protected method AFR410VOrig()      as numeric
    protected method GetChave()         as character
    
    protected data jDados               as json
    protected data cAlias               as character
    protected data cMoeda               as character
    protected data cDBMS                as character
    protected data cTipoGer             as character
    protected data aAllFields           as array
    protected data jParams              as json
    protected data oQuery               as object
    protected data oIntegratedProvider  as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe new

@return object: self

@author         Renan Gremes
@since          15/01/2025         
@version        12.1.2410
*/
//------------------------------------------------------------------- 
method new() as object class ListRecoverableValueofAssetSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)  //"Relação do Valor Recuperável de Ativo" 
    self:setPergunte("AFR410")    //Indica o pergunte que será utilizado no relatório

    self:aAllFields := {}
    self:cMoeda     := ""
    self:cDBMS      := Upper(TCGetDB())
    self:cTipoGer   := SuperGetMv( "MV_ATFTIOA", .F., "12" )
    self:jParams    := Nil
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe

@return object: string

@author         Renan Gremes
@since          15/01/2025  
@version        12.1.2410
*/
//------------------------------------------------------------------- 
method getDescription() as character class ListRecoverableValueofAssetSmartViewBusinessObject
return STR0002  //"Objeto contendo informações sobre o relatório de Relação do Valor Recuperável de Ativo"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe

@author         Renan Gremes
@since          15/01/2025       
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method getAreas() as array class ListRecoverableValueofAssetSmartViewBusinessObject
return { STR0003 } //"Ativo Fixo"

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method preSchema() as object class ListRecoverableValueofAssetSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe

@return object: self:oData

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------
method preData() as object class ListRecoverableValueofAssetSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query

@return object: self:oData

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------
method processData() class ListRecoverableValueofAssetSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos

@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos

@author         Renan Gremes
@since          15/01/2025          
@version        12.1.2410
*/
//------------------------------------------------------------------- 
method getSchema() as object class ListRecoverableValueofAssetSmartViewBusinessObject

    local aFieldsSA2 as array
    local aFieldsSN1 as array
    local aFieldsSN3 as array

    aFieldsSA2  := {"A2_NREDUZ"}

    aFieldsSN1  := {"N1_QUANTD" , "N1_NFISCAL", "N1_CHAPA"  , "N1_GRUPO"  , "N1_LOCAL"  , "N1_FORNEC" , "N1_LOJA",;
                    "N1_DESCRIC"}

    aFieldsSN3  := {"N3_FILIAL" , "N3_CBASE"  , "N3_ITEM"   , "N3_TIPO"   , "N3_TPSALDO", "N3_AQUISIC", "N3_DINDEPR",;
                    "N3_CCUSTO" , "N3_CCONTAB", "N3_CDESP"  , "N3_DTBAIXA", "N3_CCORREC", "N3_CDEPREC", "N3_CCDEPR" ,;
                    "N3_VRCACM1", "N3_BAIXA"}

    self:oSchema:aliasToSchema("SA2", aFieldsSA2)
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("SN3", aFieldsSN3)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields() 
    
    //Adiciona campos customizados
    self:oSchema:addProperty("IMP_ACUMULADO" , STR0004, "number", STR0004, "IMP_ACUMULADO" ,,,,.F.) // "Imp.Acuml" 
    self:oSchema:addProperty("VLR_TST_IMPAIR", STR0005, "number", STR0005, "VLR_TST_IMPAIR",,,,.F.) // "Vlr.Tst.Imp."
    self:oSchema:addProperty("CONTA_PERREV"  , STR0006, "string", STR0006, "CONTA_PERREV"  ,,,,.F.) // "Conta Per/Rev."
    self:oSchema:addProperty("N3_VORIG"      , STR0012, "number", STR0012, "N3_VORIG"      ,,,,.F.) // "Valor Original"
    self:oSchema:addProperty("N3_TXDEPR"     , STR0013, "number", STR0013, "N3_TXDEPR"     ,,,,.F.) // "Tx. Deprec."
    self:oSchema:addProperty("N3_VRDACM"     , STR0014, "number", STR0014, "N3_VRDACM"     ,,,,.F.) // "Deprec. Acumulada"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0007, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

    //Ordenação do relatório
    self:oSchema:addParameter("SV_NORDEM", STR0008, "number", .F.) //"Ordena por?"
    self:setCustomURL("SV_NORDEM", "/api/controladoria/smartview/v1/options/atf/listrecoverablevalueofasset/SVATFCombo005/1", 1)

    FwFreeArray(aFieldsSA2)
    FwFreeArray(aFieldsSN1)
    FwFreeArray(aFieldsSN3)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData

@author         Renan Gremes
@since          15/01/2025      
@version        12.1.2410
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ListRecoverableValueofAssetSmartViewBusinessObject

    local cQuery := "" as character

    self:jParams := oFilter:getParameters()

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(self:jParams, "AFR410")

    //Monta a query
    cQuery := self:getQuery(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setDataList()

    (self:cAlias)->(dbCloseArea())

    FreeObj(self:jParams)
    FreeObj(oFilter:getParameters())

return self:oData


//-------------------------------------------------------------------------------
/*{Protheus.doc} getQuery
Gera a query de filtragem dos dados

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method getQuery(oFilter as object) as character class ListRecoverableValueofAssetSmartViewBusinessObject
     
    local cSpace      := space(1) as character
    local cChave                  as character
    local cQuery                  as character
    local aSelFil                 as array
    local nParamOrder := 1        as numeric

    cChave := self:GetChave(self:jParams:SV_NORDEM[1])
    self:cMoeda := Str(iif(!empty(MV_PAR07), MV_PAR07, 1), 1)

    //Seleção de filiais
    if MV_PAR09 == 2
        ::oIntegratedProvider := ControlIntegratedProvider():New()

        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(self:jParams)
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        if Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        else
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        endif
    endif

    cQuery := "SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += ", N3_VORIG" + AllTrim(self:cMoeda)
    cQuery += ", N3_TXDEPR" + AllTrim(self:cMoeda)
    cQuery += ", N3_VRDACM" + AllTrim(self:cMoeda)
    cQuery += ", N3_REVACM" + AllTrim(self:cMoeda)
    cQuery += iif(self:cMoeda == "1", ", N3_PERACM1", ", N3_PERACM1, N3_PERACM" + AllTrim(self:cMoeda))    
	cQuery += ", N3_SEQ, SN3.R_E_C_N_O_ RECNOSN3 "
	cQuery += " FROM " + RetSqlName("SN3") + " SN3"

	cQuery += " LEFT JOIN " + RetSqlName("SN1") + " SN1 ON "
	cQuery += " 	SN1.N1_FILIAL  = SN3.N3_FILIAL AND " 
	cQuery += "		SN1.N1_CBASE   = SN3.N3_CBASE AND "
	cQuery += " 	SN1.N1_ITEM    = SN3.N3_ITEM AND "
	cQuery += "		SN1.D_E_L_E_T_ = ? "

	cQuery += " LEFT JOIN " + RetSqlName("SA2") + " SA2 ON "
    cQuery += " 	SA2.A2_FILIAL = ? AND "
	cQuery += " 	SA2.A2_COD = SN1.N1_FORNEC AND "
    cQuery += " 	SA2.A2_LOJA = SN1.N1_LOJA AND "
    cQuery += " 	SA2.D_E_L_E_T_ = ? "

	cQuery += " WHERE "

    //Faixa de filiais
    if MV_PAR09 == 1
        if !empty(MV_PAR10)
            cQuery += "SN3.N3_FILIAL >= ? AND "
        endif
        if !empty(MV_PAR11)
            cQuery += "SN3.N3_FILIAL <= ? AND "
        endif
	else
        if !empty(aSelFil)
            cQuery += "SN3.N3_FILIAL IN ( ? ) AND "
        endif
	endif

	//Faixa de contas contabeis
	if !empty(MV_PAR03)
		cQuery += "SN3.N3_CCONTAB >= ? AND "
	endif
	if !empty(MV_PAR04)
		cQuery += "SN3.N3_CCONTAB <= ? AND "
	endif

	//Faixa de centros de custo
	if !empty(MV_PAR05)
		cQuery += "SN3.N3_CCUSTO  >= ? AND "
	endif
	if !empty(MV_PAR06)
		cQuery += "SN3.N3_CCUSTO  <= ? AND "
	endif

	//Faixa de aquisicoes
	if !empty(MV_PAR01)
		cQuery += "SN3.N3_AQUISIC >= ? AND "
	endif
	if !empty(MV_PAR02)
		cQuery += "SN3.N3_AQUISIC <= ? AND "
	endif
	
	//Tipo de Saldo
	if !empty(MV_PAR12)
		cQuery += "SN3.N3_TPSALDO = ? AND "
	endif

	cQryTip12 := "FROM " + RetSqlName("SN3") + " SN3_TIPO12 "
	cQryTip12 += "WHERE SN3_TIPO12.N3_FILIAL = SN3.N3_FILIAL "
	cQryTip12 += " AND SN3_TIPO12.N3_CBASE = SN3.N3_CBASE "
	cQryTip12 += " AND SN3_TIPO12.N3_ITEM = SN3.N3_ITEM "
	cQryTip12 += " AND SN3_TIPO12.N3_TIPO = ? "
	cQryTip12 += " AND SN3_TIPO12.D_E_L_E_T_ = ? "

	//Retornar Inclusive Ativos que não movimentaram SN3 por IMPAIRMENT
	cQrySNJ := "FROM " + RetSqlName("SNI") + " NI "
	cQrySNJ += "INNER JOIN " + RetSqlName("SNJ") + " NJ ON NI_FILIAL = NJ_FILIAL AND NI_PROC = NJ_PROC "
	cQrySNJ += "WHERE NJ_FILIAL = SN3.N3_FILIAL"
	cQrySNJ += " AND NJ_BEM = SN3.N3_CBASE 
	cQrySNJ += " AND NJ_ITBEM = SN3.N3_ITEM  "
	cQrySNJ += " AND NJ_TPSALDO = SN3.N3_TPSALDO "
	cQrySNJ += " AND NJ_TIPO = SN3.N3_TIPO "
	cQrySNJ += " AND NI_STATUS = ? "
	cQrySNJ += " AND NJ_VLORI01 - NJ_VLREC01 = ? "
	cQrySNJ += " AND NI.D_E_L_E_T_ = ? AND NJ.D_E_L_E_T_ = ? "
	
	if self:cDBMS == "ORACLE"
		cSubQuery := "SELECT N3_TIPO " + cQryTip12 + " AND ROWNUM < ? "
		cSubSNJ   := "SELECT NJ_TIPO " + cQrySNJ   + " AND ROWNUM < ? "
	elseif self:cDBMS == "POSTGRES"
		cSubQuery := "SELECT N3_TIPO " + cQryTip12 + " LIMIT ? "
		cSubSNJ := "SELECT NJ_TIPO " + cQrySNJ   + " LIMIT ? "
	else
		cSubQuery := "SELECT TOP 1 N3_TIPO " + cQryTip12
		cSubSNJ := "SELECT TOP 1 NJ_TIPO " + cQrySNJ
	endif

	//Faixa de itens
	if MV_PAR14 == 1 //Somente depreciação fiscal, mas só pode imprimir se houver um registro de Valor Recuperável no mesmo ativo imobilizado
		cQuery += " SN3.N3_TIPO = ? AND "
		cQuery += "( EXISTS  "
		cQuery += "( "+ cSubQuery + " ) "
		cQuery += "OR EXISTS  "
		cQuery += "( "+ cSubSNJ + " ) "	
		cQuery += ") "
	elseif MV_PAR14 == 2 //Somente valor recuperável
		cQuery += " SN3.N3_TIPO = ? "
	elseif MV_PAR14 == 3 //Depreciação contábil e valor recuperável
		cQuery += " SN3.N3_TIPO IN ( ? ) AND " //Fixado os tipos 12 e 10, pois pode acontecer do parametro MV_ATFTIOA estar configurado com outro tipo difente de 12 e 10
		cQuery += "( EXISTS  "
		cQuery += "( "+ cSubQuery + " ) "
		cQuery += "OR EXISTS  "
		cQuery += "( "+ cSubSNJ + " ) "	
		cQuery += ") "
	else // Todos
		cQuery += " SN3.N3_TIPO IN ( ? ) AND " //Fixado os tipos 12 e 10, pois pode acontecer do parametro MV_ATFTIOA estar configurado com outro tipo difente de 12 e 10
		cQuery += "( EXISTS  "
		cQuery += "( "+ cSubQuery + " ) "
		cQuery += "OR EXISTS  "
		cQuery += "( "+ cSubSNJ + " ) "	
		cQuery += ") "
	endif

	//Se nao considera itens baixados, filtra na query
	if MV_PAR08 == 2
		cQuery += "AND SN3.N3_BAIXA = ? "
	endif	

	cQuery += "AND SN3.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports 
    if oFilter:hasFilter()
        cQuery += " AND " + oFilter:getSQLExpression()
    endif

	if !empty(cChave)
		cQuery += " ORDER BY " + STRTRAN(cChave , "+" , ",")
	endif

    //Instância da classe FWPreparedStatement
    self:oQuery := FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery:SetString(nParamOrder++, cSpace)
    self:oQuery:SetString(nParamOrder++, FWxFilial("SA2"))
    self:oQuery:SetString(nParamOrder++, cSpace)

    //Faixa de filiais
    if MV_PAR09 == 1
        if !empty(MV_PAR10)
            self:oQuery:SetString(nParamOrder++, MV_PAR10)
        endif
        if !empty(MV_PAR11)
            self:oQuery:SetString(nParamOrder++, MV_PAR11)
        endif
	else
        if !empty(aSelFil)
            self:oQuery:setIn(nParamOrder++, aSelFil)
        endif
	endif

    //Faixa de contas contabeis
	if !empty(MV_PAR03)
		self:oQuery:SetString(nParamOrder++, MV_PAR03)
	endif
	if !empty(MV_PAR04)
		self:oQuery:SetString(nParamOrder++, MV_PAR04)
	endif

    //Faixa de centros de custo
	if !empty(MV_PAR05)
		self:oQuery:SetString(nParamOrder++, MV_PAR05)
	endif
	if !empty(MV_PAR06)
		self:oQuery:SetString(nParamOrder++, MV_PAR06)
	endif

    //Faixa de aquisicoes
	if !empty(MV_PAR01)
		self:oQuery:setDate(nParamOrder++, MV_PAR01)
	endif
	if !empty(MV_PAR02)
		self:oQuery:setDate(nParamOrder++, MV_PAR02)
	endif    
    
    //Tipo de Saldo
	if !empty(MV_PAR12)
        self:oQuery:SetString(nParamOrder++, PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]))
	endif

    if MV_PAR14 == 1 //Somente depreciação fiscal, mas só pode imprimir se houver um registro de Valor Recuperável no mesmo ativo imobilizado
		self:oQuery:SetString(nParamOrder++, "01")
	elseif MV_PAR14 == 2 //Somente valor recuperável
        self:oQuery:SetString(nParamOrder++, self:cTipoGer)
	elseif MV_PAR14 == 3 //Depreciação contábil e valor recuperável
        self:oQuery:setIn(nParamOrder++, {self:cTipoGer, "12", "10"})
	else //Todos
        self:oQuery:setIn(nParamOrder++, {self:cTipoGer, "12", "10", "01"})
	endif

    if MV_PAR14 <> 2
        self:oQuery:SetString(nParamOrder++, self:cTipoGer)
        self:oQuery:SetString(nParamOrder++, cSpace)

        if self:cDBMS == "ORACLE"
            self:oQuery:SetNumeric(nParamOrder++, 2)
        elseif self:cDBMS == "POSTGRES"
            self:oQuery:SetNumeric(nParamOrder++, 1)
        endif        
        
        self:oQuery:SetString(nParamOrder++, "2")
        self:oQuery:SetNumeric(nParamOrder++, 0)
        self:oQuery:SetString(nParamOrder++, cSpace)
        self:oQuery:SetString(nParamOrder++, cSpace)

        if self:cDBMS == "ORACLE"
            self:oQuery:SetNumeric(nParamOrder++, 2)
        elseif self:cDBMS == "POSTGRES"
            self:oQuery:SetNumeric(nParamOrder++, 1)
        endif
    endif

    if MV_PAR08 == 2
        self:oQuery:SetString(nParamOrder++, "0")
	endif

    self:oQuery:SetString(nParamOrder++, cSpace)    

    //Retorna query com parâmetros tratados e substituídos
    cQuery := self:oQuery:GetFixQuery()

    self:oQuery:Destroy()
    self:oQuery := Nil

    FwFreeArray(aSelFil)

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setDataList
Seta os dados filtrados no objeto oData

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method setDataList() as object class ListRecoverableValueofAssetSmartViewBusinessObject

    local cCPerRev          as character
    local nX         := 0   as numeric
    local nVlImp     := 0   as numeric
    local nVlImpAcum := 0   as numeric
    local nRecSNJ    := 0   as numeric
    local nValorOrig := 0   as numeric
    local lPerAcm    := .F. as logical
    local lVlrTstImp := .F. as logical
    local lCPerRev   := .F. as logical
    local aImpAcum   := {}  as array

    dbSelectArea("SN3")
    lPerAcm := SN3->(FieldPos("N3_PERACM"+AllTrim(self:cMoeda))) > 0 .and. SN3->(FieldPos("N3_REVACM"+ AllTrim(self:cMoeda))) > 0 .and. SN3->(FieldPos("N3_PERACM1")) > 0
    
    dbSelectArea("SNJ")
    lVlrTstImp := SNJ->(FieldPos("NJ_VLRIMPT")) > 0
    lCPerRev   := SNJ->(FieldPos("NJ_CPERREV")) > 0

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    while !(self:cAlias)->(Eof())
        
        self:jDados := JsonObject():new()

        //Popula campos conforme estrutura
        for nX := 1 to Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
        next nX

        //Calcula valor original
        nValorOrig := self:AFR410VOrig((self:cAlias)->N3_FILIAL,;
                                       (self:cAlias)->N3_CBASE,; 
                                       (self:cAlias)->N3_ITEM,;
                                       (self:cAlias)->N3_TIPO,; 
                                       (self:cAlias)->N3_BAIXA,;
                                       (self:cAlias)->N3_SEQ)
        
        self:jDados["N3_VORIG"]  := nValorOrig
        self:jDados["N3_TXDEPR"] := (self:cAlias)->&("N3_TXDEPR"+AllTrim(self:cMoeda))
        self:jDados["N3_VRDACM"] := (self:cAlias)->&("N3_VRDACM"+AllTrim(self:cMoeda))

        if lPerAcm
            if ((self:cAlias)->N3_PERACM1 > 0) 
                nVlImpAcum := (&(self:cAlias + "->N3_PERACM" + AllTrim(self:cMoeda)) - &(self:cAlias + "->N3_REVACM" + AllTrim(self:cMoeda)))
            else
                if ((self:cAlias)->N3_TIPO == self:cTipoGer)
                    aImpAcum := AF380REVER((self:cAlias)->N3_CBASE, (self:cAlias)->N3_ITEM, AllTrim(self:cMoeda), (self:cAlias)->N3_FILIAL)
                    if (Len(aImpAcum) > 0)
                        nVlImpAcum := aImpAcum[2] - aImpAcum[3]
                    endif
                else
                    nVlImpAcum := 0
                endif
            endif
        endif

        if lVlrTstImp .or. lCPerRev
			nVlImp  	:= 0
			cCPerRev    := ""
			nRecSNJ     := self:AFR410SNJ((self:cAlias)->N3_FILIAL,;
                                          (self:cAlias)->N3_CBASE,;
                                          (self:cAlias)->N3_ITEM,;
                                          (self:cAlias)->N3_TIPO,;
                                          (self:cAlias)->&("N3_VORIG"+AllTrim(self:cMoeda)),;
                                          (self:cAlias)->N3_SEQ)
			if nRecSNJ > 0
				SNJ->(DbGoTo(nRecSNJ))
				if lVlrTstImp
					nVlImp   := SNJ->NJ_VLRIMPT
				endif
				if lCPerRev
					cCPerRev := SNJ->NJ_CPERREV
				endif
			endif
		endif

        if lPerAcm
            self:jDados["IMP_ACUMULADO"] := nVlImpAcum
        endif        

        if lVlrTstImp
            self:jDados["VLR_TST_IMPAIR"] := nVlImp
        endif

        if lCPerRev
            self:jDados["CONTA_PERREV"] := cCPerRev
        endif

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->(dbSkip())

    enddo
    
    FreeObj(self:jDados)
    FwFreeArray(aImpAcum)

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} AFR410SNJ
Retorna o Valor do Último Teste de Impairment do bem

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method AFR410SNJ(cFil as character, cBaseSNJ as character, cItemSNJ as character, cTipoSNJ as character, nVlOrig as numeric, cSeq as character) as numeric class ListRecoverableValueofAssetSmartViewBusinessObject

    local cSpace      := Space(1) as character
    local cQuery      := ""  as character
    local cAlias      := ""  as character
    local nParamOrder := 1   as numeric
    local nRecno      := 0   as numeric
    local oQryExec	         as object

    default cFil 	 := FWxFilial("SN3")
    default cBaseSNJ := ""
    default cItemSNJ := ""
    default cTipoSNJ := ""
    default nVlOrig  := ""
    default cSeq	 := ""

    if self:cDBMS == "ORACLE"
    	cQuery := "SELECT R_E_C_N_O_ REC  "
    	cQuery += "FROM ( 				   "
    	cQuery += "	SELECT NJ.R_E_C_N_O_,  "
    	cQuery += "		NI.NI_DTIOA        "
    	cQuery += "	FROM "+RetSQLName("SNI")+" NI "
    	cQuery += "	INNER JOIN "+RetSQLName("SNJ")+" NJ ON NI.NI_FILIAL = NJ.NJ_FILIAL AND NI.NI_PROC = NJ.NJ_PROC "
    	cQuery += "	WHERE NJ_FILIAL = ? AND NJ.NJ_BEM = ? AND NJ_ITBEM = ?  "
    	cQuery += " AND ( NJ_TIPO = ? OR NJ_SEQSN3 = ? )  "
    	cQuery += " AND NJ.NJ_VLREC01 = ? "
    	cQuery += " AND NI.NI_STATUS = ? "
    	cQuery += "	AND NI.D_E_L_E_T_ = ? "
    	cQuery += "	AND NJ.D_E_L_E_T_ = ? "

    	cQuery += "	ORDER BY NJ.R_E_C_N_O_ DESC "
    	cQuery += " )
    	cQuery += " WHERE ROWNUM = ? ; "
    else
    	if self:cDBMS == "POSTGRES"
    		cQuery := "SELECT NJ.R_E_C_N_O_ REC "
    	else
    		cQuery := "SELECT TOP 1 NJ.R_E_C_N_O_ REC"
    	endif
    	cQuery += " FROM "+RetSQLName("SNI")+" NI "
    	cQuery += " INNER JOIN "+RetSQLName("SNJ")+" NJ ON NI.NI_FILIAL = NJ.NJ_FILIAL AND NI.NI_PROC = NJ.NJ_PROC "
    	cQuery += " WHERE NJ_FILIAL = ? AND NJ.NJ_BEM = ? AND NJ_ITBEM = ?   "
    	cQuery += " AND ( NJ_TIPO = ? OR NJ_SEQSN3 = ? )  "
    	cQuery += " AND NJ.NJ_VLREC01 = ? "
    	cQuery += " AND NI.NI_STATUS = ? "
    	cQuery += " AND NI.D_E_L_E_T_ = ? "
    	cQuery += " AND NJ.D_E_L_E_T_ = ? "

    	cQuery += " ORDER BY NJ.R_E_C_N_O_ DESC "
    	if self:cDBMS == "POSTGRES"
    		cQuery += " LIMIT ?;
    	endif
    endif

    oQryExec := FWExecStatement():New(ChangeQuery(cQuery))

    oQryExec:SetString(nParamOrder++, cFil)
    oQryExec:SetString(nParamOrder++, cBaseSNJ)
    oQryExec:SetString(nParamOrder++, cItemSNJ)
    oQryExec:SetString(nParamOrder++, cTipoSNJ)
    oQryExec:SetString(nParamOrder++, cSeq)
    oQryExec:SetNumeric(nParamOrder++, nVlOrig)
    oQryExec:SetString(nParamOrder++, "2")
    oQryExec:SetString(nParamOrder++, cSpace) 
    oQryExec:SetString(nParamOrder++, cSpace)

    if self:cDBMS == "ORACLE"
        oQryExec:SetNumeric(nParamOrder++, 1)
    elseif self:cDBMS == "POSTGRES"
        oQryExec:SetNumeric(nParamOrder++, 1)
    endif

    cQuery := oQryExec:GetFixQuery()
    cAlias := MPSysOpenQuery(cQuery)

    if (cAlias)->(!Eof())
    	nRecno	:= (cAlias)->REC
    endif

    (cAlias)->(dbCloseArea())
    oQryExec:Destroy()
    oQryExec := Nil

return nRecno

//-------------------------------------------------------------------------------
/*{Protheus.doc} GetChave
Monta a chave da query de acordo com o parâmetro selecionado

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method GetChave(nOrder as numeric) as character class ListRecoverableValueofAssetSmartViewBusinessObject

    local cChave := "" as character

    default nOrder := 1

    if nOrder == 1
        cChave	:= "N3_FILIAL,N3_CCONTAB,N3_CBASE,N3_ITEM"
    elseif nOrder == 2
        cChave	:= "N3_FILIAL,N3_CCUSTO,N3_CBASE,N3_ITEM"
    elseif nOrder == 3
        cChave	:= "N3_FILIAL,N3_AQUISIC,N3_CBASE,N3_ITEM"
    endif

return cChave

//-------------------------------------------------------------------------------
/*{Protheus.doc} AFR410VOrig
Retorna o valor original do bem 

@author         Renan Gremes
@since          15/01/2025
@version        12.1.2410
*/
//-------------------------------------------------------------------------------
method AFR410VOrig(cFilSN3 as character, cBase as character, cItem as character, cTipo as character, cBaixa as character, cSeq as character) as numeric class ListRecoverableValueofAssetSmartViewBusinessObject
    
    local aAreaSN3 := SN3->(GetArea()) as array
    local nVOrig   := 0 as numeric

    default cFilSN3 := ""
    default cBase   := ""
    default cItem   := ""
    default cTipo   := ""
    default cBaixa  := ""
    default cSeq    := ""

    SN3->(dbSetOrder(1))
    SN3->(MsSeek(cFilSN3+cBase+cItem+cTipo+cBaixa+cSeq))

    while SN3->(!Eof()) .and. SN3->(N3_FILIAL+N3_CBASE+N3_ITEM+SN3->N3_TIPO+SN3->N3_BAIXA+SN3->N3_SEQ) == cFilSN3+cBase+cItem+cTipo+cBaixa+cSeq
        nVOrig += SN3->&("N3_VORIG"+AllTrim(self:cMoeda))
        SN3->(dbSkip())
    enddo

    SN3->(RestArea(aAreaSN3))
    FwFreeArray(aAreaSN3)

return nVOrig

//-------------------------------------------------------------------
/*/{Protheus.doc} SVATFCombo005
    Função para retornar combobox de parâmetro
   
    @author Renan Gremes
    @since 15/01/2025
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
function SVATFCombo005(cOpcao as character)
    local aRet := {} as array

    if cOpcao == "1"
        aRet := {EncodeUTF8(STR0009), EncodeUTF8(STR0010), EncodeUTF8(STR0011)} //"Conta", "C Custo", "Data Aquisicao"
    endif

return aRet
