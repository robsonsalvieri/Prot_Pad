#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.simulationrecoverablevalueassets.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SNI,SNJ", name="Aquisições", country="ALL", , customTables="SNI,SNJ")

class simulationRecoverableValueAssetsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()					as object
    public method getDescription()		as character
    public method getAreas()			as array
    public method getData()				as object
    public method getSchema()			as object

    //usados na internacionalização
    public method preschema()			as object 
    public method preData()				as object
    public method processData()			as json

    protected data aAllFields			as array
    protected data oIntegratedProvider	as object
    protected data jItens				as json
	
	protected method getQuery()			as character
	protected method validaMoeda()		as logical

endclass


Method new() as object Class simulationRecoverableValueAssetsSmartViewBusinessObject
    
    _Super:new()
	self:setDisplayName(STR0002)  // "Demonstrativo do Cálculo do Valor Recuperável do Ativo"
	self:setPergunte('AFR411')
    
	::aAllFields			:= {}
	::oIntegratedProvider	:= Nil

Return self


Method getDescription() as character class simulationRecoverableValueAssetsSmartViewBusinessObject
return STR0003		// "Objeto de negócio para apresentar o cálculo do valor recuperável do ativo"

Method getAreas() as array class simulationRecoverableValueAssetsSmartViewBusinessObject
Return {STR0001}	// "Ativo Fixo"

method preSchema() as object class simulationRecoverableValueAssetsSmartViewBusinessObject
return self:oSchema

method preData() as object class simulationRecoverableValueAssetsSmartViewBusinessObject    
return self:oData

method processData() as json class simulationRecoverableValueAssetsSmartViewBusinessObject
return ::jItens

method getSchema() as object class simulationRecoverableValueAssetsSmartViewBusinessObject

local lCanFilter 	:= .F.  as logical

	self:oSchema:aliasToSchema('SNI',	{	'NI_FILIAL'	,'NI_PROC'		,'NI_ROTINA'	,'NI_DTEMIS'	,'NI_DTIOA'		,'NI_LA'		,'NI_STATUS' })
	
	self:oSchema:aliasToSchema('SNJ',	{	'NJ_ITEM'	,'NJ_BEM'		,'NJ_ITBEM'		,'NJ_TIPO'		,'NJ_VLREC01'	,'NJ_TPDEPR'	,'NJ_PERDEPR'	,;
											'NJ_VMXDEPR','NJ_VLSALV1'	,'NJ_PRODANO'	,'NJ_PRODMES'	,'NJ_PRODACM'	,'NJ_CODIND'	,'NJ_DTAVALI'	,;
											'NJ_VLRIMPT','NJ_VLORI01'	,'NJ_VLACM01'	,'NJ_VLVEN01'	,'NJ_VLTAX01'	,'NJ_VORIG2'	,'NJ_VORIG3'	,;
											'NJ_VORIG4'	,'NJ_VORIG5'	,'NJ_VRDACM2'	,'NJ_VRDACM3'	,'NJ_VRDACM4'	,'NJ_VRDACM5'	,'NJ_VLVEN02'	,;
											'NJ_VLVEN03','NJ_VLVEN04'	,'NJ_VLVEN05'	,'NJ_VLREC02'	,'NJ_VLREC03'	,'NJ_VLREC04'	,'NJ_VLREC05'	,;
											'NJ_TXDEPR2','NJ_TXDEPR3'	,'NJ_TXDEPR4'	,'NJ_TXDEPR5'	,'NJ_CPERREV'	,'NJ_CRECDES'	,'NJ_TPAJUST'	;
										})
	
	self:oSchema:aliasToSchema('SN3',	{	'N3_HISTOR' })

	self:addProperty( "VLRRECU",   STR0011, "number" , STR0011, "VLRRECU",,,, lCanFilter )	// "Valor Recuperável"
	self:addProperty( "VLRORIG",   STR0007, "number" , STR0007, "VLRORIG",,,, lCanFilter )	// "Valor Original"
	self:addProperty( "VLVENDA",   STR0008, "number" , STR0008, "VLVENDA",,,, lCanFilter )	// "Valor Venda"
	self:addProperty( "TXANDPR",   STR0009, "number" , STR0009, "TXANDPR",,,, lCanFilter )	// "Tx. An. Depr."
	self:addProperty( "VLDPACU",   STR0010, "number" , STR0010, "VLDPACU",,,, lCanFilter )	// "Depr. Acum."

	//Habilita filtro de filiais
	self:oSchema:addParameter("SV_MULTBRANCH", STR0006, "string", .T.) // "Filial"
	self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

method getData(nPage as numeric, oFilter as object) as object class simulationRecoverableValueAssetsSmartViewBusinessObject

local aFld1			:= {}	as array
local aFld2			:= {}	as array
local aFld3			:= {}	as array
local aFld4			:= {}	as array
local cAlias		:= ""	as character
local cField		:= ""	as character
local cMoeda		:= ""	as character
local cProperty		:= ""	as character
local cRealName		:= ""	as character
local jParams       := Nil	as json  
local lCombo		:= .F.	as logical
local lContinua		:= .T.	as logical
local nX			:= 0	as numeric

	jParams := oFilter:getParameters()	// Método para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "AFR411")	// Carrega lista de parametros para a memória
    FreeObj(jParams)					// Elimina da memória a instância do objeto informado como parâmetro.

	cQuery := self:getQuery(oFilter)
	
	MV_PAR09 := iif( !empty(MV_PAR09), StrZero(val(MV_PAR09),2), "")

	//Validação da moeda informada no 9o parâmetro
	lContinua := self:validaMoeda(MV_PAR09)
    
	if lContinua
		
		cAlias := MPSysOpenQuery(cQuery)

		self:aAllFields := self:getStructFields()

		while (cAlias)->(!EOF())

			aFld1		:= StrToKarr2('NI_FILIAL|NI_PROC|NI_ROTINA|NI_DTEMIS|NI_DTIOA|NI_LA|NI_STATUS|NJ_ITEM|NJ_BEM|NJ_ITBEM|NJ_TIPO|N3_HISTOR','|')
			aFld2		:= StrToKarr2('VLRRECU|NJ_TPDEPR|NJ_PERDEPR|NJ_VMXDEPR|NJ_VLSALV1|NJ_PRODANO|NJ_PRODMES|NJ_PRODACM|NJ_CODIND','|')
			aFld3		:= StrToKarr2('NJ_DTAVALI|NJ_VLRIMPT|VLRORIG|VLDPACU|VLVENDA|VLRRECU|TXANDPR|NJ_CPERREV|NJ_CRECDES|NJ_TPAJUST','|')
			aFld4		:= StrToKarr2('VLRORIG|VLDPACU|VLVENDA|VLRRECU|TXANDPR|NJ_CPERREV|NJ_CRECDES|NJ_TPAJUST','|')
			::jItens	:= JsonObject():new()

			for nX := 1 to len(self:aAllFields)
				
				cProperty	:= self:aAllFields[nX]:getName()
				cRealName	:= self:aAllFields[nX]:getRealName()
				cField		:= if( cProperty == cRealName, cProperty, cRealName )
				cMoeda		:= right(alltrim(MV_PAR09),1)
				
				if aScan(aFld1, cField) > 0
					
					if (cAlias)->(fieldPos(cProperty)) > 0
						
						lCombo := At('_', cField) > 0 .and. !empty( x3Combo(cField, (cAlias)->&(cField)) )
						do case	
							case self:aAllFields[nX]:getType() == "date" 
								::jItens[cProperty] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cField))
							case lCombo
								::jItens[cProperty] := (cAlias)->&(cField) + '-' + x3Combo(cField, (cAlias)->&(cField))
							otherwise
								::jItens[cProperty] := (cAlias)->&(cField)
						endcase
					endif
				
				endif

				if MV_PAR10 == 1 .and. aScan(aFld2, cField) > 0
					
					if cField == "VLRRECU"
						::jItens["VLRRECU"] := (cAlias)->&('NJ_VLREC0' + cMoeda)
					elseif (cAlias)->(fieldPos(cProperty)) > 0
						lCombo := At('_', cField) > 0 .and. !empty( x3Combo(cField, (cAlias)->&(cField)) )
						if lCombo
							::jItens[cProperty] := (cAlias)->&(cField) + '-' + x3Combo(cField, (cAlias)->&(cField))
						else
							::jItens[cProperty] := (cAlias)->&(cField)
						endif
					endif
				
				elseif	MV_PAR10 == 2 .and. aScan(aFld4, cField) > 0

					if cField == "VLRRECU"
						::jItens["VLRRECU"] := (cAlias)->&('NJ_VLREC0' + cMoeda)
					elseif cField == "VLRORIG"
						::jItens["VLRORIG"] := (cAlias)->&( if(cMoeda == '1', 'NJ_VLORI0', 'NJ_VORIG') + cMoeda )
					elseif cField == "VLVENDA"
						::jItens["VLVENDA"] := (cAlias)->&('NJ_VLVEN0' + cMoeda)
					elseif cField == "TXANDPR"
						::jItens["TXANDPR"] := (cAlias)->&( if(cMoeda == '1', 'NJ_VLTAX0', 'NJ_TXDEPR') + cMoeda )
					elseif cField == "VLDPACU"
						::jItens["VLDPACU"] := (cAlias)->&( if(cMoeda == '1', 'NJ_VLACM0', 'NJ_VRDACM') + cMoeda )
					elseif (cAlias)->(fieldPos(cProperty)) > 0
						::jItens[cProperty] := (cAlias)->&(cField)
					endif
					
				endif
			
			next nX

			self:processData()  
			self:oData:appendData(::jItens) 
			
			(cAlias)->(dbSkip())
		enddo

		(cAlias)->(DBCloseArea())

	endif

	FwFreeArray(aFld1)
	FwFreeArray(aFld2)
	FwFreeArray(aFld3)
	FwFreeArray(aFld4)

return self:oData

method getQuery(oFilter as object) as character class simulationRecoverableValueAssetsSmartViewBusinessObject

local aSelFil		:= {}	as array
local cQuery		:= ""	as character
local nX			:= 1	as numeric
local oStatement	:= Nil	as object

	::oIntegratedProvider := ControlIntegratedProvider():New()
	::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .T.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"SNI"})

	if Valtype(::oIntegratedProvider:oFilterBranches["SNI"][1]) == "A"
		aSelFil := ::oIntegratedProvider:oFilterBranches["SNI"][1]
	else
		aSelFil := ::oIntegratedProvider:oFilterBranches["SNI"]
	endif
	
	cQuery := " SELECT " + self:getSQLFields()
	cQuery += "	FROM " + RetSQLName("SNI") + " SNI "
	cQuery += " "
	cQuery += " 	INNER JOIN " + RetSQLName("SNJ") + " SNJ "
	cQuery += "			ON	" + FWJoinFilial("SNJ", "SNI")
	cQuery += "			AND	SNJ.NJ_PROC		= SNI.NI_PROC "
	cQuery += "			AND	SNJ.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += " 	LEFT JOIN " + RetSQLName("SN3") + " SN3 "
	cQuery += "			ON	" + FWJoinFilial("SN3", "SNJ")
	cQuery += "			AND	SN3.N3_CBASE	= SNJ.NJ_BEM "
	cQuery += "			AND	SN3.N3_ITEM		= SNJ.NJ_ITBEM "
	cQuery += "			AND	SN3.N3_TIPO		= SNJ.NJ_TIPO "
	cQuery += "			AND	SN3.D_E_L_E_T_	= ? "
	cQuery += " "
	cQuery += "	WHERE "
	if !empty(aSelFil)
		cQuery += "			SNI.NI_FILIAL	IN (?) "
	else
		cQuery += "			SNI.NI_FILIAL	= ? "
	endif
	cQuery += "		AND	SNI.NI_PROC 	BETWEEN ? AND ? "
	cQuery += "		AND	SNI.NI_DTEMIS 	BETWEEN ? AND ? "

	If MV_PAR08 == 1
		cQuery += "		AND	SNI.NI_DTIOA	= ? "
		cQuery += "		AND	SNI.NI_STATUS	= ? "
	ElseIf MV_PAR08 == 2
		cQuery += "		AND SNI.NI_STATUS	= ? "
		cQuery += "		AND SNI.NI_DTIOA 	BETWEEN ? AND ? "
	ElseIf MV_PAR08 == 3
		cQuery += "		AND SNI.NI_STATUS	IN	(?) "
		cQuery += "		AND ( SNI.NI_DTIOA 	BETWEEN ? AND ? 
		cQuery += "			OR SNI.NI_DTIOA	= ? )
	EndIf
	
	if oFilter:hasFilter()
        cQuery += " AND " + oFilter:getSQLExpression()
    endif

	cQuery += "		AND	SNI.D_E_L_E_T_	= ? "
	cQuery += "	ORDER BY NI_FILIAL, NI_PROC, NJ_ITEM, NJ_BEM, NJ_ITBEM"

	oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

	oStatement:SetString(nX++, ' ')
	oStatement:SetString(nX++, ' ')
	if !empty(aSelFil)
		oStatement:SetIn(nX++, aSelFil)
	else
		oStatement:SetString(nX++, FWxFilial("SNI"))
	endif
	oStatement:SetString(nX++, MV_PAR01)
	oStatement:SetString(nX++, MV_PAR02)
	oStatement:SetDate(nX++, MV_PAR03)
	oStatement:SetDate(nX++, MV_PAR04)
	
	If MV_PAR08 == 1
		oStatement:SetString(nX++, ' ')
		oStatement:SetString(nX++, '1')
	ElseIf MV_PAR08 == 2
		oStatement:SetString(nX++, '2')
		oStatement:SetDate(nX++, MV_PAR05)
		oStatement:SetDate(nX++, MV_PAR06)
	ElseIf MV_PAR08 == 3
		oStatement:SetIn(nX++, {'1','2'})
		oStatement:SetDate(nX++, MV_PAR05)
		oStatement:SetDate(nX++, MV_PAR06)
		oStatement:SetString(nX++, ' ')
	Endif
	
	oStatement:SetString(nX++, ' ')

	cQuery := oStatement:GetFixQuery()

	FwFreeObj(oStatement)

return cQuery

method validaMoeda(cMoeda as character) as logical class simulationRecoverableValueAssetsSmartViewBusinessObject

local aArea			:= {}	as array
local lRet			:= .T.	as logical
local nErrorCode	:= 401	as numeric

default cMoeda	:= ''

	aArea := CTO->(GetArea())

	DbSelectArea('CTO')
	CTO->(DbSetOrder(1))	// CTO_FILIAL + CTO_MOEDA

	if empty(cMoeda)
		lRet := .F.
		self:setErrorStatus(nErrorCode, "MOEDANEXIST", STR0004)		// "O preenchimento do parâmetro 'Segunda Moeda?' é obrigatório."
	elseif !CTO->(MsSeek( FWxFilial('CTO') + cMoeda ))
		lRet := .F.
		self:setErrorStatus(nErrorCode, "MOEDAINVALID", STR0005)	// "A moeda informada no parâmetro 'Segunda Moeda?' não foi encontrada. Informe uma moeda cadastrada."
	endif

	restArea(aArea)
	FwFreeArray(aArea)

return lRet
