#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.inventory.bra.ch"

namespace totvs.protheus.backoffice.sv.atf.inventory.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3", name="Inventario", country="ALL", customTables="SN1,SN3")

//-------------------------------------------------------------------------------
/*{Protheus.doc} InventorySmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class InventorySmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method getQuery()     as character

    protected data jItens     as json
    protected data aAllFields as array
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object Class InventorySmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Inventário"

    self:aAllFields := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getDescription() as character class InventorySmartViewBusinessObject
return STR0002 //"Objeto contendo informações do Iventário dos Bens"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class InventorySmartViewBusinessObject
return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class InventorySmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class InventorySmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class InventorySmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class InventorySmartViewBusinessObject

    local cQuery            as character
    local jParams    := Nil as json
    local cN1TipoNeg := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) as character// Tipos de N1_PATRIM que aceitam Valor originais negativos
    local cN3TipoNeg := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) as character// Tipos de N3_TIPO que aceitam Valor originais negativos
    Local nX         := 0   as numeric
    local cId               as character
    local cRealName         as character
    local cCompanyName      as character
    local cBranchName       as character

    Private CBASEINI      := "" as character
    Private CBASEFIM      := "" as character
    Private CGRUPOINI     := "" as character
    Private CGRUPOFIM     := "" as character
    Private CCUSTOINI     := "" as character
    Private CCUSTOFIM     := "" as character
    Private CLOCALINI     := "" as character
    Private CLOCALFIM     := "" as character
    Private CPLACAINI     := "" as character
    Private CPLACAFIM     := "" as character
    Private NTIPON3       := 0  as numeric
    Private DDATAINV            as date

    jParams := oFilter:getParameters()  //Metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "")       //Carrega lista de parametros para a memória

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    cQuery := self:getQuery(oFilter)
    cAlias := MPSysOpenQuery(cQuery)

    while (cAlias)->(!Eof())
        ::jItens := JsonObject():new()

        For nX := 1 to len (self:aAllFields)
            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()

            If self:aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cRealName))
            ElseIf self:aAllFields[nX]:getType() == "string"
                ::jItens[cId] := AllTrim((cAlias)->&(cRealName))
            Else
                ::jItens[cId] := (cAlias)->&(cRealName)
            EndIf
        Next nX

        ::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial" ] := cBranchName
        ::jItens["NVLRRESID"  ] := Iif((cAlias)->N3_TIPO = "05" .Or. (cAlias)->N1_PATRIM $ cN1TipoNeg .Or. (cAlias)->N3_TIPO $ cN3TipoNeg,;
                                                                ((cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1))+Abs((cAlias)->(N3_VRDACM1+N3_VRCDA1)),;
                                                                ((cAlias)->(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)) - ((cAlias)->(N3_VRDACM1+N3_VRCDA1)))

        self:processData()  
        self:oData:appendData(::jItens)

        (cAlias)->(dbSkip())
    EndDO

    (cAlias)->(DBCloseArea())
    
    FreeObj(oFilter:getParameters())
    FreeObj(jParams)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCamposQry array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class InventorySmartViewBusinessObject

    local aFieldsSN1 := {}  as array
    local aFieldsSN3 := {}  as array

    aFieldsSN3 := {"N3_CBASE", "N3_ITEM", "N3_TIPO","N3_CCUSTO",;
                    "N3_VORIG1","N3_VRCACM1","N3_AMPLIA1","N3_VRDACM1","N3_VRCDA1"}   

    aFieldsSN1 := {"N1_CHAPA","N1_DESCRIC","N1_LOCAL", "N1_GRUPO",;
                    "N1_QUANTD","N1_PATRIM"}                

    self:oSchema:aliasToSchema("SN3",aFieldsSN3)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)

    self:aAllFields := self:getStructFields()            

    self:oSchema:addProperty("NVLRRESID"  ,STR0004  ,"number"  ,STR0004  ,"NVLRRESID"  ,,,,.F.)
    self:oSchema:addProperty("NomeEmpresa",STR0022  ,"string"  ,STR0022  ,"NomeEmpresa",,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0023  ,"string"  ,STR0023  ,"NomeFilial" ,,,,.F.) //"Nome Filial"

    //Seta os parametros manuais no SmartView
    self:oSchema:addParameter("CBASEINI",  STR0005, "string", .F.)	//"Do Codigo ?"
    self:oSchema:addParameter("CBASEFIM",  STR0006, "string", .F.)	//"Ate o Codigo ?"
	self:oSchema:addParameter("CGRUPOINI", STR0007, "string", .F.)	//"Do Grupo ?"
    self:oSchema:addParameter("CGRUPOFIM", STR0008, "string", .F.)  //"Ate o Grupo ?"
	self:oSchema:addParameter("CCUSTOINI", STR0009, "string", .F.)	//"Do Centro de Custo ?"
    self:oSchema:addParameter("CCUSTOFIM", STR0010, "string", .F.)	//"Ate o Centro Custo ?"
	self:oSchema:addParameter("CLOCALINI", STR0011, "string", .F.)	//"Do Armazem ?"
    self:oSchema:addParameter("CLOCALFIM", STR0012, "string", .F.)	//"Ate o Armazem ?"
    self:oSchema:addParameter("DDATAINV",  STR0013, "date",   .F.)	//"Data Inventario ?"
    self:oSchema:addParameter("NTIPON3",   STR0014, "number", .F.)	//"Quais Tipos ?"
    self:oSchema:addParameter("CPLACAINI", STR0015, "string", .F.)	//"Da Placa ?"
    self:oSchema:addParameter("CPLACAFIM", STR0016, "string", .F.)	//"Ate a Placa ?"
    self:oSchema:addParameter("MV_PAR13",  STR0017, "number", .F.)	//"Ordem do relatorio? 1 = Conta | 2 = C. Custo | 3 = Placa"
    self:oSchema:addParameter("MV_PAR14",  STR0021, "number", .F.)	//"Mostra Qtde/Vl Atu ? 1 = Sim | 2 = Não"

    //Seta a consulta padrão nos parametros manuais
    self:setCustomURL("CBASEINI",  "api/framework/v1/genericLookupService/smartview/SN1", 2)
    self:setCustomURL("CBASEFIM",  "api/framework/v1/genericLookupService/smartview/SN1", 2)
    self:setCustomURL("CGRUPOINI", "api/framework/v1/genericLookupService/smartview/SNG", 2)
    self:setCustomURL("CGRUPOFIM", "api/framework/v1/genericLookupService/smartview/SNG", 2)
    self:setCustomURL("CCUSTOINI", "api/framework/v1/genericLookupService/smartview/CTT", 2)
    self:setCustomURL("CCUSTOFIM", "api/framework/v1/genericLookupService/smartview/CTT", 2)

    //Seta combo do SX1 nos parametros manuais
    self:setCustomURL("NTIPON3", "/api/framework/treports/integratedprovider/v1/options/ATR210/MV_PAR11", 1)
    self:setCustomURL("MV_PAR14", "/api/framework/treports/integratedprovider/v1/options/ATR210/MV_PAR09", 1)

    //Seta combo customizado
    self:setCustomURL("MV_PAR13", "/api/controladoria/smartview/v1/options/atf/inventory/SVATFCombo003/1", 1)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Será usado para alterar a Query.

@author Bruno Oliveira
@since 25/10/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------

method getQuery(oFilter as Object) as character class InventorySmartViewBusinessObject

    local cQuery        := ""   as character
    local cTipoN3	    := ""   as character
    local oStatement            as object
    local nParamOrder   := 1    as numeric    

    cQuery := " SELECT " + self:getSQLFields(.F., , .F.) 
    cQuery += " FROM " + RetSQLName("SN3") + " AS SN3 "
    cQuery += " INNER JOIN " + RetSQLName("SN1") + " AS SN1 ON SN1.N1_FILIAL = SN3.N3_FILIAL  "
    cQuery += " AND SN1.N1_CBASE = SN3.N3_CBASE AND SN1.N1_ITEM = SN3.N3_ITEM AND SN1.D_E_L_E_T_= ? "
    cQuery += " WHERE SN3.N3_FILIAL = ? "
    cQuery += " AND SN3.N3_CBASE  >= ? AND SN3.N3_CBASE  <= ? "
    cQuery += " AND SN1.N1_GRUPO  >= ? AND SN1.N1_GRUPO  <= ? "
    cQuery += " AND SN3.N3_CCUSTO >= ? AND SN3.N3_CCUSTO <= ? "
    cQuery += " AND SN1.N1_LOCAL  >= ? AND SN1.N1_LOCAL  <= ? "
    cQuery += " AND SN1.N1_CHAPA  >= ? AND SN1.N1_CHAPA  <= ? "
    cQuery += " AND SN3.N3_BAIXA  =  ? AND SN3.D_E_L_E_T_=  ? "

    If NTIPON3 == 1     //Se for igual a (1) imprimir apenas o tipo 01
        cTipoN3	:= '01' //Quais tipos? (1)- Imprimir apenas o tipo 01 ou (2)Imprimir Todos
        cQuery += " AND SN3.N3_TIPO in ( ? )"
    EndIf

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    //Ordem dos Movimentos // "Codigo" // "C Custo"	// "Placa"
    If MV_PAR13 == 1
        cQuery += " ORDER BY N3_FILIAL, N3_CBASE, N3_ITEM, N3_TIPO, N3_BAIXA, N3_SEQ"
    ElseIf MV_PAR13 == 2
        cQuery +=  "ORDER BY N3_FILIAL, N3_CCUSTO, N3_CBASE, N3_ITEM, N3_TIPO, N3_BAIXA"
    ElseIf MV_PAR13 == 3
        cQuery +=  "ORDER BY N3_FILIAL, N1_CHAPA"
    EndIf

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)  

    oStatement:SetString(nParamOrder++,  ' ')
    oStatement:SetString(nParamOrder++, FWxFilial("SN1"))
    oStatement:SetString(nParamOrder++, CBASEINI)  
    oStatement:SetString(nParamOrder++, CBASEFIM)
    oStatement:SetString(nParamOrder++, CGRUPOINI)
    oStatement:SetString(nParamOrder++, CGRUPOFIM)
    oStatement:SetString(nParamOrder++, CCUSTOINI)
    oStatement:SetString(nParamOrder++, CCUSTOFIM)
    oStatement:SetString(nParamOrder++, CLOCALINI)
    oStatement:SetString(nParamOrder++, CLOCALFIM)
    oStatement:SetString(nParamOrder++, CPLACAINI)
    oStatement:SetString(nParamOrder++, CPLACAFIM)
    oStatement:SetString(nParamOrder++, '0')
    oStatement:SetString(nParamOrder++, ' ')
    If NTIPON3 == 1
        oStatement:SetString(nParamOrder++, cTipoN3)
    EndIf
    
    cQuery := oStatement:GetFixQuery()

return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} SVATFCombo003
    Função para retornar combobox de parâmetro
   
    @author Renan Gremes
    @since 07/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

Function SVATFCombo003(cOpcao)

    local aRet := {} as array

    if cOpcao == "1"
        aRet := {STR0018, STR0019, STR0020} //"Conta", "C Custo", "Placa"
    endif

return aRet
