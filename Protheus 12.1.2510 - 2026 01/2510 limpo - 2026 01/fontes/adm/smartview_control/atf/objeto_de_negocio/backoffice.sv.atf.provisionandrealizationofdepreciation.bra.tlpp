#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.atf.provisionandrealizationofdepreciation.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="FNB, FND, FNE", name="Provisão e Realização da Depreciação", country="ALL", customTables="FNB, FND, FNE")

//-------------------------------------------------------------------
/*/{Protheus.doc} ProvisionandRealizationofDepreciationSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Provisão e Realização da Depreciação
para o TReports

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

class ProvisionandRealizationofDepreciationSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    protected method getQuery()     as character
    protected method setDataList()  as object
    protected method loadParams()

    protected method AFR432Sld()    as numeric
    protected method AFR432Mov()    as array

    protected data jDados        as json
    protected data cSelectFields as character
    protected data cWhereQuery   as character
    protected data cAliasQuery   as character
    protected data cAlias432Mov  as character
    protected data cTipo         as character
    protected data cCompanyName  as character
    protected data cBranchName   as character
    protected data aAllFields    as array
    protected data aSelFil       as array
    protected data oQuery        as object
    protected data oQuery432Mov  as object
    protected data oIntegratedProvider as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method new() class ProvisionandRealizationofDepreciationSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) // "Provisão e Realização da Depreciação"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("AFR432")

    self:loadParams()

    self:cSelectFields := ""
    self:cWhereQuery   := ""
    self:cAliasQuery   := ""
    self:cAlias432Mov  := ""
    self:cTipo         := ""
    self:aAllFields    := {}
    self:aSelFil       := {}

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de descrição

@return character: descrição

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getDescription() as character class ProvisionandRealizationofDepreciationSmartViewBusinessObject
return STR0002 // "Objeto contendo informações sobre o relatório Provisão e Realização da Depreciação"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class ProvisionandRealizationofDepreciationSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getData(nPage as numeric, oFilter as object) as object class ProvisionandRealizationofDepreciationSmartViewBusinessObject
    local cQuery := "" as character

    //Carrega lista de parametros para a memória
    self:loadParams(oFilter)

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN3"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
    EndIf    

    //Carrega os dados da query
    cQuery := self:getQuery(oFilter)
    self:cAliasQuery  := MPSysOpenQuery(cQuery)
    self:setDataList(oFilter, nPage)
    (self:cAliasQuery)->( DBCloseArea() )

    FreeObj(oFilter:getParameters())
    FreeObj(::oIntegratedProvider)
    FwFreeArray(::aSelFil)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getSchema() as object class ProvisionandRealizationofDepreciationSmartViewBusinessObject

    local aFieldsFNB := {} as array
    local aFieldsFND := {} as array
    local aFieldsFNE := {} as array

    aFieldsFNB := {"FNB_FILIAL", "FNB_CODPRJ", "FNB_REVIS", "FNB_DESC", "FNB_DTREV", "FNB_DTENCR"}
    aFieldsFND := {"FND_ETAPA", "FND_ITEM", "FND_DTPROV"}
    aFieldsFNE := {"FNE_TPDEPR", "FNE_VORIG", "FNE_VRDACM", "FNE_PERDEP", "FNE_TAXA", "FNE_VLMXDP", "FNE_VLSALV", "FNE_PRDEST", "FNE_CODIND", "FNE_TPATF", "FNE_TPSALD"}

    //Transforma o alias em um schema com os campos definidos no array
    self:oSchema:aliasToSchema("FNB", aFieldsFNB)
    self:oSchema:aliasToSchema("FND", aFieldsFND)
    self:oSchema:aliasToSchema("FNE", aFieldsFNE)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    //Adiciona campos customizados
    self:oSchema:addProperty("MESANO"        , STR0004, "string", STR0004, "MESANO")        // "Mês/Ano"
    self:oSchema:addProperty("SALDODEPRF"    , STR0005, "number", STR0005, "SALDODEPRF")    // "Saldo"
    self:oSchema:addProperty("VORIGAVP"      , STR0006, "number", STR0006, "VORIGAVP")      // "Vlr Ori AVP"
    self:oSchema:addProperty("VRDACMAVP"     , STR0007, "number", STR0007, "VRDACMAVP")     // "Dep Acm AVP"
    self:oSchema:addProperty("SALDOPAVP"     , STR0008, "number", STR0008, "SALDOPAVP")     // "Saldo AVP"
    self:oSchema:addProperty("SOMAVORIGAVP"  , STR0009, "number", STR0009, "SOMAVORIGAVP")  // "Total Orig"
    self:oSchema:addProperty("SOMAVRDACMAVP" , STR0010, "number", STR0010, "SOMAVRDACMAVP") // "Total Dep"
    self:oSchema:addProperty("SOMASALDOPAVP" , STR0011, "number", STR0011, "SOMASALDOPAVP") // "Saldo Total"
    self:oSchema:addProperty("NomeEmpresa"   , STR0012, "string", STR0012, "NomeEmpresa")   // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    , STR0013, "string", STR0013, "NomeFilial")    // "Nome Filial"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0014, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)   

    FwFreeArray(aFieldsFNB)
    FwFreeArray(aFieldsFND)
    FwFreeArray(aFieldsFNE)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Retorna a query a ser executada na função getData

@return character: cQuery

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getQuery(oFilter) as character class ProvisionandRealizationofDepreciationSmartViewBusinessObject
    
    local cQuery      := ""       as character
    local cSpace      := Space(1) as character
    local lRevAtv     := .F.      as logical
    local nParamOrder := 1        as numeric

    lRevAtv := MV_PAR04 == 1
    self:cTipo	:= Iif( MV_PAR06 == 1, "01", "10" )

    //Define a query do Objeto de Negócio
    cQuery := "SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema

    //Se alguma classe "filha" passar mais campos no select pelo atributo cSelectFields
    cQuery += Iif(!Empty(self:cSelectFields), ", "+self:cSelectFields, "")

    cQuery += "FROM "
    cQuery +=   RetSQLName("FNB") + " FNB "
    cQuery += "INNER JOIN " + RetSQLName("FNC") + " FNC ON FNB_FILIAL = FNC_FILIAL "
	cQuery += " AND FNB_CODPRJ = FNC_CODPRJ "
	cQuery += " AND FNB_REVIS  = FNC_REVIS "
    cQuery += " AND FNB_REVIS  = FNC_REVIS "
    cQuery += " AND FNC.D_E_L_E_T_ = ? "
    
    cQuery += "INNER JOIN " + RetSQLName("FND") + " FND ON FNC_FILIAL = FND_FILIAL "
	cQuery += " AND FNC_CODPRJ = FND_CODPRJ "
	cQuery += " AND FNC_REVIS  = FND_REVIS "
    cQuery += " AND FNC_ETAPA  = FND_ETAPA "
    cQuery += " AND FND.D_E_L_E_T_ = ? "

    cQuery += "INNER JOIN " + RetSQLName("FNE") + " FNE ON FND_FILIAL = FNE_FILIAL "
	cQuery += " AND FND_CODPRJ = FNE_CODPRJ "
	cQuery += " AND FND_REVIS  = FNE_REVIS "
    cQuery += " AND FND_ETAPA  = FNE_ETAPA "
    cQuery += " AND FNE.D_E_L_E_T_ = ? "

    cQuery += "WHERE "
    cQuery += " FNB.FNB_DTINIC <= ? "
    cQuery += " AND FNB.FNB_CODPRJ BETWEEN ? AND ? "

    if lRevAtv
        cQuery += " AND ((FNB_STATUS = ?) OR (FNB_STATUS = ? AND FNB_DTENCR > ?)) "
    else 
        cQuery += " AND FNB_REVIS = ? "
    endif
    
    If !Empty(self:aSelFil)
        cQuery += " AND FNB.FNB_FILIAL IN ( ? ) "
    EndIf

    cQuery += " AND FNE_TPATF = ? "
    cQuery += " AND FNE_TPSALD = ? "

    //Se alguma classe "filha" passar mais condições no where pelo atributo cWhereQuery
    cQuery += Iif(!Empty(self:cWhereQuery), self:cWhereQuery, "")

    cQuery += " AND FNB.D_E_L_E_T_ = ? "    

    //Os filtros serão setados na interface do novo TReports
    cQuery += Iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY FNB_FILIAL,FNB_CODPRJ,FNB_REVIS"

    cQuery := ChangeQuery(cQuery)

    self:oQuery:= FWPreparedStatement():New(cQuery)

    self:oQuery:SetString(nParamOrder++,cSpace)
    self:oQuery:SetString(nParamOrder++,cSpace)
    self:oQuery:SetString(nParamOrder++,cSpace)

    self:oQuery:SetDate(nParamOrder++,MV_PAR01)
    self:oQuery:SetString(nParamOrder++,MV_PAR02)
    self:oQuery:SetString(nParamOrder++,MV_PAR03)
    if lRevAtv
        self:oQuery:SetString(nParamOrder++,"1")
        self:oQuery:SetString(nParamOrder++,"2")
        self:oQuery:SetDate(nParamOrder++,MV_PAR01)
    else
        self:oQuery:SetString(nParamOrder++,MV_PAR05)
    endif

    If !Empty(self:aSelFil)
        self:oQuery:SetIn(nParamOrder++,::aSelFil)
    EndIf
    
    self:oQuery:SetString(nParamOrder++,self:cTipo)
    self:oQuery:SetString(nParamOrder++,MV_PAR07)
    self:oQuery:SetString(nParamOrder++,cSpace)

    cQuery := self:oQuery:GetFixQuery()

Return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} setDataList
Adiciona dados ao objeto de negócio

@return objeto: self:oData

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method setDataList(oFilter as object, nPage as numeric) as object class ProvisionandRealizationofDepreciationSmartViewBusinessObject
    
    local cMesAno       := "" as character
    local cTpDepr       := "" as character
    local nVlOrig       := 0  as numeric
    local nVlDepAcm     := 0  as numeric
    local nSaldo        := 0  as numeric
    local nVlOrigAVP    := 0  as numeric
    local nVlDepAcmAVP  := 0  as numeric
    local nSaldoAVP     := 0  as numeric
    local nVlTotalOrig  := 0  as numeric
    local nVlTotalDep   := 0  as numeric
    local nVlTotalSaldo := 0  as numeric
    local nX            := 0  as numeric

    //Posiciona no registro inicial
    (self:cAliasQuery)->(DBGoTop())

    While !(self:cAliasQuery)->(Eof())

        //Tratamento de valores
        nVlOrig      := self:AFR432Sld( (self:cAliasQuery)->FNB_FILIAL,(self:cAliasQuery)->FNB_CODPRJ,(self:cAliasQuery)->FNB_REVIS,(self:cAliasQuery)->FND_ETAPA,(self:cAliasQuery)->FND_ITEM,(self:cAliasQuery)->FNE_TPATF,(self:cAliasQuery)->FNE_TPSALD,MV_PAR01,1,(self:cAliasQuery)->FNB_DTREV)
        nVlDepAcm    := self:AFR432Sld( (self:cAliasQuery)->FNB_FILIAL,(self:cAliasQuery)->FNB_CODPRJ,(self:cAliasQuery)->FNB_REVIS,(self:cAliasQuery)->FND_ETAPA,(self:cAliasQuery)->FND_ITEM,(self:cAliasQuery)->FNE_TPATF,(self:cAliasQuery)->FNE_TPSALD,MV_PAR01,2,(self:cAliasQuery)->FNB_DTREV)
        nSaldo       := nVlOrig - nVlDepAcm

        if '10' $ self:cTipo
            nVlOrigAVP    := self:AFR432Sld( (self:cAliasQuery)->FNB_FILIAL,(self:cAliasQuery)->FNB_CODPRJ,(self:cAliasQuery)->FNB_REVIS,(self:cAliasQuery)->FND_ETAPA,(self:cAliasQuery)->FND_ITEM,(self:cAliasQuery)->FNE_TPATF,(self:cAliasQuery)->FNE_TPSALD,MV_PAR01,3,(self:cAliasQuery)->FNB_DTREV)
            nVlDepAcmAVP  := self:AFR432Sld( (self:cAliasQuery)->FNB_FILIAL,(self:cAliasQuery)->FNB_CODPRJ,(self:cAliasQuery)->FNB_REVIS,(self:cAliasQuery)->FND_ETAPA,(self:cAliasQuery)->FND_ITEM,(self:cAliasQuery)->FNE_TPATF,(self:cAliasQuery)->FNE_TPSALD,MV_PAR01,4,(self:cAliasQuery)->FNB_DTREV)
            nSaldoAVP     := nVlOrigAVP - nVlDepAcmAVP
            nVlTotalOrig  := nVlOrig + nVlOrigAVP
            nVlTotalDep   := nVlDepAcm + nVlDepAcmAVP
            nVlTotalSaldo := nSaldo + nSaldoAVP
        endif
        
        //Tratamento mês/ano
        cMesAno := cValtoChar(Month(MV_PAR01)) + "/" + cValtoChar(Year(MV_PAR01))

        //Tratamento tipo depreciacao
        cTpDepr := Posicione("SN0", 1, FWxFilial("SN0")+"04"+(self:cAliasQuery)->FNE_TPDEPR, "N0_DESC01")

        self:jDados := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAliasQuery)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAliasQuery)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAliasQuery)->&(self:aAllFields[nX]:getRealName()))
            else
                self:jDados[self:aAllFields[nX]:getName()] := (self:cAliasQuery)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

        //Tratamento em campos da query
        self:jDados["FNE_TPDEPR"]    := cTpDepr
        self:jDados["FNE_VORIG"]     := nVlOrig
        self:jDados["FNE_VRDACM"]    := nVlDepAcm

        //Popula campos customizados
        self:jDados["MESANO"]        := cMesAno        
        self:jDados["SALDODEPRF"]    := nSaldo
        self:jDados["VORIGAVP"]      := nVlOrigAVP
        self:jDados["VRDACMAVP"]     := nVlDepAcmAVP
        self:jDados["SALDOPAVP"]     := nSaldoAVP
        self:jDados["SOMAVORIGAVP"]  := nVlTotalOrig
        self:jDados["SOMAVRDACMAVP"] := nVlTotalDep
        self:jDados["SOMASALDOPAVP"] := nVlTotalSaldo
        self:jDados["NomeEmpresa"]   := ::cCompanyName
        self:jDados["NomeFilial"]    := ::cBranchName

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAliasQuery)->( dbSkip() )

    EndDo

    FreeObj(self:jDados)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} loadParams
Carrega os parâmetros do oFilter para a memória

@param oFilter, objeto, contém o filtro do TReports

@author Renan Gremes
@since 15/12/2023
@version 12.1.2210
*/
//-------------------------------------------------------------------

method loadParams(oFilter as object) class ProvisionandRealizationofDepreciationSmartViewBusinessObject
    if oFilter <> NIL
        //Carrega lista de parametros para a memória
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)
    endIf
return

//-------------------------------------------------------------------
/*{Protheus.doc} AFR432Sld
Calcula e retorna o valor original e depreciacao do Ativo
nTipoVal == 1 - Valor Original
nTipoVal == 2 - Depreciacao   
nTipoVal == 3 - Valor de AVP  
nTipoVal == 4 - Deprec ACM AVP

@return numerico: nRetValor

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method AFR432Sld(cXFil,cCodPrj,cRevis,cEtapa,cItemPrj,cTipoAtf,cTipoSld,dDataRef,nTipoVal,dDataRev) as numeric class ProvisionandRealizationofDepreciationSmartViewBusinessObject

    local cTipoFiscal := "" as character
    local __cChave    := "" as character
    local nRetValor   := 0  as numeric
    local aAux        := {} as array
    local aArea       := GetArea() as array
    local aAreaSN1    := SN1->(GetArea()) as array

    SN1->(dbSetOrder(9)) //N1_FILIAL+N1_PROJETO+N1_PROJREV+N1_PROJETP+N1_PROJITE+N1_CBASE+N1_ITEM

    If __cChave != cXFil + cCodPrj + cRevis + cEtapa + cItemPrj + cTipoAtf + cTipoSld
        
        cTipoFiscal := ATFXTpBem(1)
        __aSaldo    := Array(4)
        aFill(__aSaldo,0)
        
        __cChave    := cXFil + cCodPrj + cRevis + cEtapa + cItemPrj + cTipoAtf + cTipoSld
        
        If SN1->(MSSeek( cXFil + cCodPrj + cRevis + cEtapa + cItemPrj )) .And. cToD(dDataRev) <= dDataRef 
            aAux := self:AFR432Mov(cXFil,SN1->N1_CBASE,SN1->N1_ITEM,cTipoAtf,cTipoSld,dDataRef)
            __aSaldo[1] := aAux[1]
            __aSaldo[2] := aAux[2]
            
            If !(cTipoAtf $ cTipoFiscal) // Se o bem for gerencial busca a informação do tipo 14   
                aAux := self:AFR432Mov(cXFil,SN1->N1_CBASE,SN1->N1_ITEM,"14",cTipoSld,dDataRef)
                __aSaldo[3] := aAux[1]
                __aSaldo[4] := aAux[2]
            EndIf
        EndIf
    EndIf

    __aSaldo := Iif(Empty(__aSaldo),{0,0,0,0},__aSaldo)

    nRetValor := __aSaldo[nTipoVal]

    RestArea(aAreaSN1)
    RestArea(aArea)

Return nRetValor

//-------------------------------------------------------------------
/*{Protheus.doc} AFR432Mov
Retorna o valor original e a depreciacao acumulada de um bem 
atrelado a um projeto

@return array: aRet

@author Renan Gremes
@since 15/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method AFR432Mov(cXFil,cBase,cItem,cTipoAtf,cTipoSld,dDataRef) as array class ProvisionandRealizationofDepreciationSmartViewBusinessObject

    local cQuery      := "" as character    
    local cFilBem     := "" as character
    local cCodBase    := "" as character
    local cTipo       := "" as character
    local cSeq        := "" as character
    local cSeqReav    := "" as character
    local cItemN3     := "" as character
    local cTipoFiscal := "" as character
    local nVOrig	  := 0  as numeric
    local nDeprec	  := 0  as numeric
    local aSldAux	  := {} as array
    local aRet        := {} as array
    local aArea       := GetArea() as array

    local POS_VLR_ORGINIAL   := 1 as numeric
    local POS_DEPR_FISCAL    := 3 as numeric
    local POS_DEPR_GERENCIAL := 5 as numeric

    cTipoFiscal := ATFXTpBem(1)
    aRet        := {0,0}

    cQuery := "SELECT "
    cQuery += "N3_FILIAL,  "
    cQuery += "N3_CBASE,   "
    cQuery += "N3_ITEM,	   "
    cQuery += "N3_TIPO,	   "
    cQuery += "N3_SEQREAV, "
    cQuery += "N3_SEQ  	   "
    cQuery += "FROM "
    cQuery += RetSQLName("SN3") + " SN3 "
    cQuery += "WHERE "    
    cQuery += "	SN3.N3_FILIAL = ?"
    cQuery += "	AND SN3.N3_CBASE = ?"
    cQuery += "	AND SN3.N3_ITEM	 = ?"
    cQuery += "	AND SN3.N3_TIPO	 = ?"
    cQuery += "	AND SN3.N3_TPSALDO = ?"
    cQuery += "	AND SN3.D_E_L_E_T_ = ?"

    self:oQuery432Mov := FWPreparedStatement():New(cQuery)

    self:oQuery432Mov:SetString(1,cXFil)
    self:oQuery432Mov:SetString(2,cBase)
    self:oQuery432Mov:SetString(3,cItem)
    self:oQuery432Mov:SetString(4,cTipoAtf)
    self:oQuery432Mov:SetString(5,cTipoSld)
    self:oQuery432Mov:SetString(6," ")

    //Executa a query
    cQuery := self:oQuery432Mov:GetFixQuery()
    self:cAlias432Mov := MPSysOpenQuery(cQuery)
    
    (self:cAlias432Mov)->(dbGoTop())

    //Verifica se retornou algum registro e armazena na variável
    if (self:cAlias432Mov)->(!Eof())
        cFilBem  := (self:cAlias432Mov)->N3_FILIAL
        cCodBase := (self:cAlias432Mov)->N3_CBASE
        cItemN3  := (self:cAlias432Mov)->N3_ITEM
        cTipo    := (self:cAlias432Mov)->N3_TIPO
        cSeq     := (self:cAlias432Mov)->N3_SEQ
        cSeqReav := (self:cAlias432Mov)->N3_SEQREAV
        
        aSldAux  := SaldoSN4(cFilBem,cCodBase,cItem,cTipo,cSeq,cSeqReav,,dDataRef,,,cTipoSld)
        
        if Len(aSldAux) > 0
            nVOrig	:= aSldAux[1][2][POS_VLR_ORGINIAL]
            if (self:cAlias432Mov)->N3_TIPO $ cTipoFiscal
                nDeprec	:= aSldAux[1][2][POS_DEPR_FISCAL]
            else
                nDeprec	:= aSldAux[1][2][POS_DEPR_GERENCIAL]
            endif
        endif
        aRet[1] := nVOrig
        aRet[2] := nDeprec
    endif

    (self:cAlias432Mov)->( DBCloseArea() )
    RestArea(aArea)

Return aRet
