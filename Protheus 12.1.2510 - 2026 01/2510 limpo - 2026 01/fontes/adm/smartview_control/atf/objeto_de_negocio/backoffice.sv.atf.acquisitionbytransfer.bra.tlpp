#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#Include "Protheus.ch"   
#include "backoffice.sv.atf.acquisitionbytransfer.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1, SN3, SN4, CT1", name="Aquisição por Transferência", country="ALL", , customTables="CT1,SN1,SN3,SN4")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AcquisitionByTransferSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Aquisição por Transferência para o SmartView
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class AcquisitionByTransferSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new() as object
    public method getDescription() as character
    public method getAreas() as array
    public method getData() as object
    public method getSchema() as object

    //usados na internacionalização
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

    protected method getQuery() as character
    
    protected data aAllFields as array
    protected data oIntegratedProvider as object
    protected data jItens  as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() as object Class AcquisitionByTransferSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)//"Aquisição por Transferência"
    self:setPergunte("AFR250") //Indica o pergunte que será utilizado no relatório
    
    ::aAllFields := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getDescription() as character class AcquisitionByTransferSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do relatório Aquisição por Transferência"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getAreas() as array class AcquisitionByTransferSmartViewBusinessObject
Return {STR0003} //"Ativo Fixo"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class AcquisitionByTransferSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class AcquisitionByTransferSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class AcquisitionByTransferSmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 

 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class AcquisitionByTransferSmartViewBusinessObject

    Local cN1TipoNeg    := Alltrim(SuperGetMv("MV_N1TPNEG",.F.,"")) as Character// Tipos de N1_PATRIM que aceitam Valor originais negativos
    Local cN3TipoNeg    := Alltrim(SuperGetMv("MV_N3TPNEG",.F.,"")) as Character// Tipos de N3_TIPO que aceitam Valor originais negativos
    Local cMovim        := ""  as Character    
    Local cDescSld      := ""  as Character
    Local cDescSld1     := ""  as Character
    Local cQuery        := ""  as Character
    Local cAlias        := ""  as Character
    Local cCompanyName  := ""  as Character
    Local cBranchName   := ""  as Character
    Local nX, nY        := 0   as Numeric
    Local nOrDesAn      := 0   as Numeric
    Local nOrDesAt      := 0   as Numeric
    Local jParams       := Nil as json   

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "AFR250")//Carrega lista de parametros para a memória
    FreeObj(jParams)	//Elimina da memória a instância do objeto informado como parâmetro.

    cQuery := self:getQuery(oFilter)
    cAlias := MPSysOpenQuery(cQuery)

    //Salva nome empresa e filial logada
    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	cAliasSN3 := cAlias
	cAliasSN1 := cAlias

    self:aAllFields := self:getStructFields()

    while (cAlias)->(!Eof())
        
        If (cAlias)->(N4_OCORR == "15" .Or. N4_OCORR == "01")
            cTipoMov := "Origem"
            nOrDesAn := 1
            nOrDesAt := 1
        Else
            cTipoMov := "Destino"
            nOrDesAn := 2
            nOrDesAt := 2
        EndIf

        cMovim := (cAlias)->N4_CODBAIX
        
        //Pega descrição do tipo saldo 1
        if SX5->(MsSeek(xFilial("SX5")+"SL"+"1"))
            cDescSld1 := Alltrim(SX5->(X5Descri()))
        endif

        while (cAlias)->(!Eof())   .And. (cAlias)->N4_CODBAIX == cMovim .And. nOrDesAn == nOrDesAt
       
            For nx := 1 to 4
                //Descrição Tipo Saldo
                cDescSld := ""

                if Empty((cAlias)->N3_TPSALDO) .Or. (cAlias)->N3_TPSALDO == "1"
                   cDescSld := cDescSld1
                else
                    if SX5->(MsSeek(FWxFilial("SX5")+"SL"+(cAlias)->N3_TPSALDO))
                        cDescSld := Alltrim(SX5->(X5Descri()))
                    endif
                endif

                ::jItens := JsonObject():new()

                ::jItens[ "N4_DATA" ]           := iif(!empty((cAlias)->N4_DATA), FwTimeStamp(5, StoD((cAlias)->N4_DATA), "00:00:00"), nil) 
                ::jItens[ "N4_CODBAIX" ]        := (cAlias)->N4_CODBAIX
                ::jItens[ "N4_CBASE" ]          := (cAlias)->N4_CBASE
                ::jItens[ "N4_ITEM" ]           := (cAlias)->N4_ITEM
                ::jItens[ "N3_TIPO" ]           := (cAlias)->N3_TIPO
                ::jItens[ "N3_TPSALDO" ]        := cDescSld
                ::jItens[ "N1_DESCRIC" ]        := (cAlias)->N1_DESCRIC
                ::jItens[ "N1_AQUISIC" ]        := iif(!empty((cAlias)->N1_AQUISIC), FwTimeStamp(5, StoD((cAlias)->N1_AQUISIC), "00:00:00"), nil) 
                ::jItens[ "N1_CHAPA" ]          := (cAlias)->N1_CHAPA
                ::jItens[ "N3_CUSTBEM" ]        := (cAlias)->N3_CUSTBEM
                ::jItens[ "N3_CCONTAB" ]        := (cAlias)->N3_CCONTAB
                ::jItens[ "CT1_DESC01" ]        := (cAlias)->CT1_DESC01
                ::jItens[ "N4_OCORR" ]          := (cAlias)->N4_OCORR                
                ::jItens[ "NomeEmpresa" ]       := cCompanyName
                ::jItens[ "NomeFilial" ]        := cBranchName
                ::jItens[ "cTipoMov" ]          := cTipoMov 
                ::jItens[ "IDRec" ]             := (cAlias)->IDREC
                
                If nx == 1 
                    ::jItens[ "cDescValor" ]      := STR0004 // Valor Original
                    ::jItens[ "nValMoeda1" ]      := (cAliasSN3)->(N3_VORIG1 + N3_AMPLIA1) 
                    ::jItens[ "nValMoeda2" ]      := (cAliasSN3)->(N3_VORIG2 + N3_AMPLIA2) 
                    ::jItens[ "nValMoeda3" ]      := (cAliasSN3)->(N3_VORIG3 + N3_AMPLIA3)
                    ::jItens[ "nValMoeda4" ]      := (cAliasSN3)->(N3_VORIG4 + N3_AMPLIA4)  
                    ::jItens[ "nValMoeda5" ]      := (cAliasSN3)->(N3_VORIG5 + N3_AMPLIA5) 

                ElseIf nx == 2
                    ::jItens[ "cDescValor" ]      := STR0005 //Depreciação Acumulada
                    ::jItens[ "nValMoeda1" ]      := (cAliasSN3)->N3_VRDACM1 
                    ::jItens[ "nValMoeda2" ]      := (cAliasSN3)->N3_VRDACM2 
                    ::jItens[ "nValMoeda3" ]      := (cAliasSN3)->N3_VRDACM3
                    ::jItens[ "nValMoeda4" ]      := (cAliasSN3)->N3_VRDACM4 
                    ::jItens[ "nValMoeda5" ]      := (cAliasSN3)->N3_VRDACM5 

                ElseIf nx == 3
                    ::jItens[ "cDescValor" ]      := STR0006 // Correção
                    ::jItens[ "nValMoeda1" ]      := (cAliasSN3)->N3_VRCACM1 
                    ::jItens[ "nValMoeda2" ]      := 0
                    ::jItens[ "nValMoeda3" ]      := 0
                    ::jItens[ "nValMoeda4" ]      := 0
                    ::jItens[ "nValMoeda5" ]      := 0
                Else
                    ::jItens[ "cDescValor" ]      := STR0007 //Residual
                    ::jItens[ "nValMoeda1" ]      := (cAliasSN3)->(N3_VORIG1 + N3_AMPLIA1 + (N3_VRDACM1 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) + N3_VRCACM1 )
                    ::jItens[ "nValMoeda2" ]      := (cAliasSN3)->(N3_VORIG2 + N3_AMPLIA2 + (N3_VRDACM2 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) ) 
                    ::jItens[ "nValMoeda3" ]      := (cAliasSN3)->(N3_VORIG3 + N3_AMPLIA3 + (N3_VRDACM3 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) ) 
                    ::jItens[ "nValMoeda4" ]      := (cAliasSN3)->(N3_VORIG4 + N3_AMPLIA4 + (N3_VRDACM4 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) )
                    ::jItens[ "nValMoeda5" ]      := (cAliasSN3)->(N3_VORIG5 + N3_AMPLIA5 + (N3_VRDACM5 * Iif(N3_TIPO = "05" .Or. (cAliasSn1)->N1_PATRIM $ cN1TipoNeg .Or. N3_TIPO $ cN3TipoNeg,1,(-1)) ) ) 
                        
                EndIf

                //Popula campos customizados adicionados pelo cliente
                For nY := 1 To Len(self:aAllFields)
                    if self:aAllFields[nY]:getCustomField()
                        if self:aAllFields[nY]:getType() == "date"
                            ::jItens[self:aAllFields[nY]:getName()] := iif(!empty((cAlias)->&(self:aAllFields[nY]:getRealName())), FwTimeStamp(5, StoD((cAlias)->&(self:aAllFields[nY]:getRealName())), "00:00:00"), nil)
                        elseif self:aAllFields[nY]:getType() == "string"
                            ::jItens[self:aAllFields[nY]:getName()] := AllTrim((cAlias)->&(self:aAllFields[nY]:getRealName()))
                        else
                            ::jItens[self:aAllFields[nY]:getName()] := (cAlias)->&(self:aAllFields[nY]:getRealName())
                        endif
                    endif
                Next nY

                self:processData()  
                self:oData:appendData(::jItens)   
            Next nx

            (cAlias)->(dbSkip())

            If lOrDes := (cAlias)->(N4_OCORR == "15" .Or. N4_OCORR == "01")
                nOrDesAt := 1
            Else
                nOrDesAt := 2
		    EndIf
        EndDo

    EndDO
    (cAlias)->(DBCloseArea())
      
    self:setPageSize(100)
     
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema

@author Bruno Oliveira
@since 20/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class AcquisitionByTransferSmartViewBusinessObject

    local aFieldsCT1 := {} as array
    local aFieldsSN1 := {} as array
    local aFieldsSN3 := {} as array
    local aFieldsSN4 := {} as array
    local lCanFilter := .F. as logical

    aFieldsCT1 := {"CT1_DESC01"}
    aFieldsSN1 := {"N1_DESCRIC","N1_AQUISIC","N1_PATRIM","N1_CHAPA"}
    aFieldsSN3 := {"N3_CUSTBEM","N3_CCONTAB","N3_TIPO","N3_TPSALDO"}
    aFieldsSN4 := {"N4_CBASE","N4_ITEM","N4_OCORR","N4_CODBAIX","N4_DATA"}

    self:oSchema:aliasToSchema("CT1",aFieldsCT1)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)
    self:oSchema:aliasToSchema("SN4",aFieldsSN4)

    self:addProperty("cTipoMov",    STR0008, "string", STR0008, "cTipoMov"   ,,,,lCanFilter) // Tipo Movimento
    self:addProperty("cDescValor",  STR0009, "string", STR0009, "cDescValor" ,,,,lCanFilter) // Descr. Valor
    self:addProperty("nValMoeda1",  STR0010, "number", STR0010, "nValMoeda1" ,,,,lCanFilter) // Moeda 1 
    self:addProperty("nValMoeda2",  STR0011, "number", STR0011, "nValMoeda2" ,,,,lCanFilter) // Moeda 2 
    self:addProperty("nValMoeda3",  STR0012, "number", STR0012, "nValMoeda3" ,,,,lCanFilter) // Moeda 3 
    self:addProperty("nValMoeda4",  STR0013, "number", STR0013, "nValMoeda4" ,,,,lCanFilter) // Moeda 4 
    self:addProperty("nValMoeda5",  STR0014, "number", STR0014, "nValMoeda5" ,,,,lCanFilter) // Moeda 5
    self:addProperty("IDRec",       STR0015, "number", STR0015, "IDRec"      ,,,,lCanFilter) // ID REC
    self:addProperty("NomeEmpresa", STR0016, "string", STR0016, "NomeEmpresa",,,,lCanFilter) // Nome Empresa
    self:addProperty("NomeFilial",  STR0017, "string", STR0017, "NomeFilial" ,,,,lCanFilter) // Nome Filial

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    @author Bruno Oliveira
    @since 20/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getQuery(oFilter as Object) as Character class AcquisitionByTransferSmartViewBusinessObject

    local cQuery        := ""   as character
    local oStatement            as object
    local nParamOrder   := 1    as numeric
    local cSpace        := space(1) as character
    local lAtfCusPrv    := AFXAtCsPrv() as logical

    cQuery := "SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += " ,N3_VORIG1,  N3_VORIG2,  N3_VORIG3,  N3_VORIG4, N3_VORIG5"
    cQuery += " ,N3_AMPLIA1, N3_AMPLIA2, N3_AMPLIA3, N3_AMPLIA4, N3_AMPLIA5"
    cQuery += " ,N3_VRDACM1, N3_VRDACM2, N3_VRDACM3, N3_VRDACM4, N3_VRDACM5"
    cQuery += " ,N3_VRCACM1, SN3.R_E_C_N_O_ AS IDREC"
    cQuery += " FROM "+RetSqlName("SN4")+" SN4 "
                    
    cQuery += " LEFT JOIN "+RetSqlName("SN3")+" SN3 ON SN3.N3_FILIAL = SN4.N4_FILIAL "
    cQuery += " AND SN3.N3_CBASE  = SN4.N4_CBASE " 
    cQuery += " AND SN3.N3_ITEM   = SN4.N4_ITEM " 
    cQuery += " AND SN3.N3_TIPO   = SN4.N4_TIPO " 
    cQuery += " AND SN3.D_E_L_E_T_= ? " 

    cQuery += " LEFT JOIN "+RetSqlName("SN1")+" SN1 ON SN1.N1_FILIAL = SN4.N4_FILIAL "
    cQuery += " AND SN1.N1_CBASE = SN4.N4_CBASE "
    cQuery += " AND SN1.N1_ITEM  = SN4.N4_ITEM  "
    cQuery += " AND SN1.D_E_L_E_T_= ? "

    cQuery += " LEFT JOIN "+RetSqlName("CT1")+" CT1 ON CT1_FILIAL = ? " // 01 - Filial CT1
    cQuery += " AND CT1.CT1_CONTA = SN3.N3_CCONTAB "
    cQuery += " AND CT1.D_E_L_E_T_= ? "

    cQuery += " WHERE "
    cQuery += " SN4.N4_FILIAL =  ? " // 02 - Filial SN4
    cQuery += " AND N4_CONTA BETWEEN ? AND ? " //03 e 04 - Entre Contas
    cQuery += " AND N4_DATA BETWEEN ? AND ? " // 05 e 06 - Entre Datas

    //Filtro do Usuario
    if oFilter:hasFilter()
        cQuery += " AND " + oFilter:getSQLExpression()
    endif

    If lAtfCusPrv .And. MV_PAR07 == 2
		cQuery += " AND SN3.N3_ATFCPR <> ? "
	EndIf

    If mv_par05 == 2
		cQuery += " AND (N4_OCORR = ? OR N4_OCORR = ?) AND N4_TIPOCNT = ?"
	ElseIf mv_par05 == 3
		cQuery += " AND (N4_OCORR = ? OR N4_OCORR = ? OR N4_OCORR = ? OR N4_OCORR = ?) AND N4_TIPOCNT = ?"
	Else
		cQuery += " AND (N4_OCORR = ? OR N4_OCORR = ?)"
	Endif
                    
    cQuery += " AND N4_CODBAIX <> ? 
    cQuery += " AND SN4.D_E_L_E_T_= ?
                        
    cQuery += " ORDER BY N4_FILIAL , N4_DATA , N4_CONTA , N4_CODBAIX , N4_OCORR "
        
    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)
    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetString(nParamOrder++, FWxFilial("CT1"))
    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetString(nParamOrder++, FWxFilial("SN4"))
    oStatement:SetString(nParamOrder++, MV_PAR03 )
    oStatement:SetString(nParamOrder++, MV_PAR04)
    oStatement:SetDate(nParamOrder++, MV_PAR01)
    oStatement:SetDate(nParamOrder++, MV_PAR02)

    If lAtfCusPrv .And. MV_PAR07 == 2
		oStatement:SetString(nParamOrder++, "1")
	EndIf

    If mv_par05 == 2
        oStatement:SetString(nParamOrder++, "01")
        oStatement:SetString(nParamOrder++, "05")
        oStatement:SetString(nParamOrder++, "1")
	ElseIf mv_par05 == 3
        oStatement:SetString(nParamOrder++, "15")
        oStatement:SetString(nParamOrder++, "16")
        oStatement:SetString(nParamOrder++, "01")
        oStatement:SetString(nParamOrder++, "05")
        oStatement:SetString(nParamOrder++, "1")
	Else
        oStatement:SetString(nParamOrder++, "15")
        oStatement:SetString(nParamOrder++, "16")
	Endif

    oStatement:SetString(nParamOrder++, cSpace)
    oStatement:SetString(nParamOrder++, cSpace)

    cQuery := oStatement:GetFixQuery()

Return cQuery
