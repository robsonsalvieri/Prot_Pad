#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.expansionofassets.bra.ch"

namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace control.sv.functions.utils
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,SN4", name="Ampliacoes do bem", country="ALL",customTables="SN1,SN3,SN4")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ExpansionOfAssetsBusinessObject
Classe para criação do Objeto de Negócio de Diario Geral Modelo 2 para o TReports
Fonte/Perg      CTBR117	/ CTR117
@author         Gustavo Fernandes Campos
@since          06/12/2023
@version        12.1.2310
@Revitalização  SmartView
*/
//-------------------------------------------------------------------------------
Class ExpansionOfAssetsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalizacao
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method getQuery()     as character
    protected method setData()      as object   
    
    protected data cAlias           as character
    protected data cBaseIni         as character
    protected data cBaseFim         as character
    protected data cContaIni        as character
    protected data cContaFim        as character
    protected data cMoeda           as character
    protected data nMoeda           as numeric
    protected data dDataIni         as date
    protected data dDataFim         as date
    protected data aAllFields       as array
    protected data jItens		    as json
    protected data oQuery           as object
    protected data oIntegratedProvider 	as object

    
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
new
@return object: self
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class ExpansionOfAssetsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName( STR0001 )  // "Ampliacoes" 
    self:setPergunte("AFR260")      // Indica o pergunte que será utilizado no relatorio
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ExpansionOfAssetsBusinessObject
return STR0002 // "Objeto contendo informações das Ampliações"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@author         
@since          
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class ExpansionOfAssetsBusinessObject
Return { STR0003 } // "Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ExpansionOfAssetsBusinessObject

    local cQuery as character

    //Chama funcao que Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR260")

    //Definição de parametros
    ::cBaseIni   := MV_PAR01
    ::cBaseFim   := MV_PAR02
    ::dDataIni   := MV_PAR03
    ::dDataFim   := MV_PAR04
	::cContaIni  := MV_PAR05
    ::cContaFim  := MV_PAR06
    ::nMoeda     := MV_PAR07 + 1
    ::cMoeda   	 := StrZero(::nMoeda,1)
    
    cQuery := self:getQuery(oFilter)

    self:cAlias := MPSysOpenQuery(cQuery)
    self:setData(oFilter)

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())
    iif(Select(self:cAlias) > 0, (self:cAlias)->(DbCloseArea()), MSErase(self:cAlias))

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} setData
    Seta os dados filtrados no objeto oData

    @author gustavo.campos
    @since 13/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------------------
method setData(oFilter as object) as object class ExpansionOfAssetsBusinessObject
    
    local cChaveant              as character
    local cCompanyName           as character
    local cBranchName            as character
    local nValorOriginal    := 0 as numeric
    local nAmpAcumul        := 0 as numeric    
    local nValorOutraMoeda  := 0 as numeric
    local nX                := 0 as numeric


    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    //Salva a chave inicial
    cChaveant :=  (self:cAlias)->N4_FILIAL + (self:cAlias)->N4_CBASE + (self:cAlias)->N4_ITEM    

    While !(self:cAlias)->(Eof())

        if (self:cAlias)->N4_TIPO == "11"
		    if nValorOriginal == 0
                SN3->(dbSetOrder(1))//N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
                if SN3->(MsSeek( (self:cAlias)->N4_FILIAL + (self:cAlias)->N4_CBASE + (self:cAlias)->N4_ITEM +"01"+"0"))
                    nValorOriginal := SN3->N3_VORIG1
                    nValorOutraMoeda := &("SN3->N3_VORIG" + ::cMoeda ) 
                endif
		    endif
        else	
            nAmpAcumul := (self:cAlias)->N3_AMPLIA1
        endif
        
        ::jItens := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[self:aAllFields[nX]:getName()] := ALLTRIM((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX
        
        //Altera valores de campos da query com tratativas 
        ::jItens["N3_CCONTAB"] := Mascara((self:cAlias)->N3_CCONTAB)

        //Popula campos customizados
        ::jItens["nValAtual" ] := (self:cAlias)->( (self:cAlias)->N3_VORIG1 + (self:cAlias)->N3_VRCACM1 + if( (self:cAlias)->N4_TIPO =='11',nValorOriginal+nAmpAcumul, (self:cAlias)->N3_AMPLIA1 ))
        ::jItens["nVLROC"    ] := &(self:cAlias+ "->N4_VLROC" + ::cMoeda)
        ::jItens["nVORIG"    ] := if((self:cAlias)->N4_TIPO == "11", nValorOutraMoeda, &(self:cAlias + "->N3_VORIG" + ::cMoeda))
        ::jItens["NomeEmpresa"]:= cCompanyName
        ::jItens["NomeFilial"] := cBranchName
        
        if (self:cAlias)->N4_TIPO == "11"
            nAmpAcumul += (self:cAlias)->N3_VORIG1 
        endif

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())
        
        //Se mudou chave, reseta as variáveis
    	if cChaveant != (self:cAlias)->N4_FILIAL + (self:cAlias)->N4_CBASE + (self:cAlias)->N4_ITEM
		    cChaveant        :=  (self:cAlias)->N4_FILIAL + (self:cAlias)->N4_CBASE + (self:cAlias)->N4_ITEM
            nValorOriginal   := 0
            nValorOutraMoeda := 0
            nAmpAcumul       := 0
	    endif 

    EndDo    

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class ExpansionOfAssetsBusinessObject

    local aFieldsSN1 as array
    local aFieldsSN3 as array
    local aFieldsSN4 as array
    Local lCanFilter 	:= .F.  as logical

    aFieldsSN1 := { "N1_DESCRIC" }

    aFieldsSN3 := { "N3_CCONTAB", "N3_VRCACM1", "N3_AMPLIA1", "N3_VORIG1", "N3_VORIG2",;
                    "N3_VORIG3", "N3_VORIG4", "N3_VORIG5" }

    aFieldsSN4 := { "N4_FILIAL", "N4_CBASE", "N4_ITEM", "N4_TIPO", "N4_DATA", "N4_VLROC1",;
                    "N4_VLROC2", "N4_VLROC3", "N4_VLROC4", "N4_VLROC5" }

    //Adiciona campos ao schema
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)
    self:oSchema:aliasToSchema("SN3", aFieldsSN3)
    self:oSchema:aliasToSchema("SN4", aFieldsSN4)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()
    
    //Adiciona campos customizados ao schema
    self:oSchema:addProperty("nValAtual", STR0004, "number", STR0004, "nValAtual",,,,lCanFilter   )  // Valor original + ampliacao
    self:oSchema:addProperty("nVLROC"   , STR0005, "number", STR0005, "nVLROC",,,,lCanFilter   )  // Valor original + ampliacao outra moeda
    self:oSchema:addProperty("nVORIG"   , STR0006, "number", STR0006, "nVORIG",,,,lCanFilter   )  // Valor original outra moeda
    self:addProperty( "NomeEmpresa", STR0007, "string", STR0007, "NomeEmpresa",,,,lCanFilter ) // Nome Empresa
    self:addProperty( "NomeFilial",  STR0008, "string", STR0008, "NomeFilial",,,,lCanFilter  ) // Nome Filial
	
    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0009, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    @author gustavo.campos
    @since 13/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ExpansionOfAssetsBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    AJusta o Json conforme alteracao no processData e preSchema
    @author gustavo.campos
    @since 13/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ExpansionOfAssetsBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    @author gustavo.campos
    @since 13/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ExpansionOfAssetsBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query.

    @author gustavo.campos
    @since  13/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQuery(oFilter as object) as character class ExpansionOfAssetsBusinessObject
    
    local cDelete := space(1)   as character
    local cQuery                as character
    local nParamOrder := 1      as numeric
    local aSelFil     := {}     as array

    ::oIntegratedProvider := ControlIntegratedProvider():New()

	::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .F.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"SN3"})

	If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
		aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
	Else
		aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
	EndIf


    //Campos que farão parte do select da query
    //Select
    cQuery := " SELECT  " + self:getSQLFields(.F., , .F.)
    cQuery += " FROM " + RetSqlName("SN4") + " SN4, " + RetSqlName("SN3") + " SN3, " + RetSqlName("SN1") + " SN1 "
    
    //Where
    cQuery += " WHERE "
    if !empty(aSelFil)
        cQuery += " N4_FILIAL IN ( ? ) "
    else
        cQuery += " N4_FILIAL =  ?  "
    endif
    cQuery += " AND SN4.N4_CBASE  >= ? "
    cQuery += " AND SN4.N4_CBASE  <= ? "
    cQuery += " AND SN4.N4_DATA   >= ? "
    cQuery += " AND SN4.N4_DATA   <= ? "
    cQuery += " AND SN4.N4_CONTA  >= ? "
    cQuery += " AND SN4.N4_CONTA  <= ? "
    cQuery += " AND SN4.N4_OCORR  = ? "
    cQuery += " AND SN4.D_E_L_E_T_ = ? "
    cQuery += " AND SN3.N3_FILIAL = SN4.N4_FILIAL "
    cQuery += " AND SN3.N3_CBASE  = SN4.N4_CBASE "
    cQuery += " AND SN3.N3_ITEM   = SN4.N4_ITEM "
    cQuery += " AND SN3.N3_TIPO   = SN4.N4_TIPO "
    cQuery += " AND SN3.N3_SEQ    = SN4.N4_SEQ "
    cQuery += " AND (SN3.N3_AMPLIA1 <> ? OR SN3.N3_TIPO = ? ) "
    cQuery += " AND SN3.D_E_L_E_T_ = ? "
    cQuery += " AND SN1.N1_FILIAL = SN4.N4_FILIAL "
    cQuery += " AND SN1.N1_CBASE = SN4.N4_CBASE "
    cQuery += " AND SN1.N1_ITEM  = SN4.N4_ITEM "
    cQuery += " AND SN1.D_E_L_E_T_ = ? "

    //Order by
    cQuery += " ORDER BY N4_FILIAL, N4_CBASE, N4_ITEM, N4_TIPO, N4_DATA"

    self:oQuery := FWExecStatement():New(ChangeQuery(cQuery))

    //Popula query
    if !empty(aSelFil)
        self:oQuery:setIn(nParamOrder++, aSelFil)
    else
        self:oQuery:SetString(nParamOrder++, FWXFilial("SN4"))
    endif
    self:oQuery:SetString(nParamOrder++, ::cBaseIni)
    self:oQuery:SetString(nParamOrder++, ::cBaseFim)
    self:oQuery:SetDate(nParamOrder++, ::dDataIni)
    self:oQuery:SetDate(nParamOrder++, ::dDataFim)
    self:oQuery:SetString(nParamOrder++, ::cContaIni)
    self:oQuery:SetString(nParamOrder++, ::cContaFim)
    self:oQuery:SetString(nParamOrder++, '09')
    self:oQuery:SetString(nParamOrder++, cDelete)
    self:oQuery:SetString(nParamOrder++, '0')
    self:oQuery:SetString(nParamOrder++, '11')
    self:oQuery:SetString(nParamOrder++, cDelete)
    self:oQuery:SetString(nParamOrder++, cDelete)
    
    cQuery := self:oQuery:GetFixQuery()	

return cQuery
