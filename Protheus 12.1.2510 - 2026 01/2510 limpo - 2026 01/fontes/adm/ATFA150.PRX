/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³  DATA  ³ BOPS ³Program.³					ALTERACAO				  ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³15.04.99³xxxxxx³Alice   ³ Gravar N4_SEQ e N4_SEQREAV.                  ³±±
±±³13.12.99³xxxxxx³Alice   ³ Tratar campo criado para armazenar o valor da³±±
±±³        ³      ³Alice   ³Amplia‡ao N3_AMPLIA1,AMPLIA2,...              ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
#INCLUDE "ATFA150.CH"
#Include "Protheus.Ch"
#Define CONFIRMA 1
#Define REDIGITA 2
#Define ABANDONA 3
#Define cd_MENSAL 	"0"	// Zero
// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ ATFA150    ³ Autor ³ Vin¡cius Barreira     ³ Data ³ 01/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Amplia‡„o do bem                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGAATF                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFA150
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas                                         ³
//³ mv_par01 - Se mostra ou n„o os lan‡amentos cont beis                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aPos:= {  8,  4, 11, 74 }
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa                   ³
//³ ----------- Elementos contidos por dimens„o ------------                  ³
//³ 1. Nome a aparecer no cabe‡alho                                           ³
//³ 2. Nome da Rotina associada                                               ³
//³ 3. Usado pela rotina                                                      ³
//³ 4. Tipo de Transa‡„o a ser efetuada                                       ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados                          ³
//³    2 - Simplesmente Mostra os Campos                                      ³
//³    3 - Inclui registros no Bancos de Dados                                ³
//³    4 - Altera o registro corrente                                         ³
//³    5 - Remove o registro corrente do Banco de Dados                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabe‡alho da tela de atualiza‡”es                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro 	:= STR0004 // "Amplia‡„o do Bem"
Private cCodigo 	:= ""

Private aRotina := MenuDef()

Pergunte("AFA010", .F.)
SetKey(  VK_F12, { || pergunte("AFA010",.T.) } )

Set Decimals to 4

ATFXKERNEL()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endere‡a a fun‡„o de BROWSE                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"SN1",,,,,, AtfLegenda("SN1"))

SET KEY VK_F12 TO

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ AF150Amp   ³ Autor ³ Vin¡cius Barreira     ³ Data ³ 01/09/94 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Amplia‡„o do Bem                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AF150Amp(cAlias As Character, nReg As Numeric, nOpc As Numeric, cBaseA As Character, cItemA As Character, nValorA As Numeric, cDescrA As Character, lRotAuto As Logical)

//Define Variaveis
Local aArea As Array
Local oDlg As object
Local cTexto01 As Character
Local nValorNovo As Numeric
Local nDiferenca As Numeric

// Controle de multiplas moedas
Local __nQuantas As Numeric
Local aDifMoeda As Array
Local nX As Numeric
Local cMoedaAtf As Character
Local nTaxa As Numeric
Local lCprova As Logical
Local lContabiliza As Logical
Local nCusto As Numeric
Local nDeprAc As Numeric
Local cCbase As Character
Local cItem As Character
Local cDescr As Character
Local nQtd As Numeric
Local nValor As Numeric
Local nAnterior As Numeric
Local nLInhas As Numeric
Local nPosit As Numeric
Local cTexto As Character
Local ctextoAnt As Character
Local nOpt As Numeric
Local i As Numeric
Local cDescricao As Character
Local lAppend As Logical
Local cTipoImob As Character
Local lMostra As Logical
Local lAf150Auto As Logical
Local cNomeArq As Character
Local dAmplia As Date
Local nRecAmplia As Numeric
Local lSeqCorr As Logical
Local cIDMOV As Character
Local cTpSaldo As Character
Local lSN3Saldo As Logical
Local cOcorr As Character
Local aDadosComp As Array
Local aValores As Array

//Data de Bloqueio da Movimentação - MV_ATFBLQM
Local dDataBloq As Date
Local dUltProc As Date
Local cCalcDep As Character

// Verificação da classificação de Ativo se sofre depreciação
Local lAtClDepr As Logical
Local lOnOff As Logical

Local nLinBran As Numeric

aArea        := GetArea()
oDlg         := Nil
cTexto01     := Chr(13) + Chr(10)
nValorNovo   := 0  // Novo valor final do bem.
nDiferenca   := 0  // Valor a se acrescido e contabilizado.
__nQuantas   := If(lMultMoed,AtfMoedas(),5)
aDifMoeda    := If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0} )
nX           := 0
cMoedaAtf    := GetMv("MV_ATFMOED")
nTaxa        := 0
lCprova      := .F.
lContabiliza := .T.
nCusto       := 0
nDeprAc      := 0
cCbase       := ""
cItem        := ""
cDescr       := ""
nQtd         := 0
nValor       := 0
nAnterior    := 0
nLInhas      := 0
nPosit       := 0
cTexto       := ""
ctextoAnt    := ""
nOpt         := 0
i            := 0
cDescricao   := ""
lAppend      := .F.
cTipoImob    := ""
lMostra      := .F.
lAf150Auto   := IIf(lRotAuto == Nil,.F.,lRotAuto)  //Verifica se e rotina automatica -Rotina automatica
cNomeArq     := ""
dAmplia      := dDataBase
nRecAmplia   := 0
lSeqCorr     := UsaSeqCor()
cIDMOV       := ""
cTpSaldo     := ""
lSN3Saldo    := .T.
cOcorr       := ""
aDadosComp   := {}
aValores     := {}
dDataBloq    := GetNewPar("MV_ATFBLQM",CTOD(""))
dUltProc     := GetNewPar("MV_ULTDEPR", STOD("19800101"))
cCalcDep     := GetNewPar("MV_CALCDEP",'0') //->	'0'-Mensal
lAtClDepr    := .F.
lOnOff       := .T.
nLinBran     := 0

Private cCodDiario := ""
Private aDiario    := {}

DEFAULT cAlias   := ""
DEFAULT nReg     := 0
DEFAULT nOpc     := 3
DEFAULT cBaseA   := ""
DEFAULT cItemA   := ""
DEFAULT nValorA  := 0
DEFAULT cDescrA  := ""
DEFAULT lRotAuto := .F.

nOpc := If(nOpc = 4, 5, nOpc)

If !FreeForUse("SN1",xFilial("SN1")+SN1->(N1_CBASE+N1_ITEM))
	Return
Endif

lMostra := Iif(Type("MV_PAR01") == "N",(MV_PAR01 == 1),.F.)
lOnOff  := Iif(Type("MV_PAR05") == "N",(MV_PAR05 == 1),.F.)

If (lAf150Auto)
	cBase    := cBaseA
	cItem    := cItemA
	cTexto01 := cDescrA
Else
	cBase := SN1->N1_CBASE
	cItem := SN1->N1_ITEM
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Vari veis utilizadas na contabiliza‡Æo ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lHeader          := .T.
lTrailler        := .T.
cLoteAtf         := ""
cPadrao          := ""
lPadrao          := .T.
nTotal           := 0
cArquivo         := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o n£mero do Lote do m¢dulo Ativo Fixo               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cLoteAtf := LoteCont("ATF")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no Cadastro de Bens ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SN3")
DbSetOrder(1)

If !(DbSeek(XFilial("SN3")+cBase+cItem+"01")) .and. cPaisLoc <> "BOL"
	Help(" ",1,"AF150AMP",,STR0030,1,0) //"Opção disponível somente para bens com tipo de depreciação fiscal."
	Return
Endif	
If cPaisLoc == 'BOL' .And. FindFunction("AT150TBOL")
	AT150TBOL(cBase,cItem)
EndIf	

If Val(SN3->N3_BAIXA) > 0
	Help(" ",1,"AF150JaBai")
	Return
Endif

If !Empty(SN1->N1_DTBLOQ)
	Help(" ",1,"AF150BLOQ")   		//Este bem esta bloqueado, nao pode sofrer ampliacao.
	Return
EndIf

If SN1->N1_STATUS == "2"
	Help(" ",1,"AF150BLOQ")   	//Este bem esta bloqueado, nao pode sofrer ampliacao.
	Return
EndIf

/*
 * Tratamento do Bloqueio de bens com classificação patrimonial como "Custos de Transação", não pode sofre depreciação.
 */
If SN1->N1_PATRIM == "T"
	Help(" ",1,"AF150CUSTR")
	Return
EndIf

/* ---------------------------------------------------------
   Nao permite ampliacões anteriores à data de Aquisição³
   --------------------------------------------------------- */
If  dDataBase < (SN1->(N1_AQUISIC))
	Help(" ",1,STR0033,,STR0034,1,0) //"Atenção!"##"A data da Ampliação deve ser maior que a data de aquisição do Bem "
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ NÆo permite que amplia‡äes possam se feitas ³
//³ antes da data do £ltimo c lculo.            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If	cCalcDep == cd_MENSAL .And.;
	(	dDataBase <= LastDay(dUltProc) .Or.;
	(Month(dDataBase) # Month(dUltProc + 1) .And.;
	Year(dDataBase) # Year(dUltProc)) )
	HELP(" ",1,"ATF150AD")
	Return
EndIf

If dDataBase < dUltProc .Or. (dDataBase > (LastDAy(dUltProc+1))) .OR. !(dDataBase >= dUltProc)		//Tipo Depreciacao diferente 02-Mes Subsequente
	If GetMv("MV_TIPDEPR") <> "2"
		Help(" ",1,"AF150DPR",,STR0031,1,0)
		Return
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ NÆo permite que amplia‡äes possam se feitas ³
//³ antes da data do bloqueio.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  !Empty(dDataBase) .AND. (dDataBase < dDataBloq)
	HELP(" ",1,"AF150BLQM",,STR0029 + DTOC(dDataBloq) ,1,0)    //"A data de aquisição do bem é menor que a data de bloqueio de movimentação : "
	Return
EndIf

//Validacao para o bloqueio do processo
If !CtbValiDt(,dDataBase  ,,,,{"ATF001"},)
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Localiza os ¡ndices que ser„o utilizados para convers„o   ³
//³ da amplia‡„o para outras moedas.                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea( "SM2" )
dbSetOrder(1)
DbSeek(dDataBase, .T.)

// - 22/07/2010
PcoIniLan("000369")
While .T.

	dbSelectArea("SN3")
	nValor     := Iif( SN1->N1_PATRIM # "C", (SN3->N3_VORIG1 + SN3->N3_VRCACM1 + SN3->N3_AMPLIA1), SN3->N3_VORIG1 )
	nCusto     := nValor
	nValResid  := nValor - ( SN3->N3_VRDACM1 + SN3->N3_VRCDA1 + GetSldAcel('1') )
	nDeprAc    := SN3->N3_VRDACM1 + SN3->N3_VRCDA1 + GetSldAcel('1')

	If nOpc = 5
		DbSelectArea("SN4")
		cNomeArq := CriaTrab(, .F.)
		IndRegua("SN4", cNomeArq, "N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+N4_OCORR",,;
						"N4_CBASE = '" + SN3->N3_CBASE + "' .And. N4_ITEM = '" +;
						SN3->N3_ITEM + "' .And. N4_TIPO = '" + SN3->N3_TIPO +;
						"' .And. N4_OCORR = '09' .And. DTOS(N4_DATA) > '" +;
						Dtos(GetMv("MV_ULTDEPR")) + "'", STR0023) //"Selecionando registros ...."
		DbGoBottom()

		If Eof() .Or. Deleted()
			DbClearFil()
			RetIndex("SN4")
			ApMsgAlert(STR0024) //"Nao existe ampliacao a ser cancelada !"
			Exit
		Endif

		nDiferenca 	:= N4_VLROC1
		nValorNovo 	:= (SN3->N3_VORIG1 + SN3->N3_AMPLIA1) - nDiferenca
		dAmplia		:= SN4->N4_DATA
		nRecAmplia	:= SN4->(Recno())
		DbSeek(dAmplia, .T.)

		DbClearFil()
		RetIndex("SN4")
		DbSelectArea("SN3")
	Else
		nValorNovo := 0
		nDiferenca := 0
	Endif

	If (lAf150Auto)
		nDiferenca := nValorA
		nValorNovo := Af150VALOR(@nDiferenca,@nValorNovo)
		cTexto01   := cDescrA
		nOpt       := 1
	Else
		cCbase := SN3->N3_CBASE
		cItem  := SN3->N3_ITEM
		cDescr := SN1->N1_DESCRIC
		nQtd   := SN1->N1_QUANTD

		nOpt   := 0

		DEFINE 	MSDIALOG oDlg FROM  63,1 TO 420 - If(nOpc = 5, 80, 0),530;
				TITLE 	If(nOpc = 5, STR0027 + " - " + Dtoc(dAmplia),;
						STR0008) PIXEL // "Amplia‡„o de Ativos"
		@ 004, 004 TO 049, 225 LABEL STR0009 OF oDlg PIXEL // "Descri‡„o do Bem"
		@ 055, 004 TO 126, 133 LABEL STR0010 OF oDlg PIXEL // "Informa‡”es Financeiras"
		@ 069, 139 TO 115, 263 LABEL STR0011 OF oDlg PIXEL // "Dados da Amplia‡„o"

		If nOpc <> 5
			@ 130, 004 TO 170, 263 LABEL STR0012 OF oDlg PIXEL  // "Hist¢rico da Amplia‡Æo"
		Endif

		@ 017, 008 	SAY STR0013 		SIZE 39, 07 OF oDlg PIXEL // "C¢digo Base"
		@ 016, 060 	MSGET cCbase        SIZE 39, 12 OF oDlg PIXEL When .F.
		@ 017, 166 	SAY STR0014 		SIZE 22, 07 OF oDlg PIXEL // "Item"
		@ 016, 191 	MSGET cItem         SIZE 22, 10 OF oDlg PIXEL When .F. 
		@ 035, 008 	SAY STR0015 		SIZE 39, 07 OF oDlg PIXEL // "Descri‡„o"
		@ 033, 060 	MSGET cDescr        SIZE 160,10 OF oDlg PIXEL When .F.
		@ 066, 008 	SAY STR0016 		SIZE 55, 07 OF oDlg PIXEL // "Quantidade Atual"
		@ 064, 060 	MSGET nQtd          SIZE 50, 10 OF oDlg PIXEL When .F. Picture PesqPict("SN1","N1_QUANTD",10) Readonly
		@ 081, 008 	SAY STR0017 		SIZE 53, 07 OF oDlg PIXEL // "Custo Atualizado"
		@ 079, 060 	MSGET nCusto        SIZE 70, 10 OF oDlg PIXEL When .F. Picture PesqPict("SN3","N3_VORIG1")
		@ 097, 008 	SAY STR0018 		SIZE 55, 07 OF oDlg PIXEL // "Valor Residual"
		@ 096, 060 	MSGET nValResid     SIZE 70, 10 OF oDlg PIXEL When .F. Picture PesqPict("SN3","N3_VRDACM1")
		@ 112, 008 	SAY STR0019 		SIZE 57, 07 OF oDlg PIXEL // "Deprecia‡„o Acum."
		@ 110, 060 	MSGET nDeprAc       SIZE 70, 10 OF oDlg PIXEL When .F. Picture PesqPict("SN3","N3_VRDACM1")
		@ 081, 142 	SAY STR0020 		SIZE 61, 07 OF oDlg PIXEL // "Valor da Amplia‡„o"
		@ 079, 202 	MSGET nDiferenca    SIZE 60, 10 OF oDlg PIXEL Picture PesqPict("SN3","N3_AMPLIA1");
					When nOpc <> 5 Valid Af150VALOR(@nDiferenca,@nValorNovo)
		@ 097, 142 	SAY STR0021 		SIZE 53, 07 OF oDlg PIXEL // "Novo Valor"
		@ 096, 202 	MSGET nValorNovo	SIZE 60, 10 OF oDlg PIXEL Picture PesqPict("SN3","N3_VORIG1");
					When nDiferenca <= 0 .And. nOpc <> 5;
					Valid Af150VALOR(@nDiferenca,@nValorNovo)
		If nOpc <> 5
			@ 138, 70 	GET oMemo 			VAR cTexto01 OF oDlg MEMO PIXEL;
						size 135,25 FONT oDlg:oFont COLOR CLR_BLACK,CLR_HGRAY
		Endif

		DEFINE SBUTTON FROM 05, 235 TYPE 1 ENABLE OF oDlg Action (If (AtfVal() .AND. Af150VALOR(@nDiferenca,@nValorNovo),(nOpt:=1,oDlg:End()),))
		DEFINE SBUTTON FROM 35, 235 TYPE 2 ENABLE OF oDlg Action (nOpt:=3,oDlg:End())
		ACTIVATE MSDIALOG oDlg Centered

		IF nOpt == REDIGITA
			Loop
		EndIf

		IF nOpt == ABANDONA .or. nOpt == 0
			Exit
		EndIf

	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula o valor a ser acrescido nas 5 moedas. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//nDifMoeda2 := 0; nDifMoeda3 := 0; nDifMoeda4 := 0; nDifMoeda5 := 0
	nTaxa := 0

	//********************************
	// Controle de multiplas moedas  *
	//********************************
	For nX := 2 to __nQuantas
		cMoed	:= Alltrim(Str(nX))
		If SM2->&("M2_MOEDA"+cMoed) > 0
			aDifMoeda[nX] := nDiferenca / SM2->&("M2_MOEDA"+cMoed)
			nTaxa := Iif(cMoedaAtf == cMoed,SM2->&("M2_MOEDA"+cMoed),nTaxa)
		Endif
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicia processo de prote‡„o via TTS ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If nOpc = 5
		//********************************
		// Controle de multiplas moedas  *
		//********************************
		nDiferenca *= -1
		If lMultMoed
			AtfMultMoe(,,{|x| If(x=1,.F.,aDifMoeda[x]	*= -1)  })
		Else
			aDifMoeda[2] *= -1
			aDifMoeda[3] *= -1
			aDifMoeda[4] *= -1
			aDifMoeda[5] *= -1
		EndIf

	Endif

	Begin Transaction

	dbSelectArea("SN3")
	RecLock("SN3")

	//********************************
	// Controle de multiplas moedas  *
	//********************************
	If lMultMoed
		AtfMultMoe("SN3","N3_AMPLIA",{|x| SN3->&(If(x>9,"N3_AMPLI","N3_AMPLIA")+Alltrim(Str(x))) + If(x=1,nDiferenca,aDifMoeda[x])  })
	Else
		SN3->N3_AMPLIA1 += nDiferenca
		SN3->N3_AMPLIA2 += aDifMoeda[2]
		SN3->N3_AMPLIA3 += aDifMoeda[3]
		SN3->N3_AMPLIA4 += aDifMoeda[4]
		SN3->N3_AMPLIA5 += aDifMoeda[5]
	EndIf

	if (nValResid += nDiferenca) > 0
		SN3->N3_FIMDEPR := CTOD('')
	else
		SN3->N3_FIMDEPR := dDataBase
	endif

	MsUnlock()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ BOP's nro 8086.Grava no SN2 a descricao da reavaliacao, ver 205  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	dbSelectArea("SN2")
	cChave := xFilial("SN2")+SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
	dbSeek(cChave)

	If Found()
	   nPosit := Recno()

	   While SN2->N2_FILIAL+SN2->N2_CBASE+SN2->N2_ITEM+SN2->N2_TIPO == cChave
			nAnterior += 1
			cTextoAnt += SN2->N2_HISTOR
			dbSelectArea("SN2")
			dbSkip()
			Loop
	   EndDo
	Endif
	cTexto := cTextoAnt+Iif(Empty(SubStr(cTexto01,1,40)),"",AllTrim(cTexto01))
	if nOpc == 5 //Cancelamento
	   cTexto := cTexto + STR0027 + " - " + Dtoc(dDatabase)
	else
	   cTexto := cTexto + STR0003 + " - " + Dtoc(dDatabase)
	endif


	dbSelectArea("SN2") 
	dbSetOrder(1)  //ORDEM 1 - N2_FILIAL+N2_CBASE+N2_ITEM+N2_TIPO+N2_SEQUENC

	If !Empty(cTexto)
		nLinhas := MlCount(cTexto,40)
		If nPosit > 0 //se achou o registro na SN3, pois se nao encontrou esta em fim de arquivo
			dbGoto(nPosit)
		EndIf
		For i := 1 to nLinhas
			lAppend := .f.
			cDescricao := MemoLine(cTexto,40,i )
			cDescricao := StrTran(cDescricao,chr(10),"")
			cDescricao := StrTran(cDescricao,chr(13),"")

			If Empty(AllTrim(cDescricao))
				nLinBran++
			Else
				cChave := xFilial("SN2")+SN3->N3_CBASE+SN3->N3_ITEM+SN3->N3_TIPO
				If !(dbSeek(cChave+StrZero(i-nLinBran, TamSX3("N2_SEQUENC")[1])))  //SEEK CONSIDERA COM N2_SEQUEN
					lAppend := .t.
				EndIf
				Reclock( "SN2", lAppend)
				SN2->N2_FILIAL := xFilial("SN2")
				SN2->N2_CBASE  := SN3->N3_CBASE
				SN2->N2_ITEM   := SN3->N3_ITEM
				SN2->N2_TIPO   := SN3->N3_TIPO
				SN2->N2_SEQ    := SN3->N3_SEQ
				SN2->N2_SEQUENC:= StrZero(i-nLinBran, TamSX3("N2_SEQUENC")[1] )
				SN2->N2_HISTOR := cDescricao
				MsUnlock()
			Endif
		Next
	EndIf

	// Verificação da classificação de Ativo se sofre depreciação
	lAtClDepr := AtClssVer(SN1->N1_PATRIM)

	If lAtClDepr .OR. EMPTY(SN1->N1_PATRIM)
		cTipoImob := "2"
	Elseif SN1->N1_PATRIM $ "CAS"
		cTipoImob := "E"
	Else
		cTipoIMob := "F"
	EndIf

	If nOpc = 5
		cPadrao:= "822"
	Else
		cPadrao:= "821"
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava o arquivo de movimenta‡”es ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SN4")
	If nRecAmplia > 0
		DbGoto(nRecAmplia)
		Reclock("SN4",.F.)
		DbDelete()
		fkcommit()
   		msUnlock()
	Else
		cOcorr 	   := "09"
		aDadosComp := ATFXCompl( nTaxa,nTaxa,/*cMotivo*/,/*cBaixa*/,/*cFilOrig*/,/*cSerie*/,/*cNota*/,/*nVenda*/,SN1->N1_LOCAL, SN3->N3_PRODMES )
		aValores := AtfMultMoe(,,{|x| aDifMoeda[x] })
		aValores[1] := Round(nDiferenca,X3Decimal("N4_VLROC1"))
		If lSN3Saldo
			cTpSaldo := SN3->N3_TPSALDO
		EndIf
		ATFXMOV(cFilAnt,@cIDMOV,dDataBase,cOcorr,SN3->N3_CBASE,SN3->N3_ITEM,SN3->N3_TIPO,SN3->N3_BAIXA,SN3->N3_SEQ,SN3->N3_SEQREAV,"1",0,cTpSaldo,,aValores,aDadosComp,,,,,lOnOff,cPadrao)

		If lSeqCorr
			aAdd(aDiario,{"SN4",SN4->(recno()),cCodDiario,"N4_NODIA","N4_DIACTB"})
		EndIf
	Endif

	// - 22/07/2010
	PcoDetLan("000369","01","ATFA150",IiF(nOpc == 5 ,.T., .F.))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajusta o saldo do raz„o.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMultMoed
		aValorMoed := AtfMultMoe(,,{|x| If(x=1,nDiferenca,aDifMoeda[x])  })
	EndIf
	ATFSaldo(	SN3->N3_CCONTAB,dAmplia,cTipoImob, nDiferenca,aDifMoeda[2],aDifMoeda[3],aDifMoeda[4],aDifMoeda[5] ,;
		"+",nTaxa,SN3->N3_SUBCCON,,SN3->N3_CLVLCON,SN3->N3_CUSTBEM, "1", aValorMoed )


	If nRecAmplia = 0
		If ExistBlock("AF150AMP")   // GRAVAR  a diferenca (ampliacao ) no vorig1
			ExecBlock("AF150AMP",.f.,.f., {nDiferenca})
		Endif
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gera‡„o de lan‡amentos cont beis conforme o tipo do ativo           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If nOpc = 5
		//********************************
		// Controle de multiplas moedas  *
		//********************************
		nDiferenca := Abs(nDiferenca)
		If lMultMoed
			AtfMultMoe(,,{|x| If(x=1,.F.,aDifMoeda[x] := Abs(aDifMoeda[x]))  })
		Else
			aDifMoeda[2] := Abs(aDifMoeda[2])
			aDifMoeda[3] := Abs(aDifMoeda[3])
			aDifMoeda[4] := Abs(aDifMoeda[4])
			aDifMoeda[5] := Abs(aDifMoeda[5])
		EndIf
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existe lan‡amento padr„o.                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lPadrao := VerPadrao(cPadrao)
	IF lPadrao .and. lContabiliza .And. lOnOff
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta lan‡amento cont bil.                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF !lCprova .and. lHeader
			 nHdlPrv := HeadProva(cLoteAtf,"ATFA150",Substr(cUsername,1,6),@cArquivo)
			 lCprova := .T.
			 lHeader := .T.
		End
		nTotal += DetProva(nHdlPrv,cPadrao,"ATFA150",cLoteAtf)
		IF lCprova .and. lTrailler
			RodaProva(nHdlPrv,nTotal)
			cA100Incl(cArquivo,nHdlPrv,3,cLoteAtf,mv_par01 == 1,mv_par02 == 1,,,,,,aDiario)
		End
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Final da prote‡„o via TTS ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	End Transaction

	Exit
EndDo

// - 22/07/2010
PcoFinLan("000369")
RestArea(aArea)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ AF150Valor ³ Autor ³ Vin¡cius Barreira     ³ Data ³ 08/01/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Verifica se o valor a ser ampliado ‚ v lido.                 ³±±
±±³Parƒmetros³ nDiferenca - Diferenca a ser acrescida.                      ³±±
±±³          ³ nNovovalor - Valor novo do bem.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Gen‚rico                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF150VALOR(nDiferenca,nValorNovo)

 
If nDiferenca > 0
    nValorNovo := (SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1+nDiferenca)
ElseIf nValorNovo > 0
    nDiferenca := nValorNovo-(SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1)
Endif

If nValorNovo < (SN3->N3_VORIG1+SN3->N3_VRCACM1+SN3->N3_AMPLIA1)
	Help(" ",1,"AF150INVAL")
	Return .F.
Endif

If cPaisLoc == 'BOL' .And. FindFunction("AT150VLBOL")
	lPermit:= AT150VLBOL(nValorNovo)
	If !lPermit
		Return .F.	
	EndIf
EndIf

Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³10/12/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := { 	{STR0001, "AxPesqui"  	   , 0 , 1, ,.F.},; //"Pesquisar"
						{STR0002, "VIEWDEF.ATFA012", 0 , 2},; 		 // "Visualizar"
						{STR0003, "AF150Amp"  	   , 0 , 3},; 		 // "Amplia‡„o"
						{STR0025, "AF150Amp"  	   , 0 , 5},; 		 // "Cancelar"   //"Cancelar"
						{STR0026, "AtfLegenda"	   , 0 , 6 , ,.F. } }	// "Legenda"
Return(aRotina)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AtfVal    ºAutor  ³Erica Casale        º Data ³  31/05/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Se Portugal, solicita o cod. do diario p/ contabilizacao   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function AtfVal()

Local lRet := .T.

If UsaSeqCor()
	cCodDiario := CTBAVerDia()
Endif

Return(lRet)
