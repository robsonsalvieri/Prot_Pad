#INCLUDE "ATFA230.CH"
#Include "Protheus.ch"
#INCLUDE "FWLIBVERSION.CH"

Static lFWCodFil := .T.
Static lAutomato := .F.
Static cGrava    := " "

//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .T.

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ? ATFA230  ? Autor ? Alice Y Yamamoto      ? Data ? 17/11/98 ???
????Ĵ??
???Descri??o ? Altera??o da taxa de deprecia??o automtica                ???
????Ĵ??
???Sintaxe   ? ATFA230()                                                  ???
????Ĵ??
??? Uso      ? Generico                                                   ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Function ATFA230
//?
//? Define Variaveis                                               ?
//??

Local aSays:={}, aButtons:={}
Local nOpca := 0

Private cCadastro := STR0001     //"Altera??o da Taxa de Deprecia??o "

lAutomato := (FwIsInCallStack("AUTJOBRUNCT") .AND. IsBlind() )

dbSelectArea("SN1")
dbSetOrder(1 )

Pergunte("AFA230",.F.)
AADD(aSays,STR0004) //"Este programa tem o objetivo de alterar as taxas de de- "
AADD(aSays,STR0005) //"precia??o automaticamente de acordo com  os par?metros  "
AADD(aSays,STR0006) //"escolhidos"

//?
//? Inicializa o log de processamento                            ?
//??
ProcLogIni( aButtons )

If !lAutomato

	AADD(aButtons, { 5,.T.,{|| Pergunte("AFA230",.T. ) } } )
	AADD(aButtons, { 1,.T.,{|| nOpca := 1,	IF( AtfOK(),FechaBatch(),nOpca := 0 ) }} )
	AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

	FormBatch( cCadastro, aSays, aButtons,,,375 )
EndIf 

If nOpca == 1 .OR. lAutomato
	//?
	//? Atualiza o log de processamento   ?
	//??
	ProcLogAtu("INICIO")

	If MV_PAR18 == 1 .And. !Empty(xFilial("SN3"))// Seleciona filiais
		Processa({ |lEnd| Afa230Fil(MV_PAR19,MV_PAR20) })
	Else
		Processa({ |lEnd| AFA230Proc() })
	EndIf

	//?
	//? Atualiza o log de processamento   ?
	//??
	ProcLogAtu("FIM")
Endif

Return

/*

?????????????????????????????????????????????????????????????????????????????
????????
???Programa  ?Ctb230Fil ?Autor  ?Alvaro Camillo Neto ? Data ?  21/09/09   ???
????????͚??
???Desc.     ?Executa o processamento para cada filial                    ???
???          ?                                                            ???
????͚??
???Uso       ? ATFA230                                                  ???
???????
?????????????????????????????????????????????????????????????????????????????

*/
Static Function Afa230Fil(cFilDe,cFilAte)
Local cFilIni 	:= cFIlAnt
Local aArea		:= GetArea()
Local nInc		:= 0
Local aSM0		:= {}

aSM0 := AdmAbreSM0()
For nInc := 1 To Len( aSM0 )
	If aSM0[nInc][1] == cEmpAnt .AND. aSM0[nInc][2] >= cFilDe .AND. aSM0[nInc][2] <= cFilAte
		cFilAnt := aSM0[nInc][2]
		ProcLogAtu( "MENSAGEM", "EXECUTANDO O PROCESSO DA FILIAL "  + cFilAnt )
		Afa230Proc()
	EndIf
Next

cFIlAnt := cFilIni
RestArea(aArea)
Return

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o	 ?AFA230Proc? Autor ? Alice Yamamoto	    ? Data ?17/11/98  ???
????Ĵ??
???Descri??o ? Efetua a altera??o das taxas de deprecia??o                ???
????Ĵ??
???Sintaxe e ? AFA230Proc            									  ???
????Ĵ??
??? Uso		 ? 															  ???
??????
?????????????????????????????????????????????????????????????????????????????

*/

Function AFA230Proc()
Local cMoeda
Local lUpdDepPer 	:= .F.
Local cCalcDep		:= SuperGetMV("MV_CALCDEP",.F.,"") // 0 = Monthly or 1 = Annually
Local nNewPerDep	:= 0
Local lApagaTxRl as Logical
Local nMoeda	 as numeric
Local nQtdMoedas as numeric
Local nTaxaAnt	 as numeric
Local aTaxaHist  as array

Private cObsAlt	 as character

//?
//? Carrega as perguntas selecionadas:                      ?
//? mv_par01 - Do C?digo                                    ?
//? mv_par02 - At? o C?digo                                 ?
//? mv_par03 - Grupo de                                     ?
//? mv_par04 - Grupo Ate                                    ?
//? mv_par05 - Nova Taxa                                    ?
//? mv_par06 - Alterar Taxa na Moeda                        ?
//? mv_par07 - Do CCusto                                    ?
//? mv_par08 - At? oCCusto                                  ?
//? mv_par09 - Da Conta                                     ?
//? mv_par10 - At? a Conta                                  ?
//? mv_par11 - Moedas? Todas / Especifica                   ?
//? mv_par12 - Do Item Contabil       						?
//? mv_par13 - Ate o item Contabil    						?
//? mv_par14 - Da Classe de Valor     						?
//? mv_par15 - Ate a Classe de Valor  						?
//? mv_par16 - Do Item                                      ?
//? mv_par17 - At? o Item                                   ?
//? mv_par18 - Seleciona Filiais?                           ?
//? mv_par19 - Filial De                                    ?
//? mv_par20 - Filial At?                                  ?
//? mv_par21 - Do tipo do Ativo                             ?
//? mv_par22 - Ate o tipo do Ativo                          ?
//? mv_par23 - Apagar Tx Regulamentadas ?                   ?
//??

lApagaTxRl 	:= ( Valtype(mv_par23) == "N" .And. mv_par23==1 )
cObsAlt		:= ""
aTaxaHist	:= {}
nMoeda		:= 0
nTaxaAnt	:= 0
nQtdMoedas  := iif(lMultMoed, AtfMoedas(), 5)

If MV_PAR18 == 1 .And. Empty(xFilial("SN1"))
	ProcLogAtu("MENSAGEM","TRATAMENTO MULTI FILIAL DESABILITADO: SN1 COMPARTILHADO")
EndIf

cMoeda := StrZero(mv_par06,1)

cAliasQry := SelDados(	"SN3",;
						"N3_FILIAL = '" + xFilial("SN3") + "' .And. " +;
						"N3_CBASE >= '" + mv_par01 + "' .And. " +;
						"N3_CBASE <= '" + mv_par02 + "' .And. " +;
						"N3_ITEM >= '" + mv_par16 + "' .And. " +;
						"N3_ITEM <= '" + mv_par17 + "' .And. " +;
						"N3_TIPO >= '" + mv_par21 + "' .And. " +;
						"N3_TIPO <= '" + mv_par22 + "' .And. " +;
						"N3_CCUSTO >= '" + mv_par07 + "' .And. " +;
						"N3_CCUSTO <= '" + mv_par08 + "' .And. " +;
						"N3_CCONTAB >= '" + mv_par09 + "' .And. " +;
						"N3_CCONTAB <= '" + mv_par10 + "' .And. " +;
						"N3_SUBCCON >= '" + mv_par12 + "' .And. " +;
						"N3_SUBCCON <= '" + mv_par13 + "' .And. " +;
						"N3_CLVLCON >= '" + mv_par14 + "' .And. " +;
						"N3_CLVLCON <= '" + mv_par15 + "' .And. " +;
						"N3_BAIXA < '1'", .F., "SN1",, "N1_CBASE = N3_CBASE AND " +;
						"N1_ITEM = N3_ITEM AND N1_GRUPO >= '" + mv_par03 + "' AND " +;
						"N1_GRUPO <= '" + mv_par04 + "' AND " +;
						"N1_BAIXA = ' ' " + ;
						IIF(!lApagaTxRl," AND N1_TAXAPAD = '"+space(TamSX3("N1_TAXAPAD")[1])+"'"," ") )


//?
//? Inicia a leitura do SN3                                                ?
//??

While !Eof()
    //?
    //? Movimenta a regua                                                   ?
    //??
	If ! CarregaSel("SN3", { || 	SN1->(DbSeek(xFilial()+SN3->N3_CBASE+SN3->N3_ITEM)) .And.;
									SN1->N1_GRUPO >= mv_par03 .And.;
									SN1->N1_GRUPO <= mv_par04 .And.;
									Empty(SN1->N1_BAIXA) .And.;
									IIF(!lApagaTxRl, Empty(SN1->N1_TAXAPAD), .T. ) } )
		Loop
	Endif

	If mv_par05 >= 0
		lUpdDepPer := cPaisLoc $ "RUS"

		Reclock("SN3",.F.)
		If mv_par11 = 1
			//Monta array com as taxas alteradas conforme moedas do ambiente
			for nMoeda := 1 to nQtdMoedas
				cMoeda := cValToChar(nMoeda)
				
				nTaxaAnt := SN3->&(if(nMoeda > 9,"N3_TXDEP", "N3_TXDEPR") + cMoeda)
				if nTaxaAnt <> mv_par05 //S adiciona no array se de fato alterou a taxa
					aAdd(aTaxaHist, {SN3->(Recno()),;	 //Filial do bem
									StrZero(nMoeda, 2),; //Moeda alterada
									nTaxaAnt,;			 //Valor anterior da taxa
									mv_par05})			 //Valor alterado da taxa
				endif
			next nMoeda	

			//********************************
			// Controle de multiplas moedas  *
			//********************************
			If lMultMoed
				AtfMultMoe("SN3","N3_TXDEPR",{|x| mv_par05 },,lUpdDepPer)
			Else
				If lUpdDepPer // update the field Depreciation period
					// Calculate the depreciation period according to the new depreciation tax
					If mv_par05 > 0
						nNewPerDep := (100 * 12)  / mv_par05
						If cCalcDep == "1" // 1 = Annually
							If nNewPerDep / 12 == Int(nNewPerDep / 12)
								nNewPerDep := nNewPerDep / 12
							Else
								nNewPerDep := Int(nNewPerDep / 12) + 1					
							Endif	
						Endif
						SN3->N3_PERDEPR := NoRound( nNewPerDep, TAMSX3('N3_PERDEPR')[2])
					Endif
				Endif
				SN3->N3_TXDEPR1 := mv_par05
				SN3->N3_TXDEPR2 := mv_par05
				SN3->N3_TXDEPR3 := mv_par05
				SN3->N3_TXDEPR4 := mv_par05
				SN3->N3_TXDEPR5 := mv_par05
			EndIf
		Else
			//Monta array com as taxas alteradas conforme moeda selecionada
			nTaxaAnt := SN3->&(if(Val(cMoeda) > 9,"N3_TXDEP", "N3_TXDEPR") + cMoeda)
			if nTaxaAnt <> mv_par05 //S adiciona no array se de fato alterou a taxa
				aAdd(aTaxaHist, {SN3->(Recno()),;	    //Filial do bem
								StrZero(mv_par06, 2),;	//Moeda alterada
								nTaxaAnt,; 				//Valor anterior da taxa
								mv_par05})			    //Valor alterado da taxa
			endif

			If lUpdDepPer .and. cMoeda == "1" // update the field Depreciation period
				// Calculate the depreciation period according to the new depreciation tax
				If mv_par05 > 0
					nNewPerDep := (100 * 12)  / mv_par05
					If cCalcDep == "1" // 1 = Annually
						If nNewPerDep / 12 == Int(nNewPerDep / 12)
							nNewPerDep := nNewPerDep / 12
						Else
							nNewPerDep := Int(nNewPerDep / 12) + 1					
						Endif	
					Endif
					SN3->N3_PERDEPR := NoRound( nNewPerDep, TAMSX3('N3_PERDEPR')[2])
				Endif
			Endif
			SN3->&(If(Val(cMoeda)>9,'N3_TXDEP','N3_TXDEPR')+cMoeda) := mv_par05
		Endif
		SN3->(msUnlock())

		If lApagaTxRl //Apaga Taxa Regulamentadas
			dbSelectArea("SN1")
			dbSetOrder(1)
			If SN1->(dbSeek(xFilial("SN1")+SN3->N3_CBASE+SN3->N3_ITEM))
				If SN1->(RecLock("SN1",.F.))
					SN1->N1_TAXAPAD := space(TamSX3("N1_TAXAPAD")[1])
					SN1->(MsUnLock())
				EndIf
			EndIf
		EndIf
	EndIf

	DbSelectArea(cAliasQry)
	dbSkip()
EndDo

//Executa a grava?o do histrico
if !Empty(aTaxaHist)
	Processa( {|| Af230GrvHist(aTaxaHist)}, STR0007, , .T. ) //Gravando histrico...
endif

RemoveSel("SN3")
FWFreeArray(aTaxaHist)

Return

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ? AtfOk    ? Autor ? Alice Y. Yamamoto     ? Data ? 19.01.99 ???
????Ĵ??
???Descri??o ? Mensagem e confirmacao do processamento                    ???
????Ĵ??
???Sintaxe   ? AtfOk(void)                                                ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Static function AtfOk()
Return(.T.)

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ?Af230VldTx? Autor ? Alice Y. Yamamoto     ? Data ? 19.01.99 ???
????Ĵ??
???Descri??o ? Mensagem e confirmacao do processamento                    ???
????Ĵ??
???Sintaxe   ? Af230VldTx ()                                              ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Function Af230VldTx()
Local lRet := .T.

If mv_par05 < 0
	Help(" ",1,"AF230TXDEP")  //Taxa de deprecia??o n?o pode ser menor que zero
	lRet := .F.
EndIf

Return(lRet)


/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ?AdmAbreSM0? Autor ? Orizio                ? Data ? 22/01/10 ???
????Ĵ??
???Descri??o ?Retorna um array com as informacoes das filias das empresas ???
????Ĵ??
??? Uso      ? Generico                                                   ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Static Function AdmAbreSM0()
	Local aArea			:= SM0->( GetArea() )
	Local aAux			:= {}
	Local aRetSM0		:= {}
	Local lFWLoadSM0	:= .T.
	Local lFWCodFilSM0 	:= .T.

	If lFWLoadSM0
		aRetSM0	:= FWLoadSM0()
	Else
		DbSelectArea( "SM0" )
		SM0->( DbGoTop() )
		While SM0->( !Eof() )
			aAux := { 	SM0->M0_CODIGO,;
						IIf( lFWCodFilSM0, FWGETCODFILIAL, SM0->M0_CODFIL ),;
						"",;
						"",;
						"",;
						SM0->M0_NOME,;
						SM0->M0_FILIAL }

			aAdd( aRetSM0, aClone( aAux ) )
			SM0->( DbSkip() )
		End
	EndIf

	RestArea( aArea )
Return aRetSM0

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ?Af230GrvHist? Autor ? Renan Gremes	    ? Data ? 13.03.25 ???
????Ĵ??
???Descri??o ? Grava histrico de altera?o de taxa          			  ???
????Ĵ??
???Parametros? 03 aTaxaHist  - Array com as taxas alteradas por moeda     ???
???          ?                 1 = Recno SN3                              ???
???          ?                 2 = Moeda alterada                         ???
???          ?                 3 = Valor anterior da taxa                 ???
???          ?                 4 = Valor alterado da taxa                 ???
????Ĵ??
???Sintaxe   ? Af230GrvHist ()                                            ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Function Af230GrvHist(aTaxaHist as array)
	local oBulk		as object
	local cMoeda	as character
	local cHrMvto	as character
	local cObsAlt	as character
	local dDtMovto	as date
	local nCount  	as numeric
	local nValOri	as numeric
	local nValDes	as numeric
	local lCheckBulk as logical

	default aTaxaHist := {}

	if !Empty(aTaxaHist)
		if !FWAliasInDic("FM5")
			Help(" ",1,STR0008,,;	   //Tabela inexistente
			STR0009,1,0,,,,,,;  	   //A tabela FM5 n?o foi encontrada no ambiente.			
			{STR0010+CRLF+CRLF+CRLF+;  //Apesar de a taxa ter sido alterada, n?o ser possvel registrar o histrico.
			STR0011+" https://tdn.totvs.com/x/_ipRNw"})	//Para mais informa?es, consulte a documenta?o completa em:
			
		else
			if !lAutomato
				if MsgYesNo(STR0012, "TOTVS") //"Deseja informar o motivo da altera?o?"
					cObsAlt := Af230BrwObs()
				endif
			endif

			cHrMvto		:= Time()
			dDtMovto 	:= dDataBase

			oBulk := FwBulk():New(RetSqlName("FM5"))
			lCheckBulk := TCGetBuild() >= "20181212" .And. FWLibVersion() >= "20201009" .And. oBulk:CanBulk() .And. cGrava $ ' |1'

			if lCheckBulk
				oBulk:SetFields(FM5->(dbStruct()))
			endif

			dbSelectArea("FM5")
			FM5->(dbSetOrder(1))
			
			for nCount := 1 to len(aTaxaHist)
				SN3->(dbGoTo(aTaxaHist[nCount][1]))

				cMoeda   := aTaxaHist[nCount][2]
				nValOri	 := aTaxaHist[nCount][3]
				nValDes	 := aTaxaHist[nCount][4]

				if !SM5->(dbSeek(FWXFilial("FM5")+SN3->(N3_CBASE+N3_ITEM+N3_TIPO+N3_TPSALDO)+cMoeda+DTOS(dDtMovto)+cHrMvto))
					if lCheckBulk
						oBulk:AddData( {SN3->N3_FILIAL  ,;
										SN3->N3_CBASE   ,;
										SN3->N3_ITEM	,;
										SN3->N3_TIPO	,;
										SN3->N3_TPSALDO ,;
										cMoeda	 		,;
										dDtMovto 		,;
										cHrMvto  		,;
										nValOri  		,;
										nValDes  		,;
										cUserName		,;
										cObsAlt	 		})
					else					
						Reclock("FM5", .T.)
							FM5->FM5_FILIAL := SN3->N3_FILIAL
							FM5->FM5_CBASE	:= SN3->N3_CBASE
							FM5->FM5_ITEM	:= SN3->N3_ITEM
							FM5->FM5_TIPO	:= SN3->N3_TIPO
							FM5->FM5_TPSALD	:= SN3->N3_TPSALDO
							FM5->FM5_MOEDA	:= cMoeda
							FM5->FM5_DATA	:= dDtMovto
							FM5->FM5_HORA	:= cHrMvto
							FM5->FM5_TXORI	:= nValOri
							FM5->FM5_TXDES	:= nValDes
							FM5->FM5_USER	:= cUserName
							FM5->FM5_OBS	:= cObsAlt
						FM5->(MsUnLock())
					endif
				endif
			next nCount

			if lCheckBulk
				oBulk:Close()
				oBulk:Destroy()
				oBulk := nil
			endif

			FM5->(dbCloseArea())			
		endif
	endif
Return

/*/

?????????????????????????????????????????????????????????????????????????????
?????
???Fun??o    ?Af230BrwObs? Autor ? Renan Gremes	        ? Data ? 13.03.25 ???
????Ĵ??
???Descri??o ? Browser para informar o motivo de altera?o da taxa   	  ???
????Ĵ??
???Sintaxe   ? Af230BrwObs()                                              ???
??????
?????????????????????????????????????????????????????????????????????????????

/*/
Static Function Af230BrwObs()
	Local cReturn as character
	Local oDlgObs, oGetObs as object

	cReturn := ""

	DEFINE MSDIALOG oDlgObs TITLE STR0013 FROM 15,20 TO 25,62 //"Motivo altera?o"
	DEFINE SBUTTON FROM 52, 101.8 TYPE 1  ENABLE OF oDlgObs ACTION (nOpca := 1,oDlgObs:End())
	DEFINE SBUTTON FROM 52, 128.9 TYPE 2  ENABLE OF oDlgObs ACTION (nOpca := 2,oDlgObs:End())
	@ 0.5,0.7  GET oGetObs VAR cReturn OF oDlgObs MEMO size 150,40
	oGetObs:bRClicked := {||AllwaysTrue()}
	ACTIVATE MSDIALOG oDlgObs

Return cReturn
//-----------------------------------------------------------------
/*/{Protheus.doc} AF230RoboT
Atribuicao da variavel statica l_Automato para argumento utilizado - nao retirar do fonte pois a variavel eh static 
@author Totvs
@since 22/08/2018
@version 1.0
/*/
//-----------------------------------------------------------------

Function AF230RoboT(lRobot, cGravaP)
Default lRobot := .F.
Default cGravaP := " "

cGrava     := cGravaP 
l_Automato := lRobot  

Return
