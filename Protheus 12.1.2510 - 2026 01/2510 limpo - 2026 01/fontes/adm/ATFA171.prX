#Include "Protheus.ch"
#Include "Atfa171.ch"


// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.
//********************************
// Controle de multiplas moedas  *
//********************************
Static lMultMoed := .T.
STATIC lIsRussia	:= If(cPaisLoc$"RUS",.T.,.F.) // CAZARINI - Flag to indicate if is Russia location

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ ATFA171	  ³ Autor ³ Claudio D de Souza    ³ Data ³ 25/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ C lculo de Deprecia‡„o Acelerada                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 										            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function ATFA171
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cCadastro := OemtoAnsi(STR0001) //"Efetuar a Acelera‡„o"
Local aSays := {}, aButtons := {}

Private aRotina := MenuDef()

nOpca := 0

dbSelectArea("SN3")
dbSetOrder(2) 

Pergunte("AFA171",.F.)

AADD(aSays,STR0005)
AADD(aSays,STR0006)
AADD(aSays,STR0007)
AADD(aSays,STR0008)

AADD(aButtons, { 5,.T.,{|| Pergunte("AFA171",.T. ) } } )
AADD(aButtons, { 1,.T.,{|| If(Af171VPerg().And.Af171Vld(), (FechaBatch(), Processa({|lEnd| af171Proc()})), Nil ) }} )
AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

FormBatch( cCadastro, aSays, aButtons )

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³ Af171VPerg ³ Autor ³ Claudio D de Souza    ³ Data ³ 25/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validar as perguntas                              				 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ ATFA171 													             	 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af171VPerg
Local dNextDepr 	:= (GetMv("MV_ULTDEPR")) + 1
Local lRet 			:= .F.

Do Case
Case mv_par05 <= 1
	Aviso(STR0011,STR0013, {"Ok"})  // "Para efetuar a aceleração, o fator deve ser maior que 1"
Case	Left(Dtos(mv_par06),6) != Left(Dtos(dNextDepr),6) .Or.;
		Left(Dtos(mv_par07),6) != Left(Dtos(dNextDepr),6)
	Aviso(STR0011,STR0014 + Str(Month(dNextDepr),2)+"/"+Str(Year(dNextDepr),4), {"Ok"})  // "Para efetuar a aceleração, o período deve estar dentro do mês/ano do próximo cálculo de depreciação: "
Case	dDataBase != mv_par07
	Aviso(STR0011,STR0015 + Dtoc(mv_par07), {"Ok"}) // "Para efetuar a aceleração, a data base do sistema deve igual a "
OtherWise
	lRet := .T.	
EndCase

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³AF171Proc ³ Autor ³ Claudio D de Souza	  ³ Data ³25/09/06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Calculo de deprecia‡„o Acelerada							  		  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ AF171Proc             									  			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ 															  				  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function AF171Proc()

Local oDlg1 as object
Local oQtda as object
Local oMark as object
Local oQueryDep as object
Local lInverte as logical
Local cAlias as character
Local cIndex as character
Local cChave as character
Local nSavOrd1 as numeric
Local aCampos as array
Local aSize as array 

Private cMarca   := GetMark( )
Private cArquivo	:= ""
Private nTotal 	:= 0
Private nHdlPrv	:= 0
Private cPadrao   := ""
Private nQtdBens  := 0 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega as perguntas selecionadas:                            ³
//³ mv_par01  - Do Bem ? 														³
//³ mv_par02  - Ate o Bem ? 												   ³
//³ mv_par03  - Do Item do Bem ? 								         ³
//³ mv_par04  - Ate o Item do Bem ?											³
//³ mv_par05  - Fator ?                                           ³
//³ mv_par06  - Data inicial ?                                    ³
//³ mv_par07  - Data Final ?                                      ³
//³ mv_par08  - Da Conta ?                                        ³
//³ mv_par09  - Ate a Conta ?                                     ³
//³ mv_par10  - Do Centro de Custo ?                              ³
//³ mv_par11  - Ate o Centro de Custo ?                           ³
//³ mv_par12  - Do Item Contabil ?                                ³
//³ mv_par13  - Ate o Item Contabil ?                             ³
//³ mv_par13  - Da Classe de Valor ?                              ³
//³ mv_par14  - Ate a Classe de Valor ?                           ³
//³ mv_par16  - Mostra Lanc. Cont. ?                              ³
//³ mv_par17  - Aglut Lactos ?                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

lInverte := .F.
cAlias   := Alias()
cIndex   := ""
cChave   := ""
nSavOrd1 :=IndexOrd()
aCampos  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero do Lote do modulo Ativo Fixo 		         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cLoteAtf := LoteCont("ATF")

aCampos := {}
AADD(aCampos,{"N3_OK","","  ",""})
dbSelectArea("SX3")
dbSeek ("SN3")
Do While SX3->(!EOF()) .And. (SX3->x3_arquivo == "SN3")
	IF (cNivel >= SX3->x3_nivel .and. SX3->X3_context # "V") .Or.;
		(SX3->X3_PROPRI == "U" .AND. SX3->X3_CONTEXT!="V" .AND. SX3->X3_TIPO<>'M') .Or.;
		Alltrim(SX3->X3_CAMPO) $ "N3_CBASE#N3_ITEM"
		AADD(aCampos,{SX3->X3_CAMPO,"",X3Titulo(),SX3->X3_PICTURE})
	Endif
	SX3->(dbSkip())
Enddo

dbSelectArea("SN3")
dbSetOrder(2)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta express„o do Filtro para sele‡„o							  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cIndex := CriaTrab(nil,.f.)
cChave  := IndexKey( )
IndRegua("SN3",cIndex,cChave,,Af171ChecF(),STR0010) //"Selecionando Registros..."
nIndex := RetIndex("SN3")
dbSelectArea("SN3")
#IFNDEF TOP
	dbSetIndex(cIndex+OrdBagExt())
#ENDIF
dbSetOrder(nIndex+1)
DbGoTop()
If BOF() .and. EOF()
	RetIndex("SN3")
	dbSetOrder(nSavOrd1)
	Set Filter to
	FErase (cIndex+OrdBagExt())
	Return
EndIf

nQtdBens := 0	  // quantidade de Bens,mostrado no rodap‚ do browse
dbGotop()

nOpca := 0
aF171Marca("SN3",cMarca)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o calculo automatico de dimensoes de objetos     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MSADVSIZE()	

DEFINE MSDIALOG oDlg1 TITLE OemToAnsi(STR0011) From aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Calculo de Deprecia‡„o Acelerada"
oDlg1:lMaximized := .T.
	
oPanel := TPanel():New(0,0,'',oDlg1,, .T., .T.,, ,40,40,.T.,.T. )
oPanel:Align := CONTROL_ALIGN_TOP

@1.5, .8 Say OemToAnsi(STR0012) OF oPanel //"Tot de Bens a Depre..."
@1.5, 12 Say oQtda VAR nQtdBens OF oPanel SIZE 50,10
oMark := MsSelect():New(cAlias,"N3_OK",,aCampos,@lInverte,@cMarca,{35,oDlg1:nLeft,oDlg1:nBottom,oDlg1:nRight})
oMark:bMark := {| | Af171Display(cMarca,lInverte,oQtda)}
oMark:oBrowse:Align := CONTROL_ALIGN_ALLCLIENT

ACTIVATE MSDIALOG oDlg1 ON INIT EnchoiceBar(oDlg1,{|| nOpca := 1,oDlg1:End()},{|| nOpca := 0,ODlg1:End()})

If nOpca # 1
	dbSetOrder(1)
	RetIndex("SN3")
	dbSetOrder(nSavOrd1)
	Set Filter to
	FErase (cIndex+OrdBagExt())
	Return
Endif

SN1->(DbSetOrder(1))

dbGoTop() // O SN3 esta filtrado

While !(SN3->(EOF()))

	If SN3->N3_OK == cMarca
		SN1->(MsSeek(xFilial("SN1")+SN3->N3_CBASE+SN3->N3_ITEM))
		If Empty(SN1->N1_DTBLOQ) .Or. Dtos(SN1->N1_DTBLOQ) < Dtos(dDataBase)						
			Af171Acele(@oQueryDep)   //CalCula Aceleracao			
		Endif	
	Endif	

	SN3->(dbSkip())
	
End

If oQueryDep <> Nil
	oQueryDep:Destroy()
	oQueryDep := Nil
EndIf

If nQtdBens != 0 .and. nHdlPrv > 0 .And. mv_par16 != 3
	RodaProva(nHdlPrv,nTotal)
	lDigita   := IIf(mv_par16 = 1,.T.,.f.)
	lAglutina := IIf(mv_par17 = 1, .t.,.f.)
	cA100Incl(cArquivo,nHdlPrv,1,cLoteAtf,lDigita,lAglutina)
End

//Apagar Indices temporarios
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura os ¡ndices 										 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RetIndex("SN3")
dbSetOrder(nSavOrd1)
Set Filter to
FErase (cIndex+OrdBagExt())
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Af171ChecF³ Autor ³ Claudio D de Souza    ³ Data ³ 25/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Retorna Condi‡„o para Indice filtrado da rotina   			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³Af171ChecF() 												           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ATFA171													              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Af171ChecF()
Local cFiltro := ""

	cFiltro := 'N3_FILIAL == "' + xFilial("SN3")
	cFiltro += '" .AND. N3_CBASE   >= "' + mv_par01
	cFiltro += '" .AND. N3_CBASE   <= "' + mv_par02
	cFiltro += '" .AND. N3_ITEM    >= "' + mv_par03
	cFiltro += '" .AND. N3_ITEM    <= "' + mv_par04
	cFiltro += '" .AND. N3_CCONTAB >= "' + mv_par08
	cFiltro += '" .AND. N3_CCONTAB <= "' + mv_par09
	cFiltro += '" .AND. N3_CUSTBEM >= "' + mv_par10
	cFiltro += '" .AND. N3_CUSTBEM <= "' + mv_par11
	cFiltro += '" .AND. N3_SUBCCON >= "' + mv_par12
	cFiltro += '" .AND. N3_SUBCCON <= "' + mv_par13
	cFiltro += '" .AND. N3_CLVLCON >= "' + mv_par14
	cFiltro += '" .AND. N3_CLVLCON <= "' + mv_par15
	cFiltro += '" .AND. Val(N3_BAIXA) == 0 '
	cFiltro += ' .AND.  N3_CCONTAB <> " " .And. N3_CCDEPR <> " " .And. N3_CDEPREC <> " "' 
	
	If lIsRussia
		cFiltro += ' .AND. N3_OPER  == "1" ' // CAZARINI - 22.01.2017 - Asset Into Operation?
	Endif
    If ExistBlock("AF171Filtr")
    	cFilAux := ExecBlock( "AF171Filtr", .F., .F., {cFiltro} )
    	If Valtype( cFilAux ) == "C"
    		cFiltro := cFilAux
    	EndIf
    Endif 	

Return cFiltro

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³af171Marca³ Autor ³ Claudio D de Souza    ³ Data ³ 25/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Marcar todos os bens filtrados							        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Af171Marca(ExpN1,ExpD1,ExpD2) 							        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ Atfa171													              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
STATIC Function Af171Marca(cAlias,cMarca)
LOCAL lSavTTS
LOCAL nRec
LOCAL cAliasAnt := Alias()

lSavTTS := __TTSInUse
__TTSInUse := .F.

dbSelectArea(cAlias)
nRec := Recno()
DbGoTop()
While !Eof()
	SN1->(MsSeek(xFilial("SN1") + SN3->(N3_CBASE + N3_ITEM)))
	If SN1->N1_STATUS != "2"
		RecLock("SN3")
		SN3->N3_OK := cMarca
		nQtdBens++
	Else
		RecLock("SN3")
		SN3->N3_OK := " "		
	EndIf
	/*
	 * Tratamento da seleção de bens na depreciação acelerada,
	 * Não selecionar os bens que possuirem a classificação 
	 * patrimonial como "Custos de Transação"
	 */
	If SN1->N1_PATRIM == "T"
		RecLock("SN3")
		SN3->N3_OK := " "	
	EndIf
	dbSkip()
EndDo
dbGoto(nRec)

__TTSInUse := lSavTTS
dbSelectArea(cAliasAnt)
Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³Af171Display³ Autor ³ Claudio D de Souza 	 ³ Data ³ 25/09/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualiza tela de sele‡ao de registros       	             	 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³Af171Display( cMarca,lInverte,oQtda,cArquivo) 				    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ ATFA171														             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function af171Display(cMarca,lInverte,oQtda,cArquivo)
Local cFieldMarca := "N3_OK"

SN1->(MsSeek(xFilial("SN1") + SN3->(N3_CBASE + N3_ITEM)))

If IsMark(cFieldMarca,cMarca,lInverte)
	If SN1->N1_STATUS != "2"
		nQtdBens++
		oQtda:Refresh()
	Else
		RecLock("SN3",.F.)
		SN3->N3_OK := " "
		MsUnlock()
		Help(" ",1,"AF171BLQ")	//"Este bem está bloqueado, a depreciação não poderá ser acelerada."		
	EndIf

	/*
	 * Tratamento da seleção de bens com classificação patrimonial como "Custos de Transação"
	 * Não permitir a seleção.
	 */
	If SN1->N1_PATRIM == "T"
		SN3->(RecLock("SN3",.F.))
		SN3->N3_OK := " "
		SN3->(MsUnlock())
		Help(" ",1,"AF171CUSTR")
	EndIf
Else
	nQtdBens--
	nQtdBens:= Iif(nQtdBens<0,0,nQtdBens)
	oQtda:Refresh()
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af171Acele   ³Autor ³  Alice Y Yamamoto    ³Data³ 07/05/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula a nova deprecia‡ao Acumulada                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Af171Acele( )                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ ATFA171                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Af171Acele(oQueryDep As Object)
Local aArea     := GetArea()
//********************************
// Controle de multiplas moedas  *
//********************************
Local __nQuantas := If(lMultMoed,AtfMoedas(),5)
Local nX

Private aValDepr := If(lMultMoed,AtfMultMoe(,,{|x| 0}), {0,0,0,0,0})
/*Private nValDepr1 := 0
Private nValDepr2 := 0 
Private nValDepr3 := 0 
Private nValDepr4 := 0 
Private nValDepr5 := 0*/
Private nDepr1		:= 0

DbSelectArea("SN3")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ A data  de in¡cio de deprecia‡„o deve ser a data em que o bem ³
//³ realmente come‡ou a ser depreciado.                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nDiasParcial := (mv_par07 - mv_par06) + 1
	
//nValDepr1 := 0; nValDepr2  := 0; nValDepr3 := 0; nValDepr4 := 0
//nValDepr5 := 0
nDiferenca := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ nValDepr1  - Ultima depreciacao Mensal do Bem em Real            ³
//³ nValDepr3  - Ultima Depreciacao Mensal do Bem em UFIR            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//********************************
// Controle de multiplas moedas  *
//********************************

aValDepr[1] := 0
If Abs(N3_VRDACM1 + N3_VRCDA1) < Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)
	IF SN3->N3_TXDEPR1 > 0
		nTaxa 		:= Abs((SN3->N3_TXDEPR1 * mv_par05)- SN3->N3_TXDEPR1) * (( nDiasParcial / Day(LastDay(dDataBase)))) / 1200
		aValDepr[1]  	:= Round(Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1) * nTaxa, X3Decimal("N3_VORIG1"))
		nDiferenca 	:= Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1) - (aValDepr[1] + Abs(N3_VRDACM1+ N3_VRCDA1))
		If Round(nDiferenca,X3Decimal("N3_VORIG1")) <= 0
			nDepr1  := Abs(N3_VORIG1+N3_VRCACM1+N3_AMPLIA1)-Abs(N3_VRDACM1 + N3_VRCDA1)
		Endif		
	Endif
Endif

For nX := 2 to __nQuantas
	cMoed := Alltrim(Str(nX))
	aValDepr[nX] := 0
	If Abs(N3_VRDACM2) < Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))
		IF SN3->&("N3_TXDEPR"+cMoed) > 0
			nTaxa 		:= Abs((SN3->&("N3_TXDEPR"+cMoed) * mv_par05)- SN3->&("N3_TXDEPR"+cMoed)) * (( nDiasParcial / Day(LastDay(dDataBase)))) / 1200
			aValDepr[nX]  	:= Round(Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))*nTaxa, X3Decimal("N3_VORIG"+cMoed))
			nDiferenca 	:= Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed))-(aValDepr[nX] + Abs(&("N3_VRDACM"+cMoed)))
			If Round(nDiferenca,X3Decimal("N3_VORIG2")) <= 0
				aValDepr[nX] := Abs(&("N3_VORIG"+cMoed)+&("N3_AMPLIA"+cMoed)) - &("N3_VRDACM"+cMoed)
			Endif
		Endif
	Endif
	

Next
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Trata os res¡duos de deprecia‡ao.                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nDepr1 != 0
	aValDepr[1] := nDepr1
Endif
If Af171VldItem(MV_PAR06, MV_PAR07, SN3->N3_FILIAL, SN3->N3_CBASE, SN3->N3_ITEM, MV_PAR05, @oQueryDep,,SN3->N3_TIPO, SN3->N3_BAIXA, SN3->N3_SEQ)
	Af171Grava()
EndIf

RestArea(aArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af171AGrava  ³Autor ³  Alice Y Yamamoto    ³Data³ 14/05/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza os arquivos SN3,SN4,SN5                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ Af171Grava( )                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ ATFA171                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Af171Grava()
Local aArea    := GetArea()
Local lPadrao
Local dNextDepr 	:= (GetMv("MV_ULTDEPR")) + 1
Local lSoma			:= .F.
//********************************
// Controle de multiplas moedas  *
//********************************
Local __nQuantas := If(lMultMoed,AtfMoedas(),5)
Local nX
Local lOnOff := .T.

lOnOff := MV_PAR18 == 1

dbSelectArea("SN3")
RecLock("SN3")

If aValDepr[1] != 0
	
	//Verifica se já há aceleracao no mes corrente.
	If !Empty(SN3->N3_DTACELE) .and. ( Left(DTOS(mv_par07),6) == Left(DTOS(dNextDepr),6) .And. Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6))
		lSoma := .T.
	Endif

	SN3->N3_INDICE1 := (SN3->N3_TXDEPR1 * mv_par05)
	//Se estiver no mesmo mes e ja tenha ocorrido outra depreciacao acelerada, o valor sera somado.
	If lSoma
		SN3->N3_VRDMES1 += aValDepr[1]
	Else
		SN3->N3_VRDMES1 := aValDepr[1]	
	Endif	
	SN3->N3_VRDACM1 += aValDepr[1] 
	SN3->N3_VRDBAL1 += aValDepr[1] 
	// Se a data da ultima aceleracao, estiver dentro do proximo mes de calculo de depreciacao, soma o valor de 
	// aceleracao do mes, senao substitui.
	SN3->N3_VLACEL1 := If(Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6), SN3->N3_VLACEL1 + aValDepr[1], aValDepr[1])
	SN3->N3_DTACELE := mv_par07
EndIf

//********************************
// Controle de multiplas moedas  *
//********************************
For nX:=2 to __nQuantas
	cMoed := Alltrim(Str(nX))
	If aValDepr[nX] != 0
		SN3->&(If(nX>9,"N3_INDIC","N3_INDICE")+cMoed) := (SN3->&(If(nX>9,"N3_TXDEP","N3_TXDEPR")+cMoed) * mv_par05)
		//Se estiver no mesmo mes e ja tenha ocorrido outra depreciacao acelerada, o valor sera somado.
		If lSoma
			SN3->&(If(nX>9,"N3_VRDME","N3_VRDMES")+cMoed) += aValDepr[nX]
		Else
			SN3->&(If(nX>9,"N3_VRDME","N3_VRDMES")+cMoed) := aValDepr[nX]
		Endif
		SN3->&(If(nX>9,"N3_VRDAC","N3_VRDACM")+cMoed) += aValDepr[nX]
		SN3->&(If(nX>9,"N3_VRDBA","N3_VRDBAL")+cMoed) += aValDepr[nX]
		// Se a data da ultima aceleracao, estiver dentro do proximo mes de calculo de depreciacao, soma o valor de 
		// aceleracao do mes, senao substitui.
		SN3->&(If(nX>9,"N3_VLACE","N3_VLACEL")+cMoed) := If(Left(DTOS(SN3->N3_DTACELE),6) == Left(DTOS(dNextDepr),6), SN3->&(If(nX>9,"N3_VLACE","N3_VLACEL")+cMoed) + aValDepr[nX], aValDepr[nX])
		SN3->N3_DTACELE := mv_par07
	EndIf

Next
msUnLock()

//22/07/2010
PcoIniLan("000365") 
//********************************
// Controle de multiplas moedas  *
//********************************
cPadrao := "840"

If aValDepr[1] != 0
	Reclock("SN4",.T.)
		SN4->N4_FILIAL 	:= xFilial("SN4")
		SN4->N4_CBASE  	:= SN3->N3_CBASE
		SN4->N4_ITEM   	:= SN3->N3_ITEM
		SN4->N4_TIPO   	:= SN3->N3_TIPO
		SN4->N4_OCORR  	:= IIf(aValDepr[1] > 0, "10", "11")   // 10 aCeleracao Positiva
		SN4->N4_TIPOCNT	:= "4"  //DEPREC ACUM
		SN4->N4_CONTA  	:= SN3->N3_CCDEPR
		SN4->N4_DATA   	:= dDataBase
		SN4->N4_VLROC1 	:= Abs(aValDepr[1])
		SN4->N4_VLROC2 	:= Abs(aValDepr[2])
		SN4->N4_VLROC3 	:= Abs(aValDepr[3])
		SN4->N4_VLROC4 	:= Abs(aValDepr[4])
		SN4->N4_VLROC5 	:= Abs(aValDepr[5])		
		SN4->N4_CCUSTO 	:= SN3->N3_CCCDEP
		SN4->N4_SUBCTA 	:= SN3->N3_SUBCCDE
		SN4->N4_CLVL   	:= SN3->N3_CLVLCDE
		SN4->N4_SEQ    	:= SN3->N3_SEQ
		SN4->N4_TXDEPR	:= nTaxa
		SN4->N4_TPSALDO	:= SN3->N3_TPSALDO
		SN4->N4_ORIGEM 	:= Funname()
	
		If cPaisLoc == "RUS"
		 	SN4->N4_UID	:= RU01UUIDV4()
		EndIF
	msUnLock()
	
	dbSelectArea("SN4")
	dbSetOrder(1)
	Reclock("SN4",.T.)
		SN4->N4_FILIAL := xFilial("SN4")
		SN4->N4_CBASE  := SN3->N3_CBASE
		SN4->N4_ITEM   := SN3->N3_ITEM
		SN4->N4_TIPO   := SN3->N3_TIPO
		SN4->N4_OCORR  := IIf(aValDepr[1] > 0, "10", "11")   // 10 aCeleracao Positiva
		SN4->N4_TIPOCNT:= "3"  //despesa de depreciacao
		SN4->N4_CONTA  := SN3->N3_CDEPREC
		SN4->N4_DATA   := dDataBase
		
		If lMultMoed
			AtfMultMoe("SN4","N4_VLROC",{|x| Abs(aValDepr[X]) })
		Else
			SN4->N4_VLROC1 := Abs(aValDepr[1])
			SN4->N4_VLROC2 := Abs(aValDepr[2])
			SN4->N4_VLROC3 := Abs(aValDepr[3])
			SN4->N4_VLROC4 := Abs(aValDepr[4])
			SN4->N4_VLROC5 := Abs(aValDepr[5])
		EndIf
		
		SN4->N4_TXDEPR	:= nTaxa
		SN4->N4_CCUSTO := SN3->N3_CCDESP
		SN4->N4_SUBCTA := SN3->N3_SUBCDEP
		SN4->N4_CLVL   := SN3->N3_CLVLDEP
		SN4->N4_SEQ    := SN3->N3_SEQ
		SN4->N4_LA		 := iIf(lOnOff,"S","N")
		SN4->N4_ORIGEM := Funname()
		SN4->N4_LP		 := cPadrao
		SN4->N4_TPSALDO	:= SN3->N3_TPSALDO

		If cPaisLoc == "RUS"
		 	SN4->N4_UID	:= RU01UUIDV4()
		EndIF
	msUnLock()
	
	//22/07/2010
	PcoDetLan("000365","01","ATFA171")
	ATFSaldo(	SN3->N3_CCDEPR , dDataBase, IIf(aValDepr[1] > 0,"K","L"),;
	Abs(aValDepr[1]), Abs(aValDepr[2]), Abs(aValDepr[3]), Abs(aValDepr[4]),;
	Abs(aValDepr[5]), "+", SN3->N3_TXDEPR1 * mv_par05, SN3->N3_SUBCCDE,, SN3->N3_CLVLCDE,;
	SN3->N3_CCCDEP,"4", aValDepr )
	
	ATFSaldo(	SN3->N3_CDEPREC, dDataBase,IIf(aValDepr[1] > 0,"K","L"),;
	Abs(aValDepr[1]), Abs(aValDepr[2]), Abs(aValDepr[3]), Abs(aValDepr[4]),;
	Abs(aValDepr[5]),"+",SN3->N3_TXDEPR1 * mv_par05,SN3->N3_SUBCDEP,,SN3->N3_CLVLDEP,;
	SN3->N3_CCDESP,"3", aValDepr )
	
	If lOnOff
		lPadrao := VerPadrao(cPadrao)
	
		If mv_par16 != 3
			If nHdlPrv <= 0
				nHdlPrv := HeadProva(cLoteAtf,"ATFA171",Substr(cUsername,1,6),@cArquivo)
			Endif
			nTotal  += DetProva(nHdlPrv,cPadrao,"ATFA171",cLoteAtf)
		Endif
	EndiF
	
EndIf

PcofinLan("000365")

RestArea(aArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³29/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	Local aRotina		:={ {STR0004, "AllwaysTrue",0,3}} //"Parametros"
Return(aRotina)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Af171Vld  ºAutor  ³Microsiga           º Data ³  13/09/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida se trabalha com data virada de saldos                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Af171Vld()
Local lSldAtf
Local lRet := .T.
Local dUltDepr 	:= GetMv("MV_ULTDEPR")

lSldAtf 	:= GetNewPar("MV_ATFRSLD",.F.)

//se tem o parametro MV_ATFRSLD (REFAZ SALDO) CONFIGURADO COM .T. e controla data de virada
If lSldAtf
	dDataVir :=  AtfGetSN0("13","VIRADAATIVO")
	If !Empty(dDataVir)
		dDataVir := STOD(dDataVir)
		
		//data da virada deve ser sempre no primeiro dia do ano
		If ( DTOS(dDataVir) != "19800101" .And. Year(dDataBase) != Year(dDataVir) ) .Or.  dDataBase < dDataVir
			Aviso(STR0017,STR0018+;  //"Atencao"##"O calculo de depreciacao acelerada somente pode ser efetuado após a  virada anual no exercicio vigente. "
							STR0019+CRLF+;  //"No Ativo, a virada ocorre após o cálculo de 31 de Dezembro. "
							STR0020+ cFilAnt+" "+CRLF+;  //"Filial : "
							STR0021+DtoC(dDataVir)+Space(10)+;   //"Ultima Virada : "
							STR0022+DtoC(dUltDepr), {"Ok"})  //"Ultimo Calculo Depreciacao : "
			lRet := .F.		// Invalidar o processo.
		EndIf
	Else
		Aviso(STR0017,STR0023+;  //"Atencao"##"Não encontrada a data da virada anual e o parametro MV_ATFRSLD esta ativo. Verifique! "
						STR0019+Space(50)+CRLF+;  //"No Ativo, a virada ocorre após o cálculo de 31 de Dezembro. "
						STR0020+ cFilAnt+" ", {"Ok"})  //"Filial : "
		lRet := .F.		// Invalidar o processo.
	EndIf
EndIf
  		
Return(lRet)

//-------------------------------------------------------------------
/*/{Protheus.doc}Af171VldItem
Valida se item atende lei Nº 14.871, DE 28 DE MAIO DE 2024

@author Totvs
@since  20/12/24

@version 12
/*/
//-------------------------------------------------------------------
Function Af171VldItem(dDataIni as date, dDataFim as date, cFilDep as character, cBase as character, cItem as character,; 
	nTaxaDepr as numeric, oQueryDep as object, lLote as logical, cTipo as character,cBaixa as character, cSeq as Character) as logical

Local nDeprAtual as numeric
Local nDeprAcum as numeric
Local nMaxDepr as numeric
Local dDtAquis as date
Local lRet as logical

Default lLote  := .F.
Default cTipo  := " "
Default cBaixa := " "
Default cSeq   := " "


lRet := .T.

If !FWIsInCallStack("ATFA050")
	SN3->(dbSetOrder(1)) //N3_FILIAL+N3_CBASE+N3_ITEM+N3_TIPO+N3_BAIXA+N3_SEQ
	SN3->(dbSeek(cFilDep+cBase+cItem+cTipo+cBaixa+cSeq))
EndIf	

If SN3->N3_TIPO == "01" .Or. SN3->N3_TIPO == "07"
	dDtAquis := If(!Empty(SN3->N3_AQUISIC), SN3->N3_AQUISIC, dDataIni)	
	
	If (dDtAquis >= STOD("20240528") .And. dDtAquis <= STOD("20251231")) .And.; //Periodo de vigor da lei Nº 14.871, DE 28 DE MAIO DE 2024
		(Year(dDataBase) == Year(dDtAquis) .Or. Year(dDataBase) == Year(dDtAquis)+1) //Ano de inicio da depreciacao acelerada ou proximo ano subsequente
														
		If lLote
			nDeprAtual := Round(Abs(SN3->N3_VORIG1+SN3->N3_AMPLIA1+SN3->N3_VRCACM1)* nTaxaDepr / 100, X3Decimal("N3_VORIG1"))
		Else
			nDeprAtual := aValDepr[1]
		EndIf
		
		nDeprAcum := Af171TotAcum(@oQueryDep, lLote)
		nMaxDepr  := SN3->N3_VORIG1 / 2 //Valor máximo permitido para depreciacao acelerada (50% do valor do bem)		
							
		If nDeprAtual+nDeprAcum >= nMaxDepr
			If nDeprAcum >= nMaxDepr
				lRet := .F.
			Else //Tratamento de residuo				
				aValDepr[1] := nMaxDepr - nDeprAcum				
			EndIf			
		EndIf		
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}Af171TotAcum
Soma depreciacoes acelaredas om bem no ano

@author Totvs
@since  20/12/24

@version 12
/*/
//-------------------------------------------------------------------
Static Function Af171TotAcum(oQueryDep, lLote) as numeric

Local cQuery as character

//Indice SN4: 4- N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+N4_OCORR+DTOS(N4_DATA)
If oQueryDep == Nil
	cQuery := "SELECT SUM(N4_VLROC1) TOTALDEPR"
	cQuery += " FROM "+RetSQLTab("SN4")
	cQuery += " WHERE N4_FILIAL = ?" 	//P1 FWxFilial("SN4")	
	cQuery += " AND N4_CBASE = ?" 		//P2 SN3->N3_CBASE
	cQuery += " AND N4_ITEM = ?"		//P3 SN3->N3_ITEM
	cQuery += " AND N4_TIPO = ?"	    //P4 "01" ou "07"
	cQuery += " AND N4_OCORR = ?"		//P5 "10"
	cQuery += " AND N4_DATA >= ?"		//P6 FirstYDate(dDatabase)
	cQuery += " AND N4_DATA <= ?"		//P7 LastYDate(dDatabase)
	cQuery += " AND N4_TIPOCNT = ?"		//P8 "4"
	cQuery += " AND D_E_L_E_T_ = ?"		//P9 DELETE
	cQuery := ChangeQuery(cQuery)

	oQueryDep := FwExecStatement():New(cQuery)	
EndIf

oQueryDep:SetString(1, FWxFilial("SN4", SN3->N3_FILIAL))
oQueryDep:SetString(2, SN3->N3_CBASE)
oQueryDep:setString(3, SN3->N3_ITEM)
oQueryDep:SetString(4, If(lLote, "07", "01"))
oQueryDep:SetString(5, "10")
oQueryDep:SetString(6, DTOS(FirstYDate(dDatabase)))
oQueryDep:SetString(7, DTOS(LastYDate(dDatabase)))
oQueryDep:SetString(8, "4")
oQueryDep:SetString(9, Space(1))
	
Return oQueryDep:ExecScalar("TOTALDEPR")
