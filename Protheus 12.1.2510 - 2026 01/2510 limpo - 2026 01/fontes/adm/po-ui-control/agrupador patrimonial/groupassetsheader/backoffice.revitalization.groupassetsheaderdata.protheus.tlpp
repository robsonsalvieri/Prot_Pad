#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.groupassetsheader
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GroupAssetsHeaderProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class GroupAssetsHeaderProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GroupAssetsHeaderProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class GroupAssetsHeaderProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GroupAssetsHeaderProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class GroupAssetsHeaderProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := GroupAssetsHeaderProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca dos agrupadores de Ativos Imobilizados

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cAssetGrouper as Character, nPage as Numeric, nPageSize as Numeric ) class GroupAssetsHeaderProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := "" As Character

    Local cAlias := ""  As Character

    Default nPage     := 1
    Default nPageSize := 10
    Default cAssetGrouper := ""

    If Empty(cAssetGrouper)
        cWhere := GetBranchiesForConditional("FM3", "ATFA510")
    Else
        cWhere := GetBranchiesForConditional("FM4", "ATFA510")
    EndIf

    cAlias := iIf(Empty(cAssetGrouper), "FM3", "FM4")

    ::setUseSpaces(.T.)
    ::setPage(nPage)
    ::setPageSize(nPageSize)
    addMapFields(self, cAlias)
    ::SetQuery(getQuery(cAlias))

    If cAlias = "FM3"
        cWhere += " AND FM3.D_E_L_E_T_ = ' '"
        ::SetWhere(cWhere)
        ::SetOrder("FM3.FM3_FILIAL, FM3.FM3_CODIGO")
    Else
        cWhere += " AND FM4.D_E_L_E_T_ = ' ' "
        cWhere += " AND FM4.FM4_CODIGO = '" + cAssetGrouper + "' "
        ::SetWhere(cWhere)
        ::SetOrder("FM4.FM4_FILIAL, FM4.FM4_CODIGO")
    EndIf

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object, cAlias)
    Local nTamField := 0
    Local cField    := "" as character
    Local cConcat   := tcRetConcat()+"'|'"+tcRetConcat() as character
    Local nI        := 0 as numeric
    Local aAllFields:= FWSX3Util():GetAllFields(cAlias, .F.) as array
    Local aFieldSX3 := {} as Array
    Local aStruct   := {} as Array
    Local cTitulo   := "" as character
    Local lMultiFil := FM4->(FieldPos("FM4_FILORI")) > 0
    
    For nI := 1 To Len(aAllFields)
        cField  := aAllFields[nI]
        aStruct := FWSX3Util():GetFieldStruct(cField, .F.)

        cTitulo := AllTrim(FWX3Titulo(cField))
        aAdd(aFieldSX3,{GetSx3Cache(cField, "X3_ORDEM"),;
                        AllTrim(Lower(cField)),;
                        AllTrim(If(Empty(cTitulo), Lower(cField), cTitulo)),;
                        aStruct[2],;
                        aStruct[3],;
                        aStruct[4],;
                        AllTrim(X3Picture(cField)),""})
    Next
    IF cALias = "FM3"
        nTamField := TamSx3("FM3_FILIAL")[1] + TamSx3("FM3_CODIGO")[1]
        oSelf:addMapFields("value", "value", .T., .F., {"value", "C", nTamField, 0}, "'"+cEmpAnt+"'"+cConcat+"RTRIM(FM3_FILIAL)"+;
                      cConcat+"FM3_CODIGO")
    Else
        nTamField := TamSx3("FM4_FILIAL")[1] + TamSx3("FM4_CODIGO")[1]
        oSelf:addMapFields("value", "value", .T., .F., {"value", "C", nTamField, 0}, "'"+cEmpAnt+"'"+cConcat+"RTRIM(FM4_FILIAL)"+;
                      cConcat+"FM4_CODIGO")
    EndIf
    If cALias == "FM3" .or. !lMultiFil
        For nI := 1 To Len(aFieldSX3)
            cField := Upper(aFieldSX3[nI][2])
            oSelf:addMapFields(cField, cField, .T., .F., {cField, aFieldSX3[nI][4], aFieldSX3[nI][5], aFieldSX3[nI][6]}, cField)
        Next
    Else
        For nI := 1 To Len(aFieldSX3)
            cField := Upper(aFieldSX3[nI][2])
            If cField <> "FM4_FILIAL"
                If cField == "FM4_FILORI"
                    oSelf:addMapFields("FM4_FILIAL", cField, .T., .F., {cField, aFieldSX3[nI][4], aFieldSX3[nI][5], aFieldSX3[nI][6]}, cField)
                Else
                    oSelf:addMapFields(cField, cField, .T., .F., {cField, aFieldSX3[nI][4], aFieldSX3[nI][5], aFieldSX3[nI][6]}, cField)
                EndIf
            EndIf
        Next
    EndIf

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery(cAlias) As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName(cAlias) + " "+cAlias+" "
    cQuery += " WHERE #QueryWhere#"

Return cQuery

