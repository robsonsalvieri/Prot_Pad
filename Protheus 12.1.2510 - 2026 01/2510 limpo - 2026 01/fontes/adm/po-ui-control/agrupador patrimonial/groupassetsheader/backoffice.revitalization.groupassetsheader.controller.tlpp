#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.groupassetsheader
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GroupAssetsHeader
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class GroupAssetsHeader
    public method new()

    @Get("/api/rt/groupdataheader/")
    public method getGroupAssetsHeaders()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GroupAssetsHeader
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class GroupAssetsHeader
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getGroupAssetsHeaders
Metodo que inicia a chamada dos agrupadores do ativo fixo

@author Totvs
/*/
//-------------------------------------------------------------------
method getGroupAssetsHeaders() class GroupAssetsHeader
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cassetgrouper As Character
    Local nI := 0   As Numeric

    //Inicilaiza variaveis
    oService  := GroupAssetsHeaderService():getInstance()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse := "Resposta inválida."
    cassetgrouper := ""

    tcGetPageAndPageSize(@nPage, @nPageSize)


    If oRest:getQueryRequest():hasProperty("assetgrouper")
        cassetgrouper := oRest:getQueryRequest():GetJsonText("assetgrouper")
    EndIf
    jResposta:fromJson(oService:getAllGroupAssetsHeader(cassetgrouper, nPage, nPageSize))

    If ValType(jResposta:GetJsonText("items")) <> "U"
        If Len(jResposta["items"]) > 0
            For nI := 1 to Len(jResposta["items"])
                jResposta["items"][nI]["fm3_codusr"] := AllTrim(UsrRetName(jResposta["items"][nI]["fm3_codusr"]))
            Next nI
        EndIf
        lResponse := .T.
    EndIf
    
    IIf(lResponse,;
        tcAnswerRest(jResposta, .T.),;
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp
return
