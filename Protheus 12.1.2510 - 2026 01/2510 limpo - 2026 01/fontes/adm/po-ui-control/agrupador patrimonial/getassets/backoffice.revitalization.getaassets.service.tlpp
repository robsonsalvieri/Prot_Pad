#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.getassets

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsService
Classe de serviço, realizada instância e chamada de métodos de serviços

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAssetsService
    Data jResponse  as Json

    public method new()
    public method getInstance()
    public method getAllGetAssets()
    public method getassetsbyfilter()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsService
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class GetAssetsService
    ::jResponse := JsonObject():New()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsService
Responsável por instanciar a classe controller

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class GetAssetsService
    static __instance As Object

    If ValType(__instance) == "U"
        __instance := GetAssetsService():new()
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGetAssets
Responsável por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllGetAssets(cFilters as Character, nPage as Numeric, nPageSize as Numeric, cField as Character, cType as Character,cFil as Character,oOpcFilter as Json ) class GetAssetsService
    Local oSettings := GetAssetsProtheusData():getData() As Object
    Local jResponse := JsonObject():new() As Json

    oSettings:getAll(cFilters, nPage, nPageSize, cField, cType, cFil,oOpcFilter)

    IIf(oSettings:lOk,;
            jResponse := oSettings:getJSONResponse(),;
            jResponse := SetRestFault(oSettings:GetCode(), oSettings:getMessage()))

    oSettings:DeActivate()
    FreeObj(oSettings)
return jResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} getassetsbyfilter
Responsável por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getassetsbyfilter(nPage as Numeric, nPageSize as Numeric, jBody as Json) class GetAssetsService
    Local oSettings     := GetAssetsProtheusData():getData() As Object
    Default nPage       := 1
	Default nPageSize   := 10

    oSettings:getAllByFilter(nPage, nPageSize, jBody)

    If oSettings:lOk
        ::jResponse := oSettings:getJSONResponse()
    EndIf

    oSettings:DeActivate()
    FreeObj(oSettings)
return ::jResponse

