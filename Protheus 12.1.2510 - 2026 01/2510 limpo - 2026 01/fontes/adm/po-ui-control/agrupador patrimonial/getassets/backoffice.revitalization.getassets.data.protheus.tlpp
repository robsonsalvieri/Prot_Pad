#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.getassets
using namespace totvs.protheus.backoffice.reconciliation.util
//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAssetsProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    Public method getAllByFilter()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class GetAssetsProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAssetsProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class GetAssetsProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := GetAssetsProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca dos registros da SN1 e SN3

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cFilters as Character, nPage as Numeric, nPageSize as Numeric, cField as Character, cType as Character,cFil as Character,oOpcFilter as json) class GetAssetsProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := GetBranchiesForConditional("SN3","ATFA510") As Character
    Local cConcat:= IIF("MSSQL" $ Alltrim(TcGetDB()), "+", "||") as Character
    Local cGrupo := '' as Character
    Local cAquisic := '' as Character
    Local cDescric := '' as Character
    Local cChapa := '' as Character
    Local cProd := '' as Character
    Local cNfiscal := '' as Character
    Local cSerie := '' as Character
    Local cLocal := '' as Character
    Local cTipo := '' as Character

    Default cField    := ""
    Default cType     := ""
    Default cFilters  := ""
    Default nPage     := 1
    Default nPageSize := 10
    Default cFil      := ""
    Default oOpcFilter := nil

    cField = UPPER(cField)
    cFilters = UPPER(cFilters)

    addMapFields(self)

    ::setUseSpaces(.T.)
    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery())

    If cType == "1" .And. !Empty(cFilters) .And. !Empty(cField)
        If cField == "N1_ITEM"
            cWhere += " AND (N1_CBASE"+cConcat+"N1_ITEM LIKE '%" + cFilters + "%') "
        Else
            cWhere += " AND (" + cField + " LIKE '%" + cFilters + "%') "
        EndIf
    ElseIf cType == "2" .And. !Empty(cFilters) .And. !Empty(cField)
        If !Empty(cFil)
            cWhere += " AND N3_FILIAL ='"+Padr(cFil, TamSx3("N3_FILIAL")[1])+"' "
        EndIf
        If cField == "N1_ITEM" // Caso o campo seja N1_ITEM, é necessário somar com N1_BASE para realizar a busca
            cWhere += " AND N1_CBASE"+cConcat+"N1_ITEM = '"+cFilters+"'"
        Elseif cField == "N1_NFISCAL" // Caso o campo seja N1_NF, é necessário somar com N1_NSERIE para realizar a busca
            cWhere += " AND N1_NFISCAL"+cConcat+"N1_NSERIE = '"+cFilters+"'"
        Else
            cFilters = PadR(cFilters, TamSX3(cField)[1])
            cWhere += " AND "+cField+" = '"+cFilters+"'"
        EndIf
        If oOpcFilter != nil
            IF oOpcFilter:GetJsonValue("n1_grupo", @cGrupo)
                If !Empty(cGrupo)
                    cWhere += " AND N1_GRUPO ='"+Padr(cGrupo, TamSx3("N1_GRUPO")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n3_tipo", @cTipo)
                If !Empty(cTipo)
                    cWhere += " AND N3_TIPO ='"+Padr(cTipo, TamSx3("N3_TIPO")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_aquisic", @cAquisic)
                If !Empty(cAquisic)
                    cWhere += " AND N1_AQUISIC ='"+Dtos(Ctod(cAquisic))+"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_descric", @cDescric)
                If !Empty(cDescric)
                    cWhere += " AND N1_DESCRIC ='"+Padr(cDescric, TamSx3("N1_DESCRIC")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_chapa", @cChapa)
                If !Empty(cChapa)
                    cWhere += " AND N1_CHAPA ='"+Padr(cChapa, TamSx3("N1_CHAPA")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_produto", @cProd)
                If !Empty(cProd)
                    cWhere += " AND N1_PRODUTO ='"+Padr(cProd, TamSx3("N1_PRODUTO")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_nfiscal", @cNfiscal)
                If !Empty(cNfiscal)
                    cWhere += " AND N1_NFISCAL ='"+Padr(cNfiscal, TamSx3("N1_NFISCAL")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_nserie", @cSerie)
                If !Empty(cSerie)
                    cWhere += " AND N1_NSERIE ='"+Padr(cSerie, TamSx3("N1_NSERIE")[1]) +"' "
                EndIf
            EndIf
            IF oOpcFilter:GetJsonValue("n1_local", @cLocal)
                If !Empty(cLocal)
                    cWhere += " AND N1_LOCAL ='"+Padr(cLocal, TamSx3("N1_LOCAL")[1]) +"' "
                EndIf
            EndIf
        EndIf
    EndIf

    cWhere += " AND SN1.D_E_L_E_T_ = ' '"
    cWhere += " AND SN3.D_E_L_E_T_ = ' '"
    cWhere += " AND SN1.N1_BAIXA = ' ' "
    cWhere += " AND SN3.N3_BAIXA = '0' "

    ::SetWhere(cWhere)
    ::SetOrder("SN1.N1_CBASE, SN1.N1_ITEM")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)

    // SN3
    oSelf:addMapFields("N3_TIPO",    "N3_TIPO", .T., .F.,     {"N3_TIPO", "C",  TamSx3("N3_TIPO")[1], 0},   "SN3.N3_TIPO")

    //Conta Contábil
    oSelf:addMapFields("N3_CCONTAB", "N3_CCONTAB", .T., .F.,{"N3_CCONTAB", "C", TamSx3("N3_CCONTAB")[1], 0}, "SN3.N3_CCONTAB")
    oSelf:addMapFields("N3_CDEPREC", "N3_CDEPREC", .T., .F.,{"N3_CDEPREC", "C", TamSx3("N3_CDEPREC")[1], 0}, "SN3.N3_CDEPREC")
    oSelf:addMapFields("N3_CCDEPR", "N3_CCDEPR", .T., .F.,  {"N3_CCDEPR", "C",  TamSx3("N3_CCDEPR")[1], 0 }, "SN3.N3_CCDEPR")
    oSelf:addMapFields("N3_CCORREC", "N3_CCORREC", .T., .F.,{"N3_CCORREC", "C", TamSx3("N3_CCORREC")[1], 0}, "SN3.N3_CCORREC")

    //Centro de Custo
    oSelf:addMapFields("N3_CUSTBEM", "N3_CUSTBEM", .T., .F.,{"N3_CUSTBEM", "C", TamSx3("N3_CUSTBEM")[1], 0}, "SN3.N3_CUSTBEM")
    oSelf:addMapFields("N3_CCUSTO", "N3_CCUSTO", .T., .F.,  {"N3_CCUSTO", "C",  TamSx3("N3_CCUSTO")[1], 0},  "SN3.N3_CCUSTO")
    oSelf:addMapFields("N3_CCDESP", "N3_CCDESP", .T., .F.,  {"N3_CCDESP", "C",  TamSx3("N3_CCDESP")[1], 0},  "SN3.N3_CCDESP")
    oSelf:addMapFields("N3_CCCDEP", "N3_CCCDEP", .T., .F.,  {"N3_CCCDEP", "C",  TamSx3("N3_CCCDEP")[1], 0},  "SN3.N3_CCCDEP")
    oSelf:addMapFields("N3_CCCDES", "N3_CCCDES", .T., .F.,  {"N3_CCCDES", "C",  TamSx3("N3_CCCDES")[1], 0},  "SN3.N3_CCCDES")
    oSelf:addMapFields("N3_CCCORR", "N3_CCCORR", .T., .F.,  {"N3_CCCORR", "C",  TamSx3("N3_CCCORR")[1], 0},  "SN3.N3_CCCORR")

    //Classe de Valor
    oSelf:addMapFields("N3_CLVL", "N3_CLVL", .T., .F.,        {"N3_CLVL", "C",     TamSx3("N3_CLVL")[1], 0},     "SN3.N3_CLVL")
    oSelf:addMapFields("N3_CLVLCON", "N3_CLVLCON", .T., .F.,  {"N3_CLVLCON", "C",  TamSx3("N3_CLVLCON")[1], 0},  "SN3.N3_CLVLCON")
    oSelf:addMapFields("N3_CLVLDEP", "N3_CLVLDEP", .T., .F.,  {"N3_CLVLDEP", "C",  TamSx3("N3_CLVLDEP")[1], 0},  "SN3.N3_CLVLDEP")
    oSelf:addMapFields("N3_CLVLCDE", "N3_CLVLCDE", .T., .F.,  {"N3_CLVLCDE", "C",  TamSx3("N3_CLVLCDE")[1], 0},  "SN3.N3_CLVLCDE")
    oSelf:addMapFields("N3_CLVLDES", "N3_CLVLDES", .T., .F.,  {"N3_CLVLDES", "C",  TamSx3("N3_CLVLDES")[1], 0},  "SN3.N3_CLVLDES")
    oSelf:addMapFields("N3_CLVLCOR", "N3_CLVLCOR", .T., .F.,  {"N3_CLVLCOR", "C",  TamSx3("N3_CLVLCOR")[1], 0},  "SN3.N3_CLVLCOR")

    // Item Conta
    oSelf:addMapFields("N3_SUBCTA", "N3_SUBCTA", .T., .F.,    {"N3_SUBCTA", "C",   TamSx3("N3_SUBCTA")[1], 0},  "SN3.N3_SUBCTA")
    oSelf:addMapFields("N3_SUBCCON", "N3_SUBCCON", .T., .F.,  {"N3_SUBCCON", "C",  TamSx3("N3_SUBCCON")[1], 0},  "SN3.N3_SUBCCON")
    oSelf:addMapFields("N3_SUBCDEP", "N3_SUBCDEP", .T., .F.,  {"N3_SUBCDEP", "C",  TamSx3("N3_SUBCDEP")[1], 0},  "SN3.N3_SUBCDEP")
    oSelf:addMapFields("N3_SUBCCDE", "N3_SUBCCDE", .T., .F.,  {"N3_SUBCCDE", "C",  TamSx3("N3_SUBCCDE")[1], 0},  "SN3.N3_SUBCCDE")
    oSelf:addMapFields("N3_SUBCDES", "N3_SUBCDES", .T., .F.,  {"N3_SUBCDES", "C",  TamSx3("N3_SUBCDES")[1], 0},  "SN3.N3_SUBCDES")
    oSelf:addMapFields("N3_SUBCCOR", "N3_SUBCCOR", .T., .F.,  {"N3_SUBCCOR", "C",  TamSx3("N3_SUBCCOR")[1], 0},  "SN3.N3_SUBCCOR")

    // SN1
    oSelf:addMapFields("N1_FILIAL",   "N1_FILIAL", .T., .F., {"N1_FILIAL", "C", TamSx3("N1_FILIAL")[1], 0}, "SN1.N1_FILIAL")
    oSelf:addMapFields("N1_GRUPO",    "N1_GRUPO", .T., .F.,  {"N1_GRUPO", "C",  TamSx3("N1_GRUPO")[1], 0},  "SN1.N1_GRUPO")
    oSelf:addMapFields("N1_CBASE",    "N1_CBASE", .T., .F.,  {"N1_CBASE", "C",  TamSx3("N1_CBASE")[1], 0},  "SN1.N1_CBASE")
    oSelf:addMapFields("N1_ITEM",     "N1_ITEM", .T., .F.,   {"N1_ITEM", "C",   TamSx3("N1_ITEM")[1], 0},   "SN1.N1_ITEM")
    oSelf:addMapFields("N1_DESCRIC",  "N1_DESCRIC", .T., .F.,{"N1_DESCRIC", "C",TamSx3("N1_DESCRIC")[1], 0},"SN1.N1_DESCRIC")
    oSelf:addMapFields("N1_QUANTD",   "N1_QUANTD", .T., .F., {"N1_QUANTD", "N", TamSx3("N1_QUANTD")[1], 0}, "SN1.N1_QUANTD")
    oSelf:addMapFields("N1_LOCAL",    "N1_LOCAL", .T., .F.,  {"N1_LOCAL", "C",  TamSx3("N1_LOCAL")[1], 0},  "SN1.N1_LOCAL")
    oSelf:addMapFields("N1_AQUISIC",  "N1_AQUISIC", .T., .F.,{"N1_AQUISIC", "C",TamSx3("N1_AQUISIC")[1], 0},"SN1.N1_AQUISIC")
    oSelf:addMapFields("N1_BAIXA",    "N1_BAIXA  ", .T., .F.,{"N1_BAIXA  ", "C",TamSx3("N1_BAIXA  ")[1], 0},"SN1.N1_BAIXA  ")
    oSelf:addMapFields("N1_PRODUTO",  "N1_PRODUTO", .T., .F.,{"N1_PRODUTO", "C",TamSx3("N1_PRODUTO")[1], 0},"SN1.N1_PRODUTO")
    oSelf:addMapFields("N1_NFISCAL",  "N1_NFISCAL", .T., .F.,{"N1_NFISCAL", "C",TamSx3("N1_NFISCAL")[1], 0},"SN1.N1_NFISCAL")
    oSelf:addMapFields("N1_NSERIE",   "N1_NSERIE", .T., .F., {"N1_NSERIE", "C", TamSx3("N1_NSERIE")[1], 0}, "SN1.N1_NSERIE")
    oSelf:addMapFields("N1_CHAPA",    "N1_CHAPA", .T., .F.,  {"N1_CHAPA", "C",  TamSx3("N1_CHAPA")[1], 0},  "SN1.N1_CHAPA")

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character
    
    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("SN3") + " SN3"
    cQuery += " INNER JOIN "+RetSqlName("SN1")+" SN1"
    cQuery += " ON SN3.N3_FILIAL = SN1.N1_FILIAL AND SN3.N3_CBASE = SN1.N1_CBASE AND SN3.N3_ITEM = SN1.N1_ITEM"
    cQuery += " WHERE #QueryWhere#"
Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllByFilter
Metodo responsável pela busca avançada dos itens de ativo

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllByFilter(nPage as Numeric, nPageSize as Numeric, jBody as Json) class GetAssetsProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := GetBranchiesForConditional("SN3","ATFA510") As Character

    DEFAULT nPage      := 1
    DEFAULT nPageSize  := 10

    addMapFields(self)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(GetQuery())

    cWhere += InterceptFilter(jBody)

    cWhere += " AND SN1.D_E_L_E_T_ = ' '"
    cWhere += " AND SN3.D_E_L_E_T_ = ' '"
    cWhere += " AND SN1.N1_BAIXA = ' ' "
    cWhere += " AND SN3.N3_BAIXA = '0' "

    ::SetWhere(cWhere)
    ::SetOrder("SN1.N1_CBASE, SN1.N1_ITEM")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} InterceptFilter
Intercepta os filtros para tratamento de campos que não existem nos
temporários, aqueles que são gerados por case ou campos concatenados
e like realizado sem o método ::SetUrlFilter

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function InterceptFilter(jBody as Json) as Character
    Local cFilter   := "" as Character
    Local cWhereAux := "" as Character
    Local cField    := "" as Character
    Local nI        As Numeric

    //Tratamentos para filtro composto
    If Valtype(jBody) == "J" .And. ValType(jBody["fields"]) == "A"
        For nI := 1 To Len(jBody["fields"])
            cFilter := StrTran(jBody["fields"][nI]["value"],"'","")
            If cFilter <> '' // Só adiciona o filtro se tiver valor
                cField := Upper(StrTran(jBody["fields"][nI]["field"],"'",""))
                If Len(TamSX3(cField)) > 0
                    cWhereAux += " AND "+cField+" = '"+cFilter+"'"
                EndIf
            EndIf
        Next nI
    EndIf
Return cWhereAux
