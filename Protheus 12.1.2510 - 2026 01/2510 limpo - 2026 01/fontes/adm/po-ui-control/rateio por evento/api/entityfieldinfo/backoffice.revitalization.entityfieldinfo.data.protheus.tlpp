#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.entityfieldinfo
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityFieldInfoData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class EntityFieldInfoData from FWAdapterBaseV2
	Public method new()
	Public method getData()
	Public method getEntityData()
	Public method getEntityValueRangeData()
	static method getInstance() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityFieldInfoData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------

method new(cVerbo as Character, lList as Logical) Class EntityFieldInfoData
	Default cVerbo := "GET"
	Default lList  := .T.
	_Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getInstance
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class EntityFieldInfoData As Object
	static __oActiveData As Object

	If ValType(__oActiveData) == "U"
		__oActiveData := EntityFieldInfoData():new()
	EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
Metodo responsável pela busca das informacos da entidade

@author Totvs
/*/
//-------------------------------------------------------------------
method getData(cEntity as Character, cField as character) class EntityFieldInfoData
	Local aResp as Array
	Local oResp as Json
	Local aCampos as Array
	Local aTamSx3 as Array
	Local nX := 1
	Local oRetorno as Json

	aResp := {}
	aCampos := FWSX3Util():GetAllFields( cEntity , .F.)
	oRetorno := JsonObject():new()

	If oRest:getQueryRequest():hasProperty("field")
		cField := oRest:getQueryRequest():GetJsonText("field")
	EndIf

	If !Vazio(cField)
		aTamSx3 := TamSX3(SubStr(cField,1,10))
		oResp := JsonObject():new()
		oResp["x3_campo"]   := cField
		oResp["x3_tipo"]    := aTamSx3[3]
		oResp["x3_tamanho"] := aTamSx3[1]
		oResp["x3_decimal"] := aTamSx3[2]
		aaDD(aResp, oResp)
	Else
		For nX := 1 to Len(aCampos)
			aTamSx3 := TamSX3(aCampos[nX])
			oResp := JsonObject():new()
			oResp["x3_campo"]   := aCampos[nX]
			oResp["x3_tipo"]    := aTamSx3[3]
			oResp["x3_tamanho"] := aTamSx3[1]
			oResp["x3_decimal"] := aTamSx3[2]
			aaDD(aResp, oResp)
		Next nX
	EndIf
	oRetorno["items"] := aResp
return oRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} getEntityValueRangeData
Metodo responsável pela busca das informacos da entidade

@author Totvs
/*/
//-------------------------------------------------------------------
method getEntityValueRangeData(cEntity as Character, cValue as Character, cRange as Character ) class EntityFieldInfoData
	Local oRetorno as Json	
	Local cAlias := GetNextAlias() as Character
	Local cRangeFil := "" as Character
	Local cCVOPlano := "" as Character
	Local cCVOValue := "" as Character
	local ojFieldTb := JsonObject():new() as json
	local ojFieldDesc := JsonObject():new() as json
	Local cField as character
	Local cDesc as character
	Local aRet := {} as Array		
	Local aRange1 := {} as ARRAY
	Local aRange2 := {} as ARRAY
	Local aValue  := {} as ARRAY	
	Local cDataBase := Alltrim(TcGetDB())
	Local cSubStr := IIf('SQL'$cDataBase,'SUBSTRING','SUBSTR')

	Default cRange  := ""
	Default cValue  := ""
	
	If oRest:getQueryRequest():hasProperty("entity")
		cEntity := oRest:getQueryRequest():GetJsonText("entity")
	EndIf

	If oRest:getQueryRequest():hasProperty("value")
		cValue := oRest:getQueryRequest():GetJsonText("value")
	EndIf

	If oRest:getQueryRequest():hasProperty("range")
		cRange := oRest:getQueryRequest():GetJsonText("range")
	EndIf

	cRangeFil  := cEntity + "_FILIAL "
	cDesEntity := cEntity + "_DESC1 "
	oRetorno := JsonObject():new()

	If "CV0" $ cEntity 
		cCVOPlano := SubStr(cEntity, 4, 5)
		cEntity   := SUBSTR(cEntity, 1, 3)
		cRangeFil  := cEntity + "_FILIAL "
	EndIf

	ojFieldTb["CQJ"] := 'CQJ_CODEVE'
	ojFieldTb["CT1"] := 'CT1_CONTA'
	ojFieldTb["CTT"] := 'CTT_CUSTO'
	ojFieldTb["CTD"] := 'CTD_ITEM'
	ojFieldTb["CTH"] := 'CTH_CLVL'
	ojFieldTb["CV0"] := 'CV0_CODIGO'

	ojFieldDesc["CQJ"] := 'CQJ_DESC'
	ojFieldDesc["CT1"] := 'CT1_DESC01'
	ojFieldDesc["CTT"] := 'CTT_DESC01'
	ojFieldDesc["CTD"] := 'CTD_DESC01'
	ojFieldDesc["CTH"] := 'CTH_DESC01'
	ojFieldDesc["CV0"] := 'CV0_DESC'

	cField := ojFieldTb[cEntity]
	cDesc  := ojFieldDesc[cEntity]

	if At("-", cRange) > 0
		aRange1 := Separa(cRange, "-")
		aRange2 := Separa(aRange1[2], ";")	
		aRange1 := Separa(aRange1[1], ";")
		aRange1[2] := AllTrim(Str(Val(aRange1[2])-Val(aRange1[1])+1))
		aRange2[2] := AllTrim(Str(Val(aRange2[2])-Val(aRange2[1])+1))
		aValue  := Separa(cValue, "-")
	Else
		aRange1 :=  separa( cRange, ";")	
		aRange1[2] := AllTrim(Str(Val(aRange1[2])-Val(aRange1[1])+1))
	EndIf

	If cEntity $ "CT1|CTT|CV0|CTD|CTH|CQJ" 
		cAlitas := GetNextAlias()
		cQuery := " SELECT "+cField+", "+cDesc+ " "
		cQuery += " FROM "+RetSQLName(cEntity)+" "+cEntity
		cQuery += " WHERE D_E_L_E_T_ = ' ' AND " +cRangeFil+" = '"+xFilial(cEntity)+"' AND "
		If "CV0" $ cEntity 
			cCVOValue := SubStr(cValue,3)
			cQuery += " CV0_PLANO = '"+cCVOPlano+"' AND"
		EndIf

		If At("-", cRange) > 0
			cQuery += " "+cSubStr+"( "+cField+","+aRange1[1]+","+aRange1[2]+" ) = '"+aValue[1]+"' "		
			cQuery += " AND "+cSubStr+"( "+cField+","+aRange2[1]+","+aRange2[2]+" ) = '"+aValue[2]+"' "
		Else
			cQuery += " "+cSubStr+"( "+cField+","+aRange1[1]+","+aRange1[2]+" ) = '"+cValue+"' "
		EndIf

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

		While !(cAlias)->(Eof()) 
			Aadd(aRet,JsonObject():new())
			nPos := Len(aRet)

			aRet[nPos]["cod_conta"] := AllTrim((cAlias)->(&cField))
			aRet[nPos]["cod_desc"]  := AllTrim((cAlias)->(&cdesc))

			(cAlias)->(dbSkip())
		EndDo
		(cAlias)->(dbCloseArea())
	EndIf
	oRetorno["items"]  := aRet

return oRetorno
