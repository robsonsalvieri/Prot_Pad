#include "tlpp-core.th"

namespace totvs.protheus.backoffice.revitalization.applyfromto
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ApportionmentPromToProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class ApplyFromToData
	Public method new()
	Public method getApplyFromTo()
	private method getApplyFromToAdvanded()
	static method getInstance() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyFromToData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() Class ApplyFromToData
return

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyFromToData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class ApplyFromToData As Object
	static __oActiveData As Object

	If ValType(__oActiveData) == "U"
		__oActiveData := ApplyFromToData():new()
	EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAplyFromTo
Metodo responsável por pegar o de para das contas e retornar o json
com os dados dos de para das contas
@author Totvs
/*/
//-------------------------------------------------------------------
method getApplyFromTo(ojBody as json) class ApplyFromToData
	Local cAliasQLL  := GetNextalias() as character
	Local cFieldOri  := ""	 as character
	Local cFieldDes  := ""   as character
	Local cmd        := ""   as character
	Local nX         := 1	 as numeric
	Local nI         := 1	 as numeric
	Local nPos       := 1	 as numeric
	Local aResp      := {}   as array
	Local aRespAvanced := {} as array

	Local ojFieldJson := JsonObject():new() as json
	Local oReturn := JsonObject():new() as json

	aResp := {}
	ojFieldJson["CT1"] := "accountCPar"
	ojFieldJson["CTT"] := "costCenterCpar"
	ojFieldJson["CTD"] := "accountingItemCPar"
	ojFieldJson["CTH"] := "valueClassCpar"
	ojFieldJson["CV005"] := "entity05Cpar"
	ojFieldJson["CV006"] := "entity06Cpar"
	ojFieldJson["CV007"] := "entity07Cpar"
	ojFieldJson["CV008"] := "entity08Cpar"
	ojFieldJson["CV009"] := "entity09Cpar"

	cmd := ;
		" SELECT QLL_FILIAL, QLL_CODIGO, QLL_TIPO, QLL_DESCRI, QLL_ENTORI, QLL_ENTDES," +;
		" QLL_SELORI, QLL_SELDES, QLL_POSORI, QLL_POSDES, QLL_VALORI, QLL_VALDES, QLL_EVERAT" +;
		" FROM " + RetSqlName("QLL") +;
		" WHERE " 										+;
		" QLL_FILIAL = '" + FWxFilial("QLL") + "' " +;
		" AND D_E_L_E_T_ = ' ' "   						+;
		" ORDER BY QLL_FILIAL, QLL_CODIGO, QLL_SEQ"
	dbUseArea(.T.,"TOPCONN", TcGenQry(,,cmd), cAliasQLL,.T.,.T.)

	dbSelectArea(cAliasQLL)
	While (cAliasQLL)->(!eof())
		//Regra Avancada
		If  ( ( (cAliasQLL)->QLL_TIPO == "2" .And. !Vazio((cAliasQLL)->QLL_POSORI) .And. !Vazio((cAliasQLL)->QLL_VALORI);
				.And. !Vazio((cAliasQLL)->QLL_SELDES) ) ;
				.Or. ( (cAliasQLL)->QLL_TIPO == "3" .And. !Vazio((cAliasQLL)->QLL_POSORI) .And. !Vazio((cAliasQLL)->QLL_POSDES); 
				.And. !Vazio((cAliasQLL)->QLL_VALORI) .And. !Vazio((cAliasQLL)->QLL_VALDES) ))

			aRespAvanced := ::getApplyFromToAdvanded(cAliasQLL, ojBody, ojFieldJson)
			For nI := 1 To Len(aRespAvanced)
				aAdd(aResp, aRespAvanced[nI])
			Next nI
		EndIf

		//Regra Simples
		For nX := 1 To Len(ojBody["items"])
			If !Vazio((cAliasQLL)->QLL_SELORI) .and. !Vazio((cAliasQLL)->QLL_SELDES)
				cFieldOri = ojFieldJson[AllTrim((cAliasQLL)->QLL_ENTORI)]
				cFieldDes = ojFieldJson[AllTrim((cAliasQLL)->QLL_ENTDES)]
				If AllTrim(ojBody["items"][nX][cFieldOri]) == AllTrim((cAliasQLL)->QLL_SELORI)
					aAdd( aResp, JsonObject():new() )
					nPos = Len(aResp)

					aResp[nPos]["qll_codeFromTo"] := AllTrim((cAliasQLL)->QLL_CODIGO)
					aResp[nPos]["qll_desc"]   := AllTrim((cAliasQLL)->QLL_DESCRI)
					aResp[nPos]["qll_entori"] := AllTrim((cAliasQLL)->QLL_ENTORI)
					aResp[nPos]["qll_entdes"] := AllTrim((cAliasQLL)->QLL_ENTDES)
					aResp[nPos]["qll_valori"] := AllTrim((cAliasQLL)->QLL_SELORI)
					aResp[nPos]["qll_valdes"] := AllTrim((cAliasQLL)->QLL_SELDES)
					aResp[nPos]["qll_posori"] := AllTrim((cAliasQLL)->QLL_POSORI)
					aResp[nPos]["qll_posdes"] := AllTrim((cAliasQLL)->QLL_POSDES)
					aResp[nPos]["qll_tipo"]   := AllTrim((cAliasQLL)->QLL_TIPO)
					aResp[nPos]["qll_everat"] := If(AllTrim((cAliasQLL)->QLL_EVERAT) == "1", "true", "false")
				EndIf
			EndIf
		Next
		(cAliasQLL)->(dbSkip())
	EndDo
	(cAliasQLL)->(dbCloseArea())

	oReturn["items"] := aResp

return oReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} getApplyFromToAdvanded
Metodo responsável por aplicar o de para das contas.

@author Totvs
/*/
//-------------------------------------------------------------------
method getApplyFromToAdvanded(cAliasQLL as character, ojBody as json, ojFieldJson as json) class ApplyFromToData
	Local cFieldOri	:= "" as character
	Local cFieldDes	:= "" as character
	Local ctbOri    := "" as character
	Local ctbDes    := "" as character
	Local cApportionmentCode  := "" as character
	Local cApportionmentEvent := "" as character
	Local cResult   := "" as character
	Local nI        := 0  as numeric
	Local aRanges   := {} as array
	Local aResp     := {} as array
	Local ojFieldTb := JsonObject():new() as json

	ctbOri := AllTrim((cAliasQLL)->QLL_ENTORI)
	ctbDes := AllTrim((cAliasQLL)->QLL_ENTDES)

	If "-" $ (cAliasQLL)->QLL_POSORI
		aRanges := Separa(AllTrim((cAliasQLL)->QLL_POSORI), "-")
	Else
		aRanges :=  Separa(AllTrim((cAliasQLL)->QLL_POSORI), ";")
		For nI := 1 To Len(aRanges)
			aRanges[nI] = Val(aRanges[nI])
			If nI == 2 .And. aRanges[1] == aRanges[2]
				aRanges[2] := 1 // Validação para quando estou pegando 1 posição somente
			EndIf
		Next
	EndIf

	cFieldOri = ojFieldJson[cTbOri]
	cFieldDes = ojFieldJson[cTbDes]

	ojFieldTb["CQJ"] := "CQJ_CODEVE"
	ojFieldTb["CT1"] := "CT1_CONTA"
	ojFieldTb["CTT"] := "CTT_CUSTO"
	ojFieldTb["CTD"] := "CTD_ITEM"
	ojFieldTb["CTH"] := "CTH_CLVL"
	ojFieldTb["CV0"] := "CV0_CODIGO"

	For nI := 1 To Len(ojBody["items"])
		cApportionmentCode  := ojBody["items"][nI]["apportionmentCode"]
		cApportionmentEvent := ojBody["items"][nI]["apportionmentEvent"]

		If "-" $ AllTrim((cAliasQLL)->QLL_POSORI)
			cResult := ApplyRange(ojBody["items"][nI][cFieldOri], AllTrim((cAliasQLL)->QLL_POSORI))
		Else
			cResult := SubStr(ojBody["items"][nI][cFieldOri], aRanges[1], aRanges[2])
		EndIf

		If cResult == AllTrim((cAliasQLL)->QLL_VALORI)
			If  !vazio(cTbDes+"_FILIAL")
				aAdd(aResp, JsonObject():new())
				nPos = Len(aResp)

				aResp[nPos]["qll_codeFromTo"] := AllTrim((cAliasQLL)->QLL_CODIGO)
				aResp[nPos]["qll_desc"]   := AllTrim((cAliasQLL)->QLL_DESCRI)
				aResp[nPos]["qll_entori"] := AllTrim((cAliasQLL)->QLL_ENTORI)
				aResp[nPos]["qll_entdes"] := AllTrim((cAliasQLL)->QLL_ENTDES)
				aResp[nPos]["qll_valori"] := AllTrim((cAliasQLL)->QLL_VALORI)
				
				// Avançada (De Para Avançada utiliza evento de rateio )
				If AllTrim((cAliasQLL)->QLL_EVERAT) == "1" .And. !Empty(cApportionmentEvent)
					aResp[nPos]["qll_posdes"] := AllTrim((cAliasQLL)->QLL_POSDES)
					aResp[nPos]["qll_valdes"] := GetApportionmentEvent(cApportionmentCode, cApportionmentEvent,;
																		cAliasQLL, ojBody["items"][nI])
				// Composta
				Else
					aResp[nPos]["qll_seldes"] := AllTrim((cAliasQLL)->QLL_SELDES)
				EndIf
				aResp[nPos]["qll_posori"] := AllTrim((cAliasQLL)->QLL_POSORI)
				// aResp[nPos]["qll_posdes"] := AllTrim((cAliasQLL)->QLL_POSDES)
				aResp[nPos]["qll_tipo"]   := AllTrim((cAliasQLL)->QLL_TIPO)
				aResp[nPos]["qll_everat"] := If(AllTrim((cAliasQLL)->QLL_EVERAT) == "1", "true", "false")
			EndIf
		EndIf
	Next
return aResp

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyRange
Funcao responsavel em aplicar o range das entidades

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function ApplyRange(cConta, cRange)

Local aRanges := Separa(cRange, "-") as array
Local aRange := {} as array
Local aArray := {} as array
Local cResultString := "" as character
Local nStart := 0 as numeric
Local nEnd   := 0 as numeric
Local nI     := 0 as numeric

For nI := 1 To Len(aRanges)
	aRange := separa(aRanges[nI], ";")
	nStart := Val(aRange[1])
	nEnd   := Val(aRange[2])
	aAdd(aArray, SubStr(cConta, nStart, nEnd - nStart + 1))
Next

For nI := 1 To Len(aArray)
	cResultString += aArray[nI]
	If nI < Len(aArray)
		cResultString += "-"
	EndIf
Next

Return cResultString

//-------------------------------------------------------------------
/*/{Protheus.doc} GetApportionmentEvent
Funcao responsavel em buscar correspondencia no Evento de Rateio

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetApportionmentEvent(cApportionmentCode, cApportionmentEvent, cAliasQLL, oRateio)

Local cResponse       := "" as character
Local nPosOriginStart := GetPosEntity("start", (cAliasQLL)->QLL_POSORI) as numeric
Local nPosOriginEnd   := GetPosEntity("end", (cAliasQLL)->QLL_POSORI) as numeric
Local cTableOrigin    := AllTrim((cAliasQLL)->QLL_ENTORI) as character
Local cEntityOrigin   := GetPropertyApportionment(cTableOrigin, cAliasQLL) as character

If SubStr(oRateio[cEntityOrigin], nPosOriginStart, nPosOriginEnd) == AllTrim((cAliasQLL)->QLL_VALORI)
	cResponse := GetValueEntity(cApportionmentCode, cApportionmentEvent, cAliasQLL)
EndIf

Return cResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} GetPropertyApportionment
Funcao responsavel em retornar a entidade no objeto de rateio - CTQ

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetPropertyApportionment(cTable as character, cAliasQLL as character)

Local cResponse := "" as character

If cTable == "CT1" //Conta Contabil
	cResponse := "accountCPar"
ElseIf cTable == "CTT" //Centro de Custo
	cResponse := "costCenterCpar"
ElseIf cTable == "CTD" //Item de Valor
	cResponse := "accountingItemCPar"
ElseIf cTable == "CTH" //Classe de Valor
	cResponse := "valueClassCpar"
ElseIf cTable == "CV005" // Ent05
	cResponse := "entity05Cpar"
ElseIf cTable == "CV006" // Ent06
	cResponse := "entity06Cpar"
ElseIf cTable == "CV007" // Ent07
	cResponse := "entity07Cpar"
ElseIf cTable == "CV008" // Ent08
	cResponse := "entity08Cpar"
ElseIf cTable == "CV009" // Ent09
	cResponse := "entity09Cpar"			
EndIf	

Return cResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} GetValueEntity
Funcao responsavel em retornar o valor da entidade no Cadastro de
Evento de Rateio (CQK)

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetValueEntity(cApportionmentCode as character, cApportionmentEvent as character, cAliasQLL as character)

Local cResponse      := "" as character
Local cQuery         := "" as character
Local cEntityDebit   := "" as character
Local cEntityCredit  := "" as character
Local cSuffix        := "" as character
Local cAliasCQK      := GetNextAlias() as character
Local cTableDestiny  := AllTrim((cAliasQLL)->QLL_ENTDES) as character
Local cPrefix        := AllTrim((cAliasQLL)->QLL_VALDES) as character
Local nPosDestinyEnd := GetPosEntity("end", (cAliasQLL)->QLL_POSDES) as numeric

If cTableDestiny == "CT1" //Conta Contabil
	cEntityDebit  := "CQK_DEBITO"
	cEntityCredit := "CQK_CREDIT"
ElseIf cTableDestiny == "CTT" //Centro de Custo
	cEntityDebit  := "CQK_CCD"
	cEntityCredit := "CQK_CCC"
ElseIf cTableDestiny == "CTD" //Item de Valor
	cEntityDebit  := "CQK_ITEMD"
	cEntityCredit := "CQK_ITEMC"
ElseIf cTableDestiny == "CTH" //Classe de Valor
	cEntityDebit  := "CQK_CLVLDB"
	cEntityCredit := "CQK_CLVLCR"
ElseIf cTableDestiny == "CV005" //Ent 05
	cEntityDebit  := "CQK_EC05DB"
	cEntityCredit := "CQK_EC05CR"
ElseIf cTableDestiny == "CV006" //Ent 06
	cEntityDebit  := "CQK_EC06DB"
	cEntityCredit := "CQK_EC06CR"
ElseIf cTableDestiny == "CV007" //Ent 07
	cEntityDebit  := "CQK_EC07DB"
	cEntityCredit := "CQK_EC07CR"	
ElseIf cTableDestiny == "CV008" //Ent 08
	cEntityDebit  := "CQK_EC08DB"
	cEntityCredit := "CQK_EC08CR"				
ElseIf cTableDestiny == "CV009" //Ent 09
	cEntityDebit  := "CQK_EC09DB"
	cEntityCredit := "CQK_EC09CR"					
EndIf

cQuery := "SELECT"
cQuery += " CASE WHEN CQK_ENTBAS = '1' THEN "+cEntityDebit+" ELSE "+cEntityCredit+" END AS APP_ENTITY"
cQuery += " FROM "+RetSqlName("CQK")
cQuery += " WHERE CQK_FILIAL = '"+xFilial("CQK")+"'"
cQuery += " AND CQK_CODEVE = '"+cApportionmentEvent+"' "
cQuery += " AND D_E_L_E_T_ = '"+Space(1)+"' "

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasCQK,.T.,.F.)

While (cAliasCQK)->(!EOF())
	cSuffix := SubStr((cAliasCQK)->APP_ENTITY, nPosDestinyEnd+1, Len(AllTrim((cAliasCQK)->APP_ENTITY)))

	//Valida se entidade existe na base de dados
	If GetValidEntity(@cResponse, cPrefix, cSuffix, cTableDestiny)
		Exit
	EndIf

	(cAliasCQK)->(dbSkip())
EndDo

(cAliasCQK)->(dbCloseArea())

Return cResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} GetValidEntity
Funcao responsavel validar se entidade existe no banco de dados
Evento de Rateio (CQK)

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetValidEntity(cValue as character, cPrefix as character, cSuffix as character,;
								cTableDestiny as character)

Local lResponse := .F. as logical
Local cNewEntity := cPrefix+cSuffix as character
Local cEnt   	 := ""  as character
//adicionar plano se entidade for em branco
If left(cTableDestiny,3) == "CV0"
	cEnt := PlanoCT0( SubStr(cTableDestiny,4,2))
	cTableDestiny := "CV0"
EndIf

(cTableDestiny)->(dbSetOrder(1))
If (cTableDestiny)->(MsSeek(xFilial(cTableDestiny)+cEnt+cNewEntity))
	cValue := AllTrim(cNewEntity)
	lResponse := .T.
EndIf

Return lResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} GetPosEntity
Funcao responsavel retornar as posicoes inicial e final das entidades
conforme cadastro De-Para

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetPosEntity(cTypeEntity as Character, cPosition as character)

Local nResponse := 0 as numeric
Local nPosStart := 0 as numeric
Local nPosEnd   := 0 as numeric

cPosition := AllTrim(cPosition)

If cTypeEntity == "start"
	nResponse := Val(SubStr(cPosition,1, AT(";", cPosition)-1))
Else
	nPosStart := Val(SubStr(cPosition,1, AT(";", cPosition)-1))
	nPosEnd   := Val(SubStr(cPosition, AT(";",cPosition)+1,Len(cPosition)))
	nResponse := nPosEnd-nPosStart+1
EndIf

Return nResponse



//-------------------------------------------------------------------
/*/{Protheus.doc} getPlanoCT0
Retorna Plano CT0 para entidades adicionais

@author Totvs
/*/
//-------------------------------------------------------------------

Static Function PlanoCT0(cIdEntid as Character) as Character

Local cRet := "" as Character
Local aSaveArea := GetArea() as Array

Default cIdEntid := "" 

dbSelectArea("CT0")
dbSetOrder(1)
If dbSeek(FwxFilial("CT0")+cIdEntid)
	cRet := CT0->CT0_ENTIDA
EndIf

RestArea(aSaveArea)

Return(cRet)
