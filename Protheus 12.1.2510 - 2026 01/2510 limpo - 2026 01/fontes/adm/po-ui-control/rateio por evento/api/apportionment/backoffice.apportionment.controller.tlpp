#include "tlpp-core.th"
#include "protheus.ch"
#include "backoffice.apportionment.controller.ch"

namespace totvs.protheus.backoffice.apportionment
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} Apportionment
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class Apportionment
    private Data nOpcAuto as numeric
    
    public method new()    

    @Get("/api/ctb/apportionment/")
    public method getAllApportionment()

    @Get('api/ctb/apportionment/:id')
    public method getApportionmentById()
    
    @Post("/api/ctb/apportionment/")
    public method postApportionment()

    @Delete("/api/ctb/apportionment/")
    public method deleteApportionment()

    private method setApportionment()
    private method validJBody()
    private method validItems()
    private method execDePara()
    private method ApplyRange()
    private method getEntPart()
    private method validClasseBloq()
    private method getCodFil()
    private method validEntidades()
    private method execValidEnt()
    private method getAliasCT0() as Character
    private method getPlanoCT0() as Character
    
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} Apportionment
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class Apportionment    
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postApportionment
Metodo que inicia a chamada get da Cadastro de Rateio com id

@author Totvs
/*/
//-------------------------------------------------------------------
method getApportionmentById() class Apportionment
    Local cId := oRest:getPathParamsRequest()["id"] as Character
    ::getAllApportionment(cId)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllApportionment
Metodo que inicia a chamada get da Cadastro de Rateio

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllApportionment(cId as character) class Apportionment
    Local oService  As Object
    Local oField    As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local lHeader   As Logical
    Local cSearchValue as Character

    Default cId := ""

    //Inicilaiza variaveis
    oService  := ApportionmentService():getInstance()
    oField    := tcTreatsFilter():new()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."
    lHeader   := .F.

    tcGetHeaders()

    If oRest:getQueryRequest():GetJsonText("header") == "true"
        lHeader := .T.
    EndIf

    if oRest:getQueryRequest():GetJsonText("searchvalue") <> "null"
        cSearchValue := oRest:getQueryRequest()["searchvalue"]
    endIf

    tcGetPageAndPageSize(@nPage, @nPageSize)

    jResposta:fromJson(oService:getAllApportionment(nPage, nPageSize, lHeader, cId, cSearchValue))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        tcAnswerRest(jResposta, .T.),;    
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postApportionment
Metodo que inicia a chamada de alteracao da Cadastro de Rateio

@author Totvs
/*/
//-------------------------------------------------------------------
method postApportionment() class Apportionment
    ::nOpcAuto := 4
    ::setApportionment()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} deleteApportionment
Metodo que inicia a chamada de exclusao da Cadastro de Rateio

@author Totvs
/*/
//-------------------------------------------------------------------
method deleteApportionment() class Apportionment
    ::nOpcAuto := 5
    ::setApportionment()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setApportionment
Metodo que inicia a chamada do CRUD para Cadastro de Rateio

@author Totvs
/*/
//-------------------------------------------------------------------
method setApportionment() class Apportionment
    Local cFilBkp   := cFilAnt as Character
    Local cChaveRes := "message" as Character
    Local cBody     := oRest:getBodyRequest() as Object    
    Local cResponse := STR0002 as Character //"Conteúdo do corpo inválido."
    Local jBody     := JsonObject():new() as Json    
    Local aResponse := {} as Array    
    Local oService  := ApportionmentService():getInstance() as Object    
    Local lResponse := .F. as Logical        
    Local uRet      := Nil
    Local lRet      := .T. as Logical
    Private lEnt05  := CTQ->(ColumnPos("CTQ_E05ORI")) > 0 as logical
    Private lEnt06  := CTQ->(ColumnPos("CTQ_E06ORI")) > 0 as logical
    Private lEnt07  := CTQ->(ColumnPos("CTQ_E07ORI")) > 0 as logical
    Private lEnt08  := CTQ->(ColumnPos("CTQ_E08ORI")) > 0 as logical
    Private lEnt09  := CTQ->(ColumnPos("CTQ_E09ORI")) > 0 as logical

    If ValType(cBody) == "C"
        uRet := jBody:fromJson(cBody)
        lRet := jBody:hasProperty("isCSVImport")
        If ValType(uRet) == "U" .And. lRet
            tcGetHeaders() //Posiciona na filial passada pelo tenantId
            If jBody["isCSVImport"] .or. jBody["isFrontInsert"]
                If ::validJBody(@jBody, @cResponse)
                    aResponse := oService:updateEvent(jBody)
                EndIf
            Else
                aResponse := oService:setApportionments(jBody, ::nOpcAuto)
            EndIf

            If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                lResponse := aResponse[1]
                cResponse := aResponse[2]
            EndIf
        EndIf
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse, .F.)
    FwFreeArray(aResponse)
    FreeObj(oService)                

    //Restaura empresa e filial
    cFilAnt := cFilBkp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} validJBody
Valida o JSON recebido e prepara os arrays de cabeçalho e item para 
gravação

@author Totvs
/*/
//-------------------------------------------------------------------
method validJBody(jBody as Json, cResponse as Character) class Apportionment
    Local lRet      := .T. as Logical
    Local nI        := 0 as Numeric    
    Local jBodyAux  :=  JsonObject():New() as Json    
    Local cCodEve   := "" as Character
    Local cFilArq   := "" as Character    
    Local jCodFil   := ::getCodFil()
    Private nTamEve :=  nil
    Private nTamDes :=  nil
    Private nTamReg :=  nil
    Private lCTBAPRTVLD := ExistBlock("CTBAPRTVLD")

    Default nTamEve := TamSX3("CTQ_EVERAT")[1]

    lRet := jBody:hasProperty("rules")

    If lRet
        lRet := Len(jBody["rules"]) > 0
        For nI := 1 to Len(jBody["rules"])
            jBodyAux := jBody["rules"][nI]
            If !jBodyAux:hasProperty("header") .Or. !jBodyAux:hasProperty("items")
                cResponse := STR0003 //"Estrutura inválida: Cabeçalho e Itens"
                lRet := .F.
            EndIf 

            If lRet .And. jBody["isCSVImport"]

                cCodEve := PadR(jBody["rules"][nI]["header"]["ctq_everat"],nTamEve)
                cFilArq := jBody["rules"][nI]["header"]["ctq_filial"]    
                cFilCTQ := xFilial("CTQ",cFilArq)
                cFilCQK := xFilial("CQK",cFilArq)

                If lRet 
                    lRet := jCodFil[cFilCTQ]
                    If !lRet
                        cResponse := STR0022+" ("+cFilCTQ+") "+STR0004 //"A filial" //"informada no arquivo, não foi encontrada na base de dados."
                    EndIf
                EndIf
                
                If lRet 
                    lRet := !Empty(cCodEve) .And. CQK->(dbSeek(cFilCQK+cCodEve))

                    If !lRet
                        cResponse := STR0006+" ("+cCodEve+") "+STR0005 //"informado no arquivo, não foi encontrado na base de dados." //"O evento"
                    EndIf
                EndIf
            EndIf
            
            If !lRet 
                Exit
            EndIf            
        Next nI

        If lRet 
            lRet := ::validItems(@jBody,@cResponse)
        EndIf
    Else
        cResponse := STR0007 //"O arquivo JSON recebido é inválido: Erro nas regras."
    EndIf

return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} validJBody
Aplica o de/para no conteúdo do arquivo importado
gravação

@author Totvs
/*/
//-------------------------------------------------------------------
method validItems(jBody as Json, cResponse as Character) class Apportionment
Local jBodyAux  := JsonObject():New() as Json
Local lRet      := .T. as Logical
Local nTamFil   := FwSizeFilial() as Numeric
Local nI        := 0 as Numeric
Local nJ        := 0 as Numeric
Local nPos      := 0 as Numeric
Local nPosItem  := 0 as Numeric
Local nTotPerc  := 0 as Numeric
Local cFilArq   := "" as Character
Local cCodEve   := "" as Character
Local cCodReg   := "" as Character
Local aItemsDePara := {} as Array
Local nEnt      := 5  as numeric

Default nTamEve := TamSX3("CTQ_EVERAT")[1]
Default nTamDes := TamSX3("CTQ_DESC")[1]
Default nTamReg := TamSX3("CTQ_REGRA")[1]

jBodyAux["rules"] := {}
jBodyAux["isCSVImport"] := jBody["isCSVImport"]
For nI:=1 to Len(jBody["rules"])

    cFilIns := IIF(empty(jBody["rules"][nI]["header"]["ctq_filial"]), xFilial("CTQ"), jBody["rules"][nI]["header"]["ctq_filial"])
    cFilArq := PadR(cFilIns,nTamFil)
    cCodEve := PadR(jBody["rules"][nI]["header"]["ctq_everat"],nTamEve)
    cCodReg := IIf(jBody["rules"][nI]["header"]["ctq_regra" ]==NIL,"",jBody["rules"][nI]["header"]["ctq_regra" ])
    cCodReg := PadR(cCodReg,nTamReg)
    
    CQK->(dbSetOrder(1))
    If CQK->(dbSeek(xFilial("CQK",cFilArq)+cCodEve))
        While !CQK->(Eof()) .And. CQK->CQK_FILIAL = xFilial("CQK",cFilArq) .And. CQK->CQK_CODEVE = cCodEve
            
            aAdd(jBodyAux["rules"],{})            
            nPos := Len(jBodyAux["rules"])
            jBodyAux["rules"][nPos] :=  JsonObject():New()
            jBodyAux["rules"][nPos]["header"] :=  JsonObject():New()
            jBodyAux["rules"][nPos]["header"]["ctq_filial"] := xFilial("CTQ",cFilArq)
            jBodyAux["rules"][nPos]["header"]["ctq_everat"] := cCodEve
            jBodyAux["rules"][nPos]["header"]["ctq_regra"]  := cCodReg
            jBodyAux["rules"][nPos]["header"]["ctq_rateio"] := CQK->CQK_CODRAT
            jBodyAux["rules"][nPos]["header"]["ctq_desc"]   := PadR(CQK->CQK_DESC,nTamDes)

            jBodyAux["rules"][nPos]["header"]["ctq_ctori"] := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_DEBITO, CQK->CQK_CREDIT)
            jBodyAux["rules"][nPos]["header"]["ctq_ccori"] := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_CCD,    CQK->CQK_CCC   )
            jBodyAux["rules"][nPos]["header"]["ctq_itori"] := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_ITEMD,  CQK->CQK_ITEMC )
            jBodyAux["rules"][nPos]["header"]["ctq_clori"] := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_CLVLDB, CQK->CQK_CLVLCR)
            
            For nEnt:= 5 to 9
                if &("lEnt0"+cValtochar(nEnt))
                    jBodyAux["rules"][nPos]["header"]["ctq_e0"+cValtochar(nEnt)+"ori"]  :=  IIf(CQK->CQK_ENTBAS == "1", CQK->&('CQK_EC0'+cValtoChar(nEnt)+'DB'),  CQK->&('CQK_EC0'+cValtoChar(nEnt)+'CR'))
                EndIf
            Next nEnt

            jBodyAux["rules"][nPos]["header"]["ctq_ctpar"] := ::getEntPart(CQK->CQK_ENTBAS, CQK->CQK_DEBITO, CQK->CQK_CREDIT)
            jBodyAux["rules"][nPos]["header"]["ctq_ccpar"] := ::getEntPart(CQK->CQK_ENTBAS, CQK->CQK_CCD,    CQK->CQK_CCC)
            jBodyAux["rules"][nPos]["header"]["ctq_itpar"] := ::getEntPart(CQK->CQK_ENTBAS, CQK->CQK_ITEMD,  CQK->CQK_ITEMC)
            jBodyAux["rules"][nPos]["header"]["ctq_clpar"] := ::getEntPart(CQK->CQK_ENTBAS, CQK->CQK_CLVLDB, CQK->CQK_CLVLCR)

            For nEnt:= 5 to 9
                if &("lEnt0"+cValtochar(nEnt))
                    jBodyAux["rules"][nPos]["header"]["ctq_e0"+cValtochar(nEnt)+"par"]  :=  ::getEntPart(CQK->CQK_ENTBAS, CQK->&('CQK_EC0'+cValtoChar(nEnt)+'DB'),  CQK->&('CQK_EC0'+cValtoChar(nEnt)+'CR'))
                EndIf
            Next nEnt

            jBodyAux["rules"][nPos]["items"] := {}
            nTotPerc := 0
            For nJ := 1 to Len(jBody["rules"][nI]["items"])                
                aAdd(jBodyAux["rules"][nPos]["items"], JsonObject():New())
                nPosItem := Len(jBodyAux["rules"][nPos]["items"])
                
                If !Empty(cCodReg)                       
                    aItemsDePara := ::execDePara(cFilArq, cCodReg, jBody["rules"][nI]["items"][nJ], @cResponse)

                    If Len(aItemsDePara) > 0
                        jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_ctcpar"] := aItemsDePara[1]
                        jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_cccpar"] := aItemsDePara[2]
                        jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_itcpar"] := aItemsDePara[3]
                        jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_clcpar"] := aItemsDePara[4] 

                        For nEnt:= 5 to 9
                            if &("lEnt0"+cValtochar(nEnt))
                                jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_e0"+cValtochar(nEnt)+"cp"] := aItemsDePara[nEnt] 
                            EndIf
                        Next nEnt
                                                            
                    Else
                        lRet := .F.
                        Exit
                    EndIf
                Else
                    jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_ctcpar"] := Iif( jBody["rules"][nI]["items"][nJ]["ctq_ctcpar"] == Nil,"", jBody["rules"][nI]["items"][nJ]["ctq_ctcpar"])
                    jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_cccpar"] := Iif( jBody["rules"][nI]["items"][nJ]["ctq_cccpar"] == Nil,"", jBody["rules"][nI]["items"][nJ]["ctq_cccpar"])
                    jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_itcpar"] := Iif( jBody["rules"][nI]["items"][nJ]["ctq_itcpar"] == Nil,"", jBody["rules"][nI]["items"][nJ]["ctq_itcpar"])
                    jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_clcpar"] := Iif( jBody["rules"][nI]["items"][nJ]["ctq_clcpar"] == Nil,"", jBody["rules"][nI]["items"][nJ]["ctq_clcpar"])

                    For nEnt:= 5 to 9
                        if &("lEnt0"+cValtochar(nEnt)) 
                            jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_e0"+cValtochar(nEnt)+"cp"] := Iif(jBody["rules"][nI]["items"][nJ]["ctq_e0"+cValtochar(nEnt)+"cp"]==Nil,"",jBody["rules"][nI]["items"][nJ]["ctq_e0"+cValtochar(nEnt)+"cp"])
                        EndIf
                    Next nEnt                                    
         
                EndIf

                jBodyAux["rules"][nPos]["items"][nPosItem]["ctq_percen"] := jBody["rules"][nI]["items"][nJ]["ctq_percen"]

                nTotPerc += jBody["rules"][nI]["items"][nJ]["ctq_percen"]
            Next nJ

            If lRet
                lRet := nTotPerc == 100
                If !lRet
                    cResponse :=  STR0008+" ("+cValToChar(nTotPerc)+"%) "+STR0009+cFilArq+STR0010+cCodEve+STR0011 //"A soma dos percentuais enviados" //"para a filial: " //", evento: " //", é diferente de 100%"
                EndIf
            EndIf

            If lRet 
                lRet := ::validEntidades(jBodyAux["rules"][nPos],@cResponse)
            EndIf

            If !lRet
                Exit
            EndIf

            CQK->(dbSkip())
        EndDo       

        If !lRet        
            Exit
        EndIf

    EndIf
Next nI

jBody := jBodyAux

return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} validJBody
Aplica o de/para no conteúdo do arquivo importado
gravação

@author Totvs
/*/
//-------------------------------------------------------------------
method execDePara(cFilArq as Character, cCodReg as Character, jItem as Json, cResponse as Character) class Apportionment
Local aRanges    := {} as Array
Local aReturn    := {} as Array
Local nPosSub    := 0 as Numeric
Local nTamSub    := 0 as Numeric
Local nTamRight  := 0 as Numeric
Local cResultado := "" as Character
Local cTableOri  := "" as Character
Local cEntOrigem := "" as Character
Local cTableDes  := "" as Character
Local cEntDest   := "" as Character
Local cType      := "" as Character
Local bBlock as codeblock
Local lRet       := .T. as Logical
Local cEntAdc    := ""  as Character

QLL->(dbSetOrder(1))
If QLL->(MsSeek(xFilial("QLL",cFilArq)+cCodReg))
    While !QLL->(Eof()) .And. QLL->QLL_CODIGO == cCodReg
        cType := QLL->QLL_TIPO
        If ( ( cType == "1" .And. !Empty(QLL->QLL_SELORI) .And. !Empty(QLL->QLL_SELDES) ) ;  
            .Or. ( cType == "2" .And. !Empty(QLL->QLL_POSORI) .And. !Empty(QLL->QLL_VALORI) .And. !Empty(QLL->QLL_SELDES) );
            .Or. ( cType == "3" .And. !Empty(QLL->QLL_POSORI) .And.!Empty(QLL->QLL_POSDES) .And. !Empty(QLL->QLL_VALORI) ;
            .And. !Empty(QLL->QLL_VALDES) ) )

            lRet := .F.
            
            cTableOri := AllTrim(QLL->QLL_ENTORI)
            If cTableOri == "CT1"
                cEntOrigem := jItem["ctq_ctcpar"]
            ElseIf cTableOri == "CTT"
                cEntOrigem := jItem["ctq_cccpar"]
            ElseIf cTableOri == "CTD"
                cEntOrigem := jItem["ctq_itcpar"]
            ElseIf cTableOri == "CTH"
                cEntOrigem := jItem["ctq_clcpar"]
            ElseIf cTableOri == "CV005"
                cEntOrigem := jItem["ctq_e05cp"]
            ElseIf cTableOri == "CV006"
                cEntOrigem := jItem["ctq_e06cp"]  
            ElseIf cTableOri == "CV007"
                cEntOrigem := jItem["ctq_e07cp"]
            ElseIf cTableOri == "CV008"
                cEntOrigem := jItem["ctq_e08cp"]                                                            
            ElseIf cTableOri == "CV009"
                cEntOrigem := jItem["ctq_e09cp"]                
            EndIf

            cTableDes := AllTrim(QLL->QLL_ENTDES)

            // De Para Simples
            If cType == "1"
            
                cResultado :=  AllTrim(QLL->QLL_SELDES)
                lRet := .T.
            
            // De Para Composta ou Avançada
            Else                

                If "-" $ AllTrim(QLL->QLL_POSORI)
                    cResultado := ::ApplyRange(cEntOrigem, AllTrim(QLL->QLL_POSORI))
                Else
                    aRanges := Separa(AllTrim(QLL->QLL_POSORI), ";")  
                    
                    If Len(aRanges) > 1
                        nPosSub := Val(aRanges[1])
                        nTamSub := (Val(aRanges[2]) - Val(aRanges[1]))+1
                    Endif

                    cResultado := SubStr(cEntOrigem, nPosSub, nTamSub)
                EndIf

                //Se atendeu à regra, aplico o de/para
                If AllTrim(cResultado) == AllTrim(QLL->QLL_VALORI)
                    
                    //De Para Avançado (Tipo 3) buscar na CQK (Evento de Rateio)
                    If cType == "3"
                        If cTableDes == "CT1" //Conta Contabil
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_DEBITO, CQK->CQK_CREDIT)
                        ElseIf cTableDes == "CTT" //Centro de Custo
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_CCD, CQK->CQK_CCC)
                        ElseIf cTableDes == "CTD" //Item de Valor
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_ITEMD, CQK->CQK_ITEMC)
                        ElseIf cTableDes == "CTH" //Classe de Valor
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_CLVLDB, CQK->CQK_CLVLCR)
                        ElseIf cTableDes == "CV005" //Ent05
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_EC05DB, CQK->CQK_EC05CR)      
                        ElseIf cTableDes == "CV006" //Ent06
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_EC06DB, CQK->CQK_EC06CR)    
                        ElseIf cTableDes == "CV007" //Ent07
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_EC07DB, CQK->CQK_EC07CR)    
                        ElseIf cTableDes == "CV008" //Ent08
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_EC08DB, CQK->CQK_EC08CR)    
                        ElseIf cTableDes == "CV009" //Ent09
                            cEntDest := IIf(CQK->CQK_ENTBAS == "1", CQK->CQK_EC09DB, CQK->CQK_EC09CR)                                                                                                                                          
                        EndIf

                        nTamRight := Len(cEntDest)-Len(AllTrim(QLL->QLL_VALDES))
                        
                        cResultado :=  AllTrim(QLL->QLL_VALDES) + Right(cEntDest,nTamRight)
                    Else //cType == 2

                        cResultado :=  AllTrim(QLL->QLL_SELDES)
                    
                    EndIf
                    
                    lRet := .T.

                EndIf
            EndIf 

            If lRet 
                If Left(cTableDes,3) == "CV0"
                    cEntAdc := SubStr(cTableDes,4,2)
                    lRet := ::validClasseBloq(cFilArq, cResultado, ::getAliasCT0(cEntAdc), @cResponse,cEntAdc)
                Else            
                    lRet := ::validClasseBloq(cFilArq, cResultado, cTableDes, @cResponse)
                EndIf

                If lRet

                    If cTableDes == "CT1" //Conta Contabil
                        bBlock := {|x| jItem["ctq_ctcpar"] := x}
                    ElseIf cTableDes == "CTT" //Centro de Custo
                        bBlock := {|x| jItem["ctq_cccpar"] := x}
                    ElseIf cTableDes == "CTD" //Item de Valor
                        bBlock := {|x| jItem["ctq_itcpar"] := x}
                    ElseIf cTableDes == "CTH" //Classe de Valor
                        bBlock := {|x| jItem["ctq_clcpar"] := x}
                    ElseIf cTableDes == "CV005" //Ent05 
                        bBlock := {|x| jItem["ctq_e05cp"] := x}  
                    ElseIf cTableDes == "CV006" //Ent06 
                        bBlock := {|x| jItem["ctq_e06cp"] := x}  
                    ElseIf cTableDes == "CV007" //Ent07 
                        bBlock := {|x| jItem["ctq_e07cp"] := x}  
                    ElseIf cTableDes == "CV008" //Ent08 
                        bBlock := {|x| jItem["ctq_e08cp"] := x}                                                  
                    ElseIf cTableDes == "CV009" //Ent09
                        bBlock := {|x| jItem["ctq_e09cp"] := x}                                                                          
                    EndIf

                    Eval(bBlock, cResultado)

                    aReturn := {jItem["ctq_ctcpar"],;
                                jItem["ctq_cccpar"],;
                                jItem["ctq_itcpar"],;
                                jItem["ctq_clcpar"],;
                                jItem["ctq_e05cp"],;
                                jItem["ctq_e06cp"],;
                                jItem["ctq_e07cp"],;
                                jItem["ctq_e08cp"],;
                                jItem["ctq_e09cp"]}
                Else
                    //Se alguma entidade for inválida eu paro a validação
                    //A mensagem cResponse vem do método validClasseBloq
                    aReturn := {}
                    Exit
                EndIf

                //Se atendeu a uma regra não preciso executar as próximas
                Exit
            EndIf            						
        EndIf		
        QLL->(dbSkip())
    EndDo
Else
    cResponse := STR0014+" ("+cCodReg+") "+STR0015 //"A regra enviada no arquivo" //"não existe na tabela de regras (QLL)"
EndIf

return aReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyRange
Funcao responsavel em aplicar o range das entidades

@author Totvs
/*/
//-------------------------------------------------------------------
method ApplyRange(cConta as Character, cRange as Character) class Apportionment

Local aRanges := Separa(cRange, "-") as array
Local aRange := {} as array
Local aArray := {} as array
Local cResultString := "" as character
Local nStart := 0 as numeric
Local nEnd   := 0 as numeric
Local nI     := 0 as numeric

For nI := 1 To Len(aRanges)
	aRange := separa(aRanges[nI], ";")
	nStart := Val(aRange[1])
	nEnd   := Val(aRange[2])
	aAdd(aArray, SubStr(cConta, nStart, nEnd - nStart + 1))
Next

For nI := 1 To Len(aArray)
	cResultString += aArray[nI]
	If nI < Len(aArray)
		cResultString += "-"
	EndIf
Next

Return cResultString

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyRange
Funcao responsavel em aplicar o range das entidades

@author Totvs
/*/
//-------------------------------------------------------------------
method getEntPart(cEntBas as Character, cEntDeb as Character, cEntCred as Character) class Apportionment
    Local cEntResult := "" as Character

    If cEntBas == "1"
        cEntResult := IIf(Empty(cEntCred),cEntDeb,cEntCred)            
    Else
        cEntResult := IIf(Empty(cEntDeb),cEntCred,cEntDeb) 
    EndIf

Return cEntResult

//-------------------------------------------------------------------
/*/{Protheus.doc} ApplyRange
Funcao responsavel em aplicar o range das entidades
 
@author Totvs
/*/
//-------------------------------------------------------------------
method validClasseBloq(cFilArq as Character, cEntity as Character, cTableValid as Character, cResponse as Character,cCodEnt as Character) class Apportionment
Local lRet := .F. as Logical
Local cSeek := "" as Character
Local cPlano:= "" as Character

Default cCodEnt := ""

&(cTableValid)->(dbSetOrder(1))
If Empty(AllTrim(cCodEnt))
    cSeek := cEntity
Else
    cPlano:= ::getPlanoCT0(cCodEnt)
    cSeek := cPlano+cEntity
EndIf
If &(cTableValid)->(dbSeek(xFilial(cTableValid,cFilArq)+cSeek))
    lRet := &(cTableValid+'->'+cTableValid+'_CLASSE')  == "2"
    If lRet
        //lRet := &(cTableValid+'->'+cTableValid+'_BLOQ')  == "2" // 2 = Não está bloqueada
        If Empty(cCodEnt)
            lRet := ValidaBloq(cEntity,dDataBase,cTableValid,.F.)     
        Else
            lRet := ValidaBloq(cEntity,dDataBase,cTableValid,.F.,cCodEnt,cPlano) 
        EndIf
      
        If !lRet
            cResponse := STR0017+cTableValid+" = '"+cEntity+"'" //"A regra de de/para encontrou uma entidade bloqueada: "
        EndIf
    Else
        cResponse := STR0018+cTableValid+" = '"+cEntity+"'" //"A regra de de/para encontrou uma entidade sintética: "
    EndIf
else
    cResponse := STR0019+cTableValid+" = '"+cEntity+"'" //"A regra de de/para gerou uma entidade não encontrada na base de dados: "
EndIf

return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getCodFil
Retorna as filiais disponíveis no sistema

@author Totvs
/*/
//-------------------------------------------------------------------
method getCodFil() class Apportionment
Local aRetFil := FWLoadSM0() as Array
Local jCodFil := JsonObject():New() as Json
Local nI := 0 as Numeric

For nI := 1 to Len(aRetFil)
    jCodFil[xFilial("CTQ",aRetFil[nI,2])] := .T.
Next nI

return jCodFil

//-------------------------------------------------------------------
/*/{Protheus.doc} validEntidades
Valida as entidades enviadas e calculadas na importação do arquivo.

@author Totvs
/*/
//-------------------------------------------------------------------
method validEntidades(jBody as Json, cResponse) class Apportionment
Local lRet := .T. as Logical
Local cEntCT1 := "" as Character
Local cEntCTT := "" as Character
Local cEntCTD := "" as Character
Local cEntCTH := "" as Character
Local cCodEve := "" as Character
Local cCodReg := "" as Character
Local nI      := 0  as Numeric
Local cEnt05 := "" as Character
Local cEnt06 := "" as Character
Local cEnt07 := "" as Character
Local cEnt08 := "" as Character
Local cEnt09 := "" as Character

cFilArq := jBody["header"]["ctq_filial"]
cCodEve := jBody["header"]["ctq_everat"]
cCodReg := jBody["header"]["ctq_regra"]

cEntCT1 := jBody["header"]["ctq_ctori"]
cEntCTT := jBody["header"]["ctq_ccori"]
cEntCTD := jBody["header"]["ctq_itori"]
cEntCTH := jBody["header"]["ctq_clori"]
cEnt05  := IIf( lEnt05, jBody["header"]["ctq_e05ori"], "" )
cEnt06  := IIf( lEnt06, jBody["header"]["ctq_e06ori"], "" )
cEnt07  := IIf( lEnt07, jBody["header"]["ctq_e07ori"], "" )
cEnt08  := IIf( lEnt08, jBody["header"]["ctq_e08ori"], "" )
cEnt09  := IIf( lEnt09, jBody["header"]["ctq_e09ori"], "" )

lRet := ::execValidEnt("1", cFilArq, cCodEve, "", cEntCT1, cEntCTT, cEntCTD, cEntCTH, @cResponse,cEnt05,cEnt06,cEnt07,cEnt08,cEnt09)

If lRet
    cEntCT1 := jBody["header"]["ctq_ctpar"]
    cEntCTT := jBody["header"]["ctq_ccpar"]
    cEntCTD := jBody["header"]["ctq_itpar"]
    cEntCTH := jBody["header"]["ctq_clpar"]
    cEnt05  := IIf( lEnt05, jBody["header"]["ctq_e05par"], "" )
    cEnt06  := IIf( lEnt06, jBody["header"]["ctq_e06par"], "" )
    cEnt07  := IIf( lEnt07, jBody["header"]["ctq_e07par"], "" )
    cEnt08  := IIf( lEnt08, jBody["header"]["ctq_e08par"], "" )
    cEnt09  := IIf( lEnt09, jBody["header"]["ctq_e09par"], "" )    

    lRet := ::execValidEnt("2", cFilArq, cCodEve, "", cEntCT1, cEntCTT, cEntCTD, cEntCTH, @cResponse,cEnt05,cEnt06,cEnt07,cEnt08,cEnt09)
EndIf

If lRet
     For nI := 1 to Len(jBody["items"])
        cEntCT1 := jBody["items"][nI]["ctq_ctcpar"]
        cEntCTT := jBody["items"][nI]["ctq_cccpar"]
        cEntCTD := jBody["items"][nI]["ctq_itcpar"]
        cEntCTH := jBody["items"][nI]["ctq_clcpar"]
        cEnt05  := IIf( lEnt05, jBody["items"][nI]["ctq_e05cp"], "" )
        cEnt06  := IIf( lEnt06, jBody["items"][nI]["ctq_e06cp"], "" )
        cEnt07  := IIf( lEnt07, jBody["items"][nI]["ctq_e07cp"], "" )
        cEnt08  := IIf( lEnt08, jBody["items"][nI]["ctq_e08cp"], "" )
        cEnt09  := IIf( lEnt09, jBody["items"][nI]["ctq_e09cp"], "" ) 
        lRet := ::execValidEnt("3", cFilArq, cCodEve, cCodReg, cEntCT1, cEntCTT, cEntCTD, cEntCTH, @cResponse,cEnt05,cEnt06,cEnt07,cEnt08,cEnt09)
        
        If !lRet
            Exit
        EndIf
    Next nI 
EndIf

return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} validEntidades
Aplica as regras de validação para a Origem, Partida e Contra Partida

@author Totvs
/*/
//-------------------------------------------------------------------
method ExecValidEnt(cTipoMsg as Character, cFilArq as Character, cCodEve as Character, cCodReg as Character,;
                     cEntCT1 as Character, cEntCTT as Character, cEntCTD as Character, cEntCTH as Character, cResponse as Character,;
                     cEnt05  as Character, cEnt06  as Character, cEnt07  as Character, cEnt08  as Character, cEnt09  as Character)  class Apportionment

Local lRet := .T. as Logical
Local cPrefMsg := "" as Character
Local nEnt     := 1  as Numeric
Local cRetEnt  := "" as Character
Local cDescEnt := "" as Character

Default cEnt05 := "" 
Default cEnt06 := "" 
Default cEnt07 := "" 
Default cEnt08 := "" 
Default cEnt09 := "" 

If cTipoMsg == "1"
    cPrefMsg := STR0023 //"Origem"
ElseIf cTipoMsg == "2"
    cPrefMsg := STR0024 //"Partida"
Else    
    cPrefMsg := STR0025 //"Contra Partida"
EndIf

lRet := Empty(cEntCT1) .Or. ::validClasseBloq(cFilArq, cEntCT1, "CT1", @cResponse)
If lRet 
    lRet := Empty(cEntCTT) .Or. ::validClasseBloq(cFilArq, cEntCTT, "CTT", @cResponse)
    If lRet 
        lRet := Empty(cEntCTD) .Or. ::validClasseBloq(cFilArq, cEntCTD, "CTD", @cResponse)    
        If lRet
            lRet := Empty(cEntCTH) .Or. ::validClasseBloq(cFilArq, cEntCTH, "CTH", @cResponse)
        EndIf
    EndIf
    If lRet
        For nEnt :=5 to 9
            cRetEnt:= strZero(nEnt,2)
            if lRet .and. &("lEnt"+cRetEnt) .and. !Empty(&("cEnt"+cRetEnt))
                 lRet :=  ::validClasseBloq(cFilArq, &("cEnt"+cRetEnt), ::getAliasCT0(cRetEnt), @cResponse,cRetEnt)
            endif
        Next nEnt
    EndIf
EndIF

If lRet
    lRet := CtbAmarra(cEntCT1,cEntCTT,cEntCTD,cEntCTH,.T.,.F.)
    If !lRet
        cResponse := cPrefMsg+STR0020+cEntCT1+"/"+cEntCTT+"/"+cEntCTD+"/"+cEntCTH //" As entidades utilizadas não respeitam as regras de amarração cadastradas (MV_CTBAMAR) CT1/CTT/CTD/CTH: "
    EndIf    
EndIf

If lRet 
    lRet := CtbObrig(cEntCT1,cEntCTT,cEntCTD,cEntCTH,.T.,nil,.F.,,"",;
                    If(lEnt05,cEnt05,Nil),;
                    If(lEnt06,cEnt06,Nil),;
                    If(lEnt07,cEnt07,Nil),;
                    If(lEnt08,cEnt08,Nil),;
                    If(lEnt09,cEnt09,Nil)) 		

    If !lRet
        For nEnt :=5 to 9
            cRetEnt:= strZero(nEnt,2)
            if &("lEnt"+cRetEnt) 
                cDescEnt += "/"+&("cEnt"+cRetEnt)
            EndIf
        Next nEnt
        cResponse := cPrefMsg+STR0021+cEntCT1+"/"+cEntCTT+"/"+cEntCTD+"/"+cEntCTH+cDescEnt //" As entidades utilizadas não atendem às regras de obrigatoriedade definidas no plano de contas: "
    EndIf
EndIf

If lRet .And. lCTBAPRTVLD
    cResponse := ExecBlock("CTBAPRTVLD",.F.,.F.,{cEntCT1,cEntCTT,cEntCTD,cEntCTH,If(lEnt05,cEnt05,Nil), If(lEnt06,cEnt06,Nil), If(lEnt07,cEnt07,Nil),If(lEnt08,cEnt08,Nil), If(lEnt09,cEnt09,Nil)})
    lRet := Empty(cResponse)    
    If !lRet
        cResponse := cPrefMsg+" - "+cResponse
    EndIf
EndIf

return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} getAliasCT0
Retorna Alias CT0 para entidades adicionais

@author Totvs
/*/
//-------------------------------------------------------------------
method getAliasCT0(cIdEntid as Character) as character class Apportionment

Local   cRet        := "" as Character
Local   aSaveArea   := GetArea() as Array

Default cIdEntid    := "" 

dbSelectArea("CT0")
dbSetOrder(1)
If dbSeek(FwxFilial("CT0")+cIdEntid)
	cRet := CT0->CT0_ALIAS
EndIf

RestArea(aSaveArea)

Return(cRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} getAliasCT0
Retorna Plano CT0 para entidades adicionais

@author Totvs
/*/
//-------------------------------------------------------------------

method getPlanoCT0(cIdEntid as Character) as character class Apportionment

Local cRet := "" as Character
Local aSaveArea := GetArea() as Array

Default cIdEntid := "" 

dbSelectArea("CT0")
dbSetOrder(1)
If dbSeek(FwxFilial("CT0")+cIdEntid)
	cRet := CT0->CT0_ENTIDA
EndIf

RestArea(aSaveArea)

Return(cRet)

