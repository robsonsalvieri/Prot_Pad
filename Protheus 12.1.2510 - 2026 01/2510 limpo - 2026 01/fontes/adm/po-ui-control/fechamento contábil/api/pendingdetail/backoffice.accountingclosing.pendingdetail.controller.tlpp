#include "tlpp-core.th"
#include "backoffice.accountingclosing.pendingdetail.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.pendingdetail
using namespace totvs.protheus.backoffice.accountingclosing.util
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingDetail
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class PendingDetail
	Data jResposta	as json
	Data Page       as Numeric
	Data PageSize   as Numeric
	Data module     as Character
	Data table      as Character	
	Data datefrom   as Character	
	Data dateto     as Character
	Data type       as Character
	Data oService   as Object
	Data aFieldsApi as Array
	
    public method new()

    @Get("/api/fc/pendingdetail/")
    public method getAllPendingDetail()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingDetail
Metodo construtor

@author Totvs 
/*/
//-------------------------------------------------------------------
method new() class PendingDetail
	::page       := 1
	::pagesize   := 10
	::module     := ""
	::table      := ""
	::datefrom   := ""
	::dateto     := ""
	::type       := ""
	::oService   := PendingDetailItemService():getInstance()
	::jResposta  := JsonObject():New()
	::aFieldsApi := {}
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllPendingDetail
Metodo que inicia a chamada dos itens pendentes de integração

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllPendingDetail() class PendingDetail		
	Local aJsonHead := {} as Array	
	Local oField    := tcTreatsFilter():new() as Object
	Local lResponse := .F. As Logical
	Local cChaveRes := "message" As Character 
    Local cResponse := STR0001 As Character //"Resposta inválida."

	::aFieldsApi := {}
	::module 	 := oRest:getQueryRequest():GetJsonText("module")
	::module     := StrTran(::module, "null", "")	
	::table 	 := oRest:getQueryRequest():GetJsonText("table")
	::table      := StrTran(::table, "null", "")	
	::datefrom	 := oRest:getQueryRequest():GetJsonText("datefrom")
	::datefrom   := StrTran(::datefrom, "null", "")
	::dateto 	 := oRest:getQueryRequest():GetJsonText("dateto")
	::dateto     := StrTran(::dateto, "null", "")
	::type   	 := oRest:getQueryRequest():GetJsonText("type")
	
	If AliasInDic(UPPER(::table)) .And. !Empty(CTBCpoUUID(::table))
		fcGetPageAndPageSize(@::page, @::pageSize)
    	::jResposta:fromJson(::oService:getAllPendingDetail(::page,::pageSize,::aFieldsApi,::module,::table,::datefrom,::dateto,::type,@cResponse))   

		//Adiciona informacoes SX3 dos campos no Json de retorno
		If ValType(::jResposta:GetJsonText("items")) <> "U" .And. Len(::aFieldsApi) > 0		
			aJsonHead := oField:tcSetStruct(::aFieldsApi)
			
			IIf(Len(aJsonHead) > 0,;
				(::jResposta["fields"] := JsonObject():new(),;
				::jResposta["fields"] := aJsonHead,;
				fcAnswerRest(::jResposta, .T.)),;		
				fcSetResponse(lResponse, cChaveRes, cResponse, .F.))		
		
		
		EndIf	
	Else  
		fcSetResponse(lResponse, cChaveRes, STR0002, .F.) //Tabela não preparada!
	EndIf	
	//Limpa array
	FWFreeArray(aJsonHead)
	FWFreeArray(::aFieldsApi)
return

/*/{Protheus.doc} CTBCpoUUID
Retorna o nome do campo UUID da tabela recebida

@type function
@version 12
@author TOTVS
@since 05/04/2023
/*/
Static Function CTBCpoUUID(cTable)
Local cRet := ""

If &(cTable+"->(FieldPos('"+cTable+"_MSUIDT'))") > 0
	cRet := cTable+"_MSUIDT
ElseIf &(cTable+"->(FieldPos('"+SubStr(cTable,2,2)+"_MSUIDT'))") > 0
	cRet := SubStr(cTable,2,2)+"_MSUIDT
EndIf

Return cRet
