#include "tlpp-core.th"
#include "backoffice.accountingclosing.reconciliations.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.reconciliations
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} Reconciliations
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class Reconciliations
    public method new()
    
    @Get("/api/fc/reconciliations/")
    public method getReconciliations()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} Reconciliations
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class Reconciliations
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getReconciliations
Metodo que inicia a chamada dos lancamentos contabeis conciliados no
periodo

@author Totvs
/*/
//-------------------------------------------------------------------
method getReconciliations() class Reconciliations
    Local oService  := ReconciliationsService():getInstance() as object    
    Local cFilBkp   := cFilAnt as character
    Local cDataIni  := oRest:getQueryRequest():GetJsonText("startperiod") as character
    Local cDataFim  := oRest:getQueryRequest():GetJsonText("endperiod") as character 
    Local cChaveRes := "message" as character   
    Local cResponse := STR0001 as character //"Inconsistência encontrada na API."
    Local lResponse := .F. as logical
    
    cDataIni := StrTran(cDataIni, "null", "")
    cDataFim := StrTran(cDataFim, "null", "")
    
    If CT2->(FieldPos("CT2_IDCONC")) < 1
        lResponse := .F.
        cResponse := STR0002//"Campo CT2_IDCONC não encontrado na base de dados"
    ElseIf Empty(cDataIni) .Or. Empty(cDataFim)
        lResponse := .F.
        cResponse := STR0003//"Necessário informar período inicial e período final"
    Else        
        fcGetHeaders()        
        lResponse := .T.
        cChaveRes := "reconciliations"
        cResponse := oService:getReconciliations(cDataIni, cDataFim)
    EndIf
                    
    fcSetResponse(lResponse, cChaveRes, cResponse)
    FreeObj(oService)

    //Restaura empresa e filial
    cFilAnt := cFilBkp    
return
