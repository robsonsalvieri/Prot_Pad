#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.reconciliations
using namespace totvs.protheus.backoffice.accountingclosing.util
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ReconciliationsProtheusData
Classe responsavel pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class ReconciliationsProtheusData
    Public method new()
    Public method getReconciliations()
    static method getData() as object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ReconciliationsProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ReconciliationsProtheusData
return

//-------------------------------------------------------------------
/*/{Protheus.doc} ReconciliationsProtheusData
Metodo para chamada e validacao do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class ReconciliationsProtheusData as object
    static __oActiveData as object

    If ValType(__oActiveData) == "U"
        __oActiveData := ReconciliationsProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getReconciliations
Metodo responsavel pela busca dos lancamentos contabeis conciliados
conforme parametros informados

@author Totvs
/*/
//-------------------------------------------------------------------
method getReconciliations(cDataIni as character, cDataFim as character) class ReconciliationsProtheusData
    Local aArea     := GetArea() as Array
    Local cQuery    := "" as character
    Local cResponse := "" as character
    Local cBranches := "" as character
    Local cAliasTmp := GetNextAlias() as character   

    cBranches := getBranchiesForConditional("CT2", "CTBA960")     
    
    //Busca conciliacoes nos lancamentos contabeis
    cQuery := "SELECT COUNT(1) IDCONC"
    cQuery += " FROM "+RetSqlName("CT2")
    cQuery += " WHERE "+ cBranches +" "
    cQuery += " AND CT2_DATA >= '"+cDataIni+"'"
    cQuery += " AND CT2_DATA <= '"+cDataFim+"'"
    cQuery += " AND CT2_MOEDLC = '01'"
    cQuery += " AND CT2_IDCONC = '"+Space(TamSx3("CT2_IDCONC")[1])+"'"
    cQuery += " AND D_E_L_E_T_ = ' '"
    cQuery := ChangeQuery( cQuery )
    
    dbUseArea(.T., "TOPCONN", TcGenQry(,, cQuery), cAliasTmp, .T., .F.)        
    
    If (cAliasTmp)->(!EOF()) .And. (cAliasTmp)->IDCONC > 0
        cResponse := "true"
    Else        
        cResponse := "false"
    EndIf

    (cAliasTmp)->(dbCloseArea()) //Fecha temporario

    RestArea(aArea)
    FwFreeArray(aArea)
return cResponse
