#include "tlpp-core.th"
#include "backoffice.accountingclosing.closingsetup.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.closingsetup
using namespace totvs.protheus.backoffice.accountingclosing.util
using namespace totvs.protheus.backoffice.reconciliation.util

/*/{Protheus.doc} ClosingSetup
    Classe principal de controle, onde é inicializado o EndPoint
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
    /*/
Class ClosingSetup    
    data oService As Object
    
    public method new()
    
    @Get("/api/fc/closingsetup/configs/")
    public method getClosingSetup()

    @Get("/api/fc/closingsetup/module")
    public method getClosingSetupModule()

    @Post("/api/fc/closingsetup/")
    public method postClosingSetup()    
EndClass

/*/{Protheus.doc} new
    Metodo construtor
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
/*/
Method new() class ClosingSetup
    ::oService := ClosingSetupService():getInstance()    
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetup
Metodo que retorna as informacoes de Setup do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
Method getClosingSetupModule() class ClosingSetup
    ::getClosingSetup("module")
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingSetup
Metodo que retorna as informacoes de Setup do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
Method getClosingSetup(cType as Character) class ClosingSetup                
    Local cChaveRes   := "message" As Character
    Local cResponse   := STR0001 As Character //"Parâmetro filter não enviado ou vazio, verifique a url"
    Local lResponse   := .F. As Logical    
    Local aFilters    := {} As Array

    Default cType := ""
             
    If !Empty(cType)
        cResponse := ::oService:getClosingSetupModule()
        cChaveRes := ""
        lResponse := .T.
    Else
        aFilters := tcGetFilters()
        If ValType(aFilters) == "A" .And. !Empty(aFilters[1][2])
            cResponse := ::oService:getClosingSetupConfigs(aFilters[1][2])
            cChaveRes := ""
            lResponse := .T.
        EndIf
    EndIf
    
    If lResponse
        oRest:setResponse(cResponse)
    Else
        fcSetResponse(lResponse, cChaveRes, cResponse)
    EndIf
    FwFreeArray(aFilters)    
Return

/*/{Protheus.doc} postClosingSetup
    Metodo que inicia a chamanda das gravacoes setup do fechamento contabil
    @author Totvs
    @since 09/01/2024
    @version 12.1.2310
/*/
Method postClosingSetup() class ClosingSetup                        
    Local aResponse As Array
    
    ::oService:postClosingSetup()        
    aResponse := ::oService:getClosingSetupResponse()
    fcSetResponse(aResponse[1], aResponse[2], aResponse[3])
    FwFreeArray(aResponse)
Return
