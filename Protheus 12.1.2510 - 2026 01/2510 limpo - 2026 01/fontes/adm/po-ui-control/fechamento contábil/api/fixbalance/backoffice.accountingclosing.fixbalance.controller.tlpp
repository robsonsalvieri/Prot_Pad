#include "BACKOFFICE.ACCOUNTINGCLOSING.FIXBALANCE.CONTROLLER.CH"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.fixbalance
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} FixBalance
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class FixBalance
    public method new()

    @POST("/api/fc/fixbalance/")
    public method getAllFixBalance()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} FixBalance
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class FixBalance
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllFixBalance
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllFixBalance() class FixBalance
    Local oService  As Object
    Local cFilBkp   As Character        
    Local cChaveRes As Character
    Local aResponse As Array
    Local cResponse As Character
    Local cCodUUID  As Character 
    Local lReturn   As Logical
    Local cTpSald   As Character
    Local dDataIni   As Date
    Local dDataFim   As Date

    //Inicilaiza variaveis
    oService  := FixBalanceService():getInstance()    
    cFilBkp   := cFilAnt
    cChaveRes := "message"
    cResponse := ""
    
    dDataIni  := StoD(oRest:getQueryRequest():GetJsonText("dateini"))
    dDataFim  := StoD(oRest:getQueryRequest():GetJsonText("datefin"))
    cCodUUID  := oRest:getQueryRequest():GetJsonText("uuid")
    cCodCurr  := oRest:getQueryRequest():GetJsonText("currency")
    cTpSald   := oRest:getQueryRequest():GetJsonText("balancetype")
    lReturn   := .F.
    
    If ValType(cCodUUID)=="C" .And. cCodUUID <> "null"
        If ValType(cCodCurr)=="C" .And. cCodCurr <> "null"            
            If cTpSald == "ALL"
                cTpSald := '*'
            EndIF
            aResponse := oService:postFixBalance(cCodUUID, cCodCurr, cTpSald, dDataIni, dDataFim)
            lReturn := aResponse[1]
            cResponse := aResponse[2]
            cFilAnt := cFilBkp
        Else
            cResponse := STR0001    
        EndIf
    Else      
        cResponse := STR0002
    EndIf

    fcSetResponse(lReturn, cChaveRes, cResponse, lReturn)

return
