#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace backoffice.accountingclosing.closingdata
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} BalanceModel1
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountClosing
    Private Data _service
    public method new()
    
    @Get("/api/fc/closingdata/")
    public method get_data()
    
    @Post("/api/fc/closingdata/")
    public method post_data()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} BalanceModel1
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountClosing
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGetBalanceModel1
Metodo que retorna balancete mensal no formato requerido

@author Totvs
/*/
//-------------------------------------------------------------------
method get_data() class AccountClosing
    local ojResposta as json
    local oService  as object
    local nPage     as numeric
    local nPageSize as numeric
    local nI

    oService    :=  AccountClosingService():getInstance()
    ojResposta  := JsonObject():new() 
    nPage       := 1
    nPageSize   := 10

    tcGetPageAndPageSize(@nPage, @nPageSize)

    ojResposta:fromJson( ;
        oService:service_get(oRest:getQueryRequest(), nPage, nPageSize) )

    // Incluir o username junto ao código de usuário
    for nI := 1 to Len(ojResposta["items"])
        ojResposta["items"][nI]["user"] += "-" + PswChave(ojResposta["items"][nI]["user"])
    next nI

    if ValType(ojResposta:GetJsonText("items")) <> "U"
        tcAnswerRest(ojResposta, .T.)
    endIf
return 

method post_data() class AccountClosing
    local oService      as object
    local ojResposta    as json
    local oJcheckParams as json
    local oJBody        as json
    local lResponse as logical
    local cChaveRes as character
    local cResponse as character	

    lResponse   := .F.
    cChaveRes   := "message"
    cResponse   := ""  

    oService    := AccountClosingService():getInstance()
    oJBody      := JsonObject():new()
    ojResposta  := JsonObject():new()

    oJBody:fromJson(oRest:getBodyRequest())

    oJcheckParams :=  oService:checkMandatoryParams(ojBody)
    
    if oJcheckParams["result"] == .F.
        cResponse := oJcheckParams["message"]
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.) 
    else
        ojResposta:fromJson( ;
            oService:service_post(oJBody)  )

        if ValType(ojResposta:GetJsonText("items")) <> "U"
            tcAnswerRest(ojResposta, .T.)
        endif
    endIf
return 
