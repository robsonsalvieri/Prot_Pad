#include "tlpp-core.th"
#include "backoffice.accountingclosing.saveclosingdefinitions.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.saveclosingdefinitions
using namespace totvs.protheus.backoffice.accountingclosing.util

/*/{Protheus.doc} SaveClosingDefinitions
    Classe principal de controle, onde é inicializado o EndPoint
    @author Totvs
    @since 01/12/2022
    @version 12.1.2210
    /*/
Class SaveClosingDefinitions
    public method new()

    @Post("/api/fc/saveclosingdefinitions/")
    public method postSaveClosingDefinitions()

    private method ValidStructJson() as Logical

EndClass

/*/{Protheus.doc} new
    Metodo construtor
    @author Totvs
    @since 01/12/2022
    @version 12.1.2210
/*/
Method new() class SaveClosingDefinitions
Return 

/*/{Protheus.doc} postSaveClosingDefinitions
    Metodo que inicia a chamanda das gravações matchsetting do conciliador
    @author Totvs
    @since 01/12/2022
    @version 12.1.2210
/*/
Method postSaveClosingDefinitions() class SaveClosingDefinitions
    Local oService      := SaveClosingDefinitionsService():getInstance() as Object
    Local aResponse     := {} as Array
    Local lResponse     := .F. as Logical
    Local cChaveRes     := "message" as Character
    Local cResponse     := STR0001 as Character // "Json invalido"
    Local oBody         := oRest:GetBodyRequest() as Object
    Local jBody         := JsonObject():new() as Json
    Local uRet          := Nil
    Local cBodyTreated  := "" as Character

    If ValType(oBody) == "C"
        uRet := jBody:fromJson(oBody)
        If ValType(uRet) == "U"
            
            If ::ValidStructJson(jBody, @cResponse) // Valida estrutura    
                
                /* Tratamento para converter o encoding */
                cBodyTreated := jBody:ToJSON()
                cBodyTreated := DecodeUTF8(cBodyTreated, "cp1252")
                uRet := jBody:FromJSON(cBodyTreated)

                If ValType(uRet) == "U"
                    aResponse := oService:postSaveClosingDefinitions(jBody)
                    If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                        lResponse := aResponse[1]
                        cChaveRes := If(lResponse, "", "message")
                        cResponse := aResponse[2]
                    EndIf
                EndIf
            EndIf
        EndIf
    EndIf
    fcSetResponse(lResponse, cChaveRes, cResponse)
    FwFreeArray(aResponse)
    FreeObj(oService)
Return 

/*/{Protheus.doc} ValidStructJson
    Valida estrutura de JSON
    @author user
    @since 01/12/2022
    @version 12.1.2210
    @param jBody
    @return lRet
    /*/
Method ValidStructJson(jBody, cResponse) class SaveClosingDefinitions
    Local lRet := .F. as Logical

    If (jBody:hasProperty("items") .And. jBody:hasProperty("message") .And. jBody:hasProperty("itemsToRemove") )

        If ( !( Empty(jBody["items"]) ) .And. !( Empty(jBody["message"]) ) )
             
            lRet := .T. // Estrutura Ok
        Else
            cResponse := STR0002//"Foram enviadas propriedades com valor vazio para a API"
        EndIf
    Else
        cResponse := STR0003 //"Não foram enviadas todas as propriedades esperadas para a API"
    EndIf
Return lRet
