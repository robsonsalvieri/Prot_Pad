#include "tlpp-core.th"
#include "backoffice.accountingclosing.closingtables.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.closingtables
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingTables
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class ClosingTables
    public method new()

    @Get("/api/fc/closingtables/")
    public method getClosingTables()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingTables
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ClosingTables
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getClosingTables
Metodo que inicia a chamada das configuracoes das definições de fechamento
do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getClosingTables() class ClosingTables
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local nI	

    //Inicilaiza variaveis
    oService  := ClosingTablesService():getInstance()
    jResposta := JsonObject():new()
    jTables   := JsonObject():new()
    cFilBkp   := cFilAnt
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."

    jResposta:fromJson(oService:getClosingTables()) 

    If(ValType(jResposta:GetJsonText("items")) <> "U")
        
        //Transformar em Json antes de enviar para o front
        If Len(jResposta["items"]) > 0
            for nI := 1 to Len(jResposta["items"])
                //Validar se os campos uuid existem para exibição em tela
                jResposta["items"][nI]["tables"] := ValidFieldTables(jResposta["items"][nI]["tables"])
            next nI
        EndIf

        fcAnswerRest(jResposta, .T.)     
    EndIf

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(jResposta)
	FWFreeArray(jTables)
return

/*/{Protheus.doc} ValidFieldTables
    Valida se o campo de UUID da tabela existe para permitir a inclusão
    @type  Static
    @author totvs
    @since 19/04/2023
    @version 12.1.2210
    @param
        cTables, Character, string do json que contem os dados das tabelas do fechamento
    @return
        jTableFinal, JSON, JSON com as tabelas validadas
    /*/
Static function ValidFieldTables(cTables)

    Local nI            as Numeric
    Local cAlias        as Character
    Local cUUIDField    as Character
    
    Local jTableAll := JsonObject():New() as Json
    Local jTableFinal := JsonObject():New() as Json
    
    jTableAll:fromJSON(cTables)
    jTableFinal:fromJSON('{"tables": []}')

    for nI := 1 to Len(jTableAll["tables"])
        cAlias      := jTableAll["tables"][nI]["table"]
        cUUIDField  := jTableAll["tables"][nI]["uuidfield"]
            DbSelectArea(cAlias)
            If FieldPos(cUUIDField) > 0
                aAdd(jTableFinal["tables"], jTableAll["tables"][nI])
            EndIf
    next nI

Return jTableFinal["tables"]
