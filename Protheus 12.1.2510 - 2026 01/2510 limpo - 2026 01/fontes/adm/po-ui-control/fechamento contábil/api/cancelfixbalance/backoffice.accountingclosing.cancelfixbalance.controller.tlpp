#include "tlpp-core.th"
#include "BACKOFFICE.ACCOUNTINGCLOSING.CANCELFIXBALANCE.CONTROLLER.CH"

namespace totvs.protheus.backoffice.accountingclosing.cancelfixbalance
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelfixbalance
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class cancelFixBalance
    public method new()

    @POST("/api/fc/cancelfixbalance/")
    public method postCancelFixBalance()
    
    @POST("/api/fc/clearfixbalance/")
    public method clearFixBalance()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelFixBalance
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class cancelFixBalance
return

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelFixBalance
Metodo que ira excluir da tabela de divergencias de saldos (QLJ) os registros que não serão 
corrigidos, pois o processo foi cancelado

@author Totvs
/*/
//-------------------------------------------------------------------
method postCancelFixBalance() class cancelFixBalance

    Local oService  as Object
    Local jResposta as Json
    Local cFilBkp   As Character   
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	
    Local cUUID     As Character

    //Inicializa variaveis
    oService    := cancelFixBalanceService():getInstance()
    jResposta   := JsonObject():New()
    cFilBkp     := cFilAnt
    lResponse   := .F.
    cChaveRes   := "message"
    cResponse   := ""
    cUUID       := oRest:getQueryRequest():GetJsonText("uuid")

    fcGetHeaders()
    If ValType(cUUID) == "C" .And. !Empty(cUUID) .And. cUUID <> "null"
        lResponse := oService:postCancelFixBalance(cUUID, @cResponse)            
    Else
        cResponse := STR0001 //"Código UUID não identificado"
    EndIf
    
    fcSetResponse(lResponse, cChaveRes, cResponse, .F.)

    //Restaura empresa e filial
    cFilAnt := cFilBkp

return

//-------------------------------------------------------------------
/*/{Protheus.doc} clearFixBalance
Método que ira limpar da tabela de divergencias de saldos registros restantes de 
processamentos que foram encerrados de maneira inesperada.

@author Totvs
/*/
//-------------------------------------------------------------------
method clearFixBalance() class cancelFixBalance

    Local oService  as Object
    Local jResposta as Json
    Local cFilBkp   As Character   
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	

    //Inicializa variaveis
    oService    := cancelFixBalanceService():getInstance()
    jResposta   := JsonObject():New()
    cFilBkp     := cFilAnt
    lResponse   := .F.
    cChaveRes   := "message"
    cResponse   := ""

    fcGetHeaders()

    lResponse := oService:clearFixBalance(@cResponse)          
    fcSetResponse(lResponse, cChaveRes, cResponse, .F.)

    //Restaura empresa e filial
    cFilAnt := cFilBkp

return
