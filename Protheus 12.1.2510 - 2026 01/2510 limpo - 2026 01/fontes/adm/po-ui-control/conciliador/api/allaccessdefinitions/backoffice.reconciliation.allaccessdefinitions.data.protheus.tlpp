#include "tlpp-core.th"
//#include "backoffice.reconciliation.allaccessdefinitions.data.protheus.ch"

namespace totvs.protheus.backoffice.reconciliation.allaccessdefinitions
using namespace totvs.protheus.backoffice.reconciliation.util

/*/{Protheus.doc} AllAccessDefinitionsProtheusData
    (long_description)
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
    /*/
class AllAccessDefinitionsProtheusData from FWAdapterBaseV2
    Public  method new()
    Public  method getAll()
    static  method getData() as Object
    
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchSettingProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class AllAccessDefinitionsProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchSettingProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class AllAccessDefinitionsProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := AllAccessDefinitionsProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das definições do fechamento contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(nPage as Numeric, nPageSize as Numeric) class AllAccessDefinitionsProtheusData
    Local aArea  := GetArea() As Array

    Default nPage     := 1
    Default nPageSize := 10

    addMapFields(self)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery())

    cWhere := " QLM_FILIAL = '"+xFilial("QLM")+"' AND D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder("QLM.QLM_FILIAL, QLM.QLM_CODIGO")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return


// //-------------------------------------------------------------------
// /*/{Protheus.doc} addMapFields
// Realiza o mapeamento dos campos que serão retornados

// @author Totvs
// /*/
// //-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)

    oSelf:addMapFields("branch"   ,"branch"   , .T., .F., {"branch"    , "C", TamSx3("QLM_FILIAL")[1], 0}, "QLM.QLM_FILIAL")
    oSelf:addMapFields("code"     ,"code"     , .T., .F., {"code"      , "C", TamSx3("QLM_CODIGO")[1], 0}, "QLM.QLM_CODIGO")
    oSelf:addMapFields("descri"   ,"descri"   , .T., .F., {"descri"    , "C", TamSx3("QLM_DESCRI")[1], 0}, "QLM.QLM_DESCRI")
    oSelf:addMapFields("type"     ,"type"     , .T., .F., {"type"      , "C", TamSx3("QLM_TIPO"  )[1], 0}, "QLM.QLM_TIPO"  )
    oSelf:addMapFields("statusacc","statusacc", .T., .F., { "statusacc", "C", TamSx3("QLM_STATUS")[1], 0}, "QLM.QLM_STATUS")

Return

// //-------------------------------------------------------------------
// /*/{Protheus.doc} getQuery
// Realiza a query para busca de informações

// @author Totvs
// /*/
// //-------------------------------------------------------------------

Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM (SELECT DISTINCT QLM_FILIAL, QLM_CODIGO, QLM_DESCRI, QLM_TIPO, QLM_STATUS "
    cQuery += " FROM " + RetSqlName("QLM") + " WHERE #QueryWhere#) QLM"
    cQuery += " "
Return cQuery
