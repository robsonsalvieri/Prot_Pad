#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.matchsettingitems

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchSettingItemsService
Classe de serviço, realizada instância e chamada de métodos de serviços

@author Totvs
/*/
//-------------------------------------------------------------------
class MatchSettingItemsService
    public method new()
    public method getInstance()
    public method getAllMatchSettingItems()
    public method validCidFieldsItems()
    public method handleCustomAction(cCodCfg as Character, cResponseApi as Character) as Character
    public method validPropertyCustomActionPE(jActions as Json, cProperty as Character) as Array
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchSettingItemsService
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class MatchSettingItemsService
return

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchSettingItemsService
Responsável por instanciar a classe controller

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class MatchSettingItemsService
    static __instance As Object
    
    If ValType(__instance) == "U"
        __instance := MatchSettingItemsService():new()
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllMatchSettingItems
Responsável por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllMatchSettingItems(cCodCfg as Character, nPage as Numeric, nPageSize as Numeric, cFromWhere as Character) class MatchSettingItemsService
    Local oSettings := MatchSettingItemsProtheusData():getData() As Object
    Local cResponse := "" As Character

    oSettings:getAll(cCodCfg, nPage, nPageSize)

    IIf(oSettings:lOk,;
        cResponse := self:handleCustomAction(cCodCfg, oSettings:getJSONResponse()),;
        cResponse := SetRestFault(oSettings:GetCode(), oSettings:getMessage()))

    oSettings:DeActivate()
    FreeObj(oSettings)
return cResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} validCidFieldsItems
Responsável por chamar a validação da existencia dos campos cidori e ciddes

@author Totvs
/*/
//-------------------------------------------------------------------
method validCidFieldsItems(jResponse as Json) class MatchSettingItemsService
    Local oSettings := MatchSettingItemsProtheusData():getData() As Object
    Local aCidFields := oSettings:validCidFields(jResponse) as Array
Return aCidFields

//-------------------------------------------------------------------
/*/{Protheus.doc} handleCustomAction
Realiza o tratamento para ações customizadas através do PE CTBA940AC.

@author Totvs
/*/
//-------------------------------------------------------------------
method handleCustomAction(cCodCfg as Character, cResponseAPI as Character) as Character class MatchSettingItemsService
    Local jResponseActs := JsonObject():New() as Json
    Local jActionsPE    := JsonObject():New() as Json
    Local aOrigin       := {} as Array
    Local aDestination  := {} as Array
    Local cResponse     := cResponseAPI as Character
    
    jResponseActs:FromJson(cResponse)

    If Existblock("CTB940AC") .And. jResponseActs:hasProperty('items') .And. Len(jResponseActs['items']) > 0  .And. (cCodCfg $ "0023|0024" .Or. (jResponseActs['items'][1]:hasProperty('cfgori') .And. jResponseActs['items'][1]['cfgori'] $ "0023|0024")) // Regras Financeiras
        jActionsPE      := ExecBlock( "CTB940AC", .F., .F.,{ cCodCfg })
        aOrigin         := self:validPropertyCustomActionPE(jActionsPE, 'origem')
        aDestination    := self:validPropertyCustomActionPE(jActionsPE, 'destino')

        If Len(aOrigin) > 0 .Or. Len(aDestination) > 0
            jResponseActs['items'][1]['customActions'] := { "tabori": aOrigin, "tabdes": aDestination }:toJson()
            cResponse := jResponseActs:toJson()
        EndIf
    EndIf   
Return cResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} validPropertyCustomActionPE
Valida as propriedades retornadas do PE para ações customizadas.

@author Totvs
/*/
//-------------------------------------------------------------------
method validPropertyCustomActionPE(jActions as Json, cProperty as Character) as Array class MatchSettingItemsService
    Local nCount        := 1 as Numeric
    Local aValidActions := {} as Array
    Local aActionsPE    := {} as Array

    If jActions:hasProperty(cProperty) .And.ValType(jActions[cProperty]) == "A"
        aActionsPE := jActions[cProperty]
        For nCount := 1 To Len(aActionsPE)
            If  aActionsPE[nCount]:hasProperty('label') .And.;
                aActionsPE[nCount]:hasProperty('description') .And.;
                aActionsPE[nCount]:hasProperty('action')

                aAdd(aValidActions, aActionsPE[nCount])
            EndIf
        Next nCount
    EndIf
Return aValidActions


