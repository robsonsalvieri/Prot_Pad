#include "tlpp-core.th"
#include "totvs.ch"

namespace totvs.protheus.backoffice.reconciliation.setuptaxes
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GetSetupTaxesProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class SetupTaxesProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    Public method getSetupTaxesProtheusData()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class SetupTaxesProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccountProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class SetupTaxesProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := SetupTaxesProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das configuracoes do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cTable as Character, nPage as Numeric) class SetupTaxesProtheusData
    Local aArea     := GetArea() as Array

    addMapFields(self)
    
    ::SetQuery(getQuery())
    ::SetWhere(getWhere(cTable))
    ::SetOrder("F2B_FILIAL, F2B_REGRA, F2B_TRIB")
    ::setPageSize(1000)
    ::setPage(nPage)

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return 

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)

    oSelf:addMapFields("branch", "branch", .T., .F., {"branch", "C", TamSx3("F2B_FILIAL")[1], 0}, "F2B.F2B_FILIAL")
    oSelf:addMapFields("ruletax", "ruletax", .T., .F., {"ruletax", "C", TamSx3("F2B_REGRA")[1], 0}, "F2B.F2B_REGRA")
    oSelf:addMapFields("tax" , "tax" , .T., .F., {"tax", "C", TamSx3("F2B_TRIB")[1], 0}, "F2B.F2B_TRIB" ) 
    oSelf:addMapFields("description" , "description" , .T., .F., {"description", "C", TamSx3("F2B_DESC")[1], 0}, "F2B.F2B_DESC" ) 

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("F2B") + " F2B "
    cQuery += " WHERE #QueryWhere#"

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getWhere
Função que retorna o where que será utilizado na busca
@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getWhere(cTable)
    Local cWhere as Character
    Local cWhereF2B := GetBranchiesForConditional("F2B") as Character
    
    cWhere +=   " F2B." + cWhereF2B + " "
    cWhere +=   " AND F2B.F2B_ALTERA = '2' "
    cWhere +=   " AND EXISTS ( "
    cWhere +=   "     SELECT 1 "
    cWhere +=   "     FROM " + RetSQLName("F2D") + " F2D "
    cWhere +=   "         WHERE "
    cWhere +=   "             F2D.F2D_TRIB = F2B.F2B_REGRA "
    cWhere +=   "             AND F2D.F2D_TABELA = '"+cTable+"' "
    cWhere +=   " )"

Return cWhere
