#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.lookup

//-------------------------------------------------------------------
/*/{Protheus.doc} LookUpService
Classe de serviço, realizada instância e chamada de métodos de serviços

@author Totvs
/*/
//-------------------------------------------------------------------
class LookUpService
    protected data cErrorMessage as Character

    public method new()
    public method getInstance()
    public method getAllLookUp()
    protected method erroLookUp(oError As Object)
    protected method formatResponse(oSettings as object) as json
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} LookUpService
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class LookUpService
    self:cErrorMessage := ""
return

//-------------------------------------------------------------------
/*/{Protheus.doc} LookUpService
Responsável por instanciar a classe controller

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class LookUpService
    static __instance As Object
    
    If ValType(__instance) == "U"
        __instance := LookUpService():new()
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllLookUp
Responsável por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllLookUp(cTable as Character, aFilters as Array, nPage as Numeric, nPageSize as Numeric, cSearch as Character) class LookUpService
    Local oSettings := LookUpProtheusData():getData() As Object
    Local jResponse := JsonObject():new() As Json
    Local bBlock As Codeblock

    bBlock := ErrorBlock( { |e| self:erroLookUp(e) } )
    self:cErrorMessage := ''

    BEGIN TRANSACTION
        oSettings:getAll(cTable, aFilters, nPage, nPageSize, cSearch)

        If Empty(self:cErrorMessage)
            jResponse := self:formatResponse(oSettings)
        Endif 
    END TRANSACTION
    ErrorBlock(bBlock)

    If !Empty(self:cErrorMessage)
        SetRestFault(500, self:cErrorMessage)
    Endif

    oSettings:DeActivate()
    FreeObj(oSettings)
    
return jResponse

//-------------------------------------------------------------------
 /*/{Protheus.doc} ErroLookUp
    Obtém descrição do erro ocorrido

    @type  Method
    @author pequim
    @since 14/02/2023
    @version version

    @param oError, Object, objeto de tratamento de erro
/*/
//-------------------------------------------------------------------
method erroLookUp(oError As Object) class LookUpService
    self:cErrorMessage := oError:description

Return

//-------------------------------------------------------------------
 /*/{Protheus.doc} formatResponse
    Formata a resposta de sucesso.

    @type  Method
    @author totvs
    @since 09/04/2024
    @version version
    @param oSettings, Object, objeto que cotem os dados.
    @return jResponse, json, resposta formatada.
/*/
//-------------------------------------------------------------------
method formatResponse(oSettings as object) as json class LookUpService
    Local jResponse := JsonObject():New() as json

    jResponse:fromJson(oSettings:getJSONResponse())
    jResponse['key']    := oSettings:cValor
    jResponse['index']  := oSettings:aChave
    jResponse['fields'] := oSettings:aGetStruct

Return jResponse
