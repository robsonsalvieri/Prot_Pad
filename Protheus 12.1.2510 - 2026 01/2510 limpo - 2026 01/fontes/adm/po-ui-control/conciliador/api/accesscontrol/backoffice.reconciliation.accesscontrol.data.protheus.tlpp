#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.getAccessControl
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccessControlProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAccessControlProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAllGrps()
    Public method getAllUsers()
    static method getData() As object
    private method getJsonResp()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccessControlProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class GetAccessControlProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccessControlProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class GetAccessControlProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := GetAccessControlProtheusData():new()
    EndIf
return __oActiveData


//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGrps
Metodo responsável pela busca dos grupos de usuário

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllGrps(aFilters as array) class GetAccessControlProtheusData

    Local aGrupos := FWSFAllGrps()

    aGrupos := ::getJsonResp(aGrupos,aFilters)

return aGrupos

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllUsers
Metodo responsável pela busca dos usuários de acordo com o grupo informado

@author Totvs
/*/
//-------------------------------------------------------------------

method getAllUsers(cCodGrupo as Character, aFilters as array) class GetAccessControlProtheusData

    local aUsers := {}

    If Empty(cCodGrupo)
        aUsers := FWSFALLUSERS() //carrega todos os registros do cadastro de usuários.
    Else
        aUsers := FWSFGrpUsers(cCodGrupo) // retorna os usuários que estão vinculados a um determinado grupo de usuário.
        aUsers := FWSFALLUSERS(aUsers) //carrega todos as informações do cadastro de usuários com o filtro .
    EndIf
    
    aUsers := ::getJsonResp(aUsers,aFilters)

return aUsers

//-------------------------------------------------------------------
/*/{Protheus.doc} getJsonResp
Metodo responsável pela busca dos usuários de acordo com o grupo informado

@author Totvs
/*/
//-------------------------------------------------------------------

method getJsonResp(aList as array, aFilters as array) class GetAccessControlProtheusData
    Local aRet := {} as array 
    Local nI :=0
    Local nPos := 0 
    Local cFiltro :=''

    Default aFilters := {}
    
    If len(aFilters) > 0 
        cFiltro := Upper(aFilters[1][2])
        For nI := 1 To Len( aList )
            If cFiltro $ Upper(aList[nI][3])
                Aadd(aRet,JsonObject():new())
                nPos := len(aRet)
                aRet[nPos]["label"] := aList[nI][3]
                aRet[nPos]["value"] := aList[nI][2]
            EndIf
        Next nI
    Else
        For nI := 1 To Len( aList )
            Aadd(aRet,JsonObject():new())
            aRet[nI]["label"] := aList[nI][3]
            aRet[nI]["value"] := aList[nI][2]
        Next nI
    EndIf

return aRet

