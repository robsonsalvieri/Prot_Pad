#INCLUDE "wsadm030.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSADM030   Autor Eduardo Riera           Data 04.08.2003  
Ĵ
Descrio  Web Service das funcionalidades do Financeiro para o portal  
           de Fornecedor                                                
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE FinanceSupplierBill      DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/financesupplierbill.apw" //"Servio de consulta as informaes financeiras dos fornecedores ( <b>Restrio de fornecedor</b> )"
   WSDATA UserCode                 As String
   WSDATA SupplierID               As String
   WSDATA DateFrom                 As Date OPTIONAL
   WSDATA DateTo                   As Date OPTIONAL
   WSDATA Header                   As Array Of BrwHeader
   WSDATA HeaderType               As String
   WSDATA ListOfBill               As Array Of BillView   
   WSDATA Bill                     As BillView
   WSDATA PaidBill                 As PaidBillCollection
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String OPTIONAL
   WSDATA Prefix                   As String 
   WSDATA BillNumber               As String
   WSDATA Installment              As String
   WSDATA BillType                 As String

   WSMETHOD GetHeader    DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do Servio"
   WSMETHOD GetBill      DESCRIPTION STR0003 //"Mtodo de consulta ao ttulo financeiro em aberto"
   WSMETHOD BrwBill      DESCRIPTION STR0004 //"Mtodo de listagem dos ttulos financeiros em aberto"
   WSMETHOD BrwDebitNote DESCRIPTION STR0005 //"Mtodo de listagem das notas de debito financeiras em aberto"
   WSMETHOD BrwPaidBill  DESCRIPTION STR0006 //"Mtodo de listagem dos ttulos financeiros baixados"
   WSMETHOD GetPaidBill  DESCRIPTION STR0007 //"Mtodo de consulta ao ttulos financeiro baixado"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 21.03.2003 
Ĵ
          Mtodo de recuperacao do header de titulos financeiros       
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE FinanceSupplierBill

//Ŀ
//Montagem do Header da estrutura Bill                                    
//

::Header := FinHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    GetBill   Autor   Eduardo Riera          Data 04.12.2003 
Ĵ
          Mtodo de recuperacao dos dados de um titulo do fornecedor   
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do fornecedor                                  
          ExpC3: Prefixo                                               
          ExpC4: Numero                                                
          ExpC3: Parcela                                               
          ExpC4: Tipo                                                  
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo recupera os dados de um titulo do fornecedor     
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetBill WSRECEIVE UserCode,SupplierID,Prefix,BillNumber,Installment,BillType WSSEND Bill WSSERVICE FinanceSupplierBill

Local aArea     := GetArea()
Local lRetorno  := .F.

If PrtChkUser(::UserCode,"FINANCESUPPLIERBILL","GETBILL","SA2",::SupplierID)

	lRetorno := .T.

	dbSelectArea("SE2")
	dbSetOrder(1)
	If MsSeek(xFilial("SE2")+::Prefix+::BillNumber+::Installment+::BillType+::SupplierID)
		
		GetCPBill(@::Bill,"SE2")
			
	Else
		lRetorno := .F.
		SetSoapFault("GETBILL",STR0008)					 //"Titulo nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    BrwBill   Autor   Eduardo Riera          Data 21.03.2003 
Ĵ
          Mtodo de recuperacao dos titulos financeiros do Fornecedor  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Customer ID                                           
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo recupera os titulos financeiro em aberto de um   
          Fornecedor                                                   
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwBill WSRECEIVE UserCode,SupplierID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSupplierBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE2 := "SE2"
Local cFornece  := SubStr(::SupplierID,1,Len(SA2->A2_COD))
Local cLoja     := SubStr(::SupplierID,Len(SA2->A2_COD)+1)
#IFDEF TOP
Local aStruSE2  := {}
Local cQuery    := ""
Local nX        := 0
Local lSaldo    := .T. 
#ENDIF

DEFAULT ::IndexKey := SE2->(IndexKey())

If PrtChkUser(::UserCode,"FINANCESUPPLIERBILL","BRWBILL","SA2",::SupplierID)

	If !Empty(cFornece) .And. !Empty(cLoja)
	
		lRetorno := .T.

		dbSelectArea("SE2")
		dbSetOrder(6)

		::IndexKey    := IIf(Empty(::IndexKey),SE2->(IndexKey()),::IndexKey)

		#IFDEF TOP
			lQuery    := .T.
			aStruSE2  := SE2->(dbStruct())
			cAliasSE2 := "FINGETBILL"
			
			cQuery := "SELECT E2_FILIAL,E2_FORNECE,E2_LOJA,E2_NOMFOR,E2_SALDO,E2_TIPO,E2_PREFIXO,E2_NUM,E2_PARCELA,E2_VALOR,"
			cQuery += "E2_NATUREZ,E2_EMISSAO,E2_VENCTO,E2_VENCREA,E2_MOEDA,E2_PORTADO,E2_HIST,E2_PORCJUR,E2_DTVARIA,"
			cQuery += "E2_SDACRES,E2_SDDECRE,E2_VALLIQ,E2_MULTA,E2_VALJUR,E2_DECRESC,E2_ACRESC,E2_TXMOEDA,E2_ORIGEM "
			cQuery += GetUserField("SE2")
			cQuery += "FROM "+RetSqlName("SE2")+" SE2 "
			cQuery += "WHERE "
			cQuery += "SE2.E2_FILIAL = '"+xFilial("SE2")+"' AND "
			cQuery += "SE2.E2_FORNECE='"+cFornece+"' AND "
			cQuery += "SE2.E2_LOJA='"+cLoja+"' AND "
			cQuery += "SE2.E2_SALDO > 0 AND "
			cQuery += "SE2.E2_TIPO NOT IN "+FormatIn(MV_CPNEG+"|"+MVPAGANT+"|"+MVABATIM,"|")+" AND "
			If ExistBlock( "PEF023" )
				cQuery += ExecBlock( "PEF023", .F., .F. ) + " AND "
			EndIf
			cQuery += "SE2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
			
			cQuery := ChangeQuery(cQuery)
			
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE2)
			
			For nX := 1 To Len(aStruSE2)
				If aStruSE2[nX][2]<>"C" .And. FieldPos(aStruSE2[nX][1])<>0
					TcSetField(cAliasSE2,aStruSE2[nX][1],aStruSE2[nX][2],aStruSE2[nX][3],aStruSE2[nX][4])
			    EndIf
			Next nX
		#ELSE
			If ExistBlock( "PEF023" )
				cQuery := ExecBlock( "PEF023", .F., .F. )
			EndIf
			MsSeek(xFilial("SE2")+::SupplierID)
		#ENDIF
		
		While !Eof() .And. xFilial("SE2") == (cAliasSE2)->E2_FILIAL .And.;
			cFornece == (cAliasSE2)->E2_FORNECE .And.;
			cLoja == (cAliasSE2)->E2_LOJA		
			If (cAliasSE2)->E2_SALDO > 0 .And. !(cAliasSE2)->E2_TIPO$MVABATIM .And. !(cAliasSE2)->E2_TIPO$MV_CPNEG+"/"+MVPAGANT
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				//Mostra o saldo do titulo caso os titulos sejam gerados pelo MATA103 ou FINA050
				If AllTrim((cAliasSE2)->E2_ORIGEM) $ "MATA100#FINA050"
					lSaldo := .F.
				EndIf
				GetCPBill(@::ListOfBill[nY],cAliasSE2,lSaldo)
			EndIf		
			dbSelectArea(cAliasSE2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE2)
			dbCloseArea()
			dbSelectArea("SE2")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWBILL",STR0009) //"Fornecedor invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetDebitNoAutor   Eduardo Riera          Data 15.05.2003 
Ĵ
          Mtodo de recuperacao das notas de credito de um Fornecedor  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Customer ID                                           
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo recupera as notas de credito de um Fornecedor    
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwDebitNote WSRECEIVE UserCode,SupplierID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSupplierBill

Local aArea     := GetArea()

Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE2 := "SE2"
Local cFornece  := SubStr(::SupplierID,1,Len(SA2->A2_COD))
Local cLoja     := SubStr(::SupplierID,Len(SA2->A2_COD)+1)
#IFDEF TOP
Local aStruSE2  := {}
Local cQuery    := ""
Local nX        := 0
#ENDIF

DEFAULT ::IndexKey := SE2->(IndexKey())

If PrtChkUser(::UserCode,"FINANCESUPPLIERBILL","BRWDEBITNOTE","SA2",::SupplierID)

	If !Empty(cFornece) .And. !Empty(cLoja)
	
		lRetorno := .T.

		dbSelectArea("SE2")
		dbSetOrder(6)

		::IndexKey    := IIf(Empty(::IndexKey),SE2->(IndexKey()),::IndexKey)

		#IFDEF TOP
			lQuery    := .T.
			aStruSE2  := SE2->(dbStruct())
			cAliasSE2 := "FINGETBILL"
			
			cQuery := "SELECT E2_FILIAL,E2_FORNECE,E2_LOJA,E2_NOMFOR,E2_SALDO,E2_TIPO,E2_PREFIXO,E2_NUM,E2_PARCELA,E2_VALOR,"
			cQuery += "E2_NATUREZ,E2_EMISSAO,E2_VENCTO,E2_VENCREA,E2_MOEDA,E2_PORTADO,E2_HIST,E2_PORCJUR,E2_DTVARIA,"
			cQuery += "E2_SDACRES,E2_SDDECRE,E2_VALLIQ,E2_MULTA,E2_VALJUR,E2_DECRESC,E2_ACRESC "
			cQuery += GetUserField("SE2")
			cQuery += "FROM "+RetSqlName("SE2")+" SE2 "
			cQuery += "WHERE "
			cQuery += "SE2.E2_FILIAL = '"+xFilial("SE2")+"' AND "
			cQuery += "SE2.E2_FORNECE='"+cFornece+"' AND "
			cQuery += "SE2.E2_LOJA='"+cLoja+"' AND "
			cQuery += "SE2.E2_SALDO > 0 AND "
			cQuery += "SE2.E2_TIPO IN "+FormatIn(MV_CPNEG+"|"+MVPAGANT,"|")+" AND "
			cQuery += "SE2.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
			
			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE2)
			
			For nX := 1 To Len(aStruSE2)
				If aStruSE2[nX][2]<>"C".And. FieldPos(aStruSE2[nX][1])<>0
					TcSetField(cAliasSE2,aStruSE2[nX][1],aStruSE2[nX][2],aStruSE2[nX][3],aStruSE2[nX][4])
			    EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SE2")+::SupplierID)
		#ENDIF
		
		While !Eof() .And. xFilial("SE2") == (cAliasSE2)->E2_FILIAL .And.;
			cFornece == (cAliasSE2)->E2_FORNECE .And.;
			cLoja == (cAliasSE2)->E2_LOJA		
			If (cAliasSE2)->E2_SALDO > 0 .And. (cAliasSE2)->E2_TIPO$MV_CPNEG
				aadd(::ListOfBILL,WSClassNew("BillView"))
				nY++
				GetCPBill(@::ListOfBill[nY],cAliasSE2,.T.)
			EndIf
			dbSelectArea(cAliasSE2)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE2)
			dbCloseArea()
			dbSelectArea("SE2")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWDEBITNOTE",STR0009) //"Fornecedor invalido"
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    BrwPaidBilAutor   Eduardo Riera          Data 21.03.2003 
Ĵ
          Mtodo de recuperacao dos titulos financeiros do Fornecedor  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Customer ID                                           
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo recupera uma colecao de titulos pagos jutamente  
          com seus respectivos pagamentos                              
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwPaidBill WSRECEIVE UserCode,SupplierID,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSupplierBill

Local aArea     := GetArea()
Local aChave    := {}
Local lRetorno  := .F.
Local lQuery    := .F.
Local lSkip     := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSE2 := "SE2"
Local cAliasSE5 := "SE5"
Local cFornece  := SubStr(::SupplierID,1,Len(SA2->A2_COD))
Local cLoja     := SubStr(::SupplierID,Len(SA2->A2_COD)+1)
Local dDataIni  := ::DateFrom
Local dDataFim  := ::DateTo  
#IFDEF TOP
Local aStruSE2  := {}
Local aStruSE5  := {}
Local cQuery    := ""
#ENDIF

DEFAULT ::IndexKey := SE2->(IndexKey())
dDataIni := IIF(Empty(dDataIni),Ctod("01/01/1980"),dDataIni)
dDataFim := IIF(Empty(dDataFim),dDataBase,dDataFim)

If PrtChkUser(::UserCode,"FINANCESUPPLIERBILL","BRWPAIDBILL","SA2",::SupplierID)

	If !Empty(cFornece) .And. !Empty(cLoja)
	
		lRetorno := .T.
		
		dbSelectArea("SE2")
		dbSetOrder(6)

		::IndexKey    := IIf(Empty(::IndexKey),SE2->(IndexKey()),::IndexKey)

		#IFDEF TOP
			lQuery    := .T.
			aStruSE2  := SE2->(dbStruct())
			aStruSE5  := SE5->(dbStruct())
			
			cAliasSE2 := "FINGETBILL"
			cAliasSE5 := "FINGETBILL"			
			
			cQuery := "SELECT E2_FILIAL,E2_FORNECE,E2_LOJA,E2_NOMFOR,E2_SALDO,E2_TIPO,E2_PREFIXO,E2_NUM,E2_PARCELA,E2_VALOR,"
			cQuery += "E2_NATUREZ,E2_EMISSAO,E2_VENCTO,E2_VENCREA,E2_MOEDA,E2_PORTADO,E2_HIST,E2_PORCJUR,E2_DTVARIA,"
			cQuery += "E2_SDACRES,E2_SDDECRE,E2_VALLIQ,E2_MULTA,E2_VALJUR,E2_DECRESC,E2_ACRESC, "
			cQuery += "E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
			cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
			cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC"			
			cQuery += GetUserField("SE2")
			cQuery += GetUserField("SE5")			
			cQuery += "FROM "+RetSqlName("SE2")+" SE2,"
			cQuery += RetSqlName("SE5")+" SE5 "
			cQuery += "WHERE "
			cQuery += "SE2.E2_FILIAL = '"+xFilial("SE2")+"' AND "
			cQuery += "SE2.E2_FORNECE='"+cFornece+"' AND "
			cQuery += "SE2.E2_LOJA='"+cLoja+"' AND "
			cQuery += "SE2.E2_TIPO NOT IN "+FormatIn(MV_CPNEG+"|"+MVPAGANT+"|"+MVABATIM,"|")+" AND "
			If ExistBlock( "PEF023" )
				cQuery += ExecBlock( "PEF023", .F., .F. ) + " AND "
			EndIf
			cQuery += "SE2.E2_SALDO <> E2_VALOR AND "
			cQuery += "SE2.D_E_L_E_T_=' ' AND "
			cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "SE5.E5_NATUREZ=SE2.E2_NATUREZ AND "
			cQuery += "SE5.E5_PREFIXO=SE2.E2_PREFIXO AND "
			cQuery += "SE5.E5_NUMERO=SE2.E2_NUM AND "
			cQuery += "SE5.E5_PARCELA=SE2.E2_PARCELA AND "
			cQuery += "SE5.E5_TIPO=SE2.E2_TIPO AND "
			cQuery += "SE5.E5_CLIFOR=SE2.E2_FORNECE AND "
			cQuery += "SE5.E5_LOJA=SE2.E2_LOJA AND "
			cQuery += "SE5.E5_RECPAG='P' AND "
			cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
			cQuery += "SE5.E5_SITUACA<>'C' AND "
			cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
 			cQuery += "SELECT A.E5_NUMERO "
			cQuery += "FROM "+RetSqlName("SE5")+" A "
			cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
			cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
			cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
			cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
			cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
			cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
			cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
			cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
			cQuery += "A.E5_TIPODOC='ES' AND "
			cQuery += "A.E5_RECPAG<>'P' AND "
			cQuery += "A.D_E_L_E_T_ = ' ') "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
			
			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE2)
			
			For nX := 1 To Len(aStruSE2)
				If aStruSE2[nX][2]<>"C" .And. FieldPos(aStruSE2[nX][1])<>0
					TcSetField(cAliasSE2,aStruSE2[nX][1],aStruSE2[nX][2],aStruSE2[nX][3],aStruSE2[nX][4])
			    EndIf
			Next nX
			For nX := 1 To Len(aStruSE5)
				If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
					TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
			    EndIf
			Next nX			
		#ELSE
			If ExistBlock( "PEF023" )
				cQuery := ExecBlock( "PEF023", .F., .F. )
			EndIf
			MsSeek(xFilial("SE2")+::SupplierID)
		#ENDIF
		
		While !Eof() .And. xFilial("SE2") == (cAliasSE2)->E2_FILIAL .And.;
			cFornece == (cAliasSE2)->E2_FORNECE .And.;
			cLoja == (cAliasSE2)->E2_LOJA		
			If (cAliasSE2)->E2_SALDO < (cAliasSE2)->E2_VALOR .And. !(cAliasSE2)->E2_TIPO $ MVABATIM
			
				If !lQuery
					dbSelectArea("SE5")
					dbSetOrder(7)
					MsSeek(xFilial("SE5")+(cAliasSE2)->E2_PREFIXO+(cAliasSE2)->E2_NUM+(cAliasSE2)->E2_PARCELA+(cAliasSE2)->E2_TIPO+(cAliasSE2)->E2_FORNECE+(cAliasSE2)->E2_LOJA)
				EndIf

				nX    := 0
				lSkip := .F.
				aChave := {	(cAliasSE2)->E2_PREFIXO ,;
					(cAliasSE2)->E2_NUM ,;
					(cAliasSE2)->E2_PARCELA ,;
					(cAliasSE2)->E2_TIPO ,;
					(cAliasSE2)->E2_FORNECE,;
					(cAliasSE2)->E2_LOJA}

				
				While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
					aChave[1] == (cAliasSE5)->E5_PREFIXO .And.;
					aChave[2] == (cAliasSE5)->E5_NUMERO .And.;
					aChave[3] == (cAliasSE5)->E5_PARCELA .And.;
					aChave[4] == (cAliasSE5)->E5_TIPO .And.;
					aChave[5] == (cAliasSE5)->E5_CLIFOR .And.;
					aChave[6] == (cAliasSE5)->E5_LOJA
					
					If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
						(cAliasSE5)->E5_DATA >= dDataIni .And. (cAliasSE5)->E5_DATA <= dDataFim .And.;
						(cAliasSE5)->E5_SITUACA <> "C" .And.;
						(cAliasSE5)->E5_RECPAG == "P" .And.;
						IIf(lQuery,.T.,!TemBxCanc())
						nX++					
						If nX == 1
							aadd(::ListOfBill,WSClassNew("BillView"))
							nY++
							GetCPBill(@::ListOfBill[nY],cAliasSE2,.T.)
						EndIf
					EndIf
					dbSelectArea(cAliasSE5)
					dbSkip()
					lSkip := .T.
				EndDo
			EndIf
			If !lQuery .Or. !lSkip
				dbSelectArea(cAliasSE2)
				dbSkip()
			EndIf
		EndDo
		If lQuery
			dbSelectArea(cAliasSE2)
			dbCloseArea()
			dbSelectArea("SE2")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWPAIDBILL",STR0009) //"Fornecedor invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    GetPaidBilAutor   Eduardo Riera          Data 04.12.2003 
Ĵ
          Mtodo de recuperacao dos dados de um titulo pago do fornece-
          dor                                                          
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do fornecedor                                  
          ExpC3: Prefixo                                               
          ExpC4: Numero                                                
          ExpC3: Parcela                                               
          ExpC4: Tipo                                                  
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este Mtodo recupera os dados de um titulo pago juntamente   
          com seus respectivos pagamentos.                             
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetPaidBill WSRECEIVE UserCode,SupplierID,Prefix,BillNumber,Installment,BillType WSSEND PaidBill WSSERVICE FinanceSupplierBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local lQuery    := .F.
Local nX        := 0
Local cAliasSE5 := "SE5"
Local cFornece  := SubStr(::SupplierID,1,Len(SA2->A2_COD))
Local cLoja     := SubStr(::SupplierID,Len(SA2->A2_COD)+1)
#IFDEF TOP
Local aStruSE5  := {}
Local cQuery    := ""
#ENDIF

If PrtChkUser(::UserCode,"FINANCESUPPLIERBILL","GETPAIDBILL","SA2",::SupplierID)

	If !Empty(cFornece) .And. !Empty(cLoja)
	
		dbSelectArea("SE2")
		dbSetOrder(1)
		If MsSeek(xFilial("SE2")+::Prefix+::BillNumber+::Installment+::BillType+::SupplierId)
	
			lRetorno := .T.
			dbSelectArea("SE5")
			dbSetOrder(7)			
			#IFDEF TOP
				lQuery    := .T.
				aStruSE5  := SE5->(dbStruct())
				
				cAliasSE5 := "FINGETBILL"			
				
				cQuery := "SELECT E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
				cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
				cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC"			
				cQuery += GetUserField("SE5")			
				cQuery += "FROM "+RetSqlName("SE5")+" SE5 "
				cQuery += "WHERE "
				cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
				cQuery += "SE5.E5_NATUREZ='"+SE2->E2_NATUREZ+"' AND "
				cQuery += "SE5.E5_PREFIXO='"+SE2->E2_PREFIXO+"' AND "
				cQuery += "SE5.E5_NUMERO='"+SE2->E2_NUM+"' AND "
				cQuery += "SE5.E5_PARCELA='"+SE2->E2_PARCELA+"' AND "
				cQuery += "SE5.E5_TIPO='"+SE2->E2_TIPO+"' AND "
				cQuery += "SE5.E5_CLIFOR='"+SE2->E2_FORNECE+"' AND "
				cQuery += "SE5.E5_LOJA='"+SE2->E2_LOJA+"' AND "
				cQuery += "SE5.E5_RECPAG='P' AND "
//				cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
//				cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
				cQuery += "SE5.E5_SITUACA<>'C' AND "
				cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
	 			cQuery += "SELECT A.E5_NUMERO "
				cQuery += "FROM "+RetSqlName("SE5")+" A "
				cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
				cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
				cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
				cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
				cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
				cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
				cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
				cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
				cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
				cQuery += "A.E5_TIPODOC='ES' AND "
				cQuery += "A.E5_RECPAG<>'P' AND "
				cQuery += "A.D_E_L_E_T_ = ' ') "
				cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
				cQuery += "ORDER BY "+WsSqlOrder(SE5->(IndexKey()))
				
				cQuery := ChangeQuery(cQuery)
	
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE5)
				
				For nX := 1 To Len(aStruSE5)
					If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
						TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
				    EndIf
				Next nX			
			#ELSE		
				MsSeek(xFilial("SE5")+SE2->E2_PREFIXO+SE2->E2_NUM+SE2->E2_PARCELA+SE2->E2_TIPO+SE2->E2_FORNECE+SE2->E2_LOJA)
			#ENDIF
	
			nX    := 0
			
			While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
				SE2->E2_PREFIXO == (cAliasSE5)->E5_PREFIXO .And.;
				SE2->E2_NUM == (cAliasSE5)->E5_NUMERO .And.;
				SE2->E2_PARCELA == (cAliasSE5)->E5_PARCELA .And.;
				SE2->E2_TIPO == (cAliasSE5)->E5_TIPO .And.;
				SE2->E2_FORNECE == (cAliasSE5)->E5_CLIFOR .And.;
				SE2->E2_LOJA == (cAliasSE5)->E5_LOJA

//				(cAliasSE5)->E5_DATA >= dDataIni .And. (cAliasSE5)->E5_DATA <= dDataFim .And.;					
				If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
					(cAliasSE5)->E5_SITUACA <> "C" .And.;
					(cAliasSE5)->E5_RECPAG == "P" .And.;
					IIf(lQuery,.T.,!TemBxCanc())
					nX++					
					If nX == 1
						::PaidBill:Bill := WSClassNew("BillView")
						::PaidBill:Payment := {}
						GetCPBill(@::PaidBill:Bill,"SE2")
					EndIf
					aadd(::PaidBill:PayMent,WSClassNew("PaidBillView"))
					GetCPPaidBill(@::PaidBill:PayMent[nX],cAliasSE5)
				EndIf
				dbSelectArea(cAliasSE5)
				dbSkip()
			EndDo
			If lQuery
				dbSelectArea(cAliasSE5)
				dbCloseArea()
				dbSelectArea("SE5")
			EndIf
		Else
			lRetorno := .F.
			SetSoapFault("GETPAIDBILL",STR0008) //"Titulo nao encontrado"
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETPAIDBILL",STR0009) //"Fornecedor invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetCPBill Autor   Eduardo Riera          Data 04.12.2003 
Ĵ
          Montagem da estrutura de titulos financeiros                 
                                                                       
Ĵ
ParametrosExpO1: Objeto da estrutura de titulos                        
          ExpC2: Alias do SE2                                          
          ExpL3: Indica se a otimizacao deve ser utilizada             
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Esta funcao atualiza a estrutura Bill com os dados do titulo 
          financeiro                                                   
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function GetCPBill(oObjeto,cAliasSE2,lFast)

Local aValores := {}

DEFAULT lFast := .F.
aValores := FaVlAtuCP(cAliasSE2,dDataBase,"NOR",lFast)
If lFast
	aValores[11] := Nil
	aValores[12] := Nil
EndIf
oObjeto:Prefix                := (cAliasSE2)->E2_PREFIXO
oObjeto:BillNumber            := (cAliasSE2)->E2_NUM
oObjeto:Installment           := (cAliasSE2)->E2_PARCELA	
oObjeto:BillType              := (cAliasSE2)->E2_TIPO
oObjeto:ClassCode             := (cAliasSE2)->E2_NATUREZ
oObjeto:IssueDate             := (cAliasSE2)->E2_EMISSAO
oObjeto:OriginalDueDate       := (cAliasSE2)->E2_VENCTO
oObjeto:RealDueDate           := (cAliasSE2)->E2_VENCREA
oObjeto:Currency              := (cAliasSE2)->E2_MOEDA
oObjeto:CustomerCode          := (cAliasSE2)->E2_FORNECE
oObjeto:UnitCustomerCode      := (cAliasSE2)->E2_LOJA
oObjeto:CustomerName          := (cAliasSE2)->E2_NOMFOR
oObjeto:CurrencySimbol        := SuperGetMV("MV_SIMB"+AllTrim(Str((cAliasSE2)->E2_MOEDA,2)))
oObjeto:OriginalValue         := aValores[1]
oObjeto:OriginalCurrencyValue := xMoeda(aValores[01],(cAliasSE2)->E2_MOEDA,1)
oObjeto:DeductionsValue       := aValores[02]
oObjeto:IncreaseValue         := aValores[05]
oObjeto:DecreaseValue         := aValores[04]
oObjeto:BalanceValue          := aValores[11]
oObjeto:BalanceCurrencyValue  := aValores[12]
oObjeto:Bank                  := (cAliasSE2)->E2_PORTADO
oObjeto:History               := (cAliasSE2)->E2_HIST
oObjeto:DelayInDays           := IIf((cAliasSE2)->E2_VENCREA>dDatabase,dDataBase-(cAliasSE2)->E2_VENCTO,0)
oObjeto:DaylyInterestRate     := (cAliasSE2)->E2_PORCJUR
oObjeto:DaylyInterestValue    := (cAliasSE2)->E2_VALJUR
UserFields("SE2",@oObjeto:UserFields,cAliasSE2)
Return(.T.)

/*/

Ŀ
Funo    GetCPPaid Autor   Eduardo Riera          Data 04.12.2003 
Ĵ
          Montagem da estrutura de pagamentos dos titulos financeiros  
                                                                       
Ĵ
ParametrosExpO1: Objeto da estrutura de titulos                        
          ExpC2: Alias do SE5                                          
Ĵ
Retorno   ExpL1: Indica que o Mtodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Esta funcao atualiza a estrutura Bill com os dados do titulo 
          financeiro                                                   
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Function GetCPPaidBill(oObjeto,cAliasSE5)

oObjeto:ReasonOfWriteOff    := (cAliasSE5)->E5_MOTBX
oObjeto:Bank                := (cAliasSE5)->E5_BANCO
oObjeto:BankBranch          := (cAliasSE5)->E5_AGENCIA
oObjeto:BankAccount         := (cAliasSE5)->E5_CONTA
oObjeto:PaidDate            := (cAliasSE5)->E5_DATA
oObjeto:CreditDate          := (cAliasSE5)->E5_DTDISPO
oObjeto:Currency            := 1
oObjeto:CurrencySimbol      := SuperGetMv("MV_SIMB1")
oObjeto:ValuePaid           := (cAliasSE5)->E5_VALOR
oObjeto:CurrencyValuePaid   := (cAliasSE5)->E5_VALOR
oObjeto:History             := (cAliasSE5)->E5_HISTOR
oObjeto:ReceiptOrPaymentOrder := (cAliasSE5)->E5_ORDREC
oObjeto:SerialReceipt       := (cAliasSE5)->E5_SERREC
If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ"
	oObjeto:WriteOffType        :=  "1"
	oObjeto:TypeDescription  :=  STR0010 //"Principal"
EndIf
If ( (cAliasSE5)->E5_TIPODOC$"DC#D2" )
	oObjeto:WriteOffType        :=  "2"
	oObjeto:TypeDescription  :=  STR0011 //"Desconto"
Endif
If (cAliasSE5)->E5_TIPODOC$"MT#M2"
	oObjeto:WriteOffType        :=  "3"
	oObjeto:TypeDescription  :=  STR0012 //"Multa"
EndIf
If (cAliasSE5)->E5_TIPODOC$"JR#J2"
	oObjeto:WriteOffType        :=  "4"
	oObjeto:TypeDescription  :=  STR0013 //"Juros"
EndIf
If (cAliasSE5)->E5_TIPODOC$"CM#C2#CX"
	oObjeto:WriteOffType        :=  "5"
	oObjeto:TypeDescription  :=  STR0014 //"Correcao Monetaria"
EndIf
UserFields("SE5",@oObjeto:UserFields,cAliasSE5)
Return(.T.)
