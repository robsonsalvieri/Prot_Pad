#Include "Atfa210.Ch"
#Include "Protheus.ch"

Static cAliasQry  := "" 
Static oQueryQry  := Nil
Static oQuerySN8  := Nil


// 17/08/2009 - Ajuste para filiais com mais de 2 caracteres.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Atfa210  ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastramento de Inventario                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atfa210()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Atfa210()

Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemtoAnsi(STR0006) 				//"Cadastro de Inventario"

// Verifica se o paramentro "MV_BLQINVE" existe
If SuperGetMv("MV_BLQINVE",.F.,"#") == "#"
	Help(" ",1,"AF210DESAT",,STR0011,1,0) //"Módulo SIGAATF desatualizado, por favor executar o último atualizador."
	Return
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

mBrowse( 6, 1,22,75,"SN8")

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af210Del ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 13/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao de Invent rio                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Af210Del                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AF210Del(cAlias,nReg,nOpc)
LOCAL nOpcA   
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local oDlg
Local lBlqInv	:= SuperGetMv("MV_BLQINVE",.F.,"2") == "1"
Local cStatus	:= "1"

*ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
*³ Monta a entrada de dados do arquivo                          ³
*ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]

If !Empty(SN8->N8_DTAJUST)
	Help(" ",1,"AF210DEL")  //Bens j  que sofreram ajuste contabil n„o pode ser exclu¡dos.
	dbSelectArea("SN8")
	Return
EndIf    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calculo do tamanho dos objetos                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 100, .t., .t. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }                                        
aPosObj := MsObjSize( aInfo, aObjects )

// ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// ³ Envia para processamento dos Gets          ³
// ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

nOpcA:=0
dbSelectArea(cAlias)
If !SoftLock( cAlias )
	Return
EndIf	
DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
nOpcA:=EnChoice( cAlias, nReg, nOpc,,,,,aPosObj[1])
nOpca := 1
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})

If nOpcA == 2
	Begin Transaction
		If lBlqInv 		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Bloqueia ou desbloqueia o bem de acordo com o local informado.      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SN1")
	   		SN1->(dbSetOrder(1))
			If SN1->(dbSeek(xFilial("SN1")+SN8->N8_CBASE+SN8->N8_ITEM))
				If !Empty(SN1->N1_LOCAL)
					dbSelectArea("SNL")
					SNL->(dbSetOrder(1))
					If SNL->(dbSeek( xFilial("SNL") + SN1->N1_LOCAL))
						If SNL->NL_BLOQ == "1"
							cStatus := "3"
						EndIf
					EndIf
				EndIf			
				Reclock("SN1",.F.)
				SN1->N1_STATUS := cStatus
				SN1->(MsUnlock())
			EndIf
		EndIf			
		dbSelectArea( cAlias )
		RecLock(cAlias,.F.,.T.)
		dbDelete( )		
	End Transaction	
Else
	MsUnlock( )
End
dbSelectArea( cAlias )    

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af210Alt ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 13/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Altera‡Æo de Invent rio                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Af210Alt                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AF210Alt(cAlias,nReg,nOpc)
LOCAL nOpcA
Local aCampoSN8 := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega matriz com campos que serao alterados neste cadastro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("SN8")

While !EOF() .And. (X3_ARQUIVO == cAlias)
	IF X3USO(X3_USADO).and. cNivel >= X3_NIVEL   
		AADD(aCampoSN8,X3_CAMPO)                   //Campos a serem alterados, exceto os campos chave.
	EndIF
	dbSkip()
EndDO

dbSelectArea(cAlias)

// ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// ³ Monta a entrada de dados do arquivo                          ³
// ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aTELA[0][0],aGETS[0]

If !Empty(SN8->N8_DTAJUST)
	Help(" ",1,"AF210ALT")  //Bens j  que sofreram ajuste contabil n„o pode ser exclu¡dos.
	dbSelectArea("SN8")
	Return
EndIf

nOpcA:=0
dbSelectArea(cAlias)
dbSetOrder(1)
nOpca := AxAltera(cAlias,nReg,nOpc,aCampoSN8,,,,"AF210TudOK()" )

dbGoTo( nReg )
dbSelectArea(cAlias)
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af210Inc ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 17.03.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de Invent rio                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Af210Inc                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AF210Inc(cAlias,nReg,nOpc)
Local 	nOpcA
Local	aCampoSN8 	:= {}
Local   lBlqInv	    := SuperGetMv("MV_BLQINVE",.F.,"2") == "1"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega matriz com campos que serao alterados neste cadastro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
dbSeek("SN8")

While !EOF() .And. (X3_ARQUIVO == cAlias)
	IF X3USO(X3_USADO).and. cNivel >= X3_NIVEL   
		AADD(aCampoSN8,X3_CAMPO)                   //Campos a serem alterados, exceto os campos chave.
	EndIF
	dbSkip()
EndDO

dbSelectArea(cAlias)

// *ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
// *³ Monta a entrada de dados do arquivo                          ³
// *ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aTELA[0][0],aGETS[0]

nOpcA:=0
dbSelectArea(cAlias)
dbSetOrder(1)
nOpca := AxInclui(cAlias,nReg,nOpc,aCampoSN8,,,"AF210TudOK()" )

If nOpca = 1
	dbSelectArea("SN3")
	dbSetOrder(1)
	If SN3->(dbSeek(xFilial("SN3")+SN8->N8_CBASE+SN8->N8_ITEM+SN8->N8_TIPO+"0"))
		dbSelectArea("SN8")
		Reclock("SN8")
		SN8->N8_SEQ := SN3->N3_SEQ
		MsUnlock()
	EndIf
	If lBlqInv
		dbSelectArea("SN1")
		SN1->(dbSetOrder(1))
		If SN1->(dbSeek(xFilial("SN1")+SN8->N8_CBASE+SN8->N8_ITEM))
			Reclock("SN1",.F.)
			SN1->N1_STATUS := "2"
			SN1->(MsUnlock())
		EndIf
	EndIf	
EndIf
dbSelectArea(cAlias)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AF210SN1 ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o ativo esta cadastrado no SN1                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atfa210()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Af210SN1()
//Movida para a função tudoOK da rotina
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AF210SN3 ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o ativo esta cadastrado no SN3                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atfa210()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Af210SN3()
//Movida para a função tudoOK da rotina
Return .t.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AF210Data³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ A data deve ser > MV_ULTDEPR e < ( MV_ULTDEPR + 1)         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AF210DATA                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Af210DATA()
//Movida para a função tudoOK da rotina
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AF210Vlr ³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 01/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Valor invent. e maior que o valor original   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atfa210()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Af210Vlr()
//Movida para a função tudoOK da rotina
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AF210TudOK³ Autor ³ Alice Y. Yamamoto     ³ Data ³ 13/10/98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se as validacoes estao OK                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atfa210()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AF210TudOK()

Local lRet   	:= .T. 
Local aArea  	:= GetArea()
Local cCBase 	:= M->N8_CBASE
Local cItem  	:= M->N8_ITEM
Local cTipo  	:= M->N8_TIPO
Local cSeq 		:= IIf(Altera, SN8->N8_SEQ , "")
Local nOrig		:= IIf(Altera, SN8->N8_VLRINV,0)
Local dDtIni	:= GetMv("MV_ULTDEPR")
Local dDtFim	:= LastDay(dDtIni+1)

dbSelectArea("SN3")
SN3->(dbSetOrder(1))

dbSelectArea("SN1")
dbSetOrder(1)

If lRet .And. !SN1->(dbSeek(xFilial("SN1")+cCBase+cItem))
	Help(" ",1,"AF210SN1")   // Nao esta cadastrado no SN1		
	lRet := .F.
EndIf

If INCLUI .And. (lRet .And. SN1->N1_STATUS == "2" )
	Help(" ",1,"AF210FHBLQ",,STR0010,1,0) //"Ficha de ativo bloqueada por usuário."  		
	lRet := .F.
EndIf

If lRet .And. !SN3->(dbSeek(xFilial("SN3")+ cCBase + cItem + cTipo + "0" + cSeq))
	Help(" ",1,"AF210NOSN3",,STR0008,1,0)//"Não existe ficha ativa para esse código, verifique o cadastro de ativo."
	lRet := .F.
EndIf

If lRet .And. Empty(SN3->N3_CCONTAB)
	Help(" ",1,"AF210NOCLA",,STR0009,1,0)//"Ficha de ativo não foi classificada, verifique o campo N3_CCONTAB "
	lRet := .F.
EndIf

If lRet .And. (m->n8_dtinv < dDtIni .or. m->n8_dtinv > dDtFim)
	Help(" ",1,"210DTINVEN",,STR0012,1,0)   // A data do inventário deve ser imediatamente posterior ao último cálculo.
	lRet := .F.
	Return lRet
EndIf

If lRet .And. INCLUI
	dbSelectArea("SN8")
	SN8->(dbSetOrder(2)) // Filial + Código Base + Item + Data de Inventário 
	SN8->(DbSeek(xFilial("SN8") + M->N8_CBASE + M->N8_ITEM + DTOS(dDtIni),.T.))
	While SN8->(!Eof()) .And. xFilial("SN8")+M->N8_CBASE+M->N8_ITEM == SN8->N8_FILIAL+SN8->N8_CBASE+SN8->N8_ITEM;
	.And. SN8->N8_DTINV <= dDtFim
		If SN8->N8_TIPO == cTipo
			Help(" ",1,"210TPINV",,STR0013,1,0)   // Não pode existir mais de um inventario do mesmo tipo de um bem dentro do mesmo mes/ano.  
			lRet := .F.
			Exit	
		ElseIf Empty(SN8->N8_DTAJUST)
			Help(" ",1,"210AJINV",,STR0014,1,0)   // Não é possível incluir o inventário, pois o bem já está em inventário.      
			lRet := .F.
			Exit	
		EndIf
		SN8->(DbSkip())
	EndDo
EndIf

If lRet 
	nOrig   := SN3->N3_VORIG1+SN3->N3_VRCACM1
EndIf

If lRet .And. (m->n8_vlrinv < 0 .or. nOrig < m->n8_vlrinv)
	Help(" ",1,"210VALOR")   // O valor inv. nao pode ser > que o vlr original
	lRet := .F.
EndIf
	
RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Ana Paula N. Silva     ³ Data ³29/11/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados     ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local aRotina := { { OemToAnsi(STR0001),"AxPesqui", 0 , 1 , ,.F.},;  //"Pesquisar"
							{ OemToAnsi(STR0002),"AxVisual", 0 , 2},;  //"Visualizar"
							{ OemToAnsi(STR0003),"AF210Inc",0 , 3},;   //"Incluir"
							{ OemToAnsi(STR0004),"AF210Alt",0 , 4},;   //"Alterar"
							{ OemToAnsi(STR0005),"AF210Del",0 , 5},;   //"Excluir"
							{ OemToAnsi(STR0015),"AF210Est",0 , 2} }   //"Estornar"
Return(aRotina)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Af210Est ³ Autor ³ Ewerton Franklin     ³ Data ³ 23/12/25 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Estorno de Inventario                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Af210Est                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AF210Est(lAutomato as Logical) as Logical

Local cPerg	:= "AFA210EST " as Character 
Local aSays :={}, aButtons:={}
Local nOpca := 0
Local lQtdAntN8	:= SN8->(FieldPos("N8_QTDANT")) > 0

Private cCadastro  	:= STR0016//"Estorno.Cont. de Bens Inventariados"  
Private cDataDe  	as Character
Private cDataAte 	as Character
Private cBaseDe	 	as Character
Private cBaseAte 	as Character
Private cItemDe  	as Character
Private cItemAte 	as Character
Private cTipoDe  	as Character
Private cTipoAte 	as Character
Private lCtb 		as Logical
Private lMostra		as Logical
Private lAglutina	as Logical
Private lSelFil 	as Logical
Private cFilde	 	as Character
Private cFilAte	 	as Character 

Default lAutomato := .F.

If !lAutomato
	AADD(aSays,STR0017) //"Este programa tem o objetivo de estornar o ajuste contabil" 
	AADD(aSays,STR0018) //"dos bens inventariados com base nos parâmetros informados "
	AADD(aSays,STR0019) //"a seguir. Os bens inventariados devem ser estornardos antes"
	AADD(aSays,STR0020) //"de qualquer movimentação posterior."
	AADD(aSays,STR0021) //"Deseja Continuar? " 
	AADD(aButtons, { 1,.T.,{|| nOpca := 1, FechaBatch() }} )
	AADD(aButtons, { 2,.T.,{|| FechaBatch() }} )

	FormBatch( cCadastro, aSays, aButtons ,,,380)
EndIf

If nOpca == 1 .or. lAutomato

	If !lQtdAntN8
		Help(" ",1,"AF210QTD",,STR0022,1,0,,,,,,{STR0023}) //"Campo N8_QTDANT não existente"#"Favor aplicar pacote de dicionário mais recente"
		Return
	EndIf

	If Pergunte(cPerg,.T.)			
		cDataDe  	:= Dtos(MV_PAR01)
		cDataAte 	:= Dtos(MV_PAR02)
		cBaseDe	 	:= MV_PAR03
		cBaseAte 	:= MV_PAR04
		cItemDe  	:= MV_PAR05
		cItemAte 	:= MV_PAR06
		cTipoDe  	:= MV_PAR07
		cTipoAte 	:= MV_PAR08
		lCtb 		:= MV_PAR09 == 1
		lMostra		:= MV_PAR10 == 1
		lAglutina	:= MV_PAR11 == 1
		lSelFil		:= MV_PAR12 == 2
		cFilde		:= MV_PAR13
		cFilAte		:= MV_PAR14

		If Empty(cDataDe) .or. Empty(cDataAte) 
			Help(" ",1,"AF210ESTDTVAZ",,STR0024,1,0,,,,,,{STR0025}) //"Data não pode ser vazio"#"Informar data válida após última depreciação"
			Return
		EndIf
		If LockByName("ATFA210EST", .T., .F.)
			If lSelFil  .And. !Empty(xFilial("SN1"))// Seleciona filiais
				Afa210Fil(lAutomato)
			Else
				Iif(lAutomato,AF210ExEst(),Processa({ || AF210ExEst(),,STR0026 }))
			EndIf
			UnlockByName("ATFA210EST", .T., .F.) //Efetuo a liberação do lock após o término do processamento  
		Else
			MsgInfo(STR0039,STR0040 ) //"Estorno já em execução, tente novamente em alguns instantes"#"Atenção"
		EndIf
	EndIf
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  Afa210Fil ºAutor  ³Totvs    			 º Data ³  08/01/2026 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa o processamento para cada filial                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ ATFA210            	                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Afa210Fil(lAutomato)

Local cFilIni 	:= cFilAnt      as character
Local aArea		:= GetArea()    as character
Local nInc		:= 0            as numeric
Local aSM0		:= AdmAbreSM0() as array

Default lAutomato := .F.

For nInc := 1 To Len( aSM0 )
	If aSM0[nInc][1] == cEmpAnt .AND. cFilDe <= aSM0[nInc][2] .AND. aSM0[nInc][2] <= cFilAte
		cFilAnt := aSM0[nInc][2] 
		Iif(lAutomato,AF210ExEst(),Processa({ || AF210ExEst(),,STR0027+cFilAnt })) //"Executando estorno filial: "
	EndIf
Next

cFilAnt := cFilIni
RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  AF210ExEst ºAutor  Ewerton Franklin	 º Data ³  08/01/2026 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Executa o processamento de estorno                          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ ATFA210            	                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF210ExEst() as Logical

Local lRet		:= .T. as Logical
Local cBase     as Character
Local cItem		as Character
Local cTipo		as Character
Local cSeq		as Character
Local dDtIni	:= SuperGetMV("MV_ULTDEPR",.F.,STOD("19800101")) as Date
Local dDtFim	:= LastDay(dDtIni+1)  as Date
Local nQuant	:= 0  			as Numeric
Local cBem		:= "" 			as Character

Private cArquivo 	:= "" 		as Character
Private nHdlPrv		:= 0		as Numeric
Private cLoteAtf 	:= LoteCont("ATF") as Character
Private nTotal		:= 0		as Numeric
Private cPadraoEst  := "829"	as Character


If lRet .And. ( MV_PAR01 < dDtIni .or. MV_PAR01 > dDtFim) .or.  ( MV_PAR02 < dDtIni .or. MV_PAR02 > dDtFim)
	Help(" ",1,"AF210ESTDTINV",,STR0028,1,0,,,,,,{STR0029}) //"Data não permitida"#"Informar data válida após última depreciação"
	lRet := .F.
EndIf

If lRet
	AF210QRYES() //Executa Query retornando Alias cAliasQry em Static para validação

	(cAliasQry)->(DbGotop())
	While !(cAliasQry)->(EOF())
		cBase   := (cAliasQry)->N8_CBASE
		cItem	:= (cAliasQry)->N8_ITEM 
		cTipo	:= (cAliasQry)->N8_TIPO 
		cSeq	:= (cAliasQry)->N8_SEQ 	 

		cBem	:= cFilAnt+": "+cBase + "/ "+cItem + "/ "+cTipo  

		If (cAliasQry)->N8_QTDANT == 0
			Help(" ",1,"AF210QTD",,STR0030+cBem+STR0031,1,0,,,,,,{STR0032}) //"Ativo: "#" " está com Valor Anterior(N8_QTDANT) zerado. Este campo grava a quantidade antes do último inventário"#"Não será permitido estornar o ajuste contábil deste ativo!"
			lRet := .F.
			exit
		EndIf

		If lRet .and. VldEstMov((cAliasQry)->N8_FILIAL,cBase,cItem,cTipo,(cAliasQry)->N8_DTAJUST) //Valida movimentações realizadas após inventario
			Help(" ",1,"AF210MOV",,STR0030+cBem+STR0033,1,0) //"Ativo: "#" possui movimentação após o inventário, não é possível realizar o estorno. Processo será abortado!"
			lRet := .F.
			exit
		Endif

		If lRet .and. VerSN8Rep((cAliasQry)->N8_FILIAL,cBase,cItem,cTipo)
			Help(" ",1,"AF210SN8",,STR0030+cBem+STR0041,1,0,,,,,,{STR0042}) //"Ativo: "#" já possui cadastro de Inventário em Aberto. Não é permitido mais de um inventário em aberto para o mesmo bem!# Verifique a possibilidade de exclusão do inventário em aberto, para que possa estornar este movimento.
			lRet := .F.
			exit
		EndIf

		If lRet
			dbSelectArea("SN1")
			SN1->(dbSetOrder(1))
			If SN1->(dbSeek(xFilial("SN1")+cBase+cItem))
				If !Empty(SN1->N1_DTBLOQ) .and. SN1->N1_DTBLOQ >= dDataBase
					Help(" ",1,"AF210BLOQ",,STR0030+cBem+STR0034,1,0)   //"Ativo: "#" bloqueado, não pode sofrer movimentações. Processo será abortado!"
					lRet := .F.
					exit
				EndIf
				If SN1->N1_STATUS $ "2|3"
					Help(" ",1,"AF210BLQ",,STR0030+cBem+STR0035,1,0)   //"Ativo: "#" bloqueado por usuário ou local, nao poder sofrer movimentações. Processo será abortado!"
					lRet := .F.
					exit
				EndIf
			Else
				lRet := .F.
			EndIf
		EndIf 
		(cAliasQry)->(DbSkip())
	EndDo

	If oQueryQry <> Nil
		oQueryQry:Destroy()
		oQueryQry := Nil
		FreeObj(oQueryQry)
	EndIf
	
	If oQuerySN8 <> Nil
		oQuerySN8:Destroy()
		oQuerySN8 := Nil
		FreeObj(oQuerySN8)
	EndIf

	If lRet
		(cAliasQry)->(DbGotop())

		If ( VerPadrao(cPadraoEst)  .and. lCtb )
			nHdlPrv := HeadProva(cLoteAtf,"ATFA210",Substr(cUsername,1,6),@cArquivo)
		EndIf

		Begin Transaction
			While !(cAliasQry)->(EOF())
				nQuant++
				cBase   := (cAliasQry)->N8_CBASE
				cItem	:= (cAliasQry)->N8_ITEM 
				cTipo	:= (cAliasQry)->N8_TIPO 
				cSeq	:= (cAliasQry)->N8_SEQ 	 

				cBem	:= cFilAnt+": "+ " "+cBase + "/ "+cItem + "/ "+cTipo  				
				
				dbSelectArea("SN3")
				dbSetOrder(1)
				If SN3->(dbSeek(FWxFilial("SN3")+cBase+cItem+cTipo+"0"+cSeq))		
					Processa({|| lRet := AF210EfEst(FWxFilial("SN3"),cBase,cItem,cTipo,cSeq,(cAliasQry)->N8_DTAJUST,(cAliasQry)->REC)},,STR0036)//"Aguarde, processando estorno."
					If !lRet
						Help(" ",1,"AF210EfEst",,STR0037+cBem,1,0) //"Não foi possível realizar o estorno do bem: "
						DisarmTransaction()
						exit
					EndIf
				Else
					Help(" ",1,"AF210NOSN3",,STR0008+cBem,1,0)//"Não existe ficha ativa para esse código, verifique o cadastro de ativo."
					DisarmTransaction()		
					lRet := .F.			
					exit
				EndIf 
				(cAliasQry)->(DbSkip())
			EndDo
		End Transaction	
		
		If lRet
			If nHdlPrv > 0 .And. ( nTotal > 0 ) .and. lCtb 
				RodaProva(nHdlPrv, nTotal)
				cA100Incl(cArquivo,nHdlPrv,3,cLoteAtf,lMostra,lAglutina)
			EndIf	
		EndIf
	EndIf
	(cAliasQry)->(DbCloseArea())
EndIf 


Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  AF210EfEst ºAutor  Ewerton Franklin	 º Data ³  08/01/2026 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     Efetiva o estorno     					                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ ATFA210            	                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AF210EfEst(cFil as character, cBase as character, cItem as character, cTipo as character, cSeq as character,cData as character,nRecSn8 as numeric)  as Logical

Local nQuantas 	  := AtfMoedas()   						as numeric
Local aVlrAjust   := AtfMultMoe(,,{|x| 0})              as array
Local nX		  := 1									as numeric
Local cMoedaAtf   := SuperGetMv("MV_ATFMOED",.F.,"1")	as character
Local lRet		  := .T.								as logical

Default cFil 	:= cFilAnt
Default cBase 	:= ""
Default cItem	:= ""
Default cTipo	:= ""
Default cSeq	:= ""
Default cData	:= ""
Default nRecSn8	:= 0

SN8->(DbGoto(nRecSn8))

DbSelectArea("SN1")
SN1->(DbSetOrder(1))
SN1->(DbSeek(cFil+cBase+cItem))

DbSelectArea("SN3")
SN3->(DbSetOrder(1))
SN3->(dbSeek(FWxFilial("SN3")+cBase+cItem+cTipo+"0"+cSeq))

SN4->(dbSetOrder(9)) //N4_FILIAL, N4_CBASE, N4_ITEM, N4_TIPO, N4_SEQREAV, N4_SEQ, N4_DATA, N4_OCORR, R_E_C_N_O_, D_E_L_E_T_
If SN4->(dbSeek(FwxFilial("SN4") + SN3->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQREAV+N3_SEQ)+cData+"13" ))

	aVlrAjust[1]:= SN4->N4_VLROC1 
	
	If ( VerPadrao(cPadraoEst)  .and. lCtb )
		nTotal += DetProva(nHdlPrv,cPadraoEst,"ATFA210",cLoteAtf)
	EndIf

	For nX := 2 to nQuantas
		cMoed	:= Alltrim(Str(nX))
		aVlrAjust[nX] := Round(SN4->&("N4_VLROC"+cMoed),X3Decimal("N4_VLROC"+cMoed))
	Next

	SN4->(dbSetOrder(9)) 
	If  SN4->(dbSeek(FwxFilial("SN4") + SN3->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQREAV+N3_SEQ)+cData+"13" ))
		While SN4->(!EOF()) .And. SN4->(N4_FILIAL+N4_CBASE+N4_ITEM+N4_TIPO+N4_SEQREAV+N4_SEQ+Dtos(N4_DATA)+N4_OCORR) == FwxFilial("SN4") + SN3->(N3_CBASE+N3_ITEM+N3_TIPO+N3_SEQREAV+N3_SEQ)+cData+"13"
			RecLock("SN4",.F.)
			SN4->(dbDelete())
			MsUnLock()
			SN4->(dbSkip())
		EndDo
	EndIf

	Reclock("SN3",.F.)
		//********************************
		// Controle de multiplas moedas  *
		//********************************
		AtfMultMoe("SN3","N3_VRDACM",{|x| SN3->&(If(x>9,"N3_VRDAC","N3_VRDACM")+Alltrim(Str(x))) - aVlrAjust[x] })
		AtfMultMoe("SN3","N3_VRDBAL",{|x| SN3->&(If(x>9,"N3_VRDBA","N3_VRDBAL")+Alltrim(Str(x))) - aVlrAjust[x] })
		AtfMultMoe("SN3","N3_VRDMES",{|x|SN3->&("N3_VRDMES"+Alltrim(Str(x))) - aVlrAjust[x] })

		If SN8->N8_VLRINV == 0 
			SN3->N3_FIMDEPR := ctod("")
		EndIf
	MsUnlock()

	//Atualiza SN5
	ATFSALDO(	SN3->N3_CCDEPR ,SN8->N8_DTAJUST,"P",aVlrAjust[1],aVlrAjust[2],aVlrAjust[3],aVlrAjust[4],aVlrAjust[5],"-",&('SM2->M2_MOEDA'+cMoedaAtf), SN3->N3_SUBCCDE,,SN3->N3_CLVLCDE,SN3->N3_CCCDEP ,"4", aVlrAjust )	
	ATFSALDO(	SN3->N3_CDEPREC,SN8->N8_DTAJUST,"P",aVlrAjust[1],aVlrAjust[2],aVlrAjust[3],aVlrAjust[4],aVlrAjust[5],"-",&('SM2->M2_MOEDA'+cMoedaAtf), SN3->N3_SUBCDEP,,SN3->N3_CLVLDEP,SN3->N3_CCDESP ,"3", aVlrAjust )

	Reclock("SN1",.F.)
		SN1->N1_QUANTD := SN8->N8_QTDANT 
	MsUnlock()

	Reclock("SN8",.F.)
		SN8->N8_DTAJUST := ctod("")
		SN8->N8_QTDANT	:= 0
	MsUnlock()

Else
	Help(" ",1,"AF210EfEstSN4",,STR0038+cFilAnt+": "+cBase+"/"+cItem+"/"+ cTipo,1,0)//"Não foi possível realizar o estorno, pois não foi encontrado movimentação(SN4) do bem: "
	lRet := .F.
Endif

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  AF210QRYES ºAutor  Ewerton Franklin	 º Data ³  08/01/2026 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     QUERY PARA SALVAR ALIAS				                      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ ATFA210            	                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function AF210QRYES() 

	Local cQry			as Character
	Local nSeq			as Numeric
	Local oQryExec		as Object

	nSeq		:= 1

	cQry	:= " SELECT N8_FILIAL,N8_CBASE,N8_ITEM , N8_TIPO, N8_DTINV, N8_HISTORI,	N8_DTAJUST,	N8_VLRINV, N8_QTDINV, N8_SEQ , N8_QTDANT, R_E_C_N_O_ as REC "
	cQry	+= " FROM "+ RetSqlName("SN8") +" N8 "
	cQry	+= " WHERE "
	cQry	+= " N8.N8_FILIAL = ? "
	cQry	+= " AND N8.N8_CBASE BETWEEN  ? AND ? "
	cQry	+= " AND N8.N8_ITEM  BETWEEN  ? AND ? "
	cQry	+= " AND N8.N8_TIPO  BETWEEN  ? AND ? "
	cQry	+= " AND N8.N8_DTAJUST  BETWEEN  ? AND ? "
	cQry	+= " AND N8.D_E_L_E_T_ = ? "
	cQry 	:= ChangeQuery(cQry)

	oQryExec := FwExecStatement():New(cQry)

	oQryExec:SetString(nSeq++,		FwXFilial('SN8'))
	oQryExec:SetString(nSeq++,		cBaseDe)
	oQryExec:SetString(nSeq++,		cBaseAte)
	oQryExec:SetString(nSeq++,		cItemDe)
	oQryExec:SetString(nSeq++,		cItemAte)
	oQryExec:SetString(nSeq++,		cTipoDe)
	oQryExec:SetString(nSeq++,		cTipoAte)
	oQryExec:SetString(nSeq++,		cDataDe)
	oQryExec:SetString(nSeq++,		cDataAte)
	oQryExec:SetString(nSeq++,		Space(1))
	
	cAliasQry := oQryExec:OpenAlias(GetNextAlias())

	oQryExec:Destroy()
	oQryExec := Nil
	FreeObj(oQryExec)
Return 



/* {Protheus.doc}
	VldEstMov - Funcao que verifica se existe qualquer tipo de movimento ou depreciacao
	no bem posicionado para possibilidade de estorno do inventário

	@author Totvs
	@since 
	@param return as logical 
	@version P12
*/
Static Function VldEstMov(cfil as Character ,cCodBase as Character, cCodItem as Character, cTipo as Character,cData as Charater) as Logical

	Local cQry			as Character
	Local lRetQry		as Logical
	Local nSeq			as Numeric

	Default cFil 	:= cFilAnt
	Default cCodBase:= ""
	Default cCodItem:= ""
	Default cTipo	:= ""
	Default cData	:= ""

	lRetQry			:= .T.
	nSeq			:= 1

	If oQueryQry == Nil
		cQry	:= " SELECT Count(N4_CBASE) MOV "
		cQry	+= " FROM "+ RetSqlName("SN4") +" SN4 "
		cQry	+= " WHERE "
		cQry	+= " SN4.N4_FILIAL = ? "
		cQry	+= " AND SN4.N4_CBASE = ? "
		cQry	+= " AND SN4.N4_ITEM = ?"
		cQry	+= " AND SN4.N4_TIPO = ?"
		cQry	+= " AND SN4.N4_DATA >= ?"
		cQry	+= " AND SN4.N4_OCORR not in (?)  " //Casos de depreciacoes ou movimentos
		cQry	+= " AND NOT (SN4.N4_ORIGEM = ? AND SN4.N4_OCORR in (?))
		cQry	+= " AND SN4.D_E_L_E_T_ = ? "
		cQry 	:= ChangeQuery(cQry)
		oQueryQry := FwExecStatement():New(cQry)
	EndIf

	oQueryQry:SetString(nSeq++,		cfil)
	oQueryQry:SetString(nSeq++,		cCodBase)
	oQueryQry:SetString(nSeq++,		cCodItem)
	oQueryQry:SetString(nSeq++,		cTipo)
	oQueryQry:SetString(nSeq++,		cData)
	oQueryQry:Setin(nSeq++,		{'13','05'}) 
	oQueryQry:SetString(nSeq++,		'ATFA012')
	oQueryQry:Setin(nSeq++,		{'06','20'}) 
	oQueryQry:SetString(nSeq++,		Space(1))
	
	lRetQry := oQueryQry:ExecScalar('MOV') > 0 


Return lRetQry



/* {Protheus.doc}
	VerSN8Rep - Funcao que verifica se existe o mesmo bem em aberto
	Objetivo desta validação é que evite dois movimentos na SN8 em aberto, e assim evitar que ao 
	executar a ATFA220 execute mais de 1 inventário no mesmo mês
	@author Ewerton Franklin
	@since 
	@param return as logical 
	@version P12
*/
Static Function VerSN8Rep(cfil as Character ,cCodBase as Character, cCodItem as Character, cTipo as Character) as Logical

	Local cQry			as Character
	Local lRetQry		as Logical
	Local nSeq			as Numeric

	Default cFil 	:= cFilAnt
	Default cCodBase:= ""
	Default cCodItem:= ""
	Default cTipo	:= ""

	lRetQry			:= .T.
	nSeq			:= 1

	If oQuerySN8 == Nil
		cQry	:= " SELECT Count(N8_CBASE) QTD "
		cQry	+= " FROM "+ RetSqlName("SN8") +" SN8 "
		cQry	+= " WHERE "
		cQry	+= " SN8.N8_FILIAL = ? "
		cQry	+= " AND SN8.N8_CBASE = ? "
		cQry	+= " AND SN8.N8_ITEM = ?"
		cQry	+= " AND SN8.N8_TIPO = ?"
		cQry	+= " AND SN8.N8_DTAJUST = ?"
		cQry	+= " AND SN8.D_E_L_E_T_ = ? "
		cQry 	:= ChangeQuery(cQry)
		oQuerySN8 := FwExecStatement():New(cQry)
	EndIf

	oQuerySN8:SetString(nSeq++,		cfil)
	oQuerySN8:SetString(nSeq++,		cCodBase)
	oQuerySN8:SetString(nSeq++,		cCodItem)
	oQuerySN8:SetString(nSeq++,		cTipo)
	oQuerySN8:SetString(nSeq++,		Space(1))
	oQuerySN8:SetString(nSeq++,		Space(1))
	
	lRetQry := oQuerySN8:ExecScalar('QTD') > 0 


Return lRetQry
