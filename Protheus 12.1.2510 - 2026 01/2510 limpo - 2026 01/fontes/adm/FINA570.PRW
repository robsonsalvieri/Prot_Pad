#INCLUDE "FINA570.CH" 
#INCLUDE "PROTHEUS.CH"

STATIC __lProc := .F.
STATIC __lAuto := .F.

/*/


Ŀ
Funo     FINA570   Autor  Leonardo Ruben         Data  19.06.00 
Ĵ
Descrio  Recalculo dos Saldos dos Caixinhas                         
Ĵ
Sintaxe e  Void FINA570(void)                                         
Ĵ
 Uso       Localizacoes                                               
ٱ


/*/
Function FINA570(lAutomato AS LOGICAL, nAutoOpca AS NUMERIC) 
//Ŀ
// Define Variaveis                                             
//
Local cKey, cCondicao, nIndex
Local aArea := GetArea()
Local lPergunte := .F.
Local lPanelFin := IsPanelFin()
Local lFilComp := FWModeAccess("SET",3, cEmpAnt) == "C"
Local lEmpComp := FWModeAccess("SET",1, cEmpAnt) == "C"
LOCAL lReturn := .F.
LOCAL aGetFil := {}
LOCAL nI := 0

DEFAULT lAutomato := .F.
DEFAULT nAutoOpca := 1

__lAuto := lAutomato

Private aSelFil := {}
Private cIndice := NIL
Private cIndex  := NIL

//Ŀ
// Parametros do Grupo FIA570 no SX1                          
// De Caixinha          mv_par01                              
// A Caixinha           mv_par02                              
//
//Ŀ
// Carrega funcao Pergunte									   
//
If xFilial("FIJ") <> xFilial("SET")
	//###Reclculo do caixinha###
    //###O compartilhamento das tabelas FIJ e SET esto diferentes. Acesse o Configurador > Base de Dados e ajuste o compartilhamento.###
    Help("",1,"HELP",STR0022,STR0008,1,0)
	Return
EndIf

SetKey (VK_F12,{|a,b| AcessaPerg("FIA570",.T.)})

IF lPanelFin
    lPergunte := PergInPanel("FIA570",.T.)
ELSE
    lPergunte := Pergunte("FIA570",!__lAuto)
ENDIF

If !lPergunte
	Set Key VK_F12 To
	Return
EndIf

//Ŀ
// Cria indice condicianal                               
//
cCondicao := 'ET_CODIGO>="'+mv_par01+'".AND.ET_CODIGO<="'+mv_par02+'"'
cCondicao := cCondicao + '.AND.ET_SITUAC=="0"'	// Somente caixinha com situacao "Aberto"

cIndex  := CriaTrab(Nil,.F.)
cKey    := "ET_FILIAL+ET_CODIGO"
IndRegua("SET",cIndex,cKey,,cCondicao,OemToAnsi(STR0001))  //"Un Momento por favor..."
nIndex  := RetIndex("SET")
dbSelectArea("SET")
dbSetOrder(nIndex+1)
dbGoTop()

//Ŀ
// Monta tela para chamada da funcao de processamento    
//
nOpca := IF(__lAuto,nAutoOpca,2)
IF EMPTY(__lAuto)
    If lPanelFin  //Chamado pelo Painel Financeiro
        aSays := {}
        AADD(aSays,OemToAnsi( STR0003 ) ) //"Este programa recalcula os saldos dos Caixinhas"
        AADD(aSays,OemToAnsi( STR0004 ) ) //"em aberto, considerando os comprovantes de reembolso"
        AADD(aSays,OemToAnsi( STR0005 ) ) //"e de adiantamento."

        aButtonTxt := {}
        AADD(aButtonTxt,{STR0007,STR0007, {||Pergunte("FIA570",.T. )}}) // Parametros
        FaMyFormBatch(aSays,aButtonTxt,{||nOpca :=1},{||nOpca:=0})
    Else
        DEFINE MSDIALOG oDlg FROM  96,9 TO 310,392 TITLE OemToAnsi(STR0002) PIXEL	 //"Reclculo dos Saldos dos Caixinhas"
        @ 18, 6 TO 66, 187 LABEL "" OF oDlg  PIXEL
        @ 29, 15 SAY OemToAnsi(STR0003) SIZE 168, 8 OF oDlg PIXEL					 //"Este programa recalcula os saldos dos Caixinhas"
        @ 38, 15 SAY OemToAnsi(STR0004) SIZE 168, 8 OF oDlg PIXEL					 //"em aberto, considerando os comprovantes de reembolso"
        @ 48, 15 SAY OemToAnsi(STR0005) SIZE 168, 8 OF oDlg PIXEL	                 //"e de adiantamento."
        DEFINE SBUTTON FROM 80, 093 TYPE 5 ACTION Pergunte("FIA570",.T.) ENABLE OF oDlg
        DEFINE SBUTTON FROM 80, 123 TYPE 1 ACTION (oDlg:End(),nOpca:=1) ENABLE OF oDlg
        DEFINE SBUTTON FROM 80, 153 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
        ACTIVATE MSDIALOG oDlg CENTERED
    EndIf
ENDIF
// ---------------------------------------------------------------------------------------------

IF nOpca == 1
    aSelFil := {XFILIAL("SEU",cFilAnt)}
	IF mv_par04 = 1 .and. lFilComp // considera outras filiais se SET compartilhada
        aGetFil := AdmGetFil(.T.,!lEmpComp,"SET",!lEmpComp,!lAutomato/*lHelp*/,!lAutomato/*lExibeTela*/)
        FOR nI := 1 TO LEN(aGetFil)
            IF EMPTY(ASCAN(aSelFil,{|e| e == FWxFilial("SEU",aGetFil[nI]) }))
                AADD(aSelFil,FWxFilial("SEU",aGetFil[nI]))
            ENDIF
        NEXT nI

        lReturn := EMPTY(aSelFil)
    ENDIF
    IF EMPTY(lReturn)
        //"Reclculo dos Saldos dos Caixinhas"###"Efetuando Reclculo dos Saldos dos Caixinhas..."
        Processa({|lEnd| FA570Process(@lEnd)},STR0002,STR0006,.F.)
    ENDIF
ENDIF

dbSelectArea("SET")
dbClearFil()
RetIndex("SET")

RestArea(aArea)

Set Key VK_F12 To

If lPanelFin  //Chamado pelo Painel Financeiro
	dbSelectArea(FinWindow:cAliasFile)
	ReCreateBrow(FinWindow:cAliasFile,FinWindow)
	INCLUI := .F.
	ALTERA := .F.
EndIf

RestStatic()

Return

/*


Ŀ
Funo    FA570Process Autor  Leonardo Ruben         Data 19/06/00
Ĵ
Descrio  Processa o recalculo dos saldos dos caixinhas              
Ĵ
 Uso       FINA570                                                    
ٱ


*/
Static Function FA570Process(lEnd)
Local aArea 	:= GetArea()
Local aGrvSET 	:= {}

dbSelectArea("SET")
While !Eof()  // varre o filtro

	If SET->ET_FILIAL <> FWxFilial("SET")
		SET->(dbSkip())
		Loop
	EndIf 

	aGrvSET := Fa570AtuSld(SET->ET_CODIGO)
	If aGrvSET[1][1] >= 0 .And. !aGrvSET[1][2]
		dbSelectArea("SET")
		SET->(RecLock("SET",.F.))		
		SET->ET_SALDO := aGrvSET[1][1]
		SET->(MsUnlock())
		__lProc := .T.
	Endif	
	dbSkip()
EndDo

If !IsBlind() .AND. EMPTY(__lAuto)
	If __lProc
		MsgInfo(STR0010,STR0009)
	Else
		F570Help()
	EndIf
EndIf

RestArea(aArea)

Return

/*


Ŀ
Funo     Fa570Comp   Autor  Leonardo Ruben         Data 19/06/00
Ĵ
Descrio  Retorna o valor total dos comprovantes de um caixinha      
Ĵ
 Uso       FINA570                                                    
ٱ


*/
Function Fa570Comp(cCaixa As Character) As Array
Local nTotComp 	As Numeric
Local nTotRep 	As Numeric
Local cSeqCxa	As Character
Local lF570Cal 	As Logical

Local lRepCxV	As Logical   // Verifica se trabalha com reposicao variavel
Local nSldCx	As Numeric
Local nSldAnt	As Numeric
Local nSldVlr	As Numeric
Local lF570SdCx As Logical
Local lF570Rec  As Logical
Local cQuery    As Character
Local cAliasTmp As Character
Local cSEUFil	As Character
Local cAliasWrk As Character
Local lRegIni   As Logical // Identifica primeiro registro da tabela
Local nX 		As Numeric
Local lFINCTAL	As Logical	// Despesa Necessita ser aprovada
Local lFJIMCFIL As Logical
Local cFilFIJ	As Character
Local cFilComp  As Character
Local lGrava    As Logical
Local lFirst	As Logical
Local aGrvSld   As Array
Local aGrvSET   As Array
Local aAreaSEU  As Array
Local aArea     AS Array
Local nZ 		As Numeric
Local cAliasFIJ As Character
Local lLimpaFIJ As Logical
Local nPos		As Numeric
Local oObjSEU 	As Object 
Local cFilSA6   As Character
Local nMoeBco   As Numeric

nTotComp 	:= 0
nTotRep 	:= 0
lF570Cal 	:= ExistBlock("F570CAL")
lRepCxV		:= GetNewPar("MV_RPCXMN","2")== "1" .And. ExistBlock("F550VREP")   // Verifica se trabalha com reposicao variavel
nSldCx		:= 0
nSldAnt		:= 0
nSldVlr		:= 0
lF570SdCx 	:= ExistBlock("F570SDCX")
lF570Rec  	:= ExistBlock("F570REC")
cQuery      := ""
cAliasTmp   := ""
cSEUFil		:= ""
cAliasWrk 	:= ""
lRegIni  	:= .t. // Identifica primeiro registro da tabela
nX 			:= 1
lFINCTAL	:= SuperGetMV("MV_FINCTAL", .T., "1") == "2"	// Despesa Necessita ser aprovada
lFJIMCFIL 	:= FWMODEACCESS("FIJ",3) == 'C'
cFilFIJ		:= xFilial( "FIJ" )
cFilComp  	:= Space(Len(xFilial("SET")))
lGrava    	:= .F.
lLimpaFIJ 	:= .F.
aGrvSld   	:= {}
aGrvSET   	:= {}
aAreaSEU  	:= {}
aArea       := {}
nZ 			:= 1
cAliasFIJ 	:= ""
lFirst		:= .T.
nPos		:= 0
oObjSEU     := Nil
cFilSA6     := xFilial("SA6")
nMoeBco     := 0

Private lFxMultSld := FXMultSld() //Controla saldo bancario multiplas naturezas, verifica a existencia dos campos A6_MOEDA e a tabela FIJ (Paises BRA|PER|BOL)
//Ŀ
//Posiciona-se no caixinha                               
//
dbSelectArea("SET")
dbSetOrder(1)
dbSeek( xFilial()+cCaixa)
dUltRep := SET->ET_ULTREP
nSldAnt	:= SET->ET_SALANT
nSldVlr := SET->ET_VALOR

//Ŀ
//Retorna a sequencia atual do caixinha                  
//
cSeqCxa := Fa570SeqAtu( cCaixa)


If Type( "aSelFil" ) <> "A"
	aSelFil := {xFilial( "SEU" , cFilAnt ) }
EndIf

For nX := 1 To Len(aSelFil)
	cSEUFil += "'" + FWxFilial("SEU",aSelFil[nX] ) + "',"
Next Nx
cSEUFil := SubStr(cSEUFil,1,Len(cSEUFil)-1)
cQuery := " SELECT EU_FILIAL, EU_TIPO, EU_DTDIGIT, EU_CAIXA, EU_SEQCXA, EU_NROADIA, EU_VALOR, EU_STATUS, EU_CODAPRO, R_E_C_N_O_ "
cQuery += " FROM " + RetSqlName( "SEU" )
cQuery += " WHERE EU_FILIAL IN ("+cSEUFil+") "
cQuery += " AND EU_CAIXA = '"+cCaixa+"' "
cQuery += " AND EU_SEQCXA < '"+cSeqCxa+"' "
cQuery += "   AND D_E_L_E_T_ = ' ' "
cQuery += " ORDER BY EU_FILIAL, EU_SEQCXA, R_E_C_N_O_ "
cQuery := ChangeQuery(cQuery)
cAliasWrk := GetNextAlias()
dbUseArea(.T., "TOPCONN", TcGenQry(,,cQuery), cAliasWrk, .T., .T.)
													
cAliasTmp := cAliasWrk

lFirst	:= .T.

While (cAliasTmp)->(!Eof())

	If lFxMultSld
		If lFJIMCFIL 
			cFilFIJ := xFilial('FIJ', (cAliasTmp)->EU_FILIAL)
		EndIf

		If lFJIMCFIL .and. lGrava

			If (nPos := Ascan(aGrvSld,{|x| x[1] + x[2]== cFilComp + (cAliasTmp)->EU_CAIXA })) = 0
				AADD(aGrvSld,{cFilComp,(cAliasTmp)->EU_CAIXA,nTotRep})
			Else
				aGrvSld[nPos][3] += nTotRep
			EndIf
			nSldCx := 0
			lGrava := .F.

		EndIf 	

		If ( cFilFIJ <> cFilComp .Or. Empty(AllTrim(cFilComp)) ) .And. lFirst
			//**************************************************
			// Deleta todas os Movimentos de Saldo do Caixinha *
			//**************************************************
			lFirst	:= .F.
			lLimpaFIJ := .T.

			aAreaSEU := (cAliasTmp)->(GetArea())
	
			cQuery := " SELECT FIJ_FILIAL, FIJ_CODCX, FIJ_DATA, R_E_C_N_O_ "
			cQuery += " FROM " + RetSqlName( "FIJ" )
			cQuery += " WHERE "
			cQuery += "FIJ_FILIAL = '" + cFilFIJ + "' AND "
			cQuery += "FIJ_CODCX = '" + cCaixa + "' AND "
			If MV_PAR03 = 1
				cQuery +=  "FIJ_DATA >= '" + DtoS(dUltRep) + "' AND "
			EndIf
			cQuery += "D_E_L_E_T_ = ' ' "

			cQuery := ChangeQuery(cQuery)
			cAliasFIJ := GetNextAlias()
			dbUseArea(.T., "TOPCONN", TcGenQry(,,cQuery), cAliasFIJ, .T., .T.)

			(cAliasFIJ)->( dbGoTop() )

			dbSelectArea("FIJ")

			While (cAliasFIJ)->(!Eof())
				FIJ->( dbGoTo((cAliasFIJ)-> R_E_C_N_O_))
				FIJ->(RecLock("FIJ",.F.))
				FIJ->FIJ_SALDO := 0
				FIJ->(MsUnlock())
				(cAliasFIJ)->( dbSkip() )
			EndDo

			(cAliasFIJ)->( dbCloseArea() )
			cFilComp := cFilFIJ

			RestArea(aAreaSEU)
		EndIf
	EndIf

	If (cAliasTmp)->EU_TIPO == "10" .or. (cAliasTmp)->EU_TIPO == "92"

		nTotRep := (cAliasTmp)->EU_VALOR
		 
		If lFxMultSld
			// Cria saldo inicial de reposicao
			If lRepCxV						
				If lRegIni
					AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , (cAliasTmp)->EU_VALOR, .T. , cFilFIJ )
				Else
					AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , (cAliasTmp)->EU_VALOR, .F. , cFilFIJ )
				EndIf
			Else		
				AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , (cAliasTmp)->EU_VALOR , lRegIni , cFilFIJ )
			EndIf
		EndIf
					
		lRegIni := .F.
		lGrava 	:= .T.
		nSldCx += nTotRep
		(cAliasTmp)->( dbSkip() )
		Loop
	ElseIf (cAliasTmp)->EU_TIPO == "11"


		// Zera Saldo do Caixinha  
		If lFxMultSld
			AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)), (cAliasTmp)->EU_VALOR * (-1) , .F. )
		EndIf
		nTotComp += (cAliasTmp)->EU_VALOR
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf lFINCTAL .AND. (!Empty((cAliasTmp)->EU_CODAPRO) .AND. (cAliasTmp)->EU_STATUS $ "01|02|04")
		// Despesa ainda no aprovada ou bloqueada
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf (cAliasTmp)->EU_TIPO == "00" .AND. !Empty((cAliasTmp)->EU_NROADIA)
		//Ŀ
		//Se o tipo for despesa e representar prestacao de contas
		//de adiantamento, devo ignorar pois neste caso somente  
		//os movimentos do tipo 01(Adiantamento)  e 02(devolucao 
		//de adiantamento devem ser considerados.                
		//
		(cAliasTmp)->(dbSkip())
		Loop	
	ElseIf (cAliasTmp)->EU_TIPO == "02" 

		//*************************************
		// Reposicao de saldo do adiantamento *
		//*************************************	
		aArea := GetArea()
		SEU->(dbSetOrder(1))
		dbSelectArea("SEU")			

		If SEU->(dbSeek(xFilial("SEU")+(cAliasTmp)->EU_NROADIA)) //Posiciona na Despesa 			
			If (SEU->(dbSeek(xFilial("SEU")+SEU->EU_NROADIA)) .And. SEU->EU_TIPO <> "01") .or. (Empty(SEU->EU_NROADIA) .AND. SEU->EU_TIPO <> "01") //Caso for despesa de prestao de contas, Posiciona no Adiantamento, caso exista
				If lFxMultSld
					AtuSalCxa(cCaixa, IIf(cAliasTmp == "TMPSEU", (cAliasTmp)->EU_DTDIGIT, StoD((cAliasTmp)->EU_DTDIGIT)), (cAliasTmp)->EU_VALOR , , cFilFIJ )					
				EndIf
				nTotComp -= (cAliasTmp)->EU_VALOR
			Endif
		EndIf
		RestArea(aArea)		
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf (cAliasTmp)->EU_TIPO>="90"
		//Ŀ
		//Reposicoes canceladas, aguardando autorizacao, cheques 
		//aguardando compensacao etc.                            
		//
		(cAliasTmp)->(dbSkip())
		Loop
	EndIf

	//***************************
	// Despesas ou adiantamento *
	//***************************
	If lFxMultSld
		AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , (cAliasTmp)->EU_VALOR*(-1) , , cFilFIJ )
	EndIf
	//Ŀ
	//Aqui estao sendo somados   :                           
	//- Gastos/despesas em aberto (tipo 00)                  
	//- Adiantamentos fechados/rendidos (tipo 01)            
	//
	nTotComp += (cAliasTmp)->EU_VALOR

	(cAliasTmp)->(dbSkip())
EndDo
If Select(cAliasTmp)>0
	(cAliasTmp)->(dbclosearea())
EndIf

cQuery := " SELECT EU_FILIAL, EU_TIPO, EU_DTDIGIT, EU_CAIXA, EU_SEQCXA, EU_NROADIA, EU_VALOR, EU_BAIXA, EU_STATUS, EU_CODAPRO, EU_MOEDA, EU_VLMOED2, R_E_C_N_O_ "
cQuery += " FROM " + RetSqlName( "SEU" )
cQuery += " WHERE EU_FILIAL IN (?) "
cQuery += " AND EU_CAIXA = ? "
cQuery += " AND EU_SEQCXA >= ? "
cQuery += " AND D_E_L_E_T_ = ' ' "
cQuery += " ORDER BY EU_FILIAL, EU_SEQCXA, EU_DTDIGIT, R_E_C_N_O_ "
cQuery  := ChangeQuery(cQuery)
oObjSEU := FwExecStatement():New(cQuery)

oObjSEU:SetIn(1, aSelFil )
oObjSEU:SetString(2, cCaixa)
oObjSEU:SetString(3, cSeqCxa)
cAliasTmp := oObjSEU:OpenAlias() 	

lFirst	:= .T.

While (cAliasTmp)->(!Eof())

	//Ponto de entrada para desconsiderar alguns movimentos para a recomposicao do saldo
	If lF570SdCx
		If ExecBlock("F570SDCX",.F.,.F.,{cAliasTmp})
			(cAliasTmp)->(dbSkip())
			Loop
		EndIf
	EndIf

	If lFJIMCFIL
		cFilFIJ := xFilial('FIJ', (cAliasTmp)->EU_FILIAL)
	Else
		cFilFIJ := xFilial('FIJ')
	EndIf

	If !lFxMultSld .And. cPaisLoc <> "BRA"
		cFilComp  := cFilFIJ
		If lFJIMCFIL 
			nTotRep   := nSldCx
		EndIf 
	EndIf 

	If lFJIMCFIL .and. lGrava 
		If (nPos := Ascan(aGrvSld,{|x| x[1] + x[2]== cFilComp + (cAliasTmp)->EU_CAIXA })) = 0
			AADD(aGrvSld,{cFilComp,(cAliasTmp)->EU_CAIXA,nTotRep})
		Else
			aGrvSld[nPos][3] += nTotRep
		EndIf

		nSldCx := 0
		lGrava := .F.
	EndIf

	If  !lLimpaFIJ .and. ( cFilFIJ <> cFilComp .Or. Empty(AllTrim(cFilComp)) ) .And. lFirst
		//**************************************************
		// Deleta todas os Movimentos de Saldo do Caixinha *
		//**************************************************
		lFirst	:= .F.

		aAreaSEU := (cAliasTmp)->(GetArea())

		cQuery := " SELECT FIJ_FILIAL, FIJ_CODCX, FIJ_DATA, R_E_C_N_O_ "
		cQuery += " FROM " + RetSqlName( "FIJ" )
		cQuery += " WHERE "
		cQuery += "FIJ_FILIAL = '" + cFilFIJ + "' AND "
		cQuery += "FIJ_CODCX = '" + cCaixa + "' AND "
		If MV_PAR03 = 1
			cQuery +=  "FIJ_DATA >= '" + DtoS(dUltRep) + "' AND "
		EndIf
		cQuery += "D_E_L_E_T_ = ' ' "

		cQuery := ChangeQuery(cQuery)
		cAliasFIJ := GetNextAlias()
		dbUseArea(.T., "TOPCONN", TcGenQry(,,cQuery), cAliasFIJ, .T., .T.)

		(cAliasFIJ)->( dbGoTop() )

		dbSelectArea("FIJ")

		While (cAliasFIJ)->(!Eof())
			FIJ->( dbGoTo((cAliasFIJ)-> R_E_C_N_O_))
			FIJ->(RecLock("FIJ",.F.))
			FIJ->FIJ_SALDO := 0
			FIJ->(MsUnlock())
			(cAliasFIJ)->( dbSkip() )
		EndDo

		(cAliasFIJ)->( dbCloseArea() )
		cFilComp := cFilFIJ

		RestArea(aAreaSEU)
	EndIf

	If (cAliasTmp)->EU_TIPO == "10" .or. (cAliasTmp)->EU_TIPO == "92"
		//Reposicao incial da ultima sequencia (abertura)   
		If lRepCxV
			nTotRep := (cAliasTmp)->EU_VALOR
			If lF570Rec
				nTotRep := ExecBlock( "F570REC", .f., .f., { (cAliasTmp)->EU_VALOR, SET->ET_SALANT, SET->ET_VALOR } )
			EndIf
			If lFxMultSld
				// Cria saldo inicial de reposicao 
				AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , nTotRep + SET->ET_SALANT, .T. , cFilFIJ)
			EndIf
		Else
			nTotRep := (cAliasTmp)->EU_VALOR
			If lF570Rec
				nTotRep := ExecBlock( "F570REC", .f., .f., { (cAliasTmp)->EU_VALOR, SET->ET_SALANT, SET->ET_VALOR } )
			EndIf
			If lFxMultSld
				// Cria saldo inicial de reposicao 
				AtuSalCxa( cCaixa, SET->ET_ULTREP , nTotRep , .F., cFilFIJ )
			EndIf
		EndIf

		lGrava :=  .T.
		nSldCx += nTotRep
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf (cAliasTmp)->EU_TIPO == "11"

		If lFxMultSld
			// Zera Saldo do Caixinha  
			AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)), (cAliasTmp)->EU_VALOR * (-1) , .T. )
		EndIf

		//Ŀ
		//fechou-se a sequencia (caixa fechado, pois esta   
		//eh a ultimia sequencia). Saldo zero.              
		//
		If lFxMultSld
			nTotRep -= (cAliasTmp)->EU_VALOR
		EndIf
		
		nTotComp += (cAliasTmp)->EU_VALOR
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf lFINCTAL .AND. (!Empty((cAliasTmp)->EU_CODAPRO) .AND. (cAliasTmp)->EU_STATUS $ "01|02|04")
		// Despesa ainda no aprovada ou bloqueada
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf (cAliasTmp)->EU_TIPO == "00" .AND. !Empty((cAliasTmp)->EU_NROADIA)
		//Ŀ
		//Se o tipo for despesa e representar prestacao de contas
		//de adiantamento, devo ignorar pois neste caso somente  
		//os movimentos do tipo 01(Adiantamento)  e 02(devolucao 
		//de adiantamento devem ser considerados.                
		//
		(cAliasTmp)->(dbSkip())
		Loop	
	ElseIf (cAliasTmp)->EU_TIPO == "02" 

		aArea := GetArea()
		SEU->(dbSetOrder(1))
		dbSelectArea("SEU")			

		If SEU->(dbSeek(xFilial("SEU")+(cAliasTmp)->EU_NROADIA)) //Posiciona na Despesa 			
			If cPaisLoc == "ARG" .And. (SEU->EU_TIPO == "01" .Or. (Empty(SEU->EU_NROADIA) .AND. SEU->EU_TIPO <> "01"))
				nTotComp -= (cAliasTmp)->EU_VALOR
			ElseIf cPaisLoc == "BRA" .And. SEU->EU_TIPO == "00" .And. !Empty(SEU->EU_NROADIA) 
			    //Estorno de despesa					
				//Ŀ
				//Se o tipo for despesa e representar prestacao de contas
				//de adiantamento, devo ignorar pois neste caso somente  
				//os movimentos do tipo 01(Adiantamento)  e 02(devolucao 
				//de adiantamento devem ser considerados.                
				//
				(cAliasTmp)->(dbSkip())
				Loop	
	
			Else
				If ((SEU->(dbSeek(xFilial("SEU")+SEU->EU_NROADIA)) .And. SEU->EU_TIPO <> "01") .or. (Empty(SEU->EU_NROADIA) .AND. SEU->EU_TIPO <> "01")) .And. IIf(cPaisLoc == "ARG", SEU->(!Eof()), .T.) //Caso for despesa de prestao de contas, Posiciona no Adiantamento, caso exista
					If lFxMultSld

						//Reposicao de saldo do adiantamento

						AtuSalCxa(cCaixa, IIf(cAliasTmp == "TMPSEU", (cAliasTmp)->EU_DTDIGIT, StoD((cAliasTmp)->EU_DTDIGIT)), (cAliasTmp)->EU_VALOR , , cFilFIJ )	

						
						//Ŀ
						//Se o tipo for devolucao de adiantamento, o mesmo foi   
						//rendido/baixado, logo considero o quanto foi devolvido 
						//como reposicao para o caixinha. Desconto do total de   
						//comprovantes (pois considerei o adiantamento inicial)  
						//
						nTotComp -= (cAliasTmp)->EU_VALOR

					ElseIf cPaisLoc <> "BRA"
						//Ŀ
						//Se o tipo for devolucao de adiantamento, o mesmo foi   
						//rendido/baixado, logo considero o quanto foi devolvido 
						//como reposicao para o caixinha. Desconto do total de   
						//comprovantes (pois considerei o adiantamento inicial)  
						//
						nTotComp -= (cAliasTmp)->EU_VALOR
					EndIf
				Endif
			EndIf
		EndIf
		RestArea(aArea)		
		(cAliasTmp)->(dbSkip())
		Loop
	ElseIf (cAliasTmp)->EU_TIPO>="90"
		//Ŀ
		//Reposicoes canceladas, aguardando autorizacao, cheques 
		//aguardando compensacao etc.                            
		//
		(cAliasTmp)->(dbSkip())
		Loop
	EndIf

	If lFxMultSld
		//***************************
		// Despesas ou adiantamento *
		//***************************
		AtuSalCxa( cCaixa, IIf(cAliasTmp == "TMPSEU",(cAliasTmp)->EU_DTDIGIT,StoD((cAliasTmp)->EU_DTDIGIT)) , (cAliasTmp)->EU_VALOR*(-1) , , cFilFIJ )
	EndIf
	IF cPaisLoc <> "BRA"
		dbSelectArea("SA6")
		SA6->(dbSetOrder(1)) //A6_FILIAL+A6_COD+A6_AGENCIA+A6_NUMCON
		If SA6->(MsSeek(cFilSA6 + SET->(ET_BANCO+ET_AGEBCO+ET_CTABCO)))
			nMoeBco := SA6->A6_MOEDA
		EndIf
		nTotComp +=  xMoeda((cAliasTmp)->EU_VALOR, (cAliasTmp)->EU_MOEDA, nMoeBco,,, (cAliasTmp)->EU_VLMOED2)
	Else 
		//Aqui estao sendo somados: Gastos/despesas em aberto (tipo 00), Adiantamentos fechados/rendidos (tipo 01)
		nTotComp += (cAliasTmp)->EU_VALOR
	Endif 
	//Ponto de entrada F570CAL
	//Ponto de entrada utilizado para tratamentos diferenciados do calculo
	//do saldo do caixinha
   	If lF570Cal
		nTotComp := ExecBlock("F570CAL",.F.,.F.,{nTotComp})
	EndIf

	(cAliasTmp)->(dbSkip())
EndDo

//Ponto de entrada utilizado para tratamentos diferenciados do calculo
//do saldo do caixinha
If ExistBlock("FA570SCX")
	nTotComp := ExecBlock("FA570SCX",.F.,.F.,{nTotComp})
EndIf


If  Select(cAliasTmp) > 0 .and. lFJIMCFIL .and. Len( aGrvSld ) > 0
	If (nPos := Ascan(aGrvSld,{|x| x[1] + x[2]== cFilComp + cCaixa })) > 0
		If nSldCx > 0 .And. !lFxMultSld .And. cPaisLoc <> "BRA"
			aGrvSld[nPos][3]  += nTotRep
		EndIf
		aGrvSld[nPos][3] -= nTotComp
		nSldCx := aGrvSld[nPos][3]
	EndIf
Else
	nSldCx -= nTotComp
EndIf

//**********************************************************************************************************
// Para ambiente com nivel de compartilhamento entre filiais CEE, realizo a atualizao do saldo neste momento.*
// Pois a rotina no estava preparada para tratar este nivel de compartilhamento.*
//**********************************************************************************************************
If lFJIMCFIL .and.Len( aGrvSld ) > 0
	dbSelectArea("SET")
	SET->(dbSetOrder(1))
	For nZ := 1 To Len(aGrvSld)
		SET->(dbSeek(aGrvSld[nZ][1] + aGrvSld[nZ][2]))
		RecLock("SET",.F.)
		SET->ET_SALDO := aGrvSld[nZ][3]
		SET->(MsUnlock())
		__lProc		:= .T.
	Next nZ
EndIf 
	
If Select(cAliasTmp) > 0
	(cAliasTmp)->(dbclosearea())
EndIf

If oObjSEU <> Nil
	oObjSEU:Destroy()
	oObjSEU := Nil
EndIf

AADD(aGrvSET,{nSldCx,lFJIMCFIL})	

Return aGrvSET

/*


Ŀ
Funo     Fa570SeqAtu Autor  Leonardo Ruben         Data 03/07/01
Ĵ
Descrio  Retorna a sequencia atual do caixinha                      
Ĵ
 Uso       SIGAFIN                                                    
ٱ


*/
Function Fa570SeqAtu( cCaixa)
Local cSeqAtu := ""
Local cFilSEU := ""
Local aAreaSEU 	:= {}

//Ŀ
// Posiciona-se no ultimo registro da ultima sequencia          
//
dbSelectArea("SEU")
aAreaSEU := SEU->(GetArea())
dbSetOrder(5)  // FILIAL+CAIXA+SEQCXA+NUMERO
DbSeek(xFilial()+cCaixa+"999999",.T.)
If !Found()
	dbSkip(-1)
EndIf
If Bof() .or. cCaixa <> EU_CAIXA  // nao ha registros para esse caixinha
	cSeqAtu := "000001"
Else
	If ExistBlock("FA550VERIF",.T.) .And. Fa550Verif()
		cFilSEU:=xFilial("SEU")
		While SEU->EU_TIPO>="90" .And. SEU->EU_FILIAL==cFilSEU .And. SEU->EU_CAIXA==cCaixa .And. !SEU->(Bof())
			SEU->(DbSkip(-1))
		Enddo
	Endif
	cSeqAtu := EU_SEQCXA
EndIf

SEU->(RestArea(aAreaSEU))

Return cSeqAtu

/*


Ŀ
Funo     Fa570AtuSld Autor  Leonardo Ruben         Data 19/06/00
Ĵ
Descrio  Retorna o valor do saldo do caixinha                       
Ĵ
 Uso       FINA570                                                    
ٱ


*/
Function Fa570AtuSld(cCaixa)
Local aGrvSET
Local aArea := GetArea()

aGrvSET := Fa570Comp(cCaixa)

RestArea(aArea)
Return aGrvSET

/*


ͻ
Programa  F570VldFor Autor  Clovis Magenta      Data   08/12/10   
͹
Desc.      Funo de validao dos campos EU_FORNECE e EU_LOJA no SX3 
           // nopc = 1 --> fornecedor											  
           // nopc = 2 --> loja                                       
͹
Uso        AP                                                         
ͼ


*/
Function F570VldFor(nOpc)
Local lOk := .T.
Local nPosForn := 0
Local nPosLoja := 0
Local lIsGetD  := Type("aHeader")=="A"
Local cValor	:= &(Readvar())

DEFAULT nOpc := 1

If lIsGetD
	nPosForn := aScan(aHeader,{|x| Alltrim(x[2])=="EU_FORNECE"})
	nPosLoja := aScan(aHeader,{|x| Alltrim(x[2])=="EU_LOJA"})

	If nOpc == 1
		lOk := IIF(!EMPTY(aCols[n][nPosLoja]), (Vazio() .or. ExistCpo('SA2', cValor+aCols[n][nPosLoja])),.T.)
	Else
		lOk := IIF(!EMPTY(aCols[n][nPosForn]), ExistCpo('SA2', aCols[n][nPosForn]+cValor),.T.)
	Endif

Else

	If nOpc == 1
			lOk := IIF(!EMPTY(M->EU_LOJA), (Vazio() .or. ExistCpo('SA2', M->EU_FORNECE+M->EU_LOJA)), (Vazio() .OR. ExistCpo('SA2', M->EU_FORNECE)) )
	Else
		lOk := IIF(!EMPTY(M->EU_FORNECE),ExistCpo('SA2', M->EU_FORNECE+M->EU_LOJA),.T.)
	Endif

Endif

Return lOk

/*


ͻ
Programa  RestStatic Autor  Rodrigo Oliveira     Data   15/01/20   
͹
Desc.      Restaura os valores das variveis Statics                  
͹
Uso        FINA570                                                    
ͼ


*/
Static Function RestStatic()

	__lProc	:=	.F.

Return Nil

/*/{Protheus.doc} F570Help
	Montagem do HELP no processo de recalculo do caixinha
	@type  Static Function
	@author Vitor Duca
	@since 15/12/2020
	@version 1.0
	@return 
/*/	
Static Function F570Help()
	Local cMsg 		AS Character
	Local cTitulo 	AS Character
	Local aBtLinks 	AS Array
	Local cColorTitle 	 AS Character
	Local cColorSubTitle AS Character
	Local cColorText 	 AS Character

	setHelpColors(@cColorTitle,@cColorSubTitle,@cColorText)

	cTitulo  := STR0022//"Reclculo do caixinha"
	aBtLinks := ARRAY(1,2)

	cMsg := "<font size='5' color='" + cColorTitle + "'><b>"+STR0023+"</b></font><br/><br/>"//HELP - NOPROCESS

	cMsg += "<font size='3' color='" + cColorSubTitle + "'><b>"+STR0013+"</b></font><br/>"//"Ocorrncia"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                        
	cMsg += "<font size='3' color='" + cColorText + "'>"+STR0014+"<br/>"//"No foi possivel efetuar o recalculo do caixinha."                                                                                                                                                                                                                                                                                                                                                                                                                                                                 
	cMsg += "</font><br/><br/>"

	cMsg += "<font size='3' color='" + cColorSubTitle + "'><b>"+STR0015+"</b></font>"//"Possiveis causas"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  
	cMsg += "<ul>" 
    cMsg +=   "<font size='3' color='" + cColorText + "'><li>"+STR0016+"</li></font>" //"No foi encontrado registros nas tabelas SET e SEU. "                                                                                                                                                                                                                                                                                                                                                                                                                                                              
    cMsg +=   "<font size='3' color='" + cColorText + "'><li>"+STR0017+"</li></font>" //"O saldo do caixinha no pode ser negativo."                                                                                                                                                                                                                                                                                                                                                                                                                                                                        
    cMsg +=   "<font size='3' color='" + cColorText + "'><li>"+STR0018+"</li></font>"  //"A rotina no efetua o reclculo de saldo dos caixinhas com a situao de fechado."                                                                                                                                                                                                                                                                                                                                                                                                                                 
    cMsg += "</ul><br/>"

	cMsg += "<font size='3' color='" + cColorSubTitle + "'><b>"+STR0019+"</b></font>"//"Para maiores informaes acesse:"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  

	aBtLinks[1,1] := STR0020//"Reclculo do caixinha - FINA570"
	aBtLinks[1,2] := STR0021//"https://tdn.totvs.com/x/TwB0Ig"

	FinHelp(cTitulo, cMsg, aBtLinks, 400, 600)
Return	
